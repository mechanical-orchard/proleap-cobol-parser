      *-----------------------*                                         12/20/93
00002  IDENTIFICATION DIVISION.                                         INKCS002
00003 *-----------------------*                                            LV047
00004  PROGRAM-ID.    INKCS002.                                         INKCS002
00005  AUTHOR.        M. FURLICK - STRATAGEM                            INKCS002
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          INKCS002
00007  DATE-WRITTEN.  08/93.                                            INKCS002
00008  DATE-COMPILED.                                                   INKCS002
00009                                                                   INKCS002
00010 *---------------------------------------------------------------* INKCS002
00011 *    I002 - PI INVALID DATA SUMMARY AREA LIST                   * INKCS002
00012 *                                                               * INKCS002
00013 *    THIS PROGRAM GENERATES A LIST OF THE AREAS IN A STORE THAT * INKCS002
00014 *    HAD, OR STILL HAVE, ERRORS.  THE AREA, NUMBER OF FALLOUTS, * INKCS002
00015 *    AND NUMBER OF ITEMS LEFT TO CORRECT ARE DISPLAYED.         * INKCS002
00016 *                                                               * INKCS002
00017 *---------------------------------------------------------------* INKCS002
00021 *                       CHANGE LOG                              * INKCS003
00022 *---------------------------------------------------------------* INKCS003
00025 *    3/26/99 - CHANGE BY  PAUL KEBER          WR# 10657         * INKCS005
00024 *    ADDED NEW F23=PRINT AND F24=PRNT-ALL KEYS TO ALLOW THE     * INKCS003
00016 *    STARTUP OF INKCS007 WHICH WILL PRINT THE PHYSICAL          * INKCS002
00016 *    INVENTORY INVALID DATA COMPARISON REPORT FOR EACH AREA     * INKCS002
00016 *    WHICH IS SELECTED.                                         * INKCS002
00016 *                                                               * INKCS002
00025 *    3/24/00 - CHANGE BY  PAUL KEBER          WR# 9999          * INKCS005
00024 *    SOURCE THE "NUMBER LEFT TO CORRECT" COUNT FROM THE TINVDTL * INKCS003
00016 *    TABLE INSTEAD OF THE TINVLOC TABLE.                        * INKCS002
00018 *                                                                    CL*17
      *CHANGE HISTORY:                                                          
      * WR/PRJ DATE       PGMR         DESCRIPTION                              
      * ------ ---------- ------------ -----------------------------            
W26602* W26600 06-29-2004 ROD JANSSEN  INV STORE EXPANSION.                     
00017 *---------------------------------------------------------------* INKCS002
263126* CHANGED 10/25/2021    BY JIM HUNTER      WR: CHG0263126                 
263126* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263126* MAKING THE CALL DYNAMIC VS STATIC.                                      
1022RM* CHANGED 10/26/2022    BY BOB MCASEY      WR: CHG0277188                 
1022RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1022RM* THE CICS ARCHITECTURE API.                                              
263126*----------------------------------------------------------------*   CL*17
00018 /                                                                 INKCS002
00019  ENVIRONMENT DIVISION.                                            INKCS002
00020                                                                   INKCS002
00021  DATA DIVISION.                                                   INKCS002
00022                                                                   INKCS002
00023  WORKING-STORAGE SECTION.                                         INKCS002
00024                                                                   INKCS002
00025  01  PC-PROGRAM-CONSTANTS.                                        INKCS002
00026      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         INKCS002
00027                                                     'IN002A  '.   INKCS002
00028      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         INKCS002
00029                                                     'INKM002 '.   INKCS002
00030      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         INKCS002
00031                                                     'INKCS002'.   INKCS002
00032      05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.      INKCS002
00033      05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.      INKCS002
00034      05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.    INKCS002
00035      05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +14     INKCS002
00036                                                     COMP SYNC.    INKCS002
00037      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2      INKCS002
00038                                                     COMP SYNC.    INKCS002
00039      05  PC-MAX-ITEMS                   PIC S9(09)  VALUE         INKCS002
00040                                                     +1000000      INKCS002
00041                                                     COMP-3.       INKCS002
00042      05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5      INKCS002
00043                                                     COMP SYNC.    INKCS002
00044      05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3       INKCS002
00045                                                     COMP-3.       INKCS002
00046      05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.  INKCS002
00047      05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.  INKCS002
00048      05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.  INKCS002
00049      05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.  INKCS002
00049      05  PC-PRT                         PIC  X(03)  VALUE 'PRT'.  INKCS002
00050      05  PC-Y                           PIC  X(01)  VALUE 'Y'.       CL**6
00051      05  PC-CORPORATE                   PIC  X(02)  VALUE 'HQ'.      CL*18
00052      05  PC-COMPLETE                    PIC  X(08)  VALUE            CL*18
00053                                                    'COMPLETE'.       CL**6
00054      05  PC-TSYMSG-NUMBERS.                                       INKCS002
00055          10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.  INKCS002
00056          10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.  INKCS002
00057          10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.  INKCS002
00058          10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.  INKCS002
00059          10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.  INKCS002
00060          10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.  INKCS002
00061          10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.  INKCS002
00062          10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.  INKCS002
00063          10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.  INKCS002
00064          10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.  INKCS002
00065          10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.  INKCS002
00065          10  PC-TSYMSG-00098            PIC  9(05)  VALUE 00098.  INKCS002
00066          10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.  INKCS002
00067          10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.  INKCS002
00067          10  PC-TSYMSG-01436            PIC  9(05)  VALUE 01436.  INKCS002
00067          10  PC-TSYMSG-01997            PIC  9(05)  VALUE 01997.  INKCS002
00068          10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.  INKCS002
00069 *                                                                 INKCS002
00070  01  PS-PROGRAM-SWITCHES.                                         INKCS002
00071      05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.    INKCS002
00072          88  PS-NO-ERROR                            VALUE 'Y'.    INKCS002
00073          88  PS-ERROR                               VALUE 'N'.    INKCS002
00074      05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.    INKCS002
00075          88  PS-COMMAREA-VALID                      VALUE 'Y'.    INKCS002
00076          88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.    INKCS002
00077      05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.    INKCS002
00078          88  PS-DONE                                VALUE 'Y'.    INKCS002
00079          88  PS-NOT-DONE                            VALUE 'N'.    INKCS002
00080      05  PS-STORE-REC-SW                PIC  X(01)  VALUE 'N'.       CL*18
00081          88  PS-STORE-REC                           VALUE 'Y'.       CL*18
00082          88  PS-NO-STORE-REC                        VALUE 'N'.       CL*18
00077      05  PS-SEL-AREA-PRINT-SW           PIC  X(01)  VALUE 'N'.    INKCS002
00078          88  PS-SEL-AREA-PRINT                      VALUE 'Y'.    INKCS002
00079          88  PS-NO-SEL-AREA-PRINT                   VALUE 'N'.    INKCS002
00083 *                                                                 INKCS002
00084  01  PV-PROGRAM-VARIABLES.                                        INKCS002
00085      05  PV-STORE-NBR-X       PIC X(04)  VALUE SPACES.            INKCS002
00086      05  PV-STORE-NBR-9 REDEFINES                                 INKCS002
00085          PV-STORE-NBR-X       PIC 9(04).                          INKCS002
00085      05  PV-SUB                         PIC S9(04)  VALUE +0      INKCS002
00086                                                     COMP SYNC.    INKCS002
00085      05  PV-AREA-SUB                    PIC S9(04)  VALUE +0      INKCS002
00086                                                     COMP SYNC.    INKCS002
00087      05  PV-REMAINDER                   PIC S9(04)  VALUE +0      INKCS002
00088                                                     COMP SYNC.    INKCS002
00089      05  PV-READ-COUNT                  PIC S9(09)  VALUE +0      INKCS002
00090                                                     COMP-3.       INKCS002
00091      05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0      INKCS002
00092                                                     COMP-3.       INKCS002
00093      05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0      INKCS002
00094                                                     COMP-3.       INKCS002
00095      05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0      INKCS002
00096                                                     COMP-3.       INKCS002
00097      05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0      INKCS002
00098                                                     COMP-3.       INKCS002
00099      05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0      INKCS002
00100                                                     COMP-3.       INKCS002
00101      05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0      INKCS002
00102                                                     COMP-3.       INKCS002
00103      05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0      INKCS002
00104                                                     COMP SYNC.    INKCS002
00105      05  PV-CICS-RESP                   PIC S9(04)  VALUE +0      INKCS002
00106                                                     COMP SYNC.    INKCS002
00107      05  PV-ITEM                        PIC S9(04)  VALUE +0      INKCS002
00108                                                     COMP SYNC.    INKCS002
00109      05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0      INKCS002
00110                                                     COMP SYNC.    INKCS002
00111      05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0      INKCS002
00112                                                     COMP SYNC.    INKCS002
00113      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0      INKCS002
00114                                                     COMP SYNC.    INKCS002
00115      05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0      INKCS002
00116                                                     COMP SYNC.    INKCS002
00117      05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0      INKCS002
00118                                                     COMP SYNC.    INKCS002
00119      05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS002
00120                                                     COMP SYNC.    INKCS002
00121      05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS002
00122                                                     COMP SYNC.    INKCS002
00123      05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0      INKCS002
00124                                                     COMP SYNC.    INKCS002
00125      05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0      INKCS002
00126                                                     COMP SYNC.    INKCS002
00127      05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0      INKCS002
00128                                                     COMP SYNC.    INKCS002
00129      05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0      INKCS002
00130                                                     COMP SYNC.    INKCS002
00133      05  PV-NBR-TO-CORRECT              PIC S9(09)  VALUE +0         CL**3
00134                                                     COMP SYNC.       CL**3
00131      05  PV-NBR-FALLOUTS                PIC S9(05)  VALUE ZERO       CL**3
00132                                                     COMP-3.          CL**3
00135      05  PV-HOLD-AREA-NBR               PIC S9(08)  VALUE ZERO       CL**3
00136                                                     COMP SYNC.       CL**3
00137      05  PV-START-ITEM-REQUEST          PIC  X(05).                  CL*31
00137      05  PV-NBR-SEL-ITEMS               PIC  9(04)  VALUE ZEROS.     CL*31
00138 *----------------------------------------------------------------*   CL**3
00139 *   THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.    *   CL**3
00140 *   THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE            *   CL**3
00141 *   APPLICATION-SPECIFIC COMMAREA.                               *   CL**3
00142 *----------------------------------------------------------------*   CL**3
00143  01  LQW-LIST-QUEUE-WORK-AREA.                                    INKCS002
00144      05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.  INKCS002
00145      05  LQW-ITEM-ENTRIES               OCCURS 14 TIMES           INKCS002
00146                                         INDEXED BY LQW-IDX.       INKCS002
00147          10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.       INKCS002
00148          10  LQW-TEMP-SELECTED-SW       PIC  X(01).               INKCS002
00149          10  LQW-SELECTED-SW            PIC  X(01).               INKCS002
00150              88  LQW-SELECT                         VALUE 'S'.    INKCS002
00151              88  LQW-DESELECT                       VALUE ' '.    INKCS002
00152          10  LQW-ACTION                 PIC  X(03).               INKCS002
00153          10  LQW-AREA-NBR               PIC S9(08)  COMP.            CL*10
00154          10  LQW-NBR-FALLOUTS           PIC S9(05)  COMP-3.          CL**3
00155          10  LQW-NBR-TO-CORRECT         PIC S9(05)  COMP-3.          CL**3
00156          10  LQW-COMPLETE               PIC  X(01).                  CL**3
00157              88  LQW-AREA-COMPLETE                  VALUE 'Y'.       CL**3
00158                                                                   INKCS002
00159      EJECT                                                        INKCS002
00160 *---------------------------------------------------------------* INKCS002
00161 *  RECORD LAYOUT FOR THE MESSAGE SELECTION TEMP STORAGE QUEUE.  * INKCS002
00162 *---------------------------------------------------------------* INKCS002
00163                                                                   INKCS002
00164      COPY INWS002.                                                   CL*47
00165                                                                   INKCS002
00229 *---------------------------------------------------------------* INKCS007
000200*  THIS IS THE WORKING STORAGE LAYOUT USED TO PASS DATA TO THE  *         
000300*  PHYSICAL INVENTORY INVALID DATA COMPARISON PRINT PROGRAM.    *         
00231 *---------------------------------------------------------------* INKCS007
00165                                                                   INKCS002
W26600     COPY INWS007.                                                INKCS007
00166                                                                   INKCS002
00167 *---------------------------------------------------------------* INKCS002
00168 *    MAP LAYOUT                                                 * INKCS002
00169 *---------------------------------------------------------------* INKCS002
00170                                                                   INKCS002
W26600     COPY INKM002.                                                INKCS002
00172 *                                                                 INKCS002
00173  01  MR-OCCURS-LAYOUT                   REDEFINES  IN002AO.          CL**6
W26600     05  FILLER                         PIC X(76).                   CL**8
00175      05  MR-SCROLL-AREA                 OCCURS 14 TIMES.          INKCS002
00176          10  MR-SELECTL                 PIC S9(04) COMP.          INKCS002
00177          10  MR-SELECTA                 PIC  X(01).               INKCS002
00178          10  MR-SELECTC                 PIC  X(01).               INKCS002
00179          10  MR-SELECTH                 PIC  X(01).               INKCS002
00180          10  MR-SELECTI                 PIC  X(01).               INKCS002
00181                                                                   INKCS002
00182          10  MR-ACTIONL                 PIC S9(04) COMP.          INKCS002
00183          10  MR-ACTIONA                 PIC  X(01).               INKCS002
00184          10  MR-ACTIONC                 PIC  X(01).               INKCS002
00185          10  MR-ACTIONH                 PIC  X(01).               INKCS002
00186          10  MR-ACTION                  PIC  X(03).               INKCS002
00187                                                                   INKCS002
00188          10  MR-AREAL                   PIC S9(04) COMP.          INKCS002
00189          10  MR-AREAA                   PIC  X(01).               INKCS002
00190          10  MR-AREAC                   PIC  X(01).               INKCS002
00191          10  MR-AREAH                   PIC  X(01).               INKCS002
00192          10  MR-AREA-X.                                           INKCS002
00193              15  MR-AREA                PIC ZZZZZZ9.                 CL**6
00194                                                                   INKCS002
00195          10  MR-NBR-FALLOUTSL           PIC S9(04) COMP.          INKCS002
00196          10  MR-NBR-FALLOUTSA           PIC  X(01).               INKCS002
00197          10  MR-NBR-FALLOUTSC           PIC  X(01).               INKCS002
00198          10  MR-NBR-FALLOUTSH           PIC  X(01).               INKCS002
00199          10  MR-NBR-FALLOUTS-X.                                   INKCS002
00200              15  MR-NBR-FALLOUTS        PIC ZZZZ9.                INKCS002
00201                                                                   INKCS002
00202          10  MR-NBR-TO-CORRECTL         PIC S9(04) COMP.          INKCS002
00203          10  MR-NBR-TO-CORRECTA         PIC  X(01).               INKCS002
00204          10  MR-NBR-TO-CORRECTC         PIC  X(01).               INKCS002
00205          10  MR-NBR-TO-CORRECTH         PIC  X(01).               INKCS002
00206          10  MR-NBR-TO-CORRECT-X.                                    CL**6
00207              15  MR-NBR-TO-CORRECT      PIC ZZZZ9.                INKCS002
00208                                                                   INKCS002
00209          10  MR-COMPLETEL               PIC S9(04) COMP.          INKCS002
00210          10  MR-COMPLETEA               PIC  X(01).               INKCS002
00211          10  MR-COMPLETEC               PIC  X(01).               INKCS002
00212          10  MR-COMPLETEH               PIC  X(01).               INKCS002
00213          10  MR-COMPLETE                PIC  X(08).                  CL**3
00214                                                                   INKCS002
00215 /                                                                 INKCS002
00216 *---------------------------------------------------------------* INKCS002
00217 *    ATTRIBUTE SETTINGS COPYBOOK.                               * INKCS002
00218 *---------------------------------------------------------------* INKCS002
00219                                                                   INKCS002
00220      COPY DPWS015.                                                INKCS002
00221                                                                   INKCS002
00222 *---------------------------------------------------------------* INKCS002
00223 *    FUNCTION KEYS COPYBOOK.                                    * INKCS002
00224 *---------------------------------------------------------------* INKCS002
00225                                                                   INKCS002
00226      COPY DPWS016.                                                INKCS002
00227                                                                   INKCS002
00228 *---------------------------------------------------------------* INKCS002
00229 *    NUMERIC EDIT LAYOUT.                                       * INKCS002
00230 *---------------------------------------------------------------* INKCS002
00231                                                                   INKCS002
00232      COPY DPWS010I.                                               INKCS002
00233 /                                                                 INKCS002
00234 *---------------------------------------------------------------* INKCS002
00235 *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   * INKCS002
00236 *---------------------------------------------------------------* INKCS002
00237                                                                   INKCS002
00238      COPY DPWS030.                                                INKCS002
1022RM     COPY DPWS930.                                                INKCS002
00239 /                                                                 INKCS002
00240 *---------------------------------------------------------------* INKCS002
00241 *    STANDARD COMMAREA.                                         * INKCS002
00242 *---------------------------------------------------------------* INKCS002
00243                                                                   INKCS002
00244      COPY DPWS020.                                                INKCS002
00245      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                INKCS002
00246                                                                   INKCS002
00247          10  INTER-APPL-COMM-AREA       PIC X(200).               INKCS002
W26600     COPY INWS001.                                                   CL*47
00249 *---------------------------------------------------------------* INKCS002
00250 *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   * INKCS002
00251 *---------------------------------------------------------------* INKCS002
00252          10  ASC-SPECIFIC-COMMAREA.                               INKCS002
00253              15  ASC-LIST-KEYS.                                   INKCS002
W26600                 20  ASC-KEY-STORE-NBR               PIC X(04).      CL**3
W26600                 20  ASC-KEY-AREA-NBR                PIC S9(09)      CL**3
00256                                                      COMP.           CL**2
00257              15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                INKCS002
00258                  20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).  INKCS002
00259                      88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.   INKCS002
00260                      88  ASC-BLOCK-MODIFIED          VALUE 'Y'.   INKCS002
00261                                                                   INKCS002
00262                  20  ASC-ITEM-ARRAY.                              INKCS002
00263                      25  ASC-ITEM-ENTRIES OCCURS 14 TIMES.        INKCS002
00264                          30  ASC-LIST-ITEM-NUMBER                 INKCS002
00265                                                      PIC S9(09)   INKCS002
00266                                                      COMP-3.      INKCS002
00267                          30  ASC-TEMP-SELECTED-SW    PIC  X(01).     CL**9
00268                              88  ASC-TEMP-NO-SELECT  VALUE ' '.      CL**9
00269                              88  ASC-TEMP-SELECT     VALUE 'S'.      CL**9
00270                              88  ASC-TEMP-BEGIN      VALUE 'B'.      CL**9
00271                              88  ASC-TEMP-END        VALUE 'E'.      CL**9
00272                          30  ASC-SELECTED-SW         PIC  X(01).  INKCS002
00273                              88  ASC-NO-SELECT       VALUE ' '.   INKCS002
00274                              88  ASC-SELECT          VALUE 'S'.   INKCS002
00275                              88  ASC-BEGIN           VALUE 'B'.   INKCS002
00276                              88  ASC-END             VALUE 'E'.   INKCS002
00277                          30  ASC-ACTION              PIC  X(03).  INKCS002
00278                          30  ASC-AREA-NBR            PIC S9(08)      CL*10
00279                                                           COMP.      CL*10
00280                          30  ASC-NBR-FALLOUTS        PIC S9(05)   INKCS002
00281                                                           COMP-3. INKCS002
00282                          30  ASC-NBR-TO-CORRECT      PIC S9(05)      CL**6
00283                                                           COMP-3. INKCS002
00284                          30  ASC-COMPLETE            PIC  X(01).  INKCS002
00285                              88  ASC-AREA-COMPLETE   VALUE 'Y'.   INKCS002
00286                                                                   INKCS002
00287              15  ASC-BLK-SUB                         PIC S9(04)   INKCS002
00288                                                           COMP    INKCS002
00289                                                           SYNC.   INKCS002
00290              15  ASC-ITEM-SUB                        PIC S9(04)   INKCS002
00291                                                           COMP    INKCS002
00292                                                           SYNC.   INKCS002
00293              15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)   INKCS002
00294                                                           COMP-3. INKCS002
00295              15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)   INKCS002
00296                                                           COMP-3. INKCS002
00297              15  ASC-ITEMS-DISPLAYED                 PIC S9(03)   INKCS002
00298                                                           COMP-3. INKCS002
00299              15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)   INKCS002
00300                                                           COMP-3. INKCS002
00301              15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)   INKCS002
00302                                                           COMP-3. INKCS002
00303              15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)   INKCS002
00304                                                           COMP    INKCS002
00305                                                           SYNC.   INKCS002
00306              15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)   INKCS002
00307                                                           COMP-3. INKCS002
00308              15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)   INKCS002
00309                                                           COMP-3. INKCS002
00310              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).  INKCS002
00311                  88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.   INKCS002
00312                  88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.   INKCS002
00313              15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).  INKCS002
00314                  88  ASC-UPDATE-TSQ                  VALUE 'Y'.   INKCS002
00315                  88  ASC-NO-TSQ-UPDATE               VALUE ' '.   INKCS002
00316              15  ASC-AREA-ASTERISK-SW                PIC  X(01).     CL*42
00317                  88  ASC-AREA-ASTERISK               VALUE 'Y'.      CL*42
00318              15  ASC-DISPLAY-COMPLETE-SW             PIC  X(01).     CL*44
00319                  88  ASC-DISPLAY-COMPLETE            VALUE 'Y'.      CL*44
00320              15  ASC-LIST-QUEUE-NAME.                             INKCS002
00321                  20  ASC-LIST-TERM-ID                PIC  X(04).  INKCS002
00322                  20  ASC-LIST-TASK-NUMBER            PIC S9(05)   INKCS002
00323                                                           COMP-3. INKCS002
00324                  20  ASC-LIST-SEQ                    PIC  9(01).  INKCS002
00325              15  ASC-SEL-QUEUE-NAME.                              INKCS002
00326                  20  ASC-SEL-TERM-ID                 PIC  X(04).  INKCS002
00327                  20  ASC-SEL-TASK-NUMBER             PIC S9(05)   INKCS002
00328                                                           COMP-3. INKCS002
00329                  20  ASC-SEL-SEQ                     PIC  9(01).  INKCS002
00330                                                                   INKCS002
00331              15  ASC-CURSOR-SW                       PIC  X(01).  INKCS002
00332                  88  ASC-NO-CURSOR                   VALUE 'Z'.   INKCS002
00333                  88  ASC-SYSTEM-ID-CURSOR            VALUE 'S'.   INKCS002
00334                  88  ASC-MESSAGE-NUMBER-CURSOR       VALUE 'N'.   INKCS002
00335                  88  ASC-MESSAGE-TEXT-CURSOR         VALUE 'T'.   INKCS002
00336              15  ASC-START-ITEM-REQUEST.                          INKCS002
00337                  20  ASC-START-ITEM-REQUEST-N        PIC  9(05).  INKCS002
00338              15  ASC-HOLD-INTER-APPL                 PIC X(200).     CL**2
00339 /                                                                 INKCS002
00340 *---------------------------------------------------------------* INKCS002
00341 *    ABEND PROCESSING COPYBOOK.                                 * INKCS002
00342 *---------------------------------------------------------------* INKCS002
00343                                                                   INKCS002
00344      COPY DPWS013.                                                INKCS002
00345                                                                   INKCS002
00346 /                                                                 INKCS002
00347 *---------------------------------------------------------------* INKCS002
00348 *    DB2 AREA FOR INVALID INVENTORY DETAIL TABLE                *    CL*18
00349 *---------------------------------------------------------------* INKCS002
00350      EXEC SQL                                                     INKCS002
00351           INCLUDE TINVDTL                                            CL*18
00352      END-EXEC.                                                    INKCS002
00345                                                                   INKCS002
00347 *---------------------------------------------------------------* INKCS002
00348 *    DB2 AREA FOR INVENTORY PHYSICAL LOCATION TABLE             *    CL*18
00349 *---------------------------------------------------------------* INKCS002
00350      EXEC SQL                                                     INKCS002
00351           INCLUDE TINVLOC                                            CL*18
00352      END-EXEC.                                                    INKCS002
00345                                                                   INKCS002
00353 *---------------------------------------------------------------* INKCS002
00354 *    DB2 AREA FOR COMMUNICATIONS                                * INKCS002
00355 *---------------------------------------------------------------* INKCS002
00356      EXEC SQL                                                     INKCS002
00357           INCLUDE SQLCA                                           INKCS002
00358      END-EXEC.                                                    INKCS002
00359                                                                   INKCS002
00360 *---------------------------------------------------------------* INKCS002
00361 *    TINVLOC CURSOR DECLARATION.                                *    CL*18
00362 *---------------------------------------------------------------* INKCS002
00363                                                                   INKCS002
00364      EXEC SQL                                                     INKCS002
00365           DECLARE AREA-CSR CURSOR                                 INKCS002
00366           FOR SELECT                                              INKCS002
00367                 AREA_NBR                                          INKCS002
00370               , TOT_ITM_INVLID_QTY                                   CL*18
00371               , TOT_REC_INVLID_QTY                                   CL*18
00372           FROM                                                    INKCS002
00373                 TINVLOC                                              CL*19
00374           WHERE                                                   INKCS002
W26600                LOC_NBR      =   :INVLOC-LOC-NBR                     CL**3
00376             AND                                                   INKCS002
W26600                AREA_NBR     >   :INVLOC-AREA-NBR                    CL**3
00378           ORDER BY                                                INKCS002
00379                 AREA_NBR                                             CL**3
00380      END-EXEC.                                                    INKCS002
00381                                                                      CL*36
00382      EXEC SQL                                                        CL*36
00383           DECLARE AREA-CSR2 CURSOR                                   CL*36
00384           FOR SELECT                                                 CL*36
00385                 AREA_NBR                                             CL*36
00388               , TOT_ITM_INVLID_QTY                                   CL*36
00389               , TOT_REC_INVLID_QTY                                   CL*36
00390           FROM                                                       CL*36
00391                 TINVLOC                                              CL*36
00392           WHERE                                                      CL*36
W26600                LOC_NBR      =   :INVLOC-LOC-NBR                     CL**3
00376             AND                                                   INKCS002
W26600                AREA_NBR     =   :INVLOC-AREA-NBR                    CL**3
00396           ORDER BY                                                   CL*36
00397                 AREA_NBR                                             CL*36
00398      END-EXEC.                                                       CL*36
00399                                                                   INKCS002
00400 /                                                                 INKCS002
00401  LINKAGE SECTION.                                                 INKCS002
00402                                                                   INKCS002
00403  01  DFHCOMMAREA.                                                 INKCS002
00404      05  FILLER                         OCCURS  1 TO 4072 TIMES   INKCS002
00405                                         DEPENDING ON EIBCALEN.    INKCS002
00406          10  FILLER                     PIC X.                    INKCS002
00407                                                                   INKCS002
00408  PROCEDURE DIVISION.                                                 CL*36
00409 ******************************************************************INKCS002
00410 **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **INKCS002
00411 **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **INKCS002
00412 **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **INKCS002
00413 ******************************************************************INKCS002
00414                                                                      CL**3
00415  0000-MAIN-MODULE.                                                INKCS002
00416                                                                      CL**3
00417      INITIALIZE DP030-CICS-API-FIELDS.                            INKCS002
00418      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       INKCS002
00419      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          INKCS002
00420      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         INKCS002
00421      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                          CL*40
00422                                                                      CL*14
00423      MOVE LENGTH OF IN002AI        TO DP030-MAP-LENGTH (1).          CL**6
00424      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     INKCS002
00425      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS002
00426                                                                      CL**2
00427      PERFORM 1000-CONTROL-PROCESSING                              INKCS002
00428                                                                      CL**2
00429      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     INKCS002
00430      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS002
00431                                                                   INKCS002
00432 *----------------------------------------------------------------*   CL**2
00433 *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *   CL**2
00434 *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *   CL**2
00435 *----------------------------------------------------------------*   CL**2
00436                                                                      CL**2
00437  0001-CALL-CICS-ARCH-API.                                         INKCS002
00438                                                                      CL**2
1022RM*    CALL 'DPKCS030' USING DFHEIBLK                               INKCS002
1022RM     CALL DP930-CICS-ARCH-API                                     INKCS002
1022RM                     USING DFHEIBLK                                       
00440                            DFHCOMMAREA                            INKCS002
00441                            DP030-CICS-API-FIELDS                  INKCS002
00442                            DP020-STANDARD-COMMAREA                INKCS002
00443                            IN002AI.                                  CL**6
00444                                                                      CL**2
00445      IF  DP030-RC-CALL-SUCCESSFUL                                 INKCS002
00446          CONTINUE                                                 INKCS002
00447      ELSE                                                         INKCS002
00448          SET DP013-NO-ROLLBACK                                    INKCS002
00449              DP013-XCTL-DISPLAY-RESTART                           INKCS002
00450              DP013-CICS-ABEND      TO TRUE                        INKCS002
00451          MOVE '0001-CALL-CICS-ARCH-API'                           INKCS002
00452                                    TO DP013-PARAGRAPH             INKCS002
1022RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEXINKCS002
1022RM*             'T LINE'             TO DP013-MESSAGE-TEXT (2)      INKCS002
1022RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1022RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00455          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      INKCS002
00456          PERFORM DP013-0000-PROCESS-ABEND                         INKCS002
00457      END-IF.                                                      INKCS002
00458                                                                      CL**2
00459 *----------------------------------------------------------------*INKCS002
00460 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *INKCS002
00461 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *INKCS002
00462 *                                                                *INKCS002
00463 * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *INKCS002
00464 *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *INKCS002
00465 *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *INKCS002
00466 *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *INKCS002
00467 *----------------------------------------------------------------*INKCS002
00468                                                                   INKCS002
00469  1000-CONTROL-PROCESSING.                                         INKCS002
00470                                                                   INKCS002
00471 *    SET DP030-OVERRIDE-APPL-ERROR TO TRUE.                          CL*42
00472      EVALUATE TRUE                                                INKCS002
00473          WHEN DP020-NEXT-ACT-INITIAL                              INKCS002
00474               INITIALIZE ASC-SPECIFIC-COMMAREA                       CL**2
00475               MOVE EIBTRMID        TO ASC-LIST-TERM-ID               CL**2
00476                                       ASC-SEL-TERM-ID             INKCS002
00477               MOVE DP020-SRC-TASK-NUMBER                             CL**2
00478                                    TO ASC-LIST-TASK-NUMBER        INKCS002
00479                                       ASC-SEL-TASK-NUMBER         INKCS002
00480               MOVE PC-LIST-QUEUE-SEQ                                 CL**2
00481                                    TO ASC-LIST-SEQ                INKCS002
00482               MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                   CL**2
00483                                                                      CL**2
00484               MOVE INTER-APPL-COMM-AREA                              CL*43
00485                                    TO ASC-HOLD-INTER-APPL            CL**2
00486               PERFORM 4100-PREPARE-CURSOR                            CL*43
00487               PERFORM 4000-BUILD-INITIAL-PANEL                       CL*43
00488               IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                    CL*43
00489                   SET DP020-NEXT-ACT-APPL-ERROR TO TRUE              CL*43
00490               END-IF                                                 CL*43
00491                                                                   INKCS002
00492          WHEN DP020-NEXT-ACT-READ-MAP                             INKCS002
00493               MOVE ASC-ITEM-AT-TOP-OF-PANEL                          CL**2
00494                                    TO PV-TOP-ITEM                 INKCS002
00495               PERFORM 2000-PROCESS-PANEL                             CL**2
00496                                                                   INKCS002
00497          WHEN DP020-NEXT-ACT-RETURN                               INKCS002
00498               MOVE ASC-ITEM-AT-TOP-OF-PANEL                          CL**2
00499                                    TO PV-TOP-ITEM                 INKCS002
00500               MOVE ZERO            TO ASC-ITEM-AT-TOP-OF-PANEL       CL**2
00501               MOVE ASC-HOLD-INTER-APPL                               CL**2
00502                                    TO INTER-APPL-COMM-AREA           CL**2
00503               PERFORM 6000-REFRESH-PANEL-FROM-TEMP                   CL**2
00504               PERFORM 3000-EDIT-DATA-IN-COMMAREA                     CL**2
00505               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                   CL*11
00506                                                                   INKCS002
00507          WHEN OTHER                                               INKCS002
00508               SET DP013-LOGIC-ABEND TO TRUE                          CL**2
00509               SET DP013-NO-ROLLBACK TO TRUE                          CL**2
00510               MOVE '1000-CONTROL-PROCESSING'                         CL**2
00511                                    TO DP013-PARAGRAPH             INKCS002
00512               MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'      CL**2
00513                                    TO DP013-MESSAGE-TEXT(1)       INKCS002
00514               PERFORM DP013-0000-PROCESS-ABEND                       CL**2
00515      END-EVALUATE.                                                INKCS002
00516 /                                                                    CL**2
00517 *----------------------------------------------------------------*INKCS002
00518 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *INKCS002
00519 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *INKCS002
00520 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *INKCS002
00521 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *INKCS002
00522 * FUNCTION KEYS CAN GET THIS FAR.                                *INKCS002
00523 *----------------------------------------------------------------*INKCS002
00524                                                                   INKCS002
00525  2000-PROCESS-PANEL.                                              INKCS002
00526                                                                      CL*45
00527      IF  IN001-STORE-NBR NOT NUMERIC                                 CL*45
00528          MOVE ASC-HOLD-INTER-APPL TO INTER-APPL-COMM-AREA            CL*45
00529      END-IF.                                                         CL*45
00530                                                                      CL*45
00531      EVALUATE TRUE                                                INKCS002
00532          WHEN DP020-SRC-AID = DP016-CLEAR                         INKCS002
00533              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS002
00534              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS002
00535              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS002
00536                                                                   INKCS002
00537          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    INKCS002
00538              PERFORM 2050-DELETE-LIST-QUEUE                       INKCS002
00539              IF  ASC-AREA-ASTERISK                                   CL*42
00540                  MOVE ZERO         TO  ASC-KEY-AREA-NBR              CL*42
00541              END-IF                                                  CL*42
00542              PERFORM 4000-BUILD-INITIAL-PANEL                     INKCS002
00543                                                                   INKCS002
00544          WHEN DP020-FK-DESELECT(DP020-SRC-AID)                    INKCS002
00545              PERFORM 2140-DESELECT-ALL-ITEMS                      INKCS002
00546              PERFORM 3200-RESET-ATTRIBUTES                           CL*33
00547              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS002
00548                                                                   INKCS002
00549          WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                  INKCS002
00550              PERFORM 2150-SELECT-ALL-ITEMS                        INKCS002
00551              PERFORM 3200-RESET-ATTRIBUTES                           CL*33
00552              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS002
00553                                                                   INKCS002
00554          WHEN OTHER                                               INKCS002
00555               PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                   CL*37
00556                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                 CL*37
00557                   IF  PS-NO-ERROR                                    CL*37
00558                       SET DP030-CONTINUE-GO                          CL*37
00559                                  TO TRUE                          INKCS002
00560                       PERFORM 2100-CHECK-FUNCTION-KEY                CL*37
00561                   ELSE                                               CL*37
00562                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN           CL*37
00563                       SET DP030-OVERRIDE-GO                          CL*37
00564                                  TO TRUE                          INKCS002
00565                   END-IF                                             CL*37
00566      END-EVALUATE.                                                INKCS002
00567  EJECT                                                            INKCS002
00568 *----------------------------------------------------------------*INKCS002
00569 *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *INKCS002
00570 *----------------------------------------------------------------*INKCS002
00571  2050-DELETE-LIST-QUEUE.                                          INKCS002
00572                                                                   INKCS002
00573      EXEC CICS                                                    INKCS002
00574          DELETEQ TS                                               INKCS002
00575              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS002
00576              RESP  (PV-CICS-RESP)                                 INKCS002
00577      END-EXEC.                                                    INKCS002
00578                                                                   INKCS002
00579      IF (PV-CICS-RESP = DFHRESP(NORMAL)                              CL**2
00580      OR  PV-CICS-RESP = DFHRESP(QIDERR))                             CL**2
00581          CONTINUE                                                 INKCS002
00582      ELSE                                                         INKCS002
00583          SET DP013-CICS-ABEND      TO TRUE                        INKCS002
00584          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS002
00585                                                                   INKCS002
00586          MOVE '2050-DELETE-LIST-QUEUE'                            INKCS002
00587                                    TO DP013-PARAGRAPH             INKCS002
00588          MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'    INKCS002
00589                                    TO DP013-MESSAGE-TEXT (1)      INKCS002
00590          PERFORM DP013-0000-PROCESS-ABEND                         INKCS002
00591      END-IF.                                                      INKCS002
00592                                                                   INKCS002
00593      MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.  INKCS002
00594 /                                                                    CL**2
00595 *----------------------------------------------------------------*INKCS002
00596 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*INKCS002
00597 * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *INKCS002
00598 * CICS ARCHITECTURE API.                                         *INKCS002
00599 *----------------------------------------------------------------*INKCS002
00600                                                                      CL**2
00601  2100-CHECK-FUNCTION-KEY.                                         INKCS002
00602                                                                      CL**2
00603      EVALUATE TRUE                                                INKCS002
00604          WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                  INKCS002
00605              IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO              INKCS002
00606                  MOVE ASC-NUMBER-OF-SELECTED-ITEMS                INKCS002
00607                                 TO IN002-SELECTED-ITEMS              CL**2
00608                                    IN001-NUMBER-OF-SELECTED-ITEMS    CL*14
00609                  PERFORM 5000-PREPARE-SEL-QUEUE                   INKCS002
00610              ELSE                                                 INKCS002
00611                  SET DP030-OVERRIDE-GO                            INKCS002
00612                                    TO TRUE                        INKCS002
00613                  MOVE PC-TSYMSG-00057                             INKCS002
00614                                    TO DP020-MSG-NUMBER            INKCS002
00615                  SET DP020-MSG-WARNING                            INKCS002
00616                                    TO TRUE                        INKCS002
00617                  PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                CL*38
00618              END-IF                                               INKCS002
00619                                                                   INKCS002
00430          WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                 CL190
00663              INITIALIZE IN007-COMMAREA                            DPKCS036
00434              MOVE DP020-LOCATION-TYPE                                CL190
00434                                    TO IN007-LOCATION-TYPE            CL190
01537              MOVE IN001-DISPLAY-COMPLETE-SW                          CL*46
01538                                    TO IN007-DISPLAY-COMPLETE-IND     CL*44
00432              MOVE ASC-NUMBER-OF-SELECTED-ITEMS                       CL190
00434                                    TO PV-NBR-SEL-ITEMS               CL190
00431              PERFORM 2170-PROCESS-SELECT-FOR-PRINT                   CL195
00431              PERFORM 2160-PRINT-REPORT                               CL195
00432              MOVE PC-TSYMSG-01436  TO DP020-MSG-NUMBER               CL190
                   MOVE PV-NBR-SEL-ITEMS TO DP020-MSG-TEXT                      
00433              SET DP020-MSG-INFORMATIONAL                             CL190
00434                                    TO TRUE                           CL190
00433              SET PS-SEL-AREA-PRINT TO TRUE                           CL190
00622              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS002
00619                                                                   INKCS002
00430          WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                 CL190
                   IF ASC-AREA-ASTERISK                                         
00663                  INITIALIZE IN007-COMMAREA                        DPKCS036
337200                 SET IN007-PRINT-ALL-AREAS                                
00434                                    TO TRUE                           CL190
00434                  MOVE DP020-LOCATION-TYPE                            CL190
00434                                    TO IN007-LOCATION-TYPE            CL190
01537                  MOVE IN001-DISPLAY-COMPLETE-SW                      CL*46
01538                                    TO IN007-DISPLAY-COMPLETE-IND     CL*44
00431                  PERFORM 2160-PRINT-REPORT                           CL195
00432                  MOVE PC-TSYMSG-00098                                CL190
00432                                    TO DP020-MSG-NUMBER               CL190
00433                  SET DP020-MSG-INFORMATIONAL                         CL190
00434                                    TO TRUE                           CL190
00618              ELSE                                                 INKCS002
00611                  SET DP030-OVERRIDE-GO                            INKCS002
00612                                    TO TRUE                        INKCS002
00613                  MOVE PC-TSYMSG-01997                             INKCS002
00614                                    TO DP020-MSG-NUMBER            INKCS002
00615                  SET DP020-MSG-WARNING                            INKCS002
00616                                    TO TRUE                        INKCS002
00618              END-IF                                               INKCS002
00619                                                                   INKCS002
00620          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  INKCS002
00621              MOVE +1               TO PV-TOP-ITEM                 INKCS002
00622              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS002
00623                                                                   INKCS002
00624          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   INKCS002
00625              PERFORM 2110-BROWSE-BACKWARD                         INKCS002
00626              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS002
00627                                                                   INKCS002
00628          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   INKCS002
00629              PERFORM 2120-BROWSE-FORWARD                          INKCS002
00630              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS002
00631                                                                   INKCS002
00632          WHEN DP020-SRC-AID = DP016-ENTER                         INKCS002
00633              PERFORM 2130-CHECK-FOR-JUMP-BROWSE                   INKCS002
00634              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL*31
00635                                                                   INKCS002
00636          WHEN OTHER                                               INKCS002
00637              SET DP013-NO-ROLLBACK                                INKCS002
00638                  DP013-XCTL-DISPLAY-RESTART                       INKCS002
00639                  DP013-CICS-ABEND  TO TRUE                        INKCS002
00640              MOVE '2100-CHECK-FUNCTION-KEY'                       INKCS002
00641                                    TO DP013-PARAGRAPH             INKCS002
00642              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      INKCS002
00643                                    TO DP013-MESSAGE-TEXT (1)      INKCS002
00644              PERFORM DP013-0000-PROCESS-ABEND                     INKCS002
00645      END-EVALUATE.                                                INKCS002
00646                                                                      CL**2
00647 *----------------------------------------------------------------*INKCS002
00648 *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *INKCS002
00649 *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *INKCS002
00650 *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *INKCS002
00651 *    MESSAGE.                                                    *INKCS002
00652 *----------------------------------------------------------------*INKCS002
00653                                                                   INKCS002
00654  2110-BROWSE-BACKWARD.                                            INKCS002
00655                                                                      CL**3
00656      SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL    INKCS002
00657          GIVING PV-TOP-ITEM.                                      INKCS002
00658                                                                      CL**2
00659      IF  ((PV-TOP-ITEM < +1)                                      INKCS002
00660        OR (PV-TOP-ITEM = +1))                                     INKCS002
00661          MOVE +1                   TO PV-TOP-ITEM                 INKCS002
00662 *        -- TOP OF LIST --                                           CL**2
00663          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS002
00664          SET DP020-MSG-INFORMATIONAL TO TRUE                      INKCS002
00665      END-IF.                                                      INKCS002
00666                                                                   INKCS002
00667 /                                                                    CL**2
00668 *----------------------------------------------------------------*INKCS002
00669 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *INKCS002
00670 *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *INKCS002
00671 *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *INKCS002
00672 *----------------------------------------------------------------*INKCS002
00673                                                                   INKCS002
00674  2120-BROWSE-FORWARD.                                             INKCS002
00675                                                                   INKCS002
00676      ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL              INKCS002
00677          GIVING PV-TOP-ITEM.                                      INKCS002
00678      IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                   INKCS002
00679          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS002
00680                                    TO PV-TOP-ITEM                 INKCS002
00681 *        -- BOTTOM OF LIST --                                     INKCS002
00682          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            INKCS002
00683          SET DP020-MSG-INFORMATIONAL                              INKCS002
00684                                    TO TRUE                        INKCS002
00685      ELSE                                                         INKCS002
00686          COMPUTE PV-BOTTOM-ITEM =                                 INKCS002
00687              PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                 INKCS002
00688          IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ            INKCS002
00689 *            -- BOTTOM OF LIST --                                 INKCS002
00690              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            INKCS002
00691              SET DP020-MSG-INFORMATIONAL                          INKCS002
00692                                    TO TRUE                        INKCS002
00693          END-IF                                                   INKCS002
00694      END-IF.                                                      INKCS002
00695 /                                                                    CL**2
00696 *----------------------------------------------------------------*INKCS002
00697 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *INKCS002
00698 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *INKCS002
00699 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *INKCS002
00700 *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *INKCS002
00701 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *INKCS002
00702 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *INKCS002
00703 *----------------------------------------------------------------*INKCS002
00704                                                                   INKCS002
00705  2130-CHECK-FOR-JUMP-BROWSE.                                      INKCS002
00706                                                                      CL**2
00707      SET PS-NO-ERROR               TO TRUE.                       INKCS002
00708      IF  ASC-START-ITEM-REQUEST = SPACE                              CL*28
00709          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS002
00710                                    TO PV-TOP-ITEM                 INKCS002
00711      ELSE                                                         INKCS002
00712          MOVE ASC-START-ITEM-REQUEST                              INKCS002
00713                                    TO DP010I-UNEDITED-FIELD       INKCS002
00714                                       PV-START-ITEM-REQUEST          CL*31
00715          MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS       INKCS002
00716          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS     INKCS002
00717          SET DP010I-NEGATIVE-NOT-ALLOWED                          INKCS002
00718                                    TO TRUE                        INKCS002
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
00720                                                                   INKCS002
00721          IF  NOT DP010I-ERROR-DETECTED                            INKCS002
00722              MOVE DP010I-NUMERIC-FIELD                            INKCS002
00723                                    TO  ASC-START-ITEM-REQUEST-N   INKCS002
00724              IF  ASC-START-ITEM-REQUEST-N > ZERO                  INKCS002
00725                  IF  ASC-START-ITEM-REQUEST-N >                   INKCS002
00726                                           ASC-NUMBER-OF-ITEMS-READINKCS002
00727                      MOVE SPACE TO ASC-START-ITEM-REQUEST         INKCS002
00728                      COMPUTE PV-TOP-ITEM =                        INKCS002
00729                              ASC-NUMBER-OF-ITEMS-READ             INKCS002
00730                              - PC-ITEMS-PER-PANEL + 1             INKCS002
00731                      IF  PV-TOP-ITEM < +1                         INKCS002
00732                          MOVE +1   TO PV-TOP-ITEM                 INKCS002
00733                          MOVE PC-TSYMSG-00070                     INKCS002
00734                                    TO DP020-MSG-NUMBER            INKCS002
00735                          SET DP020-MSG-INFORMATIONAL              INKCS002
00736                                    TO TRUE                        INKCS002
00737                      END-IF                                       INKCS002
00738                  ELSE                                             INKCS002
00739                      MOVE ASC-START-ITEM-REQUEST-N                INKCS002
00740                                    TO PV-TOP-ITEM                 INKCS002
00741                      MOVE SPACE    TO ASC-START-ITEM-REQUEST      INKCS002
00742                      COMPUTE PV-BOTTOM-ITEM =                     INKCS002
00743                          PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1     INKCS002
00744                      IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READINKCS002
00745 *                        -- BOTTOM OF LIST --                     INKCS002
00746                          MOVE PC-TSYMSG-00070                     INKCS002
00747                                    TO DP020-MSG-NUMBER            INKCS002
00748                          SET DP020-MSG-INFORMATIONAL              INKCS002
00749                                    TO TRUE                        INKCS002
00750                      END-IF                                       INKCS002
00751                  END-IF                                           INKCS002
00752              ELSE                                                 INKCS002
00753 *                --- VALUE OUT OF RANGE ----                      INKCS002
00754                  MOVE PC-TSYMSG-00101                             INKCS002
00755                                    TO DP020-MSG-NUMBER            INKCS002
00756                  MOVE -1           TO ASTRTITL                    INKCS002
00757                  SET DP030-SET-CURSOR-APPL-1                      INKCS002
00758                                    TO TRUE                        INKCS002
00759                  MOVE DP015-REVERSE                               INKCS002
00760                                    TO ASTRTITH                    INKCS002
00761                  MOVE DP015-RED    TO ASTRTITC                    INKCS002
00762                  MOVE DP015-UNP-NUM-BRT-OFF                          CL*26
00763                                    TO ASTRTITA                    INKCS002
00764                  SET DP020-MSG-FATAL                              INKCS002
00765                                    TO TRUE                        INKCS002
00766                  SET PS-ERROR      TO TRUE                        INKCS002
00767              END-IF                                               INKCS002
00768                                                                   INKCS002
00769          ELSE                                                     INKCS002
00770              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER            INKCS002
00771              MOVE -1               TO ASTRTITL                    INKCS002
00772              SET DP030-SET-CURSOR-APPL-1 TO TRUE                  INKCS002
00773              MOVE DP015-REVERSE    TO ASTRTITH                    INKCS002
00774              MOVE DP015-RED        TO ASTRTITC                    INKCS002
00775              MOVE DP015-UNP-NUM-BRT-OFF                              CL*26
00776                                    TO ASTRTITA                    INKCS002
00777              SET DP020-MSG-FATAL   TO TRUE                        INKCS002
00778              SET PS-ERROR          TO TRUE                        INKCS002
00779          END-IF                                                   INKCS002
00780      END-IF.                                                      INKCS002
00781 /                                                                    CL**2
00782 *----------------------------------------------------------------*INKCS002
00783 *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *INKCS002
00784 *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *INKCS002
00785 *----------------------------------------------------------------*INKCS002
00786                                                                      CL**2
00787  2140-DESELECT-ALL-ITEMS.                                         INKCS002
00788                                                                      CL**2
00789      MOVE +1                       TO ASC-BLK-SUB.                INKCS002
00790                                                                   INKCS002
00791      PERFORM 2141-CLEAR-SELECTED-INDS                             INKCS002
00792          VARYING ASC-ITEM-SUB                                     INKCS002
00793          FROM 1 BY 1                                              INKCS002
00794          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS002
00795                                                                   INKCS002
00796      ADD +1                        TO ASC-BLK-SUB.                INKCS002
00797                                                                   INKCS002
00798      PERFORM 2141-CLEAR-SELECTED-INDS                             INKCS002
00799          VARYING ASC-ITEM-SUB                                     INKCS002
00800          FROM 1 BY 1                                              INKCS002
00801          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS002
00802                                                                   INKCS002
00803      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS002
00804                                    TO PV-LIST-ITEM-NUMBER.        INKCS002
00805                                                                   INKCS002
00806      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS002
00807          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS002
00808                                                                   INKCS002
00809      IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                     INKCS002
00810         (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)      INKCS002
00811          MOVE +1                   TO ASC-BLK-SUB                 INKCS002
00812                                                                   INKCS002
00813          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS002
00814                                                                   INKCS002
00815          MOVE +1                   TO PV-BLOCK-NUMBER             INKCS002
00816                                                                   INKCS002
00817          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS002
00818              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS002
00819                                                                   INKCS002
00820          PERFORM                                                  INKCS002
00821              UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY       INKCS002
00822                                                                   INKCS002
00823              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
00824                                                                   INKCS002
00825              PERFORM 2141-CLEAR-SELECTED-INDS                     INKCS002
00826                  VARYING ASC-ITEM-SUB FROM 1 BY 1                 INKCS002
00827                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS002
00828                                                                   INKCS002
00829              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS002
00830                                                                   INKCS002
00831              ADD +1                TO PV-BLOCK-NUMBER             INKCS002
00832                                                                   INKCS002
00833          END-PERFORM                                              INKCS002
00834                                                                   INKCS002
00835          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS002
00836              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS002
00837                                                                   INKCS002
00838          PERFORM                                                  INKCS002
00839              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS002
00840                    ASC-HIGHEST-BLOCK-WRITTEN                      INKCS002
00841                                                                   INKCS002
00842              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS002
00843                                    TO PV-BLOCK-NUMBER             INKCS002
00844                                                                   INKCS002
00845              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
00846                                                                   INKCS002
00847              PERFORM 2141-CLEAR-SELECTED-INDS                     INKCS002
00848                  VARYING ASC-ITEM-SUB                             INKCS002
00849                  FROM 1 BY 1                                      INKCS002
00850                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS002
00851                                                                   INKCS002
00852              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS002
00853                                                                   INKCS002
00854              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS002
00855                                                                   INKCS002
00856          END-PERFORM                                              INKCS002
00857                                                                   INKCS002
00858          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS002
00859                                    TO PV-BLOCK-NUMBER             INKCS002
00860                                                                   INKCS002
00861          PERFORM 4410-READ-TEMP-STORAGE                           INKCS002
00862      END-IF.                                                      INKCS002
00863                                                                   INKCS002
00864      MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS. INKCS002
00865      MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.INKCS002
00866      MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.  INKCS002
00867                                                                   INKCS002
00870 /                                                                    CL**2
00871 *----------------------------------------------------------------*INKCS002
00872 *    MARK EACH ITEM AS BEING NOT SELECTED.                       *INKCS002
00873 *----------------------------------------------------------------*INKCS002
00874                                                                   INKCS002
00875  2141-CLEAR-SELECTED-INDS.                                        INKCS002
00876                                                                   INKCS002
00877      IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)           INKCS002
00878              NOT EQUAL ZERO                                       INKCS002
00879          IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS002
00880              SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS002
00881                  ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS002
00882                  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                  INKCS002
00883                                    TO TRUE                        INKCS002
00884          END-IF                                                   INKCS002
00885      END-IF.                                                      INKCS002
00886                                                                   INKCS002
00887 /                                                                    CL**2
00888 *----------------------------------------------------------------*INKCS002
00889 *  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *INKCS002
00890 *  FOR ALL ITEMS.                                                *INKCS002
00891 *----------------------------------------------------------------*INKCS002
00892                                                                   INKCS002
00893  2150-SELECT-ALL-ITEMS.                                           INKCS002
00894                                                                   INKCS002
00895      MOVE +1                       TO ASC-BLK-SUB.                INKCS002
00896                                                                   INKCS002
00897      PERFORM 2151-SET-SELECTED-INDS                               INKCS002
00898          VARYING ASC-ITEM-SUB                                     INKCS002
00899          FROM 1 BY 1                                              INKCS002
00900          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS002
00901                                                                   INKCS002
00902      ADD +1                        TO ASC-BLK-SUB.                INKCS002
00903                                                                   INKCS002
00904      PERFORM 2151-SET-SELECTED-INDS                               INKCS002
00905          VARYING ASC-ITEM-SUB                                     INKCS002
00906          FROM 1 BY 1                                              INKCS002
00907          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS002
00908                                                                   INKCS002
00909      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS002
00910                                    TO PV-LIST-ITEM-NUMBER.        INKCS002
00911                                                                   INKCS002
00912      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS002
00913          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS002
00914                                                                   INKCS002
00915      IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                     INKCS002
00916         (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)    INKCS002
00917          MOVE +1                   TO ASC-BLK-SUB                 INKCS002
00918                                                                   INKCS002
00919          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS002
00920                                                                   INKCS002
00921          MOVE +1                   TO PV-BLOCK-NUMBER             INKCS002
00922                                                                   INKCS002
00923          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS002
00924              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS002
00925                                                                   INKCS002
00926          PERFORM                                                  INKCS002
00927              UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY       INKCS002
00928                                                                   INKCS002
00929              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
00930                                                                   INKCS002
00931              PERFORM 2151-SET-SELECTED-INDS                       INKCS002
00932                  VARYING ASC-ITEM-SUB                             INKCS002
00933                  FROM 1 BY 1                                      INKCS002
00934                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS002
00935                                                                   INKCS002
00936              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS002
00937                                                                   INKCS002
00938              ADD +1                TO PV-BLOCK-NUMBER             INKCS002
00939                                                                   INKCS002
00940          END-PERFORM                                              INKCS002
00941                                                                   INKCS002
00942          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS002
00943              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS002
00944                                                                   INKCS002
00945          PERFORM                                                  INKCS002
00946              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS002
00947                    ASC-HIGHEST-BLOCK-WRITTEN                      INKCS002
00948                                                                   INKCS002
00949              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS002
00950                                    TO PV-BLOCK-NUMBER             INKCS002
00951                                                                   INKCS002
00952              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
00953                                                                   INKCS002
00954              PERFORM 2151-SET-SELECTED-INDS                       INKCS002
00955                  VARYING ASC-ITEM-SUB                             INKCS002
00956                  FROM 1 BY 1                                      INKCS002
00957                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS002
00958                                                                   INKCS002
00959              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS002
00960                                                                   INKCS002
00961              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS002
00962                                                                   INKCS002
00963          END-PERFORM                                              INKCS002
00964                                                                   INKCS002
00965          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS002
00966                                    TO PV-BLOCK-NUMBER             INKCS002
00967                                                                   INKCS002
00968          PERFORM 4410-READ-TEMP-STORAGE                           INKCS002
00969      END-IF.                                                      INKCS002
00970                                                                   INKCS002
00971                                                                   INKCS002
00972      MOVE ASC-NUMBER-OF-ITEMS-READ TO                             INKCS002
00973                        ASC-NUMBER-OF-SELECTED-ITEMS.              INKCS002
00974                                                                   INKCS002
00975      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        INKCS002
00976                                                                   INKCS002
00977 /                                                                    CL**2
00978 *----------------------------------------------------------------*INKCS002
00979 *    MARK EACH ITEM AS BEING SELECTED.                           *INKCS002
00980 *----------------------------------------------------------------*INKCS002
00981                                                                   INKCS002
00982  2151-SET-SELECTED-INDS.                                          INKCS002
00983                                                                   INKCS002
00984      IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)           INKCS002
00985              NOT EQUAL ZERO                                       INKCS002
00986          IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)             INKCS002
00987              SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)             INKCS002
00988                  ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)        INKCS002
00989                  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                  INKCS002
00990                                TO  TRUE                           INKCS002
00991          END-IF                                                   INKCS002
00992      END-IF.                                                      INKCS002
00976                                                                   INKCS002
00977 /                                                                    CL**2
00978 *----------------------------------------------------------------*INKCS002
00428 *  MOVE DATA NECESSARY TO THE PRINT REPORT PROGRAM AND START THE *GRK00004
00429 *  TRANSACTION TO START INKCS007.                                *GRK00004
00978 *----------------------------------------------------------------*INKCS002
00976                                                                   INKCS002
00431  2160-PRINT-REPORT.                                               GRK00004
00976                                                                   INKCS002
           MOVE IN001-STORE-NBR  TO IN007-STORE-NBR.                            
108000     MOVE IN001-STORE-NAME TO IN007-STORE-NAME.                   06660022
00976                                                                   INKCS002
116400     EXEC CICS                                                            
116500        START TRANSID ('I007')                                            
116600              FROM    (IN007-COMMAREA)                                    
116700     END-EXEC.                                                            
00786                                                                      CL**2
00781 /                                                                    CL**2
00782 *----------------------------------------------------------------*INKCS002
00783 *    IF THE USER HITS THE PRINT KEY, CLEAR THE SELECTION         *INKCS002
00784 *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *INKCS002
00785 *----------------------------------------------------------------*INKCS002
00786                                                                      CL**2
00787  2170-PROCESS-SELECT-FOR-PRINT.                                   INKCS002
00788                                                                      CL**2
00789      MOVE +1                       TO ASC-BLK-SUB.                INKCS002
00790                                                                   INKCS002
00791      PERFORM 2171-CLEAR-SELECTED-INDS                             INKCS002
00792          VARYING ASC-ITEM-SUB                                     INKCS002
00793          FROM 1 BY 1                                              INKCS002
00794          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS002
00795                                                                   INKCS002
00796      ADD +1                        TO ASC-BLK-SUB.                INKCS002
00797                                                                   INKCS002
00798      PERFORM 2171-CLEAR-SELECTED-INDS                             INKCS002
00799          VARYING ASC-ITEM-SUB                                     INKCS002
00800          FROM 1 BY 1                                              INKCS002
00801          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS002
00802                                                                   INKCS002
00803      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS002
00804                                    TO PV-LIST-ITEM-NUMBER.        INKCS002
00805                                                                   INKCS002
00806      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS002
00807          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS002
00808                                                                   INKCS002
00809      IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                     INKCS002
00810         (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)      INKCS002
00811          MOVE +1                   TO ASC-BLK-SUB                 INKCS002
00812                                                                   INKCS002
00813          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS002
00814                                                                   INKCS002
00815          MOVE +1                   TO PV-BLOCK-NUMBER             INKCS002
00816                                                                   INKCS002
00817          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS002
00818              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS002
00819                                                                   INKCS002
00820          PERFORM                                                  INKCS002
00821              UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY       INKCS002
00822                                                                   INKCS002
00823              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
00824                                                                   INKCS002
00825              PERFORM 2171-CLEAR-SELECTED-INDS                     INKCS002
00826                  VARYING ASC-ITEM-SUB FROM 1 BY 1                 INKCS002
00827                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS002
00828                                                                   INKCS002
00829              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS002
00830                                                                   INKCS002
00831              ADD +1                TO PV-BLOCK-NUMBER             INKCS002
00832                                                                   INKCS002
00833          END-PERFORM                                              INKCS002
00834                                                                   INKCS002
00835          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS002
00836              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS002
00837                                                                   INKCS002
00838          PERFORM                                                  INKCS002
00839              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS002
00840                    ASC-HIGHEST-BLOCK-WRITTEN                      INKCS002
00841                                                                   INKCS002
00842              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS002
00843                                    TO PV-BLOCK-NUMBER             INKCS002
00844                                                                   INKCS002
00845              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
00846                                                                   INKCS002
00847              PERFORM 2171-CLEAR-SELECTED-INDS                     INKCS002
00848                  VARYING ASC-ITEM-SUB                             INKCS002
00849                  FROM 1 BY 1                                      INKCS002
00850                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS002
00851                                                                   INKCS002
00852              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS002
00853                                                                   INKCS002
00854              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS002
00855                                                                   INKCS002
00856          END-PERFORM                                              INKCS002
00857                                                                   INKCS002
00858          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS002
00859                                    TO PV-BLOCK-NUMBER             INKCS002
00860                                                                   INKCS002
00861          PERFORM 4410-READ-TEMP-STORAGE                           INKCS002
00862      END-IF.                                                      INKCS002
00863                                                                   INKCS002
00864      MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS. INKCS002
00865      MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.INKCS002
00866      MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.  INKCS002
00867                                                                   INKCS002
00870 /                                                                    CL**2
00871 *----------------------------------------------------------------*INKCS002
00872 *    MARK EACH ITEM AS BEING NOT SELECTED AND SET THE ACTION     *INKCS002
00872 *    FIELD TO PRINT.                                             *INKCS002
00873 *----------------------------------------------------------------*INKCS002
00874                                                                   INKCS002
00875  2171-CLEAR-SELECTED-INDS.                                        INKCS002
00876                                                                   INKCS002
00877      IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)           INKCS002
00878              NOT EQUAL ZERO                                       INKCS002
00879          IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS002
00880              SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS002
00881                  ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS002
00882                  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                  INKCS002
00883                                TO TRUE                            INKCS002
                   MOVE PC-PRT TO ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)         
                   ADD +1 TO PV-AREA-SUB                                        
10800              MOVE ASC-AREA-NBR (ASC-BLK-SUB ASC-ITEM-SUB)         06660022
10800                                TO IN007-AREA-NBR (PV-AREA-SUB)    06660022
00884          END-IF                                                   INKCS002
00885      END-IF.                                                      INKCS002
00976                                                                   INKCS002
00977 /                                                                    CL**2
00994 *----------------------------------------------------------------*   CL**2
00995 *  MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE     *   CL**2
00996 *  SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM.  *   CL**2
00997 *----------------------------------------------------------------*   CL**2
00998                                                                      CL**2
00999  2200-MOVE-SCREEN-TO-COMMAREA.                                    INKCS002
01000                                                                   INKCS002
01001      MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.             INKCS002
01002      MOVE +1                       TO PV-SUB.                     INKCS002
01003                                                                      CL**2
01004      IF  ASTRTITL > ZERO                                             CL*13
01005          MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST             CL*13
01006      ELSE                                                            CL*13
01007          IF  ASTRTITF EQUAL DP015-ERASE-EOF                          CL*28
01008              INITIALIZE ASC-START-ITEM-REQUEST                       CL*28
01009          END-IF                                                      CL*28
01010      END-IF.                                                         CL*28
01011                                                                      CL*13
01012      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS002
01013                                                                      CL**2
01014      PERFORM 2210-MOVE-LINES-TO-COMMAREA                          INKCS002
01015          VARYING PV-SUB                                           INKCS002
01016          FROM 1 BY 1                                              INKCS002
01017          UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                      INKCS002
01018                                                                      CL**2
01019 ******************************************************************INKCS002
01020 ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**INKCS002
01021 ******************************************************************INKCS002
01022                                                                      CL**2
01023  2210-MOVE-LINES-TO-COMMAREA.                                     INKCS002
01024                                                                      CL**2
01025      IF  MR-SELECTL (PV-SUB) > ZERO                               INKCS002
01026          SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             INKCS002
01027          MOVE MR-SELECTI (PV-SUB)                                 INKCS002
01028                                    TO ASC-TEMP-SELECTED-SW        INKCS002
01029                                  (ASC-BLK-SUB, ASC-ITEM-SUB)      INKCS002
01030      ELSE                                                         INKCS002
01031          IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                INKCS002
01032                  SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE     INKCS002
01033                  MOVE SPACE        TO ASC-TEMP-SELECTED-SW        INKCS002
01034                                  (ASC-BLK-SUB, ASC-ITEM-SUB)      INKCS002
01035          END-IF                                                   INKCS002
01036      END-IF.                                                      INKCS002
01037                                                                      CL**2
01038      ADD +1                        TO ASC-ITEM-SUB.               INKCS002
01039      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS002
01040          ADD +1                    TO ASC-BLK-SUB                 INKCS002
01041          MOVE +1                   TO ASC-ITEM-SUB                INKCS002
01042      END-IF.                                                      INKCS002
01043                                                                      CL**2
01044 *----------------------------------------------------------------*   CL**2
01045 *  EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE       *   CL**2
01046 *  COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY).  *   CL**2
01047 *----------------------------------------------------------------*   CL**2
01048                                                                      CL**2
01049  3000-EDIT-DATA-IN-COMMAREA.                                      INKCS002
01050                                                                      CL**2
01051      SET PS-NO-ERROR               TO TRUE.                       INKCS002
01052      PERFORM 3200-RESET-ATTRIBUTES.                               INKCS002
01053      MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                     INKCS002
01054 **                                                                INKCS002
01055 * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.              INKCS002
01056 * EDITTING IS DONE FROM THE BOTTOM UP.                            INKCS002
01057 **                                                                INKCS002
01058      COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                       INKCS002
01059          ASC-ITEMS-DISPLAYED - 1.                                 INKCS002
01060      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS002
01061      PERFORM 3300-EDIT-SELECTIONS                                 INKCS002
01062          VARYING PV-SUB                                           INKCS002
01063          FROM PV-SUB BY -1                                        INKCS002
01064          UNTIL PV-SUB < +1.                                          CL*28
01065                                                                      CL**2
01066      IF  PS-NO-ERROR                                              INKCS002
01067          PERFORM 3400-CHECK-RANGE-SEQUENCE                        INKCS002
01068      END-IF.                                                      INKCS002
01069                                                                      CL**2
01070      IF  ((PS-NO-ERROR)                                           INKCS002
01071        AND (ASC-START-OF-SELECTED-RANGE > ZERO)                   INKCS002
01072        AND (ASC-END-OF-SELECTED-RANGE > ZERO))                    INKCS002
01073              PERFORM 3500-MARK-RANGE-AS-SELECTED                  INKCS002
01074      END-IF.                                                      INKCS002
01075 /                                                                    CL**2
01076 *----------------------------------------------------------------*INKCS002
01077 *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *INKCS002
01078 *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*INKCS002
01079 *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *INKCS002
01080 *                                                                *INKCS002
01081 *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *INKCS002
01082 *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *INKCS002
01083 *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *INKCS002
01084 *    US IN THE 2ND BLOCK OF THE ARRAY.                           *INKCS002
01085 *----------------------------------------------------------------*INKCS002
01086                                                                   INKCS002
01087  3100-SET-SUBSCRIPTS.                                             INKCS002
01088                                                                   INKCS002
01089      COMPUTE ASC-ITEM-SUB =                                       INKCS002
01090          (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.        INKCS002
01091                                                                   INKCS002
01092      IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                         INKCS002
01093          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS002
01094                                                                   INKCS002
01095          MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB               INKCS002
01096      ELSE                                                         INKCS002
01097          MOVE +1                     TO ASC-BLK-SUB               INKCS002
01098      END-IF.                                                      INKCS002
01099                                                                      CL**2
01100 *----------------------------------------------------------------*   CL**2
01101 *  RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.      *   CL**2
01102 *----------------------------------------------------------------*   CL**2
01103                                                                   INKCS002
01104  3200-RESET-ATTRIBUTES.                                           INKCS002
01105                                                                      CL**2
01106      MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                   INKCS002
01107      MOVE DP015-GREEN              TO ASTRTITC.                   INKCS002
01108      MOVE DP015-UNDERLINE          TO ASTRTITH.                   INKCS002
01109                                                                      CL**2
01110      PERFORM                                                      INKCS002
01111          VARYING PV-SUB                                           INKCS002
01112          FROM 1 BY 1                                              INKCS002
01113          UNTIL PV-SUB > PC-ITEMS-PER-PANEL                        INKCS002
01114          MOVE DP015-UNP-ALP-NOR-OFF                               INKCS002
01115                                    TO MR-SELECTA (PV-SUB)         INKCS002
01116          MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)         INKCS002
01117          MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)         INKCS002
01118      END-PERFORM.                                                 INKCS002
01119                                                                      CL**2
01120 *----------------------------------------------------------------*   CL**2
01121 *  EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS     *   CL**2
01122 *  WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'    *   CL**2
01123 *  IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR     *   CL**2
01124 *  WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.              *   CL**2
01125 *----------------------------------------------------------------*   CL**2
01126                                                                      CL**2
01127  3300-EDIT-SELECTIONS.                                            INKCS002
01128                                                                      CL**2
01129      EVALUATE TRUE                                                INKCS002
01130          WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS002
01131              = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS002
01132                  CONTINUE                                         INKCS002
01133                                                                      CL**2
01134          WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS002
01135              IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS002
01136                  ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS   INKCS002
01137                  MOVE ASC-TEMP-SELECTED-SW                        INKCS002
01138                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS002
01139                                 TO ASC-SELECTED-SW                INKCS002
01140                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS002
01141              END-IF                                               INKCS002
01142                                                                      CL**2
01143          WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS002
01144                  = SPACE                                          INKCS002
01145              EVALUATE TRUE                                        INKCS002
01146                  WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS002
01147                      SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMSINKCS002
01148                      MOVE SPACE TO ASC-SELECTED-SW                INKCS002
01149                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS002
01150                  WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS002
01151                      MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE    INKCS002
01152                      MOVE SPACE TO ASC-SELECTED-SW                INKCS002
01153                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS002
01154                  WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)          INKCS002
01155                      MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE      INKCS002
01156                      MOVE SPACE TO ASC-SELECTED-SW                INKCS002
01157                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS002
01158              END-EVALUATE                                         INKCS002
01159                                                                      CL**2
01160          WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)           INKCS002
01161              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS002
01162                  SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS    INKCS002
01163              END-IF                                               INKCS002
01164              PERFORM 3310-EDIT-RANGE                              INKCS002
01165                                                                      CL**2
01166          WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)             INKCS002
01167              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS002
01168                  SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS    INKCS002
01169              END-IF                                               INKCS002
01170              PERFORM 3310-EDIT-RANGE                              INKCS002
01171                                                                      CL**2
01172          WHEN OTHER                                               INKCS002
01173              MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER           INKCS002
01174              SET DP020-MSG-FATAL    TO TRUE                       INKCS002
01175              MOVE -1                TO MR-SELECTL (PV-SUB)        INKCS002
01176              SET DP030-SET-CURSOR-APPL-1                          INKCS002
01177                                     TO TRUE                       INKCS002
01178              MOVE DP015-RED         TO MR-SELECTC (PV-SUB)        INKCS002
01179              MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)        INKCS002
01180              SET PS-ERROR TO TRUE                                 INKCS002
01181              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS002
01182                  SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS     INKCS002
01183                  MOVE SPACE         TO ASC-SELECTED-SW            INKCS002
01184                                         (ASC-BLK-SUB ASC-ITEM-SUB)INKCS002
01185              END-IF                                               INKCS002
01186                                                                      CL**2
01187      END-EVALUATE.                                                INKCS002
01188                                                                   INKCS002
01189      SUBTRACT +1             FROM PV-ITEM-IN-USE.                 INKCS002
01190      SUBTRACT +1 FROM ASC-ITEM-SUB.                               INKCS002
01191      IF  ASC-ITEM-SUB < 1                                         INKCS002
01192          SUBTRACT +1 FROM ASC-BLK-SUB                             INKCS002
01193          MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                INKCS002
01194      END-IF.                                                      INKCS002
01195                                                                      CL**2
01196 *----------------------------------------------------------------*   CL**2
01197 *  IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A       *   CL**2
01198 *  RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1     *   CL**2
01199 *  ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) *   CL**2
01200 *  THE ENDING.                                                   *   CL**2
01201 *----------------------------------------------------------------*   CL**2
01202                                                                      CL**2
01203  3310-EDIT-RANGE.                                                 INKCS002
01204                                                                      CL**2
01205      IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                INKCS002
01206          IF  ASC-START-OF-SELECTED-RANGE = ZERO                   INKCS002
01207              MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)INKCS002
01208                                    TO ASC-SELECTED-SW             INKCS002
01209                                         (ASC-BLK-SUB ASC-ITEM-SUB)INKCS002
01210              MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE INKCS002
01211          ELSE                                                     INKCS002
01212              IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE     INKCS002
01213                  CONTINUE                                         INKCS002
01214              ELSE                                                 INKCS002
01215 *                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --        INKCS002
01216                  MOVE PC-TSYMSG-00054                             INKCS002
01217                                    TO DP020-MSG-NUMBER            INKCS002
01218                  SET DP020-MSG-FATAL                              INKCS002
01219                                    TO TRUE                        INKCS002
01220                  MOVE -1           TO MR-SELECTL (PV-SUB)         INKCS002
01221                  SET DP030-SET-CURSOR-APPL-1                      INKCS002
01222                                    TO TRUE                        INKCS002
01223                  MOVE DP015-RED    TO MR-SELECTC (PV-SUB)         INKCS002
01224                  MOVE DP015-UNDERLINE                             INKCS002
01225                                    TO MR-SELECTH (PV-SUB)         INKCS002
01226                  SET PS-ERROR      TO TRUE                        INKCS002
01227              END-IF                                               INKCS002
01228          END-IF                                                   INKCS002
01229      ELSE                                                         INKCS002
01230          IF  ASC-END-OF-SELECTED-RANGE = ZERO                     INKCS002
01231              MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB) INKCS002
01232                                    TO ASC-SELECTED-SW             INKCS002
01233                                      (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS002
01234              MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE   INKCS002
01235          ELSE                                                     INKCS002
01236              IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE       INKCS002
01237                  CONTINUE                                         INKCS002
01238              ELSE                                                 INKCS002
01239 *                -- ONLY ONE END-OF-RANGE ALLOWED --              INKCS002
01240                  MOVE PC-TSYMSG-00055                             INKCS002
01241                                    TO DP020-MSG-NUMBER            INKCS002
01242                  SET DP020-MSG-FATAL                              INKCS002
01243                                    TO TRUE                        INKCS002
01244                  MOVE -1           TO MR-SELECTL (PV-SUB)         INKCS002
01245                  SET DP030-SET-CURSOR-APPL-1                      INKCS002
01246                                    TO TRUE                        INKCS002
01247                  SET PS-ERROR      TO TRUE                        INKCS002
01248              END-IF                                               INKCS002
01249          END-IF                                                   INKCS002
01250      END-IF.                                                      INKCS002
01251                                                                      CL**2
01252 *----------------------------------------------------------------*   CL**2
01253 *  IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,     *   CL**2
01254 *  MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.   *   CL**2
01255 *----------------------------------------------------------------*   CL**2
01256                                                                      CL**2
01257  3400-CHECK-RANGE-SEQUENCE.                                       INKCS002
01258                                                                      CL**2
01259      IF  ASC-START-OF-SELECTED-RANGE > ZERO                       INKCS002
01260        AND  ASC-END-OF-SELECTED-RANGE > ZERO                      INKCS002
01261        AND  ASC-START-OF-SELECTED-RANGE >                         INKCS002
01262                                          ASC-END-OF-SELECTED-RANGEINKCS002
01263 *        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                INKCS002
01264          MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                 INKCS002
01265          SET DP020-MSG-FATAL TO TRUE                              INKCS002
01266          SET PS-ERROR TO TRUE                                     INKCS002
01267          PERFORM 3410-POSITION-AT-ERROR                           INKCS002
01268      END-IF.                                                      INKCS002
01269                                                                      CL**2
01270 *----------------------------------------------------------------*   CL**2
01271 *  BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE     *   CL**2
01272 *  PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR  *   CL**2
01273 *  SHOULD BE POSITIONED ON A RANGE ERROR.                        *   CL**2
01274 *----------------------------------------------------------------*   CL**2
01275                                                                      CL**2
01276  3410-POSITION-AT-ERROR.                                          INKCS002
01277                                                                      CL**2
01278      SET DP030-SET-CURSOR-APPL-1                                  INKCS002
01279                                    TO TRUE.                       INKCS002
01280      COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                 INKCS002
01281                     - PV-TOP-ITEM + 1.                            INKCS002
01282      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS002
01283          CONTINUE                                                 INKCS002
01284      ELSE                                                         INKCS002
01285          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS002
01286          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS002
01287          MOVE DP015-RED            TO MR-SELECTC (PV-SUB)         INKCS002
01288      END-IF.                                                      INKCS002
01289 *                                                                 INKCS002
01290      COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                   INKCS002
01291                     - PV-TOP-ITEM + 1.                            INKCS002
01292      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS002
01293          CONTINUE                                                 INKCS002
01294      ELSE                                                         INKCS002
01295          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS002
01296          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS002
01297          MOVE DP015-RED            TO MR-SELECTC (PV-SUB)         INKCS002
01298      END-IF.                                                      INKCS002
01299 /                                                                    CL**2
01300 *----------------------------------------------------------------*INKCS002
01301 *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *INKCS002
01302 *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *INKCS002
01303 *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *INKCS002
01304 *----------------------------------------------------------------*INKCS002
01305                                                                   INKCS002
01306  3500-MARK-RANGE-AS-SELECTED.                                     INKCS002
01307                                                                      CL**2
01308      MOVE +1                       TO  ASC-BLK-SUB.               INKCS002
01309      PERFORM 3510-MARK-SELECTED-INDS                              INKCS002
01310          VARYING ASC-ITEM-SUB                                     INKCS002
01311          FROM 1 BY 1                                              INKCS002
01312          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS002
01313                                                                   INKCS002
01314      ADD +1                        TO ASC-BLK-SUB.                INKCS002
01315      PERFORM 3510-MARK-SELECTED-INDS                              INKCS002
01316          VARYING ASC-ITEM-SUB                                     INKCS002
01317          FROM 1 BY 1                                              INKCS002
01318          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS002
01319                                                                   INKCS002
01320      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS002
01321                                    TO  PV-LIST-ITEM-NUMBER.       INKCS002
01322      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS002
01323          1 + ((PV-LIST-ITEM-NUMBER - 1) /                         INKCS002
01324                PC-ITEMS-PER-PANEL).                               INKCS002
01325                                                                   INKCS002
01326      MOVE ASC-START-OF-SELECTED-RANGE                             INKCS002
01327                                    TO PV-START-OF-SELECTED-RANGE. INKCS002
01328      COMPUTE PV-FIRST-BLOCK-IN-RANGE =                            INKCS002
01329          1 + ((PV-START-OF-SELECTED-RANGE - 1) /                  INKCS002
01330                PC-ITEMS-PER-PANEL).                               INKCS002
01331                                                                   INKCS002
01332      MOVE ASC-END-OF-SELECTED-RANGE                               INKCS002
01333                                    TO  PV-END-OF-SELECTED-RANGE.  INKCS002
01334      COMPUTE PV-LAST-BLOCK-IN-RANGE =                             INKCS002
01335          1 + ((PV-END-OF-SELECTED-RANGE - 1) /                    INKCS002
01336                PC-ITEMS-PER-PANEL).                               INKCS002
01337      COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                             INKCS002
01338          PV-FIRST-BLOCK-IN-ARRAY + 1.                             INKCS002
01339                                                                   INKCS002
01340      IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR    INKCS002
01341         (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)        INKCS002
01342          MOVE +1                   TO ASC-BLK-SUB                 INKCS002
01343          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS002
01344          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS002
01345              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS002
01346                                                                   INKCS002
01347          PERFORM                                                  INKCS002
01348              UNTIL PV-FIRST-BLOCK-IN-RANGE >                      INKCS002
01349                    PV-PREV-BLOCK-IN-ARRAY                         INKCS002
01350              MOVE PV-FIRST-BLOCK-IN-RANGE                         INKCS002
01351                                    TO PV-BLOCK-NUMBER             INKCS002
01352              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
01353              PERFORM 3510-MARK-SELECTED-INDS                      INKCS002
01354                  VARYING ASC-ITEM-SUB                             INKCS002
01355                  FROM 1 BY 1                                      INKCS002
01356                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS002
01357              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS002
01358              ADD +1                TO PV-FIRST-BLOCK-IN-RANGE     INKCS002
01359          END-PERFORM                                              INKCS002
01360                                                                   INKCS002
01361          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS002
01362              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS002
01363          PERFORM                                                  INKCS002
01364              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS002
01365                    PV-LAST-BLOCK-IN-RANGE                         INKCS002
01366              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS002
01367                                    TO PV-BLOCK-NUMBER             INKCS002
01368              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
01369              PERFORM 3510-MARK-SELECTED-INDS                      INKCS002
01370                  VARYING ASC-ITEM-SUB                             INKCS002
01371                  FROM 1 BY 1                                      INKCS002
01372                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS002
01373              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS002
01374              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS002
01375          END-PERFORM                                              INKCS002
01376                                                                   INKCS002
01377          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS002
01378                                    TO PV-BLOCK-NUMBER             INKCS002
01379          PERFORM 4410-READ-TEMP-STORAGE                           INKCS002
01380      END-IF.                                                      INKCS002
01381                                                                   INKCS002
01382      MOVE +0                       TO ASC-START-OF-SELECTED-RANGE INKCS002
01383                                       ASC-END-OF-SELECTED-RANGE.  INKCS002
01384 /                                                                    CL**2
01385 *----------------------------------------------------------------*INKCS002
01386 * SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *INKCS002
01387 *----------------------------------------------------------------*INKCS002
01388                                                                   INKCS002
01389  3510-MARK-SELECTED-INDS.                                         INKCS002
01390                                                                   INKCS002
01391      IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                         INKCS002
01392                                 ASC-ITEM-SUB) >                   INKCS002
01393          ASC-START-OF-SELECTED-RANGE)                             INKCS002
01394      OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                         INKCS002
01395                                 ASC-ITEM-SUB) =                   INKCS002
01396          ASC-START-OF-SELECTED-RANGE)                             INKCS002
01397                                                                   INKCS002
01398          IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                     INKCS002
01399                                     ASC-ITEM-SUB) <               INKCS002
01400              ASC-END-OF-SELECTED-RANGE)                           INKCS002
01401          OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                     INKCS002
01402                                     ASC-ITEM-SUB) =               INKCS002
01403              ASC-END-OF-SELECTED-RANGE)                           INKCS002
01404                                                                   INKCS002
01405              IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS002
01406                  SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)   INKCS002
01407                      ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)   INKCS002
01408                                TO TRUE                            INKCS002
01409                  SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)              INKCS002
01410                                TO TRUE                            INKCS002
01411                  ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS    INKCS002
01412              END-IF                                               INKCS002
01413          END-IF                                                   INKCS002
01414      END-IF.                                                      INKCS002
01415 /                                                                    CL**2
01416 *----------------------------------------------------------------*INKCS002
01417 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *INKCS002
01418 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *INKCS002
01419 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *INKCS002
01420 *    DISPLAY THE PANEL TO USER.                                  *INKCS002
01421 *----------------------------------------------------------------*INKCS002
01422                                                                   INKCS002
01423  4000-BUILD-INITIAL-PANEL.                                        INKCS002
01424                                                                   INKCS002
01425      INITIALIZE                  ASC-BLOCK-ENTRIES (1)            INKCS002
01426                                  ASC-BLOCK-ENTRIES (2).           INKCS002
01427      MOVE +1                 TO ASC-BLK-SUB.                      INKCS002
01428      SET ASC-ITEMS-LEFT-ON-DB                                     INKCS002
01429                              TO TRUE.                             INKCS002
01430                                                                   INKCS002
01431      MOVE +0                 TO ASC-ITEM-SUB                      INKCS002
01432                                 ASC-NUMBER-OF-ITEMS-READ          INKCS002
01433                                 ASC-NUMBER-OF-SELECTED-ITEMS      INKCS002
01434                                 ASC-HIGHEST-BLOCK-WRITTEN         INKCS002
01435                                 ASC-START-OF-SELECTED-RANGE       INKCS002
01436                                 ASC-END-OF-SELECTED-RANGE         INKCS002
01437                                 ASC-ITEM-AT-TOP-OF-PANEL.         INKCS002
01438                                                                   INKCS002
01439      PERFORM 4010-OPEN-CURSOR.                                    INKCS002
01440                                                                   INKCS002
01441      PERFORM 4300-READ-ITEMS-FROM-DB.                             INKCS002
01442                                                                   INKCS002
01443      PERFORM 4020-CLOSE-CURSOR.                                   INKCS002
01444                                                                   INKCS002
01445      MOVE +1                   TO  PV-TOP-ITEM.                   INKCS002
01446      MOVE SPACE                TO  ASC-START-ITEM-REQUEST.           CL*16
01447      IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                          INKCS002
01448 *        SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                       CL*42
01449          SET DP020-FK-RETURN (DP020-SRC-AID)                         CL*42
01450              DP020-MSG-FATAL   TO  TRUE                              CL*42
01451          MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER               INKCS002
01452      ELSE                                                         INKCS002
01453          MOVE -1               TO MR-SELECTL (1)                  INKCS002
01454          SET DP030-SET-CURSOR-APPL-1                              INKCS002
01455                                TO TRUE                            INKCS002
01456          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     INKCS002
01457      END-IF.                                                      INKCS002
01458 /                                                                    CL**2
01459 *----------------------------------------------------------------*INKCS002
01460 * OPEN THE CURSOR.                                               *   CL**2
01461 *----------------------------------------------------------------*INKCS002
01462                                                                   INKCS002
01463  4010-OPEN-CURSOR.                                                INKCS002
01464                                                                      CL**2
W26600     MOVE ASC-KEY-STORE-NBR   TO PV-STORE-NBR-X.                          
W26600     MOVE PV-STORE-NBR-9      TO INVLOC-LOC-NBR.                          
W26600     MOVE ASC-KEY-AREA-NBR    TO INVLOC-AREA-NBR.                         
W26600                                                                          
01465      IF  ASC-AREA-ASTERISK                                           CL*43
01466          EXEC SQL                                                    CL**2
01467               OPEN AREA-CSR                                          CL**2
01468          END-EXEC                                                    CL*36
01469      ELSE                                                            CL*36
01470          EXEC SQL                                                    CL*36
01471               OPEN AREA-CSR2                                         CL*36
01472          END-EXEC                                                    CL*36
01473      END-IF.                                                         CL*36
01474                                                                   INKCS002
01475          EVALUATE TRUE                                            INKCS002
01476              WHEN SQLCODE = ZERO                                  INKCS002
01477              WHEN SQLCODE = -904                                  INKCS002
01478              WHEN SQLCODE = -913                                  INKCS002
01479                  CONTINUE                                         INKCS002
01480              WHEN SQLWARN0 NOT = SPACES                           INKCS002
01481              WHEN SQLCODE < ZERO                                  INKCS002
01482              WHEN SQLCODE > ZERO                                  INKCS002
01483                  MOVE '4010-OPEN-CURSOR'                          INKCS002
01484                                    TO DP013-PARAGRAPH             INKCS002
01485                  MOVE 'OPEN AREA CURSOR - '                          CL**2
01486                                    TO DP013-MESSAGE-TEXT (1)      INKCS002
01487                  MOVE SQLCA        TO DP013-SQLCA                 INKCS002
01488                  MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)       CL**2
01489                  SET DP013-DB2-ABEND                              INKCS002
01490                      DP013-XCTL-DISPLAY-RESTART TO TRUE           INKCS002
01491                  PERFORM DP013-0000-PROCESS-ABEND                 INKCS002
01492          END-EVALUATE.                                               CL*19
01493 /                                                                    CL*19
01494 *----------------------------------------------------------------*INKCS002
01495 * CLOSE THE TMESSAGE CURSOR.                                     *INKCS002
01496 *   IN THIS PARTICULAR APPLICATION ONE OF 3 DIFFERENT CURSORS    *INKCS002
01497 *   WILL BE CLOSED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *INKCS002
01498 *   USER.                                                        *INKCS002
01499 *----------------------------------------------------------------*INKCS002
01500                                                                   INKCS002
01501  4020-CLOSE-CURSOR.                                               INKCS002
01502                                                                      CL**2
01503      IF  ASC-AREA-ASTERISK                                           CL*43
01504          EXEC SQL                                                    CL*36
01505               CLOSE AREA-CSR                                         CL*36
01506          END-EXEC                                                    CL*36
01507      ELSE                                                            CL*36
01508          EXEC SQL                                                    CL*36
01509               CLOSE AREA-CSR2                                        CL*36
01510          END-EXEC                                                    CL*36
01511      END-IF.                                                         CL*36
01512                                                                      CL*36
01513      EVALUATE TRUE                                                INKCS002
01514          WHEN SQLCODE = ZERO                                      INKCS002
01515              CONTINUE                                             INKCS002
01516          WHEN SQLWARN0 NOT = SPACES                               INKCS002
01517          WHEN SQLCODE < ZERO                                      INKCS002
01518          WHEN SQLCODE > ZERO                                      INKCS002
01519              MOVE '4020-CLOSE-CURSOR'                             INKCS002
01520                                TO DP013-PARAGRAPH                 INKCS002
01521              MOVE 'CLOSE AREA CURSOR '                               CL**2
01522                                TO DP013-MESSAGE-TEXT (1)          INKCS002
01523              MOVE SQLCA        TO DP013-SQLCA                     INKCS002
01524              MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)           CL**2
01525              SET DP013-DB2-ABEND                                  INKCS002
01526                  DP013-XCTL-DISPLAY-RESTART TO TRUE               INKCS002
01527              PERFORM DP013-0000-PROCESS-ABEND                     INKCS002
01528      END-EVALUATE.                                                INKCS002
01529 /                                                                    CL**2
01530 *----------------------------------------------------------------*INKCS002
01531 *    SET UP THE HOST VARIABLES.                                  *INKCS002
01532 *----------------------------------------------------------------*INKCS002
01533                                                                   INKCS002
01534  4100-PREPARE-CURSOR.                                             INKCS002
01535                                                                   INKCS002
01536      MOVE IN001-STORE-NBR      TO  ASC-KEY-STORE-NBR.                CL**3
01537      MOVE IN001-DISPLAY-COMPLETE-SW                                  CL*46
01538                                TO  ASC-DISPLAY-COMPLETE-SW.          CL*44
01539      IF  IN001-AREA-ASTERISK                                         CL*36
01540          SET ASC-AREA-ASTERISK TO  TRUE                              CL*42
01541          MOVE ZERO             TO  ASC-KEY-AREA-NBR                  CL*36
01542      ELSE                                                            CL*36
01543          MOVE IN001-AREA-NBR   TO  ASC-KEY-AREA-NBR                  CL*36
01544      END-IF.                                                         CL*36
01545                                                                   INKCS002
01546 /                                                                    CL**3
01547 *----------------------------------------------------------------*INKCS002
01548 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *INKCS002
01549 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *INKCS002
01550 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *INKCS002
01551 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *INKCS002
01552 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *INKCS002
01553 *                                                                *INKCS002
01554 *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *INKCS002
01555 *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *INKCS002
01556 *    UPON INITIALIZATION.                                        *INKCS002
01557 *----------------------------------------------------------------*INKCS002
01558                                                                   INKCS002
01559  4300-READ-ITEMS-FROM-DB.                                         INKCS002
01560                                                                   INKCS002
01561      PERFORM                                                      INKCS002
01562          VARYING PV-READ-COUNT                                    INKCS002
01563          FROM 1 BY 1                                              INKCS002
01564          UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                    INKCS002
01565             OR (ASC-NO-ITEMS-LEFT-ON-DB))                         INKCS002
01566                                                                   INKCS002
01567          PERFORM 4309-FETCH-LOOP                                     CL*18
01568                                                                   INKCS002
01569          IF  ASC-ITEMS-LEFT-ON-DB                                 INKCS002
01570                                                                      CL*18
01571            IF (PV-NBR-TO-CORRECT GREATER THAN ZERO                   CL*36
01572              OR (ASC-DISPLAY-COMPLETE                                CL*44
01573                  AND PV-NBR-FALLOUTS GREATER THAN ZERO))             CL*36
01574                                                                      CL*21
01575              IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                   CL*18
01576                  ADD +1            TO  ASC-ITEM-SUB                  CL*18
01577              ELSE                                                    CL*18
01578                  ADD +1            TO  ASC-BLK-SUB                   CL*18
01579                  MOVE +1           TO  ASC-ITEM-SUB                  CL*18
01580                                                                      CL*18
01581                  IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY            CL*18
01582                      MOVE +1       TO  ASC-BLK-SUB                   CL*18
01583                      SET ASC-BLOCK-MODIFIED (1)                      CL*18
01584                                    TO  TRUE                          CL*18
01585                      PERFORM 5100-WRITE-TEMP-STORAGE                 CL*18
01586                      MOVE ASC-BLOCK-ENTRIES                          CL*18
01587                                (PC-MAX-BLOCKS-IN-ARRAY)              CL*18
01588                                 TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)    CL*20
01589                      ADD +1        TO  ASC-BLK-SUB                   CL*18
01590                      INITIALIZE ASC-BLOCK-ENTRIES(ASC-BLK-SUB)       CL*18
01591                      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)         CL*18
01592                                    TO  TRUE                          CL*18
01593                  END-IF                                              CL*18
01594              END-IF                                                  CL*18
01595                                                                      CL*18
01596                  ADD +1            TO ASC-NUMBER-OF-ITEMS-READ       CL*18
01597                  MOVE ASC-NUMBER-OF-ITEMS-READ                       CL*18
01598                                    TO ASC-LIST-ITEM-NUMBER        INKCS002
01599                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS002
01600                  MOVE INVLOC-AREA-NBR  TO ASC-AREA-NBR               CL*18
01601                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS002
01602                  MOVE PV-NBR-FALLOUTS                                CL*18
01603                                    TO  ASC-NBR-FALLOUTS              CL*18
01604                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS002
01605                  MOVE PV-NBR-TO-CORRECT                              CL*18
01606                                    TO  ASC-NBR-TO-CORRECT            CL*18
01607                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS002
01608                  IF  PV-NBR-TO-CORRECT EQUAL ZERO                    CL*18
01609                      SET ASC-AREA-COMPLETE                           CL*18
01610                                        (ASC-BLK-SUB ASC-ITEM-SUB)    CL*18
01611                                    TO  TRUE                          CL*18
01612                  END-IF                                              CL*18
01613                                                                      CL*18
01614            END-IF                                                    CL*21
01615          END-IF                                                      CL**3
01616      END-PERFORM.                                                 INKCS002
01617                                                                   INKCS002
01618      IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                          INKCS002
01619          MOVE +1               TO ASC-BLK-SUB                     INKCS002
01620          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS002
01621          ADD +1                TO ASC-BLK-SUB                     INKCS002
01622          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS002
01623                                                                   INKCS002
01624          MOVE 1                TO ASC-BLK-SUB                     INKCS002
01625                                   PV-BLOCK-NUMBER                 INKCS002
01626          PERFORM 4410-READ-TEMP-STORAGE                           INKCS002
01627          IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1             INKCS002
01628              MOVE 2            TO ASC-BLK-SUB                     INKCS002
01629                                   PV-BLOCK-NUMBER                 INKCS002
01630              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
01631          END-IF                                                   INKCS002
01632      END-IF.                                                      INKCS002
01633                                                                      CL**3
01634      MOVE INVLOC-AREA-NBR      TO  ASC-KEY-AREA-NBR.                 CL*39
01635 /                                                                    CL**3
01636 *----------------------------------------------------------------*INKCS002
01637 *    LOOP THE FETCH TO ENSURE ONLY CERTAIN DATA RETURNED FOR     *   CL*18
01638 *    STORE PERSONNEL.                                            *   CL*18
01639 *----------------------------------------------------------------*INKCS002
01640                                                                   INKCS002
01641  4309-FETCH-LOOP.                                                    CL*18
01642                                                                      CL*18
01643      IF  DP020-LOCATION-TYPE EQUAL PC-CORPORATE                      CL*18
01644          PERFORM 4310-FETCH                                          CL*18
01645          MOVE INVLOC-TOT-REC-INVLID-QTY                              CL*18
01646                                TO  PV-NBR-FALLOUTS                   CL*18
01644          PERFORM 4350-SELECT-TINVDTL-COUNT                           CL*18
01650      ELSE                                                            CL*18
01651          SET PS-NO-STORE-REC   TO  TRUE                              CL*18
01652          PERFORM UNTIL PS-STORE-REC                                  CL*18
01653              PERFORM 4310-FETCH                                      CL*18
01654              IF  INVLOC-TOT-ITM-INVLID-QTY GREATER THAN ZERO         CL*34
01658                  MOVE INVLOC-TOT-ITM-INVLID-QTY                      CL*18
01659                                TO  PV-NBR-FALLOUTS                   CL*18
01644                  PERFORM 4350-SELECT-TINVDTL-COUNT                   CL*18
01660                  SET PS-STORE-REC                                    CL*18
01661                                TO  TRUE                              CL*18
01662              END-IF                                                  CL*18
01663          END-PERFORM                                                 CL*18
01664      END-IF.                                                         CL*18
01665                                                                   INKCS002
01666 /                                                                    CL*18
01667 *----------------------------------------------------------------*   CL*18
01668 *    FETCH THE CURSOR.                                           *   CL*18
01669 *----------------------------------------------------------------*   CL*18
01670                                                                      CL*18
01671  4310-FETCH.                                                         CL*18
01672                                                                      CL*18
01673      IF  ASC-AREA-ASTERISK                                           CL*43
01674          EXEC SQL                                                    CL*36
01675           FETCH                                                      CL*36
01676                 AREA-CSR                                             CL*36
01677           INTO                                                       CL*36
01678                 :INVLOC-AREA-NBR                                     CL*36
01681               , :INVLOC-TOT-ITM-INVLID-QTY                           CL*36
01682               , :INVLOC-TOT-REC-INVLID-QTY                           CL*36
01683          END-EXEC                                                    CL*36
01684      ELSE                                                            CL*36
01685          EXEC SQL                                                    CL*36
01686           FETCH                                                      CL*36
01687                 AREA-CSR2                                            CL*36
01688           INTO                                                       CL*36
01689                 :INVLOC-AREA-NBR                                     CL*36
01692               , :INVLOC-TOT-ITM-INVLID-QTY                           CL*36
01693               , :INVLOC-TOT-REC-INVLID-QTY                           CL*36
01694          END-EXEC                                                    CL*36
01695      END-IF.                                                         CL*36
01696                                                                   INKCS002
01697      EVALUATE TRUE                                                INKCS002
01698          WHEN SQLCODE = ZERO                                      INKCS002
01699          WHEN SQLCODE = +100                                      INKCS002
01700               CONTINUE                                            INKCS002
01701          WHEN SQLWARN0 NOT = SPACE                                INKCS002
01702          WHEN SQLCODE NOT = ZERO                                  INKCS002
01703               MOVE '4310-FETCH-CURSOR'                            INKCS002
01704                                TO DP013-PARAGRAPH                 INKCS002
01705               MOVE 'FETCH AREA CURSOR'                               CL**3
01706                                TO DP013-MESSAGE-TEXT (1)          INKCS002
01707               MOVE SQLCA       TO DP013-SQLCA                     INKCS002
01708               SET DP013-DB2-ABEND                                 INKCS002
01709                                TO TRUE                            INKCS002
01710               PERFORM DP013-0000-PROCESS-ABEND                    INKCS002
01711      END-EVALUATE.                                                INKCS002
01712                                                                   INKCS002
01713      IF  SQLCODE EQUAL +100                                       INKCS002
01714          SET ASC-NO-ITEMS-LEFT-ON-DB                              INKCS002
01715              PS-STORE-REC      TO  TRUE                              CL*18
01716      END-IF.                                                         CL**3
01666 /                                                                    CL*18
01667 *----------------------------------------------------------------*   CL*18
01668 *    SELECT THE COUNT OF INVALID REC LINES FOR AN AREA           *   CL*18
01669 *----------------------------------------------------------------*   CL*18
01670                                                                      CL*18
01644  4350-SELECT-TINVDTL-COUNT.                                          CL*18
01672                                                                      CL*18
W26600     MOVE ASC-KEY-STORE-NBR   TO PV-STORE-NBR-X.                          
W26600     MOVE PV-STORE-NBR-9      TO INVDTL-LOC-NBR.                          
W26600     MOVE INVLOC-AREA-NBR     TO INVDTL-AREA-NBR.                         
W26600                                                                          
           EXEC SQL                                                        CL*17
               SELECT                                                      CL*17
                      COUNT(*)                                             CL*17
               INTO                                                        CL*17
                     :PV-NBR-TO-CORRECT                                    CL*17
               FROM                                                        CL*17
                      TINVDTL                                              CL*17
               WHERE                                                       CL*17
W26600                LOC_NBR         =  :INVDTL-LOC-NBR                   CL*36
W26600           AND  AREA_NBR        =  :INVDTL-AREA-NBR                  CL*36
                 AND  INVALID_REC_IND =  'Y'                               CL*17
00631      END-EXEC.                                                       CL*17
01696                                                                   INKCS002
01697      EVALUATE TRUE                                                INKCS002
01698          WHEN SQLCODE = ZERO                                      INKCS002
01700               CONTINUE                                            INKCS002
01699          WHEN SQLCODE = -305                                      INKCS002
01700               MOVE ZERO TO PV-NBR-TO-CORRECT                      INKCS002
01701          WHEN SQLWARN0 NOT = SPACE                                INKCS002
01702          WHEN SQLCODE NOT = ZERO                                  INKCS002
01703               MOVE '4350-SELECT-TINVDTL-COUNT'                    INKCS002
01704                                TO DP013-PARAGRAPH                 INKCS002
01705               MOVE 'SELECT TINVDTL COUNT'                            CL**3
01706                                TO DP013-MESSAGE-TEXT (1)          INKCS002
01707               MOVE SQLCA       TO DP013-SQLCA                     INKCS002
01708               SET DP013-DB2-ABEND                                 INKCS002
01709                                TO TRUE                            INKCS002
01710               PERFORM DP013-0000-PROCESS-ABEND                    INKCS002
01711      END-EVALUATE.                                                INKCS002
01712                                                                   INKCS002
01717 /                                                                    CL**3
01718 *----------------------------------------------------------------*INKCS002
01719 * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *INKCS002
01720 *----------------------------------------------------------------*INKCS002
01721                                                                   INKCS002
01722  4400-MOVE-COMMAREA-TO-SCREEN.                                    INKCS002
01723                                                                      CL**3
01724      IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))             INKCS002
01725          AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))          INKCS002
01726          OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))          INKCS002
01727              MOVE +1    TO ASC-BLK-SUB                            INKCS002
01728              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS002
01729              ADD +1     TO ASC-BLK-SUB                            INKCS002
01730              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS002
01731              DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL             INKCS002
01732                  GIVING PV-BLOCK-NUMBER                           INKCS002
01733                  REMAINDER PV-REMAINDER                           INKCS002
01734              IF  PV-REMAINDER > ZERO                              INKCS002
01735                  ADD +1 TO PV-BLOCK-NUMBER                        INKCS002
01736              END-IF                                               INKCS002
01737              MOVE +1    TO ASC-BLK-SUB                            INKCS002
01738              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
01739              ADD +1     TO ASC-BLK-SUB                            INKCS002
01740                            PV-BLOCK-NUMBER                        INKCS002
01741              PERFORM 4410-READ-TEMP-STORAGE                       INKCS002
01742      END-IF.                                                      INKCS002
01743                                                                      CL**3
01744      MOVE +1            TO ASC-BLK-SUB.                           INKCS002
01745      COMPUTE ASC-ITEM-SUB =                                       INKCS002
01746          ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))              INKCS002
01747          + PC-ITEMS-PER-PANEL).                                   INKCS002
01748                                                                      CL**3
01749      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS002
01750          ADD +1         TO ASC-BLK-SUB                            INKCS002
01751          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS002
01752      END-IF.                                                      INKCS002
01753                                                                      CL**3
01754      COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                           INKCS002
01755          ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                INKCS002
01756      PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                        INKCS002
01757      PERFORM 4420-FORMAT-LIST-LINES                               INKCS002
01758          VARYING PV-SUB                                           INKCS002
01759          FROM PC-ITEMS-PER-PANEL BY -1                            INKCS002
01760          UNTIL PV-SUB < +1.                                       INKCS002
01761                                                                      CL**3
01762      COMPUTE ASC-ITEMS-DISPLAYED =                                INKCS002
01763          ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).          INKCS002
01764      MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL.      CL*32
01765      IF  PS-NO-ERROR                                                 CL*32
01766          MOVE PV-TOP-ITEM          TO ASTRTITO                       CL*32
01767      END-IF.                                                         CL*32
01768      MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                    INKCS002
01769      MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                    INKCS002
01770      MOVE ASC-NUMBER-OF-SELECTED-ITEMS                            INKCS002
01771                                    TO ASELITO.                    INKCS002
01772      MOVE SPACE                    TO ASC-START-ITEM-REQUEST.        CL*25
W26600     MOVE IN001-STORE-NBR-X        TO ASTRNBRO.                      CL*35
01773      MOVE IN001-STORE-NAME         TO ASTRNAMO.                      CL*35
01774                                                                      CL**3
01775      IF  DP020-MSG-NUMBER-X = SPACE                               INKCS002
01776          IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ  INKCS002
01777 *            --- MORE ITEMS TO DISPLAY ----                       INKCS002
01778              MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER            INKCS002
01779              SET DP020-MSG-INFORMATIONAL TO TRUE                  INKCS002
01780          END-IF                                                   INKCS002
01781      END-IF.                                                      INKCS002
01782                                                                      CL**3
01783 *----------------------------------------------------------------*INKCS002
01784 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *INKCS002
01785 *----------------------------------------------------------------*INKCS002
01786                                                                   INKCS002
01787  4410-READ-TEMP-STORAGE.                                          INKCS002
01788                                                                   INKCS002
01789      EXEC CICS                                                    INKCS002
01790          READQ TS                                                 INKCS002
01791              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS002
01792              INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS002
01793              ITEM  (PV-BLOCK-NUMBER)                              INKCS002
01794              RESP  (PV-CICS-RESP)                                 INKCS002
01795      END-EXEC.                                                    INKCS002
01796                                                                   INKCS002
01797      IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                       INKCS002
01798        OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                     INKCS002
01799          CONTINUE                                                 INKCS002
01800      ELSE                                                         INKCS002
01801          SET DP013-CICS-ABEND     TO TRUE                         INKCS002
01802          SET DP013-NO-ROLLBACK    TO TRUE                         INKCS002
01803          MOVE '4410-READ-TEMP-STORAGE'                            INKCS002
01804                                   TO DP013-PARAGRAPH              INKCS002
01805          MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'      INKCS002
01806                                   TO DP013-MESSAGE-TEXT(1)        INKCS002
01807          PERFORM DP013-0000-PROCESS-ABEND                         INKCS002
01808      END-IF.                                                      INKCS002
01809                                                                   INKCS002
01810      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS002
01811                                                                      CL**3
01812      IF  PV-CICS-RESP = DFHRESP (ITEMERR)                         INKCS002
01813          INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)               INKCS002
01814      END-IF.                                                      INKCS002
01815 /                                                                    CL**3
01816 *----------------------------------------------------------------*INKCS002
01817 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *INKCS002
01818 *----------------------------------------------------------------*INKCS002
01819                                                                   INKCS002
01820  4420-FORMAT-LIST-LINES.                                          INKCS002
01821                                                                      CL**3
01822      IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ      INKCS002
01823          SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                INKCS002
01824          MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)           INKCS002
01825          MOVE SPACE              TO MR-SELECTI (PV-SUB)           INKCS002
01826                                     MR-ACTION (PV-SUB)            INKCS002
01827                                     MR-AREA-X  (PV-SUB)              CL**3
01828                                     MR-NBR-FALLOUTS-X(PV-SUB)        CL**3
01829                                     MR-NBR-TO-CORRECT-X(PV-SUB)      CL**3
01830                                     MR-COMPLETE   (PV-SUB)           CL**3
01831      ELSE                                                         INKCS002
01832          IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS002
01833                                                    > SPACE        INKCS002
01834              MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB) INKCS002
01835                                  TO MR-SELECTI (PV-SUB)           INKCS002
01836          ELSE                                                     INKCS002
01837              MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS002
01838                                  TO MR-SELECTI (PV-SUB)           INKCS002
01839          END-IF                                                   INKCS002
01840                                                                      CL**3
01841          IF  DP030-SET-CURSOR-TRAILER                             INKCS002
01842              IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)INKCS002
01843                  > SPACE)                                         INKCS002
01844                OR (PV-SUB = 1))                                   INKCS002
01845                 SET DP030-SET-CURSOR-APPL-1 TO TRUE               INKCS002
01846                 MOVE -1          TO MR-SELECTL (PV-SUB)           INKCS002
01847              END-IF                                               INKCS002
01848          END-IF                                                   INKCS002
01849                                                                      CL**3
01850          IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL           INKCS002
01851              MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)           INKCS002
01852                                  TO MR-ACTION (PV-SUB)            INKCS002
01853              MOVE ASC-AREA-NBR (ASC-BLK-SUB ASC-ITEM-SUB)            CL**3
01854                                  TO MR-AREA (PV-SUB)                 CL**3
01855              MOVE ASC-NBR-FALLOUTS (ASC-BLK-SUB ASC-ITEM-SUB)        CL**3
01856                                  TO MR-NBR-FALLOUTS(PV-SUB)          CL**3
01857              MOVE ASC-NBR-TO-CORRECT (ASC-BLK-SUB ASC-ITEM-SUB)      CL**3
01858                                  TO MR-NBR-TO-CORRECT(PV-SUB)        CL**3
01859              IF  ASC-AREA-COMPLETE (ASC-BLK-SUB ASC-ITEM-SUB)        CL**6
01860                  MOVE PC-COMPLETE TO MR-COMPLETE(PV-SUB)             CL**3
01861              ELSE                                                    CL**3
01862                  MOVE SPACES      TO MR-COMPLETE(PV-SUB)             CL**3
01863              END-IF                                                  CL**3
01849          ELSE                                                        CL**3
01850              IF PS-SEL-AREA-PRINT                                 INKCS002
01851                  MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS002
01852                                  TO MR-ACTION (PV-SUB)            INKCS002
01863              END-IF                                                  CL**3
01864          END-IF                                                      CL**3
01865      END-IF.                                                      INKCS002
01866                                                                      CL**3
01867      SUBTRACT +1 FROM ASC-ITEM-SUB                                INKCS002
01868      IF  ASC-ITEM-SUB < + 1                                       INKCS002
01869          MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                  INKCS002
01870          MOVE +1                 TO ASC-BLK-SUB                   INKCS002
01871      END-IF.                                                      INKCS002
01872                                                                      CL**3
01873 ******************************************************************INKCS002
01874 ** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **INKCS002
01875 ** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **INKCS002
01876 ** ISSUE A WARNING.                                             **INKCS002
01877 ******************************************************************INKCS002
01878                                                                      CL**3
01879  4430-CHECK-FOR-BEGIN-AND-END.                                    INKCS002
01880                                                                      CL**3
01881      IF  ASC-START-OF-SELECTED-RANGE > ZERO                       INKCS002
01882          IF  ASC-END-OF-SELECTED-RANGE = ZERO                     INKCS002
01883 *            -- NO END-OF-RANGE SPECFIED ---                      INKCS002
01884              MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER            INKCS002
01885              SET DP020-MSG-WARNING TO TRUE                        INKCS002
01886              PERFORM 4431-POSITION-AT-WARNING                     INKCS002
01887          END-IF                                                   INKCS002
01888      ELSE                                                         INKCS002
01889          IF  ASC-END-OF-SELECTED-RANGE > ZERO                     INKCS002
01890 *            -- NO BEGINNING-OF-RANGE SPECFIED ---                INKCS002
01891              MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER            INKCS002
01892              SET DP020-MSG-WARNING TO TRUE                        INKCS002
01893              PERFORM 4431-POSITION-AT-WARNING                     INKCS002
01894          END-IF                                                   INKCS002
01895      END-IF.                                                      INKCS002
01896 ******************************************************************INKCS002
01897 ** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **INKCS002
01898 ** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **INKCS002
01899 ** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **INKCS002
01900 ** POSITION THE CURSOR THERE.                                   **INKCS002
01901 ******************************************************************INKCS002
01902                                                                      CL**3
01903  4431-POSITION-AT-WARNING.                                        INKCS002
01904                                                                      CL**3
01905      COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                 INKCS002
01906                     - PV-TOP-ITEM + 1.                            INKCS002
01907      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS002
01908          CONTINUE                                                 INKCS002
01909      ELSE                                                         INKCS002
01910          SET DP030-SET-CURSOR-APPL-1                              INKCS002
01911                                    TO TRUE                        INKCS002
01912          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS002
01913          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS002
01914          MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)         INKCS002
01915      END-IF.                                                      INKCS002
01916                                                                      CL**3
01917      COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                   INKCS002
01918                     - PV-TOP-ITEM + 1.                            INKCS002
01919      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS002
01920          CONTINUE                                                 INKCS002
01921      ELSE                                                         INKCS002
01922          SET DP030-SET-CURSOR-APPL-1                              INKCS002
01923                                    TO TRUE                        INKCS002
01924          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS002
01925          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS002
01926          MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)         INKCS002
01927      END-IF.                                                      INKCS002
01928 /                                                                    CL**3
01929 *----------------------------------------------------------------*INKCS002
01930 * PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *INKCS002
01931 * ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *INKCS002
01932 * FOR THE SELECTED ITEMS QUEUE.                                  *INKCS002
01933 *                                                                *INKCS002
01934 * FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *INKCS002
01935 * QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *INKCS002
01936 * THE ARRAY IS FULL.                                             *INKCS002
01937 *----------------------------------------------------------------*INKCS002
01938                                                                   INKCS002
01939  5000-PREPARE-SEL-QUEUE.                                          INKCS002
01940                                                                   INKCS002
01941      MOVE +1                   TO  ASC-BLK-SUB.                   INKCS002
01942      PERFORM 5100-WRITE-TEMP-STORAGE.                             INKCS002
01943      ADD +1                    TO  ASC-BLK-SUB.                   INKCS002
01944      PERFORM 5100-WRITE-TEMP-STORAGE.                             INKCS002
01945                                                                   INKCS002
01946      MOVE ASC-SEL-QUEUE-NAME   TO  IN001-SEL-QUEUE-NAME.             CL**6
01947      PERFORM 6010-DELETE-SEL-QUEUE.                               INKCS002
01948                                                                   INKCS002
01949      SET ASC-UPDATE-TSQ        TO  TRUE.                          INKCS002
01950                                                                   INKCS002
01951      SET IN002-IDX             TO  1.                                CL**6
01952      MOVE ZERO                 TO IN001-NUMBER-OF-TS-ITEMS.          CL**6
01953      INITIALIZE IN002-SELECTED-ITEM-ARRAY.                           CL**6
01954                                                                   INKCS002
01955      PERFORM 5200-FILL-SEL-QUEUE-RECORD                           INKCS002
01956              VARYING PV-ITEM                                      INKCS002
01957              FROM 1 BY 1                                          INKCS002
01958              UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.     INKCS002
01959                                                                   INKCS002
01960      IF  IN002-IDX > 1                                               CL**6
01961          PERFORM 5205-WRITE-SEL-QUEUE                             INKCS002
01962          ADD +1                TO IN001-NUMBER-OF-TS-ITEMS           CL**6
01963      END-IF.                                                      INKCS002
01964  EJECT                                                            INKCS002
01965 *----------------------------------------------------------------*INKCS002
01966 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *INKCS002
01967 *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *INKCS002
01968 *----------------------------------------------------------------*INKCS002
01969                                                                   INKCS002
01970  5100-WRITE-TEMP-STORAGE.                                         INKCS002
01971                                                                      CL**6
01972      MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                     INKCS002
01973                                    TO PV-LIST-ITEM-NUMBER.        INKCS002
01974      COMPUTE PV-BLOCK-NUMBER =                                    INKCS002
01975          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS002
01976                                                                   INKCS002
01977      IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)             INKCS002
01978      OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)             INKCS002
01979          IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      INKCS002
01980              PERFORM 5105-REWRITE-ASC-LISTQ                       INKCS002
01981          END-IF                                                   INKCS002
01982      ELSE                                                         INKCS002
01983          COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       INKCS002
01984          ASC-HIGHEST-BLOCK-WRITTEN + 1                            INKCS002
01985          PERFORM 5110-WRITE-ASC-LISTQ                             INKCS002
01986          ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN   INKCS002
01987      END-IF.                                                      INKCS002
01988                                                                   INKCS002
01989      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS002
01990                                                                      CL**3
01991 ******************************************************************INKCS002
01992 ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **INKCS002
01993 ** SPECIFIC COMMAREA.                                           **INKCS002
01994 ******************************************************************INKCS002
01995                                                                      CL**3
01996  5105-REWRITE-ASC-LISTQ.                                          INKCS002
01997                                                                      CL**3
01998      EXEC CICS                                                    INKCS002
01999          WRITEQ TS                                                INKCS002
02000              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS002
02001              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS002
02002              ITEM  (PV-BLOCK-NUMBER)                              INKCS002
02003              REWRITE                                              INKCS002
02004              RESP  (PV-CICS-RESP)                                 INKCS002
02005      END-EXEC.                                                    INKCS002
02006                                                                   INKCS002
02007      IF  PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS002
02008          CONTINUE                                                 INKCS002
02009      ELSE                                                         INKCS002
02010          SET DP013-CICS-ABEND            TO TRUE                  INKCS002
02011          SET DP013-NO-ROLLBACK           TO TRUE                  INKCS002
02012          MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH       INKCS002
02013          MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'     INKCS002
02014                                          TO DP013-MESSAGE-TEXT(1) INKCS002
02015          PERFORM DP013-0000-PROCESS-ABEND                         INKCS002
02016      END-IF.                                                      INKCS002
02017                                                                   INKCS002
02018 ******************************************************************INKCS002
02019 ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **INKCS002
02020 ** SPECIFIC COMMAREA.                                           **INKCS002
02021 ******************************************************************INKCS002
02022                                                                      CL**3
02023  5110-WRITE-ASC-LISTQ.                                            INKCS002
02024                                                                      CL**3
02025      EXEC CICS                                                    INKCS002
02026          WRITEQ TS                                                INKCS002
02027              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS002
02028              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS002
02029              ITEM  (PV-BLOCK-NUMBER)                              INKCS002
02030              RESP  (PV-CICS-RESP)                                 INKCS002
02031      END-EXEC.                                                    INKCS002
02032      IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                      INKCS002
02033          SET DP013-CICS-ABEND      TO TRUE                        INKCS002
02034          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS002
02035          MOVE '5110-WRITE-ASC-LISTQ'                              INKCS002
02036                                    TO DP013-PARAGRAPH             INKCS002
02037          MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS002
02038                                    TO DP013-MESSAGE-TEXT(1)       INKCS002
02039          PERFORM DP013-0000-PROCESS-ABEND                         INKCS002
02040      END-IF.                                                      INKCS002
02041 /                                                                    CL**3
02042 *----------------------------------------------------------------*INKCS002
02043 * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *INKCS002
02044 * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *INKCS002
02045 *----------------------------------------------------------------*INKCS002
02046                                                                   INKCS002
02047  5200-FILL-SEL-QUEUE-RECORD.                                      INKCS002
02048                                                                   INKCS002
02049      PERFORM 6112-READ-LIST-QUEUE.                                INKCS002
02050                                                                   INKCS002
02051      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       INKCS002
02052          PERFORM                                                  INKCS002
02053          VARYING LQW-IDX                                          INKCS002
02054          FROM 1 BY 1                                              INKCS002
02055          UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL            INKCS002
02056            OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO          INKCS002
02057                                                                   INKCS002
02058              IF  LQW-SELECT (LQW-IDX)                             INKCS002
02059                  MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)              INKCS002
02060                                    TO IN002-ITEM (IN002-IDX)         CL**6
02061                  MOVE LQW-AREA-NBR       (LQW-IDX)                   CL**6
02062                                    TO IN002-AREA-NBR                 CL**6
02063                                         (IN002-IDX)                  CL**6
02064                  MOVE SPACE        TO IN002-ACTION-SW (IN002-IDX)    CL**6
02065                  SET IN002-IDX UP BY 1                               CL**6
02066                  IF  IN002-IDX > IN002-MAX-ITEMS                     CL**6
02067                      PERFORM 5205-WRITE-SEL-QUEUE                 INKCS002
02068                      ADD +1        TO IN001-NUMBER-OF-TS-ITEMS       CL**6
02069                      INITIALIZE IN002-SELECTED-ITEM-ARRAY            CL**6
02070                      SET IN002-IDX TO  1                             CL**6
02071                  END-IF                                           INKCS002
02072              END-IF                                               INKCS002
02073          END-PERFORM                                              INKCS002
02074      END-IF.                                                      INKCS002
02075                                                                   INKCS002
02076 *----------------------------------------------------------------*INKCS002
02077 *    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *INKCS002
02078 *----------------------------------------------------------------*INKCS002
02079                                                                   INKCS002
02080  5205-WRITE-SEL-QUEUE.                                            INKCS002
02081                                                                   INKCS002
02082      MOVE ASC-NUMBER-OF-SELECTED-ITEMS                            INKCS002
02083                                    TO IN002-SELECTED-ITEMS.          CL**6
02084                                                                   INKCS002
02085      EXEC CICS                                                    INKCS002
02086           WRITEQ TS                                               INKCS002
02087               QUEUE (ASC-SEL-QUEUE-NAME)                          INKCS002
02088               FROM  (IN002-SELECTED-AREAS)                           CL**7
02089               ITEM  (PV-SEL-QUEUE-ITEM)                           INKCS002
02090               RESP  (PV-CICS-RESP)                                INKCS002
02091      END-EXEC.                                                    INKCS002
02092                                                                   INKCS002
02093      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS002
02094          CONTINUE                                                 INKCS002
02095      ELSE                                                         INKCS002
02096          SET DP013-CICS-ABEND      TO TRUE                        INKCS002
02097          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS002
02098          MOVE '5205-WRITE-SEL-QUEUE'                              INKCS002
02099                                    TO DP013-PARAGRAPH             INKCS002
02100          MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'          INKCS002
02101                                    TO DP013-MESSAGE-TEXT(1)       INKCS002
02102          PERFORM DP013-0000-PROCESS-ABEND                         INKCS002
02103      END-IF.                                                      INKCS002
02104 /                                                                    CL**3
02105 *----------------------------------------------------------------*   CL**6
02106 *  WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,  *   CL**6
02107 *  SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT  *   CL**6
02108 *  THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO     *   CL**6
02109 *  THE USER.                                                     *   CL**6
02110 *----------------------------------------------------------------*   CL**6
02111                                                                      CL**3
02112  6000-REFRESH-PANEL-FROM-TEMP.                                    INKCS002
02113                                                                      CL**3
02114      IF  ASC-UPDATE-TSQ                                           INKCS002
02115          SET ASC-NO-TSQ-UPDATE TO TRUE                            INKCS002
02116          MOVE ZERO                 TO PV-SAVE-ITEM                INKCS002
02117                                       PV-SEL-QUEUE-ITEM           INKCS002
02118          MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        INKCS002
02119                                    TO PV-HOLD-SELECTED-ITEMS      INKCS002
02120          PERFORM 6100-UPDATE-TSQ                                  INKCS002
02121              UNTIL PV-ITEMS-PROCESSED >                           INKCS002
02122                                      ASC-NUMBER-OF-SELECTED-ITEMS INKCS002
02123          PERFORM 6010-DELETE-SEL-QUEUE                            INKCS002
02124          INITIALIZE ASC-BLOCK-ENTRIES (1)                         INKCS002
02125                     ASC-BLOCK-ENTRIES (2)                         INKCS002
02126                                                                      CL*17
02127          DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                    CL*17
02128                 GIVING PV-BLOCK-NUMBER                               CL*17
02129                 REMAINDER PV-REMAINDER                               CL*17
02130          IF  PV-REMAINDER > ZERO                                     CL*17
02131              ADD +1     TO PV-BLOCK-NUMBER                           CL*17
02132          END-IF                                                      CL*17
02133          MOVE +1        TO ASC-BLK-SUB                               CL*17
02134          PERFORM 4410-READ-TEMP-STORAGE                              CL*17
02135          ADD +1         TO ASC-BLK-SUB                               CL*17
02136                            PV-BLOCK-NUMBER                           CL*17
02137          PERFORM 4410-READ-TEMP-STORAGE                              CL*17
02138                                                                      CL*17
02139      END-IF.                                                      INKCS002
02140                                                                   INKCS002
02141 *----------------------------------------------------------------*INKCS002
02142 *    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *INKCS002
02143 *----------------------------------------------------------------*INKCS002
02144                                                                      CL**6
02145  6010-DELETE-SEL-QUEUE.                                           INKCS002
02146                                                                   INKCS002
02147      EXEC CICS                                                    INKCS002
02148          DELETEQ TS                                               INKCS002
02149              QUEUE (ASC-SEL-QUEUE-NAME)                           INKCS002
02150              RESP  (PV-CICS-RESP)                                 INKCS002
02151      END-EXEC.                                                    INKCS002
02152                                                                      CL**3
02153      IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                      INKCS002
02154         (PV-CICS-RESP = DFHRESP(QIDERR)))                         INKCS002
02155          CONTINUE                                                 INKCS002
02156      ELSE                                                         INKCS002
02157          SET DP013-CICS-ABEND TO TRUE                             INKCS002
02158          SET DP013-NO-ROLLBACK TO TRUE                            INKCS002
02159          MOVE '6010-DELETE-SEL-QUEUE'                             INKCS002
02160                         TO DP013-PARAGRAPH                        INKCS002
02161          MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'         INKCS002
02162                         TO DP013-MESSAGE-TEXT (1)                 INKCS002
02163          PERFORM DP013-0000-PROCESS-ABEND                         INKCS002
02164      END-IF.                                                      INKCS002
02165                                                                   INKCS002
02166 *----------------------------------------------------------------*INKCS002
02167 *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *INKCS002
02168 *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *INKCS002
02169 *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *INKCS002
02170 *    THE TEMP QUEUE.                                             *INKCS002
02171 *----------------------------------------------------------------*INKCS002
02172                                                                   INKCS002
02173  6100-UPDATE-TSQ.                                                 INKCS002
02174                                                                   INKCS002
02175      ADD 1                     TO  PV-SEL-QUEUE-ITEM.             INKCS002
02176      PERFORM 6105-READ-SEL-QUEUE.                                 INKCS002
02177      PERFORM                                                      INKCS002
02178          VARYING IN002-IDX                                           CL**6
02179          FROM 1 BY 1                                              INKCS002
02180          UNTIL ((PV-ITEMS-PROCESSED >                             INKCS002
02181                               PV-HOLD-SELECTED-ITEMS)             INKCS002
02182          OR (IN002-IDX GREATER THAN IN002-MAX-ITEMS))                CL**6
02183          IF  IN002-ACTION-SW (IN002-IDX) > SPACE                     CL**6
02184              PERFORM 6110-PROCESS-ITEMS                           INKCS002
02185          END-IF                                                   INKCS002
02186          ADD +1                TO PV-ITEMS-PROCESSED              INKCS002
02187      END-PERFORM.                                                 INKCS002
02188      PERFORM 6111-WRITE-LIST-QUEUE.                               INKCS002
02189 *----------------------------------------------------------------*INKCS002
02190 *    READ THE LIST QUEUE RECORD.                                 *INKCS002
02191 *----------------------------------------------------------------*INKCS002
02192                                                                   INKCS002
02193  6105-READ-SEL-QUEUE.                                             INKCS002
02194                                                                   INKCS002
02195      EXEC CICS                                                    INKCS002
02196           READQ TS                                                INKCS002
02197               QUEUE (ASC-SEL-QUEUE-NAME)                          INKCS002
02198               INTO  (IN002-SELECTED-AREAS)                           CL**7
02199               ITEM  (PV-SEL-QUEUE-ITEM)                           INKCS002
02200               RESP  (PV-CICS-RESP)                                INKCS002
02201      END-EXEC.                                                    INKCS002
02202                                                                      CL**3
02203      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS002
02204          CONTINUE                                                 INKCS002
02205      ELSE                                                         INKCS002
02206          SET DP013-CICS-ABEND      TO  TRUE                       INKCS002
02207          SET DP013-NO-ROLLBACK     TO  TRUE                       INKCS002
02208          MOVE '6105-READ-SEL-QUEUE'                               INKCS002
02209                                    TO  DP013-PARAGRAPH            INKCS002
02210          MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'           INKCS002
02211                                    TO  DP013-MESSAGE-TEXT(1)      INKCS002
02212          PERFORM DP013-0000-PROCESS-ABEND                         INKCS002
02213      END-IF.                                                      INKCS002
02214 /                                                                    CL**3
02215 *----------------------------------------------------------------*INKCS002
02216 *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*INKCS002
02217 *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*INKCS002
02218 *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *INKCS002
02219 *----------------------------------------------------------------*INKCS002
02220                                                                   INKCS002
02221  6110-PROCESS-ITEMS.                                              INKCS002
02222                                                                   INKCS002
02223      COMPUTE PV-ITEM = 1 + ((IN002-ITEM (IN002-IDX) - 1)             CL**6
02224                      / PC-ITEMS-PER-PANEL).                       INKCS002
02225                                                                   INKCS002
02226      IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                           INKCS002
02227          IF  PV-SAVE-ITEM NOT EQUAL ZERO                          INKCS002
02228              PERFORM 6111-WRITE-LIST-QUEUE                        INKCS002
02229          END-IF                                                   INKCS002
02230          MOVE PV-ITEM          TO PV-SAVE-ITEM                    INKCS002
02231                                                                   INKCS002
02232          PERFORM 6112-READ-LIST-QUEUE                             INKCS002
02233          IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)               INKCS002
02234              CONTINUE                                             INKCS002
02235          END-IF                                                   INKCS002
02236      END-IF.                                                      INKCS002
02237                                                                   INKCS002
02238      COMPUTE PV-SUB = (IN002-ITEM (IN002-IDX)                        CL**6
02239                     - LQW-LIST-ITEM-NUMBER (1)) + 1.              INKCS002
02240      SET LQW-DESELECT (PV-SUB) TO    TRUE                         INKCS002
02241      MOVE LQW-SELECTED-SW (PV-SUB)                                INKCS002
02242                                TO LQW-TEMP-SELECTED-SW (PV-SUB).  INKCS002
02243      EVALUATE TRUE                                                INKCS002
02244          WHEN IN002-INQ (IN002-IDX)                                  CL**6
02245               MOVE PC-INQ      TO LQW-ACTION (PV-SUB)             INKCS002
02246                                                                   INKCS002
02247          WHEN IN002-UPD (IN002-IDX)                                  CL**6
02248               MOVE PC-UPD      TO LQW-ACTION (PV-SUB)             INKCS002
02249                                                                   INKCS002
02250          WHEN IN002-DEL (IN002-IDX)                                  CL**6
02251               MOVE PC-DEL      TO LQW-ACTION (PV-SUB)             INKCS002
02252                                                                   INKCS002
02253          WHEN IN002-ERR (IN002-IDX)                                  CL**6
02254               MOVE PC-ERR      TO LQW-ACTION (PV-SUB)             INKCS002
02255      END-EVALUATE.                                                INKCS002
02256                                                                   INKCS002
02257      SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                INKCS002
02258                                                                   INKCS002
02259 *----------------------------------------------------------------*INKCS002
02260 *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *INKCS002
02261 *----------------------------------------------------------------*INKCS002
02262                                                                   INKCS002
02263  6111-WRITE-LIST-QUEUE.                                           INKCS002
02264                                                                   INKCS002
02265      EXEC CICS                                                    INKCS002
02266          WRITEQ TS                                                INKCS002
02267              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS002
02268              FROM  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS002
02269              ITEM  (PV-SAVE-ITEM)                                 INKCS002
02270              REWRITE                                              INKCS002
02271              RESP  (PV-CICS-RESP)                                 INKCS002
02272      END-EXEC.                                                    INKCS002
02273                                                                      CL**3
02274      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS002
02275          CONTINUE                                                 INKCS002
02276      ELSE                                                         INKCS002
02277          SET DP013-CICS-ABEND  TO  TRUE                           INKCS002
02278          SET DP013-NO-ROLLBACK TO  TRUE                           INKCS002
02279          MOVE '6111-WRITE-LIST-QUEUE'                             INKCS002
02280                                TO  DP013-PARAGRAPH                INKCS002
02281          MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                  INKCS002
02282                                TO  DP013-MESSAGE-TEXT(1)          INKCS002
02283          PERFORM DP013-0000-PROCESS-ABEND                         INKCS002
02284      END-IF.                                                      INKCS002
02285 /                                                                    CL**3
02286 *----------------------------------------------------------------*INKCS002
02287 *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*INKCS002
02288 *----------------------------------------------------------------*INKCS002
02289                                                                   INKCS002
02290  6112-READ-LIST-QUEUE.                                            INKCS002
02291                                                                   INKCS002
02292      EXEC CICS                                                    INKCS002
02293          READQ TS                                                 INKCS002
02294              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS002
02295              INTO  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS002
02296              ITEM  (PV-ITEM)                                      INKCS002
02297              RESP  (PV-CICS-RESP)                                 INKCS002
02298      END-EXEC.                                                    INKCS002
02299                                                                      CL**3
02300      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS002
02301          CONTINUE                                                 INKCS002
02302      ELSE                                                         INKCS002
02303          SET DP013-CICS-ABEND        TO TRUE                      INKCS002
02304          SET DP013-NO-ROLLBACK       TO TRUE                      INKCS002
02305          MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH           INKCS002
02306          MOVE 'ATTEMPTING TO READQ LIST QUEUE'                    INKCS002
02307                                      TO DP013-MESSAGE-TEXT(1)     INKCS002
02308          PERFORM DP013-0000-PROCESS-ABEND                         INKCS002
02309      END-IF.                                                      INKCS002
02310                                                                   INKCS002
02311  EJECT                                                            INKCS002
02312 *----------------------------------------------------------------*INKCS002
02313 *    ABEND PROCESSOR MODULE                                      *INKCS002
02314 *----------------------------------------------------------------*INKCS002
02315                                                                   INKCS002
02316      COPY DPPD013.                                                INKCS002
