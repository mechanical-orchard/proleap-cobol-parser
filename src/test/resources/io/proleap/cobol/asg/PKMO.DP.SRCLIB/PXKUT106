       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.           PXKUT106.                                  00020000
       AUTHOR.               DAVE WELLER.                               00030000
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00040000
       DATE-WRITTEN          08/2010.                                   00050000
       DATE-COMPILED.                                                   00060000
                                                                        00070000
      ******************************************************************00080000
      *   PXKUT106 - EXCLUDE SKU / STORE ROWS FROM THE WHITE TICKET     00090000
      *              CTA EXTRACT FILE WHEN A MATCH IS FOUND ON THE      00100000
      *              CTA EXCLUSION FILE.                                00101000
      ******************** HISTORY OF CHANGES **************************00110000
      * CHG ID/                                                        *00120000
      * WR/PROJ          DATE        DESCRIPTION OF CHANGES            *00130000
      * ---------------  ----------  --------------------------------  *00140000
      * PKE000000004715  08/01/2010  INITIAL IMPLEMENTATION             00150004
      *                                                                 00160000
      ******************************************************************00170000
                                                                        00180000
       ENVIRONMENT DIVISION.                                            00190000
                                                                        00200000
       CONFIGURATION SECTION.                                           00210000
                                                                        00220000
       SOURCE-COMPUTER. IBM-3090.                                       00230000
       OBJECT-COMPUTER. IBM-3090.                                       00240000
                                                                        00250000
       INPUT-OUTPUT SECTION.                                            00260000
       FILE-CONTROL.                                                    00270000
           SELECT WHITE-TICKET-EXTR-FILE-IN                             00280000
               ASSIGN TO INP01.                                         00290000
                                                                        00300000
           SELECT SKU-STR-EXCLSN-FILE-IN                                00310000
               ASSIGN TO INP02.                                         00320000
                                                                        00330000
           SELECT WHITE-TICKET-EXTR-FILE-OUT                            00340000
               ASSIGN TO OUT01.                                         00350000
                                                                        00360000
       DATA DIVISION.                                                   00370000
                                                                        00380000
       FILE SECTION.                                                    00390000
       FD  WHITE-TICKET-EXTR-FILE-IN                                    00400000
           LABEL RECORDS ARE STANDARD                                   00410000
           RECORDING MODE IS F                                          00420000
           BLOCK CONTAINS 0 RECORDS.                                    00430000
       01  WHITE-TICKET-EXTR-REC-IN        PIC  X(213).                 00440000
                                                                        00450000
       FD  SKU-STR-EXCLSN-FILE-IN                                       00460000
           LABEL RECORDS ARE STANDARD                                   00470000
           RECORDING MODE IS F                                          00480000
           BLOCK CONTAINS 0 RECORDS.                                    00490000
       01  SKU-STR-EXCLSN-REC-OUT          PIC  X(007).                 00500000
                                                                        00510000
       FD  WHITE-TICKET-EXTR-FILE-OUT                                   00520000
           LABEL RECORDS ARE STANDARD                                   00530000
           RECORDING MODE IS F                                          00540000
           BLOCK CONTAINS 0 RECORDS.                                    00550000
       01  WHITE-TICKET-EXTR-REC-OUT       PIC X(213).                  00560000
                                                                        00570000
       WORKING-STORAGE SECTION.                                         00580000
                                                                        00610000
      ***************************************************************** 00620000
      *  PROGRAM'S ACCUMULATORS                                       * 00630000
      ***************************************************************** 00640000
       01  PA-PROGRAM-ACCUMULATORS.                                     00650000
           05  PA-RECORDS-READ             PIC 9(10)       VALUE ZEROES.00660000
           05  PA-RECORDS-WRITTEN          PIC 9(10)       VALUE ZEROES.00663000
                                                                        00664000
      ***************************************************************** 00665000
      *  PROGRAM'S CONSTANTS                                          * 00666000
      ***************************************************************** 00667000
       01  PC-PROGRAM-CONSTANTS.                                        00668000
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE        00669000
               'PXKUT106'.                                              00670000
                                                                        00680000
      ***************************************************************** 00690000
      *  PROGRAM'S SWITCHES                                           * 00700000
      ***************************************************************** 00710000
       01  PS-PROGRAM-SWITCHES.                                         00720000
           05  PS-END-OF-EXTR-INP-FILE-SW PIC X(01)        VALUE 'N'.   00730000
               88  PS-END-OF-EXTR-INP-FILE                 VALUE 'Y'.   00740000
               88  PS-NOT-END-OF-EXTR-INP-FILE             VALUE 'N'.   00750000
           05  PS-END-OF-EXCLSN-INP-FILE-SW PIC X(01)      VALUE 'N'.   00760000
               88  PS-END-OF-EXCLSN-INP-FILE               VALUE 'Y'.   00770000
               88  PS-NOT-END-OF-EXCLSN-INP-FILE           VALUE 'N'.   00780000
           05  PS-OPERATION-ATTEMPTED      PIC X(40)       VALUE SPACES.00790000
               88  PS-DEADLOCK-MSG                         VALUE        00800000
                   'MAXIMUM DEADLOCK COUNT EXCEEDED         '.          00810000
                                                                        00820000
      ***************************************************************** 00830000
      *  PROGRAM'S VARIABLES                                          * 00840000
      ***************************************************************** 00850000
       01  PV-PROGRAM-VARIABLES.                                        00860000
           05  PV-PARAGRAPH-NAME           PIC X(40)   VALUE SPACES.    00870000
           05  PV-OPERATION-MSG            PIC  X(60)  VALUE SPACES.    00880000
           05  PV-OPERATION-MSG-1          PIC  X(40)  VALUE SPACES.    00890000
           05  PV-OPERATION-MSG-2          PIC  X(40)  VALUE SPACES.    00900000
                                                                        00930000
                                                                        00940000
      ***************************************************************** 00950000
      *  ABEND CODE AREA                                              * 00960000
      ***************************************************************** 00970000
       01  ABEND-CODE                      PIC S9(04)  VALUE +0  COMP.  00980000
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.     00990000
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     01000000
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.     01010000
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.     01020000
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.     01030000
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.     01040000
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.     01050000
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.     01060000
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.     01070000
           88  ABEND-4013-DB-ERROR                     VALUE +4013.     01080000
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.     01090000
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.     01100000
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.     01110000
                                                                        01120000
       01  WS-MSG-TEXT                     PIC X(512)      VALUE SPACES.01130000
       01  WS-MAX-SIZE                     PIC S9(09)      VALUE +512   01140000
                               COMP.                                    01150000
       01  WS-MSG-LENGTH                   PIC S9(09)      VALUE ZEROES 01160000
                               COMP.                                    01170000
                                                                        01180000
      ******************************************************************01190000
      **COPYBOOKS                                                       01200000
      ******************************************************************01210000
                                                                        01270000
      ****WHITE TICKET EVENT EXTRACT INPUT / OUTPUT RECORD LAYOUT       01280000
           COPY PXRD040.                                                01290000
                                                                        01291000
      ****CTA EXCLUSION INPUT RECORD LAYOUT                             01292000
           COPY PXRD053.                                                01293000
                                                                        01310000
      ******************************************************************01311000
       PROCEDURE DIVISION.                                              01320000
      ******************************************************************01340000
      *    MAINLINE LOGIC                                              *01350000
      ******************************************************************01360000
                                                                        01370000
       0000-MAINLINE.                                                   01380000
                                                                        01390000
           PERFORM 1000-INITIALIZATION.                                 01400000
                                                                        01410000
           PERFORM 2000-PROCESS-EXTR-FILE                               01420000
               UNTIL PS-END-OF-EXTR-INP-FILE.                           01430000
                                                                        01440000
           PERFORM 9900-EOJ-LOGIC.                                      01450000
                                                                        01460000
           GOBACK.                                                      01470000
                                                                        01480000
      ******************************************************************01490000
      *    OPEN FILES AND READ FIRST RECORDS FROM INPUT FILES.         *01500000
      ******************************************************************01510000
       1000-INITIALIZATION.                                             01520000
                                                                        01530000
           OPEN INPUT  WHITE-TICKET-EXTR-FILE-IN                        01540000
                       SKU-STR-EXCLSN-FILE-IN                           01550000
               OUTPUT  WHITE-TICKET-EXTR-FILE-OUT.                      01560000
                                                                        01570000
           PERFORM 6100-READ-WHITE-TCK-EXTR-FL.                         01580000
           PERFORM 6200-READ-CTA-EXCLSN-FL.                             01590000
           IF PS-END-OF-EXTR-INP-FILE                                   01600000
               DISPLAY ' *** NOTHING TO PROCESS ***'                    01610000
               DISPLAY ' '                                              01620000
           END-IF.                                                      01630000
                                                                        01640000
      ******************************************************************01650000
      *    2000-PROCESS-EXTR-FILE                                      *01660001
      *    THE INPUT FILES ARE IN SKU, STORE SEQUENCE.                 *01670000
      ******************************************************************01680000
       2000-PROCESS-EXTR-FILE.                                          01690001
                                                                        01700000
           IF PX053-SKU-NBR = PXRD040-SKU-NBR                           01710000
             IF PX053-STR-NBR = PXRD040-STR-NBR                         01720000
      * DO NOT WRITE OUTPUT ON THE MATCH - THIS EXCLUDES THE ROW        01780000
               PERFORM 6200-READ-CTA-EXCLSN-FL                          01800000
               PERFORM 6100-READ-WHITE-TCK-EXTR-FL                      01810000
             ELSE                                                       01820000
      * NEVER WRITE WHEN ONLY THE EXCLUSION ROW EXISTS                  01830000
               IF PX053-STR-NBR < PXRD040-STR-NBR                       01840000
                 PERFORM 6200-READ-CTA-EXCLSN-FL                        01850000
               ELSE                                                     01860000
      * WRITE WHEN ONLY THE INPUT ROW EXISTS                            01870000
                 PERFORM 6300-WRITE-WHITE-TCK-EXTR-FL                   01890000
                 PERFORM 6100-READ-WHITE-TCK-EXTR-FL                    01910000
               END-IF                                                   01911000
             END-IF                                                     01912000
           ELSE                                                         01920000
      * NEVER WRITE WHEN ONLY THE EXCLUSION ROW EXISTS                  01930000
           IF PX053-SKU-NBR < PXRD040-SKU-NBR                           01940000
             PERFORM 6200-READ-CTA-EXCLSN-FL                            01950000
           ELSE                                                         01960000
      ******* PX053-SKU-NBR > PXRD040-SKU-NBR                           01961000
      * WRITE OUTPUT WHEN ONLY THE INPUT ROW EXISTS                     01970000
             PERFORM 6300-WRITE-WHITE-TCK-EXTR-FL                       01990000
             PERFORM 6100-READ-WHITE-TCK-EXTR-FL                        02010000
           END-IF.                                                      02011000
                                                                        02020000
      ******************************************************************02030000
      *  READ EXTRACT INPUT FILE                                        02040000
      ******************************************************************02050000
       6100-READ-WHITE-TCK-EXTR-FL.                                     02060000
                                                                        02070000
           READ WHITE-TICKET-EXTR-FILE-IN INTO PXRD040-EXTRACT-RECORD   02080000
               AT END SET PS-END-OF-EXTR-INP-FILE TO TRUE.              02090000
                                                                        02100000
           IF PS-NOT-END-OF-EXTR-INP-FILE                               02110000
               ADD 1 TO PA-RECORDS-READ                                 02130000
           END-IF.                                                      02170000
                                                                        02180000
      ******************************************************************02190000
      *  READ ADJUSTMENT INPUT FILE                                     02200000
      ******************************************************************02210000
       6200-READ-CTA-EXCLSN-FL.                                         02220000
                                                                        02230000
           READ SKU-STR-EXCLSN-FILE-IN INTO PX053-SKU-STR-EXCLSN-REC    02240000
               AT END SET PS-END-OF-EXCLSN-INP-FILE TO TRUE.            02250000
                                                                        02260000
           IF PS-END-OF-EXCLSN-INP-FILE                                 02270000
               MOVE ALL '9' TO PX053-SKU-NBR                            02280001
                               PX053-STR-NBR                            02290001
           END-IF.                                                      02300000
                                                                        02310000
      ******************************************************************02320000
      *  WRITE OH QTY ADJ FILE                                          02330000
      ******************************************************************02340000
       6300-WRITE-WHITE-TCK-EXTR-FL.                                    02350000
           WRITE WHITE-TICKET-EXTR-REC-OUT                              02360000
             FROM PXRD040-EXTRACT-RECORD.                               02370000
           ADD 1 TO PA-RECORDS-WRITTEN.                                 02380000
                                                                        02390000
      ******************************************************************02400000
      *    9900-EOJ-LOGIC.                                              02410000
      *      THIS IS PERFORMED AT END OF INPUT FILE.                    02420000
      *      PURPOSE: CLOSE INPUT FILE,                                 02430000
      *      DISPLAY RECORD COUNTS.                                     02440000
      ******************************************************************02450000
       9900-EOJ-LOGIC.                                                  02460000
                                                                        02470000
           CLOSE WHITE-TICKET-EXTR-FILE-IN                              02480000
                 SKU-STR-EXCLSN-FILE-IN                                 02490000
                 WHITE-TICKET-EXTR-FILE-OUT.                            02500000
                                                                        02510000
           DISPLAY '**---- START ' PC-PROGRAM-NAME ' ------**'.         02511003
           DISPLAY ' '.                                                 02520000
           DISPLAY 'RECORDS READ       ' PA-RECORDS-READ.               02530000
           DISPLAY 'RECORDS WRITTEN    ' PA-RECORDS-WRITTEN.            02560000
           DISPLAY '**------ END ' PC-PROGRAM-NAME ' ------**'.         02570000
                                                                        02580000
      ******************************************************************02590000
      *    END OF SOURCE CODE FOR PXKUT106                             *02600002
      ******************************************************************02610000
