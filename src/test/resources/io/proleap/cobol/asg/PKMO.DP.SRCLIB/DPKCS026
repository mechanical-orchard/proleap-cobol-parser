00001 *                                                                 02/05/91
00002 *                                                                 DPKCS026
00003 *                                                                    LV016
00004  IDENTIFICATION DIVISION.                                            CL**1
00005                                                                      CL**1
00006  PROGRAM-ID.    DPKCS026.                                            CL**1
00007  AUTHOR.        DAN FRANKLIN.                                        CL**1
00008  INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**1
00009  DATE-WRITTEN.  12-10-90.                                            CL**1
00010  DATE-COMPILED.                                                      CL**1
00011                                                                      CL**1
00012 *****************************************************************    CL**1
00013 *    SELECT ABEND EXIT - Z026                                        CL*13
00014 *                                                                    CL*13
00015 *    THIS TRANSACTION IS KICKED OF BY THE ABEND PROCESSOR FOR        CL*13
00016 *    THOSE USERS THAT HAVE AUTHORITY TO RETURN TO NATIVE CICS        CL*15
00017 *    AFTER A TRANSACTION ABENDS.  A SCREEN IS DISPLAYED GIVING       CL*14
00018 *    THE USER THE OPTION OF RETURNING TO NATIVE CICS, RETURNING      CL*16
00019 *    TO THE MAIN MENU, OR LOGGING OFF OF CICS.                       CL*13
00020 *****************************************************************    CL**1
00021                                                                      CL**1
00022 *                                                                    CL**1
00023 *                                                                    CL**1
00024 *                                                                    CL**1
00025  ENVIRONMENT DIVISION.                                               CL**1
00026 *                                                                    CL**1
00027 *                                                                    CL**1
00028 *                                                                    CL**1
00029  DATA DIVISION.                                                      CL**1
00030 *                                                                    CL**1
00031 *                                                                    CL**1
00032  WORKING-STORAGE SECTION.                                            CL**1
00033                                                                      CL**1
00034 *****************************************************************    CL**1
00035 *    STANDARD ATTENTION IDENTIFIER LIST                              CL**1
00036 *****************************************************************    CL**1
00037                                                                      CL**1
00038      COPY DFHAID.                                                    CL**1
00039                                                                      CL**1
00040 *****************************************************************    CL**1
00041 *    STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST           CL**1
00042 *****************************************************************    CL**1
00043                                                                      CL**1
00044      COPY DFHBMSCA.                                                  CL**1
00045                                                                      CL**1
00046 *****************************************************************    CL**1
00047 *    MAP LAYOUT                                                      CL**1
00048 *****************************************************************    CL**1
00049                                                                      CL**1
00050      COPY DPKM026.                                                   CL**1
00051                                                                      CL**1
00052  01  EM-ERROR-MESSAGES.                                              CL**1
00053      05  EM-ERROR-MESSAGE-1              PIC X(78)   VALUE           CL*12
00054      'INVALID FUNCTION KEY'.                                         CL**2
00055                                                                      CL**7
00056  01  EM-EXIT-MESSAGES.                                               CL**7
00057      05  EM-EXIT-MESSAGE-1               PIC X(78)   VALUE           CL**7
00058      'CICS ARCHITECTURE TERMINATED'.                                 CL**7
00059                                                                      CL**1
00060  01  FV-FIXED-VARIABLES.                                             CL**1
00061      05  FV-NAVIGATOR-TRAN-ID            PIC X(04)   VALUE 'Z008'.   CL*10
00062      05  FV-LOGOFF-TRAN-ID               PIC X(04)   VALUE 'Z011'.   CL*10
00063      05  FV-THIS-TRAN-ID                 PIC X(04)   VALUE 'Z026'.   CL*10
00064      05  FV-PROGRAM-ID                   PIC X(08)   VALUE           CL**1
00065      'DPKCS026'.                                                     CL**1
00066      05  FV-MESSAGE-LENGTH               PIC S9(04)  VALUE +78       CL**1
00067                                                      COMP SYNC.      CL**1
00068      05  FV-COMMAREA-LENGTH              PIC S9(04)  VALUE +1        CL**8
00069                                                      COMP SYNC.      CL**8
00070                                                                      CL**6
00071  01  PM-PROGRAM-MESSAGE                  PIC X(78)   VALUE SPACES.   CL**6
00072                                                                      CL**1
00073  01  ST-SYSTEM-TIME-AREA.                                            CL**1
00074      05  ST-ABSTIME                      PIC S9(15)  VALUE +0        CL**1
00075                                                      COMP-3.         CL**1
00076  01  WV-WORK-VARIABLES.                                              CL**1
00077      05  WV-RESP                         PIC S9(04)  VALUE +0        CL**1
00078                                                      COMP SYNC.      CL**1
00079      05  WV-COMMAREA                     PIC X(01)   VALUE SPACES.   CL**8
00080                                                                      CL**1
00081  LINKAGE SECTION.                                                    CL**1
00082                                                                      CL**1
00083  01  DFHCOMMAREA                         PIC X(01).                  CL*11
00084 *                                                                    CL**1
00085 *                                                                    CL**1
00086 *                                                                    CL**1
00087  PROCEDURE DIVISION.                                                 CL**1
00088                                                                      CL**1
00089  A100-MAIN-MODULE.                                                   CL**1
00090                                                                      CL**1
00091 ******************************************************************   CL**1
00092 *    IF COMING INTO THIS TRANSACTION FOR THE 1ST TIME, SET UP        CL*13
00093 *    THE SCREEN HEADER, SEND THE MAP AND RETURN TO NATIVE CICS       CL*13
00094 *    HAVING IT KICK OFF THIS TRANSACTION.                            CL*13
00095 *                                                                    CL*13
00096 *    IF COMING INTO THIS TRANSACTION FOR THE 2ND TIME OR MORE,       CL*13
00097 *    RECEIVE THE MAP, SET UP THE SCREEN HEADER, AND CHECK WHICH      CL*13
00098 *    FUNCTION KEY THE USER HAS SELECTED.                             CL*13
00099 ******************************************************************   CL**1
00100                                                                      CL**1
00101      MOVE LOW-VALUES TO DP026AO.                                     CL**1
00102                                                                      CL**1
00103      IF EIBCALEN   =  ZERO                                           CL*16
00104          PERFORM B100-SCREEN-HEADER                                  CL**8
00105          PERFORM Z100-SEND-MAP                                       CL**9
00106          PERFORM Z200-RETURN-TO-Z026                                 CL**9
00107      ELSE                                                            CL**1
00108          PERFORM Z300-RECEIVE-MAP                                    CL**8
00109          PERFORM B100-SCREEN-HEADER                                  CL**8
00110          PERFORM B200-CHECK-FUNCTION-KEY                             CL**1
00111      END-IF.                                                         CL**1
00112                                                                      CL**1
00113      GOBACK.                                                         CL**1
00114  EJECT                                                               CL**1
00115  B100-SCREEN-HEADER.                                                 CL**1
00116                                                                      CL**1
00117 ******************************************************************   CL**1
00118 *    SET UP THE SCREEN HEADER.                                       CL*13
00119 *                                                                    CL*13
00120 *    PERFORMED BY A100-MAIN-MODULE.                                  CL**1
00121 ******************************************************************   CL**1
00122                                                                      CL**1
00123      EXEC CICS                                                       CL**1
00124          ASKTIME                                                     CL**1
00125          ABSTIME(ST-ABSTIME)                                         CL**1
00126      END-EXEC.                                                       CL**1
00127                                                                      CL**1
00128      EXEC CICS                                                       CL**1
00129          FORMATTIME                                                  CL**1
00130          ABSTIME(ST-ABSTIME)                                         CL**1
00131          MMDDYY(ADATEO)                                              CL**1
00132          DATESEP                                                     CL**1
00133          TIME(ATIMEO)                                                CL**1
00134          TIMESEP                                                     CL**1
00135      END-EXEC.                                                       CL**1
00136                                                                      CL**1
00137      MOVE EIBTRMID      TO ATERMIDO.                                 CL**1
00138      MOVE FV-PROGRAM-ID TO APGMIDO.                                  CL**1
00139  EJECT                                                               CL**1
00140  B200-CHECK-FUNCTION-KEY.                                            CL**1
00141                                                                      CL**1
00142 ******************************************************************   CL**1
00143 *    PERFORM THE APPROPRIATE PARAGRAPH BASED ON WHICH FUNCTION       CL*13
00144 *    KEY WAS SELECTED BY THE USER.                                   CL*13
00145 *                                                                    CL*13
00146 *    PERFORMED BY A100-MAIN-MODULE.                                  CL*13
00147 ******************************************************************   CL**1
00148                                                                      CL**1
00149      EVALUATE TRUE                                                   CL**1
00150                                                                      CL**1
00151          WHEN EIBAID = DFHPF3                                        CL**1
00152              MOVE EM-EXIT-MESSAGE-1 TO PM-PROGRAM-MESSAGE            CL**7
00153                                                                      CL**6
00154              PERFORM Z400-SEND-MESSAGE                               CL**9
00155              PERFORM Z500-RETURN-TO-CICS                             CL**9
00156                                                                      CL**2
00157          WHEN EIBAID = DFHPF4                                        CL**2
00158                                                                      CL*10
00159              PERFORM Z600-START-NAVIGATOR                            CL**2
00160              PERFORM Z500-RETURN-TO-CICS                             CL**9
00161                                                                      CL**2
00162          WHEN EIBAID = DFHPF24                                       CL**2
00163                                                                      CL*10
00164              PERFORM Z700-START-LOGOFF-TRAN                          CL**2
00165              PERFORM Z500-RETURN-TO-CICS                             CL**9
00166                                                                      CL**2
00167          WHEN OTHER                                                  CL**2
00168              MOVE DFHRED             TO AMSGC                        CL**3
00169              MOVE EM-ERROR-MESSAGE-1 TO AMSGO                        CL*12
00170                                                                      CL**2
00171              PERFORM Z100-SEND-MAP                                   CL**9
00172              PERFORM Z200-RETURN-TO-Z026                             CL**9
00173                                                                      CL**3
00174      END-EVALUATE.                                                   CL**3
00175  EJECT                                                               CL**9
00176  Z100-SEND-MAP.                                                      CL**9
00177                                                                      CL**1
00178 ******************************************************************   CL**1
00179 *    ERASE THE SCREEN AND SEND THE MAP.                              CL**1
00180 *                                                                    CL**1
00181 *    PERFORMED BY A100-MAIN-MODULE.                                  CL**1
00182 ******************************************************************   CL**1
00183                                                                      CL**1
00184      EXEC CICS                                                       CL**1
00185          SEND                                                        CL**1
00186          MAP('DP026A')                                               CL**1
00187          MAPSET('DPKM026')                                           CL**1
00188          ERASE                                                       CL**1
00189          CURSOR                                                      CL**1
00190      END-EXEC.                                                       CL**1
00191  EJECT                                                               CL**1
00192  Z200-RETURN-TO-Z026.                                                CL**9
00193                                                                      CL**1
00194 ******************************************************************   CL**1
00195 *    RETURN TO NATIVE CICS AND HAVE IT KICK OFF THIS TRANSACTION.    CL*13
00196 *                                                                    CL*13
00197 *    PERFORMED BY A100-MAIN-MODULE.                                  CL*13
00198 ******************************************************************   CL**1
00199                                                                      CL**1
00200      EXEC CICS                                                       CL**1
00201          RETURN                                                      CL**1
00202          TRANSID(FV-THIS-TRAN-ID)                                    CL*10
00203          COMMAREA(WV-COMMAREA)                                       CL**8
00204          LENGTH(FV-COMMAREA-LENGTH)                                  CL**8
00205      END-EXEC.                                                       CL**1
00206  EJECT                                                               CL**1
00207  Z300-RECEIVE-MAP.                                                   CL**9
00208                                                                      CL**9
00209 ******************************************************************   CL**9
00210 *    RECEIVE THE MAP.                                                CL**9
00211 *                                                                    CL*13
00212 *    PERFORMED BY A100-MAIN-MODULE.                                  CL*13
00213 ******************************************************************   CL**9
00214                                                                      CL**9
00215      EXEC CICS                                                       CL**9
00216          RECEIVE                                                     CL**9
00217          MAP('DP026A')                                               CL**9
00218          MAPSET('DPKM026')                                           CL**9
00219          RESP(WV-RESP)                                               CL**9
00220      END-EXEC.                                                       CL**9
00221  EJECT                                                               CL**9
00222  Z400-SEND-MESSAGE.                                                  CL**9
00223                                                                      CL**9
00224 ******************************************************************   CL**9
00225 *    SEND A MESSAGE TO THE TERMINAL.                                 CL*13
00226 *                                                                    CL*13
00227 *    PERFORMED BY B200-CHECK-FUNCTION-KEY.                           CL*13
00228 ******************************************************************   CL**9
00229                                                                      CL**9
00230      EXEC CICS                                                       CL**9
00231          SEND                                                        CL**9
00232          FROM(PM-PROGRAM-MESSAGE)                                    CL**9
00233          LENGTH(FV-MESSAGE-LENGTH)                                   CL**9
00234          ERASE                                                       CL**9
00235      END-EXEC.                                                       CL**9
00236  EJECT                                                               CL**9
00237  Z500-RETURN-TO-CICS.                                                CL**9
00238                                                                      CL**9
00239 ******************************************************************   CL**9
00240 *    RETURN TO NATIVE CICS.                                          CL*13
00241 *                                                                    CL*13
00242 *    PERFORMED BY B200-CHECK-FUNCTION-KEY.                           CL*13
00243 ******************************************************************   CL**9
00244                                                                      CL**9
00245      EXEC CICS                                                       CL**9
00246          RETURN                                                      CL**9
00247      END-EXEC.                                                       CL**9
00248  EJECT                                                               CL**9
00249  Z600-START-NAVIGATOR.                                               CL**2
00250                                                                      CL**2
00251 ******************************************************************   CL**2
00252 *    START THE NAVIGATOR TRANSACTION (Z008)                          CL*13
00253 *                                                                    CL*13
00254 *    PERFORMED BY B200-CHECK-FUNCTION-KEY.                           CL*13
00255 ******************************************************************   CL**2
00256                                                                      CL**2
00257      EXEC CICS                                                       CL**2
00258          START                                                       CL**2
00259          TRANSID(FV-NAVIGATOR-TRAN-ID)                               CL*10
00260          TERMID(EIBTRMID)                                            CL*12
00261          RESP(WV-RESP)                                               CL**2
00262      END-EXEC.                                                       CL**2
00263  EJECT                                                               CL**2
00264  Z700-START-LOGOFF-TRAN.                                             CL**2
00265                                                                      CL**2
00266 ******************************************************************   CL**2
00267 *    START THE LOGOFF TRANSACTION (Z011)                             CL*13
00268 *                                                                    CL*13
00269 *    PERFORMED BY B200-CHECK-FUNCTION-KEY.                           CL*13
00270 ******************************************************************   CL**2
00271                                                                      CL**2
00272      EXEC CICS                                                       CL**2
00273          START                                                       CL**2
00274          TRANSID(FV-LOGOFF-TRAN-ID)                                  CL*10
00275          TERMID(EIBTRMID)                                            CL*12
00276          RESP(WV-RESP)                                               CL**2
00277      END-EXEC.                                                       CL**2
