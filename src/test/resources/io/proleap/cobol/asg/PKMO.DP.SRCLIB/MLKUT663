       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT663.                                                 
       AUTHOR.        MARK A FRAMNESS.                                          
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04/11/2011.                                               
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      *                                                                         
      * THIS PROGRAM GENERATES A LOAD FILE FOR MDSE EDW TERADATA                
      *                                                                         
      ******************************************************************        
PG0100*  01/06/00 - PATI GREEN                                                  
PG0100*             CHANGED DELIMITERS ON PRD-DIM-KY-ID-OUT TO BE + AND         
PG0100*             . INSTEAD OF _ AND _ (TO ELIMINATE UNIX MASSAGE).           
      ******************************************************************        
      ******************************************************************        
BD1012*  10/12/05 - BETTY DORST                                                 
BD1012*             EXPANDED INPUT AND OUPTUT FILES TO ACCOMODATE ADDING        
BD1012*             NEW FIELDS MKDN-CDE, ORIG-CL-DTE, NEW-UNT-RTL-AMT           
BD1012*             OLD-UNT-RTL-AMT.  ALSO ADDED NEW OUTPUT FILE                
BD1012*             OUT-FILE2 TO CREATE EXTRACT TO LOAD NEW WORK                
BD1012*             TABLE FOR QUERY TOOL MWT_CLR_EVNT_WRK FOR                   
BD1012*             THE MARKDOWN OPTIMIZATION PROJECT.                          
      ******************************************************************        
      ******************************************************************        
39512 *  04/11/2011 - MARK A FRAMNESS WR39512                                   
39512 *               CLONED FROM MWKUT663 TO MLKUT663 FOR MDSE EDW             
39512 *               TERADATA SOURCE TO STAGE & ELIMINATED THE '_'             
      *               DELIMITATION FROM WS-OUTPUT-RECORD1                       
      *               DELIMITERS ON PRD-DIM-KY-ID-OUT '+' AND '.'               
      *               ARE REMOVED                                               
      ******************************************************************        
MAF212*  02/29/2012 - MARK A FRAMNESS                                           
MAF212*               ADD AN INPUT & OUTPUT FIELD TO THE REC1 LAYOUTS           
MAF212*               TO ACCEPT AND OUTPUT THE PRCHG-FIN-LIAB-DTE FIELD         
MAF212******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
           SELECT IN-FILE1 ASSIGN TO INP01.                                     
           SELECT OUT-FILE1 ASSIGN TO OUT01.                                    
           SELECT OUT-FILE2 ASSIGN TO OUT02.                                    
      *                                                                         
      ****************************************************************          
      * DATA DIVISION:                                                          
      ****************************************************************          
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  IN-FILE1                                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
BD1012 01  INPUT-RECORD1      PIC X(88).                                        
      *                                                                         
       FD  OUT-FILE1                                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
BD1012 01  OUTPUT-RECORD1        PIC X(121).                                    
      *                                                                         
BD1012 FD  OUT-FILE2                                                            
BD1012     RECORDING MODE IS F                                                  
BD1012     LABEL RECORDS ARE STANDARD                                           
BD1012     BLOCK CONTAINS 0 RECORDS.                                            
BD1012 01  OUTPUT-RECORD2        PIC X(63).                                     
      *                                                                         
      ********************************************************                  
      * WORKING STORAGE:                                                        
      ********************************************************                  
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
PG0100 01  PC-PROGRAM-CONSTANTS.                                                
PG0100     05  PC-PROGRAM-NAME         PIC X(08)  VALUE 'MLKUT663'.             
                                                                                
       01  COUNT-FIELDS.                                                        
      *                                                                         
           05 RECORD-COUNT             PIC S9(8)  COMP-3 VALUE ZERO.            
           05 SELECTED-RECORD-COUNT1   PIC S9(8)  COMP-3 VALUE ZERO.            
BD1012     05 SELECTED-RECORD-COUNT2   PIC S9(8)  COMP-3 VALUE ZERO.            
      *                                                                         
       01  SWITCHES.                                                            
      *                                                                         
           05  SW-IN-FILE1-EOF         PIC  X      VALUE 'N'.                   
               88 IN-FILE1-EOF                     VALUE 'Y'.                   
      *                                                                         
      ********************************************************                  
      * INPUT RECORD LAYOUT                                                     
      ********************************************************                  
      *                                                                         
       01  WS-INPUT-RECORD1.                                                    
                                                                                
           05 PRD-DIM-KY-ID-IN           PIC X(30) .                            
           05 TM-DIM-KY-DTE-IN           PIC X(10) .                            
           05 PHY-SITE-ID-IN             PIC S9(4) COMP.                        
           05 PRCHG-TYP-CDE-IN           PIC X(1) .                             
           05 PRCHG-CTG-CDE-IN           PIC X(1) .                             
           05 PRCHG-RSN-CDE-IN           PIC X(3) .                             
BD1012     05 PRCHG-MKDN-CDE-IN          PIC X(01).                             
BD1012     05 PRCHG-ORIG-CLR-DTE-IN      PIC X(10).                             
BD1012     05 PRCHG-OLD-UNT-RTL-AMT-IN   PIC S9(07)V9(2) COMP-3.                
BD1012     05 PRCHG-NEW-UNT-RTL-AMT-IN   PIC S9(07)V9(2) COMP-3.                
           05 PRCHG-UNT-QTY-IN           PIC S9(9) COMP.                        
           05 PRCHG-AMT-IN               PIC S9(9)V9(2) COMP-3.                 
MAF212     05 PRCHG-FIN-LIAB-DTE-IN      PIC X(10).                             
      *                                                                         
      ********************************************************                  
      * OUTPUT1 RECORD LAYOUT                                                   
      ********************************************************                  
39512 * REMOVE "_" DELIMITER CHARACTER FROM THE RECORD                          
      * DELIMITERS ON PRD-DIM-KY-ID-OUT '+' AND '.' ARE REMOVED                 
       01  WS-OUTPUT-RECORD1.                                                   
                                                                                
PG0100*    05 FILLER                     PIC X VALUE '+'.                       
           05 PRD-DIM-KY-ID-OUT          PIC X(30).                             
PG0100*    05 FILLER                     PIC X VALUE '.'.                       
           05 FILLER                     PIC X VALUE ','.                       
           05 TM-DIM-KY-DTE-OUT          PIC X(10).                             
           05 FILLER                     PIC X VALUE ','.                       
           05 PHY-SITE-ID-OUT            PIC +9(4).                             
           05 FILLER                     PIC X VALUE ','.                       
           05 PRCHG-TYP-CDE-OUT          PIC X(1).                              
           05 FILLER                     PIC X VALUE ','.                       
           05 PRCHG-CTG-CDE-OUT          PIC X(1).                              
           05 FILLER                     PIC X VALUE ','.                       
           05 PRCHG-RSN-CDE-OUT          PIC X(3).                              
BD1012     05 FILLER                     PIC X(3) VALUE ','.                    
BD1012     05 PRCHG-MKDN-CDE-OUT         PIC X(1).                              
BD1012     05 FILLER                     PIC X(3) VALUE ','.                    
BD1012     05 PRCHG-ORIG-CLR-DTE-OUT     PIC X(10).                             
BD1012     05 FILLER                     PIC X(2) VALUE ','.                    
BD1012     05 PRCHG-UNT-QTY-OUT          PIC +9(9).                             
BD1012     05 FILLER                     PIC X VALUE ','.                       
BD1012     05 PRCHG-AMT-OUT              PIC +9(9).9(2).                        
BD1012     05 FILLER                     PIC X VALUE ','.                       
BD1012     05 PRCHG-UNT-RTL-AMT-OUT      PIC +9(7).9(2).                        
MAF212     05 FILLER                     PIC X VALUE ','.                       
MAF212     05 PRCHG-FIN-LIAB-DTE-OUT     PIC X(10).                             
      *                                                                         
      ********************************************************                  
      * OUTPUT2 RECORD LAYOUT                                                   
      ********************************************************                  
      *                                                                         
BD1012 01  WS-OUTPUT-RECORD2.                                                   
           05 FILLER                     PIC X VALUE '+'.                       
           05 PRD-DIM-KY-ID-OUT2         PIC X(30).                             
           05 FILLER                     PIC X VALUE '.'.                       
           05 FILLER                     PIC X VALUE ','.                       
           05 PHY-SITE-ID-OUT2           PIC +9(4).                             
           05 FILLER                     PIC X VALUE ','.                       
           05 FILLER                     PIC X VALUE '_'.                       
           05 PRCHG-ORIG-CLR-DTE-OUT2    PIC X(10).                             
           05 FILLER                     PIC X VALUE '_'.                       
           05 FILLER                     PIC X VALUE ','.                       
           05 PRCHG-ORIG-UNT-RTL-OUT2    PIC +9(7).9(2).                        
      *                                                                         
      ********************************************************                  
      * PROCEDURE DIVISION:                                                     
      ********************************************************                  
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *                                                                         
       0000-MAINLINE-SECTION.                                                   
      *                                                                         
           OPEN INPUT IN-FILE1                                                  
                OUTPUT OUT-FILE1                                                
BD1012          OUTPUT OUT-FILE2.                                               
                                                                                
PG0100     DISPLAY '   '.                                                       
PG0100     DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 1000-INITIALIZE.                                             
           PERFORM 1100-PREPARE-OUTPUT-RECORDS                                  
               UNTIL IN-FILE1-EOF.                                              
                                                                                
           CLOSE IN-FILE1                                                       
                 OUT-FILE1                                                      
BD1012           OUT-FILE2.                                                     
                                                                                
PG0100     DISPLAY '   '.                                                       
PG0100     DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
                                                                                
PG0100     DISPLAY '   PRCHG NONTEXT RECS IN: ' RECORD-COUNT.                   
PG0100     DISPLAY '   PRCHG LOAD RECS OUT:   ' SELECTED-RECORD-COUNT1.         
BD1012     DISPLAY '   ORIG CLR LOAD RECS OUT:' SELECTED-RECORD-COUNT2.         
                                                                                
PG0100     GOBACK.                                                              
      *                                                                         
       1000-INITIALIZE.                                                         
      *                                                                         
           MOVE 0 TO RECORD-COUNT.                                              
           MOVE 0 TO SELECTED-RECORD-COUNT1.                                    
BD1012     MOVE 0 TO SELECTED-RECORD-COUNT2.                                    
      *                                                                         
       1100-PREPARE-OUTPUT-RECORDS.                                             
      *                                                                         
           PERFORM 1200-READ-IN-FILE1.                                          
           INITIALIZE WS-OUTPUT-RECORD1.                                        
           IF NOT IN-FILE1-EOF                                                  
      *                                                                         
              MOVE PRD-DIM-KY-ID-IN TO PRD-DIM-KY-ID-OUT                        
              MOVE TM-DIM-KY-DTE-IN TO TM-DIM-KY-DTE-OUT                        
              MOVE PHY-SITE-ID-IN TO PHY-SITE-ID-OUT                            
              MOVE PRCHG-TYP-CDE-IN TO PRCHG-TYP-CDE-OUT                        
              MOVE PRCHG-CTG-CDE-IN TO PRCHG-CTG-CDE-OUT                        
              MOVE PRCHG-RSN-CDE-IN TO PRCHG-RSN-CDE-OUT                        
BD1012        MOVE PRCHG-MKDN-CDE-IN TO PRCHG-MKDN-CDE-OUT                      
BD1012        MOVE PRCHG-ORIG-CLR-DTE-IN TO PRCHG-ORIG-CLR-DTE-OUT              
BD1012        MOVE PRCHG-NEW-UNT-RTL-AMT-IN TO PRCHG-UNT-RTL-AMT-OUT            
              MOVE PRCHG-UNT-QTY-IN TO PRCHG-UNT-QTY-OUT                        
              MOVE PRCHG-AMT-IN TO PRCHG-AMT-OUT                                
MAF212        MOVE PRCHG-FIN-LIAB-DTE-IN TO  PRCHG-FIN-LIAB-DTE-OUT             
              PERFORM 2000-PRINT-OUTPUT-RECORD1                                 
BD1012********************************************************                  
BD1012* ONLY WRITE TO OUTPUT FILE 2 FOR THE 1ST MARKDOWN TO                     
BD1012* CAPTURE THE ORIGINAL CLEARANCE DATE AND ORIG UNIT RTL                   
BD1012* FOR SAS OPTIMIZATION MARKDOWNS                                          
BD1012********************************************************                  
BD1012        IF PRCHG-RSN-CDE-IN = '021' OR PRCHG-RSN-CDE-IN = '024'           
BD1012          MOVE PRD-DIM-KY-ID-IN TO PRD-DIM-KY-ID-OUT2                     
BD1012          MOVE PHY-SITE-ID-IN TO PHY-SITE-ID-OUT2                         
BD1012          MOVE PRCHG-ORIG-CLR-DTE-IN TO PRCHG-ORIG-CLR-DTE-OUT2           
BD1012          MOVE PRCHG-OLD-UNT-RTL-AMT-IN TO PRCHG-ORIG-UNT-RTL-OUT2        
BD1012          PERFORM 2100-PRINT-OUTPUT-RECORD2                               
BD1012        END-IF                                                            
BD1012     END-IF.                                                              
      *                                                                         
       1200-READ-IN-FILE1.                                                      
      *                                                                         
           READ IN-FILE1 INTO WS-INPUT-RECORD1                                  
             AT END                                                             
                MOVE 'Y' TO SW-IN-FILE1-EOF.                                    
           IF NOT IN-FILE1-EOF                                                  
                ADD 1 TO RECORD-COUNT.                                          
      *                                                                         
       2000-PRINT-OUTPUT-RECORD1.                                               
      *                                                                         
           WRITE OUTPUT-RECORD1 FROM WS-OUTPUT-RECORD1.                         
           ADD 1 TO SELECTED-RECORD-COUNT1.                                     
      *                                                                         
BD1012 2100-PRINT-OUTPUT-RECORD2.                                               
BD1012*                                                                         
BD1012     WRITE OUTPUT-RECORD2 FROM WS-OUTPUT-RECORD2.                         
BD1012     ADD 1 TO SELECTED-RECORD-COUNT2.                                     
      *                                                                         
