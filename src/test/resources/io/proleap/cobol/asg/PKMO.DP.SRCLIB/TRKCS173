000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    TRKCS173.                                                 
000300 AUTHOR.        DENNIS STEFFEN - OMNI RESOURCES.                          
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  JANUARY, 1999.                                            
000600 DATE-COMPILED.                                                           
000700                                                                          
000800*    Y173 - CUSTOMS BILL OF LADING LIST                         *         
263697*---------------------------------------------------------------*         
263697* CHANGED 11/08/2021    BY JIM HUNTER      WR: CHG0263697                 
263697* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263697* MAKING THE CALL DYNAMIC VS STATIC.                                      
263697*---------------------------------------------------------------*         
CICS  * CHANGED  1/12/2023    BY JIM HUNTER      WR: CHG0278495                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
000900                                                                          
001000 ENVIRONMENT DIVISION.                                                    
001100                                                                          
001200 DATA DIVISION.                                                           
001300                                                                          
001400 WORKING-STORAGE SECTION.                                                 
001500                                                                          
001600 01  DK-DB2-KEYS.                                                         
001700     15  DK-CSTM-ENTRY-ID               PIC  X(12)  VALUE SPACE.          
001710     15  DK-ENT-ID                      PIC  S9(09) COMP                  
001720                                             VALUE ZERO.                  
001800                                                                          
001900 01  PC-PROGRAM-CONSTANTS.                                                
002000     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
002100                                                    'TR173A  '.           
002200     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
002300                                                    'TRKM173 '.           
002400     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
002500                                                    'TRKCS173'.           
002600     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
002700     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
002800     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
002900     05  PC-LIST-FORMAT                 PIC  X(01)  VALUE '2'.            
003000     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +8              
003100                                                    COMP SYNC.            
003200     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
003300                                                    COMP SYNC.            
003400     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
003500                                                    +1000000              
003600                                                    COMP-3.               
003700     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
003800                                                    COMP SYNC.            
003900     05  PC-PERCENT                     PIC  X(01)  VALUE '%'.            
003901     05  PC-INIT-DATE                   PIC  X(10)  VALUE                 
003910                                                    '0001-01-01'.         
004000     05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
004100     05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.          
004200     05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.          
004300     05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
004301     05  PC-MAX-RETRIES                 PIC S9(04)  VALUE  +3           99
004310                                                    COMP.               99
004400*                                                                         
004500     05  PC-TSYMSG-NUMBERS.                                               
004600         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
004700         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
004800         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
004900         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
005000         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
005100         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
005200         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
005300         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
005400         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
005500         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
005600         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
005700         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
005800         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
005900         10  PC-TSYMSG-00551            PIC  9(05)  VALUE 00551.          
006000         10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
006100*                                                                         
006200 01  PS-PROGRAM-SWITCHES.                                                 
006300     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
006400         88  PS-NO-ERROR                            VALUE 'Y'.            
006500         88  PS-ERROR                               VALUE 'N'.            
006600     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
006700         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
006800         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
006900     05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.            
007000         88  PS-DONE                                VALUE 'Y'.            
007100         88  PS-NOT-DONE                            VALUE 'N'.            
007200*                                                                         
007300 01  PV-PROGRAM-VARIABLES.                                                
007310                                                                          
007400     05  PV-SUB                         PIC S9(04)  VALUE +0              
007500                                                    COMP SYNC.            
007600     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
007700                                                    COMP SYNC.            
007800     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
007900                                                    COMP-3.               
008000     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
008100                                                    COMP-3.               
008200     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
008300                                                    COMP-3.               
008400     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
008500                                                    COMP-3.               
008600     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
008700                                                    COMP-3.               
008800     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
008900                                                    COMP-3.               
009000     05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
009100                                                    COMP-3.               
009200     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
009300                                                    COMP SYNC.            
009400     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
009500                                                    COMP SYNC.            
009600     05  PV-ITEM                        PIC S9(04)  VALUE +0              
009700                                                    COMP SYNC.            
009800     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
009900                                                    COMP SYNC.            
010000     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
010100                                                    COMP SYNC.            
010200     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
010300                                                    COMP SYNC.            
010400     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
010500                                                    COMP SYNC.            
010600     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
010700                                                    COMP SYNC.            
010800     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
010900                                                    COMP SYNC.            
011000     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
011100                                                    COMP SYNC.            
011200     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
011300                                                    COMP SYNC.            
011400     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
011500                                                    COMP SYNC.            
011600     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
011700                                                    COMP SYNC.            
011800     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
011900                                                    COMP SYNC.            
012000     05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
012100                                                    COMP SYNC.            
012110     05  PV-NULL-IND                    PIC S9(04)  VALUE +0              
012120                                                    COMP SYNC.            
012121     05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0                  99
012130                                              COMP SYNC.                99
012200******************************************************************        
012300**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
012400**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
012500**  APPLICATION-SPECIFIC COMMAREA.                              **        
012600******************************************************************        
012700 01  LQW-LIST-QUEUE-WORK-AREA.                                            
012800     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
012900     05  LQW-ITEM-ENTRIES               OCCURS  8 TIMES                   
013000                                        INDEXED BY LQW-IDX.               
013100         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
013200         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
013300         10  LQW-SELECTED-SW            PIC  X(01).                       
013400             88  LQW-SELECT                         VALUE 'S'.            
013500             88  LQW-DESELECT                       VALUE ' '.            
013600         10  LQW-ACTION                 PIC  X(03).                       
013700         10  LQW-MASTER-BOL-NUMBER      PIC  X(30).                       
013800         10  LQW-HOUSE-BOL-NUMBER       PIC  X(30).                       
013810         10  LQW-LAST-TRN               PIC  X(05).                       
013900                                                                          
014000     EJECT                                                                
014100*---------------------------------------------------------------*         
014200*  RECORD LAYOUT FOR SELECTION TEMP STORAGE QUEUE.                        
014300*---------------------------------------------------------------*         
014400                                                                          
014500     COPY TRWS112.                                                        
014600                                                                          
014700     COPY TRKM173.                                                        
014800*                                                                         
014900 01  MR-OCCURS-LAYOUT                   REDEFINES  TR173AO.               
015000     05  FILLER                         PIC X(322).                       
015100     05  MR-SCROLL-AREA                 OCCURS  8 TIMES.                  
015200         10  MR-SELECTL                 PIC S9(04) COMP.                  
015300         10  MR-SELECTA                 PIC  X(01).                       
015400         10  MR-SELECTC                 PIC  X(01).                       
015500         10  MR-SELECTH                 PIC  X(01).                       
015600         10  MR-SELECTI                 PIC  X(01).                       
015700                                                                          
015800         10  MR-ACTIONL                 PIC S9(04) COMP.                  
015900         10  MR-ACTIONA                 PIC  X(01).                       
016000         10  MR-ACTIONC                 PIC  X(01).                       
016100         10  MR-ACTIONH                 PIC  X(01).                       
016200         10  MR-ACTION                  PIC  X(03).                       
016300                                                                          
016400         10  MR-MASTER-BOL-NUMBERL      PIC S9(04) COMP.                  
016500         10  MR-MASTER-BOL-NUMBERA      PIC  X(01).                       
016600         10  MR-MASTER-BOL-NUMBERC      PIC  X(01).                       
016700         10  MR-MASTER-BOL-NUMBERH      PIC  X(01).                       
016800         10  MR-MASTER-BOL-NUMBER       PIC X(30).                        
016900                                                                          
017000         10  MR-HOUSE-BOL-NUMBERL       PIC S9(04) COMP.                  
017100         10  MR-HOUSE-BOL-NUMBERA       PIC  X(01).                       
017200         10  MR-HOUSE-BOL-NUMBERC       PIC  X(01).                       
017300         10  MR-HOUSE-BOL-NUMBERH       PIC  X(01).                       
017400         10  MR-HOUSE-BOL-NUMBER        PIC X(30).                        
017500                                                                          
017510         10  MR-LAST-TRNL               PIC S9(04) COMP.                  
017520         10  MR-LAST-TRNA               PIC  X(01).                       
017530         10  MR-LAST-TRNC               PIC  X(01).                       
017540         10  MR-LAST-TRNH               PIC  X(01).                       
017550         10  MR-LAST-TRN                PIC  X(05).                       
017560/                                                                         
017600*---------------------------------------------------------------*         
017700*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
017800*---------------------------------------------------------------*         
017900                                                                          
018000     COPY DPWS015.                                                        
018100                                                                          
018200*---------------------------------------------------------------*         
018300*    FUNCTION KEYS COPYBOOK.                                    *         
018400*---------------------------------------------------------------*         
018500                                                                          
018600     COPY DPWS016.                                                        
018700                                                                          
018800*---------------------------------------------------------------*         
018900*    NUMERIC EDIT LAYOUT.                                       *         
019000*---------------------------------------------------------------*         
019100                                                                          
019200     COPY DPWS010I.                                                       
019300 EJECT                                                                    
019400*---------------------------------------------------------------*         
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
019600*---------------------------------------------------------------*         
019700                                                                          
019800     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
019900 EJECT                                                                    
020000*---------------------------------------------------------------*         
020100*    STANDARD COMMAREA.                                         *         
020200*---------------------------------------------------------------*         
020300                                                                          
020400     COPY DPWS020.                                                        
020500     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
020600         10  INTER-APPL-COMM-AREA       PIC X(200).                       
020700                                                                          
020800     COPY TRWS114.                                                        
020900                                                                          
021000*---------------------------------------------------------------*         
021100*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
021200*---------------------------------------------------------------*         
021300         10  ASC-SPECIFIC-COMMAREA.                                       
021400             15  ASC-USE-SELECTION-QUEUE-IND     PIC X(01).               
021500                 88  ASC-USE-SELECTION-QUEUE    VALUE 'Y'.                
021600             15  ASC-IN-SEL-QUEUE-NAME           PIC X(08).               
021700             15  ASC-NUMBER-OF-TS-ITEMS    PIC S9(05) COMP-3.     .       
021800             15  ASC-LIST-KEYS.                                           
021900                 20  ASC-KEY-CSTM-ENTRY-ID                                
022000                                      PIC X(12).                          
022100                 20  ASC-KEY-ENT-ID   PIC S9(9) COMP.                     
022200             15  ASC-SEL-SUB               PIC S9(04) COMP.               
022300             15  ASC-SEL-ITEM              PIC S9(04) COMP.               
022400             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
022500                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
022600                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
022700                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
022800                                                                          
022900                 20  ASC-ITEM-ARRAY.                                      
023000                     25  ASC-ITEM-ENTRIES OCCURS  8 TIMES.                
023100                         30  ASC-LIST-ITEM-NUMBER                         
023200                                                     PIC S9(09)           
023300                                                     COMP-3.              
023400                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
023500                             88  ASC-VALID-SELECT    VALUE ' '            
023600                                                           'S'            
023700                                                           'B'            
023800                                                           'E'.           
023900                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
024000                             88  ASC-TEMP-SELECT     VALUE 'S'.           
024100                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
024200                             88  ASC-TEMP-END        VALUE 'E'.           
024300                         30  ASC-SELECTED-SW         PIC  X(01).          
024400                             88  ASC-NO-SELECT       VALUE ' '.           
024500                             88  ASC-SELECT          VALUE 'S'.           
024600                             88  ASC-BEGIN           VALUE 'B'.           
024700                             88  ASC-END             VALUE 'E'.           
024800                         30  ASC-ACTION              PIC X(03).           
024900                         30  ASC-MASTER-BOL-NUMBER   PIC X(30).           
025000                         30  ASC-HOUSE-BOL-NUMBER    PIC X(30).           
025010                         30  ASC-LAST-TRN            PIC X(05).           
025100                                                                          
025200             15  ASC-BLK-SUB                         PIC S9(04)           
025300                                                          COMP            
025400                                                          SYNC.           
025500             15  ASC-ITEM-SUB                        PIC S9(04)           
025600                                                          COMP            
025700                                                          SYNC.           
025800             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
025900                                                          COMP-3.         
026000             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
026100                                                          COMP-3.         
026200             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
026300                                                          COMP-3.         
026400             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
026500                                                          COMP-3.         
026600             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
026700                                                          COMP-3.         
026800             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
026900                                                          COMP            
027000                                                          SYNC.           
027100             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
027200                                                          COMP-3.         
027300             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
027400                                                          COMP-3.         
027500             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
027600                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
027700                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
027800             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
027900                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
028000                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
028100             15  ASC-LIST-QUEUE-NAME.                                     
028200                 20  ASC-LIST-TERM-ID                PIC  X(04).          
028300                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
028400                                                          COMP-3.         
028500                 20  ASC-LIST-SEQ                    PIC  9(01).          
028600             15  ASC-SEL-QUEUE-NAME.                                      
028700                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
028800                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
028900                                                          COMP-3.         
029000                 20  ASC-SEL-SEQ                     PIC  9(01).          
029100                                                                          
029200             15  ASC-CURSOR-SW                       PIC  X(01).          
029300                 88  ASC-NO-CURSOR                   VALUE 'Z'.           
029400                 88  ASC-SYSTEM-ID-CURSOR            VALUE 'S'.           
029500                 88  ASC-MESSAGE-NUMBER-CURSOR       VALUE 'N'.           
029600                 88  ASC-MESSAGE-TEXT-CURSOR         VALUE 'T'.           
029700             15  ASC-START-ITEM-REQUEST.                                  
029800                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
029900             15  ASC-ENTR-TYP-CDE     PIC X(30).                          
029901             15  ASC-MOM-TRIP-TYP-CDE  PIC X(01).                         
029910             15  ASC-FILD-DATE        PIC X(10).                          
030000             15  ASC-REJ-DATE         PIC X(10).                          
030100             15  ASC-REJ-DESC         PIC X(30).                          
030200             15  ASC-EST-CLR-DATE     PIC X(10).                          
030210             15  ASC-ACTL-CLR-DATE    PIC X(10).                          
030300             15  ASC-EXM-DATE         PIC X(10).                          
030400             15  ASC-LQDN-DATE        PIC X(10).                          
030500             15  ASC-LQDN-SSPNN-DATE  PIC X(10).                          
030600             15  ASC-LQDN-SSPNN-DESC  PIC X(30).                          
030700             15  ASC-BKR-REF-NBR      PIC X(15).                          
030710             15  ASC-POE-CODE         PIC X(04).                          
030800     EJECT                                                                
030900*---------------------------------------------------------------*         
031000*    ABEND PROCESSING COPYBOOK.                                 *         
031100*---------------------------------------------------------------*         
031200                                                                          
031300     COPY DPWS013.                                                        
031400                                                                          
031500/                                                                         
031600     EXEC SQL                                                             
031700          INCLUDE TIMCSEN                                                 
031800     END-EXEC.                                                            
031900/                                                                         
032000     EXEC SQL                                                             
032100          INCLUDE TIMCSBL                                                 
032200     END-EXEC.                                                            
032300/                                                                         
032400     EXEC SQL                                                             
032500          INCLUDE TENTNME                                                 
032600     END-EXEC.                                                            
032700/                                                                         
032704     EXEC SQL                                                     DPKCS036
032705          INCLUDE TIMPOE                                          DPKCS036
032710     END-EXEC.                                                    DPKCS036
      /                                                                         
           EXEC SQL                                                     DPKCS036
                INCLUDE TICDVAL                                         DPKCS036
           END-EXEC.                                                    DPKCS036
032800*---------------------------------------------------------------*         
032900*    DB2 AREA FOR COMMUNICATIONS                                *         
033000*---------------------------------------------------------------*         
033100     EXEC SQL                                                             
033200          INCLUDE SQLCA                                                   
033300     END-EXEC.                                                            
033400                                                                          
033500                                                                          
033600     EXEC SQL                                                             
033700          DECLARE BOL-CSR CURSOR                                          
033800          FOR SELECT                                                      
033900                MSTR_BOL_NBR                                              
034000              , HSE_BOL_NBR                                               
034010              , LAST_UPD_TMST                                             
034100          FROM                                                            
034200                TIMCSBL                                                   
034300          WHERE                                                           
034400                CSTM_ENTR_ID   = :DK-CSTM-ENTRY-ID                        
034500     END-EXEC.                                                            
034600                                                                          
034601*---------------------------------------------------------------* 03880099
034602*    CURSOR DECLARATION FOR SELECTING POE CODE.                 *         
034603*---------------------------------------------------------------* 03900099
034604                                                                  03910099
034605     EXEC SQL                                                     03920099
034606          DECLARE  POE-CSR CURSOR                                 03930099
034607              FOR  SELECT DISTINCT                                03940099
034608                   POE_CDE                                                
034609             FROM  TIMPOE                                                 
034610            WHERE  POE_STD_EDI_CDE = :ASC-POE-CODE                        
034611              AND  STAT_CDE        = 'A'                                  
034612         ORDER BY  POE_CDE                                                
034613     END-EXEC.                                                            
034614                                                                          
034615 EJECT                                                            04110099
034620                                                                          
034700 LINKAGE SECTION.                                                         
034800                                                                          
034900 01  DFHCOMMAREA.                                                         
035000     05  FILLER                         OCCURS  1 TO 4072 TIMES           
035100                                        DEPENDING ON EIBCALEN.            
035200         10  FILLER                     PIC X.                            
035300                                                                          
035400/                                                                         
035500 PROCEDURE DIVISION.                                                      
035600                                                                          
035700 0000-MAIN-MODULE.                                                        
035800     INITIALIZE DP030-CICS-API-FIELDS.                                    
035900     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
036000     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
036100     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
036200     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
036300     MOVE LENGTH OF TR173AI        TO DP030-MAP-LENGTH (1).               
036400     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
036500     PERFORM 0001-CALL-CICS-ARCH-API.                                     
036600*                                                                         
036700     PERFORM 1000-CONTROL-PROCESSING                                      
036800*                                                                         
036900     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
037000     PERFORM 0001-CALL-CICS-ARCH-API.                                     
037100                                                                          
037200 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
037400                                    DFHCOMMAREA                           
037500                                    DP030-CICS-API-FIELDS                 
037600                                    DP020-STANDARD-COMMAREA               
037700                                    TR173AI.                              
037800*                                                                         
037900     IF  DP030-RC-CALL-SUCCESSFUL                                         
038000         CONTINUE                                                         
038100     ELSE                                                                 
038200         SET DP013-NO-ROLLBACK                                            
038300             DP013-XCTL-DISPLAY-RESTART                                   
038400             DP013-CICS-ABEND      TO TRUE                                
038500         MOVE '0001-CALL-CICS-ARCH-API'                                   
038600                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
038900         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
039000         PERFORM DP013-0000-PROCESS-ABEND                                 
039100     END-IF.                                                              
039200*----------------------------------------------------------------*        
039300* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
039400* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
039500*                                                                *        
039600* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
039700*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
039800*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
039900*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
040000*----------------------------------------------------------------*        
040100                                                                          
040200 1000-CONTROL-PROCESSING.                                                 
040300                                                                          
040400     EVALUATE TRUE                                                        
040500         WHEN DP020-NEXT-ACT-INITIAL                                      
040600             INITIALIZE ASC-SPECIFIC-COMMAREA                             
040700             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
040800                                      ASC-SEL-TERM-ID                     
040900             MOVE DP020-SRC-TASK-NUMBER                                   
041000                                   TO ASC-LIST-TASK-NUMBER                
041100                                      ASC-SEL-TASK-NUMBER                 
041200             MOVE PC-LIST-QUEUE-SEQ                                       
041300                                   TO ASC-LIST-SEQ                        
041400             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
041500             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
041600             IF  PS-COMMAREA-NOT-VALID                                    
041700                 SET DP020-NEXT-ACT-APPL-ERROR                            
041800                                   TO TRUE                                
041900             ELSE                                                         
042000                 PERFORM 4000-BUILD-INITIAL-PANEL                         
042100             END-IF                                                       
042200                                                                          
042300                                                                          
042400         WHEN DP020-NEXT-ACT-READ-MAP                                     
042500             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
042600                                   TO PV-TOP-ITEM                         
042700             PERFORM 2000-PROCESS-PANEL                                   
042800                                                                          
042900         WHEN DP020-NEXT-ACT-RETURN                                       
043000             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
043100                                   TO PV-TOP-ITEM                         
043200             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
043300             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
043400             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
043500             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
043600                                                                          
043700         WHEN OTHER                                                       
043800             SET DP013-LOGIC-ABEND TO TRUE                                
043900             SET DP013-NO-ROLLBACK TO TRUE                                
044000             MOVE '1000-CONTROL-PROCESSING'                               
044100                                   TO DP013-PARAGRAPH                     
044200             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
044300                                   TO DP013-MESSAGE-TEXT(1)               
044400             PERFORM DP013-0000-PROCESS-ABEND                             
044500     END-EVALUATE.                                                        
044600                                                                          
044700 EJECT                                                                    
044800*----------------------------------------------------------------*        
044900* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
045000* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
045100*----------------------------------------------------------------*        
045200                                                                          
045300 1100-PROCESS-INTER-APPL-COMM.                                            
045400                                                                          
045500     SET PS-COMMAREA-VALID         TO TRUE.                               
045600     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
045700     EVALUATE TRUE                                                        
045800     WHEN  DP020-INT-APPL-FORMAT-IND = PC-CRIT-SPEC-FORMAT                
045900         MOVE TR114-CSTM-ENTRY-ID     TO ASC-KEY-CSTM-ENTRY-ID            
045910         MOVE TR114-ENT-ID            TO ASC-KEY-ENT-ID                   
046000     WHEN  DP020-INT-APPL-FORMAT-IND = PC-LIST-FORMAT                     
046100         IF TR114-NUMBER-OF-SELECTED-ITEMS > ZERO                         
046200             SET ASC-USE-SELECTION-QUEUE                                  
046300                           TO TRUE                                        
046400             MOVE TR114-NUMBER-OF-SELECTED-ITEMS                          
046500                           TO ASC-NUMBER-OF-SELECTED-ITEMS                
046600             MOVE TR114-NUMBER-OF-TS-ITEMS                                
046700                           TO ASC-NUMBER-OF-TS-ITEMS                      
046800             MOVE TR114-SEL-QUEUE-NAME                                    
046900                           TO ASC-IN-SEL-QUEUE-NAME                       
047000             PERFORM 1120-PREPARE-LIST-ENTRY                              
047100         END-IF                                                           
047200     WHEN DP020-NAV-KEY-DATA > SPACE                                      
047300             MOVE DP020-NAV-KEY-DATA                                      
047400                                   TO DK-CSTM-ENTRY-ID                    
047500                                      ASC-KEY-CSTM-ENTRY-ID               
047600     END-EVALUATE.                                                        
047700                                                                          
047800 1120-PREPARE-LIST-ENTRY.                                                 
047900     MOVE TR112-MAX-ITEMS          TO ASC-SEL-SUB.                        
048000     MOVE ZERO                     TO ASC-SEL-ITEM.                       
048100     PERFORM 2160-GET-NEXT-RECORD.                                        
048200 EJECT                                                                    
048300*----------------------------------------------------------------*        
048400* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
048500* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
048600* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
048700* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
048800* FUNCTION KEYS CAN GET THIS FAR.                                *        
048900*----------------------------------------------------------------*        
049000                                                                          
049100 2000-PROCESS-PANEL.                                                      
049200     EVALUATE TRUE                                                        
049300         WHEN DP020-SRC-AID = DP016-CLEAR                                 
049400             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
049500             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
049600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
049700                                                                          
049800         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
049900             MOVE ASC-KEY-CSTM-ENTRY-ID                                   
050000                                   TO DK-CSTM-ENTRY-ID                    
050010             MOVE ASC-KEY-ENT-ID                                          
050020                                   TO DK-ENT-ID                           
050100             PERFORM 2050-DELETE-LIST-QUEUE                               
050200             PERFORM 4000-BUILD-INITIAL-PANEL                             
050300                                                                          
050400         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
050500             PERFORM 2140-DESELECT-ALL-ITEMS                              
050600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
050700                                                                          
050800         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
050900             PERFORM 2150-SELECT-ALL-ITEMS                                
051000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
051100                                                                          
051200         WHEN OTHER                                                       
051300             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
051400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
051500             IF  PS-NO-ERROR                                              
051600                 SET DP030-CONTINUE-GO                                    
051700                                 TO TRUE                                  
051800                 PERFORM 2100-CHECK-FUNCTION-KEY                          
051900             ELSE                                                         
052000                 SET DP030-OVERRIDE-GO                                    
052100                                 TO TRUE                                  
052200             END-IF                                                       
052300     END-EVALUATE.                                                        
052400 EJECT                                                                    
052500*----------------------------------------------------------------*        
052600*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
052700*----------------------------------------------------------------*        
052800 2050-DELETE-LIST-QUEUE.                                                  
052900                                                                          
053000     EXEC CICS                                                            
053100         DELETEQ TS                                                       
053200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
053300             RESP  (PV-CICS-RESP)                                         
053400     END-EXEC.                                                            
053500                                                                          
053600     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
053700        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
053800         CONTINUE                                                         
053900     ELSE                                                                 
054000         SET DP013-CICS-ABEND      TO TRUE                                
054100         SET DP013-NO-ROLLBACK     TO TRUE                                
054200                                                                          
054300         MOVE '2050-DELETE-LIST-QUEUE'                                    
054400                                   TO DP013-PARAGRAPH                     
054500         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
054600                                   TO DP013-MESSAGE-TEXT (1)              
054700         PERFORM DP013-0000-PROCESS-ABEND                                 
054800     END-IF.                                                              
054900                                                                          
055000     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
055100 EJECT                                                                    
055200*----------------------------------------------------------------*        
055300* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
055400* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
055500* CICS ARCHITECTURE API.                                         *        
055600*----------------------------------------------------------------*        
055700 2100-CHECK-FUNCTION-KEY.                                                 
055800     EVALUATE TRUE                                                        
055900         WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                          
056000             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
056100                 INITIALIZE TR114-INTER-APPL-COMMAREA                     
056200                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
056300                                TO TR114-NUMBER-OF-SELECTED-ITEMS         
056400                 PERFORM 5000-PREPARE-SEL-QUEUE                           
056500             ELSE                                                         
056600                 SET DP030-OVERRIDE-GO                                    
056700                                   TO TRUE                                
056800                 MOVE PC-TSYMSG-00057                                     
056900                                   TO DP020-MSG-NUMBER                    
057000                 SET DP020-MSG-WARNING                                    
057100                                   TO TRUE                                
057200             END-IF                                                       
057300                                                                          
057400         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
057500             MOVE +1               TO PV-TOP-ITEM                         
057600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
057700                                                                          
057800         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
057900             PERFORM 2110-BROWSE-BACKWARD                                 
058000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
058100                                                                          
058200         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
058300             PERFORM 2120-BROWSE-FORWARD                                  
058400             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
058500                                                                          
058600         WHEN DP020-SRC-AID = DP016-ENTER                                 
058700             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
058800             IF  PS-ERROR                                                 
058900                 CONTINUE                                                 
059000             ELSE                                                         
059100                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
059200             END-IF                                                       
059300                                                                          
059400         WHEN OTHER                                                       
059500             SET DP013-NO-ROLLBACK                                        
059600                 DP013-XCTL-DISPLAY-RESTART                               
059700                 DP013-CICS-ABEND  TO TRUE                                
059800             MOVE '2100-CHECK-FUNCTION-KEY'                               
059900                                   TO DP013-PARAGRAPH                     
060000             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
060100                                   TO DP013-MESSAGE-TEXT (1)              
060200             PERFORM DP013-0000-PROCESS-ABEND                             
060300     END-EVALUATE.                                                        
060400*                                                                         
060500*----------------------------------------------------------------*        
060600*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
060700*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
060800*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
060900*    MESSAGE.                                                    *        
061000*----------------------------------------------------------------*        
061100                                                                          
061200 2110-BROWSE-BACKWARD.                                                    
061300     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
061400         GIVING PV-TOP-ITEM.                                              
061500*                                                                         
061600     IF  ((PV-TOP-ITEM < +1)                                              
061700       OR (PV-TOP-ITEM = +1))                                             
061800         MOVE +1                   TO PV-TOP-ITEM                         
061900*        -- TOP OF LIST --                                                
062000         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
062100         SET DP020-MSG-INFORMATIONAL TO TRUE                              
062200     END-IF.                                                              
062300                                                                          
062400 EJECT                                                                    
062500*----------------------------------------------------------------*        
062600*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
062700*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
062800*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
062900*----------------------------------------------------------------*        
063000                                                                          
063100 2120-BROWSE-FORWARD.                                                     
063200                                                                          
063300     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
063400         GIVING PV-TOP-ITEM.                                              
063500     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
063600         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
063700                                   TO PV-TOP-ITEM                         
063800*        -- BOTTOM OF LIST --                                             
063900         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
064000         SET DP020-MSG-INFORMATIONAL                                      
064100                                   TO TRUE                                
064200     ELSE                                                                 
064300         COMPUTE PV-BOTTOM-ITEM =                                         
064400             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
064500         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
064600*            -- BOTTOM OF LIST --                                         
064700             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
064800             SET DP020-MSG-INFORMATIONAL                                  
064900                                   TO TRUE                                
065000         END-IF                                                           
065100     END-IF.                                                              
065200 EJECT                                                                    
065300*----------------------------------------------------------------*        
065400*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
065500*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
065600*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
065700*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
065800*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
065900*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
066000*----------------------------------------------------------------*        
066100                                                                          
066200 2130-CHECK-FOR-JUMP-BROWSE.                                              
066300     SET PS-NO-ERROR               TO TRUE.                               
066400     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
066500         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
066600                                   TO PV-TOP-ITEM                         
066700     ELSE                                                                 
066800         MOVE ASC-START-ITEM-REQUEST                                      
066900                                   TO DP010I-UNEDITED-FIELD               
067000         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
067100         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
067200         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
067300                                   TO TRUE                                
263697         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263697              USING DP010I-NUMERIC-EDIT-AREA                              
067500                                                                          
067600         IF  NOT DP010I-ERROR-DETECTED                                    
067700             MOVE DP010I-NUMERIC-FIELD                                    
067800                                   TO  ASC-START-ITEM-REQUEST-N           
067900             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
068000                 IF  ASC-START-ITEM-REQUEST-N >                           
068100                                          ASC-NUMBER-OF-ITEMS-READ        
068200                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
068300                     COMPUTE PV-TOP-ITEM =                                
068400                             ASC-NUMBER-OF-ITEMS-READ                     
068500                             - PC-ITEMS-PER-PANEL + 1                     
068600                     IF  PV-TOP-ITEM < +1                                 
068700                         MOVE +1   TO PV-TOP-ITEM                         
068800                         MOVE PC-TSYMSG-00070                             
068900                                   TO DP020-MSG-NUMBER                    
069000                         SET DP020-MSG-INFORMATIONAL                      
069100                                   TO TRUE                                
069200                     END-IF                                               
069300                 ELSE                                                     
069400                     MOVE ASC-START-ITEM-REQUEST-N                        
069500                                   TO PV-TOP-ITEM                         
069600                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
069700                     COMPUTE PV-BOTTOM-ITEM =                             
069800                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
069900                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
070000*                        -- BOTTOM OF LIST --                             
070100                         MOVE PC-TSYMSG-00070                             
070200                                   TO DP020-MSG-NUMBER                    
070300                         SET DP020-MSG-INFORMATIONAL                      
070400                                   TO TRUE                                
070500                     END-IF                                               
070600                 END-IF                                                   
070700             ELSE                                                         
070800*                --- VALUE OUT OF RANGE ----                              
070900                 MOVE PC-TSYMSG-00101                                     
071000                                   TO DP020-MSG-NUMBER                    
071100                 MOVE -1           TO ASTRTITL                            
071200                 SET DP030-SET-CURSOR-APPL-1                              
071300                                   TO TRUE                                
071400                 MOVE DP015-REVERSE                                       
071500                                   TO ASTRTITH                            
071600                 MOVE DP015-RED    TO ASTRTITC                            
071700                 MOVE DP015-UNP-NUM-BRT-MDT                               
071800                                   TO ASTRTITA                            
071900                 SET DP020-MSG-FATAL                                      
072000                                   TO TRUE                                
072100                 SET PS-ERROR      TO TRUE                                
072200             END-IF                                                       
072300                                                                          
072400         ELSE                                                             
072500             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
072600             MOVE -1               TO ASTRTITL                            
072700             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
072800             MOVE DP015-REVERSE    TO ASTRTITH                            
072900             MOVE DP015-RED        TO ASTRTITC                            
073000             MOVE DP015-UNP-NUM-BRT-MDT                                   
073100                                   TO ASTRTITA                            
073200             SET DP020-MSG-FATAL   TO TRUE                                
073300             SET PS-ERROR          TO TRUE                                
073400         END-IF                                                           
073500     END-IF.                                                              
073600 EJECT                                                                    
073700*----------------------------------------------------------------*        
073800*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
073900*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
074000*----------------------------------------------------------------*        
074100 2140-DESELECT-ALL-ITEMS.                                                 
074200     MOVE +1                       TO ASC-BLK-SUB.                        
074300                                                                          
074400     PERFORM 2141-CLEAR-SELECTED-INDS                                     
074500         VARYING ASC-ITEM-SUB                                             
074600         FROM 1 BY 1                                                      
074700         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
074800                                                                          
074900     ADD +1                        TO ASC-BLK-SUB.                        
075000                                                                          
075100     PERFORM 2141-CLEAR-SELECTED-INDS                                     
075200         VARYING ASC-ITEM-SUB                                             
075300         FROM 1 BY 1                                                      
075400         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
075500                                                                          
075600     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
075700                                   TO PV-LIST-ITEM-NUMBER.                
075800                                                                          
075900     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
076000         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
076100                                                                          
076200     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
076300        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
076400         MOVE +1                   TO ASC-BLK-SUB                         
076500                                                                          
076600         PERFORM 5100-WRITE-TEMP-STORAGE                                  
076700                                                                          
076800         MOVE +1                   TO PV-BLOCK-NUMBER                     
076900                                                                          
077000         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
077100             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
077200                                                                          
077300         PERFORM                                                          
077400             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
077500                                                                          
077600             PERFORM 4410-READ-TEMP-STORAGE                               
077700                                                                          
077800             PERFORM 2141-CLEAR-SELECTED-INDS                             
077900                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
078000                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
078100                                                                          
078200             PERFORM 5100-WRITE-TEMP-STORAGE                              
078300                                                                          
078400             ADD +1                TO PV-BLOCK-NUMBER                     
078500                                                                          
078600         END-PERFORM                                                      
078700                                                                          
078800         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
078900             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
079000                                                                          
079100         PERFORM                                                          
079200             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
079300                   ASC-HIGHEST-BLOCK-WRITTEN                              
079400                                                                          
079500             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
079600                                   TO PV-BLOCK-NUMBER                     
079700                                                                          
079800             PERFORM 4410-READ-TEMP-STORAGE                               
079900                                                                          
080000             PERFORM 2141-CLEAR-SELECTED-INDS                             
080100                 VARYING ASC-ITEM-SUB                                     
080200                 FROM 1 BY 1                                              
080300                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
080400                                                                          
080500             PERFORM 5100-WRITE-TEMP-STORAGE                              
080600                                                                          
080700             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
080800                                                                          
080900         END-PERFORM                                                      
081000                                                                          
081100         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
081200                                   TO PV-BLOCK-NUMBER                     
081300                                                                          
081400         PERFORM 4410-READ-TEMP-STORAGE                                   
081500     END-IF.                                                              
081600                                                                          
081700                                                                          
081800     MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
081900     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
082000     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
082100                                                                          
082200     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
082300                                                                          
082400 EJECT                                                                    
082500*----------------------------------------------------------------*        
082600*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
082700*----------------------------------------------------------------*        
082800                                                                          
082900 2141-CLEAR-SELECTED-INDS.                                                
083000                                                                          
083100     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
083200             NOT EQUAL ZERO                                               
083300         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
083400             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
083500                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
083600                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
083700                                   TO TRUE                                
083800         END-IF                                                           
083900     END-IF.                                                              
084000                                                                          
084100 EJECT                                                                    
084200*----------------------------------------------------------------*        
084300*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
084400*  FOR ALL ITEMS.                                                *        
084500*----------------------------------------------------------------*        
084600                                                                          
084700 2150-SELECT-ALL-ITEMS.                                                   
084800                                                                          
084900     MOVE +1                       TO ASC-BLK-SUB.                        
085000                                                                          
085100     PERFORM 2151-SET-SELECTED-INDS                                       
085200         VARYING ASC-ITEM-SUB                                             
085300         FROM 1 BY 1                                                      
085400         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
085500                                                                          
085600     ADD +1                        TO ASC-BLK-SUB.                        
085700                                                                          
085800     PERFORM 2151-SET-SELECTED-INDS                                       
085900         VARYING ASC-ITEM-SUB                                             
086000         FROM 1 BY 1                                                      
086100         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
086200                                                                          
086300     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
086400                                   TO PV-LIST-ITEM-NUMBER.                
086500                                                                          
086600     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
086700         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
086800                                                                          
086900     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
087000        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
087100         MOVE +1                   TO ASC-BLK-SUB                         
087200                                                                          
087300         PERFORM 5100-WRITE-TEMP-STORAGE                                  
087400                                                                          
087500         MOVE +1                   TO PV-BLOCK-NUMBER                     
087600                                                                          
087700         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
087800             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
087900                                                                          
088000         PERFORM                                                          
088100             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
088200                                                                          
088300             PERFORM 4410-READ-TEMP-STORAGE                               
088400                                                                          
088500             PERFORM 2151-SET-SELECTED-INDS                               
088600                 VARYING ASC-ITEM-SUB                                     
088700                 FROM 1 BY 1                                              
088800                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
088900                                                                          
089000             PERFORM 5100-WRITE-TEMP-STORAGE                              
089100                                                                          
089200             ADD +1                TO PV-BLOCK-NUMBER                     
089300                                                                          
089400         END-PERFORM                                                      
089500                                                                          
089600         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
089700             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
089800                                                                          
089900         PERFORM                                                          
090000             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
090100                   ASC-HIGHEST-BLOCK-WRITTEN                              
090200                                                                          
090300             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
090400                                   TO PV-BLOCK-NUMBER                     
090500                                                                          
090600             PERFORM 4410-READ-TEMP-STORAGE                               
090700                                                                          
090800             PERFORM 2151-SET-SELECTED-INDS                               
090900                 VARYING ASC-ITEM-SUB                                     
091000                 FROM 1 BY 1                                              
091100                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
091200                                                                          
091300             PERFORM 5100-WRITE-TEMP-STORAGE                              
091400                                                                          
091500             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
091600                                                                          
091700         END-PERFORM                                                      
091800                                                                          
091900         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
092000                                   TO PV-BLOCK-NUMBER                     
092100                                                                          
092200         PERFORM 4410-READ-TEMP-STORAGE                                   
092300     END-IF.                                                              
092400                                                                          
092500                                                                          
092600     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
092700                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
092800                                                                          
092900     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
093000                                                                          
093100 EJECT                                                                    
093200*----------------------------------------------------------------*        
093300*    MARK EACH ITEM AS BEING SELECTED.                           *        
093400*----------------------------------------------------------------*        
093500                                                                          
093600 2151-SET-SELECTED-INDS.                                                  
093700                                                                          
093800     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
093900             NOT EQUAL ZERO                                               
094000         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
094100             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
094200                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
094300                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
094400                               TO  TRUE                                   
094500         END-IF                                                           
094600     END-IF.                                                              
094700/                                                                         
094800 2160-GET-NEXT-RECORD.                                                    
094900                                                                          
095000     ADD +1                        TO ASC-SEL-SUB.                        
095100     IF  ASC-SEL-SUB > TR112-MAX-ITEMS                                    
095200         ADD +1                    TO ASC-SEL-ITEM                        
095300         MOVE +1                   TO ASC-SEL-SUB                         
095400     END-IF.                                                              
095500                                                                          
095600     IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
095700         PERFORM 9100-READ-SEL-QUEUE                                      
095800         IF  TR112-ITEM (ASC-SEL-SUB) > ZERO                              
095900             MOVE TR112-CSTM-ENTRY-ID (ASC-SEL-SUB) TO                    
096000                                   ASC-KEY-CSTM-ENTRY-ID                  
096010             MOVE TR112-ENT-ID (ASC-SEL-SUB) TO                           
096020                                   ASC-KEY-ENT-ID                         
096100             SET TR112-INQ (ASC-SEL-SUB)                                  
096200                                   TO TRUE                                
096300             PERFORM 9200-REWRITE-LIST-QUEUE                              
096400         ELSE                                                             
096500             SUBTRACT +1 FROM ASC-SEL-SUB                                 
096600*            --- BOTTOM OF LIST ---                                       
096700             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
096800             SET DP020-MSG-INFORMATIONAL                                  
096900                                   TO TRUE                                
097000         END-IF                                                           
097100     ELSE                                                                 
097200         SUBTRACT +1 FROM ASC-SEL-ITEM                                    
097300         MOVE TR112-MAX-ITEMS      TO ASC-SEL-SUB                         
097400         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
097500         SET DP020-MSG-INFORMATIONAL                                      
097600                                   TO TRUE                                
097700     END-IF.                                                              
097800                                                                          
097900                                                                          
098000/                                                                         
098100******************************************************************        
098200** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
098300** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
098400******************************************************************        
098500 2200-MOVE-SCREEN-TO-COMMAREA.                                            
098600                                                                          
098700     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
098800     MOVE +1                       TO PV-SUB.                             
098900*                                                                         
099000     PERFORM 3100-SET-SUBSCRIPTS.                                         
099100     IF  ASTRTITL > ZERO                                                  
099200         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
099300     ELSE                                                                 
099400         IF  ASTRTITF = DP015-ERASE-EOF                                   
099500             INITIALIZE ASC-START-ITEM-REQUEST                            
099600         END-IF                                                           
099700     END-IF.                                                              
099800     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
099900         VARYING PV-SUB                                                   
100000         FROM 1 BY 1                                                      
100100         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
100200******************************************************************        
100300** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
100400******************************************************************        
100500 2210-MOVE-LINES-TO-COMMAREA.                                             
100600     IF  MR-SELECTL (PV-SUB) > ZERO                                       
100700         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
100800         MOVE MR-SELECTI (PV-SUB)                                         
100900                                   TO ASC-TEMP-SELECTED-SW                
101000                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
101100     ELSE                                                                 
101200         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
101300                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
101400                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
101500                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
101600         END-IF                                                           
101700     END-IF.                                                              
101800     ADD +1                        TO ASC-ITEM-SUB.                       
101900     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
102000         ADD +1                    TO ASC-BLK-SUB                         
102100         MOVE +1                   TO ASC-ITEM-SUB                        
102200     END-IF.                                                              
102300******************************************************************        
102400** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
102500** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
102600******************************************************************        
102700 3000-EDIT-DATA-IN-COMMAREA.                                              
102800     SET PS-NO-ERROR               TO TRUE.                               
102900     PERFORM 3200-RESET-ATTRIBUTES.                                       
103000     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
103100**                                                                        
103200* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
103300* EDITTING IS DONE FROM THE BOTTOM UP.                                    
103400**                                                                        
103500     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
103600         ASC-ITEMS-DISPLAYED - 1.                                         
103700     PERFORM 3100-SET-SUBSCRIPTS.                                         
103800     PERFORM 3300-EDIT-SELECTIONS                                         
103900         VARYING PV-SUB                                                   
104000         FROM PV-SUB BY -1                                                
104100         UNTIL PV-SUB < +1                                                
104200*                                                                         
104300     IF  PS-NO-ERROR                                                      
104400         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
104500     END-IF.                                                              
104600*                                                                         
104700     IF  ((PS-NO-ERROR)                                                   
104800       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
104900       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
105000             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
105100     END-IF.                                                              
105200 EJECT                                                                    
105300*----------------------------------------------------------------*        
105400*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
105500*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
105600*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
105700*                                                                *        
105800*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
105900*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
106000*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
106100*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
106200*----------------------------------------------------------------*        
106300                                                                          
106400 3100-SET-SUBSCRIPTS.                                                     
106500                                                                          
106600     COMPUTE ASC-ITEM-SUB =                                               
106700         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
106800                                                                          
106900     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
107000         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
107100                                                                          
107200         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
107300     ELSE                                                                 
107400         MOVE +1                     TO ASC-BLK-SUB                       
107500     END-IF.                                                              
107600******************************************************************        
107700** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
107800******************************************************************        
107900                                                                          
108000 3200-RESET-ATTRIBUTES.                                                   
108100     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
108200     MOVE DP015-GREEN              TO ASTRTITC.                           
108300     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
108400     MOVE -1                       TO ASTRTITL.                           
108500*                                                                         
108600     PERFORM                                                              
108700         VARYING PV-SUB                                                   
108800         FROM 1 BY 1                                                      
108900         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
109000         MOVE DP015-ASK-DRK-OFF                                           
109100                                   TO MR-SELECTA (PV-SUB)                 
109200         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
109300         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
109400     END-PERFORM.                                                         
109500******************************************************************        
109600** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
109700** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
109800** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
109900** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
110000******************************************************************        
110100 3300-EDIT-SELECTIONS.                                                    
110200     EVALUATE TRUE                                                        
110300         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
110400             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
110500                 CONTINUE                                                 
110600         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
110700             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
110800                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
110900                 MOVE ASC-TEMP-SELECTED-SW                                
111000                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
111100                                TO ASC-SELECTED-SW                        
111200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
111300             END-IF                                                       
111400         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
111500                 = SPACE                                                  
111600             EVALUATE TRUE                                                
111700                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
111800                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
111900                     MOVE SPACE TO ASC-SELECTED-SW                        
112000                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
112100                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
112200                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
112300                     MOVE SPACE TO ASC-SELECTED-SW                        
112400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
112500                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
112600                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
112700                     MOVE SPACE TO ASC-SELECTED-SW                        
112800                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
112900             END-EVALUATE                                                 
113000         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
113100             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
113200                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
113300             END-IF                                                       
113400             PERFORM 3310-EDIT-RANGE                                      
113500         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
113600             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
113700                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
113800             END-IF                                                       
113900             PERFORM 3310-EDIT-RANGE                                      
114000         WHEN OTHER                                                       
114100             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
114200             SET DP020-MSG-FATAL    TO TRUE                               
114300             MOVE -1                TO MR-SELECTL (PV-SUB)                
114400             SET DP030-SET-CURSOR-APPL-1                                  
114500                                    TO TRUE                               
114600             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
114700             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
114800             SET PS-ERROR TO TRUE                                         
114900             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
115000                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
115100                 MOVE SPACE         TO ASC-SELECTED-SW                    
115200                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
115300             END-IF                                                       
115400     END-EVALUATE.                                                        
115500                                                                          
115600     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
115700     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
115800     IF  ASC-ITEM-SUB < 1                                                 
115900         SUBTRACT +1 FROM ASC-BLK-SUB                                     
116000         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
116100     END-IF.                                                              
116200******************************************************************        
116300** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
116400** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
116500** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
116600** THE ENDING.                                                  **        
116700******************************************************************        
116800 3310-EDIT-RANGE.                                                         
116900     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
117000         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
117100             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
117200                                   TO ASC-SELECTED-SW                     
117300                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
117400             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
117500         ELSE                                                             
117600             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
117700                 CONTINUE                                                 
117800             ELSE                                                         
117900*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
118000                 MOVE PC-TSYMSG-00054                                     
118100                                   TO DP020-MSG-NUMBER                    
118200                 SET DP020-MSG-FATAL                                      
118300                                   TO TRUE                                
118400                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
118500                 SET DP030-SET-CURSOR-APPL-1                              
118600                                   TO TRUE                                
118700                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
118800                 MOVE DP015-UNDERLINE                                     
118900                                   TO MR-SELECTH (PV-SUB)                 
119000                 SET PS-ERROR      TO TRUE                                
119100             END-IF                                                       
119200         END-IF                                                           
119300     ELSE                                                                 
119400         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
119500             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
119600                                   TO ASC-SELECTED-SW                     
119700                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
119800             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
119900         ELSE                                                             
120000             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
120100                 CONTINUE                                                 
120200             ELSE                                                         
120300*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
120400                 MOVE PC-TSYMSG-00055                                     
120500                                   TO DP020-MSG-NUMBER                    
120600                 SET DP020-MSG-FATAL                                      
120700                                   TO TRUE                                
120800                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
120900                 SET DP030-SET-CURSOR-APPL-1                              
121000                                   TO TRUE                                
121100                 SET PS-ERROR      TO TRUE                                
121200             END-IF                                                       
121300         END-IF                                                           
121400     END-IF.                                                              
121500*                                                                         
121600******************************************************************        
121700** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
121800** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
121900******************************************************************        
122000 3400-CHECK-RANGE-SEQUENCE.                                               
122100     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
122200       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
122300       AND  ASC-START-OF-SELECTED-RANGE >                                 
122400                                         ASC-END-OF-SELECTED-RANGE        
122500*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
122600         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
122700         SET DP020-MSG-FATAL TO TRUE                                      
122800         SET PS-ERROR TO TRUE                                             
122900         PERFORM 3410-POSITION-AT-ERROR                                   
123000     END-IF.                                                              
123100******************************************************************        
123200** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
123300** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
123400** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
123500******************************************************************        
123600 3410-POSITION-AT-ERROR.                                                  
123700     SET DP030-SET-CURSOR-APPL-1                                          
123800                                   TO TRUE.                               
123900     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
124000                    - PV-TOP-ITEM + 1.                                    
124100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
124200         CONTINUE                                                         
124300     ELSE                                                                 
124400         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
124500         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
124600         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
124700     END-IF.                                                              
124800*                                                                         
124900     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
125000                    - PV-TOP-ITEM + 1.                                    
125100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
125200         CONTINUE                                                         
125300     ELSE                                                                 
125400         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
125500         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
125600         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
125700     END-IF.                                                              
125800 EJECT                                                                    
125900*----------------------------------------------------------------*        
126000*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
126100*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
126200*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
126300*----------------------------------------------------------------*        
126400                                                                          
126500 3500-MARK-RANGE-AS-SELECTED.                                             
126600     MOVE +1                       TO  ASC-BLK-SUB.                       
126700     PERFORM 3510-MARK-SELECTED-INDS                                      
126800         VARYING ASC-ITEM-SUB                                             
126900         FROM 1 BY 1                                                      
127000         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
127100                                                                          
127200     ADD +1                        TO ASC-BLK-SUB.                        
127300     PERFORM 3510-MARK-SELECTED-INDS                                      
127400         VARYING ASC-ITEM-SUB                                             
127500         FROM 1 BY 1                                                      
127600         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
127700                                                                          
127800     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
127900                                   TO  PV-LIST-ITEM-NUMBER.               
128000     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
128100         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
128200               PC-ITEMS-PER-PANEL).                                       
128300                                                                          
128400     MOVE ASC-START-OF-SELECTED-RANGE                                     
128500                                   TO PV-START-OF-SELECTED-RANGE.         
128600     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
128700         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
128800               PC-ITEMS-PER-PANEL).                                       
128900                                                                          
129000     MOVE ASC-END-OF-SELECTED-RANGE                                       
129100                                   TO  PV-END-OF-SELECTED-RANGE.          
129200     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
129300         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
129400               PC-ITEMS-PER-PANEL).                                       
129500     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
129600         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
129700                                                                          
129800     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
129900        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
130000         MOVE +1                   TO ASC-BLK-SUB                         
130100         PERFORM 5100-WRITE-TEMP-STORAGE                                  
130200         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
130300             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
130400                                                                          
130500         PERFORM                                                          
130600             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
130700                   PV-PREV-BLOCK-IN-ARRAY                                 
130800             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
130900                                   TO PV-BLOCK-NUMBER                     
131000             PERFORM 4410-READ-TEMP-STORAGE                               
131100             PERFORM 3510-MARK-SELECTED-INDS                              
131200                 VARYING ASC-ITEM-SUB                                     
131300                 FROM 1 BY 1                                              
131400                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
131500             PERFORM 5100-WRITE-TEMP-STORAGE                              
131600             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
131700         END-PERFORM                                                      
131800                                                                          
131900         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
132000             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
132100         PERFORM                                                          
132200             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
132300                   PV-LAST-BLOCK-IN-RANGE                                 
132400             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
132500                                   TO PV-BLOCK-NUMBER                     
132600             PERFORM 4410-READ-TEMP-STORAGE                               
132700             PERFORM 3510-MARK-SELECTED-INDS                              
132800                 VARYING ASC-ITEM-SUB                                     
132900                 FROM 1 BY 1                                              
133000                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
133100             PERFORM 5100-WRITE-TEMP-STORAGE                              
133200             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
133300         END-PERFORM                                                      
133400                                                                          
133500         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
133600                                   TO PV-BLOCK-NUMBER                     
133700         PERFORM 4410-READ-TEMP-STORAGE                                   
133800     END-IF.                                                              
133900                                                                          
134000     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
134100                                      ASC-END-OF-SELECTED-RANGE.          
134200                                                                          
134300 EJECT                                                                    
134400*----------------------------------------------------------------*        
134500* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
134600*----------------------------------------------------------------*        
134700                                                                          
134800 3510-MARK-SELECTED-INDS.                                                 
134900                                                                          
135000     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
135100                                ASC-ITEM-SUB) >                           
135200         ASC-START-OF-SELECTED-RANGE)                                     
135300     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
135400                                ASC-ITEM-SUB) =                           
135500         ASC-START-OF-SELECTED-RANGE)                                     
135600                                                                          
135700         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
135800                                    ASC-ITEM-SUB) <                       
135900             ASC-END-OF-SELECTED-RANGE)                                   
136000         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
136100                                    ASC-ITEM-SUB) =                       
136200             ASC-END-OF-SELECTED-RANGE)                                   
136300                                                                          
136400             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
136500                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
136600                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
136700                               TO TRUE                                    
136800                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
136900                               TO TRUE                                    
137000                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
137100             END-IF                                                       
137200         END-IF                                                           
137300     END-IF.                                                              
137400                                                                          
137500 EJECT                                                                    
137600*----------------------------------------------------------------*        
137700*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
137800*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
137900*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
138000*    DISPLAY THE PANEL TO USER.                                  *        
138100*----------------------------------------------------------------*        
138200                                                                          
138300 4000-BUILD-INITIAL-PANEL.                                                
138400                                                                          
138500     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
138600                                 ASC-BLOCK-ENTRIES (2).                   
138700     MOVE +1                 TO ASC-BLK-SUB.                              
138800     SET ASC-ITEMS-LEFT-ON-DB                                             
138900                             TO TRUE.                                     
139000                                                                          
139100     MOVE +0                 TO ASC-ITEM-SUB                              
139200                                ASC-NUMBER-OF-ITEMS-READ                  
139300                                ASC-NUMBER-OF-SELECTED-ITEMS              
139400                                ASC-HIGHEST-BLOCK-WRITTEN                 
139500                                ASC-START-OF-SELECTED-RANGE               
139600                                ASC-END-OF-SELECTED-RANGE                 
139700                                ASC-ITEM-AT-TOP-OF-PANEL.                 
139800     PERFORM 4100-PREPARE-CURSOR                                          
139900                                                                          
140000     PERFORM 4010-OPEN-CURSOR.                                            
140100                                                                          
140200     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
140300                                                                          
140400     PERFORM 4020-CLOSE-CURSOR.                                           
140500                                                                          
140600     MOVE +1                   TO  PV-TOP-ITEM.                           
140700     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
140800         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
140900         SET DP020-MSG-FATAL   TO  TRUE                                   
141000         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
141100     ELSE                                                                 
141200*        MOVE -1               TO MR-SELECTL (1)                          
141300         MOVE -1               TO ASTRTITL                                
141400         SET DP030-SET-CURSOR-APPL-1                                      
141500                               TO TRUE                                    
141600         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
141700     END-IF.                                                              
141800 EJECT                                                                    
141900*----------------------------------------------------------------*        
142000* OPEN THE TMESSAGE CURSOR.                                      *        
142100*   IN THIS PARTICULAR APPLICATION ONE OF 3 DIFFERENT CURSORS    *        
142200*   WILL BE OPENED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
142300*   USER.                                                        *        
142400*----------------------------------------------------------------*        
142500                                                                          
142600 4010-OPEN-CURSOR.                                                        
142700                                                                          
142800     EXEC SQL                                                             
142900         OPEN BOL-CSR                                                     
143000     END-EXEC                                                             
143100                                                                          
143200     EVALUATE TRUE                                                        
143300         WHEN SQLCODE = ZERO                                              
143400              CONTINUE                                                    
143500         WHEN SQLCODE = -904                                              
143600         WHEN SQLCODE = -911                                              
143700         WHEN SQLCODE = -913                                              
143800             PERFORM 9900-SOFT-ABORT                                      
143900         WHEN SQLWARN0 NOT = SPACE                                        
144000         WHEN SQLCODE  NOT = ZERO                                         
144100             MOVE '4010-OPEN-CURSOR'                                      
144200                                   TO DP013-PARAGRAPH                     
144300             MOVE 'OPEN RCVR-CSR CURSOR'                                  
144400                                   TO DP013-MESSAGE-TEXT (1)              
144500             MOVE SQLCA            TO DP013-SQLCA                         
144600              MOVE 'TPURCHS'       TO DP013-DB2-TABLE-NAME (1)            
144700              MOVE 'TSTATCD'       TO DP013-DB2-TABLE-NAME (2)            
144800              MOVE 'TRECEIV'       TO DP013-DB2-TABLE-NAME (3)            
144900              SET DP013-DB2-ABEND  TO TRUE                                
145000              PERFORM DP013-0000-PROCESS-ABEND                            
145100     END-EVALUATE.                                                        
145200/                                                                         
145300 4020-CLOSE-CURSOR.                                                       
145400     EXEC SQL                                                             
145500         CLOSE BOL-CSR                                                    
145600     END-EXEC                                                             
145700                                                                          
145800     EVALUATE TRUE                                                        
145900         WHEN SQLCODE = ZERO                                              
146000             CONTINUE                                                     
146100         WHEN SQLWARN0 NOT = SPACES                                       
146200         WHEN SQLCODE < ZERO                                              
146300         WHEN SQLCODE > ZERO                                              
146400             MOVE '4020-CLOSE-CURSOR'                                     
146500                               TO DP013-PARAGRAPH                         
146600             MOVE 'CLOSE SYSTEM MESSAGE CURSOR '                          
146700                               TO DP013-MESSAGE-TEXT (1)                  
146800             MOVE SQLCA        TO DP013-SQLCA                             
146900             MOVE 'TIMCSBL '   TO DP013-DB2-TABLE-NAME (1)                
147000             SET DP013-DB2-ABEND                                          
147100                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
147200             PERFORM DP013-0000-PROCESS-ABEND                             
147300     END-EVALUATE.                                                        
147400/                                                                         
147500 4100-PREPARE-CURSOR.                                                     
147600                                                                          
147700     MOVE ASC-KEY-CSTM-ENTRY-ID       TO DK-CSTM-ENTRY-ID.                
147710     MOVE ASC-KEY-ENT-ID              TO DK-ENT-ID.                       
147800/                                                                         
147900*----------------------------------------------------------------*        
148000*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
148100*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
148200*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
148300*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
148400*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
148500*                                                                *        
148600*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
148700*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
148800*    UPON INITIALIZATION.                                        *        
148900*----------------------------------------------------------------*        
149000                                                                          
149100 4300-READ-ITEMS-FROM-DB.                                                 
149200                                                                          
149300     PERFORM                                                              
149400         VARYING PV-READ-COUNT                                            
149500         FROM 1 BY 1                                                      
149600         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
149700            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
149800                                                                          
149900         PERFORM 4310-FETCH                                               
150000                                                                          
150100         IF  ASC-ITEMS-LEFT-ON-DB                                         
150200             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
150300                 ADD +1            TO ASC-ITEM-SUB                        
150400             ELSE                                                         
150500                 ADD +1            TO ASC-BLK-SUB                         
150600                 MOVE +1           TO ASC-ITEM-SUB                        
150700                                                                          
150800                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
150900                     MOVE +1       TO ASC-BLK-SUB                         
151000                     SET ASC-BLOCK-MODIFIED (1)                           
151100                                   TO TRUE                                
151200                     PERFORM 5100-WRITE-TEMP-STORAGE                      
151300                     MOVE ASC-BLOCK-ENTRIES                               
151400                               (PC-MAX-BLOCKS-IN-ARRAY)                   
151500                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
151600                     ADD +1        TO ASC-BLK-SUB                         
151700                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
151800                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
151900                                   TO TRUE                                
152000                 END-IF                                                   
152100             END-IF                                                       
152200             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
152300             MOVE ASC-NUMBER-OF-ITEMS-READ                                
152400                                   TO ASC-LIST-ITEM-NUMBER                
152500                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
152600             MOVE IMCSBL-MSTR-BOL-NBR                                     
152700                               TO ASC-MASTER-BOL-NUMBER                   
152800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
152900             MOVE IMCSBL-HSE-BOL-NBR                                      
153000                               TO  ASC-HOUSE-BOL-NUMBER                   
153100                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
153101                                                                          
153102             IF IMCSBL-LAST-UPD-TMST (1:10)                               
153103                                        = SPACES OR PC-INIT-DATE          
153104                 MOVE SPACES       TO ASC-LAST-TRN                        
153105                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
153106             ELSE                                                         
153107                 STRING  IMCSBL-LAST-UPD-TMST(6:2) '/'                    
153108                         IMCSBL-LAST-UPD-TMST(9:2)                        
153109                         DELIMITED BY SIZE                                
153110                                   INTO ASC-LAST-TRN                      
153111                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
153112             END-IF                                                       
153200         END-IF                                                           
153300     END-PERFORM.                                                         
153400                                                                          
153500     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
153600         MOVE +1               TO ASC-BLK-SUB                             
153700         PERFORM 5100-WRITE-TEMP-STORAGE                                  
153800         ADD +1                TO ASC-BLK-SUB                             
153900         PERFORM 5100-WRITE-TEMP-STORAGE                                  
154000                                                                          
154100         MOVE 1                TO ASC-BLK-SUB                             
154200                                  PV-BLOCK-NUMBER                         
154300         PERFORM 4410-READ-TEMP-STORAGE                                   
154400         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
154500             MOVE 2            TO ASC-BLK-SUB                             
154600                                  PV-BLOCK-NUMBER                         
154700             PERFORM 4410-READ-TEMP-STORAGE                               
154800         END-IF                                                           
154900     END-IF.                                                              
155000/                                                                         
155100 4310-FETCH.                                                              
155200                                                                          
155300     EXEC SQL                                                             
155400         FETCH BOL-CSR                                                    
155500             INTO  :IMCSBL-MSTR-BOL-NBR                                   
155600                 , :IMCSBL-HSE-BOL-NBR                                    
155610                 , :IMCSBL-LAST-UPD-TMST                                  
155700     END-EXEC                                                             
155800                                                                          
155900     EVALUATE TRUE                                                        
156000         WHEN SQLCODE = ZERO                                              
156100              CONTINUE                                                    
156200         WHEN SQLCODE = +100                                              
156300             SET ASC-NO-ITEMS-LEFT-ON-DB                                  
156400                                   TO TRUE                                
156500         WHEN SQLWARN0 NOT = SPACE                                        
156600         WHEN SQLCODE NOT = ZERO                                          
156700              MOVE '4310-FETCH-CURSOR'                                    
156800                               TO DP013-PARAGRAPH                         
156900         MOVE 'FETCH BOL CURSOR'                                          
157000                               TO DP013-MESSAGE-TEXT (1)                  
157100         MOVE SQLCA            TO DP013-SQLCA                             
157200         SET DP013-DB2-ABEND                                              
157300                               TO TRUE                                    
157400         PERFORM DP013-0000-PROCESS-ABEND                                 
157500     END-EVALUATE.                                                        
157600                                                                          
157700                                                                          
157800 EJECT                                                                    
157900*----------------------------------------------------------------*        
158000* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
158100*----------------------------------------------------------------*        
158200                                                                          
158300 4400-MOVE-COMMAREA-TO-SCREEN.                                            
158400     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
158500         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
158600         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
158700             MOVE +1    TO ASC-BLK-SUB                                    
158800             PERFORM 5100-WRITE-TEMP-STORAGE                              
158900             ADD +1     TO ASC-BLK-SUB                                    
159000             PERFORM 5100-WRITE-TEMP-STORAGE                              
159100             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
159200                 GIVING PV-BLOCK-NUMBER                                   
159300                 REMAINDER PV-REMAINDER                                   
159400             IF  PV-REMAINDER > ZERO                                      
159500                 ADD +1 TO PV-BLOCK-NUMBER                                
159600             END-IF                                                       
159700             MOVE +1    TO ASC-BLK-SUB                                    
159800             PERFORM 4410-READ-TEMP-STORAGE                               
159900             ADD +1     TO ASC-BLK-SUB                                    
160000                           PV-BLOCK-NUMBER                                
160100             PERFORM 4410-READ-TEMP-STORAGE                               
160200     END-IF.                                                              
160300     MOVE +1            TO ASC-BLK-SUB.                                   
160400     COMPUTE ASC-ITEM-SUB =                                               
160500         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
160600         + PC-ITEMS-PER-PANEL).                                           
160700*                                                                         
160800     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
160900         ADD +1         TO ASC-BLK-SUB                                    
161000         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
161100     END-IF.                                                              
161200*                                                                         
161300     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
161400         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
161500     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
161600     PERFORM 4420-FORMAT-LIST-LINES                                       
161700         VARYING PV-SUB                                                   
161800         FROM PC-ITEMS-PER-PANEL BY -1                                    
161900         UNTIL PV-SUB < +1.                                               
162000*                                                                         
162100     COMPUTE ASC-ITEMS-DISPLAYED =                                        
162200         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
162300     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
162400                                      ASTRTITO.                           
162500     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
162600     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
162700     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
162800                                   TO ASELITO.                            
162900*                                                                         
163000     IF  DP020-MSG-NUMBER-X = SPACE                                       
163100         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
163200*            --- MORE ITEMS TO DISPLAY ----                               
163300             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
163400             SET DP020-MSG-INFORMATIONAL TO TRUE                          
163500         END-IF                                                           
163600     END-IF.                                                              
163700     PERFORM 7000-SELECT-TIMCSEN.                                         
163800     MOVE ASC-KEY-CSTM-ENTRY-ID       TO AENTRYO                          
           PERFORM 4600-ENTR-TYP-DESC                                           
163820     MOVE ASC-MOM-TRIP-TYP-CDE        TO AMOMTRPO                         
163900     MOVE ASC-FILD-DATE               TO AFILDTEO                         
164000     MOVE ASC-REJ-DATE                TO AREJDTEO                         
164100     MOVE ASC-REJ-DESC                TO AREJDSCO                         
164200     MOVE ASC-EST-CLR-DATE            TO AESTCLRO                         
164210     MOVE ASC-ACTL-CLR-DATE           TO AACTCLRO                         
164300     MOVE ASC-EXM-DATE                TO AEXMDTEO                         
164400     MOVE ASC-LQDN-DATE               TO ALIQDTEO                         
164500     MOVE ASC-LQDN-SSPNN-DATE         TO ASUSDTEO                         
164600     MOVE ASC-LQDN-SSPNN-DESC         TO ASUSDSCO                         
164700     MOVE ASC-BKR-REF-NBR             TO ABKRNUMO                         
164710     IF ASC-POE-CODE NUMERIC                                              
164711       IF ASC-POE-CODE = ZERO                                             
164712         MOVE SPACES                  TO APOEO                            
164722       ELSE                                                               
164724         PERFORM 4500-OPEN-POE-CURSOR                                     
164725         PERFORM 4510-FETCH-POE-CURSOR                                    
164726         PERFORM 4505-CLOSE-POE-CURSOR                                    
164727         MOVE IMPOE-POE-CDE           TO APOEO                            
164728       END-IF                                                             
164730     ELSE                                                                 
164740       MOVE ASC-POE-CODE              TO APOEO                            
164750     END-IF.                                                              
164800     PERFORM 7100-SELECT-TENTNME.                                         
164900     MOVE ENTNME-ENT-NAME-DESC        TO ABKRNMEO.                        
165000*----------------------------------------------------------------*        
165100*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
165200*----------------------------------------------------------------*        
165300                                                                          
165400 4410-READ-TEMP-STORAGE.                                                  
165500                                                                          
165600     EXEC CICS                                                            
165700         READQ TS                                                         
165800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
165900             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
166000             ITEM  (PV-BLOCK-NUMBER)                                      
166100             RESP  (PV-CICS-RESP)                                         
166200     END-EXEC.                                                            
166300                                                                          
166400     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
166500       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
166600         CONTINUE                                                         
166700     ELSE                                                                 
166800         SET DP013-CICS-ABEND     TO TRUE                                 
166900         SET DP013-NO-ROLLBACK    TO TRUE                                 
167000         MOVE '4410-READ-TEMP-STORAGE'                                    
167100                                  TO DP013-PARAGRAPH                      
167200         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
167300                                  TO DP013-MESSAGE-TEXT(1)                
167400         PERFORM DP013-0000-PROCESS-ABEND                                 
167500     END-IF.                                                              
167600                                                                          
167700     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
167800*                                                                         
167900     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
168000         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
168100     END-IF.                                                              
168200 EJECT                                                                    
168300*----------------------------------------------------------------*        
168400*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
168500*----------------------------------------------------------------*        
168600                                                                          
168700 4420-FORMAT-LIST-LINES.                                                  
168800     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
168900         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
169000         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
169100         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
169200         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
169300                                    MR-ACTION (PV-SUB)                    
169400                                    MR-MASTER-BOL-NUMBER (PV-SUB)         
169500                                    MR-HOUSE-BOL-NUMBER (PV-SUB)          
169510                                    MR-LAST-TRN (PV-SUB)                  
169600     ELSE                                                                 
169700         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
169800                                                   > SPACE                
169900             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
170000                                 TO MR-SELECTI (PV-SUB)                   
170100         ELSE                                                             
170200             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
170300                                 TO MR-SELECTI (PV-SUB)                   
170400         END-IF                                                           
170500*                                                                         
170600         IF  DP030-SET-CURSOR-TRAILER                                     
170700             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
170800                 > SPACE)                                                 
170900               OR (PV-SUB = 1))                                           
171000                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
171100                MOVE -1          TO MR-SELECTL (PV-SUB)                   
171200             END-IF                                                       
171300         END-IF                                                           
171400*                                                                         
171500         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
171600             MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)                   
171700                                 TO MR-ACTION (PV-SUB)                    
171800             MOVE ASC-MASTER-BOL-NUMBER                                   
171900                                    (ASC-BLK-SUB ASC-ITEM-SUB)            
172000                                 TO MR-MASTER-BOL-NUMBER (PV-SUB)         
172100             MOVE ASC-HOUSE-BOL-NUMBER                                    
172200                                    (ASC-BLK-SUB ASC-ITEM-SUB)            
172300                                 TO MR-HOUSE-BOL-NUMBER (PV-SUB)          
172310             MOVE ASC-LAST-TRN      (ASC-BLK-SUB ASC-ITEM-SUB)            
172330                                 TO MR-LAST-TRN (PV-SUB)                  
172400         END-IF                                                           
172500     END-IF.                                                              
172600                                                                          
172700* ASKIP AND DARKEN ALL SELECT FIELDS.  THESE CAN BE UNDONE AT             
172800* A LATER DATE TO MAKE THE SCREEN SELECTABLE AGAIN.                       
172900     MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                       
173000     MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                       
173100     MOVE SPACE              TO MR-SELECTI (PV-SUB)                       
173200                                                                          
173300     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
173400     IF  ASC-ITEM-SUB < + 1                                               
173500         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
173600         MOVE +1                 TO ASC-BLK-SUB                           
173700     END-IF.                                                              
173800/                                                                         
173900******************************************************************        
174000** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
174100** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
174200** ISSUE A WARNING.                                             **        
174300******************************************************************        
174400 4430-CHECK-FOR-BEGIN-AND-END.                                            
174500     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
174600         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
174700*            -- NO END-OF-RANGE SPECFIED ---                              
174800             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
174900             SET DP020-MSG-WARNING TO TRUE                                
175000             PERFORM 4431-POSITION-AT-WARNING                             
175100         END-IF                                                           
175200     ELSE                                                                 
175300         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
175400*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
175500             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
175600             SET DP020-MSG-WARNING TO TRUE                                
175700             PERFORM 4431-POSITION-AT-WARNING                             
175800         END-IF                                                           
175900     END-IF.                                                              
176000******************************************************************        
176100** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
176200** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
176300** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
176400** POSITION THE CURSOR THERE.                                   **        
176500******************************************************************        
176600 4431-POSITION-AT-WARNING.                                                
176700     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
176800                    - PV-TOP-ITEM + 1.                                    
176900     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
177000         CONTINUE                                                         
177100     ELSE                                                                 
177200         SET DP030-SET-CURSOR-APPL-1                                      
177300                                   TO TRUE                                
177400         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
177500         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
177600         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
177700     END-IF.                                                              
177800*                                                                         
177900     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
178000                    - PV-TOP-ITEM + 1.                                    
178100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
178200         CONTINUE                                                         
178300     ELSE                                                                 
178400         SET DP030-SET-CURSOR-APPL-1                                      
178500                                   TO TRUE                                
178600         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
178700         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
178800         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
178900     END-IF.                                                              
179000 EJECT                                                                    
179001*----------------------------------------------------------------*        
179002*   OPEN THE POE CODE CURSOR                                     *        
179003*----------------------------------------------------------------*        
179004 4500-OPEN-POE-CURSOR.                                                    
179005                                                                          
179006     PERFORM                                                              
179007         WITH TEST AFTER                                                  
179008         VARYING PV-RETRY-COUNTER                                         
179009         FROM 1 BY 1                                                      
179010         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
179011                   OR (SQLCODE = ZERO))                                   
179012                                                                          
179013         MOVE 'CURSOR:  POE-CSR'     TO DP013-MESSAGE-TEXT (2)            
179014                                                                          
179015         EXEC SQL                                                         
179016              OPEN POE-CSR                                                
179017         END-EXEC                                                         
179018                                                                          
179019         EVALUATE TRUE                                                    
179020             WHEN SQLCODE = ZERO                                          
179021             WHEN SQLCODE = -904                                          
179022             WHEN SQLCODE = -913                                          
179023                 CONTINUE                                                 
179024             WHEN SQLWARN0 NOT = SPACES                                   
179025             WHEN SQLCODE < ZERO                                          
179026             WHEN SQLCODE > ZERO                                          
179027                 MOVE '4500-OPEN-POE-CURSOR'                              
179028                                   TO DP013-PARAGRAPH                     
179029                 MOVE 'OPEN POE CURSOR - '                                
179030                                   TO DP013-MESSAGE-TEXT (1)              
179031                 MOVE SQLCA        TO DP013-SQLCA                         
179032                 MOVE 'TIMPOE  '   TO DP013-DB2-TABLE-NAME (1)            
179033                 SET DP013-DB2-ABEND                                      
179034                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
179035                 PERFORM DP013-0000-PROCESS-ABEND                         
179036         END-EVALUATE                                                     
179037     END-PERFORM.                                                         
179038*                                                                         
179039     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
179040         MOVE '4500-OPEN-POE-CURSOR' TO DP013-PARAGRAPH                   
179041         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
179042                                   TO DP013-MESSAGE-TEXT (1)              
179043         MOVE SQLCA                TO DP013-SQLCA                         
179044         MOVE 'TIMPOE  '           TO DP013-DB2-TABLE-NAME (1)            
179045         SET DP013-DB2-ABEND                                              
179046             DP013-XCTL-DISPLAY-RESTART                                   
179047                                   TO TRUE                                
179048         PERFORM DP013-0000-PROCESS-ABEND                                 
179049     END-IF.                                                              
179050                                                                          
179051 EJECT                                                                    
179052*----------------------------------------------------------------*        
179053*   CLOSE THE POE CODE CURSOR                                    *        
179054*----------------------------------------------------------------*        
179055 4505-CLOSE-POE-CURSOR.                                                   
179056                                                                          
179057     EXEC SQL                                                             
179058          CLOSE POE-CSR                                                   
179059     END-EXEC.                                                            
179060                                                                          
179061     EVALUATE TRUE                                                        
179062         WHEN SQLCODE = ZERO                                              
179063             CONTINUE                                                     
179064         WHEN SQLWARN0 NOT = SPACES                                       
179065         WHEN SQLCODE < ZERO                                              
179066         WHEN SQLCODE > ZERO                                              
179067             MOVE '4505-CLOSE-POE-CURSOR'                                 
179068                               TO DP013-PARAGRAPH                         
179069             MOVE 'CLOSE POE CURSOR '                                     
179070                               TO DP013-MESSAGE-TEXT (1)                  
179071             MOVE SQLCA        TO DP013-SQLCA                             
179072             MOVE 'TIMPOE  '   TO DP013-DB2-TABLE-NAME (1)                
179073             SET DP013-DB2-ABEND                                          
179074                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
179075             PERFORM DP013-0000-PROCESS-ABEND                             
179076     END-EVALUATE.                                                        
179077                                                                          
179078 EJECT                                                                    
179079*----------------------------------------------------------------*11340099
179080*    FETCH THE POE CODE CURSOR                                   *11350099
179081*----------------------------------------------------------------*11360099
179082 4510-FETCH-POE-CURSOR.                                           11370099
179083                                                                  11380099
179084     EXEC SQL                                                     11390099
179085          FETCH POE-CSR                                           11400099
179086          INTO  :IMPOE-POE-CDE                                    11410099
179087     END-EXEC.                                                    11520099
179088                                                                  11530099
179089     EVALUATE TRUE                                                11540099
179090         WHEN SQLCODE = ZERO                                      11550099
179091         WHEN SQLCODE = -811                                              
179092              CONTINUE                                            11570099
179093         WHEN SQLCODE = +100                                      11560099
179094              MOVE SPACES TO IMPOE-POE-CDE                                
179095         WHEN SQLWARN0 NOT = SPACE                                11580099
179096         WHEN SQLCODE NOT = ZERO                                  11590099
179097              MOVE '4510-FETCH-POE-CURSOR'                        11600099
179098                               TO DP013-PARAGRAPH                 11610099
179099              MOVE 'FETCH POE CURSOR'                             11620099
179100                               TO DP013-MESSAGE-TEXT (1)          11630099
179101              MOVE SQLCA       TO DP013-SQLCA                     11640099
179102              SET DP013-DB2-ABEND                                 11650099
179103                               TO TRUE                            11660099
179104              PERFORM DP013-0000-PROCESS-ABEND                    11670099
179105     END-EVALUATE.                                                11680099
179106                                                                  11690099
      *----------------------------------------------------------------*11340099
      *    SELECT THE ENTRY TYPE DESCRIPTION                           *11350099
      *----------------------------------------------------------------*11360099
       4600-ENTR-TYP-DESC.                                              11370099
           EXEC SQL                                                     11390099
                SELECT CDE_ID,                                          11400099
                       CDVL_CDE,                                                
                       CDVL_DESC                                                
                INTO  :ICDVAL-CDE-ID,                                   11410099
                      :ICDVAL-CDVL-CDE,                                         
                      :ICDVAL-CDVL-DESC                                         
                FROM  TICDVAL                                                   
                WHERE CDE_ID       = 900                                        
                 AND CDVL_CDE      = :ASC-ENTR-TYP-CDE                          
           END-EXEC.                                                    11520099
                                                                        11530099
           EVALUATE TRUE                                                11540099
               WHEN SQLCODE = ZERO                                      11550099
                   MOVE ICDVAL-CDVL-DESC                                        
                            TO AENTYPEO                                         
               WHEN SQLCODE = +100                                              
                   MOVE ASC-ENTR-TYP-CDE                                        
                            TO AENTYPEO                                         
                    CONTINUE                                            11570099
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                   PERFORM 9900-SOFT-ABORT                                      
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE NOT = ZERO                                          
                   MOVE '4600-ENTR-TYP-DESC'                                    
                            TO DP013-PARAGRAPH                                  
               MOVE 'SELECT CDVL DESC'                                          
                            TO DP013-MESSAGE-TEXT (1)                           
               MOVE SQLCA   TO DP013-SQLCA                                      
               SET DP013-DB2-ABEND                                              
                            TO TRUE                                             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-EVALUATE.                                                        
                                                                                
179107 EJECT                                                            11750099
179110*----------------------------------------------------------------*        
179200* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
179300* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
179400* FOR THE SELECTED ITEMS QUEUE.                                  *        
179500*                                                                *        
179600* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
179700* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
179800* THE ARRAY IS FULL.                                             *        
179900*----------------------------------------------------------------*        
180000                                                                          
180100 5000-PREPARE-SEL-QUEUE.                                                  
180200                                                                          
180300     MOVE +1                   TO  ASC-BLK-SUB.                           
180400     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
180500     ADD +1                    TO  ASC-BLK-SUB.                           
180600     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
180700                                                                          
180800     MOVE ASC-SEL-QUEUE-NAME   TO  TR114-SEL-QUEUE-NAME.                  
180900     PERFORM 6010-DELETE-SEL-QUEUE.                                       
181000                                                                          
181100     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
181200                                                                          
181300     SET TR112-IDX             TO  1.                                     
181400     MOVE ZERO                 TO TR114-NUMBER-OF-TS-ITEMS.               
181500     INITIALIZE TR112-SELECTED-ITEM-ARRAY.                                
181600                                                                          
181700*    PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
181800*            VARYING PV-ITEM                                              
181900*            FROM 1 BY 1                                                  
182000*            UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
182100                                                                          
182200     IF  TR112-IDX > 1                                                    
182300         PERFORM 5205-WRITE-SEL-QUEUE                                     
182400         ADD +1                TO TR114-NUMBER-OF-TS-ITEMS                
182500     END-IF.                                                              
182600 EJECT                                                                    
182700*----------------------------------------------------------------*        
182800*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
182900*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
183000*----------------------------------------------------------------*        
183100                                                                          
183200 5100-WRITE-TEMP-STORAGE.                                                 
183300     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
183400                                   TO PV-LIST-ITEM-NUMBER.                
183500     COMPUTE PV-BLOCK-NUMBER =                                            
183600         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
183700                                                                          
183800     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
183900     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
184000         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
184100             PERFORM 5105-REWRITE-ASC-LISTQ                               
184200         END-IF                                                           
184300     ELSE                                                                 
184400         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
184500         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
184600         PERFORM 5110-WRITE-ASC-LISTQ                                     
184700         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
184800     END-IF.                                                              
184900                                                                          
185000     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
185100******************************************************************        
185200** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
185300** SPECIFIC COMMAREA.                                           **        
185400******************************************************************        
185500 5105-REWRITE-ASC-LISTQ.                                                  
185600     EXEC CICS                                                            
185700         WRITEQ TS                                                        
185800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
185900             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
186000             ITEM  (PV-BLOCK-NUMBER)                                      
186100             REWRITE                                                      
186200             RESP  (PV-CICS-RESP)                                         
186300     END-EXEC.                                                            
186400                                                                          
186500     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
186600         CONTINUE                                                         
186700     ELSE                                                                 
186800         SET DP013-CICS-ABEND            TO TRUE                          
186900         SET DP013-NO-ROLLBACK           TO TRUE                          
187000         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
187100         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
187200                                         TO DP013-MESSAGE-TEXT(1)         
187300         PERFORM DP013-0000-PROCESS-ABEND                                 
187400     END-IF.                                                              
187500                                                                          
187600******************************************************************        
187700** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
187800** SPECIFIC COMMAREA.                                           **        
187900******************************************************************        
188000 5110-WRITE-ASC-LISTQ.                                                    
188100     EXEC CICS                                                            
188200         WRITEQ TS                                                        
188300             QUEUE (ASC-LIST-QUEUE-NAME)                                  
188400             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
188500             ITEM  (PV-BLOCK-NUMBER)                                      
188600             RESP  (PV-CICS-RESP)                                         
188700     END-EXEC.                                                            
188800     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
188900         SET DP013-CICS-ABEND      TO TRUE                                
189000         SET DP013-NO-ROLLBACK     TO TRUE                                
189100         MOVE '5110-WRITE-ASC-LISTQ'                                      
189200                                   TO DP013-PARAGRAPH                     
189300         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
189400                                   TO DP013-MESSAGE-TEXT(1)               
189500         PERFORM DP013-0000-PROCESS-ABEND                                 
189600     END-IF.                                                              
189700 EJECT                                                                    
189800*----------------------------------------------------------------*        
189900* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
190000* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
190100*----------------------------------------------------------------*        
190200                                                                          
190300*5200-FILL-SEL-QUEUE-RECORD.                                              
190400*                                                                         
190500*    PERFORM 6112-READ-LIST-QUEUE.                                        
190600*                                                                         
190700*    IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
190800*        PERFORM                                                          
190900*        VARYING LQW-IDX                                                  
191000*        FROM 1 BY 1                                                      
191100*        UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
191200*          OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
191300*                                                                         
191400*            IF  LQW-SELECT (LQW-IDX)                                     
191500*                MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
191600*                                  TO TR112-ITEM (DP057-IDX)              
191700*                MOVE LQW-MESSAGE-NUMBER (LQW-IDX)                        
191800*                                  TO TR112-MESSAGE-NUMBER                
191900*                                       (TR112-IDX)                       
192000*                MOVE SPACE        TO TR112-ACTION-SW (TR112-IDX)         
192100*                SET TR112-IDX UP BY 1                                    
192200*                IF  TR112-IDX > DP057-MAX-ITEMS                          
192300*                    PERFORM 5205-WRITE-SEL-QUEUE                         
192400*                    ADD +1        TO TR114-NUMBER-OF-TS-ITEMS            
192500*                    INITIALIZE TR112-SELECTED-ITEM-ARRAY                 
192600*                    SET TR112-IDX TO  1                                  
192700*                END-IF                                                   
192800*            END-IF                                                       
192900*        END-PERFORM                                                      
193000*    END-IF.                                                              
193100*                                                                         
193200*----------------------------------------------------------------*        
193300*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
193400*----------------------------------------------------------------*        
193500                                                                          
193600 5205-WRITE-SEL-QUEUE.                                                    
193700                                                                          
193800     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
193900                                   TO TR112-SELECTED-ITEMS.               
194000                                                                          
194100     EXEC CICS                                                            
194200          WRITEQ TS                                                       
194300              QUEUE (ASC-SEL-QUEUE-NAME)                                  
194400              FROM  (TR112-PO-SELECT-RECORD)                              
194500              ITEM  (PV-SEL-QUEUE-ITEM)                                   
194600              RESP  (PV-CICS-RESP)                                        
194700     END-EXEC.                                                            
194800                                                                          
194900     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
195000         CONTINUE                                                         
195100     ELSE                                                                 
195200         SET DP013-CICS-ABEND      TO TRUE                                
195300         SET DP013-NO-ROLLBACK     TO TRUE                                
195400         MOVE '5205-WRITE-SEL-QUEUE'                                      
195500                                   TO DP013-PARAGRAPH                     
195600         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
195700                                   TO DP013-MESSAGE-TEXT(1)               
195800         PERFORM DP013-0000-PROCESS-ABEND                                 
195900     END-IF.                                                              
196000 EJECT                                                                    
196100******************************************************************        
196200** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
196300** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
196400** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
196500** THE USER.                                                    **        
196600******************************************************************        
196700 6000-REFRESH-PANEL-FROM-TEMP.                                            
196800     IF  ASC-UPDATE-TSQ                                                   
196900         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
197000         MOVE ZERO                 TO PV-SAVE-ITEM                        
197100                                      PV-SEL-QUEUE-ITEM                   
197200         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
197300                                   TO PV-HOLD-SELECTED-ITEMS              
197400         MOVE +1                   TO PV-ITEMS-PROCESSED                  
197500         PERFORM 6100-UPDATE-TSQ                                          
197600             UNTIL PV-ITEMS-PROCESSED >                                   
197700                                     PV-HOLD-SELECTED-ITEMS               
197800         PERFORM 6010-DELETE-SEL-QUEUE                                    
197900         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
198000                    ASC-BLOCK-ENTRIES (2)                                 
198100     END-IF.                                                              
198200                                                                          
198300*----------------------------------------------------------------*        
198400*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
198500*----------------------------------------------------------------*        
198600 6010-DELETE-SEL-QUEUE.                                                   
198700                                                                          
198800     EXEC CICS                                                            
198900         DELETEQ TS                                                       
199000             QUEUE (ASC-SEL-QUEUE-NAME)                                   
199100             RESP  (PV-CICS-RESP)                                         
199200     END-EXEC.                                                            
199300*                                                                         
199400     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
199500        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
199600         CONTINUE                                                         
199700     ELSE                                                                 
199800         SET DP013-CICS-ABEND TO TRUE                                     
199900         SET DP013-NO-ROLLBACK TO TRUE                                    
200000         MOVE '6010-DELETE-SEL-QUEUE'                                     
200100                        TO DP013-PARAGRAPH                                
200200         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
200300                        TO DP013-MESSAGE-TEXT (1)                         
200400         PERFORM DP013-0000-PROCESS-ABEND                                 
200500     END-IF.                                                              
200600                                                                          
200700*----------------------------------------------------------------*        
200800*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
200900*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
201000*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
201100*    THE TEMP QUEUE.                                             *        
201200*----------------------------------------------------------------*        
201300                                                                          
201400 6100-UPDATE-TSQ.                                                         
201500                                                                          
201600     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
201700     PERFORM 6105-READ-SEL-QUEUE.                                         
201800     PERFORM                                                              
201900         VARYING TR112-IDX                                                
202000         FROM 1 BY 1                                                      
202100         UNTIL ((PV-ITEMS-PROCESSED >                                     
202200                              PV-HOLD-SELECTED-ITEMS)                     
202300         OR (TR112-IDX GREATER THAN TR112-MAX-ITEMS))                     
202400         IF  TR112-ACTION-SW (TR112-IDX) > SPACE                          
202500             PERFORM 6110-PROCESS-ITEMS                                   
202600         END-IF                                                           
202700         ADD +1                TO PV-ITEMS-PROCESSED                      
202800     END-PERFORM.                                                         
202900     PERFORM 6111-WRITE-LIST-QUEUE.                                       
203000*----------------------------------------------------------------*        
203100*    READ THE LIST QUEUE RECORD.                                 *        
203200*----------------------------------------------------------------*        
203300                                                                          
203400 6105-READ-SEL-QUEUE.                                                     
203500                                                                          
203600     EXEC CICS                                                            
203700          READQ TS                                                        
203800              QUEUE (ASC-SEL-QUEUE-NAME)                                  
203900              INTO  (TR112-PO-SELECT-RECORD)                              
204000              ITEM  (PV-SEL-QUEUE-ITEM)                                   
204100              RESP  (PV-CICS-RESP)                                        
204200     END-EXEC.                                                            
204300*                                                                         
204400     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
204500         CONTINUE                                                         
204600     ELSE                                                                 
204700         SET DP013-CICS-ABEND      TO  TRUE                               
204800         SET DP013-NO-ROLLBACK     TO  TRUE                               
204900         MOVE '6105-READ-SEL-QUEUE'                                       
205000                                   TO  DP013-PARAGRAPH                    
205100         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
205200                                   TO  DP013-MESSAGE-TEXT(1)              
205300         PERFORM DP013-0000-PROCESS-ABEND                                 
205400     END-IF.                                                              
205500 EJECT                                                                    
205600*----------------------------------------------------------------*        
205700*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
205800*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
205900*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
206000*----------------------------------------------------------------*        
206100                                                                          
206200 6110-PROCESS-ITEMS.                                                      
206300                                                                          
206400     COMPUTE PV-ITEM = 1 + ((TR112-ITEM (TR112-IDX) - 1)                  
206500                     / PC-ITEMS-PER-PANEL).                               
206600                                                                          
206700     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
206800         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
206900             PERFORM 6111-WRITE-LIST-QUEUE                                
207000         END-IF                                                           
207100         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
207200                                                                          
207300         PERFORM 6112-READ-LIST-QUEUE                                     
207400         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
207500             CONTINUE                                                     
207600         END-IF                                                           
207700     END-IF.                                                              
207800                                                                          
207900     COMPUTE PV-SUB = (TR112-ITEM (TR112-IDX)                             
208000                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
208100     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
208200     MOVE LQW-SELECTED-SW (PV-SUB)                                        
208300                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
208400     EVALUATE TRUE                                                        
208500         WHEN TR112-INQ (TR112-IDX)                                       
208600              MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
208700                                                                          
208800         WHEN TR112-UPD (TR112-IDX)                                       
208900              MOVE PC-UPD      TO LQW-ACTION (PV-SUB)                     
209000                                                                          
209100         WHEN TR112-DEL (TR112-IDX)                                       
209200              MOVE PC-DEL      TO LQW-ACTION (PV-SUB)                     
209300                                                                          
209400         WHEN TR112-ERR (TR112-IDX)                                       
209500              MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
209600     END-EVALUATE.                                                        
209700                                                                          
209800     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
209900                                                                          
210000*----------------------------------------------------------------*        
210100*    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
210200*----------------------------------------------------------------*        
210300                                                                          
210400 6111-WRITE-LIST-QUEUE.                                                   
210500                                                                          
210600     EXEC CICS                                                            
210700         WRITEQ TS                                                        
210800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
210900             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
211000             ITEM  (PV-SAVE-ITEM)                                         
211100             REWRITE                                                      
211200             RESP  (PV-CICS-RESP)                                         
211300     END-EXEC.                                                            
211400*                                                                         
211500     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
211600         CONTINUE                                                         
211700     ELSE                                                                 
211800         SET DP013-CICS-ABEND  TO  TRUE                                   
211900         SET DP013-NO-ROLLBACK TO  TRUE                                   
212000         MOVE '6111-WRITE-LIST-QUEUE'                                     
212100                               TO  DP013-PARAGRAPH                        
212200         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
212300                               TO  DP013-MESSAGE-TEXT(1)                  
212400         PERFORM DP013-0000-PROCESS-ABEND                                 
212500     END-IF.                                                              
212600                                                                          
212700 EJECT                                                                    
212800*----------------------------------------------------------------*        
212900*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
213000*----------------------------------------------------------------*        
213100                                                                          
213200 6112-READ-LIST-QUEUE.                                                    
213300                                                                          
213400     EXEC CICS                                                            
213500         READQ TS                                                         
213600             QUEUE (ASC-LIST-QUEUE-NAME)                                  
213700             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
213800             ITEM  (PV-ITEM)                                              
213900             RESP  (PV-CICS-RESP)                                         
214000     END-EXEC.                                                            
214100*                                                                         
214200     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
214300         CONTINUE                                                         
214400     ELSE                                                                 
214500         SET DP013-CICS-ABEND        TO TRUE                              
214600         SET DP013-NO-ROLLBACK       TO TRUE                              
214700         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
214800         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
214900                                     TO DP013-MESSAGE-TEXT(1)             
215000         PERFORM DP013-0000-PROCESS-ABEND                                 
215100     END-IF.                                                              
215200                                                                          
215300/                                                                         
215400 7000-SELECT-TIMCSEN.                                                     
215500     EXEC SQL                                                             
215600     SELECT                                                               
215700         CASE                                                             
215800           FILD_TMST                                                      
215900         WHEN '0001-01-01-00.00.00.000000'                                
215901         THEN '          '                                                
215902         ELSE                                                             
215903           CHAR(DATE(FILD_TMST),USA)                                      
215904         END                                                              
215905                                                                          
215910       , CASE                                                             
216000           REJ_TMST                                                       
216001         WHEN '0001-01-01-00.00.00.000000'                                
216002         THEN '          '                                                
216003         ELSE                                                             
216004           CHAR(DATE(REJ_TMST),USA)                                       
216005         END                                                              
216020                                                                          
216100         , REJ_DESC                                                       
216110                                                                          
216221       , CASE                                                             
216222           EST_CLR_TMST                                                   
216223         WHEN '0001-01-01-00.00.00.000000'                                
216224         THEN '          '                                                
216225         ELSE                                                             
216226           CHAR(DATE(EST_CLR_TMST),USA)                                   
216227         END                                                              
216228                                                                          
216229       , CASE                                                             
216230           ACTL_CLR_TMST                                                  
216231         WHEN '0001-01-01-00.00.00.000000'                                
216232         THEN '          '                                                
216233         ELSE                                                             
216234           CHAR(DATE(ACTL_CLR_TMST),USA)                                  
216235         END                                                              
216236                                                                          
216240       , CASE                                                             
216300           EXM_TMST                                                       
216301         WHEN '0001-01-01-00.00.00.000000'                                
216302         THEN '          '                                                
216303         ELSE                                                             
216304           CHAR(DATE(EXM_TMST),USA)                                       
216310         END                                                              
216320                                                                          
216340       , CASE                                                             
216400           LQDN_TMST                                                      
216401         WHEN '0001-01-01-00.00.00.000000'                                
216402         THEN '          '                                                
216403         ELSE                                                             
216404           CHAR(DATE(LQDN_TMST),USA)                                      
216410         END                                                              
216420                                                                          
216430       , CASE                                                             
216500           LQDN_SSPNN_TMST                                                
216501         WHEN '0001-01-01-00.00.00.000000'                                
216502         THEN '          '                                                
216503         ELSE                                                             
216504           CHAR(DATE(LQDN_SSPNN_TMST),USA)                                
216510         END                                                              
216520                                                                          
216600         , LQDN_SSPNN_DESC                                                
216700         , BKR_REF_NBR                                                    
216810         , ENT_ID                                                         
216820         , MOM_DEST_POE_CDE                                               
216830         , MOM_TRIP_TYP_CDE                                               
216840         , ENTR_TYP_CDE                                                   
216900     INTO                                                                 
217000           :ASC-FILD-DATE                                                 
217100         , :ASC-REJ-DATE                                                  
217200         , :ASC-REJ-DESC                                                  
217310         , :ASC-EST-CLR-DATE                                              
217320         , :ASC-ACTL-CLR-DATE                                             
217400         , :ASC-EXM-DATE                                                  
217500         , :ASC-LQDN-DATE                                                 
217600         , :ASC-LQDN-SSPNN-DATE                                           
217700         , :ASC-LQDN-SSPNN-DESC                                           
217800         , :ASC-BKR-REF-NBR                                               
217900         , :ASC-KEY-ENT-ID                                                
217910         , :ASC-POE-CODE                                                  
217920         , :ASC-MOM-TRIP-TYP-CDE                                          
217930         , :ASC-ENTR-TYP-CDE                                              
218000     FROM TIMCSEN                                                         
218100     WHERE CSTM_ENTR_ID               = :DK-CSTM-ENTRY-ID                 
218110**   AND   ENT_ID                     = :DK-ENT-ID                        
218200     END-EXEC.                                                            
218300     EVALUATE TRUE                                                        
218400         WHEN SQLCODE = ZERO                                              
218500         WHEN SQLCODE = +100                                              
218700              CONTINUE                                                    
218800         WHEN SQLCODE = -904                                              
218900         WHEN SQLCODE = -911                                              
219000         WHEN SQLCODE = -913                                              
219100             PERFORM 9900-SOFT-ABORT                                      
219200         WHEN SQLWARN0 NOT = SPACE                                        
219300         WHEN SQLCODE NOT = ZERO                                          
219400              MOVE '7000-SELECT-TIMCSEN'                                  
219500                               TO DP013-PARAGRAPH                         
219600         MOVE 'SELECT CUSTOMS ENTRY DATA'                                 
219700                               TO DP013-MESSAGE-TEXT (1)                  
219800         MOVE SQLCA            TO DP013-SQLCA                             
219900         SET DP013-DB2-ABEND                                              
220000                               TO TRUE                                    
220100         PERFORM DP013-0000-PROCESS-ABEND                                 
220200     END-EVALUATE.                                                        
220300                                                                          
220400/                                                                         
220500 7100-SELECT-TENTNME.                                                     
220600     INITIALIZE DCLTENTNME                                                
220700     MOVE ASC-KEY-ENT-ID              TO ENTNME-ENT-ID                    
220800     EXEC SQL                                                             
220900         SELECT                                                           
221000             ENT_NAME_DESC                                                
221100         INTO                                                             
221200              :ENTNME-ENT-NAME-DESC                                       
221300         FROM                                                             
221400             TENTNME                                                      
221500         WHERE                                                            
221600            ENT_ID = :ASC-KEY-ENT-ID                                      
221700            AND TYPE_CDE       = '01'                                     
221800     END-EXEC.                                                            
221900     EVALUATE TRUE                                                        
222000         WHEN SQLCODE = ZERO                                              
222100         WHEN SQLCODE = +100                                              
222200              CONTINUE                                                    
222300         WHEN SQLCODE = -904                                              
222400         WHEN SQLCODE = -911                                              
222500         WHEN SQLCODE = -913                                              
222600             PERFORM 9900-SOFT-ABORT                                      
222700         WHEN SQLWARN0 NOT = SPACE                                        
222800         WHEN SQLCODE NOT = ZERO                                          
222900              MOVE '7100-SELECT-TENTNME'                                  
223000                               TO DP013-PARAGRAPH                         
223100         MOVE 'SELECT CUSTOMS BROKER NAME'                                
223200                               TO DP013-MESSAGE-TEXT (1)                  
223300         MOVE SQLCA            TO DP013-SQLCA                             
223400         SET DP013-DB2-ABEND                                              
223500                               TO TRUE                                    
223600         PERFORM DP013-0000-PROCESS-ABEND                                 
223700     END-EVALUATE.                                                        
223800/                                                                         
223900 9100-READ-SEL-QUEUE.                                                     
224000     EXEC CICS                                                            
224100         READQ TS                                                         
224200             QUEUE (ASC-IN-SEL-QUEUE-NAME)                                
224300             INTO  (TR112-PO-SELECT-RECORD)                               
224400             ITEM  (ASC-SEL-ITEM)                                         
224500             RESP  (PV-CICS-RESP)                                         
224600     END-EXEC.                                                            
224700                                                                          
224800     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
224900         CONTINUE                                                         
225000     ELSE                                                                 
225100         SET DP013-LOGIC-ABEND                                            
225200             DP013-NO-ROLLBACK     TO TRUE                                
225300         MOVE '9100-READ-SEL-QUEUE'                                       
225400                                   TO DP013-PARAGRAPH                     
225500         MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
225600                                   TO DP013-MESSAGE-TEXT (1)              
225700         PERFORM DP013-0000-PROCESS-ABEND                                 
225800     END-IF.                                                              
225900/                                                                         
226000 9200-REWRITE-LIST-QUEUE.                                                 
226100     EXEC CICS                                                            
226200          WRITEQ TS                                                       
226300              QUEUE  (ASC-IN-SEL-QUEUE-NAME)                              
226400              FROM   (TR112-PO-SELECT-RECORD)                             
226500              ITEM   (ASC-SEL-ITEM)                                       
226600              RESP   (PV-CICS-RESP)                                       
226700              REWRITE                                                     
226800     END-EXEC.                                                            
226900                                                                          
227000     IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                           
227100         SET DP013-CICS-ABEND                                             
227200             DP013-NO-ROLLBACK     TO TRUE                                
227300         MOVE '9200-REWRITE-LIST-QUEUE'                                   
227400                                   TO DP013-PARAGRAPH                     
227500         MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
227600                                   TO DP013-MESSAGE-TEXT (1)              
227700         PERFORM DP013-0000-PROCESS-ABEND                                 
227800     END-IF.                                                              
227900/                                                                         
228000 9900-SOFT-ABORT.                                                 RCKCS215
228100     SET PS-ERROR        TO TRUE.                                 RCKCS215
228200     SET DP020-MSG-FATAL TO TRUE.                                 RCKCS215
228300     MOVE PC-TSYMSG-00551 TO DP020-MSG-NUMBER.                    RCKCS215
228400     SET DP020-NEXT-ACT-APPL-ERROR TO TRUE.                       RCKCS215
228500     MOVE 'PROGRAM EXIT CALL' TO DP013-MESSAGE-TEXT (1).          RCKCS215
228600     PERFORM 0001-CALL-CICS-ARCH-API.                             RCKCS215
228700/                                                                         
228800*----------------------------------------------------------------*        
228900*    ABEND PROCESSOR MODULE                                      *        
229000*----------------------------------------------------------------*        
229100                                                                          
229200     COPY DPPD013.                                                        
