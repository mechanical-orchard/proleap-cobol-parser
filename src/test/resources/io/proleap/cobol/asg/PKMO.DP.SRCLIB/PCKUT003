000100 TITLE 'COPY PC.MERCHANDISE.ADJ.ERROR.Q TO A FLAT FILE'.                  
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.      PCKUT003.                                               
000400 AUTHOR.          DENNIS STEFFEN.                                         
000500 INSTALLATION.    KOHLS DEPARTMENT STORES.                                
000600 DATE-WRITTEN.    EARLY WINTER 2004.                                      
000700 ENVIRONMENT DIVISION.                                                    
000800                                                                          
000900 CONFIGURATION SECTION.                                                   
001000                                                                          
001100 SOURCE-COMPUTER.       IBM-3090.                                         
001200 OBJECT-COMPUTER.       IBM-3090.                                         
001300                                                                          
001400 INPUT-OUTPUT SECTION.                                                    
001500                                                                          
001600 FILE-CONTROL.                                                            
001700     SELECT CONTROL-CARD-1                                                
001800            ASSIGN TO INP01.                                              
001900     SELECT CONTROL-CARD-2                                                
002000            ASSIGN TO INP02.                                              
002100     SELECT CC-PARM-FILE                                                  
002200            ASSIGN TO INP03.                                              
002300                                                                          
002400     SELECT ERR-EXTRACT-FILE                                              
002500            ASSIGN TO OUT01.                                              
002600     SELECT LOG-EXTRACT-FILE                                              
002700            ASSIGN TO OUT02.                                              
002800                                                                          
003000 DATA DIVISION.                                                           
003200 FILE SECTION.                                                            
003300                                                                          
003400 FD  CONTROL-CARD-1                                                       
003500     RECORDING MODE F                                                     
003600     BLOCK CONTAINS 0 RECORDS                                             
003700     LABEL RECORDS ARE OMITTED.                                           
003800 01  CONTROL-CARD-1-LINE              PIC  X(80).                         
003900                                                                          
004000 FD  CONTROL-CARD-2                                                       
004100     RECORDING MODE F                                                     
004200     BLOCK CONTAINS 0 RECORDS                                             
004300     LABEL RECORDS ARE OMITTED.                                           
004400 01  CONTROL-CARD-2-LINE              PIC X(80).                          
004500                                                                          
004600 FD  CC-PARM-FILE                                                         
004700     RECORDING MODE F                                                     
004800     BLOCK CONTAINS 0 RECORDS                                             
004900     LABEL RECORDS ARE OMITTED.                                           
005000 01  CC-PARM-RECORD                   PIC X(80).                          
005100                                                                          
005200 FD  ERR-EXTRACT-FILE                                                     
005300     RECORDING MODE F                                                     
005400     BLOCK CONTAINS 0 RECORDS                                             
005500     LABEL RECORDS ARE OMITTED.                                           
005600 01  ERR-EXTRACT-RECORD               PIC X(0200).                        
005700                                                                          
005800 FD  LOG-EXTRACT-FILE                                                     
005900     RECORDING MODE F                                                     
006000     BLOCK CONTAINS 0 RECORDS                                             
006100     LABEL RECORDS ARE OMITTED.                                           
006200 01  LOG-EXTRACT-RECORD               PIC X(0530).                        
006300                                                                          
006400 WORKING-STORAGE SECTION.                                                 
006500                                                                          
006600 01  CC-CONTROL-CARD-1.                                                   
006700     05  CC-QMGR-NAME              PIC X(48).                             
006800     05  FILLER                    PIC X(32).                             
006900                                                                          
007000 01  CC-CONTROL-CARD-2.                                                   
007100     05  CC-QNAME                  PIC X(48).                             
007200     05  FILLER                    PIC X(32).                             
007300                                                                          
007400 01  CC-PARM-REC.                                                         
007500*    TYPES ARE 'LOG' AND 'ERR'.  BLANK DEFAULTS TO 'ALL'                  
007600     05  CC-PARM-MSG-TYPE          PIC X(03).                             
007700         88  CC-PARM-ALL                 VALUE 'ALL'.                     
007800         88  CC-PARM-LOG                 VALUE 'LOG'.                     
007900         88  CC-PARM-ERR                 VALUE 'ERR'.                     
007900         88  CC-PARM-END                 VALUE 'END'.                     
008000     05  CC-PARM-MQ-KEEP-DELETE    PIC X(06).                             
008100         88  CC-PARM-KEEP                VALUE 'KEEP  '.                  
008200         88  CC-PARM-DELETE              VALUE 'DELETE'.                  
008300     05  FILLER                    PIC X(71).                             
008400                                                                          
008500     05  PC-PGM-NAME               PIC X(09) VALUE 'PCKUT003 '.           
008600     05  PC-FIFTY-STARS            PIC X(50) VALUE ALL '*'.               
008700                                                                          
008800     COPY PCRD013.                                                        
008900     COPY PCRD014.                                                        
009000/                                                                         
009100 01  PS-PROGRAM-SWITCHES.                                                 
009200     05  PS-ERROR-SW                  PIC X(01) VALUE 'N'.                
009300         88  PS-ERRORS-YES                      VALUE 'Y'.                
009400         88  PS-ERRORS-NO                       VALUE 'N'.                
009500     05  PS-FILES-OPEN-SW             PIC X(01) VALUE 'N'.                
009600         88  PS-FILES-OPEN-YES                  VALUE 'Y'.                
009700                                                                          
009800     05  PS-END-ERROR-QUE-SW          PIC X(01) VALUE 'N'.                
009900         88  PS-END-ERROR-QUE-YES               VALUE 'Y'.                
010000         88  PS-END-ERROR-QUE-NO                VALUE 'N'.                
010100     05  PS-END-LOG-QUE-SW            PIC X(01) VALUE 'N'.                
010200         88  PS-END-LOG-QUE-YES                 VALUE 'Y'.                
010300         88  PS-END-LOG-QUE-NO                  VALUE 'N'.                
010400                                                                          
010500 01  WS-MQCONN               PIC X(8)  VALUE 'CSQBCONN'.                  
010600 01  WS-MQOPEN               PIC X(8)  VALUE 'CSQBOPEN'.                  
010700 01  WS-MQGET                PIC X(8)  VALUE 'CSQBGET'.                   
010800 01  WS-MQCLOSE              PIC X(8)  VALUE 'CSQBCLOS'.                  
010900 01  WS-MQDISC               PIC X(8)  VALUE 'CSQBDISC'.                  
011000 01  WS-MQCMIT               PIC X(8)  VALUE 'CSQBCOMM'.                  
011100 01  WS-MQBACK               PIC X(8)  VALUE 'CSQBBACK'.                  
011200*                                                                         
011300*    GENERAL WORK FIELDS                                                  
011400*                                                                         
011500 01  PV-CRAPOLA.                                                          
011600     05  NUMRECS              PIC 9(9)  VALUE ZEROS.                      
011700     05  NUMGETS              PIC S9(9) BINARY VALUE 0.                   
011800     05  PV-ERROR-COUNT       PIC 9(9)  VALUE ZEROS.                      
011900     05  PV-LOG-COUNT         PIC 9(9)  VALUE ZEROS.                      
012000     05  NUMCMTS              PIC 9(9)  VALUE ZEROS.                      
012100     05  ERROR-MESSAGE        PIC X(48) VALUE SPACES.                     
012200     05  PV-PARAGRAPH         PIC X(48) VALUE SPACES.                     
012300*                                                                         
012400*   PARAMETER VARIABLES                                                   
012500*                                                                         
012600 01  MQ-STUFF.                                                            
012700     05  QM-NAME            PIC X(48).                                    
012800     05  QNAME              PIC X(48).                                    
012900     05  PV-CORRELID        PIC X(03).                                    
013000     05  PV-CORRELID-SAVE   PIC X(03).                                    
013100     05  PV-SAVE-MQOO-BROWSE          PIC S9(9) BINARY VALUE 8.           
013200     05  PADCHAR            PIC X(1) VALUE '*'.                           
013300     05  MSGBUFFER          PIC X(1000000) VALUE SPACES.                  
013400     05  NUMMSGS            PIC S9(9) BINARY VALUE 1.                     
013500     05  MSGLENGTH          PIC S9(9) BINARY VALUE +1000000.              
013600     05  DATA-LENGTH        PIC S9(9) BINARY.                             
013700     05  MQCHECK            PIC X(8) VALUE 'MQR2C'.                       
013800     05  MQ-REASON-TEXT     PIC X(30) VALUE SPACES.                       
013900     05  HOLD-REASON        PIC S9(9) BINARY.                             
014000     05  HOLD-COMPLETION-CODE PIC S9(9) BINARY.                           
014100                                                                          
014200*                                                                         
014300 01  HCONN                   PIC S9(9) BINARY VALUE 0.                    
014400 01  GQ-HANDLE               PIC S9(9) BINARY VALUE 0.                    
014500 01  OPEN-OPTIONS            PIC S9(9) BINARY.                            
014600 01  COMPLETION-CODE         PIC S9(9) BINARY.                            
014700 01  REASON                  PIC S9(9) BINARY.                            
014800 01  CON-REASON              PIC S9(9) BINARY.                            
014900                                                                          
015000 01  PV-ABEND-VARIABLES.                                                  
015100     05  PV-ABEND-CODE              PIC S9(04) VALUE +0 COMP SYNC.        
015200     88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
015300     88  ABEND-4002-READ-ERROR                       VALUE +4002.         
015400     88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
015500     88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
015600     88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
015700     88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
015800     88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
015900     88  ABEND-4008-DATA-ERROR                       VALUE +4008.         
016000     88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009.         
016100     88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
016200     88  ABEND-4019-CAL-SUB-ERROR                    VALUE +4019.         
016300     88  ABEND-4020-MQ-ERROR                         VALUE +4020.         
016400     88  ABEND-4444-FORCED-ABEND                     VALUE +4444.         
016500                                                                          
016600                                                                          
016700 01  PE-MQ-ABEND-VARIABLES.                                               
016800     10  PE-MQ-QMANAGER           PIC X(05) VALUE SPACES.                 
016900     10  PE-MQ-QNAME              PIC X(30) VALUE SPACES.                 
017000     10  PE-MQ-COMPLETION-CODE    PIC 9(04) VALUE ZEROS.                  
017100     10  PE-MQ-REASON-CODE        PIC 9(04) VALUE ZEROS.                  
017200                                                                          
017300*                                                                         
017400*    API CONTROL BLOCKS                                                   
017500*                                                                         
017600 01  MQM-OBJECT-DESCRIPTOR.                                               
017700     COPY CMQODV.                                                         
017800 01  MQM-MESSAGE-DESCRIPTOR.                                              
017900     COPY CMQMDV.                                                         
018000 01  MQM-GET-MESSAGE-OPTIONS.                                             
018100     COPY CMQGMOV.                                                        
018200*                                                                         
018300*    MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)           
018400*    AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)                  
018500*                                                                         
018600 01  MQM-CONSTANTS.                                                       
018700     COPY CMQV.                                                           
018800*                                                                         
018900/                                                                         
019000 PROCEDURE DIVISION.                                                      
019100                                                                          
019200     PERFORM 1000-INITIALIZE.                                             
019300                                                                          
019400     PERFORM 2000-PROCESS-QUEUE UNTIL                                     
019500                   (PS-END-LOG-QUE-YES AND PS-END-ERROR-QUE-YES)          
019600                     OR PS-ERRORS-YES                                     
019700                                                                          
019800     PERFORM 9000-EOJ.                                                    
019900                                                                          
020000     GOBACK.                                                              
020100                                                                          
020200 1000-INITIALIZE.                                                         
020300                                                                          
020400      OPEN INPUT CONTROL-CARD-1.                                          
020500      READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1                          
020600         AT END                                                           
020700             DISPLAY PC-PGM-NAME                                          
020800              'CONTROL CARD MISSING - FOR QMGR NAME'                      
020900               SET PS-ERRORS-YES      TO TRUE                             
021000         NOT AT END                                                       
021100             DISPLAY PC-PGM-NAME                                          
021200                     '- '                                                 
021300                     PC-FIFTY-STARS                                       
021400             DISPLAY PC-PGM-NAME                                          
021500                     '- CONTROL CARD: '                                   
021600                      CC-CONTROL-CARD-1                                   
021700     END-READ.                                                            
021800                                                                          
021900      CLOSE CONTROL-CARD-1.                                               
022000                                                                          
022100      OPEN INPUT CONTROL-CARD-2.                                          
022200      READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                          
022300          AT END                                                          
022400              DISPLAY PC-PGM-NAME                                         
022500             'CONTROL CARD MISSING - MSG QUEUE NAME'                      
022600               SET PS-ERRORS-YES      TO TRUE                             
022700         NOT AT END                                                       
022800             DISPLAY PC-PGM-NAME                                          
022900                  '- '                                                    
023000                  PC-FIFTY-STARS                                          
023100             DISPLAY PC-PGM-NAME                                          
023200                  '- CONTROL CARD: '                                      
023300                  CC-CONTROL-CARD-2                                       
023400      END-READ.                                                           
023500                                                                          
023600     CLOSE CONTROL-CARD-2.                                                
023700                                                                          
023800     OPEN INPUT CC-PARM-FILE                                              
023900     READ CC-PARM-FILE INTO CC-PARM-REC                                   
024000     AT END                                                               
024100         SET CC-PARM-ALL                                                  
024200             CC-PARM-DELETE           TO TRUE                             
024300     NOT AT END                                                           
024400         EVALUATE CC-PARM-MSG-TYPE                                        
024500         WHEN 'ALL'                                                       
024600         WHEN 'LOG'                                                       
024600         WHEN 'ERR'                                                       
024600         WHEN 'END'                                                       
024700             MOVE CC-PARM-MSG-TYPE    TO PV-CORRELID-SAVE                 
024800         WHEN OTHER                                                       
024900             SET PS-ERRORS-YES        TO TRUE                             
025000             DISPLAY ' *** INVALID MSG TYPE PARM *** '                    
025100                                    CC-PARM-MSG-TYPE                      
025200         END-EVALUATE                                                     
025300         IF PS-ERRORS-NO                                                  
025400                 EVALUATE TRUE                                            
025500                 WHEN CC-PARM-KEEP                                        
025600                 WHEN CC-PARM-DELETE                                      
025700                     CONTINUE                                             
025800                 WHEN OTHER                                               
025900                 SET PS-ERRORS-YES    TO TRUE                             
026000                 DISPLAY ' *** INVALID PARM *** '                         
026100                                  CC-PARM-MQ-KEEP-DELETE                  
026200             END-EVALUATE                                                 
026300         END-IF                                                           
026400     END-READ.                                                            
026500                                                                          
026600     CLOSE CC-PARM-FILE                                                   
026700                                                                          
026800     IF PS-ERRORS-NO                                                      
026900         OPEN OUTPUT ERR-EXTRACT-FILE                                     
027000                     LOG-EXTRACT-FILE                                     
027100         SET PS-FILES-OPEN-YES        TO TRUE                             
027200                                                                          
027300         INITIALIZE ERR-EXTRACT-RECORD                                    
027400                    LOG-EXTRACT-RECORD                                    
027500     END-IF.                                                              
027600                                                                          
027700     IF PS-ERRORS-NO                                                      
027800         PERFORM 8000-CONNECT-Q-MGR                                       
027900     END-IF                                                               
028000                                                                          
028100     IF PS-ERRORS-NO                                                      
028200         PERFORM 8050-OPEN-Q-FOR-INPUT                                    
028300     END-IF.                                                              
028400/                                                                         
028500 2000-PROCESS-QUEUE.                                                      
028600                                                                          
028700     DISPLAY ' PV-CORRELID-SAVE=' PV-CORRELID-SAVE                        
028800                                                                          
028900     EVALUATE PV-CORRELID-SAVE                                            
029000     WHEN 'LOG'                                                           
029100         MOVE 'LOG'                   TO PV-CORRELID                      
029200         PERFORM UNTIL PS-END-LOG-QUE-YES OR PS-ERRORS-YES                
029300             PERFORM 8100-GET-MSG                                         
029400             IF PS-END-LOG-QUE-NO AND PS-ERRORS-NO                        
029500                 PERFORM 8900-WRITE-LOG-RECORD                            
029600             END-IF                                                       
029700         END-PERFORM                                                      
029800     WHEN 'ERR'                                                           
029900         MOVE 'ERR'                   TO PV-CORRELID                      
030000         PERFORM UNTIL PS-END-ERROR-QUE-YES OR PS-ERRORS-YES              
030100             PERFORM 8100-GET-MSG                                         
030200             IF PS-END-ERROR-QUE-NO AND PS-ERRORS-NO                      
030300                 PERFORM 8910-WRITE-ERR-RECORD                            
030400             END-IF                                                       
030500         END-PERFORM                                                      
030600     WHEN 'ALL'                                                           
      *        EXTRACT EACH TYPE OF MESSAGE IN TURN                             
030700         MOVE 'LOG'                   TO PV-CORRELID                      
030800         PERFORM UNTIL PS-END-LOG-QUE-YES OR PS-ERRORS-YES                
030900             PERFORM 8100-GET-MSG                                         
031000             IF PS-END-LOG-QUE-NO AND PS-ERRORS-NO                        
031100                 PERFORM 8900-WRITE-LOG-RECORD                            
031200             END-IF                                                       
031300         END-PERFORM                                                      
031400                                                                          
031000         SET PS-END-LOG-QUE-NO        TO TRUE                             
031500         MOVE 'ERR'                   TO PV-CORRELID                      
031600         PERFORM UNTIL PS-END-ERROR-QUE-YES OR PS-ERRORS-YES              
031700             PERFORM 8100-GET-MSG                                         
031800             IF PS-END-ERROR-QUE-NO AND PS-ERRORS-NO                      
031900                 PERFORM 8910-WRITE-ERR-RECORD                            
032000             END-IF                                                       
032100         END-PERFORM                                                      
                                                                                
031000         SET PS-END-LOG-QUE-NO        TO TRUE                             
031500         MOVE 'END'                   TO PV-CORRELID                      
031600         PERFORM UNTIL PS-END-LOG-QUE-YES OR PS-ERRORS-YES                
031700             PERFORM 8100-GET-MSG                                         
031000             IF PS-END-LOG-QUE-NO AND PS-ERRORS-NO                        
031100                 PERFORM 8900-WRITE-LOG-RECORD                            
031200             END-IF                                                       
032100         END-PERFORM                                                      
                                                                                
029000     WHEN 'END'                                                           
030700         MOVE 'END'                   TO PV-CORRELID                      
030800         PERFORM UNTIL PS-END-LOG-QUE-YES OR PS-ERRORS-YES                
030900             PERFORM 8100-GET-MSG                                         
031000             IF PS-END-LOG-QUE-NO AND PS-ERRORS-NO                        
031100                 PERFORM 8900-WRITE-LOG-RECORD                            
031200             END-IF                                                       
031300         END-PERFORM                                                      
031400                                                                          
032200     END-EVALUATE.                                                        
032300/                                                                         
032400 8000-CONNECT-Q-MGR.                                                      
032500*    CONNECT TO THE QUEUE MANAGER                                         
032600                                                                          
032700     MOVE CC-QMGR-NAME TO QM-NAME.                                        
032800     CALL WS-MQCONN USING QM-NAME                                         
032900                         HCONN                                            
033000                         COMPLETION-CODE                                  
033100                         REASON.                                          
033200                                                                          
033300*    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE                      
033400*    AND EXIT                                                             
033500                                                                          
033600     MOVE REASON TO CON-REASON.                                           
033700     IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
033800        MOVE 'MQCONN'   TO ERROR-MESSAGE                                  
033900        PERFORM 9400-DISPLAY-ERROR-MESSAGE                                
034000         SET PS-ERRORS-YES            TO TRUE                             
034100     END-IF.                                                              
034200     DISPLAY 'MQCONN SUCCESSFUL TO ', QM-NAME.                            
034300                                                                          
034400 8050-OPEN-Q-FOR-INPUT.                                                   
034500*    OPEN THE QUEUE FOR INPUT                                             
034600*                                                                         
034700     EVALUATE TRUE                                                        
034800     WHEN CC-PARM-KEEP                                                    
034900*        QUEUE RECORDS ARE NOT DESTROYED                                  
035000         COMPUTE OPEN-OPTIONS =   MQOO-BROWSE                             
035100                                + MQOO-FAIL-IF-QUIESCING                  
035200     WHEN CC-PARM-DELETE                                                  
035300*        QUEUE RECORDS WILL BE DESTROYED                                  
035400         COMPUTE OPEN-OPTIONS =   MQOO-INPUT-SHARED                       
035500                                + MQOO-OUTPUT                             
035600                                + MQOO-FAIL-IF-QUIESCING                  
035700     END-EVALUATE.                                                        
035800                                                                          
035900     MOVE CC-QNAME                    TO MQOD-OBJECTNAME.                 
036000     MOVE QM-NAME                     TO MQOD-OBJECTQMGRNAME.             
036100*                                                                         
036200     CALL WS-MQOPEN USING HCONN                                           
036300                         MQM-OBJECT-DESCRIPTOR                            
036400                         OPEN-OPTIONS                                     
036500                         GQ-HANDLE                                        
036600                         COMPLETION-CODE                                  
036700                         REASON.                                          
036800*                                                                         
036900*    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                            
037000*    AND EXIT.                                                            
037100*                                                                         
037200     IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
037300        MOVE 'MQOPEN'   TO ERROR-MESSAGE                                  
037400        PERFORM 9400-DISPLAY-ERROR-MESSAGE                                
037500        GO TO 8300-DISCONNECT-QMGR                                        
037600     END-IF.                                                              
037700     DISPLAY 'MQOPEN SUCCESSFUL'.                                         
037800                                                                          
037900 8100-GET-MSG.                                                            
038000                                                                          
038100     MOVE MQMI-NONE TO MQMD-MSGID                                         
038200     MOVE PV-CORRELID         TO MQMD-CORRELID                            
034600*                                                                         
034700     EVALUATE TRUE                                                        
034800     WHEN CC-PARM-KEEP                                                    
034900*        QUEUE RECORDS ARE NOT DESTROYED                                  
038500         COMPUTE MQGMO-OPTIONS  =   MQGMO-NO-SYNCPOINT                    
038600                                  + MQGMO-CONVERT                         
038700                                  + MQGMO-WAIT                            
135400                                  + MQGMO-BROWSE-NEXT                     
038800                                  + MQGMO-FAIL-IF-QUIESCING               
035200     WHEN CC-PARM-DELETE                                                  
035300*        QUEUE RECORDS WILL BE DESTROYED                                  
038500         COMPUTE MQGMO-OPTIONS  =   MQGMO-SYNCPOINT                       
038600                                  + MQGMO-CONVERT                         
038700                                  + MQGMO-WAIT                            
038800                                  + MQGMO-FAIL-IF-QUIESCING               
035700     END-EVALUATE.                                                        
035800                                                                          
038400     MOVE +550                    TO MSGLENGTH                            
038900                                                                          
039000     CALL WS-MQGET USING HCONN                                            
039100                         GQ-HANDLE                                        
039200                         MQM-MESSAGE-DESCRIPTOR                           
039300                         MQM-GET-MESSAGE-OPTIONS                          
039400                         MSGLENGTH                                        
039500                         MSGBUFFER                                        
039600                         DATA-LENGTH                                      
039700                         COMPLETION-CODE                                  
039800                         REASON                                           
039900                                                                          
040000*        IF GET FAILED THEN DISPLAY ERROR MESSAGE                         
040100*        AND BREAK OUT OF LOOP                                            
040200                                                                          
040300         EVALUATE TRUE                                                    
040400         WHEN COMPLETION-CODE = MQCC-OK                                   
040500             ADD 1                    TO NUMRECS                          
040600             IF NUMRECS > 1000                                            
040700                 PERFORM 8150-MQ-COMMIT                                   
040800                 MOVE ZERO TO NUMRECS                                     
040900             END-IF                                                       
041000             ADD 1 TO NUMGETS                                             
041100         WHEN OTHER                                                       
041200             IF REASON = MQRC-NO-MSG-AVAILABLE                            
041300                 IF PV-CORRELID = 'LOG' OR 'END'                          
041400                     SET PS-END-LOG-QUE-YES                               
041600                                      TO TRUE                             
041700                 ELSE                                                     
041800                     SET PS-END-ERROR-QUE-YES                             
042000                                      TO TRUE                             
042100                 END-IF                                                   
042200             ELSE                                                         
042300                 MOVE 'MQGET'     TO ERROR-MESSAGE                        
042400                 SET PS-ERRORS-YES        TO TRUE                         
042500                 PERFORM 9400-DISPLAY-ERROR-MESSAGE                       
042600             END-IF                                                       
042700     END-EVALUATE.                                                        
042800/                                                                         
042900 8150-MQ-COMMIT.                                                          
043000                                                                          
043100     CALL WS-MQCMIT USING HCONN                                           
043200                          COMPLETION-CODE                                 
043300                          REASON.                                         
043400                                                                          
043500     IF (COMPLETION-CODE NOT = MQCC-OK)                                   
043600         CALL MQCHECK USING REASON                                        
043700                               MQ-REASON-TEXT                             
043800        MOVE '3600-COMMIT-MQ'   TO PV-PARAGRAPH                           
043900        MOVE 'MQCMIT ERROR'     TO ERROR-MESSAGE                          
044000        MOVE COMPLETION-CODE    TO HOLD-COMPLETION-CODE                   
044100        MOVE REASON             TO HOLD-REASON                            
044200        PERFORM 8200-MQ-BACKOUT                                           
044300        MOVE HOLD-COMPLETION-CODE TO COMPLETION-CODE                      
044400                                        PE-MQ-COMPLETION-CODE             
044500        MOVE HOLD-REASON          TO REASON                               
044600                                        PE-MQ-REASON-CODE                 
044700        PERFORM 9500-MQ-ABEND                                             
044800     ELSE                                                                 
044900        ADD +1 TO NUMCMTS                                                 
045000     END-IF.                                                              
045100/                                                                         
045200 8200-MQ-BACKOUT.                                                         
045300                                                                          
045400     CALL WS-MQBACK USING HCONN                                           
045500                          COMPLETION-CODE                                 
045600                          REASON.                                         
045700                                                                          
045800     IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
045900         CALL MQCHECK USING REASON                                        
046000                            MQ-REASON-TEXT                                
046100        MOVE '8200-MQ-BACKOUT'        TO PV-PARAGRAPH                     
046200        MOVE 'MQBACK ERROR'           TO ERROR-MESSAGE                    
046300        PERFORM 9500-MQ-ABEND                                             
046400     END-IF.                                                              
046500                                                                          
046600 8250-CLOSE-QUEUE.                                                        
046700     CALL WS-MQCLOSE USING HCONN                                          
046800                          GQ-HANDLE                                       
046900                          MQCO-NONE                                       
047000                          COMPLETION-CODE                                 
047100                          REASON.                                         
047200     IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
047300        MOVE 'MQCLOSE'  TO ERROR-MESSAGE                                  
047400        PERFORM 9400-DISPLAY-ERROR-MESSAGE                                
047500     ELSE                                                                 
047600        DISPLAY 'MQCLOSE SUCCESSFUL'                                      
047700     END-IF.                                                              
047800*                                                                         
047900 8300-DISCONNECT-QMGR.                                                    
048000*                                                                         
048100*    DISCONNECT FROM THE QUEUE MANAGER                                    
048200*                                                                         
048300     IF CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED                 
048400        CALL WS-MQDISC USING HCONN                                        
048500                            COMPLETION-CODE                               
048600                            REASON                                        
048700        IF (COMPLETION-CODE NOT = MQCC-OK) THEN                           
048800           MOVE 'MQDISC'   TO ERROR-MESSAGE                               
048900           PERFORM 9400-DISPLAY-ERROR-MESSAGE                             
049000        ELSE                                                              
049100           DISPLAY 'MQDISC SUCCESSFUL'                                    
049200        END-IF                                                            
049300     END-IF.                                                              
049400                                                                          
049500 8900-WRITE-LOG-RECORD.                                                   
049600     INITIALIZE PC014-MESSAGE-LOG-RECORD                                  
049700     MOVE MSGBUFFER                   TO PC014-MESSAGE-LOG-RECORD         
049800     WRITE LOG-EXTRACT-RECORD FROM PC014-MESSAGE-LOG-RECORD               
049900     ADD +1                           TO PV-LOG-COUNT.                    
050000                                                                          
050100 8910-WRITE-ERR-RECORD.                                                   
050200     INITIALIZE ERR-EXTRACT-RECORD.                                       
050300     MOVE MSGBUFFER                   TO PC013-IT-ERROR-RECORD            
050400     WRITE ERR-EXTRACT-RECORD FROM PC013-IT-ERROR-RECORD                  
050500     ADD +1                           TO PV-ERROR-COUNT.                  
050600/                                                                         
050700 9000-EOJ.                                                                
050800     PERFORM 8250-CLOSE-QUEUE.                                            
050900     PERFORM 8300-DISCONNECT-QMGR.                                        
051000                                                                          
051100     IF PS-FILES-OPEN-YES                                                 
051200         CLOSE  ERR-EXTRACT-FILE                                          
051300                LOG-EXTRACT-FILE                                          
051400     END-IF                                                               
051500                                                                          
051600*    DISPLAY THE NUMBER OF MESSAGES SUCCESSFULLY PUT                      
051700*    TO THE QUEUE                                                         
051800*                                                                         
051900     DISPLAY NUMGETS,      ' MESSAGES READ FROM QUEUE'.                   
052000     DISPLAY PV-LOG-COUNT, ' LOG MESSAGES'.                               
052100     DISPLAY PV-ERROR-COUNT, ' ERROR MESSAGES'.                           
052200     DISPLAY NUMCMTS,      ' NO. OF COMMITS TAKEN'.                       
052300*                                                                         
052400     IF PV-ERROR-COUNT > ZERO                                             
052500        MOVE 4095 TO RETURN-CODE                                          
052600     END-IF.                                                              
052700 9400-DISPLAY-ERROR-MESSAGE.                                              
052800*                                                                         
052900     DISPLAY '************************************************'.          
053000     DISPLAY '* ', ERROR-MESSAGE.                                         
053100     DISPLAY '* COMPLETION CODE : ', COMPLETION-CODE.                     
053200     DISPLAY '* REASON CODE     : ', REASON.                              
053300     DISPLAY '************************************************'.          
053400*                                                                         
053500 9500-MQ-ABEND.                                                           
053600                                                                          
053700     DISPLAY '**** ABEND *************************************'.          
053800     DISPLAY '** MQSERIES ERROR **'.                                      
053900     DISPLAY '** PROGRAM:    ', PC-PGM-NAME.                              
054000     DISPLAY '** ABEND CODE: ', PV-ABEND-CODE.                            
054100     DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH.                             
054200     DISPLAY '** MESSAGE:    ', ERROR-MESSAGE.                            
054300     DISPLAY '** COMP CODE:  ', PE-MQ-COMPLETION-CODE.                    
054400     DISPLAY '** RSN CODE:   ', PE-MQ-REASON-CODE.                        
054500     DISPLAY '** RSN TEXT:   ', MQ-REASON-TEXT.                           
054600                                                                          
054700     MOVE +4013 TO PV-ABEND-CODE.                                         
054800     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
054900                                                                          
