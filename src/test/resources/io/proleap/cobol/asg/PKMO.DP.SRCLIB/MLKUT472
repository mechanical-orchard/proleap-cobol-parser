       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT472.                                         00020021
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09-01-2000.                                       00050021
       DATE-COMPILED.                                                   00060000
                                                                        00061021
      ******************************************************************00070000
      * MLKUT472 - WEEKLY MAJOR CLASS TABLE (TWKMCLC) RECALC EXTRACT    00090021
      *                                                                 00100000
      * THIS PROGRAM WILL CREATE THE EXTRACT FOR THE RECALC PROGRAM     00120000
      * ON TABLE TWKMCLC. IT WILL FIND ALL DEPARTMENT/MAJOR CLASS/CHNL  00130021
      * COMBINATIONS ON THE TWKMCLC DB2 TABLE FOR THE CURRENT AND       00140021
      * PREVIOUS FISCAL WEEKS WHICH WILL BE USED TO PROCESS THE TWKMCLC 00150021
      * TABLE RECALC.                                                   00160000
      ******************************************************************00170000
      * WR/PITS#  DATE      DESCRIPTION                                *00171025
      * -------- --------   -----------------------------------------  *00172025
      * W31475  06/01/2009  COGNIZANT                                  *00173025
      *                     MODIFIED CURSOR DECLARATION FOR MULTI ROW  *00174025
      *                     FETCH AS PART OF ML BATCH PERF TUNE PRJ    *00175025
      ******************************************************************00176025
      *                                                                 00180000
       ENVIRONMENT DIVISION.                                            00190000
       CONFIGURATION SECTION.                                           00210000
       SOURCE-COMPUTER.        IBM-3090.                                00230000
       OBJECT-COMPUTER.        IBM-3090.                                00240000
       INPUT-OUTPUT SECTION.                                            00260000
       FILE-CONTROL.                                                    00270000
                                                                        00280000
           SELECT WKMCLC-EXTR-FILE                                      00290021
               ASSIGN TO OUT01.                                         00300000
                                                                        00310000
       DATA DIVISION.                                                   00330000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  WKMCLC-EXTR-FILE                                             00380021
           RECORDING MODE IS F                                          00390000
           LABEL RECORDS ARE STANDARD                                   00400000
           BLOCK CONTAINS 0 RECORDS.                                    00410000
                                                                        00420000
       01  WKMCLC-EXTR-REC             PIC X(10).                       00430021
                                                                        00440000
      ******************************************************************00460000
       WORKING-STORAGE SECTION.                                         00470000
      ******************************************************************00480000
                                                                        00490000
W31475 01  ROW-ID                      PIC S9(04) COMP VALUE 0.         00492024
  |    01  HOST-VARIABLES.                                              00493024
  |        05  HV-WKMCLC-DEPT-NBR      PIC X(03)   VALUE SPACES         00494024
  |                                    OCCURS 100  TIMES.               00495024
  |        05  HV-WKMCLC-MAJ-CL-NBR    PIC X(02)   VALUE SPACES         00496024
  |                                    OCCURS 100  TIMES.               00497024
  |        05  HV-WKMCLC-CHNL-CDE      PIC X(01)   VALUE SPACES         00498024
W31475                                 OCCURS 100  TIMES.               00499024
       01  PS-PROGRAM-SWITCHES.                                         00500000
           05  PS-TWKMCLC-DONE         PIC X       VALUE 'N'.           00510021
               88  PS-TWKMCLC-IS-DONE              VALUE 'Y'.           00520021
               88  PS-TWKMCLC-IS-NOT-DONE          VALUE 'N'.           00530021
                                                                        00540000
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              00550000
           05  PA-TWKMCLC-ROWS-FETCHED PIC S9(13)  VALUE ZERO.          00560021
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.          00570000
                                                                        00580000
       01  CD-COUNTER-DISPLAY.                                          00590000
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00600000
                                                                        00610000
       01 ABEND-AREAS.                                                  00620000
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00630000
                                                   COMP SYNC.           00640000
               88  AC-BAD-DATA                     VALUE +4003.         00650000
               88  AC-DB2-ERROR                    VALUE +4013.         00660000
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00670000
                 '*****       ABEND'.                                   00680000
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00690000
                   '*****   PROGRAM: MLKUT472'.                         00700021
           05  AA-PARAGRAPH-LIT.                                        00710000
               10  FILLER              PIC X(17)   VALUE                00720000
                   '***** PARAGRAPH: '.                                 00730000
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00740000
           05  AA-MESSAGE-LINE.                                         00750000
               10  FILLER              PIC X(06)   VALUE '*****'.       00760000
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00770000
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00780000
                   '*****    DB2 ERROR'.                                00790000
           05  AA-DB2-OPERATION-LIT.                                    00800000
               10  FILLER              PIC X(17)   VALUE                00810000
                   '***** OPERATION: '.                                 00820000
               10  AA-DB2-OPERATION.                                    00830000
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        00840000
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        00850000
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        00860000
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        00870000
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        00880000
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        00890000
                                                                        00900000
      ***                                                               00910000
       01  EXTR-RECORD.                                                 00920007
           05  EXTR-DEPT-NBR    PIC X(03)    VALUE SPACES.              00930007
           05  EXTR-MAJ-CL-NBR  PIC X(02)    VALUE SPACES.              00940007
           05  EXTR-CHNL-CDE    PIC X(01)    VALUE SPACE.               00950021
           05  FILLER           PIC X(04)    VALUE SPACES.              00960021
       EJECT                                                            00970000
                                                                        00980000
      ******************************************************************00990000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01000000
      ******************************************************************01010000
           COPY DPWS004.                                                01020000
                                                                        01030000
      ******************************************************************01040000
      *  DB2 ERRORS                                                     01050000
      ******************************************************************01060000
           COPY SQLCA2.                                                 01070000
                                                                        01080000
      ******************************************************************01090000
      *  DB2 COMMUNICATION AREA                                         01100000
      ******************************************************************01110000
           EXEC SQL                                                     01120000
                INCLUDE SQLCA                                           01130000
           END-EXEC.                                                    01140000
                                                                        01150000
      ******************************************************************01160000
      *  DB2 TWKMCLC TABLE LAYOUT                                       01170021
      ******************************************************************01180000
           EXEC SQL                                                     01190000
                INCLUDE TWKMCLC                                         01200021
           END-EXEC.                                                    01210000
                                                                        01220000
      ******************************************************************01221021
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO *              01240000
      ******************************************************************01241021
           EXEC SQL                                                     01260000
               INCLUDE TDTATTR                                          01270000
           END-EXEC.                                                    01280000
                                                                        01290000
      ******************************************************************01291021
      * M/L DATE CONTROL INFO                            *              01310000
      ******************************************************************01321021
           EXEC SQL                                                     01330010
               INCLUDE TMLCNTL                                          01340010
           END-EXEC.                                                    01350010
                                                                        01360000
      ******************************************************************01370000
      *  DB2 TWKMCLC CURSOR                                             01380021
      ******************************************************************01390000
           EXEC SQL                                                     01400000
W31475         DECLARE TWKMCLC_CURSOR CURSOR                            01410024
W31475         WITH ROWSET POSITIONING FOR                              01411024
               SELECT DISTINCT DEPT_NBR                                 01420000
                              ,MAJ_CL_NBR                               01430000
                              ,CHNL_CDE                                 01440021
               FROM TWKMCLC                                             01450021
                   ,TMLCNTL                                             01460012
               WHERE (FSCL_WK_END_DTE BETWEEN                           01470001
WEEK                               OPN_FSCL_WK_DTE                      01480012
WEEK                           AND MXPSTG_FSCL_WK_DTE) OR               01490012
                    ((FSCL_WK_END_DTE BETWEEN                           01500008
                      :DTATTR-PFM-END-DTE AND                           01510008
                      MXPSTG_FSCL_MN_DTE) AND                           01520008
                       (END_INV_RTL_AMT  <> 0  OR                       01530008
                        END_INV_COST_AMT <> 0))                         01540008
               ORDER BY DEPT_NBR, MAJ_CL_NBR, CHNL_CDE                  01550021
           END-EXEC.                                                    01560000
                                                                        01570000
       EJECT                                                            01580000
                                                                        01590000
      ******************************************************************01600000
      ******************************************************************01601021
       PROCEDURE DIVISION.                                              01610000
      ******************************************************************01620000
                                                                        01630000
      ******************************************************************01640000
       A000-MAIN-MODULE.                                                01650000
      ******************************************************************01660000
      *                                                                 01670000
           PERFORM B000-INITIALIZE.                                     01680000
                                                                        01690000
           PERFORM B100-PROCESS-DATA.                                   01700000
                                                                        01710000
           PERFORM B900-EOJ-ROUTINE.                                    01720000
                                                                        01730000
           STOP RUN.                                                    01740000
                                                                        01750000
       EJECT                                                            01760000
                                                                        01770000
      ******************************************************************01780000
       B000-INITIALIZE.                                                 01790000
      ******************************************************************01800000
      *                                                                 01810000
           OPEN OUTPUT WKMCLC-EXTR-FILE.                                01820021
           PERFORM 7100-SELECT-DB2-DATES.                               01830000
                                                                        01840000
       EJECT                                                            01860000
      ******************************************************************01880000
       B100-PROCESS-DATA.                                               01890000
      ******************************************************************01900000
      *                                                                 01910000
           PERFORM Z210-OPEN-CURSOR-TWKMCLC.                            01920021
                                                                        01930000
           PERFORM Z220-FETCH-TWKMCLC.                                  01940021
                                                                        01950000
           PERFORM                                                      01960000
             UNTIL PS-TWKMCLC-IS-DONE                                   01970021
W31475         PERFORM VARYING ROW-ID FROM 1 BY 1                       01971024
  |            UNTIL ROW-ID > SQLERRD (3)                               01972024
  |                 PERFORM C100-WRITE-EXTRACT                          01980024
  |            END-PERFORM                                              01981026
W31475*        PERFORM C100-WRITE-EXTRACT                               01982026
               PERFORM Z220-FETCH-TWKMCLC                               01990021
           END-PERFORM.                                                 02000000
                                                                        02010000
           PERFORM Z230-CLOSE-CURSOR-TWKMCLC.                           02020021
                                                                        02030000
       EJECT                                                            02040000
      ******************************************************************02060000
       C100-WRITE-EXTRACT.                                              02070000
      ******************************************************************02080000
      *                                                                 02090000
W31475*    MOVE WKMCLC-DEPT-NBR        TO EXTR-DEPT-NBR.                02091026
  |   *    MOVE WKMCLC-MAJ-CL-NBR      TO EXTR-MAJ-CL-NBR.              02092026
  |   *    MOVE WKMCLC-CHNL-CDE        TO EXTR-CHNL-CDE.                02093026
  |        MOVE HV-WKMCLC-DEPT-NBR(ROW-ID)                              02100026
  |                                    TO EXTR-DEPT-NBR.                02101024
  |        MOVE HV-WKMCLC-MAJ-CL-NBR(ROW-ID)                            02110024
  |                                    TO EXTR-MAJ-CL-NBR.              02111024
  |        MOVE HV-WKMCLC-CHNL-CDE(ROW-ID)                              02120024
W31475                                 TO EXTR-CHNL-CDE.                02121024
                                                                        02130017
           WRITE WKMCLC-EXTR-REC       FROM EXTR-RECORD.                02140021
                                                                        02150021
           ADD 1                       TO PA-OUTPUT-RECS-WRITTEN.       02160021
                                                                        02170000
       EJECT                                                            02180000
      ******************************************************************02200000
       B900-EOJ-ROUTINE.                                                02210000
      ******************************************************************02220000
      *                                                                 02230000
           DISPLAY ' '.                                                 02240000
           DISPLAY '  PROGRAM MLKUT472 PROCESSING SUMMARY'.             02250021
           DISPLAY '  -----------------------------------'.             02260000
           DISPLAY ' '.                                                 02270000
      *                                                                 02280000
           MOVE PA-TWKMCLC-ROWS-FETCHED TO CD-COUNT-HOLD1.              02290021
           DISPLAY 'TWKMCLC ROWS FETCHED =     ' CD-COUNT-HOLD1.        02300021
      *                                                                 02310000
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               02320000
           DISPLAY 'EXTRACT RECORDS WRITTEN = ' CD-COUNT-HOLD1.         02330000
      *                                                                 02340000
           DISPLAY ' '.                                                 02350000
                                                                        02360000
           CLOSE WKMCLC-EXTR-FILE.                                      02370021
                                                                        02380000
       EJECT                                                            02390000
      ******************************************************************02420000
       Z210-OPEN-CURSOR-TWKMCLC.                                        02430021
      ******************************************************************02440000
      *                                                                 02450000
           EXEC SQL                                                     02460000
               OPEN TWKMCLC_CURSOR                                      02470021
           END-EXEC.                                                    02480000
                                                                        02490000
           EVALUATE TRUE                                                02500000
               WHEN SQLCODE < 0                                         02510000
                   MOVE 'Z210-OPEN-CURSOR-TWKMCLC'                      02520021
                                       TO AA-PARAGRAPH-NAME             02530000
                   MOVE 'TWKMCLC TABLE ERROR'                           02540021
                                       TO AA-MESSAGE                    02550000
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02560000
                   PERFORM Z998-SQL-ERROR                               02570000
               WHEN SQLCODE > 0                                         02580000
               WHEN SQLWARN0 NOT = SPACES                               02590000
                   MOVE 'Z210-OPEN-CURSOR-TWKMCLC'                      02600021
                                       TO AA-PARAGRAPH-NAME             02610000
                   MOVE 'TWKMCLC TABLE WARNING'                         02620021
                                       TO AA-MESSAGE                    02630000
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02640000
                   PERFORM Z998-SQL-ERROR                               02650000
           END-EVALUATE.                                                02660000
                                                                        02670000
       EJECT                                                            02680000
                                                                        02690000
      ******************************************************************02700000
       Z220-FETCH-TWKMCLC.                                              02710021
      ******************************************************************02720000
W31475*                                                                 02731024
  |   *    EXEC SQL                                                     02740024
  |   *        FETCH TWKMCLC_CURSOR                                     02750024
  |   *         INTO :WKMCLC-DEPT-NBR                                   02760024
  |   *             ,:WKMCLC-MAJ-CL-NBR                                 02770024
  |   *             ,:WKMCLC-CHNL-CDE                                   02780024
  |   *    END-EXEC.                                                    02790024
  |        INITIALIZE HOST-VARIABLES.                                   02790124
  |                                                                     02790224
  |        EXEC SQL                                                     02791024
  |            FETCH NEXT ROWSET FROM TWKMCLC_CURSOR FOR 100 ROWS       02792024
  |             INTO  :HV-WKMCLC-DEPT-NBR                               02793024
  |                  ,:HV-WKMCLC-MAJ-CL-NBR                             02794024
  |                  ,:HV-WKMCLC-CHNL-CDE                               02795024
W31475     END-EXEC.                                                    02796024
                                                                        02800000
           EVALUATE TRUE                                                02810000
               WHEN SQLCODE = 0                                         02820000
W31475*            ADD +1              TO PA-TWKMCLC-ROWS-FETCHED       02830026
W31475             ADD +100            TO PA-TWKMCLC-ROWS-FETCHED       02831026
               WHEN SQLCODE = +100                                      02840000
                   MOVE 'Y'            TO PS-TWKMCLC-DONE               02850021
W31475             ADD SQLERRD (3)     TO PA-TWKMCLC-ROWS-FETCHED       02850124
  |                PERFORM VARYING ROW-ID FROM 1 BY 1                   02851024
  |                UNTIL ROW-ID > SQLERRD (3)                           02852024
  |                     PERFORM C100-WRITE-EXTRACT                      02853024
W31475             END-PERFORM                                          02854024
               WHEN SQLCODE < 0                                         02860000
                   MOVE 'Z220-FETCH-TWKMCLC'                            02870021
                                       TO AA-PARAGRAPH-NAME             02880000
                   MOVE 'TWKMCLC TABLE ERROR'                           02890021
                                       TO AA-MESSAGE                    02900000
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              02910000
                   PERFORM Z998-SQL-ERROR                               02920000
               WHEN ((SQLCODE > 0) AND (SQLCODE NOT = +100))            02930000
               WHEN SQLWARN0 NOT = SPACES                               02940000
                   MOVE 'Z220-FETCH-TWKMCLC'                            02950021
                                       TO AA-PARAGRAPH-NAME             02960000
                   MOVE 'TWKMCLC TABLE WARNING'                         02970021
                                       TO AA-MESSAGE                    02980000
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              02990000
                   PERFORM Z998-SQL-ERROR                               03000000
           END-EVALUATE.                                                03010000
                                                                        03020000
       EJECT                                                            03030000
      ******************************************************************03050000
       Z230-CLOSE-CURSOR-TWKMCLC.                                       03060021
      ******************************************************************03070000
      *                                                                 03080000
           EXEC SQL                                                     03090000
               CLOSE TWKMCLC_CURSOR                                     03100021
           END-EXEC.                                                    03110000
                                                                        03120000
           EVALUATE TRUE                                                03130000
               WHEN SQLCODE < 0                                         03140000
                   MOVE 'Z230-CLOSE-CURSOR-TWKMCLC'                     03150021
                                       TO AA-PARAGRAPH-NAME             03160000
                   MOVE 'TWKMCLC TABLE ERROR'                           03170021
                                       TO AA-MESSAGE                    03180000
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03190000
                   PERFORM Z998-SQL-ERROR                               03200000
               WHEN SQLCODE > 0                                         03210000
               WHEN SQLWARN0 NOT = SPACES                               03220000
                   MOVE 'Z230-CLOSE-CURSOR-TWKMCLC'                     03230021
                                       TO AA-PARAGRAPH-NAME             03240000
                   MOVE 'TWKMCLC TABLE WARNING'                         03250021
                                       TO AA-MESSAGE                    03260000
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03270000
                   PERFORM Z998-SQL-ERROR                               03280000
           END-EVALUATE.                                                03290000
                                                                        03300000
       EJECT                                                            03310000
                                                                        03320000
       7100-SELECT-DB2-DATES.                                           03330000
      *                                                                 03340000
           EXEC SQL                                                     03350000
               SELECT  PFM_END_DTE                                      03360000
                      ,OPN_FSCL_WK_DTE                                  03370021
                      ,OPN_FSCL_MN_DTE                                  03380021
                INTO  :DTATTR-PFM-END-DTE                               03390000
 ---                 ,:MLCNTL-OPN-FSCL-WK-DTE                           03400014
 ---                 ,:MLCNTL-OPN-FSCL-MN-DTE                           03410016
                FROM TDTATTR                                            03420000
                    ,TMLCNTL                                            03430013
WEEK            WHERE KY_DTE  = OPN_FSCL_WK_DTE                         03440013
           END-EXEC.                                                    03450000
      *                                                                 03460000
           EVALUATE SQLCODE                                             03470000
               WHEN 0                                                   03480000
                   DISPLAY 'DTATTR-PFM-END-DTE : ',                     03490013
                            DTATTR-PFM-END-DTE                          03500000
                   DISPLAY 'MLCNTL-OPN-FSCL-WK-DTE    : ',              03510013
                            MLCNTL-OPN-FSCL-WK-DTE                      03520015
                   DISPLAY 'MLCNTL-OPN-FSCL-MN-DTE    : ',              03530016
                            MLCNTL-OPN-FSCL-MN-DTE                      03540016
               WHEN OTHER                                               03570000
                  MOVE '7100-SELECT-DB2-DATES'                          03580000
                                       TO AA-PARAGRAPH-NAME             03590000
                   PERFORM Z998-SQL-ERROR                               03600000
           END-EVALUATE.                                                03610000
      *                                                                 03630000
      ******************************************************************03640000
       Z998-SQL-ERROR.                                                  03650000
      ******************************************************************03660000
                                                                        03680000
           CLOSE WKMCLC-EXTR-FILE.                                      03690021
                                                                        03700000
           DISPLAY AA-ABEND-LIT.                                        03710000
           DISPLAY AA-DB2-ERROR-LIT.                                    03720000
           DISPLAY AA-PROGRAM-LIT.                                      03730000
           DISPLAY AA-PARAGRAPH-LIT.                                    03740000
           DISPLAY AA-MESSAGE-LINE.                                     03750000
           DISPLAY AA-DB2-OPERATION-LIT.                                03760000
           SET AC-DB2-ERROR TO TRUE.                                    03770000
           MOVE +4013                  TO ABEND-CODE.                   03780000
                                                                        03790000
           COPY DPPD004.                                                03800000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03810000
                                                                        03820000
