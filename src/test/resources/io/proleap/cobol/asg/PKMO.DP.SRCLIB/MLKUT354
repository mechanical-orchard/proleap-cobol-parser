       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT354.                                                 
       AUTHOR.        VL YANG.                                                  
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04/12/2007.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM FORCE THE EIC, EIC1, AND EIC2 VALUE TO PREVIOUS            
      * FISCAL PERIOD'S DEPT/BRAND/VENDOR CMU%.                                 
      *                                                                         
      * THE INPUT FILE NEEDS TO BE IN MERCH HEIRARCHY THEN DATE.                
      *                                                                         
      * THE PROGRAM WILL EXECUTE IN AN DETAIL MODE AND AN SUMMARY MODE.         
      * THE DETAIL MODE WILL PRODUCE THE REPORT AT THE SUB-CLASS                
      * LOCATION LEVEL AND THE SUMMARY MODE WILL PRODUCE THE REPORT             
      * AT THE DEPT/BRAND/VENDOR LEVEL.                                         
      *                                                                         
      * THE OPTIONAL ADJUSTMENT INPUT FILE IS A FILE THAT WOULD HAVE            
      * BEEN CREATED FROM PREVIOUS EXECUTION.  THIS FILE CONTAINS               
      * ADDITIONAL ADJUSTMENTS THAT WOULD NEED TO BE MADE TO THE                
      * ORIGINAL EIC BEFORE THE THE CURRENT PRORATION CAN TAKE PLACE.           
      *                                                                         
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
33539 * 33539    10-15-2007  BUILD IN EFFICIENCY TO IMPROVE CYCLE PERF.*        
33539 *                      -ROD JANSSEN                              *        
257901* 257901   06-01-2021  INCREASED THE LENGTH OF LABEL SEQ NUMBER  *        
257901*                      FROM 4 TO 5 TO AVOID THE TRUNCATION OF    *        
257901*                      LABEL SEQ NUMBER WHICH IS GREATER THAN 4  *        
257901*                      DIGITS.                                   *        
257901*                      -SRINIVASAN SIVARAJ                       *        
257901*                      CHANGE NUMBER : CHG0257901 *                       
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT OPTIONAL EXECUTION-CONTROL-FILE                               
               ASSIGN TO INP01.                                                 
           SELECT COMMON-RECALC-IN-FILE                                         
               ASSIGN TO INP02.                                                 
           SELECT COMMON-RECALC-OUT-FILE                                        
               ASSIGN TO OUT01.                                                 
           SELECT ADJUST-AUDIT-FILE                                             
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  EXECUTION-CONTROL-FILE                                               
           RECORDING  MODE IS F                                                 
           BLOCK CONTAINS 0 RECORDS.                                            
       01  EXECUTION-CONTROL-REC           PIC X(080).                          
                                                                                
       FD  COMMON-RECALC-IN-FILE                                                
           RECORDING  MODE IS F                                                 
           BLOCK CONTAINS 0 RECORDS.                                            
       01  COMMON-RECALC-IN-REC            PIC X(400).                          
                                                                                
       FD  COMMON-RECALC-OUT-FILE                                               
           RECORDING  MODE IS F                                                 
           BLOCK CONTAINS 0 RECORDS.                                            
       01  COMMON-RECALC-OUT-REC           PIC X(400).                          
                                                                                
       FD  ADJUST-AUDIT-FILE                                                    
           RECORDING  MODE IS F                                                 
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ADJUST-AUDIT-REC                PIC X(325).                          
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                      PIC S9(4)       VALUE ZEROES         
                               COMP SYNC.                                       
           88  SQL-ABEND                                   VALUE +4001.         
           88  NON-SQL-ABEND                               VALUE +4011.         
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
                                                                                
           05  PA-CONTROL-CNT              PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-RECORD-IN-CNT            PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-RECORD-IGNORED           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-RECORD-OUT-CNT           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-RECORD-RPT-CNT           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-PARTN-MISMATCH           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-DKEY-NOT-FOUND           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-BRND-VND-NOT-FOUND       PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-BRND-NOT-FOUND           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-VNDR-NOT-FOUND           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05 PC-PROGRAM-NAME              PIC X(08)   VALUE 'MLKUT354'.        
           05 PC-UNKNOWN                   PIC X(08)   VALUE '????????'.        
           05 PC-NOT-BEG-SEASON            PIC S9(08)      VALUE +4090          
                               COMP SYNC.                                       
           05 PC-BEG-SEASON                PIC S9(08)      VALUE +4091          
                               COMP SYNC.                                       
           05 PC-3                         PIC S9(05)      VALUE +3             
                               COMP-3.                                          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-COMMON-RECALC-IN-EOF-SW  PIC X(01)       VALUE 'N'.           
               88  PS-COMMON-RECALC-IN-NOT-EOF             VALUE 'N'.           
               88  PS-COMMON-RECALC-IN-EOF                 VALUE 'Y'.           
           05  PS-EXEC-CONTROL-SW         PIC X(01)        VALUE 'N'.           
               88  PS-EXEC-CONTROL-NOT-EOF                 VALUE 'N'.           
               88  PS-EXEC-CONTROL-EOF                     VALUE 'Y'.           
           05  PS-DEPT-BRND-VND-SW        PIC X(01)        VALUE 'N'.           
               88  PS-DEPT-BRND-VND-NOT-FOUND              VALUE 'N'.           
               88  PS-DEPT-BRND-VND-FOUND                  VALUE 'Y'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
33539      05  PV-MLT00003-FSCL-DTE-SAVE PIC X(10)  VALUE SPACES.               
33539      05  PV-MLT00002-DEPT-NBR-SAVE PIC S9(4)V COMP-3 VALUE ZEROS.         
33539      05  PV-MLT00003-PARTN-NBR     PIC S9(4)  COMP VALUE ZEROS.           
33539      05  PV-MLT00003-DEPT-GP-CDE   PIC X(01)  VALUE SPACES.               
33539      05  PV-DTATTR-FSCL-MN-NBR     PIC X(02)  VALUE SPACES.               
33539      05  PV-DTATTR-FSCL-WK-NBR     PIC X(02)  VALUE SPACES.               
33539      05  PV-DTATTR-PFM-END-DTE     PIC X(10)  VALUE SPACES.               
33539      05  PV-DTATTR-FSCL-MN-END-DTE PIC X(10)  VALUE SPACES.               
33539      05  PV-DTATTR-PFW-END-DTE     PIC X(10)  VALUE SPACES.               
33539      05  PV-DTATTR-FSCL-WK-END-DTE PIC X(10)  VALUE SPACES.               
33539                                                                           
           05  PV-DEPT-NBR-9               PIC 9(04)       VALUE ZEROES.        
           05  PV-DEPT-NBR-X REDEFINES PV-DEPT-NBR-9                            
                                           PIC X(04).                           
           05  FILLER REDEFINES PV-DEPT-NBR-9.                                  
               10  FILLER                  PIC X(01).                           
               10  PV-DEPT-NBR-3X          PIC X(03).                           
           05  PV-ML-LWST-ID-9             PIC 9(09)       VALUE ZEROES.        
           05  PV-DRIVING-DKEY             PIC S9(09)      VALUE ZEROES         
                               COMP.                                            
           05  PV-CURR-PARTN-NBR           PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-CURR-PARTN-GROUP         PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-LBL-SEQ-NBR              PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
257901     05  PV-LBL-SEQ-NBR-9            PIC 9(05)       VALUE ZEROES.        
           05  PV-ENT-ID                   PIC S9(09)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ENT-ID-9                 PIC 9(09)       VALUE ZEROES.        
           05  PV-CURR-FSCL-PERIOD         PIC X(02)       VALUE SPACES.        
           05  PV-CURR-FSCL-END-DTE        PIC X(10)       VALUE SPACES.        
           05  PV-PREV-FSCL-END-DTE        PIC X(10)       VALUE SPACES.        
           05  PV-PARAGRAPH-NAME           PIC X(30)       VALUE SPACES.        
           05  PV-DB2-OPER-ATTEMPTED       PIC X(30)       VALUE SPACES.        
           05  PV-DBV-BOP-CMU-PCT          PIC S9(3)V9(10) VALUE ZEROES         
                               COMP-3.                                          
                                                                                
      ******************************************************************        
      *  OPTIONAL CONTROL CARD                                                  
      ******************************************************************        
       01  PV-CC-OPTIONAL-CONTROL.                                              
           05  FILLER                      PIC X(01)       VALUE SPACES.        
               88  PV-CC-COMMENT                           VALUE '*'.           
               88  PV-CC-NOT-COMMENT                       VALUE SPACES.        
           05  PV-CC-REQUEST-TYPE          PIC X(08)       VALUE SPACES.        
               88  PV-CC-WEEKLY-REQUEST                    VALUE                
                   'WEEKLY'.                                                    
               88  PV-CC-MONTHLY-REQUEST                   VALUE                
                   'MONTHLY'.                                                   
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  PV-CC-FSCL-END-DATE         PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-CC-FSCL-END-DATE.                            
               10  PV-CC-FSCL-END-YYYY     PIC X(04).                           
               10  FILLER                  PIC X(01).                           
                   88  PV-CC-FSCL-END-DASH-1               VALUE '-'.           
               10  PV-CC-FSCL-END-MM       PIC X(02).                           
               10  FILLER                  PIC X(01).                           
                   88  PV-CC-FSCL-END-DASH-2               VALUE '-'.           
               10  PV-CC-FSCL-END-DD       PIC X(02).                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  PV-CC-PROCESS-TABLE         PIC X(18)       VALUE SPACES.        
               88  PV-CC-MONTHLY-TABLE                     VALUE                
                   'MLT_MNLY_SUM'.                                              
               88  PV-CC-WEEKLY-TABLE                      VALUE                
                   'MLT_WKLY_SUM'.                                              
               88  PV-CC-VALID-TABLES                      VALUES ARE           
                   'MLT_WKLY_SUM',                                              
                   'MLT_MNLY_SUM'.                                              
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  PV-CC-PROCESS-MODE          PIC X(10)       VALUE SPACES.        
               88  PV-CC-DETAIL-MODE                       VALUE                
                   'DETAIL'.                                                    
               88  PV-CC-SUMMARY-MODE                      VALUE                
                   'SUMMARY'.                                                   
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  PV-CC-PROCESS-OPT           PIC X(01)       VALUE SPACES.        
               88  PV-CC-PROCESS-NORMAL-OPT                VALUE '0'.           
               88  PV-CC-PROCESS-SMOOTH-OPT                VALUE '1'.           
           05  FILLER                      PIC X(28)       VALUE SPACES.        
                                                                                
      ******************************************************************        
      *  MLRD070 COMMON RECALC EXTRACT INPUT RECORD                             
      ******************************************************************        
      *01  ML070-COMMON-RECALC-EXTRACT.                                         
           COPY MLRD070.                                                        
                                                                                
      ******************************************************************        
      *  MLRD070 COMMON RECALC EXTRACT ADJUSTMENT RECORD                        
      ******************************************************************        
      *01  MLADJ-COMMON-RECALC-EXTRACT.                                         
           COPY MLRD070 REPLACING ML070 BY MLADJ.                               
                                                                                
      ******************************************************************        
      *  MLRD354 BRAND VENDOR RECALC ALLOCATION EXTRACT                         
      ******************************************************************        
      *01  ML354-RECALC-ALLOCATION-EXTRACT.                                     
           COPY MLRD354.                                                        
                                                                                
      *****************************************************************         
      * ERROR MESSAGE AREA FOR DB2                                              
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *************************************************************             
      *    DB2 TVSCHVL (GET BRAND NAME)                           *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 XMT_VND (GET VENDOR NAME)                          *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE XMT00000                                                 
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 TDTATTR (GET DATE INFORMATION FROM DATE ATTRIBUTE) *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 MLT_PARTN_BAL (GET PARTITION BALANCE INFOR)        *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE MLT00002                                                 
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 MLT_PARTN_CNTL(GET PARTITION CONTROL INFO)         *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE MLT00003                                                 
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 MLT_DEPT_BRND_VND (GET F-CALC DEPT/BRAND/VENDOR)   *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE MLT00010                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 - MLV_BRND_VND.                                                     
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE MLV00000                                                 
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 COMMUNICATION AREA                                 *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 1100-PROCESS-INPUT                                           
               UNTIL PS-COMMON-RECALC-IN-EOF.                                   
                                                                                
           PERFORM 9000-END-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  PERFORM THE NECESSARY INITIALIZATION STEPS                             
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN  INPUT EXECUTION-CONTROL-FILE                                   
                       COMMON-RECALC-IN-FILE                                    
                OUTPUT COMMON-RECALC-OUT-FILE                                   
                       ADJUST-AUDIT-FILE.                                       
                                                                                
           MOVE ZEROES                TO MLT00002-DEPT-NBR                      
                                         MLT00010-ML-LO-MDSE-LVL-ID.            
                                                                                
      * GET THE FIRST VALID CONTROL CARD                                        
           PERFORM 1050-PROCESS-CONTROL.                                        
                                                                                
           PERFORM 8000-READ-INPUT.                                             
                                                                                
      ******************************************************************        
      *  CHECK/PROCESS CONTROL CARDS                                            
      ******************************************************************        
       1050-PROCESS-CONTROL.                                                    
                                                                                
           DISPLAY ' '.                                                         
                                                                                
           PERFORM 8100-READ-CONTROL.                                           
           PERFORM                                                              
               UNTIL PS-EXEC-CONTROL-EOF                                        
                   OR PV-CC-NOT-COMMENT                                         
               DISPLAY ' ' PV-CC-OPTIONAL-CONTROL                               
               ADD +1 TO PA-CONTROL-CNT                                         
               PERFORM 8100-READ-CONTROL                                        
           END-PERFORM.                                                         
           IF PV-CC-OPTIONAL-CONTROL = SPACES                                   
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY ' ' PV-CC-OPTIONAL-CONTROL                               
               IF PV-CC-FSCL-END-YYYY IS NUMERIC                                
                   AND PV-CC-FSCL-END-MM IS NUMERIC                             
                   AND PV-CC-FSCL-END-DD IS NUMERIC                             
                   AND PV-CC-FSCL-END-DASH-1                                    
                   AND PV-CC-FSCL-END-DASH-2                                    
                   DISPLAY ' * PROCESSING TABLE ' PV-CC-PROCESS-TABLE           
                           ' FOR FISCAL DATE ' PV-CC-FSCL-END-DATE              
                           ' IN ' PV-CC-PROCESS-MODE ' MODE.'                   
               ELSE                                                             
                   MOVE SPACES TO PV-CC-FSCL-END-DATE                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PV-CC-FSCL-END-DATE = SPACES                                      
               OR NOT PV-CC-VALID-TABLES                                        
               DISPLAY ' *** INVALID DATE OR TABLE IN CONTROL CARD ***'         
               SET NON-SQL-ABEND   TO TRUE                                      
               MOVE '1050-PROCESS-CONTROL' TO PV-PARAGRAPH-NAME                 
               PERFORM 9999-OTHER-ABEND                                         
           ELSE                                                                 
               IF PV-CC-WEEKLY-TABLE                                            
                   MOVE 'W'                TO MLT00003-TBL-TM-LVL-CDE           
               ELSE                                                             
                   IF PV-CC-MONTHLY-TABLE                                       
                       MOVE 'M'            TO MLT00003-TBL-TM-LVL-CDE           
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
                                                                                
      ******************************************************************        
      *  PROCESS THE INPUT FILES FOR TOTAL RECORD BREAK                         
      ******************************************************************        
       1100-PROCESS-INPUT.                                                      
                                                                                
           ADD +1 TO PA-RECORD-IN-CNT.                                          
           PERFORM 3000-LOAD-DETAIL-ORIG.                                       
                                                                                
      * IS THERE A CHANGE IN DEPT, BRAND, VENDOR?                               
           IF ML070-DEPT-NBR = MLADJ-DEPT-NBR                                   
               AND ML070-ML-LWST-ID = MLADJ-ML-LWST-ID                          
      * IS THERE A CHANGE IN MAJ/SUB-CLASS AND LOCATION?                        
               IF ML070-MAJ-CL-NBR = MLADJ-MAJ-CL-NBR                           
                   AND ML070-SUB-CL-NBR = MLADJ-SUB-CL-NBR                      
                   AND ML070-LOC-NBR = MLADJ-LOC-NBR                            
      * IS THERE A CHANGE IN FISCAL DATE?                                       
                   IF ML070-FSCL-END-DTE NOT = MLADJ-FSCL-END-DTE               
                       MOVE MLADJ-END-INV-COST-AMT2                             
                           TO ML070-PREV-END-INV-COST-AMT                       
                              ML354-DTL-BOP-COST                                
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               PERFORM 1500-GET-DBV-INFO                                        
           END-IF.                                                              
                                                                                
      * SAVE OFF CURRENT RECORD                                                 
           MOVE ML070-COMMON-RECALC-EXTRACT                                     
               TO MLADJ-COMMON-RECALC-EXTRACT.                                  
                                                                                
           PERFORM 2075-CALC-EIC-FROM-CMU.                                      
                                                                                
           MOVE MLADJ-CURR-END-INV-COST-AMT                                     
               TO MLADJ-END-INV-COST-AMT1                                       
                  MLADJ-END-INV-COST-AMT2.                                      
                                                                                
           PERFORM 3100-LOAD-DETAIL-ADJ.                                        
                                                                                
           PERFORM 8400-WRITE-COMMON-RECALC-OUT.                                
                                                                                
33539 ***  PERFORM 8500-WRITE-ADJUST-AUDIT.                                     
                                                                                
           PERFORM 8000-READ-INPUT.                                             
                                                                                
                                                                                
      ******************************************************************        
      *  GET PREVIOUS DEPT/BRAND/VENDOR INFORMATION                             
      ******************************************************************        
       1500-GET-DBV-INFO.                                                       
                                                                                
           MOVE ML070-FSCL-END-DTE  TO PV-CURR-FSCL-END-DTE                     
                                       MLT00003-FSCL-DTE.                       
                                                                                
           MOVE ML070-DEPT-NBR      TO MLT00002-DEPT-NBR.                       
           MOVE ML070-ML-LWST-ID    TO MLV00000-ML-LO-MDSE-LVL-ID               
                                       MLT00010-ML-LO-MDSE-LVL-ID.              
                                                                                
      * GET CURRENT PARTITION INFO, REPLACE IF NEED BE.                         
           PERFORM 3500-GET-PARTN-INFO                                          
           IF PV-CC-WEEKLY-TABLE                                                
               MOVE DTATTR-FSCL-WK-NBR     TO PV-CURR-FSCL-PERIOD               
      *        MOVE DTATTR-PFW-END-DTE     TO PV-PREV-FSCL-END-DTE              
               MOVE DTATTR-PFM-END-DTE     TO PV-PREV-FSCL-END-DTE              
           ELSE                                                                 
               IF PV-CC-MONTHLY-TABLE                                           
                   MOVE DTATTR-FSCL-MN-NBR TO PV-CURR-FSCL-PERIOD               
                   MOVE DTATTR-PFM-END-DTE TO PV-PREV-FSCL-END-DTE              
               END-IF                                                           
           END-IF.                                                              
           MOVE MLT00003-PARTN-NBR     TO PV-CURR-PARTN-NBR.                    
           MOVE MLT00003-DEPT-GP-CDE   TO PV-CURR-PARTN-GROUP.                  
                                                                                
      * LOAD PREV DEPT/BRAND/VENDOR INFORMATION                                 
           MOVE PV-PREV-FSCL-END-DTE   TO MLT00010-FSCL-MN-END-DTE.             
           PERFORM 2400-GET-DEPT-BRAND-VND.                                     
           MOVE MLT00010-END-INV-COST2-AMT TO ML354-DBV-BOP-COST.               
           MOVE MLT00010-END-INV-RTL-AMT   TO ML354-DBV-BOP-RETAIL.             
      * CALC BOP CMU PERCENT = 1 - DBV EIC2 / DBV EIR                           
           IF MLT00010-END-INV-RTL-AMT = ZEROES                                 
               IF MLT00010-END-INV-COST2-AMT = ZEROES                           
                   MOVE ZEROES             TO PV-DBV-BOP-CMU-PCT                
               ELSE                                                             
                   MOVE +1                 TO PV-DBV-BOP-CMU-PCT                
               END-IF                                                           
           ELSE                                                                 
               COMPUTE PV-DBV-BOP-CMU-PCT ROUNDED                               
                       = 1 - MLT00010-END-INV-COST2-AMT                         
                             / MLT00010-END-INV-RTL-AMT                         
           END-IF.                                                              
           MOVE PV-DBV-BOP-CMU-PCT         TO ML354-DBV-BOP-CMU-PCT.            
                                                                                
33539 ***  PERFORM 2100-GET-BV-VIEW.                                            
33539 ***  PERFORM 2200-GET-BRAND-NAME.                                         
33539 ***  PERFORM 2300-GET-VENDOR-NAME.                                        
      * SETUP REPORT                                                            
           MOVE ML070-PARTN-NBR            TO ML354-PARTN-NBR.                  
           MOVE PV-CC-PROCESS-TABLE        TO ML354-TABLE-NAME.                 
           MOVE ML070-DEPT-NBR             TO ML354-DEPT-NBR.                   
           MOVE ML070-ML-LWST-ID           TO ML354-ML-LWST-ID.                 
           MOVE PV-LBL-SEQ-NBR             TO ML354-LBL-SEQ-NBR.                
           MOVE PV-ENT-ID                  TO ML354-ENT-ID.                     
                                                                                
      * LOAD CURRENT DEPT/BRAND/VENDOR INFORMATION                              
           MOVE DTATTR-FSCL-MN-END-DTE     TO MLT00010-FSCL-MN-END-DTE.         
      *    MOVE ML070-FSCL-END-DTE  TO MLT00010-FSCL-MN-END-DTE.                
           PERFORM 2400-GET-DEPT-BRAND-VND.                                     
           MOVE MLT00010-END-INV-RTL-AMT   TO ML354-DBV-EIR.                    
           MOVE MLT00010-END-INV-COST-AMT  TO ML354-DBV-PRE-SMOOTH-EIC.         
           MOVE MLT00010-INV-COST-ADJ1-AMT TO ML354-DBV-INV-ADJ-1.              
           MOVE MLT00010-END-INV-COST1-AMT TO ML354-DBV-EIC-1.                  
           MOVE MLT00010-INV-COST-ADJ2-AMT TO ML354-DBV-INV-ADJ-2.              
           MOVE MLT00010-END-INV-COST2-AMT TO ML354-DBV-EIC-2.                  
           MOVE MLT00010-GRO-MRGN-AMT      TO ML354-DBV-GM.                     
                                                                                
      ******************************************************************        
      *  CALCULATE THE ADJUSTED EIC BASE ON CMU%                                
      ******************************************************************        
       2075-CALC-EIC-FROM-CMU.                                                  
                                                                                
      * CALC EIC = (1 - CMU%) X EIR                                             
           COMPUTE MLADJ-CURR-END-INV-COST-AMT ROUNDED                          
                   = (1 - PV-DBV-BOP-CMU-PCT)                                   
                   * MLADJ-CURR-END-INV-RTL-AMT.                                
                                                                                
                                                                                
      ******************************************************************        
      *  TRANSLATE THE DKEY TO BRAND AND VENDOR                                 
      ******************************************************************        
       2100-GET-BV-VIEW.                                                        
                                                                                
           EXEC SQL                                                             
              SELECT                                                            
                      LBL_SEQ_NBR                                               
                     ,ENT_ID                                                    
                INTO  :MLV00000-LBL-SEQ-NBR                                     
                     ,:MLV00000-ENT-ID                                          
                FROM  MLV_BRND_VND                                              
               WHERE  ML_LO_MDSE_LVL_ID = :MLV00000-ML-LO-MDSE-LVL-ID           
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   MOVE MLV00000-LBL-SEQ-NBR TO PV-LBL-SEQ-NBR                  
                   MOVE MLV00000-ENT-ID      TO PV-ENT-ID                       
               WHEN +100                                                        
                   ADD +1                  TO PA-DKEY-NOT-FOUND                 
                   MOVE MLV00000-ML-LO-MDSE-LVL-ID                              
                       TO PV-ML-LWST-ID-9                                       
                   DISPLAY 'UNKNOWN BV VIEW FOR ' PV-ML-LWST-ID-9               
                   MOVE PC-UNKNOWN         TO ML354-BRAND-NAME                  
                   MOVE PC-UNKNOWN         TO ML354-VENDOR-NAME                 
                   MOVE ZEROS              TO PV-LBL-SEQ-NBR                    
                   MOVE ZEROS              TO PV-ENT-ID                         
               WHEN OTHER                                                       
                   DISPLAY ' DKEY: ' MLV00000-ML-LO-MDSE-LVL-ID                 
                   MOVE 'SELECT MLV_BRND_VND ' TO PV-DB2-OPER-ATTEMPTED         
                   MOVE '2100-GET-BV-VIEW   ' TO PV-PARAGRAPH-NAME              
                   PERFORM 9950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  GET THE BRAND NAME FROM TVSCHVL                                        
      ******************************************************************        
       2200-GET-BRAND-NAME.                                                     
                                                                                
           EXEC SQL                                                             
              SELECT   CHARTC_VAL_DESC                                          
                INTO  :VSCHVL-CHARTC-VAL-DESC                                   
                FROM   TVSCHVL                                                  
               WHERE   CHARTC_CDE = :PC-3                                       
                 AND   CHARTC_SEQ_NBR = :PV-LBL-SEQ-NBR                         
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               INSPECT VSCHVL-CHARTC-VAL-DESC                                   
                                REPLACING ALL ',' BY ' '                        
               MOVE VSCHVL-CHARTC-VAL-DESC TO ML354-BRAND-NAME                  
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   ADD +1                  TO PA-BRND-NOT-FOUND                 
                   MOVE PC-UNKNOWN         TO ML354-BRAND-NAME                  
                   MOVE PV-LBL-SEQ-NBR     TO PV-LBL-SEQ-NBR-9                  
                   DISPLAY 'UNKNOWN BRAND FOR ' PV-LBL-SEQ-NBR-9                
           ELSE                                                                 
               DISPLAY ' LABEL SEQ: ' PV-LBL-SEQ-NBR                            
               MOVE 'SELECT TVSCHVL     ' TO PV-DB2-OPER-ATTEMPTED              
               MOVE '2200-GET-BRAND-NAME' TO PV-PARAGRAPH-NAME                  
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  GET THE VENDOR NAME FROM XMT_VND                                       
      ******************************************************************        
       2300-GET-VENDOR-NAME.                                                    
                                                                                
           EXEC SQL                                                             
              SELECT   PRIM_VND_NM                                              
                INTO  :XMT00000-PRIM-VND-NM                                     
                FROM   XMT_VND                                                  
               WHERE   ENT_ID = :PV-ENT-ID                                      
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               INSPECT XMT00000-PRIM-VND-NM                                     
                                      REPLACING ALL ',' BY ' '                  
               MOVE XMT00000-PRIM-VND-NM   TO ML354-VENDOR-NAME                 
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   ADD +1                  TO PA-VNDR-NOT-FOUND                 
                   MOVE PC-UNKNOWN         TO ML354-VENDOR-NAME                 
                   MOVE PV-ENT-ID          TO PV-ENT-ID-9                       
                   DISPLAY 'UNKNOWN VENDOR FOR ' PV-ENT-ID-9                    
           ELSE                                                                 
               DISPLAY ' ENT-ID: ' PV-ENT-ID                                    
               MOVE 'SELECT XMT_VND     ' TO PV-DB2-OPER-ATTEMPTED              
               MOVE '2300-GET-VENDOR-NAME ' TO PV-PARAGRAPH-NAME                
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  GET THE VARIOUS DEPT/BRAND/VENDOR INFO TO USE IN PRORATION             
      ******************************************************************        
       2400-GET-DEPT-BRAND-VND.                                                 
                                                                                
           EXEC SQL                                                             
              SELECT                                                            
                      A.END_INV_RTL_AMT                                         
                     ,A.END_INV_COST_AMT                                        
                     ,A.INV_COST_ADJ1_AMT                                       
                     ,A.END_INV_COST1_AMT                                       
                     ,A.INV_COST_ADJ2_AMT                                       
                     ,A.END_INV_COST2_AMT                                       
                     ,A.GRO_MRGN_AMT                                            
                INTO                                                            
                     :MLT00010-END-INV-RTL-AMT                                  
                    ,:MLT00010-END-INV-COST-AMT                                 
                    ,:MLT00010-INV-COST-ADJ1-AMT                                
                    ,:MLT00010-END-INV-COST1-AMT                                
                    ,:MLT00010-INV-COST-ADJ2-AMT                                
                    ,:MLT00010-END-INV-COST2-AMT                                
                    ,:MLT00010-GRO-MRGN-AMT                                     
                FROM   MLT_DEPT_BRND_VND  A                                     
                WHERE                                                           
                       A.PARTN_NBR IN (                                         
                         SELECT  B.PARTN_NBR                                    
                           FROM  MLT_PARTN_CNTL  B                              
                           WHERE B.TBL_TM_LVL_CDE                               
                                     = 'M'                                      
                             AND B.FSCL_DTE = :MLT00010-FSCL-MN-END-DTE         
                             AND B.DEPT_GP_CDE IN (                             
                                 SELECT  C.DEPT_GP_CDE                          
                                   FROM  MLT_PARTN_BAL  C                       
                                   WHERE C.TBL_TM_LVL_CDE                       
                                             = 'M'                              
                                     AND C.DEPT_NBR = :MLT00002-DEPT-NBR        
                                     AND :MLT00010-FSCL-MN-END-DTE              
                                         BETWEEN C.EFF_BEG_DTE AND              
                                             VALUE (C.EFF_END_DTE,              
                                                 '9999-12-30')                  
                                   )                                            
                           )                                                    
                   AND A.DEPT_NBR = :MLT00002-DEPT-NBR                          
                   AND A.ML_LO_MDSE_LVL_ID = :MLT00010-ML-LO-MDSE-LVL-ID        
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   SET PS-DEPT-BRND-VND-FOUND     TO TRUE                       
               WHEN +100                                                        
      *            DISPLAY ' DEPT-BRAND-VENDOR INFO NOT FOUND FOR'              
      *                    ' DEPT ' MLT00002-DEPT-NBR                           
      *                    ' DKEY ' MLT00010-ML-LO-MDSE-LVL-ID                  
      *                    ' DATE ' MLT00010-FSCL-MN-END-DTE                    
                   ADD +1                  TO PA-BRND-VND-NOT-FOUND             
                   SET PS-DEPT-BRND-VND-NOT-FOUND TO TRUE                       
                   MOVE ZEROES     TO MLT00010-END-INV-RTL-AMT                  
                                      MLT00010-END-INV-COST-AMT                 
                                      MLT00010-INV-COST-ADJ1-AMT                
                                      MLT00010-END-INV-COST1-AMT                
                                      MLT00010-INV-COST-ADJ2-AMT                
                                      MLT00010-END-INV-COST2-AMT                
                                      MLT00010-GRO-MRGN-AMT                     
               WHEN OTHER                                                       
                   DISPLAY ' DEPT ' MLT00002-DEPT-NBR                           
                           ' DKEY ' MLT00010-ML-LO-MDSE-LVL-ID                  
                           ' DATE ' MLT00010-FSCL-MN-END-DTE                    
                   MOVE 'SELECT MLT_DEPT_BRND_VND'                              
                       TO PV-DB2-OPER-ATTEMPTED                                 
                   MOVE '2400-GET-DEPT-BRAND-VND'                               
                       TO PV-PARAGRAPH-NAME                                     
                   PERFORM 9950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  LOAD THE ORIGINAL DETAIL INFORMATION FOR REPORT/CSV                    
      ******************************************************************        
       3000-LOAD-DETAIL-ORIG.                                                   
                                                                                
           MOVE ML070-FSCL-END-DTE         TO ML354-FSCL-END-DTE.               
           MOVE ML070-SUB-CL-NBR           TO ML354-SUB-CLASS.                  
           MOVE ML070-LOC-NBR              TO ML354-LOCATION.                   
                                                                                
           MOVE ML070-PREV-END-INV-COST-AMT  TO ML354-DTL-BOP-COST              
           MOVE ML070-CURR-SLS-RTL-AMT       TO ML354-DTL-SALES-RTL.            
           MOVE ML070-CURR-RCPT-NET-COST-AMT TO ML354-DTL-RCPT-NET-CST.         
           MOVE ML070-CURR-PADJ-NET-COST-AMT TO ML354-DTL-PADJ-NET-CST.         
           MOVE ML070-CURR-END-INV-RTL-AMT   TO ML354-DTL-EIR.                  
                                                                                
           MOVE ML070-CURR-END-INV-COST-AMT TO ML354-DTL-ORIG-EIC.              
                                                                                
           MOVE ML070-END-INV-COST-AMT1     TO ML354-DTL-ORIG-EIC-1.            
                                                                                
           MOVE ML070-INV-COST-ADJ1         TO ML354-DTL-ORIG-INV-ADJ-1.        
                                                                                
           MOVE ML070-END-INV-COST-AMT2     TO ML354-DTL-ORIG-EIC-2.            
                                                                                
           MOVE ML070-INV-COST-ADJ2         TO ML354-DTL-ORIG-INV-ADJ-2.        
                                                                                
           MOVE ML070-ADJ-GRO-MRGN-AMT      TO ML354-DTL-ORIG-ADJ-GM.           
                                                                                
                                                                                
      ******************************************************************        
      *  LOAD THE ADJUSTED DETAIL INFO FOR REPORT/CSV, RECALC GM$               
      ******************************************************************        
       3100-LOAD-DETAIL-ADJ.                                                    
                                                                                
           MOVE MLADJ-CURR-END-INV-COST-AMT TO ML354-DTL-ADJ-EIC.               
                                                                                
           MOVE MLADJ-END-INV-COST-AMT1     TO ML354-DTL-ADJ-EIC-1.             
                                                                                
           MOVE MLADJ-INV-COST-ADJ1         TO ML354-DTL-ADJ-INV-ADJ-1.         
                                                                                
           MOVE MLADJ-END-INV-COST-AMT2     TO ML354-DTL-ADJ-EIC-2.             
                                                                                
           MOVE MLADJ-INV-COST-ADJ2         TO ML354-DTL-ADJ-INV-ADJ-2.         
                                                                                
      * CALC ADJUSTED SALES COST                                                
           COMPUTE MLADJ-CURR-SLS-COST-AMT =                                    
                 (  MLADJ-PREV-END-INV-COST-AMT                                 
                  + MLADJ-CURR-RCPT-NET-COST-AMT                                
                  + MLADJ-CURR-PADJ-NET-COST-AMT                                
                  - MLADJ-END-INV-COST-AMT2     ).                              
                                                                                
      * CALC ADJUSTED GROSS MARGIN                                              
           COMPUTE MLADJ-ADJ-GRO-MRGN-AMT =                                     
                       MLADJ-CURR-SLS-RTL-AMT                                   
                     - MLADJ-CURR-SLS-COST-AMT.                                 
                                                                                
           MOVE MLADJ-ADJ-GRO-MRGN-AMT      TO ML354-DTL-ADJ-ADJ-GM.            
                                                                                
                                                                                
      ******************************************************************        
      *  GET PARTITION INFORMATION FOR A GIVEN FISCAL DATE                      
      ******************************************************************        
       3500-GET-PARTN-INFO.                                                     
                                                                                
33539      IF MLT00003-FSCL-DTE EQUAL PV-MLT00003-FSCL-DTE-SAVE AND             
33539         MLT00002-DEPT-NBR EQUAL PV-MLT00002-DEPT-NBR-SAVE                 
33539        MOVE PV-MLT00003-PARTN-NBR     TO MLT00003-PARTN-NBR               
33539        MOVE PV-MLT00003-DEPT-GP-CDE   TO MLT00003-DEPT-GP-CDE             
33539        MOVE PV-DTATTR-FSCL-MN-NBR     TO DTATTR-FSCL-MN-NBR               
33539        MOVE PV-DTATTR-FSCL-WK-NBR     TO DTATTR-FSCL-WK-NBR               
33539        MOVE PV-DTATTR-PFM-END-DTE     TO DTATTR-PFM-END-DTE               
33539        MOVE PV-DTATTR-FSCL-MN-END-DTE TO DTATTR-FSCL-MN-END-DTE           
33539        MOVE PV-DTATTR-PFW-END-DTE     TO DTATTR-PFW-END-DTE               
33539        MOVE PV-DTATTR-FSCL-WK-END-DTE TO DTATTR-FSCL-WK-END-DTE           
33539      ELSE                                                                 
33539        EXEC SQL                                                           
               SELECT                                                           
                       B.PARTN_NBR                                              
                      ,B.DEPT_GP_CDE                                            
                      ,D.FSCL_MN_NBR                                            
                      ,D.FSCL_WK_NBR                                            
                      ,D.PFM_END_DTE                                            
                      ,D.FSCL_MN_END_DTE                                        
                      ,D.PFW_END_DTE                                            
                      ,D.FSCL_WK_END_DTE                                        
                 INTO                                                           
                       :MLT00003-PARTN-NBR                                      
                      ,:MLT00003-DEPT-GP-CDE                                    
                      ,:DTATTR-FSCL-MN-NBR                                      
                      ,:DTATTR-FSCL-WK-NBR                                      
                      ,:DTATTR-PFM-END-DTE                                      
                      ,:DTATTR-FSCL-MN-END-DTE                                  
                      ,:DTATTR-PFW-END-DTE                                      
                      ,:DTATTR-FSCL-WK-END-DTE                                  
                 FROM                                                           
                       MLT_PARTN_CNTL  B                                        
                      ,TDTATTR         D                                        
                 WHERE                                                          
                       B.TBL_TM_LVL_CDE                                         
                           = :MLT00003-TBL-TM-LVL-CDE                           
                   AND B.FSCL_DTE = :MLT00003-FSCL-DTE                          
                   AND B.DEPT_GP_CDE IN (                                       
                         SELECT  C.DEPT_GP_CDE                                  
                           FROM  MLT_PARTN_BAL  C                               
                           WHERE C.TBL_TM_LVL_CDE                               
                                     = :MLT00003-TBL-TM-LVL-CDE                 
                             AND C.DEPT_NBR = :MLT00002-DEPT-NBR                
                             AND :MLT00003-FSCL-DTE                             
                                 BETWEEN C.EFF_BEG_DTE AND                      
                                     VALUE (C.EFF_END_DTE,                      
                                         '9999-12-30')                          
                           )                                                    
                   AND KY_DTE = :MLT00003-FSCL-DTE                              
               WITH UR                                                          
33539        END-EXEC                                                           
                                                                                
33539        EVALUATE SQLCODE                                                   
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY ' TABLE LEVEL: ' MLT00003-TBL-TM-LVL-CDE             
                           ' FISCAL DATE: ' MLT00003-FSCL-DTE                   
                   MOVE 'SELECT PARTN_CNTL   ' TO PV-DB2-OPER-ATTEMPTED         
                   MOVE '3500-GET-PARTN-INFO ' TO PV-PARAGRAPH-NAME             
                   PERFORM 9950-SQL-ABEND                                       
             END-EVALUATE                                                       
33539                                                                           
33539        MOVE MLT00003-FSCL-DTE TO PV-MLT00003-FSCL-DTE-SAVE                
33539        MOVE MLT00002-DEPT-NBR TO PV-MLT00002-DEPT-NBR-SAVE                
33539                                                                           
33539        MOVE MLT00003-PARTN-NBR     TO PV-MLT00003-PARTN-NBR               
33539        MOVE MLT00003-DEPT-GP-CDE   TO PV-MLT00003-DEPT-GP-CDE             
33539        MOVE DTATTR-FSCL-MN-NBR     TO PV-DTATTR-FSCL-MN-NBR               
33539        MOVE DTATTR-FSCL-WK-NBR     TO PV-DTATTR-FSCL-WK-NBR               
33539        MOVE DTATTR-PFM-END-DTE     TO PV-DTATTR-PFM-END-DTE               
33539        MOVE DTATTR-FSCL-MN-END-DTE TO PV-DTATTR-FSCL-MN-END-DTE           
33539        MOVE DTATTR-PFW-END-DTE     TO PV-DTATTR-PFW-END-DTE               
33539        MOVE DTATTR-FSCL-WK-END-DTE TO PV-DTATTR-FSCL-WK-END-DTE           
33539                                                                           
33539      END-IF.                                                              
                                                                                
      ******************************************************************        
      * READ THE COMMON RECALC INPUT EXTRACT                                    
      ******************************************************************        
       8000-READ-INPUT.                                                         
                                                                                
           READ COMMON-RECALC-IN-FILE INTO ML070-COMMON-RECALC-EXTRACT          
               AT END                                                           
                   SET PS-COMMON-RECALC-IN-EOF TO TRUE.                         
                                                                                
                                                                                
      ******************************************************************        
      * READ THE CONTROL CARD                                                   
      ******************************************************************        
       8100-READ-CONTROL.                                                       
                                                                                
           READ EXECUTION-CONTROL-FILE INTO PV-CC-OPTIONAL-CONTROL              
               AT END                                                           
                   SET PS-EXEC-CONTROL-EOF TO TRUE.                             
                                                                                
      ******************************************************************        
      * WRITE/GENERATE THE CORRECT/CURRENT RECALC EXTRACT                       
      ******************************************************************        
       8400-WRITE-COMMON-RECALC-OUT.                                            
                                                                                
           WRITE COMMON-RECALC-OUT-REC FROM MLADJ-COMMON-RECALC-EXTRACT.        
                                                                                
           ADD +1 TO PA-RECORD-OUT-CNT.                                         
                                                                                
      ******************************************************************        
      * WRITE/GENERATE THE CSV REPORT OF THE ADJUSTMENTS                        
      ******************************************************************        
       8500-WRITE-ADJUST-AUDIT.                                                 
                                                                                
           WRITE ADJUST-AUDIT-REC FROM ML354-RECALC-ALLOCATION-EXTR.            
                                                                                
           ADD +1 TO PA-RECORD-RPT-CNT.                                         
                                                                                
      ******************************************************************        
      *  PERFORM THE NECESSARY END PROCESSING STEPS                             
      ******************************************************************        
       9000-END-PROCESSING.                                                     
                                                                                
           CLOSE EXECUTION-CONTROL-FILE                                         
                 COMMON-RECALC-IN-FILE                                          
                 COMMON-RECALC-OUT-FILE                                         
                 ADJUST-AUDIT-FILE.                                             
                                                                                
           DISPLAY '*************************************************'          
           DISPLAY ' RECORD IN:      ' PA-RECORD-IN-CNT.                        
           DISPLAY ' HEADER RECORDS: ' PA-RECORD-IGNORED.                       
           DISPLAY ' REPORT RECORD:  ' PA-RECORD-RPT-CNT.                       
           DISPLAY ' ADJUST RECORD:  ' PA-RECORD-OUT-CNT.                       
           DISPLAY ' '.                                                         
           DISPLAY ' BRAND VENDOR NOT FOUND: ' PA-BRND-VND-NOT-FOUND.           
           DISPLAY ' PARTITION INFO MISMATCH ' PA-PARTN-MISMATCH.               
           DISPLAY ' DKEY NOT FOUND:         ' PA-DKEY-NOT-FOUND.               
           DISPLAY ' BRAND NAME NOT FOUND:   ' PA-BRND-NOT-FOUND.               
           DISPLAY ' VENDOR NAME NOT FOUND:  ' PA-VNDR-NOT-FOUND.               
           DISPLAY '*************************************************'.         
                                                                                
                                                                                
      ***************************************************************           
      *    SQL ABEND PROCESSING                                     *           
      ***************************************************************           
       9950-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** OPERATION = ' PV-DB2-OPER-ATTEMPTED.                     
                                                                                
           COPY DPPD004.                                                        
                                                                                
           SET SQL-ABEND       TO TRUE.                                         
           PERFORM 9999-OTHER-ABEND.                                            
                                                                                
                                                                                
      ***************************************************************           
      *    ISSUE THE ACTUAL ABEND CALL                              *           
      ***************************************************************           
       9999-OTHER-ABEND.                                                        
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   = ' PC-PROGRAM-NAME.                          
           DISPLAY '**  PARAGRAPH  = ' PV-PARAGRAPH-NAME.                       
           DISPLAY '************************************************'.          
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
