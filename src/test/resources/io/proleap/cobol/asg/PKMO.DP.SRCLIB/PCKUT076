      * ------------------------------------------------------------- *         
       IDENTIFICATION DIVISION.                                                 
      * ------------------------------------------------------------- *         
       PROGRAM-ID.      PCKUT076.                                               
       AUTHOR.          JEAN KURKOWSKI.                                         
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    07-2007.                                                
                                                                                
      *****************************************************************         
      *  THIS PROGRAM WILL PUT A MESSAGE ONTO THE SPECIFIED QUEUE.    *         
      *  THIS MESSAGE IS RECOGNIZED BY THE APPLICATION.               *         
      *  THE APPLICATION WILL THEN END ITS EXECUTION.                 *         
      *                                                               *         
      *****************************************************************         
      * ------------------------------------------------------------- *         
       ENVIRONMENT DIVISION.                                                    
      * ------------------------------------------------------------- *         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.       IBM-3090.                                         
       OBJECT-COMPUTER.       IBM-3090.                                         
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT CONTROL-CARD-1                                                
                  ASSIGN TO INP01.                                              
                                                                                
      * ------------------------------------------------------------- *         
       DATA DIVISION.                                                           
      * ------------------------------------------------------------- *         
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-1                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-1-LINE              PIC  X(80).                         
                                                                                
      * ------------------------------------------------------------- *         
       WORKING-STORAGE SECTION.                                                 
      * ------------------------------------------------------------- *         
                                                                                
       01  ABEND-CODE              PIC S9(4)  VALUE ZEROS COMP SYNC.            
           88  AC-MQ-ERROR                    VALUE +4020.                      
                                                                                
      * ------------------------------------------------------------- *         
      *   PROGRAM VARIABLES                                                     
      * ------------------------------------------------------------- *         
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH             PIC X(30)     VALUE SPACES.     01415399
           05  PV-QM-NAME               PIC X(48).                              
           05  PV-MSGBUFFER.                                                    
               10  PV-MSGBUFFER-ARRAY   PIC X(1) OCCURS 4096 TIMES.             
           05  PV-MSGLENGTH             PIC S9(9) BINARY VALUE 50.              
           05  PV-NUMPUTS               PIC S9(9) BINARY VALUE 0.               
           05  PV-ERROR-MESSAGE         PIC X(48) VALUE SPACES.                 
           05  PV-HCONN                 PIC S9(9) BINARY VALUE 0.               
           05  PV-PQ-HANDLE             PIC S9(9) BINARY VALUE 0.               
           05  PV-OPEN-OPTIONS          PIC S9(9) BINARY.                       
           05  PV-COMPLETION-CODE       PIC S9(9) BINARY.                       
           05  PV-REASON                PIC S9(9) BINARY.                       
           05  PV-CON-REASON            PIC S9(9) BINARY.                       
           05  PV-DISPLAY-NBR           PIC -Z(8)9.                             
                                                                                
      * ------------------------------------------------------------- *         
      *   WORKING STORAGE VARIABLES                                             
      * ------------------------------------------------------------- *         
                                                                                
       01  WS-WORKING-STORAGE-VARIABLES.                                        
           05  WS-MQCONN                  PIC X(8)  VALUE 'CSQBCONN'.           
           05  WS-MQOPEN                  PIC X(8)  VALUE 'CSQBOPEN'.           
           05  WS-MQPUT                   PIC X(8)  VALUE 'CSQBPUT'.            
           05  WS-MQCLOSE                 PIC X(8)  VALUE 'CSQBCLOS'.           
           05  WS-MQDISC                  PIC X(8)  VALUE 'CSQBDISC'.           
                                                                                
      *    API control blocks                                                   
                                                                                
       01  MQM-OBJECT-DESCRIPTOR.                                               
           COPY CMQODV.                                                         
       01  MQM-MESSAGE-DESCRIPTOR.                                              
           COPY CMQMDV.                                                         
       01  MQM-PUT-MESSAGE-OPTIONS.                                             
           COPY CMQPMOV.                                                        
      *                                                                         
      *    MQV contains constants (for filling in the control blocks)           
      *    and return codes (for testing the result of a call)                  
      *                                                                         
       01  MQM-CONSTANTS.                                                       
           COPY CMQV.                                                           
      *                                                                         
      *----------------------------------------------------------------*        
      *    CONTROL CARD                                                         
      *----------------------------------------------------------------*        
       01  CC-CONTROL-CARD-1.                                                   
           05  CC-PRIORITY               PIC X(01).                             
           05  FILLER                    PIC X(79).                             
                                                                                
      *----------------------------------------------------------------*        
      *    PROGRAM CONSTANTS                                                    
      *----------------------------------------------------------------*        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME               PIC X(09) VALUE 'PCKUT076 '.           
           05  PC-FIFTY-STARS            PIC X(50) VALUE ALL '*'.               
           05  PC-MSG-EXPIRY             PIC S9(9) VALUE 36000.                 
           05  PC-MSG-TEXT               PIC X(100)                             
                      VALUE 'STOP PROCESSING P2L LABOR DATA'.                   
           05  PC-CORRELID-ASCII         PIC X(24)                              
                                  VALUE 'ê&.•€                '.             
                                                                                
      *----------------------------------------------------------------*        
      *    PROGRAM SWITCHES                                                     
      *----------------------------------------------------------------*        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTROL-CARD-1-SW      PIC X(01) VALUE 'N'.                   
               88  PS-CONTROL-CARD-1-END           VALUE 'Y'.                   
               88  PS-CONTROL-CARD-1-START         VALUE 'N'.                   
                                                                                
      *----------------------------------------------------------------*        
      *    COMMUNICATION AREA FOR DB2                                           
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    DB2 VIEW DECLARATION - PROVIDER QUEUE VIEW (PUT)                     
      *        WSV_PRVDR_QUE - WSV00005                                         
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE WSV00005                                                 
           END-EXEC.                                                            
                                                                                
                                                                                
      * ------------------------------------------------------------- *         
       PROCEDURE DIVISION.                                                      
      * ------------------------------------------------------------- *         
                                                                                
       1000-MAIN SECTION.                                                       
                                                                                
      * ------------------------------------------------------------- *         
      *  READ CONTROL CARD TO GET PRIORITY                            *         
      * ------------------------------------------------------------- *         
                                                                                
            SET PS-CONTROL-CARD-1-START TO TRUE.                                
            OPEN INPUT CONTROL-CARD-1.                                          
            READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1                          
                AT END                                                          
                    SET PS-CONTROL-CARD-1-END                                   
                                        TO TRUE.                                
                                                                                
           IF PS-CONTROL-CARD-1-END                                             
               DISPLAY PC-PGM-NAME                                              
                  'CONTROL CARD MISSING - FOR PRIORITY'                         
               GO TO 4000-END-PROGRAM                                           
           ELSE                                                                 
               DISPLAY PC-PGM-NAME                                              
                       '- '                                                     
                       PC-FIFTY-STARS                                           
               DISPLAY PC-PGM-NAME                                              
                       '- CONTROL CARD: '                                       
                       CC-CONTROL-CARD-1                                        
           END-IF.                                                              
                                                                                
           CLOSE CONTROL-CARD-1.                                                
                                                                                
           PERFORM 1100-GET-QUEUE-INFO.                                         
                                                                                
           PERFORM 1200-CONNECT-QUEUE-MANAGER.                                  
                                                                                
           PERFORM 1500-OPEN-QUEUE.                                             
                                                                                
           PERFORM 2000-MQPUT-MSG.                                              
                                                                                
           PERFORM 3000-CLOSE-QUEUE.                                            
                                                                                
           PERFORM 3500-DISCONNECT-QMGR.                                        
                                                                                
           GO TO 4000-END-PROGRAM.                                              
                                                                                
                                                                                
      * ------------------------------------------------------------- *         
      *    Get the information about the queue                                  
      * ------------------------------------------------------------- *         
                                                                                
       1100-GET-QUEUE-INFO.                                                     
                                                                                
           EXEC SQL                                                             
              SELECT LCL_QUE_MGR,                                               
                     QUE_NM                                                     
               INTO :WSV00005-LCL-QUE-MGR                                       
                   ,:WSV00005-QUE-NM                                            
               FROM WSV_PRVDR_QUE                                               
              WHERE APPL_FUNC_DESC  = 'QUEUE'                                   
                AND INTGRTN_TYP_CDE = 'P2LLABORQ'                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE SQLCODE                 TO PV-COMPLETION-CODE           
                   MOVE 'WSV PRVDR ERROR'       TO PV-ERROR-MESSAGE             
                   MOVE '1100-GET-QUEUE-INFO'   TO PV-PARAGRAPH                 
                   PERFORM 9000-DISPLAY-ERROR-MESSAGE                           
                   GO TO 4000-END-PROGRAM                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      * ------------------------------------------------------------- *         
      *    Connect to the queue manager                                         
      * ------------------------------------------------------------- *         
                                                                                
       1200-CONNECT-QUEUE-MANAGER.                                              
                                                                                
           MOVE WSV00005-LCL-QUE-MGR-TEXT TO PV-QM-NAME.                        
           CALL WS-MQCONN USING PV-QM-NAME                                      
                                PV-HCONN                                        
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
                                                                                
      *    If connection failed then display error message                      
      *    and exit                                                             
                                                                                
           MOVE PV-REASON TO PV-CON-REASON.                                     
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
              MOVE 'MQCONN ERROR'               TO PV-ERROR-MESSAGE             
              MOVE '1000-CONNECT-QUEUE-MANAGER' TO PV-PARAGRAPH                 
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
              GO TO 4000-END-PROGRAM                                            
           END-IF.                                                              
           DISPLAY PC-PGM-NAME                                                  
                   '  MQCONN SUCCESSFUL TO ', PV-QM-NAME.                       
                                                                                
                                                                                
      * ------------------------------------------------------------- *         
      *    Open the queue for output                                            
      * ------------------------------------------------------------- *         
                                                                                
       1500-OPEN-QUEUE.                                                         
                                                                                
           COMPUTE PV-OPEN-OPTIONS = MQOO-OUTPUT +                              
                                  MQOO-FAIL-IF-QUIESCING +                      
                                  MQOO-SET-IDENTITY-CONTEXT.                    
           MOVE WSV00005-QUE-NM-TEXT        TO MQOD-OBJECTNAME.                 
           MOVE WSV00005-LCL-QUE-MGR-TEXT   TO MQOD-OBJECTQMGRNAME.             
                                                                                
           CALL WS-MQOPEN USING PV-HCONN                                        
                               MQM-OBJECT-DESCRIPTOR                            
                               PV-OPEN-OPTIONS                                  
                               PV-PQ-HANDLE                                     
                               PV-COMPLETION-CODE                               
                               PV-REASON.                                       
                                                                                
      *    If open failed then display error message                            
      *    and exit.                                                            
                                                                                
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
              MOVE 'MQOPEN ERROR'    TO PV-ERROR-MESSAGE                        
              MOVE '1000-OPEN-QUEUE' TO PV-PARAGRAPH                            
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
              PERFORM 3500-DISCONNECT-QMGR                                      
              GO TO   4000-END-PROGRAM                                          
           END-IF.                                                              
           DISPLAY PC-PGM-NAME                                                  
                   '  MQOPEN SUCCESSFUL - '                                     
                   WSV00005-QUE-NM-TEXT.                                        
                                                                                
                                                                                
      * ------------------------------------------------------------- *         
      * PUT MESSAGE ON LOCAL QUEUE TO END THE DATA COLLECT PROCESS              
      * ------------------------------------------------------------- *         
       2000-MQPUT-MSG.                                                          
                                                                                
      *    Set persistence                                                      
      *                                                                         
           MOVE MQMI-NONE         TO MQMD-MSGID.                                
           MOVE MQCI-NONE         TO MQMD-CORRELID.                             
           MOVE PC-MSG-TEXT       TO PV-MSGBUFFER.                              
           MOVE MQPER-PERSISTENT  TO MQMD-PERSISTENCE.                          
           MOVE CC-PRIORITY       TO MQMD-PRIORITY.                             
           MOVE MQFMT-STRING      TO MQMD-FORMAT.                               
           COMPUTE MQPMO-OPTIONS  = MQPMO-SYNCPOINT +                           
                                    MQPMO-SET-IDENTITY-CONTEXT.                 
                                                                                
           CALL WS-MQPUT USING PV-HCONN                                         
                               PV-PQ-HANDLE                                     
                               MQM-MESSAGE-DESCRIPTOR                           
                               MQM-PUT-MESSAGE-OPTIONS                          
                               PV-MSGLENGTH                                     
                               PV-MSGBUFFER                                     
                               PV-COMPLETION-CODE                               
                               PV-REASON                                        
                                                                                
      *        If put failed then display error message                         
      *        and break out of loop                                            
      *                                                                         
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
              MOVE 'MQPUT ERROR'            TO PV-ERROR-MESSAGE                 
              MOVE '2000-MQPUT-MSG' TO PV-PARAGRAPH                             
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
           ELSE                                                                 
              ADD 1 TO PV-NUMPUTS                                               
           END-IF.                                                              
                                                                                
                                                                                
      * ------------------------------------------------------------- *         
      * CLOSE THE QUEUE                                                         
      * ------------------------------------------------------------- *         
                                                                                
       3000-CLOSE-QUEUE.                                                        
           CALL WS-MQCLOSE USING PV-HCONN                                       
                                PV-PQ-HANDLE                                    
                                MQCO-NONE                                       
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
              MOVE 'MQCLOSE ERROR'     TO PV-ERROR-MESSAGE                      
              MOVE '3000-CLOSE-QUEUE ' TO PV-PARAGRAPH                          
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
           ELSE                                                                 
              DISPLAY PC-PGM-NAME                                               
                      '  MQCLOSE SUCCESSFUL'                                    
           END-IF.                                                              
                                                                                
                                                                                
      * ------------------------------------------------------------- *         
      * DISCONNECT FROM THE MQ QUEUE MANAGER                                    
      * ------------------------------------------------------------- *         
       3500-DISCONNECT-QMGR.                                                    
                                                                                
           IF PV-CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED              
              CALL WS-MQDISC USING PV-HCONN                                     
                                   PV-COMPLETION-CODE                           
                                   PV-REASON                                    
              IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                        
                 MOVE 'MQDISC ERROR'         TO PV-ERROR-MESSAGE                
                 MOVE '3500-DISCONNECT-QMGR' TO PV-PARAGRAPH                    
                 PERFORM 9000-DISPLAY-ERROR-MESSAGE                             
              ELSE                                                              
                 DISPLAY PC-PGM-NAME                                            
                         '  MQDISC SUCCESSFUL'                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      * ------------------------------------------------------------- *         
      * END THE PROGRAM                                                         
      * ------------------------------------------------------------- *         
       4000-END-PROGRAM.                                                        
                                                                                
      *    Display the number of messages successfully put                      
      *    to the queue                                                         
      *                                                                         
           DISPLAY PC-PGM-NAME                                                  
                   '  ' PV-NUMPUTS, ' MESSAGES PUT TO QUEUE'.                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      * ------------------------------------------------------------- *         
      * DISPLAY THE VALUES OF A FAILED MQ OR DB2 COMMAND                        
      * ------------------------------------------------------------- *         
       9000-DISPLAY-ERROR-MESSAGE.                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '** ABEND ***************************************'.          
           DISPLAY '** PROGRAM:   ', PC-PGM-NAME.                               
           DISPLAY '** PARAGRAPH: ', PV-PARAGRAPH.                              
           DISPLAY '** MESSAGE:   ', PV-ERROR-MESSAGE.                          
           MOVE PV-COMPLETION-CODE     TO PV-DISPLAY-NBR.                       
           DISPLAY '** COMP CODE: ', PV-DISPLAY-NBR.                            
           MOVE PV-REASON              TO PV-DISPLAY-NBR.                       
           DISPLAY '** RSN CODE:  ', PV-DISPLAY-NBR.                            
           DISPLAY '************************************************'.          
           SET AC-MQ-ERROR TO TRUE.                                     04354099
           CALL 'ILBOABN0' USING ABEND-CODE.                            04355099
                                                                                
