000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    TRKUP265.                                                 
000600 AUTHOR.        MIKE FRIESS.                                              
000700 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000800 DATE-WRITTEN   JUNE 2004.                                                
000900                                                                          
001000******************************************************************        
001100* THIS PROGRAM READS FILES FROM CONTAINER LOAD (CMN) AND TRKUP260         
001200* (CON) A PROGRAM CHECKS FOR CHANGES TO TRIP/CONTAINER/PO CHANGES         
001300* FOR PO OVERRIDES.  THE DATA IS USED TO ADD, CHANGE (DELETE/ADD)         
001400* OR DELETE DATA ON THE DECONSOLIDATOR CONRTROL PANEL RULES DATA-         
001500* BASE.                                                                   
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 INPUT-OUTPUT SECTION.                                                    
002100 FILE-CONTROL.                                                            
002200                                                                          
002300     SELECT RULES-INPUT-FILE                                              
002400       ASSIGN TO INP01.                                                   
002500                                                                          
002600     SELECT RULES-EXP-OUT-FILE                                            
002700       ASSIGN TO OUT01.                                                   
002800                                                                          
002900******************************************************************        
003000 DATA DIVISION.                                                           
003100******************************************************************        
003200                                                                          
003300 FILE SECTION.                                                            
003400                                                                          
003500 FD  RULES-INPUT-FILE                                                     
003600     RECORDING MODE IS F                                                  
003700     LABEL RECORDS ARE STANDARD                                           
003800     RECORD CONTAINS 500 CHARACTERS                                       
003900     BLOCK CONTAINS 0 RECORDS                                             
004000     DATA RECORD IS RULES-INPUT-RECORD.                                   
004100                                                                          
004200 01  RULES-INPUT-RECORD                     PIC X(500).                   
004300                                                                          
004400 FD  RULES-EXP-OUT-FILE                                                   
004500     RECORDING MODE IS F                                                  
004600     LABEL RECORDS ARE STANDARD                                           
004700     RECORD CONTAINS 500 CHARACTERS                                       
004800     BLOCK CONTAINS 0 RECORDS                                             
004900     DATA RECORD IS RULES-EXCEP-RECORD.                                   
005000                                                                          
005100 01  RULES-EXCEP-RECORD                     PIC X(500).                   
005200                                                                          
005300******************************************************************        
005400 WORKING-STORAGE SECTION.                                                 
005500******************************************************************        
005600*                                                                         
005700*                                                                         
005800******************************************************************        
005900*  DB2 FORMATTED MESSAGE AREA                                             
006000******************************************************************        
006100     COPY DPWS004.                                                        
006200*                                                                         
006300******************************************************************        
006400*  COMMUNICATIONS AREA FOR DB2                                            
006500******************************************************************        
006600     EXEC SQL                                                             
006700          INCLUDE SQLCA                                                   
006800     END-EXEC.                                                            
006900*                                                                         
007000******************************************************************        
007100*  DB2 TABLE DECLARATION - TRIP TABLE (TVHTRIP)                           
007200******************************************************************        
007300     EXEC SQL                                                             
007400          INCLUDE TVHTRIP                                                 
007500     END-EXEC.                                                            
007600*                                                                         
007700******************************************************************        
007800*  DB2 TABLE DECLARATION - SHIPMENT TO PURCHASE ORDER ASSOCIATION         
007900*      TABLE (TSHPTPO)                                                    
008000******************************************************************        
008100     EXEC SQL                                                             
008200          INCLUDE TSHPTPO                                                 
008300     END-EXEC.                                                            
008400*                                                                         
008500******************************************************************        
008600*  DB2 TABLE DECLARATION - SHIPMENT TABLE  (TSHIPMT)                      
008700******************************************************************        
008800     EXEC SQL                                                             
008900          INCLUDE TSHIPMT                                                 
009000     END-EXEC.                                                            
009100*                                                                         
009200******************************************************************        
009300*  DB2 TABLE DECLARATION - SHIPMENT STATUS TABLE LAYOUT                   
009400*                               (TSHIPST)                                 
009500******************************************************************        
009600     EXEC SQL                                                             
009700          INCLUDE TSHIPST                                                 
009800     END-EXEC.                                                            
009900*                                                                         
010000******************************************************************        
010100*  DB2 TABLE DECLARATION - SHIPMENT STATUS TYPE TABLE LAYOUT              
010200*                               (TSHSTYP)                                 
010300******************************************************************        
010400     EXEC SQL                                                             
010500          INCLUDE TSHSTYP                                                 
010600     END-EXEC.                                                            
010700*                                                                         
010800******************************************************************        
010900*  DB2 TABLE DECLARATION - PURCHASE ORDER PLANNED SHIPMENT                
011000*      TABLE  (TPLNSHP)                                                   
011100******************************************************************        
011200     EXEC SQL                                                             
011300          INCLUDE TPLNSHP                                                 
011400     END-EXEC.                                                            
011500*                                                                         
011600******************************************************************        
011700*  DB2 TABLE DECLARATION - PURCHASE ORDER PURCHASE TABLE (TPURCHS)        
011800******************************************************************        
011900     EXEC SQL                                                             
012000          INCLUDE TPURCHS                                                 
012100     END-EXEC.                                                            
012200*                                                                         
012300******************************************************************        
012400*  DB2 TABLE DECLARATION - PURCHASE ORDER PURCHASE ITEM TABLE             
012500*      (TPURITM)                                                          
012600******************************************************************        
012700     EXEC SQL                                                             
012800          INCLUDE TPURITM                                                 
012900     END-EXEC.                                                            
013000*                                                                         
013100******************************************************************        
013200*  DB2 TABLE DECLARATION - IMPORT PURCHASE ORDER RELEASE TABLE            
013300*      (TIMPORL)                                                          
013400******************************************************************        
013500     EXEC SQL                                                             
013600          INCLUDE TIMPORL                                                 
013700     END-EXEC.                                                            
013800*                                                                         
013900******************************************************************        
014000*  DB2 TABLE DECLARATION - TRAFFIC TRIP STATUS TABLE (TTRIPST)            
014100******************************************************************        
014200     EXEC SQL                                                             
014300          INCLUDE TTRIPST                                                 
014400     END-EXEC.                                                            
014500*                                                                         
014600******************************************************************        
014700*  DB2 TABLE DECLARATION - TRAFFIC TRIP STATUS TYPE TBL (TTRSTYP)         
014800******************************************************************        
014900     EXEC SQL                                                             
015000          INCLUDE TTRSTYP                                                 
015100     END-EXEC.                                                            
015200*                                                                         
015300******************************************************************        
015400*  DB2 TABLE DECLARATION - TRAFFIC VEHICLE TABLE (TVEHICL)                
015500******************************************************************        
015600     EXEC SQL                                                             
015700          INCLUDE TVEHICL                                                 
015800     END-EXEC.                                                            
015900                                                                          
016000******************************************************************        
016100*  DB2 TABLE DECLARATION - SHPMNT/PO ASSOCIATN AUDT TBL (TAUSHPO)         
016200******************************************************************        
016300     EXEC SQL                                                             
016400          INCLUDE TAUSHPO                                                 
016500     END-EXEC.                                                            
016600******************************************************************        
016700*  DB2 TABLE DECLARATION - TRIP AUDIT TABLE       (TAUTRIP)               
016800******************************************************************        
016900     EXEC SQL                                                             
017000          INCLUDE TAUTRIP                                                 
017100     END-EXEC.                                                            
017200******************************************************************        
017300*  DB2 TABLE DECLARATION - CHECKPOINT RESTART INFO TBL(TCKRSTINF)         
017400*  THIS TABLE IS USED TO STORE WORKING STOREAGE INFORMATION FOR           
017500*  CHECKPOINT RESTARTS.                                                   
017600******************************************************************        
017700     EXEC SQL                                                             
017800         INCLUDE TCKRSTIN                                                 
017900     END-EXEC.                                                            
018000*                                                                         
018100******************************************************************        
018200*  DB2 TABLE DECLARATION - CHECKPOINT RESTART CONTROL (TCKRSTCNTL)        
018300*  THIS TABLE IS USED FOR CHECKPOINT RESTARTS.                            
018400******************************************************************        
018500     EXEC SQL                                                             
018600         INCLUDE TCKRSTCN                                                 
018700     END-EXEC.                                                            
018800*                                                                         
018900******************************************************************        
019000*  DB2 TABLE DECLARATION - TRANSPORTATION PO TRACKING - CONTAINER         
019100*  PO TABLE.                                                              
019200******************************************************************        
019300     EXEC SQL                                                             
019400         INCLUDE TIMCNPO                                                  
019500     END-EXEC.                                                            
019600*                                                                         
019700******************************************************************        
019800*  DB2 TABLE DECLARATION - TRANSPORTATION PO TRACKING - CONTAINER         
019900*  TRACKING TABLE.                                                        
020000******************************************************************        
020100     EXEC SQL                                                             
020200         INCLUDE TIMCNTK                                                  
020300     END-EXEC.                                                            
020400*                                                                         
020500******************************************************************        
020600*  DB2 TABLE DECLARATION - TRANSPORTATION PO TRACKING - CONTROL           
020700*  FILE TABLE.                                                            
020800******************************************************************        
020900     EXEC SQL                                                             
021000         INCLUDE TIMCNTL                                                  
021100     END-EXEC.                                                            
021200*                                                                         
021300******************************************************************        
021400*  DB2 TABLE DECLARATION - TRANSPORTATION PO TRACKING - CONTAINER         
021500*  RELEASE SPLITS TABLE.                                                  
021600******************************************************************        
021700     EXEC SQL                                                             
021800         INCLUDE TIMCNRL                                                  
021900     END-EXEC.                                                            
022000*                                                                         
022100******************************************************************        
022200*  DB2 TABLE DECLARATION - TRANSPORTATION PO TRACKING - PORT OF           
022300*  ENTRY CODE TABLE.                                                      
022400******************************************************************        
022500     EXEC SQL                                                             
022600         INCLUDE TIMPOE                                                   
022700     END-EXEC.                                                            
022800*                                                                         
022900******************************************************************        
023000*  DB2 TABLE DECLARATION - TRANSPORTATION PO TRACKING - FREIGHT           
023100*  ORIGIN CODE TABLE.                                                     
023200******************************************************************        
023300     EXEC SQL                                                             
023400         INCLUDE TIMFOB                                                   
023500     END-EXEC.                                                            
023600*                                                                         
023700******************************************************************        
023800*  DB2 TABLE DECLARATION - KOHL'S ENTITY TABLE                            
023900*  ORIGIN CODE TABLE.                                                     
024000******************************************************************        
024100     EXEC SQL                                                             
024200         INCLUDE TEXTENT                                                  
024300     END-EXEC.                                                            
024400*                                                                         
024500******************************************************************        
024600* DECON CONTROL PANEL RULE TABLE - TRT_RUL                                
024700******************************************************************        
024800     EXEC SQL                                                             
024900         INCLUDE TRT00014                                                 
025000     END-EXEC.                                                            
025100*                                                                         
025200******************************************************************        
025300* DECON CONTROL PANEL RULE CONSTRAINT TABLE - TRT_RUL_CNSTR               
025400******************************************************************        
025500     EXEC SQL                                                             
025600         INCLUDE TRT00015                                                 
025700     END-EXEC.                                                            
025800*                                                                         
025900******************************************************************        
026000* DECON CONTROL PANEL AUDIT RULE TABLE - TRT_AUD_RUL                      
026100******************************************************************        
026200     EXEC SQL                                                             
026300         INCLUDE TRT00017                                                 
026400     END-EXEC.                                                            
026500*                                                                         
026600******************************************************************        
026700* DECON CONTROL PANEL RULE AUDIT CONSTRAINT TABLE -                       
026800*                                           TRT_AUD_RUL_CNSTR             
026900******************************************************************        
027000     EXEC SQL                                                             
027100         INCLUDE TRT00018                                                 
027200     END-EXEC.                                                            
027300*                                                                         
027400******************************************************************        
027500* D266-GET-BEGIN-END-DATES (TRPD266) WORK FIELDS.                         
027600******************************************************************        
027700     COPY TRRD266.                                                        
027800                                                                          
027900******************************************************************        
028000* RULES IMPUT RECORD FROM CONTAINER LOAD (TRK200) AND PO OVERRIDE         
028100* CHECKER (TRK???).                                                       
028200******************************************************************        
028300     COPY TRRD265.                                                        
028400                                                                          
028500 EJECT                                                                    
028600                                                                          
028700 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
028800                                                   COMP SYNC.             
028900*                                                                         
029000 01  PA-PROGRAM-ACCUMULATORS.                                             
029100     05  PA-TOTALS.                                                       
029200         10 PA-TOTAL-INPUT-REC-COUNT   PIC  9(9)  VALUE 0.                
029300         10 PA-TOTAL-INPUT-REC-COUNT-X                                    
029400                  REDEFINES PA-TOTAL-INPUT-REC-COUNT                      
029500                                         PIC X(9).                        
029510         10 PA-TOTAL-LOADED-RUL-COUNT   PIC  9(9)  VALUE 0.               
029520         10 PA-TOTAL-LOADED-RUL-COUNT-X                                   
029530                  REDEFINES PA-TOTAL-LOADED-RUL-COUNT                     
029540                                         PIC X(9).                        
029600                                                                          
029700 01  PS-PROGRAM-SWITCHES.                                                 
029800     05  PS-EOF-RULES-IN-SW            PIC  X(01)    VALUE 'N'.           
029900         88  PS-EOF-RULES-IN                         VALUE 'Y'.           
030000     05  PS-PO-SEL-INFO-PRESENT-SW     PIC  X(01)    VALUE 'N'.           
030100         88 PS-PO-SEL-INFO-PRESENT                   VALUE 'Y'.           
030200         88 PS-PO-SEL-INFO-PRESENT-NO                VALUE 'N'.           
030300     05  PS-TIMCNTK-EXISTS-SW          PIC  X(01)    VALUE 'N'.           
030400         88 PS-TIMCNTK-EXISTS                        VALUE 'Y'.           
030500         88 PS-TIMCNTK-EXISTS-NO                     VALUE 'N'.           
030600     05  PS-PO-CONSTRAINTS-SW          PIC  X(01)    VALUE 'N'.           
030700         88 PS-PO-CONSTRAINTS                        VALUE 'Y'.           
030800         88 PS-PO-CONSTRAINTS-NO                     VALUE 'N'.           
030900     05  PS-BYPASS-CONTAINER-SW        PIC  X(01)    VALUE 'N'.           
031000         88 PS-BYPASS-CONTAINER                      VALUE 'Y'.           
031100         88 PS-BYPASS-CONTAINER-NO                   VALUE 'N'.           
031200     05  PS-LANDED-POS-SW              PIC  X(01)    VALUE 'N'.           
031300         88 PS-LANDED-POS                            VALUE 'Y'.           
031400         88 PS-LANDED-POS-NO                         VALUE 'N'.           
031500     05  PS-DFLT-RULE-EXISTS-SW        PIC  X(01)    VALUE 'N'.           
031600         88 PS-DFLT-RULE-EXISTS                      VALUE 'Y'.           
031700         88 PS-DFLT-RULE-EXISTS-NO                   VALUE 'N'.           
031800     05  PS-AUD-ROW-EXISTS-SW          PIC  X(01)    VALUE 'N'.           
031900         88 PS-AUD-ROW-EXISTS                        VALUE 'Y'.           
032000         88 PS-AUD-ROW-EXISTS-NO                     VALUE 'N'.           
032100     05  PS-TPLNSHP-SELECTED-SW        PIC  X(01)    VALUE 'N'.           
032200         88 PS-TPLNSHP-SELECTED                      VALUE 'Y'.           
032300         88 PS-TPLNSHP-SELECTED-NO                   VALUE 'N'.           
032400     05  PS-SCAC-FROM-TIMPOE-SW        PIC  X(01)    VALUE 'N'.           
032500         88 PS-SCAC-FROM-TIMPOE                      VALUE 'Y'.           
032600         88 PS-SCAC-FROM-TIMPOE-NO                   VALUE 'N'.           
032700     05  PS-SCAC-FROM-TIMFOB-SW        PIC  X(01)    VALUE 'N'.           
032800         88 PS-SCAC-FROM-TIMFOB                      VALUE 'Y'.           
032900         88 PS-SCAC-FROM-TIMFOB-NO                   VALUE 'N'.           
033000     05  PS-DECON-ON-TIMFOB-SW         PIC  X(01)    VALUE 'N'.           
033100         88 PS-DECON-ON-TIMFOB                       VALUE 'Y'.           
033200         88 PS-DECON-ON-TIMFOB-NO                    VALUE 'N'.           
033300     05  PS-DECON-ON-TIMPOE-SW         PIC  X(01)    VALUE 'N'.           
033400         88 PS-DECON-ON-TIMPOE                       VALUE 'Y'.           
033500         88 PS-DECON-ON-TIMPOE-NO                    VALUE 'N'.           
033600     05  PS-EOC-CNSTR-CUR-SW           PIC  X(01)    VALUE 'N'.           
033700         88 PS-EOC-CNSTR-CUR                         VALUE 'Y'.           
033800         88 PS-EOC-CNSTR-CUR-NO                      VALUE 'N'.           
033801     05  PS-EOC-CONPO-CNSTR-CUR-SW     PIC  X(01)    VALUE 'N'.           
033802         88 PS-EOC-CONPO-CNSTR-CUR                   VALUE 'Y'.           
033803         88 PS-EOC-CONPO-CNSTR-CUR-NO                VALUE 'N'.           
033804     05  PS-EOC-PO-CUR-SW              PIC  X(01)    VALUE 'N'.           
033805         88 PS-EOC-PO-CUR                            VALUE 'Y'.           
033806         88 PS-EOC-PO-CUR-NO                         VALUE 'N'.           
033810     05 PS-DATA-CHANGED-SW             PIC  X(01)    VALUE 'N'.           
033820         88 PS-DATA-CHANGED                          VALUE 'Y'.           
033830         88 PS-DATA-CHANGED-NO                       VALUE 'N'.           
033840     05 PS-FOR-CONPO-ONLY-SW           PIC  X(01)    VALUE 'N'.           
033850         88 PS-FOR-CONPO-ONLY                        VALUE 'Y'.           
033860         88 PS-FOR-CONPO-ONLY-NO                     VALUE 'N'.           
033870     05 PS-REUSED-CONTAINER-SW         PIC  X(01)    VALUE 'N'.           
033880         88 PS-REUSED-CONTAINER                      VALUE 'Y'.           
033890         88 PS-REUSED-CONTAINER-NO                   VALUE 'N'.           
033900                                                                          
034000     05  PS-RUN-TYPE-SW                PIC  X(01)    VALUE ' '.           
034100         88  PS-NORMAL-RUN                           VALUE '0'.           
034200         88  PS-RESTART-RUN                          VALUE '9'.           
034300     05  PS-COMMITS-TAKEN-SW           PIC  X(01)    VALUE ' '.           
034400         88  PS-COMMITS-TAKEN                        VALUE '0'.           
034500                                                                          
034600 01  PC-PROGRAM-CONSTANTS.                                                
034700     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
034800         'TRKUP265'.                                                      
034900     05  PC-CURRENT-JOB-NAME         PIC  X(06)    VALUE 'TRK260'.        
035000     05  PC-IN-PROCESS-CODE          PIC X(01)     VALUE '9'.             
035100     05  PC-RUN-COMPLETE-CODE        PIC X(01)     VALUE '0'.             
035200     05  PC-NULL-DTE                PIC X(10) VALUE '9999-09-09'.         
035300     05  PC-DEFAULT-TMST             PIC X(26)     VALUE                  
035400                                     '0001-01-01-00.00.00.000000'.        
035500     05  PC-DEFAULT-DTE              PIC X(10)     VALUE                  
035600                                     '0001-01-01'.                        
035700*                                                                         
035800 01  PV-PROGRAM-VARIABLES.                                                
035900     05  PV-HOLD-TMST                PIC X(26)     VALUE SPACES.          
036000     05  PV-CNTNR-SEQ-NBR            PIC 9(06)     VALUE ZERO.            
036100     05  PV-CNTNR-SEQ-NBR-X REDEFINES PV-CNTNR-SEQ-NBR                    
036200                                     PIC X(06).                           
036300     05  PV-TMST                     PIC X(26)     VALUE SPACES.          
036400     05  PV-IMPORTS-COUNT            PIC S9(04)    VALUE +0               
036500                                                   COMP SYNC.             
036510     05  PV-CONPO-COUNT              PIC S9(04)    VALUE +0               
036520                                                   COMP SYNC.             
036600     05  PV-CURRENT-RULE-ID          PIC S9(9) COMP.                      
036700     05  PV-TYP-SEQ-OR-ETA           PIC X(08)     VALUE SPACES.          
036800     05  PV-SEQ-OR-ETA               PIC X(30)     VALUE SPACES.          
036900                                                                          
037000     05  PV-INSERTED-RULE-ID         PIC S9(9) COMP.                      
037100                                                                          
037200     05  PV-AUDIT-RULE-ID            PIC S9(9) COMP.                      
037300     05  PV-AUDIT-DECON-SCAC         PIC X(30)     VALUE SPACES.          
037400     05  PV-NULL                     PIC S9(04) VALUE +0                  
037500                                                   COMP.                  
037600     05  PV-NEXT-RUL-CNSTR-CDE       PIC S9(04) VALUE +0                  
037700                                                   COMP.                  
037800     05  PV-CONT-USED                PIC X(12)     VALUE SPACES.          
037900     05  PV-SEQ-USED                 PIC X(06)     VALUE SPACES.          
038000     05  PV-ETA-USED                 PIC X(26)     VALUE SPACES.          
038100     05  PV-VES-USED                 PIC X(25)     VALUE SPACES.          
038200     05  PV-VOY-USED                 PIC X(12)     VALUE SPACES.          
038300     05  PV-FOB-USED                 PIC X(03)     VALUE SPACES.          
038400     05  PV-POE-USED                 PIC X(03)     VALUE SPACES.          
038500     05  PV-DECON-SCAC-USED          PIC X(04)     VALUE SPACES.          
038510     05  PV-PO-NBR                   PIC 9(09).                           
038520     05  PV-PO-NBR-X  REDEFINES PV-PO-NBR                                 
038530                                     PIC X(09).                           
038600                                                                          
038700 01  PV-CKPT-RESTART-INFO.                                                
038800     05  PV-RESTART-TOT-IN-REC-COUNT PIC  S9(13)     VALUE +0             
038900                                                     COMP-3.              
039000     05  PV-CKPT-STAT-CODE           PIC X(01)     VALUE SPACE.           
039100                                                                          
039200*                                                                         
039300*  CURSOR DECLARATION TO OBTAIN ALL RULE CONSTRAINTS                      
039400*                                                                         
039500     EXEC SQL                                                             
039600          DECLARE CNSTR_CUR CURSOR FOR                                    
039700           SELECT  RUL_ID                                                 
039800                  ,RUL_CNSTR_CDE                                          
039900                  ,RUL_CNSTR_TYP_CDE                                      
040000                  ,RUL_CNSTR_VAL_TXT                                      
040100                  ,CRE_TMST                                               
040200                  ,CRE_BY_USR_ID                                          
040300             FROM TRT_RUL_CNSTR                                           
040400             WHERE RUL_ID = :PV-CURRENT-RULE-ID                           
040500     END-EXEC.                                                            
040510*                                                                         
040520*  CURSOR DECLARATION TO OBTAIN ONLY CNPO RULE CONSTRAINTS                
040530*                                                                         
040540     EXEC SQL                                                             
040550          DECLARE CNSTR_CNPO_CUR CURSOR FOR                               
040560           SELECT  RUL_ID                                                 
040570                  ,RUL_CNSTR_CDE                                          
040580                  ,RUL_CNSTR_TYP_CDE                                      
040590                  ,RUL_CNSTR_VAL_TXT                                      
040591                  ,CRE_TMST                                               
040592                  ,CRE_BY_USR_ID                                          
040593             FROM TRT_RUL_CNSTR                                           
040594             WHERE RUL_ID = :PV-CURRENT-RULE-ID                           
040595               AND RUL_CNSTR_TYP_CDE = 'CONPO'                            
040596     END-EXEC.                                                            
040600*                                                                         
040700*  CURSOR DECLARATION TO OBTAIN CONPO ORDERS FOR EACH CONTAINER           
040800*                                                                         
040900     EXEC SQL                                                             
041000          DECLARE PO_CUR CURSOR FOR                                       
041010            SELECT DISTINCT SHPO.PO_NBR                                   
041020              FROM TSHIPMT SHPMT                                          
041021                  ,TPURCHS PO                                             
041022                  ,TSHPTPO SHPO                                           
041023                  ,TVHTRIP TRIP                                           
041030             WHERE SHPMT.PRO_NBR  = :TR265-CONTNR-ID                      
041040               AND TRIP.EST_AR_TMST                                       
041041                          = :TR265-CONTNR-POE-DELY-TMST-CUR               
041050               AND TRIP.TRIP_ID   = SHPMT.TRIP_ID                         
041060               AND SHPMT.SHIPT_ID = SHPO.SHIPT_ID                         
041070               AND PO.PO_NBR      = SHPO.PO_NBR                           
042000               AND PO.VER_STATUS_CDE   = 'A'                              
042001     END-EXEC.                                                            
042010                                                                          
042100******************************************************************        
042200 LINKAGE SECTION.                                                         
042300******************************************************************        
042400                                                                          
042500******************************************************************        
042600 PROCEDURE DIVISION.                                                      
042700******************************************************************        
042800                                                                          
042900 0000-PROGRAM-MAINLINE.                                                   
043000                                                                          
043100     PERFORM 1000-INITIALIZE.                                             
043200                                                                          
043300     IF PS-EOF-RULES-IN                                                   
043400        DISPLAY 'INPUT FILE EMPTY'                                        
043500     ELSE                                                                 
043600        PERFORM 2000-PROCESS-RULES                                        
043700          UNTIL PS-EOF-RULES-IN                                           
043800     END-IF.                                                              
043900                                                                          
044000     PERFORM 9000-EOJ-ROUTINE.                                            
044100     STOP RUN.                                                            
044200                                                                          
044300******************************************************************        
044400*  OPEN RULES FILE, READ FIRST RECORD.                                    
044500*  CHECKPOIN RESTAR LOGIC.                                                
044600******************************************************************        
044700 1000-INITIALIZE.                                                         
044800                                                                          
044900     OPEN INPUT RULES-INPUT-FILE.                                         
045000     OPEN OUTPUT RULES-EXP-OUT-FILE.                                      
045100                                                                          
045200     PERFORM 8300-GET-CHECKPOINT-CNTL.                                    
045300     MOVE PV-CKPT-STAT-CODE       TO                                      
045400         PS-RUN-TYPE-SW.                                                  
045500                                                                          
045600     IF PS-RESTART-RUN                                                    
045700        DISPLAY 'RESTART TRKUP265'                                        
045800        PERFORM 8400-CHECKPOINT-RESTART                                   
045900     ELSE                                                                 
046000        PERFORM 5000-READ-RULES-FILE                                      
046100     END-IF.                                                              
046200                                                                          
046300     IF PS-EOF-RULES-IN                                                   
046400        CONTINUE                                                          
046500     ELSE                                                                 
046600        PERFORM 8200-SET-CHECKPOINT-TIME                                  
046700     END-IF.                                                              
046800                                                                          
046900******************************************************************        
047000 2000-PROCESS-RULES.                                                      
047100                                                                          
047200     SET PS-BYPASS-CONTAINER-NO TO TRUE.                                  
047210     INITIALIZE PV-SEQ-OR-ETA.                                            
047220                                                                          
047300     IF TR265-CONTNR-REC-SRC-CMN                                          
047400       PERFORM 2100-CMN-RULES                                             
047500     ELSE                                                                 
047600       IF TR265-CONTNR-REC-SRC-CON                                        
047700         PERFORM 2500-CON-RULES                                           
047800       ELSE                                                               
047900         MOVE 'INVALID REC SOURCE CDE - NOT PROCESSED'                    
048000                              TO TR265-ERROR-TEXT                         
048100         SET TR265-REPORT-REC-TYP-ERR TO TRUE                             
048200         PERFORM 5100-WRITE-EXCEPTION-REC                                 
048300         SET PS-BYPASS-CONTAINER   TO TRUE                                
048400       END-IF                                                             
048500     END-IF.                                                              
048600                                                                          
048700     PERFORM 8000-CHECKPOINT-CHECK.                                       
048800                                                                          
048900     PERFORM 5000-READ-RULES-FILE.                                        
049000                                                                          
049100 EJECT                                                                    
049200                                                                          
049300******************************************************************        
049400 2100-CMN-RULES.                                                          
049700                                                                          
049900     IF TR265-CONTNR-DVN-TMST > PC-DEFAULT-TMST                           
049910      MOVE 'CMN - CONTAINER HAS BEEN DEVANED - NOT PROCESSED'             
049920                                  TO TR265-ERROR-TEXT                     
049930      SET TR265-REPORT-REC-TYP-ERR TO TRUE                                
049940      PERFORM 5100-WRITE-EXCEPTION-REC                                    
050000      SET PS-BYPASS-CONTAINER TO TRUE                                     
050100     END-IF.                                                              
050200                                                                          
051800     IF PS-BYPASS-CONTAINER                                               
051900       CONTINUE                                                           
052000     ELSE                                                                 
052001*      *************************************************                  
052002*      * THERE ARE CONTAINERS THAT ARE BEING REUSED    *                  
052003*      * ON A NEW TRIP, SO DON'T RESTORE ANY WORKING   *                  
052004*      * OR ADJUSTED RULES AND USE THE CURRENT SEQ#    *                  
052005*      * TO SEE IF THE RULE HAS BEEN PREVIOUSLY LOADED *                  
052006*      *************************************************                  
052007       SET PS-REUSED-CONTAINER-NO TO TRUE                                 
052010       IF  TR265-CONTNR-TRN-TYP-ADD                                       
052030         AND TR265-CONTNR-SEQ-NBR-CUR                                     
052040                       NOT =  TR265-CONTNR-SEQ-NBR-PREV                   
052050        SET PS-REUSED-CONTAINER TO TRUE                                   
052060       END-IF                                                             
052070                                                                          
052100*      ************************************************                   
052200*      * SET UP CONTAINER SEQUENCE FOR SELECT OF OLD  *                   
052300*      * RULES.  CHANGES AND ADDS USE PREV SEQ, FOR   *                   
052400*      * DELETES USE CURRENT.                         *                   
052500*      ************************************************                   
052600       IF TR265-CONTNR-TRN-TYP-DEL                                        
052700         OR TR265-CONTNR-SEQ-NBR-PREV = ZERO                              
052710         OR PS-REUSED-CONTAINER                                           
052800          MOVE TR265-CONTNR-SEQ-NBR-CUR                                   
052900                                 TO PV-CNTNR-SEQ-NBR                      
053000       ELSE                                                               
053100          MOVE TR265-CONTNR-SEQ-NBR-PREV                                  
053200                                 TO PV-CNTNR-SEQ-NBR                      
053300       END-IF                                                             
053700       MOVE 'SEQ' TO PV-TYP-SEQ-OR-ETA                                    
053800       MOVE PV-CNTNR-SEQ-NBR-X   TO PV-SEQ-OR-ETA                         
053801*      **** ALWAYS USE CURRENT SEQ FOR SELECTING FOB ETC.                 
053802       MOVE TR265-CONTNR-SEQ-NBR-CUR  TO IMCNTK-CONTNR-SEQ-NBR            
053810                                                                          
053900*      ************************************************                   
054000*      * WHICH ETA DATE TO USE.  IF CHANGES, USE      *                   
054100*      * PREV FIELDS, ELSE USE CURRENT.               *                   
054200*      ************************************************                   
054300       IF TR265-CONTNR-TRN-TYP-CHG                                        
054400          MOVE TR265-CONTNR-POE-DELY-TMST-PRV (1:10)                      
054500                                 TO PV-266-DATE-ENTERED                   
054600       ELSE                                                               
054700          MOVE TR265-CONTNR-POE-DELY-TMST-CUR (1:10)                      
054800                                 TO PV-266-DATE-ENTERED                   
054900       END-IF                                                             
055000                                                                          
055100*      *************************************************                  
055200*      * CHECK IF THIS CONTAINER IS ALL LANDED POS.    *                  
055300*      * IF IT IS, USE DO NOT SHIP BEFORE DATE NOT ETA.*                  
055400*      *************************************************                  
055500       PERFORM 2120-SELECT-LD-DNT-SHP-DT-CMN                              
055600       IF PS-LANDED-POS                                                   
055700         PERFORM 2130-SELECT-COUNT-IMPORTS-CMN                            
055710         DISPLAY '#### LANDED PO ####'                                    
055800*        *** ALL POS ON CONTAINER NEED TO BE LANDED ****                  
055900         IF PV-IMPORTS-COUNT = ZERO                                       
056100            MOVE PLNSHP-DONT-SHIP-BEF-DTE                                 
056200                        TO PV-266-DATE-ENTERED                            
056210         ELSE                                                             
056220            SET PS-LANDED-POS-NO TO TRUE                                  
056300         END-IF                                                           
056400       END-IF                                                             
056500     END-IF.                                                              
056600                                                                          
056610*    *****************************************************                
056620*    * IF THERE IS A PO OVERRIDE, DON'T MESS WITH RULES. *                
056630*    *****************************************************                
056640     IF PS-BYPASS-CONTAINER                                               
056650       CONTINUE                                                           
056660     ELSE                                                                 
056670       PERFORM 7110-SELECT-PO-CONSTAINTS                                  
056680       IF PS-PO-CONSTRAINTS                                               
056690         MOVE 'CMN - PO OVERRIDE FOUND - NOT PROCESSED'                   
056691                                     TO TR265-ERROR-TEXT                  
056692         SET TR265-REPORT-REC-TYP-ERR TO TRUE                             
056693         PERFORM 5100-WRITE-EXCEPTION-REC                                 
056694         SET PS-BYPASS-CONTAINER   TO TRUE                                
056695       END-IF                                                             
056696     END-IF.                                                              
056697                                                                          
056700     IF PS-BYPASS-CONTAINER                                               
056800       CONTINUE                                                           
056900     ELSE                                                                 
057000*       *******************************************                       
057100*       * GET PV-266-BEGIN-DATE & PV-266-END-DATE *                       
057200*       *******************************************                       
057300        PERFORM D266-GET-BEGIN-END-DATES                                  
057400        IF PS-266-ERRORS                                                  
057500          DISPLAY '****** CMN PROCESSING *******'                         
057600          DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                          
057700          DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                     
057800          PERFORM 9100-SQL-ABEND                                          
057900        END-IF                                                            
058000     END-IF.                                                              
058100                                                                          
058200     IF PS-BYPASS-CONTAINER                                               
058300       CONTINUE                                                           
058400     ELSE                                                                 
058401       PERFORM 2900-DECIDE-ADD-DEL-CHG                                    
058402     END-IF.                                                              
058403                                                                          
064400******************************************************************        
064500 2120-SELECT-LD-DNT-SHP-DT-CMN.                                           
064600                                                                          
064700     SET PS-LANDED-POS-NO  TO TRUE.                                       
064800     INITIALIZE PV-NULL.                                                  
064900                                                                          
065000     EXEC SQL                                                             
065100           SELECT MAX(PL.DONT_SHIP_BEF_DTE - 2 DAYS)                      
065200             INTO :PLNSHP-DONT-SHIP-BEF-DTE:PV-NULL                       
065300             FROM TPURCHS   PO                                            
065400                , TIMCNPO   CNPO                                          
065500                , TPLNSHP   PL                                            
065600             WHERE CNPO.CONTNR_ID      = :TR265-CONTNR-ID                 
065700               AND CNPO.CONTNR_SEQ_NBR = :IMCNTK-CONTNR-SEQ-NBR           
065800               AND PO.PO_NBR           = CNPO.PO_NBR                      
065900               AND PO.VER_STATUS_CDE   = 'A'                              
066000               AND PO.PO_TYP_CDE       NOT IN ('IM','IE','IF' )           
066100               AND PL.PO_NBR           = PO.PO_NBR                        
066200               AND PL.VER_STATUS_CDE   = 'A'                              
066300     END-EXEC                                                             
066400                                                                          
066500     EVALUATE TRUE                                                        
066600         WHEN SQLCODE = ZERO                                              
066700           IF PV-NULL < +0                                                
066800            CONTINUE                                                      
066900           ELSE                                                           
067000             SET PS-LANDED-POS TO TRUE                                    
067100           END-IF                                                         
067200                                                                          
067300         WHEN SQLCODE = +100                                              
067400             CONTINUE                                                     
067500                                                                          
067600         WHEN SQLWARN0 NOT EQUAL SPACE                                    
067700         WHEN SQLCODE NOT EQUAL ZERO                                      
067800         DISPLAY '** ABEND **'                                            
067900         DISPLAY '** PROGRAM = TRKUP265 **'                               
068000         DISPLAY '** PARAGRAPH = 2120-SELECT-LD-DNT-SHP-DT-CMN'           
068100         DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                           
068200         DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                      
068300         PERFORM 9100-SQL-ABEND                                           
068400     END-EVALUATE.                                                        
068500                                                                          
068600******************************************************************        
068700 2130-SELECT-COUNT-IMPORTS-CMN.                                           
068800                                                                          
068900     INITIALIZE PV-IMPORTS-COUNT.                                         
069000                                                                          
069100     EXEC SQL                                                             
069200           SELECT COUNT (*)                                               
069300             INTO :PV-IMPORTS-COUNT                                       
069400             FROM TPURCHS   PO                                            
069500                , TIMCNPO   CNPO                                          
069600             WHERE CNPO.CONTNR_ID      = :TR265-CONTNR-ID                 
069700               AND CNPO.CONTNR_SEQ_NBR = :IMCNTK-CONTNR-SEQ-NBR           
069800               AND PO.PO_NBR           = CNPO.PO_NBR                      
069900               AND PO.VER_STATUS_CDE   = 'A'                              
070000               AND PO.PO_TYP_CDE       IN ('IM','IE','IF' )               
070100     END-EXEC                                                             
070200                                                                          
070300     EVALUATE TRUE                                                        
070400         WHEN SQLCODE = ZERO                                              
070500         WHEN SQLCODE = +100                                              
070600             CONTINUE                                                     
070700                                                                          
070800         WHEN SQLWARN0 NOT EQUAL SPACE                                    
070900         WHEN SQLCODE NOT EQUAL ZERO                                      
071000          DISPLAY '** ABEND **'                                           
071100          DISPLAY '** PROGRAM = TRKUP265 **'                              
071200          DISPLAY '** PARAGRAPH = 2130-SELECT-COUNT-IMPORTS-CMN'          
071300          DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                          
071400          DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                     
071500          PERFORM 9100-SQL-ABEND                                          
071600     END-EVALUATE.                                                        
071700                                                                          
071710******************************************************************        
071720 2500-CON-RULES.                                                          
071730                                                                          
071740*    *** MAKE SURE DATES WERE KEYED                                       
071741     IF (TR265-CONTNR-POE-DELY-TMST-PRV = SPACES                          
071742          OR TR265-CONTNR-POE-DELY-TMST-PRV = LOW-VALUES)                 
071743      AND (TR265-CONTNR-POE-DELY-TMST-CUR = SPACES                        
071744          OR TR265-CONTNR-POE-DELY-TMST-CUR = LOW-VALUES)                 
071745         MOVE                                                             
071746           'CON - INVALID ETA DATES'                                      
071747                                       TO TR265-ERROR-TEXT                
071748         SET TR265-REPORT-REC-TYP-ERR TO TRUE                             
071749         PERFORM 5100-WRITE-EXCEPTION-REC                                 
071750       SET PS-BYPASS-CONTAINER TO TRUE                                    
071751     END-IF.                                                              
071752                                                                          
071753     IF PS-BYPASS-CONTAINER                                               
071754       CONTINUE                                                           
071755     ELSE                                                                 
071756*      ***DEPENDS ON ADD CHANGE OR DELETE                                 
071757       IF TR265-CONTNR-POE-DELY-TMST-PRV = SPACES                         
071758          OR TR265-CONTNR-POE-DELY-TMST-PRV = LOW-VALUES                  
071760         MOVE TR265-CONTNR-POE-DELY-TMST-CUR                              
071770                                TO IMCNTK-MOM-POE-DELY-TMST               
071780       ELSE                                                               
071790         MOVE TR265-CONTNR-POE-DELY-TMST-PRV                              
071791                                TO IMCNTK-MOM-POE-DELY-TMST               
071792       END-IF                                                             
071793     END-IF.                                                              
071794                                                                          
071795     IF PS-BYPASS-CONTAINER                                               
071796       CONTINUE                                                           
071797     ELSE                                                                 
071798       PERFORM 2510-SELECT-DEVAN-CON                                      
071799       IF PS-TIMCNTK-EXISTS                                               
071800        AND IMCNTK-DVN-TMST > PC-DEFAULT-TMST                             
071801         MOVE                                                             
071802           'CON - CONTAINER HAS BEEN DEVANED - NOT PROCESSED'             
071803                                       TO TR265-ERROR-TEXT                
071804         SET TR265-REPORT-REC-TYP-ERR TO TRUE                             
071805         PERFORM 5100-WRITE-EXCEPTION-REC                                 
071806         SET PS-BYPASS-CONTAINER TO TRUE                                  
071807       END-IF                                                             
071808     END-IF.                                                              
071809                                                                          
071810     IF PS-BYPASS-CONTAINER                                               
071811       CONTINUE                                                           
071812     ELSE                                                                 
071813*      ************************************************                   
071814*      * SET UP FIELDS FOR SELECTS.  IF CHANGES, USE  *                   
071815*      * PREV FIELDS, ELSE USE CURRENT.               *                   
071816*      ************************************************                   
071817       IF TR265-CONTNR-TRN-TYP-CHG                                        
071818        AND (TR265-CONTNR-POE-DELY-TMST-PRV  NOT = SPACES                 
071819              AND                                                         
071820             TR265-CONTNR-POE-DELY-TMST-PRV  NOT = LOW-VALUES)            
071822          MOVE TR265-CONTNR-POE-DELY-TMST-PRV (1:10)                      
071823                                 TO PV-266-DATE-ENTERED                   
071824                                    PV-SEQ-OR-ETA                         
071825       ELSE                                                               
071826          MOVE TR265-CONTNR-POE-DELY-TMST-CUR (1:10)                      
071827                                 TO PV-266-DATE-ENTERED                   
071828                                    PV-SEQ-OR-ETA                         
071829       END-IF                                                             
071830*      * ***************************************                          
071831*      * FOR CON, USE THE ETA TO MATCH CONT ID *                          
071832*      * ***************************************                          
071833       MOVE 'ETA' TO PV-TYP-SEQ-OR-ETA                                    
071834                                                                          
071835*      *************************************************                  
071836*      * CHECK IF THIS CONTAINER IS ALL LANDED POS.    *                  
071837*      * IF IT IS, USE DO NOT SHIP BEFORE DATE NOT ETA.*                  
071838*      *************************************************                  
071839       PERFORM 2520-SELECT-LD-DNT-SHP-DT-CON                              
071840       IF PS-LANDED-POS                                                   
071841         PERFORM 2530-SELECT-COUNT-IMPORTS-CON                            
071842*        *** ALL POS ON CONTAINER NEED TO BE LANDED ****                  
071843         IF PV-IMPORTS-COUNT = ZERO                                       
071844            MOVE PLNSHP-DONT-SHIP-BEF-DTE                                 
071845                        TO PV-266-DATE-ENTERED                            
071846         END-IF                                                           
071847       END-IF                                                             
071848     END-IF.                                                              
071849                                                                          
071850     IF PS-BYPASS-CONTAINER                                               
071851       CONTINUE                                                           
071852     ELSE                                                                 
071853*       *******************************************                       
071854*       * GET PV-266-BEGIN-DATE & PV-266-END-DATE *                       
071855*       *******************************************                       
071856        PERFORM D266-GET-BEGIN-END-DATES                                  
071857        IF PS-266-ERRORS                                                  
071858          DISPLAY '****** CON PROCESSING *******'                         
071859          DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                          
071860          DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                     
071861          PERFORM 9100-SQL-ABEND                                          
071862        END-IF                                                            
071863     END-IF.                                                              
071864                                                                          
071865     IF PS-BYPASS-CONTAINER                                               
071866       CONTINUE                                                           
071867     ELSE                                                                 
071868       PERFORM 2900-DECIDE-ADD-DEL-CHG                                    
071869     END-IF.                                                              
071870                                                                          
071871******************************************************************        
071872 2510-SELECT-DEVAN-CON.                                                   
071873                                                                          
071874     SET PS-TIMCNTK-EXISTS-NO     TO TRUE.                                
071875     INITIALIZE IMCNTK-DVN-TMST.                                          
071876                                                                          
071877     EXEC SQL                                                             
071878           SELECT  DVN_TMST                                               
071879             INTO :IMCNTK-DVN-TMST                                        
071880             FROM  TIMCNTK                                                
071881             WHERE CONTNR_ID = :TR265-CONTNR-ID                           
071882               AND MOM_POE_DELY_TMST                                      
071883                            = :IMCNTK-MOM-POE-DELY-TMST                   
071884     END-EXEC.                                                            
071885                                                                          
071886     EVALUATE TRUE                                                        
071887         WHEN SQLCODE = ZERO                                              
071888            SET PS-TIMCNTK-EXISTS     TO TRUE                             
071889                                                                          
071890         WHEN SQLCODE = +100                                              
071891         WHEN SQLCODE = -811                                              
071892            CONTINUE                                                      
071893                                                                          
071894         WHEN OTHER                                                       
071895             DISPLAY '** ABEND **'                                        
071896             DISPLAY '** PROGRAM = TRKUP265 **'                           
071897             DISPLAY '** PARAGRAPH = 2510-SELECT-DEVAN-CON **'            
071898             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
071899             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
071900             PERFORM 9100-SQL-ABEND                                       
071901     END-EVALUATE.                                                        
071902                                                                          
071903******************************************************************        
071904 2520-SELECT-LD-DNT-SHP-DT-CON.                                           
071905                                                                          
071906     SET PS-LANDED-POS-NO  TO TRUE.                                       
071907     INITIALIZE PV-NULL.                                                  
071908                                                                          
071909     EXEC SQL                                                             
071910           SELECT MAX(PL.DONT_SHIP_BEF_DTE - 2 DAYS)                      
071911             INTO :PLNSHP-DONT-SHIP-BEF-DTE:PV-NULL                       
071912             FROM TPURCHS   PO                                            
071913                , TSHPTPO   SHPO                                          
071914                , TPLNSHP   PL                                            
071915             WHERE SHPO.SHIPT_ID       = :TR265-CONTNR-SHIPT-ID           
071916               AND PO.PO_NBR           = SHPO.PO_NBR                      
071917               AND PO.VER_STATUS_CDE   = 'A'                              
071918               AND PO.PO_TYP_CDE       NOT IN ('IM','IE','IF' )           
071919               AND PL.PO_NBR           = PO.PO_NBR                        
071920               AND PL.VER_STATUS_CDE   = 'A'                              
071921     END-EXEC                                                             
071922                                                                          
071923     EVALUATE TRUE                                                        
071924         WHEN SQLCODE = ZERO                                              
071925           IF PV-NULL < +0                                                
071926            CONTINUE                                                      
071927           ELSE                                                           
071928             SET PS-LANDED-POS TO TRUE                                    
071929           END-IF                                                         
071930                                                                          
071931         WHEN SQLCODE = +100                                              
071932             CONTINUE                                                     
071933                                                                          
071934         WHEN SQLWARN0 NOT EQUAL SPACE                                    
071935         WHEN SQLCODE NOT EQUAL ZERO                                      
071936         DISPLAY '** ABEND **'                                            
071937         DISPLAY '** PROGRAM = TRKUP265 **'                               
071938         DISPLAY '** PARAGRAPH = 2520-SELECT-LD-DNT-SHP-DT-CON'           
071939         DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                           
071940         DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                      
071941         PERFORM 9100-SQL-ABEND                                           
071942     END-EVALUATE.                                                        
071943                                                                          
071944******************************************************************        
071945 2530-SELECT-COUNT-IMPORTS-CON.                                           
071946                                                                          
071947     INITIALIZE PV-IMPORTS-COUNT                                          
071948                                                                          
071949     EXEC SQL                                                             
071950           SELECT COUNT (*)                                               
071951             INTO :PV-IMPORTS-COUNT                                       
071952             FROM TPURCHS   PO                                            
071953                , TSHPTPO   SHPO                                          
071954             WHERE SHPO.SHIPT_ID       = :TR265-CONTNR-SHIPT-ID           
071955               AND PO.PO_NBR           = SHPO.PO_NBR                      
071956               AND PO.VER_STATUS_CDE   = 'A'                              
071957               AND PO.PO_TYP_CDE       IN ('IM','IE','IF' )               
071958     END-EXEC                                                             
071959                                                                          
071960     EVALUATE TRUE                                                        
071961         WHEN SQLCODE = ZERO                                              
071962         WHEN SQLCODE = +100                                              
071963             CONTINUE                                                     
071964                                                                          
071965         WHEN SQLWARN0 NOT EQUAL SPACE                                    
071966         WHEN SQLCODE NOT EQUAL ZERO                                      
071967          DISPLAY '** ABEND **'                                           
071968          DISPLAY '** PROGRAM = TRKUP265 **'                              
071969          DISPLAY '** PARAGRAPH = 2530-SELECT-COUNT-IMPORTS-CON'          
071970          DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                          
071971          DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                     
071972          PERFORM 9100-SQL-ABEND                                          
071973     END-EVALUATE.                                                        
071974                                                                          
071975******************************************************************        
071976 2900-DECIDE-ADD-DEL-CHG.                                                 
071977                                                                          
071978     MOVE 'DEF' TO TRT00014-RUL-TYP-CDE.                                  
071979     PERFORM 7020-SELECT-CONT-RULE.                                       
071980     SET PS-FOR-CONPO-ONLY-NO TO TRUE.                                    
071981     IF PS-DFLT-RULE-EXISTS                                               
071982       IF TR265-CONTNR-REC-SRC-CON                                        
071983         PERFORM 2905-SEE-IF-CONPO-ONLY                                   
071990       END-IF                                                             
072000     END-IF.                                                              
072028                                                                          
072029     EVALUATE TRUE                                                        
072030                                                                          
072043*    **CONPO CHANGE**                                                     
072044     WHEN TR265-CONTNR-TRN-TYP-CHG                                        
072045      AND TR265-CONTNR-PO-UPD-YES                                         
072048        IF PS-DFLT-RULE-EXISTS                                            
072049          PERFORM 3205-DELTE-CONPO-ONLY                                   
072050          PERFORM 3105-ADD-CONPO-ONLY                                     
072051        ELSE                                                              
072052          MOVE 'CHANGE CONPO - RULE DOES NOT EXIST - ERROR'               
072053                                   TO TR265-ERROR-TEXT                    
072054          SET TR265-REPORT-REC-TYP-ERR TO TRUE                            
072055          PERFORM 5100-WRITE-EXCEPTION-REC                                
072056        END-IF                                                            
072060                                                                          
072061*    **NORMAL CHANGE***                                                   
072062     WHEN TR265-CONTNR-TRN-TYP-CHG                                        
072075         PERFORM 3000-CHANGES                                             
072077                                                                          
072078*    **ADD CONPO ONLY**                                                   
072079     WHEN TR265-CONTNR-TRN-TYP-ADD                                        
072080      AND PS-FOR-CONPO-ONLY                                               
072081          PERFORM 3105-ADD-CONPO-ONLY                                     
072082                                                                          
072083*    **NORMAL ADDS***                                                     
072084     WHEN TR265-CONTNR-TRN-TYP-ADD                                        
072085       IF PS-DFLT-RULE-EXISTS                                             
072086          MOVE 'ADD - RULE EXISTS - NOT PROCESSED'                        
072087                                TO TR265-ERROR-TEXT                       
072088          SET TR265-REPORT-REC-TYP-ERR TO TRUE                            
072089          PERFORM 5100-WRITE-EXCEPTION-REC                                
072090          SET PS-BYPASS-CONTAINER   TO TRUE                               
072091       ELSE                                                               
072092          PERFORM 3100-ADDS                                               
072093       END-IF                                                             
072094                                                                          
072095*    **DELETE CONPO**                                                     
072096     WHEN TR265-CONTNR-TRN-TYP-DEL                                        
072097      AND PS-FOR-CONPO-ONLY                                               
072098        PERFORM 3205-DELTE-CONPO-ONLY                                     
072099                                                                          
072100*    **NORMAL DELETES ****                                                
072101     WHEN TR265-CONTNR-TRN-TYP-DEL                                        
072102       IF PS-DFLT-RULE-EXISTS                                             
072103         PERFORM 3200-DELETES                                             
072104       ELSE                                                               
072105         MOVE 'DELETE - RULE DOES NOT EXIT - NOT PROCESSED'               
072106                                     TO TR265-ERROR-TEXT                  
072107         SET TR265-REPORT-REC-TYP-ERR TO TRUE                             
072108         PERFORM 5100-WRITE-EXCEPTION-REC                                 
072109         SET PS-BYPASS-CONTAINER   TO TRUE                                
072110       END-IF                                                             
072111                                                                          
072112*    ***NONE OF THE ABOVE***                                              
072113     WHEN OTHER                                                           
072114       MOVE 'INVALID TRAN TYPE - NOT PROCESSED'                           
072115                            TO TR265-ERROR-TEXT                           
072116       SET TR265-REPORT-REC-TYP-ERR TO TRUE                               
072117       PERFORM 5100-WRITE-EXCEPTION-REC                                   
072118       SET PS-BYPASS-CONTAINER   TO TRUE                                  
072119                                                                          
072120     END-EVALUATE.                                                        
072121                                                                          
072122********************************************************                  
072123*  ADDS OR DELETES FOR PO OVERRIDES COULD                                 
072124*  BE FOR THE POS (CONPO CONSTRAINTS) ON                                  
072125*  THE RULE ONLY.  SET A SWITCH SO THE                                    
072126*  RULE IS NOT TOUCHED BUT THE CONPOS ARE                                 
072127*  ADDED OR DELETE.                                                       
072128********************************************************                  
072129                                                                          
072130 2905-SEE-IF-CONPO-ONLY.                                                  
072131                                                                          
072132     PERFORM 2910-SELECT-CNSTR-CONPO.                                     
072133*    ***********************************                                  
072134*    * CONPO ADDS ONLY:                *                                  
072135*    * IF THERE ARE NO CONPO CONSTRNTS *                                  
072136*    * ONLY ADD PO CONSTRAINTS.        *                                  
072137*    ***********************************                                  
072138     IF TR265-CONTNR-TRN-TYP-ADD                                          
072139       IF PV-CONPO-COUNT = ZERO                                           
072140         SET PS-FOR-CONPO-ONLY TO TRUE                                    
072141       END-IF                                                             
072142     END-IF.                                                              
072143*    ***********************************                                  
072144*    * CONPO DELETES ONLY:             *                                  
072145*    * IF THERE ARE CONPO CONSTRAINTS  *                                  
072146*    * AND ON TIMCNTK, ONLY DELETE THE *                                  
072147*    * CONPO CONSTRAINTS.              *                                  
072148*    ***********************************                                  
072149     IF TR265-CONTNR-TRN-TYP-DEL                                          
072150       IF PV-CONPO-COUNT > ZERO                                           
072151         IF PS-TIMCNTK-EXISTS                                             
072152           SET PS-FOR-CONPO-ONLY TO TRUE                                  
072153         END-IF                                                           
072154       END-IF                                                             
072155     END-IF.                                                              
072156                                                                          
072157******************************************************************        
072158 2910-SELECT-CNSTR-CONPO.                                                 
072159                                                                          
072160     INITIALIZE PV-CONPO-COUNT.                                           
072161                                                                          
072162     EXEC SQL                                                             
072163                                                                          
072164         SELECT COUNT(*)                                                  
072165           INTO :PV-CONPO-COUNT                                           
072166           FROM TRT_RUL_CNSTR                                             
072167          WHERE RUL_ID             = :PV-CURRENT-RULE-ID                  
072168            AND RUL_CNSTR_TYP_CDE  = 'CONPO'                              
072169                                                                          
072170     END-EXEC                                                             
072171                                                                          
072172     EVALUATE TRUE                                                        
072173         WHEN SQLCODE = ZERO                                              
072174         WHEN SQLCODE = +100                                              
072175             CONTINUE                                                     
072176                                                                          
072177         WHEN SQLWARN0 NOT EQUAL SPACE                                    
072178         WHEN SQLCODE NOT EQUAL ZERO                                      
072179             DISPLAY '** ABEND **'                                        
072180             DISPLAY '** PROGRAM = TRKUP265 **'                           
072181             DISPLAY '** PARAGRAPH = 2910-SELECT-CNSTR-CONPO'             
072182             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
072183             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
072184             PERFORM 9100-SQL-ABEND                                       
072185     END-EVALUATE.                                                        
072186                                                                          
072187******************************************************************        
072188 3000-CHANGES.                                                            
072189                                                                          
072190     SET PS-DATA-CHANGED-NO TO TRUE.                                      
072191                                                                          
072200     IF TR265-CONTNR-POE-DELY-TMST-PRV (1:10)                             
072300               NOT =   TR265-CONTNR-POE-DELY-TMST-CUR (1:10)              
072310      AND (TR265-CONTNR-POE-DELY-TMST-CUR (1:10)  NOT = SPACES            
072311              AND                                                         
072330           TR265-CONTNR-POE-DELY-TMST-CUR (1:10)                          
072340                                          NOT = LOW-VALUES)               
072400        SET PS-DATA-CHANGED TO TRUE                                       
072401                                                                          
072402        IF PS-LANDED-POS                                                  
072403          CONTINUE                                                        
072404        ELSE                                                              
072410*         *******************************************                     
072420*         * GET PV-266-BEGIN-DATE & PV-266-END-DATE *                     
072421*         * IF NEW ETA DATE.                        *                     
072430*         *******************************************                     
072435          MOVE TR265-CONTNR-POE-DELY-TMST-CUR (1:10)                      
072436                                 TO PV-266-DATE-ENTERED                   
072440          PERFORM D266-GET-BEGIN-END-DATES                                
072450          IF PS-266-ERRORS                                                
072460            DISPLAY '****** 3000-CHANGES *******'                         
072470            DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                        
072480            DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                   
072490            PERFORM 9100-SQL-ABEND                                        
072491          END-IF                                                          
072493        END-IF                                                            
072500     END-IF.                                                              
072510                                                                          
072600     IF TR265-CONTNR-POE-CDE-PREV                                         
072700                          NOT = TR265-CONTNR-POE-CDE-CUR                  
072800        SET PS-DATA-CHANGED TO TRUE                                       
072900     END-IF.                                                              
072910                                                                          
073000     IF TR265-CONTNR-VEH-NM-PREV                                          
073100                          NOT = TR265-CONTNR-VEH-NM-CUR                   
073200        SET PS-DATA-CHANGED TO TRUE                                       
073300     END-IF.                                                              
073310                                                                          
073400     IF TR265-CONTNR-TRIP-ID-PREV                                         
073500                          NOT = TR265-CONTNR-TRIP-ID-CUR                  
073600        SET PS-DATA-CHANGED TO TRUE                                       
073700     END-IF.                                                              
073710                                                                          
073800     IF TR265-CONTNR-FOB-CDE-PREV                                         
073900                          NOT = TR265-CONTNR-FOB-CDE-CUR                  
074000        SET PS-DATA-CHANGED TO TRUE                                       
074100     END-IF.                                                              
074200                                                                          
074300     IF PS-DATA-CHANGED                                                   
074310        IF PS-DFLT-RULE-EXISTS                                            
074400          PERFORM 3200-DELETES                                            
074401          PERFORM 3100-ADDS                                               
074410        ELSE                                                              
074421          MOVE 'CHANGE - RULE DOES NOT EXIST - NOT PROCESSED'             
074430                             TO TR265-ERROR-TEXT                          
074440          SET TR265-REPORT-REC-TYP-ERR TO TRUE                            
074450          PERFORM 5100-WRITE-EXCEPTION-REC                                
074451          SET PS-BYPASS-CONTAINER   TO TRUE                               
074460        END-IF                                                            
074600     ELSE                                                                 
074700       MOVE 'CHANGE - CUR DATA = PREV DATA - NOT PROCESSED'               
074800                             TO TR265-ERROR-TEXT                          
074900       SET TR265-REPORT-REC-TYP-ERR TO TRUE                               
075000       PERFORM 5100-WRITE-EXCEPTION-REC                                   
075100       SET PS-BYPASS-CONTAINER   TO TRUE                                  
075200     END-IF.                                                              
075300                                                                          
075400******************************************************************        
075410                                                                          
075500 3100-ADDS.                                                               
075600                                                                          
076600     MOVE 'CONT'                 TO TRT00014-RUL-CDE.                     
076700     MOVE 'DEF'                  TO TRT00014-RUL-TYP-CDE.                 
076800     MOVE PV-266-BEGIN-DATE      TO TRT00014-RUL-BEG-DTE.                 
076900     MOVE PV-266-END-DATE        TO TRT00014-RUL-END-DTE.                 
077000     PERFORM 7030-INSERT-TRTRUL.                                          
077100     PERFORM 7990-RETRIEVE-RULE-ID.                                       
077200     PERFORM 3110-ADD-RULE-CONSTRAINTS.                                   
077300                                                                          
077400*    *******************************************                          
077401*    * CMNS ONLY:                              *                          
077410*    * CONTAINER LOADS ARE SOMETIMES PAIRED AS *                          
077411*    * DELETE/ADD TRANSACTIONS.  RESTORE ANY   *                          
077412*    * DELETED WORKING OR ADJUSTED RULES.      *                          
077420*    *******************************************                          
077500     IF TR265-CONTNR-REC-SRC-CMN                                          
077600      AND PS-REUSED-CONTAINER-NO                                          
077800       MOVE 'WKG'  TO TRT00017-RUL-TYP-CDE                                
077900       PERFORM 7500-SELECT-AUDIT-RULE                                     
078000       IF PS-AUD-ROW-EXISTS                                               
078100         MOVE 'WKG'  TO TRT00014-RUL-TYP-CDE                              
078200         PERFORM 7030-INSERT-TRTRUL                                       
078300         PERFORM 7990-RETRIEVE-RULE-ID                                    
078400         PERFORM 3120-RESTORE-RULE-CONSTRAINTS                            
078410       MOVE 'ADJ'  TO TRT00017-RUL-TYP-CDE                                
078420       PERFORM 7500-SELECT-AUDIT-RULE                                     
078430       IF PS-AUD-ROW-EXISTS                                               
078440         MOVE 'ADJ'  TO TRT00014-RUL-TYP-CDE                              
078450         PERFORM 7030-INSERT-TRTRUL                                       
078460         PERFORM 7990-RETRIEVE-RULE-ID                                    
078470         PERFORM 3120-RESTORE-RULE-CONSTRAINTS                            
078500       END-IF                                                             
078501     END-IF.                                                              
079500                                                                          
079510******************************************************************        
079520 3105-ADD-CONPO-ONLY.                                                     
079530                                                                          
079591     MOVE PV-CURRENT-RULE-ID  TO PV-INSERTED-RULE-ID.                     
079592     PERFORM 7130-OPEN-PO-CURSOR.                                         
079594     PERFORM 3130-ADD-CONPO-CURSOR                                        
079595         UNTIL PS-EOC-PO-CUR.                                             
079596     PERFORM 7150-CLOSE-PO-CURSOR.                                        
079609                                                                          
079610******************************************************************        
079700 3110-ADD-RULE-CONSTRAINTS.                                               
079710                                                                          
079900** CONT                                                                   
080000     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
080100     MOVE TR265-CONTNR-ID      TO TRT00015-RUL-CNSTR-VAL-TXT              
080200                                  PV-CONT-USED.                           
080300     PERFORM 4000-ADD-CONT-CONSTR.                                        
080400                                                                          
080500** SEQ (ONLY CMNS)                                                        
080510     IF TR265-CONTNR-REC-SRC-CMN                                          
080600       INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT                              
080700       MOVE TR265-CONTNR-SEQ-NBR-CUR                                      
080800                                   TO PV-CNTNR-SEQ-NBR                    
080900       MOVE PV-CNTNR-SEQ-NBR-X  TO TRT00015-RUL-CNSTR-VAL-TXT             
081000                                   PV-SEQ-USED                            
081100       PERFORM 4010-ADD-SEQ                                               
081110     END-IF.                                                              
081200                                                                          
081300** ETA                                                                    
081400     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
081500     MOVE TR265-CONTNR-POE-DELY-TMST-CUR                                  
081600                               TO TRT00015-RUL-CNSTR-VAL-TXT              
081700                                  PV-ETA-USED.                            
081800     PERFORM 4020-ADD-ETA.                                                
081900                                                                          
082000** VESSEL                                                                 
082100     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
082200     MOVE TR265-CONTNR-VEH-NM-CUR TO TRT00015-RUL-CNSTR-VAL-TXT           
082300                                     PV-VES-USED.                         
082400     PERFORM 4030-ADD-VESSEL.                                             
082500                                                                          
082600** VOYAGE                                                                 
082700     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
082800     MOVE TR265-CONTNR-TRIP-ID-CUR TO TRT00015-RUL-CNSTR-VAL-TXT          
082900                                      PV-VOY-USED.                        
083000     PERFORM 4040-ADD-VOYAGE.                                             
083001                                                                          
083002**  CONPO (PO OVERRIDES ONLY - NOT CMN)                                   
083003     IF TR265-CONTNR-REC-SRC-CON                                          
083010       PERFORM 7130-OPEN-PO-CURSOR                                        
083020       PERFORM 3130-ADD-CONPO-CURSOR                                      
083030           UNTIL PS-EOC-PO-CUR                                            
083040       PERFORM 7150-CLOSE-PO-CURSOR                                       
083050     END-IF                                                               
083100                                                                          
083200**  FOB START                                                             
083300     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT                                
083310                        PV-DECON-SCAC-USED.                               
083400     SET PS-TPLNSHP-SELECTED-NO TO TRUE.                                  
083500*    ******** TRY TIMFOB FIRST ***************                            
083600     IF TR265-CONTNR-FOB-CDE-CUR = SPACES                                 
083700       MOVE 'FOB CODE NOT SENT'  TO TR265-ERROR-TEXT                      
083800       SET TR265-REPORT-REC-TYP-ERR TO TRUE                               
083900       PERFORM 5100-WRITE-EXCEPTION-REC                                   
084000     ELSE                                                                 
084100       MOVE TR265-CONTNR-FOB-CDE-CUR                                      
084200                          TO TRT00015-RUL-CNSTR-VAL-TXT                   
084300       PERFORM 7080-SELECT-TIMFOB                                         
084400       IF PS-SCAC-FROM-TIMFOB                                             
084600         MOVE EXTENT-SCAC-CDE TO PV-DECON-SCAC-USED                       
084700       ELSE                                                               
084800         MOVE 'FOB DECON ENTID NOT FOUND ON TEXTENT'                      
084900                                 TO TR265-ERROR-TEXT                      
085000         SET TR265-REPORT-REC-TYP-ERR TO TRUE                             
085100         PERFORM 5100-WRITE-EXCEPTION-REC                                 
085200     END-IF.                                                              
085300*    ******** IF NOT ON TIMFOB - TRY TPLNSHP  ***********                 
085400     IF TRT00015-RUL-CNSTR-VAL-TXT = SPACES                               
085410       IF TR265-CONTNR-REC-SRC-CON                                        
085411         PERFORM 7075-SELECT-TPLNSHP-CON                                  
085420       ELSE                                                               
085500         PERFORM 7070-SELECT-TPLNSHP-CMN                                  
085510       END-IF                                                             
085600       SET PS-TPLNSHP-SELECTED TO TRUE                                    
085700       IF PLNSHP-FOB-CDE = SPACES                                         
085710          MOVE 'FOB FROM PO UNAVAILABLE - DEFAULTED TO BLANKS'            
085900                                    TO TR265-ERROR-TEXT                   
086000          SET TR265-REPORT-REC-TYP-ERR TO TRUE                            
086100          PERFORM 5100-WRITE-EXCEPTION-REC                                
086200       ELSE                                                               
086300          MOVE PLNSHP-FOB-CDE TO TRT00015-RUL-CNSTR-VAL-TXT               
086400          MOVE 'USED FOB CODE FROM PO'                                    
086500                                    TO TR265-ERROR-TEXT                   
086600          SET TR265-REPORT-REC-TYP-ERR TO TRUE                            
086700          PERFORM 5100-WRITE-EXCEPTION-REC                                
086800       END-IF                                                             
086900     END-IF.                                                              
087000     MOVE TRT00015-RUL-CNSTR-VAL-TXT  TO PV-FOB-USED.                     
087100     PERFORM 4050-ADD-FOB.                                                
087200**  FOB END                                                               
087300                                                                          
087400**  POE START                                                             
087500     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
087600*    ******** TRY TIMPOE FIRST ***************                            
087700     IF TR265-CONTNR-POE-CDE-CUR = SPACES                                 
087800       MOVE 'POE CODE NOT SENT'  TO TR265-ERROR-TEXT                      
087900       SET TR265-REPORT-REC-TYP-ERR TO TRUE                               
088000       PERFORM 5100-WRITE-EXCEPTION-REC                                   
088100     ELSE                                                                 
088200       MOVE TR265-CONTNR-POE-CDE-CUR                                      
088300                      TO TRT00015-RUL-CNSTR-VAL-TXT                       
088400       PERFORM 7085-SELECT-POE-SCAC                                       
088500       IF PS-SCAC-FROM-TIMPOE                                             
088800         IF PV-DECON-SCAC-USED = SPACES                                   
088900           MOVE EXTENT-SCAC-CDE TO PV-DECON-SCAC-USED                     
089000         END-IF                                                           
089100       ELSE                                                               
089200         MOVE 'POE DECON ENTID NOT FOUND ON TEXTENT'                      
089300                                 TO TR265-ERROR-TEXT                      
089400         SET TR265-REPORT-REC-TYP-ERR TO TRUE                             
089500         PERFORM 5100-WRITE-EXCEPTION-REC                                 
089600     END-IF.                                                              
089700*    ******** IF NOT ON TIMPOE - TRY TPLNSHP  ***********                 
089800     IF TRT00015-RUL-CNSTR-VAL-TXT = SPACES                               
089900       IF PS-TPLNSHP-SELECTED                                             
090000         CONTINUE                                                         
090100       ELSE                                                               
090110          IF TR265-CONTNR-REC-SRC-CON                                     
090120            PERFORM 7075-SELECT-TPLNSHP-CON                               
090130          ELSE                                                            
090140            PERFORM 7070-SELECT-TPLNSHP-CMN                               
090150          END-IF                                                          
090300       END-IF                                                             
090400       IF PLNSHP-POE-CDE = SPACES                                         
090500          MOVE 'LA' TO TRT00015-RUL-CNSTR-VAL-TXT                         
090600          MOVE 'POE FROM PO UNAVAILABLE - USED LA AS POE DEFAULT'         
090700                                    TO TR265-ERROR-TEXT                   
090800          SET TR265-REPORT-REC-TYP-ERR TO TRUE                            
090900          PERFORM 5100-WRITE-EXCEPTION-REC                                
091000       ELSE                                                               
091100          MOVE PLNSHP-POE-CDE TO TRT00015-RUL-CNSTR-VAL-TXT               
091200          MOVE 'USED POE CODE FROM PO'                                    
091300                                    TO TR265-ERROR-TEXT                   
091400          SET TR265-REPORT-REC-TYP-ERR TO TRUE                            
091500          PERFORM 5100-WRITE-EXCEPTION-REC                                
091600       END-IF                                                             
091700     END-IF.                                                              
091800     MOVE TRT00015-RUL-CNSTR-VAL-TXT  TO PV-POE-USED.                     
091900     PERFORM 4060-ADD-POE.                                                
092000**  POE END                                                               
092100                                                                          
092200**  DECN START                                                            
092300*  *************************************************************          
092400*  * THESE ARE THE STEPS IN ORDER TO COME UP WITH DECON SCAC -            
092500*  * 1. ALREADY SELECTED WHEN FOB WAS FOUND IN FOB LOGIC ABOVE.           
092600*  * 2. ALREADY SELECTED WHEN POE WAS FOUND IN POE LOGIC ABOVE.           
092700*  * 3. USE FOB CODE FROM TPLNSHP.                                        
092800*  * 4. USE POE CODE FROM TPLNSHP.                                        
092900*  * 5. DEFAULT TO TRIANGLE LA.                                           
093000*  *************************************************************          
093100                                                                          
093200*    **** 1 & 2 - ALREADY SELECTED IN FOB OR POE LOGIC?                   
093300     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
093400     IF PV-DECON-SCAC-USED   NOT =   SPACES                               
093500       MOVE PV-DECON-SCAC-USED   TO TRT00015-RUL-CNSTR-VAL-TXT            
093600     ELSE                                                                 
093700       IF PS-TPLNSHP-SELECTED                                             
093800         CONTINUE                                                         
093900       ELSE                                                               
093910          IF TR265-CONTNR-REC-SRC-CON                                     
093920            PERFORM 7075-SELECT-TPLNSHP-CON                               
093930          ELSE                                                            
093940            PERFORM 7070-SELECT-TPLNSHP-CMN                               
093950          END-IF                                                          
094100       END-IF                                                             
094200*      ********************************************                       
094300*      * 3. USE FOB FROM TPLNSHP                  *                       
094400*      ********************************************                       
094500       IF PLNSHP-FOB-CDE   NOT =   SPACES                                 
094600         MOVE PV-FOB-USED          TO IMFOB-FOB-CDE                       
094700         PERFORM 7090-SELECT-TEXTENT-FOB                                  
094800         IF PS-DECON-ON-TIMFOB                                            
094900          MOVE EXTENT-SCAC-CDE   TO TRT00015-RUL-CNSTR-VAL-TXT            
095000          MOVE 'USED PLAN SHIP FOB TO GET DECON SCAC'                     
095100                                 TO TR265-ERROR-TEXT                      
095200          SET TR265-REPORT-REC-TYP-ERR TO TRUE                            
095300          PERFORM 5100-WRITE-EXCEPTION-REC                                
095400       ELSE                                                               
095500*        ********************************************                     
095600*        * 4. USE POE FROM TPLNSHP                  *                     
095700*        ********************************************                     
095800         MOVE PV-POE-USED          TO IMPOE-POE-CDE                       
095900         PERFORM 7100-SELECT-TEXTENT-POE                                  
096000         IF PS-DECON-ON-TIMPOE                                            
096100           MOVE EXTENT-SCAC-CDE   TO TRT00015-RUL-CNSTR-VAL-TXT           
096200           MOVE 'USED PLAN SHIP POE TO GET DECON SCAC'                    
096300                                 TO TR265-ERROR-TEXT                      
096400           SET TR265-REPORT-REC-TYP-ERR TO TRUE                           
096500           PERFORM 5100-WRITE-EXCEPTION-REC                               
096600       END-IF                                                             
096700     END-IF.                                                              
096800*    ************************************************                     
096900*    * 5. IF ALL ELSE FAILS DEFAULT TO TRIANGLE LA  *                     
097000*    ************************************************                     
097100     IF TRT00015-RUL-CNSTR-VAL-TXT = SPACES                               
097200        MOVE 'GLTR'  TO TRT00015-RUL-CNSTR-VAL-TXT                        
097300        MOVE 'USED TRIANGLE LA AS DECON DEFAULT'                          
097400                                   TO TR265-ERROR-TEXT                    
097500        SET TR265-REPORT-REC-TYP-ERR TO TRUE                              
097600        PERFORM 5100-WRITE-EXCEPTION-REC                                  
097700     END-IF.                                                              
097800     PERFORM 4070-ADD-DECN.                                               
097900                                                                          
098000******************************************************************        
098100 3120-RESTORE-RULE-CONSTRAINTS.                                           
098200                                                                          
098300     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
098400     MOVE PV-CONT-USED         TO TRT00015-RUL-CNSTR-VAL-TXT.             
098500     PERFORM 4000-ADD-CONT-CONSTR.                                        
098600     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
098700     MOVE PV-SEQ-USED          TO TRT00015-RUL-CNSTR-VAL-TXT.             
098800     PERFORM 4010-ADD-SEQ.                                                
098900     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
099000     MOVE PV-ETA-USED          TO TRT00015-RUL-CNSTR-VAL-TXT.             
099100     PERFORM 4020-ADD-ETA.                                                
099200     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
099300     MOVE PV-VES-USED          TO TRT00015-RUL-CNSTR-VAL-TXT.             
099400     PERFORM 4030-ADD-VESSEL.                                             
099500     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
099600     MOVE PV-VOY-USED          TO TRT00015-RUL-CNSTR-VAL-TXT.             
099700     PERFORM 4040-ADD-VOYAGE.                                             
099800     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
099900     MOVE PV-FOB-USED          TO TRT00015-RUL-CNSTR-VAL-TXT.             
100000     PERFORM 4050-ADD-FOB.                                                
100100     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
100200     MOVE PV-POE-USED          TO TRT00015-RUL-CNSTR-VAL-TXT.             
100300     PERFORM 4060-ADD-POE.                                                
100400     INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT.                               
100500     MOVE PV-AUDIT-DECON-SCAC  TO TRT00015-RUL-CNSTR-VAL-TXT.             
100600     PERFORM 4070-ADD-DECN.                                               
100700                                                                          
100701******************************************************************        
100710 3130-ADD-CONPO-CURSOR.                                                   
100711                                                                          
100712     PERFORM 7140-FETCH-PO-CURSOR.                                        
100720     IF PS-EOC-PO-CUR                                                     
100730       CONTINUE                                                           
100740     ELSE                                                                 
100741       INITIALIZE TRT00015-RUL-CNSTR-VAL-TXT                              
100742       MOVE SHPTPO-PO-NBR TO PV-PO-NBR                                    
100743       MOVE PV-PO-NBR-X   TO TRT00015-RUL-CNSTR-VAL-TXT                   
100744       PERFORM 4080-ADD-CONPO-CNSTR                                       
100750     END-IF.                                                              
100760                                                                          
100800******************************************************************        
100900 3200-DELETES.                                                            
102800                                                                          
103200*    ***** DELETE DEFAULT RULE                                            
103201     PERFORM 7510-INSERT-RULE-AUD.                                        
103202     PERFORM 7520-OPEN-CNSTR-CUR.                                         
103203     PERFORM 3210-PROCESS-CNSTR-CUR                                       
103204            UNTIL PS-EOC-CNSTR-CUR.                                       
103206     PERFORM 7540-CLOSE-CNSTR-CUR.                                        
103207     PERFORM 7120-DELETE-RULE.                                            
103208                                                                          
103210*    ***** CHECK FOR WORKING RULE                                         
103300     MOVE 'WKG' TO TRT00014-RUL-TYP-CDE.                                  
103400     PERFORM 7020-SELECT-CONT-RULE.                                       
103500     IF PS-DFLT-RULE-EXISTS                                               
103600       MOVE 'CONT'         TO TRT00014-RUL-CDE                            
103700       PERFORM 7510-INSERT-RULE-AUD                                       
103800       PERFORM 7520-OPEN-CNSTR-CUR                                        
103900       PERFORM 3210-PROCESS-CNSTR-CUR                                     
104000            UNTIL PS-EOC-CNSTR-CUR                                        
104200       PERFORM 7540-CLOSE-CNSTR-CUR                                       
104210       PERFORM 7120-DELETE-RULE                                           
104300     END-IF.                                                              
104310                                                                          
104400*    ***** CHECK FOR ADJUSTED RULE                                        
104500     MOVE 'ADJ' TO TRT00014-RUL-TYP-CDE.                                  
104600     PERFORM 7020-SELECT-CONT-RULE.                                       
104700     IF PS-DFLT-RULE-EXISTS                                               
104800       MOVE 'CONT'         TO TRT00014-RUL-CDE                            
104900       PERFORM 7510-INSERT-RULE-AUD                                       
105000       PERFORM 7520-OPEN-CNSTR-CUR                                        
105100       PERFORM 3210-PROCESS-CNSTR-CUR                                     
105200            UNTIL PS-EOC-CNSTR-CUR                                        
105400       PERFORM 7540-CLOSE-CNSTR-CUR                                       
105410       PERFORM 7120-DELETE-RULE                                           
105500     END-IF.                                                              
105700                                                                          
105800******************************************************************        
105801 3205-DELTE-CONPO-ONLY.                                                   
105810                                                                          
105811     PERFORM 7160-OPEN-CONPO-CNSTR-CUR.                                   
105812     PERFORM 3220-PROCESS-CONPO-CNSTR-CUR                                 
105813            UNTIL PS-EOC-CONPO-CNSTR-CUR.                                 
105815     PERFORM 7180-CLOSE-CONPO-CNSTR-CUR.                                  
105816                                                                          
105820******************************************************************        
105900 3210-PROCESS-CNSTR-CUR.                                                  
106000                                                                          
106100     PERFORM 7530-FETCH-CNSTR-CUR.                                        
106200     IF PS-EOC-CNSTR-CUR                                                  
106300        CONTINUE                                                          
106400     ELSE                                                                 
106500       PERFORM 7550-INSERT-CNSTR-AUD                                      
106600     END-IF.                                                              
106700                                                                          
106800******************************************************************        
106900 3220-PROCESS-CONPO-CNSTR-CUR.                                            
107000                                                                          
107100     PERFORM 7170-FETCH-CONPO-CNSTR-CUR.                                  
107200     IF PS-EOC-CONPO-CNSTR-CUR                                            
107300        CONTINUE                                                          
107400     ELSE                                                                 
107410       MOVE PV-PO-NBR-X   TO TRT00015-RUL-CNSTR-VAL-TXT                   
107500       PERFORM 7550-INSERT-CNSTR-AUD                                      
107510       PERFORM 7190-DELETE-CONPO-CNSTR                                    
107600     END-IF.                                                              
107700                                                                          
114200***************************************************************           
114300 4000-ADD-CONT-CONSTR.                                                    
114400                                                                          
114500     INITIALIZE TRT00015-RUL-CNSTR-TYP-CDE.                               
114600     MOVE PV-INSERTED-RULE-ID  TO TRT00015-RUL-ID.                        
114700     PERFORM 7040-SELECT-TIMCNTL.                                         
114800     MOVE IMCNTL-RUL-CNSTR-CDE TO TRT00015-RUL-CNSTR-CDE.                 
114900     MOVE 'CONT'               TO TRT00015-RUL-CNSTR-TYP-CDE.             
115000     PERFORM 7060-INSERT-CONSTRAINT.                                      
115100     PERFORM 7050-UPDATE-TIMCNTL.                                         
115200                                                                          
115300***************************************************************           
115400 4010-ADD-SEQ.                                                            
115500                                                                          
115600**   *** NOTE - RUL-CNSTR-CDE WILL BE SAME AS CONT                        
115700     INITIALIZE TRT00015-RUL-CNSTR-TYP-CDE.                               
115800     MOVE PV-INSERTED-RULE-ID   TO TRT00015-RUL-ID.                       
115900     MOVE IMCNTL-RUL-CNSTR-CDE  TO TRT00015-RUL-CNSTR-CDE.                
116000     MOVE 'SEQ'               TO TRT00015-RUL-CNSTR-TYP-CDE.              
116100     PERFORM 7060-INSERT-CONSTRAINT.                                      
116200                                                                          
116300***************************************************************           
116400 4020-ADD-ETA.                                                            
116500                                                                          
116600**   ** NOTE - RUL-CNSTR-CDE WILL BE SAME AS CONT                         
116700     INITIALIZE TRT00015-RUL-CNSTR-TYP-CDE.                               
116800     MOVE PV-INSERTED-RULE-ID  TO TRT00015-RUL-ID.                        
116900     MOVE IMCNTL-RUL-CNSTR-CDE TO TRT00015-RUL-CNSTR-CDE.                 
117000     MOVE 'ETA'                TO TRT00015-RUL-CNSTR-TYP-CDE.             
117100     PERFORM 7060-INSERT-CONSTRAINT.                                      
117200                                                                          
117300***************************************************************           
117400 4030-ADD-VESSEL.                                                         
117500                                                                          
117600     INITIALIZE TRT00015-RUL-CNSTR-TYP-CDE.                               
117700     MOVE PV-INSERTED-RULE-ID  TO TRT00015-RUL-ID.                        
117800     PERFORM 7040-SELECT-TIMCNTL.                                         
117900     MOVE IMCNTL-RUL-CNSTR-CDE TO TRT00015-RUL-CNSTR-CDE.                 
118000     MOVE 'VSL'                TO TRT00015-RUL-CNSTR-TYP-CDE.             
118100     PERFORM 7060-INSERT-CONSTRAINT.                                      
118200     PERFORM 7050-UPDATE-TIMCNTL.                                         
118300                                                                          
118400***************************************************************           
118500 4040-ADD-VOYAGE.                                                         
118600                                                                          
118700**  NOTE - RULE ID SAME AS VESSEL                                         
118800     INITIALIZE TRT00015-RUL-CNSTR-TYP-CDE.                               
118900     MOVE PV-INSERTED-RULE-ID  TO TRT00015-RUL-ID.                        
119000     MOVE IMCNTL-RUL-CNSTR-CDE TO TRT00015-RUL-CNSTR-CDE.                 
119100     MOVE 'VOY'                TO TRT00015-RUL-CNSTR-TYP-CDE.             
119200     PERFORM 7060-INSERT-CONSTRAINT.                                      
119300                                                                          
119400***************************************************************           
119500 4050-ADD-FOB.                                                            
119600                                                                          
119700     INITIALIZE TRT00015-RUL-CNSTR-TYP-CDE.                               
119900     MOVE PV-INSERTED-RULE-ID  TO TRT00015-RUL-ID.                        
120000     PERFORM 7040-SELECT-TIMCNTL.                                         
120100     MOVE IMCNTL-RUL-CNSTR-CDE TO TRT00015-RUL-CNSTR-CDE.                 
120200     MOVE 'FOB'                TO TRT00015-RUL-CNSTR-TYP-CDE.             
120300     PERFORM 7060-INSERT-CONSTRAINT.                                      
120400     PERFORM 7050-UPDATE-TIMCNTL.                                         
120500                                                                          
120600***************************************************************           
120700 4060-ADD-POE.                                                            
120800                                                                          
120900     INITIALIZE TRT00015-RUL-CNSTR-TYP-CDE.                               
121000     MOVE PV-INSERTED-RULE-ID  TO TRT00015-RUL-ID.                        
121100     PERFORM 7040-SELECT-TIMCNTL.                                         
121200     MOVE IMCNTL-RUL-CNSTR-CDE TO TRT00015-RUL-CNSTR-CDE.                 
121300     MOVE 'POE'                TO TRT00015-RUL-CNSTR-TYP-CDE.             
121400     PERFORM 7060-INSERT-CONSTRAINT.                                      
121500     PERFORM 7050-UPDATE-TIMCNTL.                                         
121600                                                                          
121700***************************************************************           
121800 4070-ADD-DECN.                                                           
121900                                                                          
122000** DECN (DECONSOLIDATOR SCAC)                                             
122100     INITIALIZE TRT00015-RUL-CNSTR-TYP-CDE.                               
122200     MOVE PV-INSERTED-RULE-ID  TO TRT00015-RUL-ID.                        
122300     PERFORM 7040-SELECT-TIMCNTL.                                         
122400     MOVE IMCNTL-RUL-CNSTR-CDE TO TRT00015-RUL-CNSTR-CDE.                 
122500     MOVE 'DECN'               TO TRT00015-RUL-CNSTR-TYP-CDE.             
122600     PERFORM 7060-INSERT-CONSTRAINT.                                      
122700     PERFORM 7050-UPDATE-TIMCNTL.                                         
122800                                                                          
122810***************************************************************           
122820 4080-ADD-CONPO-CNSTR.                                                    
122830                                                                          
122840** PO OVERIDE PURCHASE ORDER                                              
122850     INITIALIZE TRT00015-RUL-CNSTR-TYP-CDE.                               
122860     MOVE PV-INSERTED-RULE-ID  TO TRT00015-RUL-ID.                        
122870     PERFORM 7040-SELECT-TIMCNTL.                                         
122880     MOVE IMCNTL-RUL-CNSTR-CDE TO TRT00015-RUL-CNSTR-CDE.                 
122890     MOVE 'CONPO'              TO TRT00015-RUL-CNSTR-TYP-CDE.             
122891     PERFORM 7060-INSERT-CONSTRAINT.                                      
122892     PERFORM 7050-UPDATE-TIMCNTL.                                         
122893                                                                          
122900******************************************************************        
122901* NOT CURRENTLY USED - MIKE FRIESS 09/03/2004                    *        
122910******************************************************************        
123000 4900-PO-EDITS.                                                           
123100                                                                          
123200     SET PS-PO-SEL-INFO-PRESENT-NO TO TRUE.                               
123300     PERFORM 7000-SELECT-TPURCHS-INFO.                                    
123400                                                                          
123500     IF PS-PO-SEL-INFO-PRESENT                                            
123600       IF PURCHS-STATUS-CDE = 'AC' OR 'AP'                                
123700         CONTINUE                                                         
123800       ELSE                                                               
123900         DISPLAY PURCHS-PO-NBR                                            
124000                   ' - PO HAS INVALID STATUS - '                          
124100                   PURCHS-STATUS-CDE                                      
124200         MOVE 'PO HAS INVALID STATUS' TO TR265-ERROR-TEXT                 
124300         SET TR265-REPORT-REC-TYP-ERR TO TRUE                             
124400         PERFORM 5100-WRITE-EXCEPTION-REC                                 
124500       END-IF                                                             
124600     END-IF.                                                              
124700                                                                          
124800**************************************************************            
124900 5000-READ-RULES-FILE.                                                    
125000                                                                          
125100     READ RULES-INPUT-FILE   INTO TR265-CONTNR-RULES-REC                  
125200        AT END                                                            
125300         SET PS-EOF-RULES-IN TO TRUE.                                     
125400                                                                          
125500     IF PS-EOF-RULES-IN                                                   
125600       CONTINUE                                                           
125700     ELSE                                                                 
125800       ADD 1 TO PA-TOTAL-INPUT-REC-COUNT                                  
125900     END-IF.                                                              
126000                                                                          
126100********                      ************************************        
126200 5100-WRITE-EXCEPTION-REC.                                                
126300                                                                          
126400     WRITE RULES-EXCEP-RECORD                                             
126500        FROM TR265-CONTNR-RULES-REC.                                      
126600                                                                          
126710******************************************************************        
126720* NOT CURRENTLY USED - MIKE FRIESS 09/03/2004                    *        
126730******************************************************************        
126800 7000-SELECT-TPURCHS-INFO.                                                
126900*                                                                         
127000     EXEC SQL                                                             
127100           SELECT DISTINCT                                                
127200                  PO.STATUS_CDE                                           
127300                , PO.PO_NBR                                               
127400                , PO.PO_TYP_CDE                                           
127500             INTO                                                         
127600                  :PURCHS-STATUS-CDE                                      
127700                , :PURCHS-PO-NBR                                          
127800                , :PURCHS-PO-TYP-CDE                                      
127900             FROM                                                         
128000                  TPURCHS   PO                                            
128100                , TSHPTPO   SP                                            
128200             WHERE PO.PO_NBR     =  :SHPTPO-PO-NBR                        
128300               AND SP.PO_NBR     =  PO.PO_NBR                             
128400               AND PO.VER_STATUS_CDE = 'A'                                
128500     END-EXEC                                                             
128600                                                                          
128700     EVALUATE TRUE                                                        
128800         WHEN SQLCODE = ZERO                                              
128900             SET PS-PO-SEL-INFO-PRESENT TO TRUE                           
129000                                                                          
129100         WHEN SQLCODE = +100                                              
129200             DISPLAY 'PO-NBR = ' SHPTPO-PO-NBR                            
129300             DISPLAY 'NOT ON PO DATABASE'                                 
129400                                                                          
129500         WHEN SQLWARN0 NOT EQUAL SPACE                                    
129600         WHEN SQLCODE NOT EQUAL ZERO                                      
129700             DISPLAY '** ABEND **'                                        
129800             DISPLAY '** PROGRAM = TRKUP265 **'                           
129900             DISPLAY '** PARAGRAPH = 7000-SELECT-TPURCHS-INFO***'         
130000             DISPLAY 'PO-NBR = ' SHPTPO-PO-NBR                            
130100             PERFORM 9100-SQL-ABEND                                       
130200     END-EVALUATE.                                                        
130300                                                                          
130400******************************************************************        
130500 7020-SELECT-CONT-RULE.                                                   
130600                                                                          
130610     DISPLAY '7020-SELECT-CONT-RULE'.                                     
130620     DISPLAY '   TR265-CONTNR-ID      - ' TR265-CONTNR-ID.                
130630     DISPLAY '   PV-TYP-SEQ-OR-ETA    - ' PV-TYP-SEQ-OR-ETA.              
130631     DISPLAY '   PV-SEQ-OR-ETA        - ' PV-SEQ-OR-ETA.                  
130640     DISPLAY '   TRT00014-RUL-TYP-CDE - ' TRT00014-RUL-TYP-CDE.           
130650     DISPLAY '   PV-266-BEGIN-DATE    - ' PV-266-BEGIN-DATE.              
130660     DISPLAY '   PV-266-END-DATE      - ' PV-266-END-DATE.                
130700     SET PS-DFLT-RULE-EXISTS-NO    TO TRUE.                               
130800                                                                          
130900     EXEC SQL                                                             
131000                                                                          
131100         SELECT RT.RUL_ID                                                 
131200               ,RT.RUL_CDE                                                
131300               ,RT.RUL_TYP_CDE                                            
131400               ,RT.RUL_BEG_DTE                                            
131500               ,RT.RUL_END_DTE                                            
131600               ,RT.CRE_TMST                                               
131700               ,RT.CRE_BY_USR_ID                                          
131800          INTO :PV-CURRENT-RULE-ID                                        
131900              ,:TRT00014-RUL-CDE                                          
132000              ,:TRT00014-RUL-TYP-CDE                                      
132100              ,:TRT00014-RUL-BEG-DTE                                      
132200              ,:TRT00014-RUL-END-DTE                                      
132300              ,:TRT00014-CRE-TMST                                         
132400              ,:TRT00014-CRE-BY-USR-ID                                    
132500          FROM TRT_RUL_CNSTR CT                                           
132600              ,TRT_RUL RT                                                 
132700         WHERE CT.RUL_CNSTR_TYP_CDE = 'CONT'                              
132800           AND CT.RUL_CNSTR_VAL_TXT = :TR265-CONTNR-ID                    
132900           AND RT.RUL_ID            =  CT.RUL_ID                          
133000           AND RT.RUL_BEG_DTE       = :PV-266-BEGIN-DATE                  
133100           AND RT.RUL_END_DTE       = :PV-266-END-DATE                    
133200           AND RT.RUL_CDE           = 'CONT'                              
133300           AND RT.RUL_TYP_CDE       = :TRT00014-RUL-TYP-CDE               
133400           AND :PV-SEQ-OR-ETA =                                           
133500                (SELECT SUBSTR(SUBA.RUL_CNSTR_VAL_TXT,1,10)               
133600                   FROM TRT_RUL_CNSTR SUBA                                
133700                  WHERE SUBA.RUL_ID = RT.RUL_ID                           
133800                    AND SUBA.RUL_CNSTR_TYP_CDE =                          
133900                                          :PV-TYP-SEQ-OR-ETA)             
134000                                                                          
134100     END-EXEC                                                             
134200                                                                          
134300     EVALUATE TRUE                                                        
134400         WHEN SQLCODE = +100                                              
134500             CONTINUE                                                     
134600                                                                          
134700         WHEN SQLCODE = ZERO                                              
134800         WHEN SQLCODE = -811                                              
134900             SET PS-DFLT-RULE-EXISTS    TO TRUE                           
135000                                                                          
135100         WHEN SQLWARN0 NOT EQUAL SPACE                                    
135200         WHEN SQLCODE NOT EQUAL ZERO                                      
135300             DISPLAY '** ABEND **'                                        
135400             DISPLAY '** PROGRAM = TRKUP265 **'                           
135500             DISPLAY '** PARAGRAPH = 7020-SELECT-CONT-RULE'               
135600             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
135700             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
135800             PERFORM 9100-SQL-ABEND                                       
135900     END-EVALUATE.                                                        
135910     DISPLAY '      SQLCODE - '  SQLCODE.                                 
136000                                                                          
136100******************************************************************        
136200 7030-INSERT-TRTRUL.                                                      
136400     EXEC SQL                                                             
136500                                                                          
136600         INSERT INTO TRT_RUL                                              
136700            (RUL_CDE                                                      
136800            ,RUL_TYP_CDE                                                  
136900            ,RUL_BEG_DTE                                                  
137000            ,RUL_END_DTE                                                  
137100            ,CRE_TMST                                                     
137200            ,CRE_BY_USR_ID)                                               
137300          VALUES                                                          
137400           (:TRT00014-RUL-CDE                                             
137500           ,:TRT00014-RUL-TYP-CDE                                         
137600           ,:TRT00014-RUL-BEG-DTE                                         
137700           ,:TRT00014-RUL-END-DTE                                         
137800           , CURRENT_TIMESTAMP                                            
137900           ,:PC-CURRENT-PROGRAM-NAME)                                     
138000                                                                          
138100     END-EXEC                                                             
138200                                                                          
138300     EVALUATE TRUE                                                        
138400         WHEN SQLCODE = ZERO                                              
138500             ADD 1 TO PA-TOTAL-LOADED-RUL-COUNT                           
138510             DISPLAY ' LOADED RULE FOR RECORD # '                         
138511                     PA-TOTAL-INPUT-REC-COUNT-X                           
138520                     '  CONTAINER ID ' TR265-CONTNR-ID                    
138600                                                                          
138700         WHEN SQLWARN0 NOT EQUAL SPACE                                    
138800         WHEN SQLCODE NOT EQUAL ZERO                                      
138900             DISPLAY '** ABEND **'                                        
139000             DISPLAY '** PROGRAM = TRKUP265 **'                           
139100             DISPLAY '** PARAGRAPH = 7030-INSERT-TRTRUL'                  
139200             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
139300             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
139400             PERFORM 9100-SQL-ABEND                                       
139500     END-EVALUATE.                                                        
139600                                                                          
139700******************************************************************        
139800 7040-SELECT-TIMCNTL.                                                     
139900                                                                          
140000     EXEC SQL                                                             
140100                                                                          
140200         SELECT RUL_CNSTR_CDE                                             
140300           INTO :IMCNTL-RUL-CNSTR-CDE                                     
140400           FROM TIMCNTL                                                   
140500                                                                          
140600     END-EXEC                                                             
140700                                                                          
140800     EVALUATE TRUE                                                        
140900         WHEN SQLCODE = ZERO                                              
141000             CONTINUE                                                     
141100                                                                          
141200         WHEN SQLWARN0 NOT EQUAL SPACE                                    
141300         WHEN SQLCODE NOT EQUAL ZERO                                      
141400             DISPLAY '** ABEND **'                                        
141500             DISPLAY '** PROGRAM = TRKUP265 **'                           
141600             DISPLAY '** PARAGRAPH = 7040-SELECT-TIMCNTL'                 
141700             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
141800             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
141900             PERFORM 9100-SQL-ABEND                                       
142000     END-EVALUATE.                                                        
142100                                                                          
142200******************************************************************        
142300 7050-UPDATE-TIMCNTL.                                                     
142400                                                                          
142500     COMPUTE PV-NEXT-RUL-CNSTR-CDE =                                      
142600               IMCNTL-RUL-CNSTR-CDE + 1.                                  
142700                                                                          
142800     EXEC SQL                                                             
142900                                                                          
143000         UPDATE TIMCNTL                                                   
143100            SET RUL_CNSTR_CDE = :PV-NEXT-RUL-CNSTR-CDE                    
143200                                                                          
143300     END-EXEC                                                             
143400                                                                          
143500     EVALUATE TRUE                                                        
143600         WHEN SQLCODE = ZERO                                              
143700             CONTINUE                                                     
143800                                                                          
143900         WHEN SQLWARN0 NOT EQUAL SPACE                                    
144000         WHEN SQLCODE NOT EQUAL ZERO                                      
144100             DISPLAY '** ABEND **'                                        
144200             DISPLAY '** PROGRAM = TRKUP265 **'                           
144300             DISPLAY '** PARAGRAPH = 7050-UPDATE-TIMCNTL'                 
144400             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
144500             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
144600             PERFORM 9100-SQL-ABEND                                       
144700     END-EVALUATE.                                                        
144800                                                                          
144900******************************************************************        
145000 7060-INSERT-CONSTRAINT.                                                  
145100                                                                          
145200     EXEC SQL                                                             
145300                                                                          
145400         INSERT INTO TRT_RUL_CNSTR                                        
145500             (RUL_ID                                                      
145600             ,RUL_CNSTR_CDE                                               
145700             ,RUL_CNSTR_TYP_CDE                                           
145800             ,RUL_CNSTR_VAL_TXT                                           
145900             ,CRE_TMST                                                    
146000             ,CRE_BY_USR_ID)                                              
146100          VALUES                                                          
146200           (:TRT00015-RUL-ID                                              
146300           ,:TRT00015-RUL-CNSTR-CDE                                       
146400           ,:TRT00015-RUL-CNSTR-TYP-CDE                                   
146500           ,:TRT00015-RUL-CNSTR-VAL-TXT                                   
146600           ,CURRENT_TIMESTAMP                                             
146700           ,:PC-CURRENT-PROGRAM-NAME)                                     
146800                                                                          
146900     END-EXEC                                                             
147000                                                                          
147100     EVALUATE TRUE                                                        
147200         WHEN SQLCODE = ZERO                                              
147300             CONTINUE                                                     
147400                                                                          
147500         WHEN SQLWARN0 NOT EQUAL SPACE                                    
147600         WHEN SQLCODE NOT EQUAL ZERO                                      
147700             DISPLAY '** ABEND **'                                        
147800             DISPLAY '** PROGRAM = TRKUP265 **'                           
147900             DISPLAY '** PARAGRAPH = 7060-INSERT-CONSTRAINT'              
148000             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
148100             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
148200             PERFORM 9100-SQL-ABEND                                       
148300     END-EVALUATE.                                                        
148400                                                                          
148500******************************************************************        
148600 7070-SELECT-TPLNSHP-CMN.                                                 
148700                                                                          
148800     EXEC SQL                                                             
148900           SELECT  DISTINCT                                               
148910                   PL.FOB_CDE                                             
149000                  ,PL.POE_CDE                                             
149100                  ,MIN(CNPO.PO_NBR)                                       
149200             INTO :PLNSHP-FOB-CDE                                         
149300                 ,:PLNSHP-POE-CDE                                         
149400                 ,:IMCNPO-PO-NBR                                          
149500             FROM TPURCHS   PO                                            
149600                , TIMCNPO   CNPO                                          
149700                , TPLNSHP   PL                                            
149800             WHERE CNPO.CONTNR_ID      = :TR265-CONTNR-ID                 
149900               AND CNPO.CONTNR_SEQ_NBR = :IMCNTK-CONTNR-SEQ-NBR           
150000               AND PO.PO_NBR           = CNPO.PO_NBR                      
150100               AND PO.VER_STATUS_CDE   = 'A'                              
150200               AND PL.PO_NBR           = PO.PO_NBR                        
150300               AND PL.VER_STATUS_CDE   = 'A'                              
150400         GROUP BY PL.FOB_CDE                                              
150500                 ,PL.POE_CDE                                              
150600     END-EXEC                                                             
150700                                                                          
150800     EVALUATE TRUE                                                        
150900         WHEN SQLCODE = ZERO                                              
151000             CONTINUE                                                     
151100                                                                          
151200         WHEN SQLCODE = +100                                              
151201             MOVE SPACES TO PLNSHP-FOB-CDE                                
151202                            PLNSHP-POE-CDE                                
151203                                                                          
151210         WHEN SQLCODE = -811                                              
151220             DISPLAY '*************************************'              
151230             DISPLAY '* THIS SHIPMENT HAS PURCHASE ORDERS *'              
151240             DISPLAY '* WITH DIFFERENT POE OR FOB.        *'              
151250             DISPLAY '*************************************'              
151260             DISPLAY 'PARAGRAPH = 7070-SELECT-TPLNSHP-CMN'                
151270             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
151280             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
151290             DISPLAY 'SHIPMT ID = ' TR265-CONTNR-SHIPT-ID                 
151291             DISPLAY '*************************************'              
151300             MOVE SPACES TO PLNSHP-FOB-CDE                                
151400                            PLNSHP-POE-CDE                                
151500                                                                          
151600         WHEN SQLWARN0 NOT EQUAL SPACE                                    
151700         WHEN SQLCODE NOT EQUAL ZERO                                      
151800         DISPLAY '** ABEND **'                                            
151900         DISPLAY '** PROGRAM = TRKUP265 **'                               
152000         DISPLAY '** PARAGRAPH = 7070-SELECT-TPLNSHP-CMN'                 
152100         DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                           
152200         DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                      
152300         PERFORM 9100-SQL-ABEND                                           
152400     END-EVALUATE.                                                        
152500                                                                          
152510******************************************************************        
152520 7075-SELECT-TPLNSHP-CON.                                                 
152530                                                                          
152540     EXEC SQL                                                             
152550           SELECT  DISTINCT                                               
152552                   PL.FOB_CDE                                             
152560                  ,PL.POE_CDE                                             
152570                  ,MIN(SHPO.PO_NBR)                                       
152580             INTO :PLNSHP-FOB-CDE                                         
152590                 ,:PLNSHP-POE-CDE                                         
152591                 ,:SHPTPO-PO-NBR                                          
152592             FROM TPURCHS   PO                                            
152593                , TSHPTPO   SHPO                                          
152594                , TPLNSHP   PL                                            
152595             WHERE SHPO.SHIPT_ID       = :TR265-CONTNR-SHIPT-ID           
152597               AND PO.PO_NBR           = SHPO.PO_NBR                      
152598               AND PO.VER_STATUS_CDE   = 'A'                              
152599               AND PL.PO_NBR           = PO.PO_NBR                        
152600               AND PL.VER_STATUS_CDE   = 'A'                              
152601         GROUP BY PL.FOB_CDE                                              
152602                 ,PL.POE_CDE                                              
152603     END-EXEC                                                             
152604                                                                          
152605     EVALUATE TRUE                                                        
152606         WHEN SQLCODE = ZERO                                              
152607             CONTINUE                                                     
152608                                                                          
152609         WHEN SQLCODE = +100                                              
152610             MOVE SPACES TO PLNSHP-FOB-CDE                                
152611                            PLNSHP-POE-CDE                                
152612                                                                          
152613         WHEN SQLCODE = -811                                              
152614             DISPLAY '*************************************'              
152615             DISPLAY '* THIS SHIPMENT HAS PURCHASE ORDERS *'              
152616             DISPLAY '* WITH DIFFERENT POE OR FOB.        *'              
152617             DISPLAY '*************************************'              
152618             DISPLAY 'PARAGRAPH = 7075-SELECT-TPLNSHP-CON'                
152619             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
152620             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
152621             DISPLAY 'SHIPMT ID = ' TR265-CONTNR-SHIPT-ID                 
152622             DISPLAY '*************************************'              
152623             MOVE SPACES TO PLNSHP-FOB-CDE                                
152624                            PLNSHP-POE-CDE                                
152625                                                                          
152626         WHEN SQLWARN0 NOT EQUAL SPACE                                    
152627         WHEN SQLCODE NOT EQUAL ZERO                                      
152628         DISPLAY '** ABEND **'                                            
152629         DISPLAY '** PROGRAM = TRKUP265 **'                               
152630         DISPLAY '** PARAGRAPH = 7075-SELECT-TPLNSHP-CON **'              
152631         DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                           
152632         DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                      
152633         PERFORM 9100-SQL-ABEND                                           
152634     END-EVALUATE.                                                        
152635                                                                          
152640******************************************************************        
152700 7080-SELECT-TIMFOB.                                                      
152800                                                                          
152900     SET PS-SCAC-FROM-TIMFOB-NO TO TRUE.                                  
153000     INITIALIZE PV-NULL.                                                  
153100                                                                          
153200     EXEC SQL                                                             
153300           SELECT DISTINCT                                                
153400                  TXT.SCAC_CDE                                            
153600             INTO :EXTENT-SCAC-CDE:PV-NULL                                
153700             FROM TIMFOB  FOB                                             
153710                 ,TEXTENT TXT                                             
154200             WHERE FOB.FOB_CDE =                                          
154210                            :TR265-CONTNR-FOB-CDE-CUR                     
154300               AND FOB.STAT_CDE = 'A'                                     
154310               AND FOB.DCONSLR_ENT_ID = TXT.ENT_ID                        
154320               AND TXT.STATUS_CDE     = 'A'                               
154400     END-EXEC                                                             
154500                                                                          
154600     EVALUATE TRUE                                                        
154700         WHEN SQLCODE = ZERO                                              
155100             SET PS-SCAC-FROM-TIMFOB TO TRUE                              
155300                                                                          
155400         WHEN SQLCODE = +100                                              
155410         WHEN SQLCODE = -811                                              
155500             CONTINUE                                                     
155600                                                                          
155700         WHEN SQLWARN0 NOT EQUAL SPACE                                    
155800         WHEN SQLCODE NOT EQUAL ZERO                                      
155900         DISPLAY '** ABEND **'                                            
156000         DISPLAY '** PROGRAM = TRKUP265 **'                               
156100         DISPLAY '** PARAGRAPH = 7080-SELECT-TIMFOB'                      
156200         DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                           
156300         DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                      
156400         PERFORM 9100-SQL-ABEND                                           
156500     END-EVALUATE.                                                        
156600                                                                          
156700******************************************************************        
156800 7085-SELECT-POE-SCAC.                                                    
156900                                                                          
157000     SET PS-SCAC-FROM-TIMPOE-NO TO TRUE.                                  
157100     INITIALIZE PV-NULL.                                                  
157200                                                                          
157300     EXEC SQL                                                             
157400        SELECT DISTINCT                                                   
157500                  TXT.SCAC_CDE                                            
157700             INTO :EXTENT-SCAC-CDE:PV-NULL                                
157800             FROM TIMPOE POE                                              
158000                 ,TEXTENT TXT                                             
158310             WHERE POE.POE_CDE  = :TR265-CONTNR-POE-CDE-CUR               
158400               AND POE.STAT_CDE = 'A'                                     
158410               AND POE.DCONSLR_ENT_ID = TXT.ENT_ID                        
158420               AND TXT.STATUS_CDE     = 'A'                               
158500     END-EXEC                                                             
158600                                                                          
158700     EVALUATE TRUE                                                        
158800         WHEN SQLCODE = +0                                                
159200             SET PS-SCAC-FROM-TIMPOE TO TRUE                              
159400                                                                          
159500         WHEN SQLCODE = +100                                              
159510         WHEN SQLCODE = -811                                              
159600             CONTINUE                                                     
159700                                                                          
159800         WHEN SQLWARN0 NOT EQUAL SPACE                                    
159900         WHEN SQLCODE NOT EQUAL ZERO                                      
160000         DISPLAY '** ABEND **'                                            
160100         DISPLAY '** PROGRAM = TRKUP265 **'                               
160200         DISPLAY '** PARAGRAPH = 7085-SELECT-POE-SCAC'                    
160300         DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                           
160400         DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                      
160500         PERFORM 9100-SQL-ABEND                                           
160600     END-EVALUATE.                                                        
160700                                                                          
160800******************************************************************        
160900 7090-SELECT-TEXTENT-FOB.                                                 
161000                                                                          
161100     SET PS-DECON-ON-TIMFOB-NO          TO TRUE.                          
161200     INITIALIZE PV-NULL.                                                  
161300                                                                          
161400     EXEC SQL                                                             
161500                                                                          
161600           SELECT TXT.SCAC_CDE                                            
161700             INTO :EXTENT-SCAC-CDE:PV-NULL                                
161800             FROM TIMFOB  FOB                                             
161900                 ,TEXTENT TXT                                             
162000            WHERE FOB.FOB_CDE        = :PV-FOB-USED                       
162100              AND FOB.STAT_CDE       = 'A'                                
162200              AND TXT.ENT_ID         = FOB.DCONSLR_ENT_ID                 
162300              AND TXT.STATUS_CDE     = 'A'                                
162400                                                                          
162500     END-EXEC                                                             
162600                                                                          
162700     EVALUATE TRUE                                                        
162800         WHEN SQLCODE = ZERO                                              
162900           IF PV-NULL < +0                                                
163000            CONTINUE                                                      
163100           ELSE                                                           
163200             SET PS-DECON-ON-TIMFOB  TO TRUE                              
163300           END-IF                                                         
163400                                                                          
163500         WHEN SQLCODE = +100                                              
163600             CONTINUE                                                     
163700                                                                          
163800         WHEN SQLWARN0 NOT EQUAL SPACE                                    
163900         WHEN SQLCODE NOT EQUAL ZERO                                      
164000         DISPLAY '** ABEND **'                                            
164100         DISPLAY '** PROGRAM = TRKUP265 **'                               
164200         DISPLAY '** PARAGRAPH = 7090-SELECT-TEXTENT-FOB'                 
164300         DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                           
164400         DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                      
164500         PERFORM 9100-SQL-ABEND                                           
164600     END-EVALUATE.                                                        
164700                                                                          
164800******************************************************************        
164900 7100-SELECT-TEXTENT-POE.                                                 
165000                                                                          
165100     SET PS-DECON-ON-TIMPOE-NO          TO TRUE.                          
165200                                                                          
165300     EXEC SQL                                                             
165400           SELECT TXT.SCAC_CDE                                            
165500             INTO :EXTENT-SCAC-CDE:PV-NULL                                
165600             FROM TIMPOE  POE                                             
165700                 ,TEXTENT TXT                                             
165800            WHERE POE.POE_CDE        = :PV-POE-USED                       
165900              AND POE.STAT_CDE       = 'A'                                
166000              AND TXT.ENT_ID         = POE.DCONSLR_ENT_ID                 
166100              AND TXT.STATUS_CDE     = 'A'                                
166200                                                                          
166300     END-EXEC                                                             
166400                                                                          
166500     EVALUATE TRUE                                                        
166600         WHEN SQLCODE = ZERO                                              
166700           IF PV-NULL < +0                                                
166800            CONTINUE                                                      
166900           ELSE                                                           
167000             SET PS-DECON-ON-TIMPOE  TO TRUE                              
167100           END-IF                                                         
167200                                                                          
167300         WHEN SQLCODE = +100                                              
167400             CONTINUE                                                     
167500                                                                          
167600         WHEN SQLWARN0 NOT EQUAL SPACE                                    
167700         WHEN SQLCODE NOT EQUAL ZERO                                      
167800         DISPLAY '** ABEND **'                                            
167900         DISPLAY '** PROGRAM = TRKUP265 **'                               
168000         DISPLAY '** PARAGRAPH = 7100-SELECT-TEXTENT-POE'                 
168100         DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                           
168200         DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                      
168300         PERFORM 9100-SQL-ABEND                                           
168400     END-EVALUATE.                                                        
168500                                                                          
168600******************************************************************        
168700 7110-SELECT-PO-CONSTAINTS.                                               
168800                                                                          
168900     SET PS-PO-CONSTRAINTS-NO    TO TRUE.                                 
169000                                                                          
169100     EXEC SQL                                                             
169200        SELECT RC.RUL_ID                                                  
169300          INTO :TRT00015-RUL-ID                                           
169400          FROM TRT_RUL_CNSTR RC                                           
169500         WHERE RC.RUL_CNSTR_TYP_CDE = 'CONT'                              
169600           AND RC.RUL_CNSTR_VAL_TXT = :TR265-CONTNR-ID                    
169700           AND EXISTS                                                     
169800            (SELECT *                                                     
169900               FROM TRT_RUL_CNSTR RC2                                     
170000              WHERE RC2.RUL_ID = RC.RUL_ID                                
170100                AND RC2.RUL_CNSTR_TYP_CDE = 'CONPO')                      
170110           AND :PV-266-DATE-ENTERED =                                     
170120                (SELECT SUBSTR(RC3.RUL_CNSTR_VAL_TXT,1,10)                
170130                   FROM TRT_RUL_CNSTR RC3                                 
170140                  WHERE RC3.RUL_ID = RC.RUL_ID                            
170150                    AND RC3.RUL_CNSTR_TYP_CDE = 'ETA')                    
170200     END-EXEC.                                                            
170300                                                                          
170400     EVALUATE TRUE                                                        
170500         WHEN SQLCODE = ZERO                                              
170600         WHEN SQLCODE = -811                                              
170700            SET PS-PO-CONSTRAINTS TO TRUE                                 
170800                                                                          
170900         WHEN SQLCODE = +100                                              
171000            CONTINUE                                                      
171100                                                                          
171200         WHEN OTHER                                                       
171300             DISPLAY '** ABEND **'                                        
171400             DISPLAY '** PROGRAM = TRKUP265 **'                           
171500             DISPLAY '** PARAGRAPH = 7110-SELECT-PO-CONSTAINTS **'        
171600             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
171700             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
171800             PERFORM 9100-SQL-ABEND                                       
171900     END-EVALUATE.                                                        
172000                                                                          
172100******************************************************************        
172200 7120-DELETE-RULE.                                                        
172300                                                                          
172400     EXEC SQL                                                             
172500        DELETE FROM TRT_RUL                                               
172600         WHERE RUL_ID = :PV-CURRENT-RULE-ID                               
172700     END-EXEC.                                                            
172800                                                                          
172900     EVALUATE TRUE                                                        
173000         WHEN SQLCODE = ZERO                                              
173100            CONTINUE                                                      
173200                                                                          
173300         WHEN OTHER                                                       
173400             DISPLAY '** ABEND **'                                        
173500             DISPLAY '** PROGRAM = TRKUP265 **'                           
173600             DISPLAY '** PARAGRAPH = 7120-DELETE-RULE'                    
173700             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
173800             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
173900             PERFORM 9100-SQL-ABEND                                       
174000     END-EVALUATE.                                                        
174100                                                                          
174171******************************************************************        
174172 7130-OPEN-PO-CURSOR.                                                     
174190                                                                          
174191     EXEC SQL                                                             
174192          OPEN PO_CUR                                                     
174193     END-EXEC.                                                            
174194                                                                          
174195     EVALUATE TRUE                                                        
174196                                                                          
174197         WHEN SQLCODE = ZERO                                              
174198             SET PS-EOC-PO-CUR-NO TO TRUE                                 
174199                                                                          
174200         WHEN SQLWARN0 NOT EQUAL SPACE                                    
174201         WHEN SQLCODE NOT EQUAL ZERO                                      
174202             DISPLAY '** ABEND **'                                        
174203             DISPLAY '** PROGRAM = TRKUP265 **'                           
174204             DISPLAY                                                      
174205                '** 7130-OPEN-PO-CURSOR **'                               
174206             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
174207             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
174208             PERFORM 9100-SQL-ABEND                                       
174209     END-EVALUATE.                                                        
174210                                                                          
174212******************************************************************        
174213 7140-FETCH-PO-CURSOR.                                                    
174214                                                                          
174218     EXEC SQL                                                             
174219          FETCH PO_CUR                                                    
174220           INTO :SHPTPO-PO-NBR                                            
174226     END-EXEC.                                                            
174227                                                                          
174228     EVALUATE TRUE                                                        
174229                                                                          
174230         WHEN SQLCODE = ZERO                                              
174231            CONTINUE                                                      
174232                                                                          
174233         WHEN SQLCODE = +100                                              
174234            SET PS-EOC-PO-CUR   TO  TRUE                                  
174235                                                                          
174236         WHEN SQLWARN0 NOT EQUAL SPACE                                    
174237         WHEN SQLCODE NOT EQUAL ZERO                                      
174238             DISPLAY '** ABEND **'                                        
174239             DISPLAY '** PROGRAM = TRKUP265 **'                           
174240             DISPLAY                                                      
174241                '** 7140-FETCH-PO-CURSOR **'                              
174242             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
174243             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
174244             PERFORM 9100-SQL-ABEND                                       
174245     END-EVALUATE.                                                        
174246                                                                          
174247******************************************************************        
174248 7150-CLOSE-PO-CURSOR.                                                    
174250                                                                          
174251     EXEC SQL                                                             
174252         CLOSE PO_CUR                                                     
174253     END-EXEC.                                                            
174254                                                                          
174255     EVALUATE TRUE                                                        
174256         WHEN SQLWARN0 NOT EQUAL SPACE                                    
174257         WHEN SQLCODE NOT EQUAL ZERO                                      
174258             DISPLAY '** ABEND **'                                        
174259             DISPLAY '** PROGRAM = TRKUP265 **'                           
174260             DISPLAY                                                      
174261                '** PARAGRAPH = 7150-CLOSE-PO-CURSOR **'                  
174262             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
174263             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
174264             PERFORM 9100-SQL-ABEND                                       
174265     END-EVALUATE.                                                        
174266                                                                          
174267******************************************************************        
174268 7160-OPEN-CONPO-CNSTR-CUR.                                               
174269                                                                          
174270     EXEC SQL                                                             
174271          OPEN CNSTR_CNPO_CUR                                             
174272     END-EXEC.                                                            
174273                                                                          
174274     EVALUATE TRUE                                                        
174275                                                                          
174276         WHEN SQLCODE = ZERO                                              
174277             SET PS-EOC-CONPO-CNSTR-CUR-NO TO TRUE                        
174278                                                                          
174279         WHEN SQLWARN0 NOT EQUAL SPACE                                    
174280         WHEN SQLCODE NOT EQUAL ZERO                                      
174281             DISPLAY '** ABEND **'                                        
174282             DISPLAY '** PROGRAM = TRKUP265 **'                           
174283             DISPLAY                                                      
174284                '** PARAGRAPH = 7160-OPEN-CONPO-CNSTR-CUR'                
174285             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
174286             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
174287             PERFORM 9100-SQL-ABEND                                       
174288     END-EVALUATE.                                                        
174289                                                                          
174290******************************************************************        
174291 7170-FETCH-CONPO-CNSTR-CUR.                                              
174292                                                                          
174293     EXEC SQL                                                             
174294          FETCH CNSTR_CNPO_CUR                                            
174295           INTO :TRT00015-RUL-ID                                          
174296               ,:TRT00015-RUL-CNSTR-CDE                                   
174297               ,:TRT00015-RUL-CNSTR-TYP-CDE                               
174298               ,:PV-PO-NBR-X                                              
174299               ,:TRT00015-CRE-TMST                                        
174300               ,:TRT00015-CRE-BY-USR-ID                                   
174301     END-EXEC.                                                            
174302                                                                          
174303     EVALUATE TRUE                                                        
174304                                                                          
174305         WHEN SQLCODE = ZERO                                              
174306            CONTINUE                                                      
174307                                                                          
174308         WHEN SQLCODE = +100                                              
174309            SET PS-EOC-CONPO-CNSTR-CUR TO TRUE                            
174310                                                                          
174311         WHEN SQLWARN0 NOT EQUAL SPACE                                    
174312         WHEN SQLCODE NOT EQUAL ZERO                                      
174313             DISPLAY '** ABEND **'                                        
174314             DISPLAY '** PROGRAM = TRKUP265 **'                           
174315             DISPLAY                                                      
174316                '** PARAGRAPH = 7170-FETCH-CONPO-CNSTR-CUR'               
174317             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
174318             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
174319             PERFORM 9100-SQL-ABEND                                       
174320     END-EVALUATE.                                                        
174321                                                                          
174322******************************************************************        
174323 7180-CLOSE-CONPO-CNSTR-CUR.                                              
174324                                                                          
174325     EXEC SQL                                                             
174326         CLOSE CNSTR_CNPO_CUR                                             
174327     END-EXEC.                                                            
174328                                                                          
174329     EVALUATE TRUE                                                        
174330         WHEN SQLWARN0 NOT EQUAL SPACE                                    
174331         WHEN SQLCODE NOT EQUAL ZERO                                      
174332             DISPLAY '** ABEND **'                                        
174333             DISPLAY '** PROGRAM = TRKUP265 **'                           
174334             DISPLAY                                                      
174335                '** PARAGRAPH = 7180-CLOSE-CONPO-CNSTR-CUR'               
174336             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
174337             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
174338             PERFORM 9100-SQL-ABEND                                       
174339     END-EVALUATE.                                                        
174340                                                                          
174341******************************************************************        
174342 7190-DELETE-CONPO-CNSTR.                                                 
174343                                                                          
174344     EXEC SQL                                                             
174345        DELETE FROM TRT_RUL_CNSTR                                         
174346         WHERE RUL_ID            = :TRT00015-RUL-ID                       
174347           AND RUL_CNSTR_CDE     = :TRT00015-RUL-CNSTR-CDE                
174348           AND RUL_CNSTR_TYP_CDE = :TRT00015-RUL-CNSTR-TYP-CDE            
174349           AND RUL_CNSTR_VAL_TXT = :PV-PO-NBR-X                           
174350     END-EXEC.                                                            
174351                                                                          
174352     EVALUATE TRUE                                                        
174353         WHEN SQLCODE = ZERO                                              
174354            CONTINUE                                                      
174355                                                                          
174356         WHEN OTHER                                                       
174357             DISPLAY '** ABEND **'                                        
174358             DISPLAY '** PROGRAM = TRKUP265 **'                           
174359             DISPLAY '** PARAGRAPH = 7190-DELETE-CONPO-CNSTR'             
174360             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
174361             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
174362             PERFORM 9100-SQL-ABEND                                       
174363     END-EVALUATE.                                                        
174364                                                                          
174365******************************************************************        
174370 7500-SELECT-AUDIT-RULE.                                                  
174400                                                                          
174500     SET PS-AUD-ROW-EXISTS-NO     TO TRUE.                                
174600                                                                          
174700     EXEC SQL                                                             
174800                                                                          
174900         SELECT RT.RUL_ID                                                 
175000               ,CT.RUL_CNSTR_VAL_TXT                                      
175100               ,MAX(RT.AUD_CRE_TMST)                                      
175200          INTO :PV-AUDIT-RULE-ID                                          
175300              ,:PV-AUDIT-DECON-SCAC                                       
175400              ,:TRT00017-AUD-CRE-TMST                                     
175500          FROM TRT_AUD_RUL_CNSTR CT                                       
175600              ,TRT_AUD_RUL RT                                             
175700          WHERE CT.RUL_CNSTR_TYP_CDE = 'DECN'                             
175800            AND RT.RUL_ID           =  CT.RUL_ID                          
175900            AND RT.RUL_BEG_DTE      = :PV-266-BEGIN-DATE                  
176000            AND RT.RUL_END_DTE      = :PV-266-END-DATE                    
176100            AND RT.RUL_TYP_CDE      = :TRT00017-RUL-TYP-CDE               
176200            AND RT.RUL_CDE          = 'CONT'                              
176300            AND RT.AUD_CRE_USR_ID = :PC-CURRENT-PROGRAM-NAME              
176400            AND :PV-POE-USED =                                            
176500                (SELECT SUBA.RUL_CNSTR_VAL_TXT                            
176600                   FROM TRT_AUD_RUL_CNSTR SUBA                            
176700                  WHERE SUBA.RUL_ID = RT.RUL_ID                           
176800                          AND SUBA.RUL_CNSTR_TYP_CDE = 'POE')             
176900            AND :TR265-CONTNR-ID =                                        
177000                (SELECT SUBB.RUL_CNSTR_VAL_TXT                            
177100                   FROM TRT_AUD_RUL_CNSTR SUBB                            
177200                  WHERE SUBB.RUL_ID = RT.RUL_ID                           
177300                          AND SUBB.RUL_CNSTR_TYP_CDE = 'CONT')            
177400            AND :PV-SEQ-OR-ETA =                                          
177500                (SELECT SUBC.RUL_CNSTR_VAL_TXT                            
177600                   FROM TRT_AUD_RUL_CNSTR SUBC                            
177700                  WHERE SUBC.RUL_ID = RT.RUL_ID                           
177800                    AND SUBC.RUL_CNSTR_TYP_CDE =                          
177900                                          :PV-TYP-SEQ-OR-ETA)             
178000       GROUP BY RT.RUL_ID                                                 
178100               ,CT.RUL_CNSTR_VAL_TXT                                      
178200                                                                          
178300     END-EXEC                                                             
178400                                                                          
178500     EVALUATE TRUE                                                        
178600                                                                          
178700         WHEN SQLCODE = ZERO                                              
178800             SET PS-AUD-ROW-EXISTS    TO TRUE                             
178900                                                                          
179000         WHEN SQLCODE = +100                                              
179100             CONTINUE                                                     
179200                                                                          
179300         WHEN SQLWARN0 NOT EQUAL SPACE                                    
179400         WHEN SQLCODE NOT EQUAL ZERO                                      
179500             DISPLAY '** ABEND **'                                        
179600             DISPLAY '** PROGRAM = TRKUP265 **'                           
179700             DISPLAY '** PARAGRAPH = 7500-SELECT-CONT-AUDIT'              
179800             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
179900             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
180000             PERFORM 9100-SQL-ABEND                                       
180100     END-EVALUATE.                                                        
180200                                                                          
180300******************************************************************        
180400 7510-INSERT-RULE-AUD.                                                    
180500                                                                          
180600     EXEC SQL                                                             
180700                                                                          
180800         INSERT INTO TRT_AUD_RUL                                          
180900            (RUL_ID                                                       
181000            ,RUL_CDE                                                      
181100            ,RUL_TYP_CDE                                                  
181200            ,RUL_BEG_DTE                                                  
181300            ,RUL_END_DTE                                                  
181310            ,CRE_TMST                                                     
181320            ,CRE_BY_USR_ID                                                
181400            ,AUD_CRE_USR_ID                                               
181500            ,AUD_FUNC_CDE                                                 
181600            ,AUD_CRE_TMST)                                                
181700          VALUES                                                          
181800           (:PV-CURRENT-RULE-ID                                           
181900           ,:TRT00014-RUL-CDE                                             
182000           ,:TRT00014-RUL-TYP-CDE                                         
182100           ,:TRT00014-RUL-BEG-DTE                                         
182200           ,:TRT00014-RUL-END-DTE                                         
182210           ,:TRT00014-CRE-TMST                                            
182220           ,:TRT00014-CRE-BY-USR-ID                                       
182300           ,:PC-CURRENT-PROGRAM-NAME                                      
182400           ,'DEL'                                                         
182500           ,CURRENT_TIMESTAMP)                                            
182600                                                                          
182700     END-EXEC                                                             
182800                                                                          
182900     EVALUATE TRUE                                                        
183000         WHEN SQLCODE = ZERO                                              
183100             CONTINUE                                                     
183200                                                                          
183300         WHEN SQLWARN0 NOT EQUAL SPACE                                    
183400         WHEN SQLCODE NOT EQUAL ZERO                                      
183500             DISPLAY '** ABEND **'                                        
183600             DISPLAY '** PROGRAM = TRKUP265 **'                           
183700             DISPLAY '** PARAGRAPH = 7510-INSERT-RULE-AUD'                
183800             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
183900             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
184000             PERFORM 9100-SQL-ABEND                                       
184100     END-EVALUATE.                                                        
184200                                                                          
184300******************************************************************        
184400 7520-OPEN-CNSTR-CUR.                                                     
184500                                                                          
184600     EXEC SQL                                                             
184700          OPEN CNSTR_CUR                                                  
184800     END-EXEC.                                                            
184900                                                                          
185000     EVALUATE TRUE                                                        
185100                                                                          
185200         WHEN SQLCODE = ZERO                                              
185300             SET PS-EOC-CNSTR-CUR-NO TO TRUE                              
185400                                                                          
185500         WHEN SQLWARN0 NOT EQUAL SPACE                                    
185600         WHEN SQLCODE NOT EQUAL ZERO                                      
185700             DISPLAY '** ABEND **'                                        
185800             DISPLAY '** PROGRAM = TRKUP265 **'                           
185900             DISPLAY                                                      
186000                '** PARAGRAPH = 7520-OPEN-CNSTR-CUR'                      
186100             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
186200             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
186300             PERFORM 9100-SQL-ABEND                                       
186400     END-EVALUATE.                                                        
186500                                                                          
186600******************************************************************        
186700 7530-FETCH-CNSTR-CUR.                                                    
186800                                                                          
186900     EXEC SQL                                                             
187000          FETCH CNSTR_CUR                                                 
187100           INTO :TRT00015-RUL-ID                                          
187200               ,:TRT00015-RUL-CNSTR-CDE                                   
187300               ,:TRT00015-RUL-CNSTR-TYP-CDE                               
187400               ,:TRT00015-RUL-CNSTR-VAL-TXT                               
187500               ,:TRT00015-CRE-TMST                                        
187600               ,:TRT00015-CRE-BY-USR-ID                                   
187700     END-EXEC.                                                            
187800                                                                          
187900     EVALUATE TRUE                                                        
188000                                                                          
188100         WHEN SQLCODE = ZERO                                              
188200            CONTINUE                                                      
188300                                                                          
188400         WHEN SQLCODE = +100                                              
188500            SET PS-EOC-CNSTR-CUR   TO  TRUE                               
188600                                                                          
188700         WHEN SQLWARN0 NOT EQUAL SPACE                                    
188800         WHEN SQLCODE NOT EQUAL ZERO                                      
188900             DISPLAY '** ABEND **'                                        
189000             DISPLAY '** PROGRAM = TRKUP265 **'                           
189100             DISPLAY                                                      
189200                '** PARAGRAPH = 7530-FETCH-CNSTR-CUR'                     
189300             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
189400             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
189500             PERFORM 9100-SQL-ABEND                                       
189600     END-EVALUATE.                                                        
189700                                                                          
189800******************************************************************        
189900 7540-CLOSE-CNSTR-CUR.                                                    
190000                                                                          
190100     EXEC SQL                                                             
190200         CLOSE CNSTR_CUR                                                  
190300     END-EXEC.                                                            
190400                                                                          
190500     EVALUATE TRUE                                                        
190600         WHEN SQLWARN0 NOT EQUAL SPACE                                    
190700         WHEN SQLCODE NOT EQUAL ZERO                                      
190800             DISPLAY '** ABEND **'                                        
190900             DISPLAY '** PROGRAM = TRKUP265 **'                           
191000             DISPLAY                                                      
191100                '** PARAGRAPH = 7540-CLOSE-CNSTR-CUR'                     
191200             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
191300             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
191400             PERFORM 9100-SQL-ABEND                                       
191500     END-EVALUATE.                                                        
191600                                                                          
191700******************************************************************        
191800 7550-INSERT-CNSTR-AUD.                                                   
191900                                                                          
192000     EXEC SQL                                                             
192100                                                                          
192200         INSERT INTO TRT_AUD_RUL_CNSTR                                    
192300            (RUL_ID                                                       
192400            ,RUL_CNSTR_CDE                                                
192500            ,RUL_CNSTR_TYP_CDE                                            
192600            ,RUL_CNSTR_VAL_TXT                                            
192610            ,CRE_TMST                                                     
192620            ,CRE_BY_USR_ID                                                
192700            ,AUD_CRE_USR_ID                                               
192800            ,AUD_FUNC_CDE                                                 
192900            ,AUD_CRE_TMST)                                                
193000          VALUES                                                          
193100           (:TRT00015-RUL-ID                                              
193200           ,:TRT00015-RUL-CNSTR-CDE                                       
193300           ,:TRT00015-RUL-CNSTR-TYP-CDE                                   
193400           ,:TRT00015-RUL-CNSTR-VAL-TXT                                   
193410           ,:TRT00015-CRE-TMST                                            
193420           ,:TRT00015-CRE-BY-USR-ID                                       
193500           ,:PC-CURRENT-PROGRAM-NAME                                      
193600           ,'DEL'                                                         
193700           ,CURRENT_TIMESTAMP)                                            
193800                                                                          
193900     END-EXEC                                                             
194000                                                                          
194100     EVALUATE TRUE                                                        
194200         WHEN SQLCODE = ZERO                                              
194300             CONTINUE                                                     
194400                                                                          
194500         WHEN SQLWARN0 NOT EQUAL SPACE                                    
194600         WHEN SQLCODE NOT EQUAL ZERO                                      
194700             DISPLAY '** ABEND **'                                        
194800             DISPLAY '** PROGRAM = TRKUP265 **'                           
194900             DISPLAY '** PARAGRAPH = 7550-INSERT-CNSTR-AUD'               
195000             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
195100             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
195200             PERFORM 9100-SQL-ABEND                                       
195300     END-EVALUATE.                                                        
195400                                                                          
195560******************************************************************        
195600 7990-RETRIEVE-RULE-ID.                                                   
195700                                                                          
195800     EXEC SQL                                                             
195900          SET :PV-INSERTED-RULE-ID = IDENTITY_VAL_LOCAL ()                
196000     END-EXEC.                                                            
196100                                                                          
196200     EVALUATE TRUE                                                        
196300         WHEN SQLCODE = ZERO                                              
196400             CONTINUE                                                     
196500                                                                          
196600         WHEN SQLWARN0 NOT EQUAL SPACE                                    
196700         WHEN SQLCODE NOT EQUAL ZERO                                      
196800             DISPLAY '** ABEND **'                                        
196900             DISPLAY '** PROGRAM = TRKUP265 **'                           
197000             DISPLAY '** PARAGRAPH = 7990-RETRIEVE-RULE-ID'               
197100             DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                       
197200             DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                  
197300             DISPLAY 'SQLCODE   = ' SQLCODE                               
197400             DISPLAY 'SQLWARN0  = ' SQLWARN0                              
197500             PERFORM 9100-SQL-ABEND                                       
197600     END-EVALUATE.                                                        
197700                                                                          
197800*----------------------------------------------------------------*        
197900*    CHECK TO SEE IF A CHECKPOINT SHOULD BE TAKEN.               *        
198000*----------------------------------------------------------------*        
198100 8000-CHECKPOINT-CHECK.                                                   
198200                                                                          
198300     EXEC SQL                                                             
198400       SET :PV-TMST = CURRENT TIMESTAMP                                   
198500     END-EXEC.                                                            
198600                                                                          
198700     EVALUATE TRUE                                                        
198800         WHEN SQLWARN0 NOT EQUAL SPACE                                    
198900         WHEN SQLCODE NOT EQUAL ZERO                                      
199000             DISPLAY '** ABEND **'                                        
199100             DISPLAY '** PROGRAM = TRKUP265 **'                           
199200             DISPLAY '** PARAGRAPH = 8000-CHEKPOINT-CHECK**'              
199300             DISPLAY '**UNSUCCESSFUL SELECT TIMESTAMP**'                  
199400             PERFORM 9100-SQL-ABEND                                       
199500     END-EVALUATE.                                                        
199600                                                                          
199700     IF PV-TMST > PV-HOLD-TMST                                            
199800        IF PS-COMMITS-TAKEN                                               
199900            CONTINUE                                                      
200000        ELSE                                                              
200100            MOVE PC-IN-PROCESS-CODE TO PV-CKPT-STAT-CODE                  
200200            PERFORM 8600-UPDATE-TCKRSTCNTL                                
200300            SET PS-COMMITS-TAKEN   TO TRUE                                
200400        END-IF                                                            
200500        PERFORM 8700-UPDATE-TCKRSTINF                                     
200600        PERFORM 8100-COMMIT                                               
200700        DISPLAY '  '                                                      
200800        DISPLAY 'COMMIT TAKEN AT '                                        
200900        DISPLAY  PV-TMST (6:2) '/' PV-TMST (9:2) '/' PV-TMST (3:2)        
201000         '  ' PV-TMST (12:2) ':' PV-TMST (15:2) ':' PV-TMST (18:2)        
201100        DISPLAY 'REC COUNT = ' PA-TOTAL-INPUT-REC-COUNT-X                 
201200        DISPLAY 'CONTR ID  = ' TR265-CONTNR-ID                            
201300        DISPLAY 'TRAN TYPE = ' TR265-CONTNR-TRN-TYP                       
201400        DISPLAY '  '                                                      
201500        PERFORM 8200-SET-CHECKPOINT-TIME                                  
201600     END-IF.                                                              
201700                                                                          
201800 EJECT                                                                    
201900******************************************************************        
202000* EXECUTES AN SQL COMMIT AFTER ALL TABLES FOR TRIP ARE UPDATED.           
202100******************************************************************        
202200 8100-COMMIT.                                                             
202300                                                                          
202400     EXEC SQL                                                             
202500         COMMIT                                                           
202600     END-EXEC.                                                            
202700                                                                          
202800     EVALUATE TRUE                                                        
202900         WHEN SQLWARN0 NOT EQUAL SPACE                                    
203000         WHEN SQLCODE NOT EQUAL ZERO                                      
203100       DISPLAY '***********CHECKPOINT*************'                       
203200          DISPLAY 'CONTNR-TRN-TYP = ' TR265-CONTNR-TRN-TYP                
203300          DISPLAY 'CONTAINER ID   = ' TR265-CONTNR-ID                     
203400          DISPLAY '**********************************'                    
203500                                                                          
203600             DISPLAY '** ABEND **'                                        
203700             DISPLAY '** PROGRAM = TRKUP265 **'                           
203800             DISPLAY '** PARAGRAPH = 8100-COMMIT**'                       
203900             DISPLAY '**UNSUCCESSFUL COMMIT**'                            
204000             PERFORM 9100-SQL-ABEND                                       
204100     END-EVALUATE.                                                        
204200 EJECT                                                                    
204300******************************************************************        
204400* SET THE TIME FOR THE NEXT CHECKPOINT.                                   
204500******************************************************************        
204600 8200-SET-CHECKPOINT-TIME.                                                
204700                                                                          
204800      EXEC SQL                                                            
204900          SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)            
205000          INTO :PV-HOLD-TMST                                              
205100          FROM TCKRSTCNTL                                                 
205200          WHERE PLAN_NAME = :PC-CURRENT-PROGRAM-NAME                      
205300      END-EXEC.                                                           
205400                                                                          
205500     EVALUATE TRUE                                                        
205600         WHEN SQLWARN0 NOT EQUAL SPACE                                    
205700         WHEN SQLCODE NOT EQUAL ZERO                                      
205800             DISPLAY '** ABEND **'                                        
205900             DISPLAY '** PROGRAM = TRKUP265 **'                           
206000             DISPLAY '** PARAGRAPH = 8200-SET-CHECKPOINT-TIME**'          
206100             DISPLAY '**UNSUCESSFUL SET OF CHECKPOINT INTERVAL**'         
206200             PERFORM 9100-SQL-ABEND                                       
206300     END-EVALUATE.                                                        
206400 EJECT                                                                    
206500******************************************************************        
206600*    SELECT CHECKPOINT RESTART CONTROL INFO.                     *        
206700******************************************************************        
206800 8300-GET-CHECKPOINT-CNTL.                                                
206900                                                                          
207000     EXEC SQL                                                             
207100      SELECT CKPT_STAT_CODE                                               
207200       INTO :PV-CKPT-STAT-CODE                                            
207300       FROM TCKRSTCNTL                                                    
207400       WHERE PLAN_NAME = :PC-CURRENT-PROGRAM-NAME                         
207500     END-EXEC.                                                            
207600                                                                          
207700                                                                          
207800     EVALUATE TRUE                                                        
207900         WHEN SQLWARN0 NOT EQUAL SPACE                                    
208000         WHEN SQLCODE NOT EQUAL ZERO                                      
208100             DISPLAY '** ABEND **'                                        
208200             DISPLAY '** PROGRAM = TRKUP265 **'                           
208300             DISPLAY '** PARAGRAPH = 8300-GET-CHECKPOINT-CNTL**'          
208400             DISPLAY '** CHECKING IF RUN IS NORMAL OR RESTART*'           
208500             PERFORM 9100-SQL-ABEND                                       
208600     END-EVALUATE.                                                        
208700                                                                          
208800 EJECT                                                                    
208900******************************************************************        
209000 8400-CHECKPOINT-RESTART.                                                 
209100     DISPLAY '8400-CHECKPOINT-RESTART.'.                                  
209200     PERFORM 8500-GET-CHECKPOINT-INFO.                                    
209300     MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO PV-CKPT-RESTART-INFO.          
209400                                                                          
209500     DISPLAY '** TRKUP265 CHECKPOINT RESTART ** '                         
209600     DISPLAY 'RESTART RECORD COUNT ' PV-RESTART-TOT-IN-REC-COUNT.         
209700     DISPLAY '***********************************************'            
209800                                                                          
209900     PERFORM 5000-READ-RULES-FILE                                         
210000           UNTIL PA-TOTAL-INPUT-REC-COUNT                                 
210100                 = PV-RESTART-TOT-IN-REC-COUNT.                           
210200                                                                          
210300******************************************************************        
210400*    SELECT CHECKPOINT RESTART WORKING STORAGE INFORMATION.      *        
210500******************************************************************        
210600 8500-GET-CHECKPOINT-INFO.                                                
210700                                                                          
210800     EXEC SQL                                                             
210900      SELECT WORK_STRG_DESC                                               
211000       INTO :TCKRSTINF-WORK-STRG-DESC                                     
211100       FROM TCKRSTINF                                                     
211200      WHERE PLAN_NAME = :PC-CURRENT-PROGRAM-NAME                          
211300     END-EXEC.                                                            
211400                                                                          
211500     EVALUATE TRUE                                                        
211600         WHEN SQLWARN0 NOT EQUAL SPACE                                    
211700         WHEN SQLCODE NOT EQUAL ZERO                                      
211800             DISPLAY '** ABEND **'                                        
211900             DISPLAY '** PROGRAM = TRKUP265 **'                           
212000             DISPLAY '** PARAGRAPH = 8500-GET-CHECKPOINT-INFO**'          
212100             DISPLAY '** GETTING RESTART WORKING STORAGE INFO**'          
212200             PERFORM 9100-SQL-ABEND                                       
212300     END-EVALUATE.                                                        
212400                                                                          
212500 EJECT                                                                    
212600******************************************************************        
212700*    UPDATES THE CHECKPOINT RESTART CONTROL TABLE.  THE STATUS   *        
212800*    CODE IS SET TO '9' (RUN IN PROGRESS) OR '0' (RUN COMPLETE). *        
212900******************************************************************        
213000 8600-UPDATE-TCKRSTCNTL.                                                  
213100                                                                          
213200     EXEC SQL                                                             
213300       UPDATE TCKRSTCNTL                                                  
213400       SET CKPT_STAT_CODE   = :PV-CKPT-STAT-CODE                          
213500       WHERE PLAN_NAME      = :PC-CURRENT-PROGRAM-NAME                    
213600     END-EXEC.                                                            
213700                                                                          
213800     EVALUATE TRUE                                                        
213900         WHEN SQLWARN0 NOT EQUAL SPACE                                    
214000         WHEN SQLCODE NOT EQUAL ZERO                                      
214100             DISPLAY '** ABEND **'                                        
214200             DISPLAY '** PROGRAM = TRKUP265 **'                           
214300             DISPLAY '** PARAGRAPH = 8600-UPDATE-TCKRSTCNTL**'            
214400             DISPLAY '** GETTING RESTART INFORMATION AFTER ABEND*'        
214500             PERFORM 9100-SQL-ABEND                                       
214600     END-EVALUATE.                                                        
214700                                                                          
214800 EJECT                                                                    
214900******************************************************************        
215000*    UPDATES THE CHECKPOINT RESTART INFORMATION TABLE.  THIS     *        
215100*    INFORMATION WOULD BE USED TO RESTART JOB IN CASE OF AN ABEND*        
215200******************************************************************        
215300 8700-UPDATE-TCKRSTINF.                                                   
215400                                                                          
215500     MOVE PV-CKPT-RESTART-INFO            TO                              
215600         TCKRSTINF-WORK-STRG-DESC-TEXT.                                   
215700     MOVE 4022                            TO                              
215800         TCKRSTINF-WORK-STRG-DESC-LEN.                                    
215900                                                                          
216000     EXEC SQL                                                             
216100        UPDATE TCKRSTINF                                                  
216200         SET WORK_STRG_DESC   = :TCKRSTINF-WORK-STRG-DESC                 
216300         WHERE PLAN_NAME      = :PC-CURRENT-PROGRAM-NAME                  
216400     END-EXEC.                                                            
216500                                                                          
216600     EVALUATE TRUE                                                        
216700         WHEN SQLWARN0 NOT EQUAL SPACE                                    
216800         WHEN SQLCODE NOT EQUAL ZERO                                      
216900             DISPLAY '** ABEND **'                                        
217000             DISPLAY '** PROGRAM = TRKUP265 **'                           
217100             DISPLAY '** PARAGRAPH = 8700-UPDATE-TCKRSTINF**'             
217200             DISPLAY '** UNSUCCESSFUL UPDATE TCKRSTINF**'                 
217300             PERFORM 9100-SQL-ABEND                                       
217400     END-EVALUATE.                                                        
217500                                                                          
217600******************************************************************        
217700* END OF JOB ROUTINE                                                      
217800******************************************************************        
217900                                                                          
218000 9000-EOJ-ROUTINE.                                                        
218100                                                                          
218200     CLOSE RULES-INPUT-FILE                                               
218300           RULES-EXP-OUT-FILE.                                            
218400                                                                          
218500     DISPLAY 'TOTAL RECORDS READ = ' PA-TOTAL-INPUT-REC-COUNT-X.          
218510     DISPLAY 'TOTAL RULES LOADED = ' PA-TOTAL-LOADED-RUL-COUNT-X.         
218600                                                                          
218700     MOVE PC-RUN-COMPLETE-CODE TO PV-CKPT-STAT-CODE.                      
218800     PERFORM 8600-UPDATE-TCKRSTCNTL.                                      
218900                                                                          
219000******************************************************************        
219100* EXECUTABLE COPYBOOK FOR DATE ROUTINE - D266-GET-BEGIN-END-DATES         
219200******************************************************************        
219300     COPY TRPD266.                                                        
219400                                                                          
219500******************************************************************        
219600* 9100-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
219700* EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
219800******************************************************************        
219900 9100-SQL-ABEND.                                                          
220000******************************************************************        
220100* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
220200* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
220300* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
220400* FORMAT.                                                                 
220500******************************************************************        
220600     COPY DPPD004.                                                        
220700                                                                          
220800     MOVE +4013 TO ABEND-CODE.                                            
220900     CALL 'ILBOABN0' USING ABEND-CODE.                                    
