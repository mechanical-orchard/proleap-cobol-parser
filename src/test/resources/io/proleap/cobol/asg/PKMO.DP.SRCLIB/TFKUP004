       IDENTIFICATION DIVISION.                                         00010020
                                                                        00020021
       PROGRAM-ID.      TFKUP004.                                       00030021
       AUTHOR.          CHERI PARMLEY.                                  00040021
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00050021
       DATE-WRITTEN.    07/11/11.                                       00060021
       DATE-COMPILED.                                                   00070021
                                                                        00080021
      ******************************************************************00090021
      *  MODULE TYPE:   COBOL2 DB2 BATCH.                               00100021
      *                                                                 00110021
      *  DESCRIPTION:   THIS DB2 CHECKPOINT RESTART PROGRAM             00120021
      *                 HANDLES INSERTS FOR TTFRCPT, TTRITRC AND        00130021
      *                 TTFSTAT.                                        00140021
      *                                                                 00150021
      *========================== CHANGE LOG ===========================00160021
      *  AUTH      BY          DATE                 DESCRIPTION         00170021
      *=================================================================00180021
      * WR40189  C. PARMLEY  07-11-11   CREATE PROGRAM                  00190021
      ******************************************************************00200021
022812* PRJ3403  C. PARMLEY  02-28-12   COMPILE TTFITRC TABLE CHANGES   00210020
022812*                                                                 00220020
260107* CHG0260107 JIM HUNTER 08-19-21  ADD COPYBOOK DPWS991 TO         00221024
260107*                                 MAKE DPKUT901 CALL DYNAMIC.     00222024
      ******************************************************************00230021
                                                                        00240021
       ENVIRONMENT DIVISION.                                            00250021
       CONFIGURATION SECTION.                                           00260021
                                                                        00270021
       SOURCE-COMPUTER.    IBM-3090.                                    00280021
       OBJECT-COMPUTER.    IBM-3090.                                    00290021
                                                                        00300021
       INPUT-OUTPUT SECTION.                                            00310021
       FILE-CONTROL.                                                    00320021
                                                                        00330021
       DATA DIVISION.                                                   00340021
       FILE SECTION.                                                    00350021
                                                                        00360021
                                                                        00370021
      ***************************************************************** 00380022
      *                       WORKING STORAGE                           00390022
      ***************************************************************** 00400022
       WORKING-STORAGE SECTION.                                         00410022
                                                                        00420022
       01  W-START                          PIC  X(40)                  00430022
           VALUE 'TFKUP004 - WORKING STORAGE BEGINS HERE'.              00440022
                                                                        00450022
                                                                        00460022
      ***************************************************************** 00470022
      *                         SWITCHES                                00480022
      ***************************************************************** 00490022
       01  PROGRAM-SWITCHES.                                            00500022
           05  PS-RESTART-REQUIRED-SW       PIC  X(01) VALUE 'N'.       00510022
               88  PS-RESTART-REQUIRED                 VALUE 'Y'.       00520022
               88  PS-RESTART-NOT-REQUIRED             VALUE 'N'.       00530022
                                                                        00540022
      ******************************************************************00550022
      *                       ACCUMULATORS                              00560022
      ******************************************************************00570022
       01  PROGRAM-ACCUM.                                               00580022
           05  PA-READ                      PIC  9(11) VALUE ZEROS.     00590022
           05  PA-CKPT                      PIC  9(11) VALUE ZEROS.     00600022
                                                                        00610022
      ******************************************************************00620022
      *                           CONSTANTS                             00630022
      ******************************************************************00640022
       01  PROGRAM-CONSTANTS.                                           00650022
           05  PC-MAX-RETRIES               PIC S9(04) VALUE +3         00660022
                                                       COMP SYNC.       00670022
           05  PC-MAX-DEADLOCKS             PIC S9(04) VALUE +3         00680022
                                                       COMP SYNC.       00690022
                                                                        00700022
           05  PC-CKPT-JOB-NAME             PIC  X(08) VALUE 'TFK006  '.00710022
           05  PC-CKPT-PLAN-NAME            PIC  X(08) VALUE 'TFKUP004'.00720022
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'TFKUP004'.00730022
                                                                        00740022
           05  PC-TRAN-PRES-FUNC-CODES.                                 00750022
               10  PC-TRAN-PRES-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   00760022
               10  PC-TRAN-PRES-FUNC-TRANS  PIC  X(05) VALUE 'TRANS'.   00770022
               10  PC-TRAN-PRES-FUNC-RSTRT  PIC  X(05) VALUE 'RSTRT'.   00780022
               10  PC-TRAN-PRES-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   00790022
                                                                        00800022
           05  PC-MINUS-ONE                 PIC S9(01) VALUE -1.        00810022
           05  PC-SYSTEM                    PIC  X(08) VALUE 'SYSTEM  '.00820022
                                                                        00830022
      ******************************************************************00840022
      *                          VARIABLES                              00850022
      ******************************************************************00860022
       01  PROGRAM-VARIABLES.                                           00870022
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.      00880022
           05  PV-DEADLOCK-COUNTER          PIC  9(03) VALUE ZERO.      00890022
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     00900022
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.     00910022
           05  PV-OPERATION-MSG             PIC  X(35) VALUE SPACE.     00920022
           05  PV-RET-CODE                  PIC  9(04) VALUE ZEROS.     00930022
           05  PV-KEY                       PIC S9(09) COMP VALUE ZEROS.00940022
           05  PV-LOC                       PIC S9(04) COMP VALUE ZEROS.00950022
           05  PV-LNE-NBR                   PIC S9(04) COMP VALUE ZEROS.00960022
           05  PV-LNE-SEQ-NBR               PIC S9(04) COMP VALUE ZEROS.00970022
           05  PV-ITM-QTY                   PIC S9(04) COMP             00980022
                                                       VALUE ZEROS.     00990022
           05  PV-ITM-RTL-PRCE-AMT          PIC S9(06)V99 COMP-3        01000022
                                                       VALUE ZEROS.     01010022
           05  PV-ITM-XFER-PRCE-AMT         PIC S9(06)V99 COMP-3        01020022
                                                       VALUE ZEROS.     01030022
                                                                        01040022
      ******************************************************************01050022
      *               SYSTEM TIME AND DATE VARIABLES                    01060022
      ******************************************************************01070022
       01  SYS-DATE-TIME.                                               01080022
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    01090022
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    01100023
                                                                        01110023
       01  ST-BEG-TIME.                                                 01120023
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     01130023
           05  FILLER                       PIC  X(01) VALUE ':'.       01140023
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     01150023
                                                                        01160023
       01  ST-END-TIME.                                                 01170023
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     01180023
           05  FILLER                       PIC  X(01) VALUE ':'.       01190023
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     01200023
                                                                        01210023
       01  SD-EDIT-DATE.                                                01220023
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     01230023
           05  FILLER                       PIC  X(01) VALUE '-'.       01240023
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     01250023
           05  FILLER                       PIC  X(01) VALUE '-'.       01260023
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     01270023
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     01280023
                                                                        01290023
      ******************************************************************01300023
      *                        ERROR HANDLING                           01310023
      ******************************************************************01320023
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROS      01330023
                                                       COMP SYNC.       01340023
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     01350023
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.     01360023
           88  ABEND-4019-CAL-SUB-ERROR                VALUE +4019.     01370023
                                                                        01380023
                                                                        01390023
      ******************************************************************01400023
      *  LAYOUT FOR UNLOAD FILE RECORDS                                 01410023
      ******************************************************************01420023
           COPY TFRD068.                                                01430023
                                                                        01440023
      ******************************************************************01450023
      *  DB2 FORMATTED ERROR MESSAGE                                    01460023
      ******************************************************************01470023
           COPY DPWS004.                                                01480023
                                                                        01490023
      ******************************************************************01500023
      *  SQL COMMUNICATIONS WORK AREA                                   01510023
      ******************************************************************01520023
           COPY SQLCA2.                                                 01530023
                                                                        01540023
      ******************************************************************01550023
      *  COMMUNICATIONS AREA FOR DB2                                    01560023
      ******************************************************************01570023
           EXEC SQL                                                     01580023
               INCLUDE SQLCA                                            01590023
           END-EXEC.                                                    01600023
                                                                        01610023
      ******************************************************************01620023
      *  DB2 TABLE TTFRCPT FOR THE TRANSFER RECEIPT TABLE               01630023
      ******************************************************************01640023
           EXEC SQL                                                     01650023
                INCLUDE TTFRCPT                                         01660023
           END-EXEC.                                                    01670023
                                                                        01680023
      ******************************************************************01690023
      *  DB2 TABLE TTFITRC FOR THE TRANSFER RECEIPT ITEM TABLE          01700023
      ******************************************************************01710023
           EXEC SQL                                                     01720023
                INCLUDE TTFITRC                                         01730023
           END-EXEC.                                                    01740023
                                                                        01750023
      ******************************************************************01760023
      *  DB2 TABLE TTFSTAT FOR THE TRANSFER RECEIPT STATUS TABLE        01770023
      ******************************************************************01780023
           EXEC SQL                                                     01790023
                INCLUDE TTFSTAT                                         01800023
           END-EXEC.                                                    01810023
                                                                        01820023
                                                                        01830023
      ******************************************************************01840023
      *  TRANSACTION PREPARATION MODULE COMMUNICATIONS AREA             01850023
      ******************************************************************01860023
260107     COPY DPWS991.                                                01870024
                                                                        01871024
           COPY DPLS001.                                                01872024
           05  WORK-STORAGE-PASSED.                                     01880023
      ******************************************************************01890023
      *PA - PROGRAM ACCUMULATORS                                        01900023
      ******************************************************************01910023
               10  PROGRAM-ACCUMULATORS.                                01920023
                   15  PA-ROWS-INSERTED-RCPT PIC S9(11)       VALUE ZERO01930023
                                                              COMP-3.   01940023
                   15  PA-ROWS-INSERTED-ITRC PIC S9(11)       VALUE ZERO01950023
                                                              COMP-3.   01960023
                   15  PA-ROWS-INSERTED-STAT PIC S9(11)       VALUE ZERO01970023
                                                              COMP-3.   01980023
                                                                        01990023
       01  FILLER                          PIC X(4096)  VALUE SPACE.    02000023
                                                                        02010023
                                                                        02020023
       PROCEDURE DIVISION.                                              02030023
      ******************************************************************02040023
      * MAINLINE-PROCESSING                                             02050023
      ******************************************************************02060023
                                                                        02070023
           PERFORM A100-INITIALIZE.                                     02080023
                                                                        02090023
           PERFORM B100-PROCESS-INSERT-RECORDS                          02100023
             UNTIL DP001-PREP-TRANS-EOF.                                02110023
                                                                        02120023
           PERFORM Z990-EOJ-ROUTINE.                                    02130023
                                                                        02140023
           GOBACK.                                                      02150023
                                                                        02160023
                                                                        02170023
      ******************************************************************02180023
      * PREFORMS TASK INITIATION                                        02190023
      ******************************************************************02200023
       A100-INITIALIZE.                                                 02210023
                                                                        02220023
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 02230023
                                                                        02240023
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            02250023
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            02260023
                                                                        02270023
           MOVE SYS-DATE (1:2) TO SD-CENT.                              02280023
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              02290023
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             02300023
           MOVE SYS-DATE (7:2) TO SD-DAY.                               02310023
                                                                        02320023
           MOVE PC-TRAN-PRES-FUNC-OPEN TO DP001-FUNC-CODE.              02330023
                                                                        02340023
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      02350023
                                                                        02360023
                                                                        02370023
      ******************************************************************02380023
      * PROCESS INSERT RECORDS                                          02390023
      ******************************************************************02400023
       B100-PROCESS-INSERT-RECORDS.                                     02410023
                                                                        02420023
           SET PS-RESTART-NOT-REQUIRED TO TRUE.                         02430023
                                                                        02440023
           PERFORM C100-INSERT-FIELDS.                                  02450023
                                                                        02460023
      *----------------------------------------------------------------*02470023
      *  IF A RESTART IS REQUIRED, ISSUE A RESTART CALL RATHER THAN A  *02480023
      *  TRANS CALL.  THIS WILL ALLOW PROCESSING TO RESUME WITH THE    *02490023
      *  FIRST TRANSACTION AFTER THE LAST COMMIT.                      *02500023
      *----------------------------------------------------------------*02510023
           IF  PS-RESTART-REQUIRED                                      02520023
               MOVE PC-TRAN-PRES-FUNC-RSTRT TO DP001-FUNC-CODE          02530023
           ELSE                                                         02540023
               MOVE PC-TRAN-PRES-FUNC-TRANS TO DP001-FUNC-CODE          02550023
           END-IF.                                                      02560023
                                                                        02570023
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      02580023
                                                                        02590023
           ADD 1 TO PA-READ.                                            02600023
                                                                        02610023
                                                                        02620023
      ******************************************************************02630023
      * INSERTS INTO THE FOLLOWING 3 TABLES BASED ON INDICATOR          02640023
      ******************************************************************02650023
       C100-INSERT-FIELDS.                                              02660023
                                                                        02670023
           EVALUATE TF068-TABLE-TO-UPD                                  02680023
               WHEN 'RCPT'                                              02690023
                   PERFORM D100-TTFRCPT-INSERT                          02700023
               WHEN 'ITRC'                                              02710023
                   PERFORM D200-TTFITRC-INSERT                          02720023
               WHEN 'STAT'                                              02730023
                   PERFORM D300-TTFSTAT-INSERT                          02740023
           END-EVALUATE.                                                02750023
                                                                        02760023
      ******************************************************************02770023
      * INSERT THE TTFRCPT TABLE                                        02780023
      ******************************************************************02790023
       D100-TTFRCPT-INSERT.                                             02800023
                                                                        02810023
           MOVE TF068-DKEY-ID TO PV-KEY.                                02820023
           MOVE TF068-STR-NBR TO PV-LOC.                                02830023
                                                                        02840023
           PERFORM WITH TEST AFTER                                      02850023
               VARYING PV-SQL-SUB                                       02860023
               FROM 1 BY 1                                              02870023
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       02880023
                      OR SQLCODE = +100                                 02890023
                      OR SQLCODE = 0)                                   02900023
                                                                        02910023
               EXEC SQL                                                 02920023
                   INSERT  INTO TTFRCPT                                 02930023
                        (  XFER_DKEY_ID                                 02940023
                        ,  RCV_XFER_BEG_TMST                            02950023
                        ,  RCV_XFER_END_TMST                            02960023
                        ,  RCPT_MTHD_CDE                                02970023
                        ,  ENTR_MTHD_TYP_CDE                            02980023
                        ,  STR_NBR                                      02990023
                        ,  RCVR_INIT_1ST_NM                             03000023
                        ,  RCVR_LAST_NM                                 03010023
                        ,  RCV_BY_USR_ID                                03020023
                        ,  RCV_STR_OVRRID_IND                           03030023
                        ,  RCVG_PROB_IND )                              03040023
                   VALUES                                               03050023
                        (  :PV-KEY                                      03060023
                        ,  :TF068-RCV-XFER-BEG-TMST                     03070023
                        ,  :TF068-RCV-XFER-END-TMST                     03080023
                        ,  :TF068-RCPT-MTHD-CDE                         03090023
                        ,  :TF068-ENTR-MTHD-TYP-CDE                     03100023
                        ,  :PV-LOC                                      03110023
                        ,  :TF068-RCVR-INIT-1ST-NM                      03120023
                        ,  :TF068-RCVR-LAST-NM                          03130023
                        ,  :TF068-RCV-BY-USER-ID                        03140023
                        ,  :TF068-STR-OVRRID-IND                        03150023
                        ,  :TF068-RCVG-PROB-IND )                       03160023
               END-EXEC                                                 03170023
                                                                        03180023
               EVALUATE TRUE                                            03190023
                   WHEN SQLCODE = 0                                     03200023
                       ADD +1 TO PA-ROWS-INSERTED-RCPT                  03210023
                                                                        03220023
                   WHEN SQLCODE = -904                                  03230023
                   WHEN SQLCODE = -911                                  03240023
                       ADD +1 TO PV-DEADLOCK-COUNTER                    03250023
                                                                        03260023
                       DISPLAY '                   '                    03270023
                       DISPLAY 'DEADLOCK INCOUNTERED -911'              03280023
                       DISPLAY 'DEADLOCK-COUNTER = ' PV-DEADLOCK-COUNTER03290023
                                                                        03300023
                       IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS        03310023
                           MOVE 'D100-TTFRCPT-INSERT'                   03320023
                                            TO PV-PARAGRAPH-NAME        03330023
                           MOVE 'MAXIMUM DEADLOCKS EXCEEDED    '        03340023
                                            TO PV-DB2-OPER-ATTEMPTED    03350023
                           PERFORM Z200-SQL-ABEND                       03360023
                                                                        03370023
                       ELSE                                             03380023
                           SET PS-RESTART-REQUIRED TO TRUE              03390023
                       END-IF                                           03400023
                                                                        03410023
                   WHEN OTHER                                           03420023
                       DISPLAY '                   '                    03430023
                       DISPLAY 'SQLCODE = ' SQLCODE                     03440023
                       DISPLAY 'DP001-FUNC-CODE = ' DP001-FUNC-CODE     03450023
                       DISPLAY 'DP001-RET-CODE  = ' DP001-RET-CODE      03460023
                       DISPLAY '                   '                    03470023
                       DISPLAY 'INSERT ABENDED ON: '                    03480023
                       DISPLAY 'XFER DKEY ID ' TF068-DKEY-ID            03490023
                       MOVE 'D100-TTFRCPT-INSERT'                       03500023
                                              TO PV-PARAGRAPH-NAME      03510023
                       MOVE 'TTFRCPT INSERT'                            03520023
                                              TO PV-DB2-OPER-ATTEMPTED  03530023
                       PERFORM Z200-SQL-ABEND                           03540023
               END-EVALUATE                                             03550023
           END-PERFORM.                                                 03560023
                                                                        03570023
           IF PV-SQL-SUB > PC-MAX-RETRIES                               03580023
               MOVE 'D100-TTFRCPT-INSERT    ' TO PV-PARAGRAPH-NAME      03590023
               MOVE 'VERIFY RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED  03600023
               PERFORM Z200-SQL-ABEND                                   03610023
           END-IF.                                                      03620023
                                                                        03630023
                                                                        03640023
      ******************************************************************03650023
      * INSERT THE TTFITRC TABLE                                        03660023
      ******************************************************************03670023
       D200-TTFITRC-INSERT.                                             03680023
                                                                        03690023
           MOVE TF068-DKEY-ID2      TO PV-KEY.                          03700023
           MOVE TF068-XFER-LNE-NBR  TO PV-LNE-NBR.                      03710023
           MOVE TF068-LNE-SEQ-NBR   TO PV-LNE-SEQ-NBR.                  03720023
           MOVE TF068-XFER-QTY      TO PV-ITM-QTY.                      03730023
           MOVE TF068-XFER-PRIC-AMT TO PV-ITM-XFER-PRCE-AMT.            03740023
           MOVE TF068-RTL-PRIC-AMT  TO PV-ITM-RTL-PRCE-AMT.             03750023
                                                                        03760023
           PERFORM WITH TEST AFTER                                      03770023
               VARYING PV-SQL-SUB                                       03780023
               FROM 1 BY 1                                              03790023
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       03800023
                      OR SQLCODE = +100                                 03810023
                      OR SQLCODE = 0)                                   03820023
                                                                        03830023
               EXEC SQL                                                 03840023
                   INSERT  INTO TTFITRC                                 03850023
                        (  XFER_DKEY_ID                                 03860023
                        ,  XFER_LNE_NBR                                 03870023
                        ,  XFER_LNE_SEQ_NBR                             03880023
                        ,  XFER_RCPT_TMST                               03890023
                        ,  RCVD_XFER_QTY                                03900023
                        ,  RCVD_XFER_PRIC_AMT                           03910023
                        ,  RCVD_RTL_PRIC_AMT                            03920023
                        ,  XFER_ITM_DSCRP_IND )                         03930023
                   VALUES                                               03940023
                        (  :PV-KEY                                      03950023
                        ,  :PV-LNE-NBR                                  03960023
                        ,  :PV-LNE-SEQ-NBR                              03970023
                        ,  :TF068-RCPT-TMST                             03980023
                        ,  :PV-ITM-QTY                                  03990023
                        ,  :PV-ITM-XFER-PRCE-AMT                        04000023
                        ,  :PV-ITM-RTL-PRCE-AMT                         04010023
                        ,  :TF068-XFER-ITM-DSCRP-IND )                  04020023
               END-EXEC                                                 04030023
                                                                        04040023
               EVALUATE TRUE                                            04050023
                   WHEN SQLCODE = 0                                     04060023
                       ADD +1 TO PA-ROWS-INSERTED-ITRC                  04070023
                                                                        04080023
                   WHEN SQLCODE = -904                                  04090023
                   WHEN SQLCODE = -911                                  04100023
                       ADD +1 TO PV-DEADLOCK-COUNTER                    04110023
                                                                        04120023
                       DISPLAY '                   '                    04130023
                       DISPLAY 'DEADLOCK INCOUNTERED -911'              04140023
                       DISPLAY 'DEADLOCK-COUNTER = ' PV-DEADLOCK-COUNTER04150023
                                                                        04160023
                       IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS        04170023
                           MOVE 'D200-TTFITRC-INSERT'                   04180023
                                            TO PV-PARAGRAPH-NAME        04190023
                           MOVE 'MAXIMUM DEADLOCKS EXCEEDED    '        04200023
                                            TO PV-DB2-OPER-ATTEMPTED    04210023
                           PERFORM Z200-SQL-ABEND                       04220023
                                                                        04230023
                       ELSE                                             04240023
                           SET PS-RESTART-REQUIRED TO TRUE              04250023
                       END-IF                                           04260023
                                                                        04270023
                   WHEN OTHER                                           04280023
                       DISPLAY '                   '                    04290023
                       DISPLAY 'SQLCODE = ' SQLCODE                     04300023
                       DISPLAY 'DP001-FUNC-CODE = ' DP001-FUNC-CODE     04310023
                       DISPLAY 'DP001-RET-CODE  = ' DP001-RET-CODE      04320023
                       DISPLAY '                   '                    04330023
                       DISPLAY 'INSERT ABENDED ON: '                    04340023
                       DISPLAY 'XFER DKEY ID ' TF068-DKEY-ID2           04350023
                       MOVE 'D200-TTFITRC-INSERT'                       04360023
                                              TO PV-PARAGRAPH-NAME      04370023
                       MOVE 'TTFITRC INSERT'                            04380023
                                              TO PV-DB2-OPER-ATTEMPTED  04390023
                       PERFORM Z200-SQL-ABEND                           04400023
               END-EVALUATE                                             04410023
           END-PERFORM.                                                 04420023
                                                                        04430023
           IF PV-SQL-SUB > PC-MAX-RETRIES                               04440023
               MOVE 'D200-TTFITRC-INSERT    ' TO PV-PARAGRAPH-NAME      04450023
               MOVE 'VERIFY RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED  04460023
               PERFORM Z200-SQL-ABEND                                   04470023
           END-IF.                                                      04480023
                                                                        04490023
      ******************************************************************04500023
      * INSERT THE TTFSTAT TABLE                                        04510023
      ******************************************************************04520023
       D300-TTFSTAT-INSERT.                                             04530023
                                                                        04540023
           MOVE TF068-DKEY-ID3      TO PV-KEY.                          04550023
                                                                        04560023
           PERFORM WITH TEST AFTER                                      04570023
               VARYING PV-SQL-SUB                                       04580023
               FROM 1 BY 1                                              04590023
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       04600023
                      OR SQLCODE = +100                                 04610023
                      OR SQLCODE = 0)                                   04620023
                                                                        04630023
               EXEC SQL                                                 04640023
                   INSERT  INTO TTFSTAT                                 04650023
                        (  XFER_DKEY_ID                                 04660023
                        ,  XFER_STAT_TMST                               04670023
                        ,  XFER_STAT_CDE )                              04680023
                   VALUES                                               04690023
                        (  :PV-KEY                                      04700023
                        ,  :TF068-XFER-STAT-TMST                        04710023
                        ,  :TF068-XFER-STAT-CDE )                       04720023
               END-EXEC                                                 04730023
                                                                        04740023
               EVALUATE TRUE                                            04750023
                   WHEN SQLCODE = 0                                     04760023
                       ADD +1 TO PA-ROWS-INSERTED-STAT                  04770023
                                                                        04780023
                   WHEN SQLCODE = -904                                  04790023
                   WHEN SQLCODE = -911                                  04800023
                       ADD +1 TO PV-DEADLOCK-COUNTER                    04810023
                                                                        04820023
                       DISPLAY '                   '                    04830023
                       DISPLAY 'DEADLOCK INCOUNTERED -911'              04840023
                       DISPLAY 'DEADLOCK-COUNTER = ' PV-DEADLOCK-COUNTER04850023
                                                                        04860023
                       IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS        04870023
                           MOVE 'D300-TTFSTAT-INSERT'                   04880023
                                            TO PV-PARAGRAPH-NAME        04890023
                           MOVE 'MAXIMUM DEADLOCKS EXCEEDED    '        04900023
                                            TO PV-DB2-OPER-ATTEMPTED    04910023
                           PERFORM Z200-SQL-ABEND                       04920023
                                                                        04930023
                       ELSE                                             04940023
                           SET PS-RESTART-REQUIRED TO TRUE              04950023
                       END-IF                                           04960023
                                                                        04970023
                   WHEN OTHER                                           04980023
                       DISPLAY '                   '                    04990023
                       DISPLAY 'SQLCODE = ' SQLCODE                     05000023
                       DISPLAY 'DP001-FUNC-CODE = ' DP001-FUNC-CODE     05010023
                       DISPLAY 'DP001-RET-CODE  = ' DP001-RET-CODE      05020023
                       DISPLAY '                   '                    05030023
                       DISPLAY 'INSERT ABENDED ON: '                    05040023
                       DISPLAY 'XFER DKEY ID ' TF068-DKEY-ID3           05050023
                       MOVE 'D300-TTFSTAT-INSERT'                       05060023
                                              TO PV-PARAGRAPH-NAME      05070023
                       MOVE 'TTFITRC INSERT'                            05080023
                                              TO PV-DB2-OPER-ATTEMPTED  05090023
                       PERFORM Z200-SQL-ABEND                           05100023
               END-EVALUATE                                             05110023
           END-PERFORM.                                                 05120023
                                                                        05130023
           IF PV-SQL-SUB > PC-MAX-RETRIES                               05140023
               MOVE 'D300-TTFSTAT-INSERT    ' TO PV-PARAGRAPH-NAME      05150023
               MOVE 'VERIFY RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED  05160023
               PERFORM Z200-SQL-ABEND                                   05170023
           END-IF.                                                      05180023
                                                                        05190023
                                                                        05200023
      ******************************************************************05210023
      * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION      05220023
      * PRESENTATION MODULE                                             05230023
      ******************************************************************05240023
       X100-BUILD-COMM-AREA-TRAN-PRES.                                  05250023
                                                                        05260023
           MOVE LENGTH OF                                               05270023
                WORK-STORAGE-PASSED TO DP001-WORK-STRG-LENGTH.          05280023
           MOVE PC-CKPT-JOB-NAME    TO DP001-JOB-NAME.                  05290023
           MOVE PC-CKPT-PLAN-NAME   TO DP001-PLAN-NAME.                 05300023
                                                                        05310023
           PERFORM X200-CALL-TRANS-PRES-MODULE.                         05320023
                                                                        05330023
           MOVE DP001-TRANS-RECORD  TO TF068-TRANSFER-REC.              05340023
                                                                        05350023
           IF  DP001-CKPT-TAKEN                                         05360023
               ADD 1 TO PA-CKPT                                         05370023
                                                                        05380023
               INITIALIZE PV-DEADLOCK-COUNTER                           05390023
           END-IF.                                                      05400023
                                                                        05410023
                                                                        05420023
      ******************************************************************05430023
      * VERIFY THE RETURN CODE ON THE CALLED MODULE                     05440023
      ******************************************************************05450023
       X200-CALL-TRANS-PRES-MODULE.                                     05460023
                                                                        05470023
           PERFORM Z900-CALL-TRANS-PRES-MODULE.                         05480023
                                                                        05490023
           IF  DP001-GOOD-RET-CODE                                      05500023
           OR  DP001-CKPT-TAKEN                                         05510023
           OR  DP001-RESTART                                            05520023
               CONTINUE                                                 05530023
                                                                        05540023
           ELSE                                                         05550023
               MOVE 'X200-CALL-TRANS-PRES-MODULE' TO PV-PARAGRAPH-NAME  05560023
               MOVE '** BAD CALL TO TRANS PREP MODULE **'               05570023
                                                  TO PV-OPERATION-MSG   05580023
               MOVE DP001-RET-CODE                TO PV-RET-CODE        05590023
                                                                        05600023
               SET ABEND-4019-CAL-SUB-ERROR TO TRUE                     05610023
                                                                        05620023
               PERFORM Z100-ABEND                                       05630023
           END-IF.                                                      05640023
                                                                        05650023
                                                                        05660023
      ******************************************************************05670023
      * NON-DB2 ABEND                                                   05680023
      ******************************************************************05690023
       Z100-ABEND.                                                      05700023
                                                                        05710023
           PERFORM Z999-CONTROLS.                                       05720023
                                                                        05730023
           DISPLAY '                     '.                             05740023
           DISPLAY '** ABEND           **'.                             05750023
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          05760023
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        05770023
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         05780023
           DISPLAY '** RETURN CODE     ** = ' PV-RET-CODE.              05790023
                                                                        05800023
           CALL 'ILBOABN0' USING ABEND-CODE.                            05810023
                                                                        05820023
                                                                        05830023
      ******************************************************************05840023
      * ABEND THE PROGRAM IN THE EVENT THAT A SQL ERROR OR WARNING      05850023
      * CODE OCCURS.                                                    05860023
      ******************************************************************05870023
       Z200-SQL-ABEND.                                                  05880023
                                                                        05890023
           PERFORM Z999-CONTROLS.                                       05900023
                                                                        05910023
           DISPLAY '** ABEND     **'.                                   05920023
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                05930023
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              05940023
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.          05950023
                                                                        05960023
      ******************************************************************05970023
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    05980023
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         05990023
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     06000023
      * FORMAT.                                                         06010023
      ******************************************************************06020023
           COPY DPPD004.                                                06030023
                                                                        06040023
           SET ABEND-4013-DB2-ERROR TO TRUE.                            06050023
                                                                        06060023
           CALL 'ILBOABN0' USING ABEND-CODE.                            06070023
                                                                        06080023
                                                                        06090023
      ******************************************************************06100023
      * PREFORMS TASK TERMINATION PROCESSING                            06110023
      ******************************************************************06120023
       Z990-EOJ-ROUTINE.                                                06130023
                                                                        06140023
           MOVE PC-TRAN-PRES-FUNC-CLOSE TO DP001-FUNC-CODE.             06150023
                                                                        06160023
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      06170023
                                                                        06180023
           PERFORM Z999-CONTROLS.                                       06190023
                                                                        06200023
                                                                        06210023
      ******************************************************************06220023
      * DISPLAY CONTROLS FROM THE PROGRAM                               06230023
      ******************************************************************06240023
       Z999-CONTROLS.                                                   06250023
                                                                        06260023
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 06270023
                                                                        06280023
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            06290023
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            06300023
                                                                        06310023
           DISPLAY '                    '.                              06320023
           DISPLAY '**********************'.                            06330023
           DISPLAY '   TFKUP004 CONTROLS  '.                            06340023
           DISPLAY '**********************'.                            06350023
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         06360023
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          06370023
           DISPLAY 'END TIME  = ' ST-END-TIME.                          06380023
           DISPLAY '======================'.                            06390023
           DISPLAY 'RECORDS READ   = ' PA-READ.                         06400023
           DISPLAY 'CHECKPOINTS    = ' PA-CKPT.                         06410023
           DISPLAY '                '.                                  06420023
           DISPLAY 'RCPT ROWS INSERTED  = ' PA-ROWS-INSERTED-RCPT.      06430023
           DISPLAY 'ITRC ROWS INSERTED  = ' PA-ROWS-INSERTED-ITRC.      06440023
           DISPLAY 'STAT ROWS INSERTED  = ' PA-ROWS-INSERTED-STAT.      06450023
           DISPLAY '                '.                                  06460023
                                                                        06470023
      ******************************************************************06480023
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    06490023
      * MENTS THAT ARE REQUIRED TO INVOKE THE TRANSACTION PRESENTATION  06500023
      * MODULE (DPKUT901) OF THE DB2 CHECKPOINT RESTART SYSTEM.         06510023
      ******************************************************************06520023
           COPY DPPD001.                                                06530023
                                                                        06540023
                                                                        06550023
