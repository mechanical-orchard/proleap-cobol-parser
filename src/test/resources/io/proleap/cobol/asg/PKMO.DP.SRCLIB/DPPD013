00001 ******************************************************************07/14/95
00002 **     THIS PROCEDURE DIVISION COPY BOOK IS USED TO CALL THE    **DPPD013 
00003 ** CICS ABEND PROCESSOR FROM AN ARCHITECTURE PROGRAM.           **   LV001
00004 ** USE DPPD013 TO CALL THE ABEND PROCESSOR FROM AN ARCHITECTURE **DPPD013 
00005 ** PROGRAM.                                                     **DPPD013 
00006 **                                                              **DPPD013 
00007 ** OTHER COPYBOOKS TO INCLUDE:  DPWS013.                        **DPPD013 
00008 **                                                              **DPPD013 
00009 ** USAGE:                                                       **DPPD013 
00010 **   FILL THE APPROPRIATE FIELDS IN DPWS013 AND PERFORM         **DPPD013 
00011 ** DP013-0000-PROCESS-ABEND.  NOTE:  MAKE SURE                  **DPPD013 
00012 ** PC-CURRENT-PROGRAM-NAME IS DEFINED IN THE APPLICATION PROGRAM**DPPD013 
00013 ** AND HAS A VALUE EQUAL TO THE PROGRAM ID OF THE CURRENT       **DPPD013 
00014 ** PROGRAM.                                                     **DPPD013 
00015 **                                                              **DPPD013 
00016 **                   *****  NOTE  *****                         **DPPD013 
00017 ** THERE IS A VERSION OF THIS COPYBOOK TO BE USED BY NON-       **DPPD013 
00018 ** ARCHITECTURE PROGRAMS CALLED DPPD014.                        **DPPD013 
00019 ******************************************************************DPPD013 
00020                                                                   DPPD013 
00021  DP013-0000-PROCESS-ABEND.                                        DPPD013 
00022      MOVE PC-CURRENT-PROGRAM-NAME TO DP013-PROGRAM.               DPPD013 
00023      MOVE DFHEIBLK                TO DP013-DFHEIBLK.              DPPD013 
00024      INITIALIZE DP013-SRC-TASK-NUMBER                             DPPD013 
00025                 DP013-STACK.                                      DPPD013 
00026                                                                   DPPD013 
00027      IF DP020-GOOD-COMMAREA                                       DPPD013 
00028          MOVE DP020-SRC-TASK-NUMBER TO DP013-SRC-TASK-NUMBER      DPPD013 
00029          PERFORM                                                  DPPD013 
00030                  VARYING DP020-STACK-INDEX                        DPPD013 
00031                  FROM 1 BY 1                                      DPPD013 
00032                  UNTIL DP020-STACK-INDEX > DP020-ENTRIES-IN-STACK DPPD013 
00033              SET DP013-STACK-INDEX TO DP020-STACK-INDEX           DPPD013 
00034              MOVE DP020-STACK-TRAN-ID (DP020-STACK-INDEX) TO      DPPD013 
00035                       DP013-STACK-TRAN-ID (DP013-STACK-INDEX)     DPPD013 
00036              MOVE DP020-STACK-TASK-NUMBER (DP020-STACK-INDEX) TO  DPPD013 
00037                       DP013-STACK-TASK-NUMBER (DP013-STACK-INDEX) DPPD013 
00038          END-PERFORM                                              DPPD013 
00039      END-IF.                                                      DPPD013 
00040                                                                   DPPD013 
00041      IF  DP013-LINK-RETURN                                        DPPD013 
00042          EXEC CICS                                                DPPD013 
00043              LINK                                                 DPPD013 
00044                  PROGRAM  (DP013-NAME)                            DPPD013 
00045                  COMMAREA (DP013-PARAMETERS)                      DPPD013 
00046                  LENGTH   (DP013-LENGTH)                          DPPD013 
00047          END-EXEC                                                 DPPD013 
00048      ELSE                                                         DPPD013 
00049          EXEC CICS                                                DPPD013 
00050              XCTL                                                 DPPD013 
00051                  PROGRAM  (DP013-NAME)                            DPPD013 
00052                  COMMAREA (DP013-PARAMETERS)                      DPPD013 
00053                  LENGTH   (DP013-LENGTH)                          DPPD013 
00054          END-EXEC                                                 DPPD013 
00055      END-IF.                                                      DPPD013 
