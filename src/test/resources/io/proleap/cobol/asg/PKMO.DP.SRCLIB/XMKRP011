       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.      XMKRP011.                                       00020001
       AUTHOR.          R. MCDONALD                                     00030001
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040001
       DATE-WRITTEN.    10/13/2004.                                     00050001
       DATE-COMPILED.                                                   00060001
      ******************************************************************00070001
      **XM DOMAIN TABLE INTEGRITY CHECK - FOR TABLE XMT_STR_GP_PRCHG    00080001
      ******************************************************************00090001
      **THIS PROGRAM VALIDATES THE DATA IN THE XM DOMAIN IS THE SAME    00100001
      **AS THE DATA IN THE SOR                                          00110001
      **   CREATED FOR NEW PERM SYSTEM WORK WR27472                     00120014
      ******************************************************************00121001
      *--------------------------------------------------------------*  00121131
      * DATE:        07/26/2010                                         00121231
      * WR/IR#:      WR36734                                            00121332
      * PROGRAMMER:  DAVE METZGER                                       00121431
      * DESCRIPTION: REMOVED TMST VALIDATION CHECK AS PART OF THE       00121531
      *              MDO SPRING 2010 PROJECT DUE TO MESSAGING           00121631
      *              CHANGES FROM PERM TO XM.                           00121731
      *                                                                 00121831
      *--------------------------------------------------------------*  00121931
       ENVIRONMENT DIVISION.                                            00122001
       CONFIGURATION SECTION.                                           00123001
       SOURCE-COMPUTER.        IBM-3090.                                00124001
       OBJECT-COMPUTER.        IBM-3090.                                00125001
       INPUT-OUTPUT SECTION.                                            00126001
       FILE-CONTROL.                                                    00127001
                                                                        00128001
           SELECT SOR-STR-GP-PRCHG-EXT-FILE                             00129002
                  ASSIGN TO INP01.                                      00130001
           SELECT XM-STR-GP-PRCHG-EXT-FILE                              00140002
                  ASSIGN TO INP02.                                      00150001
           SELECT XM-STR-GP-PRCHG-INTEG-RPT-FILE                        00160001
                  ASSIGN TO RPT01.                                      00170001
                                                                        00180001
       DATA DIVISION.                                                   00190001
       FILE SECTION.                                                    00200001
                                                                        00210001
       FD  SOR-STR-GP-PRCHG-EXT-FILE                                    00220002
           RECORDING MODE F                                             00230001
           BLOCK CONTAINS 0 RECORDS                                     00240001
           LABEL RECORDS ARE OMITTED                                    00250001
           DATA RECORD IS SOR-STR-GP-PRCHG-EXT-RECORD.                  00260002
       01  SOR-STR-GP-PRCHG-EXT-RECORD PIC X(200).                      00270002
                                                                        00280001
       FD  XM-STR-GP-PRCHG-EXT-FILE                                     00290002
           RECORDING MODE F                                             00300001
           BLOCK CONTAINS 0 RECORDS                                     00310001
           LABEL RECORDS ARE OMITTED                                    00320001
           DATA RECORD IS XM-STR-GP-PRCHG-EXT-RECORD.                   00330002
       01  XM-STR-GP-PRCHG-EXT-RECORD  PIC X(071).                      00340005
                                                                        00350001
       FD  XM-STR-GP-PRCHG-INTEG-RPT-FILE                               00360001
           RECORDING MODE IS F                                          00370001
           BLOCK CONTAINS 0  RECORDS.                                   00380001
       01  XM-STR-GP-PRCHG-INTEG-RPT-REC PIC X(200).                    00390003
                                                                        00400001
       WORKING-STORAGE SECTION.                                         00410001
                                                                        00420001
       01  FILLER                              PIC X(41)                00430001
           VALUE 'WORKING STORAGE STARTS HERE FOR XMKRP011'.            00440001
                                                                        00450001
       01  PS-PROGRAM-SWITCHES.                                         00460001
           05  PS-END-OF-SOR-STR-GP-PRCHG-SW   PIC X(01) VALUE 'N'.     00470001
               88  PS-END-OF-SOR-STR-GP-PRCHG            VALUE 'Y'.     00480001
               88  PS-NOT-END-OF-SOR-STR-GP-PRCHG        VALUE 'N'.     00490001
           05  PS-END-OF-XM-STR-GP-PRCHG-SW    PIC X(01) VALUE 'N'.     00500001
               88  PS-END-OF-XM-STR-GP-PRCHG             VALUE 'Y'.     00510001
               88  PS-NOT-END-OF-XM-STR-GP-PRCHG         VALUE 'N'.     00520001
           05  PS-ERROR-FOUND-SW               PIC X(01) VALUE 'N'.     00560001
               88  PS-ERROR-FOUND                        VALUE 'Y'.     00570001
               88  PS-NO-ERROR-FOUND                     VALUE 'N'.     00580001
                                                                        00590001
       01  PC-PROGRAM-CONSTANTS.                                        00600001
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)  VALUE         00610001
                                                          'XMKRP011'.   00620001
           05  PC-REPORT-NBR-1                 PIC  9(01) VALUE 1.      00630001
           05  PC-ONE                          PIC  9(01) VALUE 1.      00640001
           05  PC-NULL                         PIC S9(04) VALUE -1.     00650001
           05  PC-MAX-REPORT-LINES             PIC S9(05) VALUE +74     00660001
                                                          COMP-3.       00670001
                                                                        00680001
           05  PC-NO-ERROR-MESSAGE             PIC  X(200) VALUE        00690001
               'NO ERRORS'.                                             00700001
                                                                        00710001
           05  PC-MISSING-FR-SOR-STR-GP-PRCHG PIC X(50) VALUE           00720003
               'RECORD NOT FOUND ON SOR STR_GP_PRCHG EXTRACT.     '.    00730024
                                                                        00740001
           05  PC-MISSING-FR-XM-STR-GP-PRCHG  PIC  X(50) VALUE          00750004
               'RECORD NOT FOUND ON XM STR_GP_PRCHG EXTRACT.      '.    00760024
                                                                        00770001
           05  PC-XTRA-FOUND-SOR-STR-GP-PRCHG PIC   X(50) VALUE         00780004
               'ADDITIONAL DATA FOUND IN SOR STR_GP_PRCHG EXTRACT.'.    00790024
                                                                        00800001
           05  PC-XTRA-FOUND-XM-STR-GP-PRCHG  PIC   X(50) VALUE         00810004
               'ADDITIONAL DATA FOUND IN XM STR_GP_PRCHG EXTRACT. '.    00820024
                                                                        00830001
           05  PC-FIELD-DOES-NOT-MATCH        PIC  X(25) VALUE          00840001
               ' - FIELD DOES NOT MATCH. '.                             00850001
                                                                        00860001
                                                                        00870001
       01  PV-MISCELANEOUS.                                             00880001
           05  PV-TEMP-STR-GP-PRCHG-ID         PIC S9(11) COMP          00890001
                                                          VALUE ZEROS.  00900001
           05  PV-PRINT-RECORD                 PIC X(200) VALUE SPACE.  00910001
           05  PV-SAVE-PRINT-RECORD            PIC X(200) VALUE SPACE.  00911011
           05  PV-LINE-SPACING                 PIC S9(04) VALUE ZEROES  00920001
                                                          COMP SYNC.    00930001
           05  PV-CURR-TIME.                                            00940001
              10  PV-CURR-TIME-HH              PIC  9(02) VALUE ZEROES. 00950001
              10  PV-CURR-TIME-MM              PIC  9(02) VALUE ZEROES. 00960001
              10  PV-CURR-TIME-SSHH            PIC  9(04) VALUE ZEROES. 00970001
                                                                        00980001
           05  PV-CURR-DATE-CCYYMMDD.                                   00990001
               10  PV-CURR-DATE-CCYY           PIC  9(04) VALUE ZEROES. 01000001
               10  PV-CURR-DATE-MM             PIC  9(02) VALUE ZEROES. 01010001
               10  PV-CURR-DATE-DD             PIC  9(02) VALUE ZEROES. 01020001
                                                                        01020101
           05  PV-RFMT-DTE.                                             01021001
               10  PV-RFMT-CC                  PIC  9(02) VALUE ZEROES. 01022110
               10  PV-RFMT-YY                  PIC  9(02) VALUE ZEROES. 01022210
               10  PV-RFMT-MM                  PIC  9(02) VALUE ZEROES. 01022310
               10  PV-RFMT-DD                  PIC  9(02) VALUE ZEROES. 01022410
                                                                        01030001
           05  PV-XM-EFF-DTE.                                           01030101
               10  PV-XM-EFF-CC                PIC  9(02) VALUE ZEROES. 01030201
               10  PV-XM-EFF-YY                PIC  9(02) VALUE ZEROES. 01030301
               10  FILLER                      PIC  X     VALUE SPACES. 01030410
               10  PV-XM-EFF-MM                PIC  9(02) VALUE ZEROES. 01030501
               10  FILLER                      PIC  X     VALUE SPACES. 01030610
               10  PV-XM-EFF-DD                PIC  9(02) VALUE ZEROES. 01030701
                                                                        01030801
           05  PV-XMKEY-XM011.                                          01031201
              10  PV-XMKEY-PRCHG-ID            PIC  9(10).              01031312
              10  PV-XMKEY-STR-GP-TYP-CDE      PIC  9(04).              01031401
              10  PV-XMKEY-STR-GP-ID           PIC  9(04).              01031501
                                                                        01031701
           05  PV-PEKEY-PE104.                                          01031801
              10  PV-PEKEY-PRCHG-ID            PIC  9(10).              01031912
              10  PV-PEKEY-STR-GP-TYP-CDE      PIC  9(04).              01032001
              10  PV-PEKEY-STR-GP-ID           PIC  9(04).              01032101
                                                                        01032201
                                                                        01033001
       01  PV-PROGRAM-COUNTER.                                          01040001
           05  PV-PAGE-CNT                     PIC  9(05) VALUE ZEROS.  01050001
           05  PV-LINE-CNT                     PIC S9(07) VALUE ZEROS.  01060001
           05  PV-SOR-STR-GP-PRCHG-RECS-READ   PIC 9(09) VALUE ZEROS.   01070004
           05  PV-XM-EXTRACT-REC-READ-CNT      PIC  9(09) VALUE ZEROS.  01080001
           05  PV-SOR-STR-GP-PRCHG-RECS-UNIQU  PIC  9(09) VALUE ZEROS.  01090004
           05  PV-XM-EXTRACT-REC-UNIQUE        PIC  9(09) VALUE ZEROS.  01100001
           05  PV-SOR-XM-COMMON-CNT            PIC  9(09) VALUE ZEROS.  01110001
           05  PV-SOR-XM-TOTAL-CNT             PIC  9(09) VALUE ZEROS.  01120001
           05  PV-ERROR-RECORD-WRITTEN-CNT     PIC  9(09) VALUE ZEROS.  01130001
           05  PV-VALID-MATCH-CNT              PIC  9(09) VALUE ZEROS.  01140001
           05  PV-TOTAL-ERROR-CNT              PIC  9(09) VALUE ZEROS.  01150001
           05  PV-RECORD-ERR-CNT               PIC  9(09) VALUE ZEROS.  01160001
                                                                        01170001
      ******************************************************************01270001
      *  TOP OF PAGE HEADING LINE FOR PRINTING A 200 CHARACTER REPORT.  01280001
      ******************************************************************01290001
           COPY DPWS200.                                                01300001
                                                                        01310001
       01  HL2-HEADING-LINE-2.                                          01320001
           05  FILLER                  PIC X(81)   VALUE SPACES.        01321021
           05  FILLER                  PIC X(45)   VALUE                01322021
               'XMT_STR_GP_PRCHG DATA INTEGRITY ERRORS'.                01323021
           05  FILLER                  PIC X(74)   VALUE SPACES.        01324021
                                                                        01370001
       01  HL3-HEADING-LINE-3.                                          01380001
           05  FILLER                  PIC X(02)   VALUE SPACES.        01390001
           05  FILLER                  PIC X(50)   VALUE                01400001
      ********* 123456789-123456789-123456789-123456789-123456789-      01410001
               'PRCHG ID  STORE GROUP TYPE    STORE GROUP ID      '.    01420001
           05  FILLER                  PIC X(02)   VALUE SPACES.        01430001
           05  FILLER                  PIC X(40)   VALUE                01440001
      ********* 123456789-123456789-123456789-123456789-                01450001
               'SOR VALUE                               '.              01460001
           05  FILLER                  PIC X(02)   VALUE SPACES.        01470001
           05  FILLER                  PIC X(40)   VALUE                01480001
      ********* 123456789-123456789-123456789-123456789-                01490001
               'XM VALUE                                '.              01500001
           05  FILLER                  PIC X(02)   VALUE SPACES.        01501001
           05  FILLER                  PIC X(50)   VALUE                01502001
      ********* 123456789-123456789-123456789-123456789-123456789-      01503001
               'ERROR MESSAGE                                     '.    01504001
           05  FILLER                  PIC X(12)   VALUE SPACES.        01505001
       01  HL4-HEADING-LINE-4.                                          01507001
           05  FILLER                  PIC X(02)   VALUE SPACES.        01508001
           05  FILLER                  PIC X(50)   VALUE                01509001
      ********* 123456789-123456789-123456789-123456789-123456789-      01510001
               '--------- ----------------    --------------      '.    01520001
           05  FILLER                  PIC X(02)   VALUE SPACES.        01530001
           05  FILLER                  PIC X(40)   VALUE                01540001
      ********* 123456789-123456789-123456789-123456789-                01550001
               '----------------------------------------'.              01560001
           05  FILLER                  PIC X(02)   VALUE SPACES.        01570001
           05  FILLER                  PIC X(40)   VALUE                01580001
      ********* 123456789-123456789-123456789-123456789-                01590001
               '----------------------------------------'.              01600001
           05  FILLER                  PIC X(02)   VALUE SPACES.        01610001
           05  FILLER                  PIC X(50)   VALUE                01620001
      ********* 123456789-123456789-123456789-123456789-123456789-      01630001
               '--------------------------------------------------'.    01640001
           05  FILLER                  PIC X(12)   VALUE SPACES.        01650001
                                                                        01660001
      ******************************************************************01670001
      *  DETAIL LINE LAYOUT FOR THE ERROR REPORT.                       01680001
      ******************************************************************01681001
       01  DL1-DETAIL-LINE-1.                                           01682001
           05  FILLER                  PIC X(02)     VALUE SPACES.      01683001
           05  DL1-KEY.                                                 01684001
               10  DL1-PRCHG-ID        PIC Z(09)     VALUE ZEROES.      01685006
               10  FILLER              PIC X(01)     VALUE SPACES.      01686007
               10  DL1-STR-GP-TYP-CDE  PIC Z(04)     VALUE SPACES.      01686104
               10  FILLER              PIC X(16)     VALUE SPACES.      01686201
               10  DL1-STR-GP-ID       PIC Z(04)     VALUE SPACES.      01686301
               10  FILLER              PIC X(16)     VALUE SPACES.      01686401
           05  FILLER                  PIC X(02)     VALUE SPACES.      01687001
           05  DL1-SOR-VALUE           PIC X(40)     VALUE SPACES.      01688001
           05  FILLER                  PIC X(02)     VALUE SPACES.      01689001
           05  DL1-XM-VALUE            PIC X(40)     VALUE SPACES.      01690001
           05  FILLER                  PIC X(02)     VALUE SPACES.      01700019
           05  DL1-ERROR-MESSAGE.                                       01710001
               10  DL1-ERROR-FIELD     PIC X(25)     VALUE SPACES.      01720001
               10  DL1-ERROR-MSG       PIC X(25)     VALUE SPACES.      01730001
           05  FILLER                  PIC X(12)     VALUE SPACES.      01740019
                                                                        01750001
                                                                        01760001
      ******************************************************************01770001
      *  TRAILER LAYOUT FOR THE ERROR REPORT.                           01780001
      ******************************************************************01790001
       01  TR1-TRAILER-LINE-1.                                          01800001
           05  FILLER                  PIC X(25)   VALUE                01810001
               ' TOTAL NUMBER OF ERRORS: '.                             01820001
           05  TR1-TOTAL-ERROR-CNT     PIC Z(09)   VALUE ZEROS.         01830001
           05  FILLER                  PIC X(166)  VALUE SPACES.        01840001
                                                                        01850001
       01  TR2-TRAILER-LINE-2.                                          01860001
           05  FILLER                  PIC X(25)   VALUE                01870001
               ' NOT FOUND ON SOR      : '.                             01880001
           05  TR2-SOR-MISSING-CNT     PIC Z(09)   VALUE ZEROS.         01890001
           05  FILLER                  PIC X(166)  VALUE SPACES.        01900001
                                                                        01910001
       01  TR3-TRAILER-LINE-3.                                          01920001
           05  FILLER                  PIC X(25)   VALUE                01930001
               ' NOT FOUND ON XM       : '.                             01940001
           05  TR3-XM-MISSING-CNT      PIC Z(09)   VALUE ZEROS.         01950001
           05  FILLER                  PIC X(166)  VALUE SPACES.        01960001
                                                                        01970001
       01  TR4-TRAILER-LINE-4.                                          01980001
           05  FILLER                  PIC X(25)   VALUE                01990001
               ' RECORD LEVEL ERRORS   : '.                             02000001
           05  TR4-RECORD-ERR-CNT      PIC Z(09)   VALUE ZEROS.         02010001
           05  FILLER                  PIC X(166)  VALUE SPACES.        02020001
                                                                        02030001
      ******************************************************************02040001
      **COPYBOOKS                                                       02050001
      ******************************************************************02060001
                                                                        02070001
      ***SOR EXTRACT FILE LAYOUT                                        02080001
           COPY PERD104.                                                02090002
                                                                        02100001
      ***XM EXTRACT FILE LAYOUT                                         02110001
           COPY XMRD011.                                                02120002
                                                                        02130001
      ***************************************************************** 02140001
       PROCEDURE DIVISION.                                              02150001
      ***************************************************************** 02160001
       0000-MAINLINE.                                                   02170001
                                                                        02180001
           PERFORM 1000-INITIALIZATION.                                 02190001
                                                                        02200001
           PERFORM 2000-PROCESS-RECORDS                                 02210001
             UNTIL PS-END-OF-SOR-STR-GP-PRCHG                           02220001
                OR PS-END-OF-XM-STR-GP-PRCHG.                           02230001
                                                                        02240001
           PERFORM 2200-PROCESS-XTRA-RECORDS                            02250004
             UNTIL PS-END-OF-SOR-STR-GP-PRCHG                           02260001
               AND PS-END-OF-XM-STR-GP-PRCHG.                           02270001
                                                                        02280001
           IF PV-ERROR-RECORD-WRITTEN-CNT = 0                           02290001
               MOVE PC-NO-ERROR-MESSAGE TO DL1-DETAIL-LINE-1            02300001
               MOVE DL1-DETAIL-LINE-1   TO PV-PRINT-RECORD              02310001
               MOVE +2                  TO PV-LINE-SPACING              02311025
               PERFORM 8900-WRITE-REPORT-LINE                           02320001
           ELSE                                                         02330001
               MOVE PV-ERROR-RECORD-WRITTEN-CNT TO                      02340001
                      TR4-RECORD-ERR-CNT                                02350001
               MOVE TR4-TRAILER-LINE-4  TO PV-PRINT-RECORD              02360001
               MOVE +2                  TO PV-LINE-SPACING              02361025
               PERFORM 8900-WRITE-REPORT-LINE                           02370001
                                                                        02380001
               MOVE PV-XM-EXTRACT-REC-UNIQUE   TO                       02390013
                      TR2-SOR-MISSING-CNT                               02400001
               MOVE TR2-TRAILER-LINE-2  TO PV-PRINT-RECORD              02410001
               MOVE +1                  TO PV-LINE-SPACING              02411025
               PERFORM 8900-WRITE-REPORT-LINE                           02420001
                                                                        02430001
               MOVE PV-SOR-STR-GP-PRCHG-RECS-UNIQU  TO                  02441013
                      TR3-XM-MISSING-CNT                                02450001
               MOVE TR3-TRAILER-LINE-3  TO PV-PRINT-RECORD              02460001
               PERFORM 8900-WRITE-REPORT-LINE                           02470001
                                                                        02480001
               MOVE PV-TOTAL-ERROR-CNT          TO                      02490001
                      TR1-TOTAL-ERROR-CNT                               02500001
               MOVE TR1-TRAILER-LINE-1  TO PV-PRINT-RECORD              02510001
               PERFORM 8900-WRITE-REPORT-LINE                           02520001
                                                                        02530001
           END-IF.                                                      02540001
                                                                        02550001
           PERFORM 9000-END-OF-PGM.                                     02560001
                                                                        02570001
           GOBACK.                                                      02580001
                                                                        02590001
      ******************************************************************02600001
      *1000-INITIALIZATION.                                             02610001
      * INITIALIZATION AREA                                             02620001
      ******************************************************************02630001
       1000-INITIALIZATION.                                             02640001
                                                                        02650001
           INITIALIZE PV-PROGRAM-COUNTER                                02661028
                      PE104-RECORD                                      02662028
                      XM011-STR-GP-PRCHG-UNLOAD.                        02663028
                                                                        02670001
           OPEN INPUT  SOR-STR-GP-PRCHG-EXT-FILE                        02680002
                       XM-STR-GP-PRCHG-EXT-FILE.                        02690002
                                                                        02700001
           OPEN OUTPUT XM-STR-GP-PRCHG-INTEG-RPT-FILE.                  02710001
                                                                        02720001
           PERFORM 7000-READ-SOR-STR-GP-PRCHG-EXT                       02730002
           PERFORM 7100-READ-XM-STR-GP-PRCHG-EXT.                       02830004
                                                                        02840001
           ACCEPT PV-CURR-TIME FROM TIME.                               02850001
           MOVE PV-CURR-TIME-HH          TO DP200-RUN-HOUR.             02860001
           MOVE PV-CURR-TIME-MM          TO DP200-RUN-MINUTE.           02870001
                                                                        02880001
           MOVE FUNCTION CURRENT-DATE (1:8)                             02890001
                                         TO PV-CURR-DATE-CCYYMMDD.      02900001
                                                                        02910001
           MOVE PV-CURR-DATE-MM          TO DP200-RUN-MONTH.            02920001
           MOVE PV-CURR-DATE-DD          TO DP200-RUN-DAY.              02930001
           MOVE PV-CURR-DATE-CCYY        TO DP200-RUN-YEAR.             02940001
                                                                        02950001
           MOVE PC-CURRENT-PROGRAM-NAME  TO DP200-PROGRAM-NAME.         02960001
                                                                        02970001
           PERFORM 8700-REPORT-HEADER-ROUTINE.                          02980001
                                                                        02990001
      ******************************************************************03000001
      *2000-PROCESS-RECORDS.                                            03010001
      * THIS SECTION COMPARES RECORDS ON THE SOR PRICE CHANGE FILE &    03020001
      * XM DOMAIN STR-GP-PRCHG EXTRACT FILE.  IF KEYS MATCH, PROGRAM    03030001
      * LOOKS AT DETAIL FIELDS AND CHECKS THAT THEY MATCH, DIFFERENCES  03031001
      * ARE REPORTED. IF THE KEY (PRCHG ID, STR GROUP TYPE CODE, STR    03040001
      * GROUP ID) DOES NOT APPEAR ON ONE FILE, BUT IS ON THE OTHER FILE 03050001
      * THEN AN APPROPRIATE MESSAGE WILL BE WRITTEN TO THE ERROR FILE,  03051001
      * ALONG WITH THE DATA THAT WAS ON THE CORRESPONDING FILES.        03060001
      *                                                                 03070001
      ******************************************************************03090001
       2000-PROCESS-RECORDS.                                            03100001
                                                                        03110001
           INITIALIZE DL1-DETAIL-LINE-1.                                03120001
                                                                        03130001
           EVALUATE TRUE                                                03180001
              WHEN PV-PEKEY-PE104 = PV-XMKEY-XM011                      03193001
                   ADD +1 TO PV-SOR-XM-COMMON-CNT                       03200001
                   PERFORM 2100-COMPARE-FIELDS                          03210001
                   PERFORM 7000-READ-SOR-STR-GP-PRCHG-EXT               03220002
                   PERFORM 7100-READ-XM-STR-GP-PRCHG-EXT                03230002
              WHEN PV-PEKEY-PE104 < PV-XMKEY-XM011                      03240001
      ***     DISPLAY '=== SOR LOWER *'                                 03250001
      ***          PE104-PRCHG-ID, '-', PE104-PRCHG-ID                  03260001
      ***             '== AND XM*'                                      03270001
      ***          XM011-PRCHG-ID, '-', XM011-PRCHG, '**'               03280001
                   PERFORM 2400-MOVE-SOR-VALUES                         03290001
                   MOVE PC-MISSING-FR-XM-STR-GP-PRCHG                   03300004
                      TO DL1-ERROR-MESSAGE                              03310001
                   MOVE DL1-DETAIL-LINE-1 TO PV-PRINT-RECORD            03320001
                   ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                03330001
                   ADD +1 TO PV-SOR-STR-GP-PRCHG-RECS-UNIQU             03340004
                   ADD +1 TO PV-TOTAL-ERROR-CNT                         03350001
                   PERFORM 8900-WRITE-REPORT-LINE                       03360001
                   PERFORM 7000-READ-SOR-STR-GP-PRCHG-EXT               03370002
              WHEN PV-PEKEY-PE104 > PV-XMKEY-XM011                      03380001
                   PERFORM 2500-MOVE-XM-VALUES                          03390001
                   MOVE PC-MISSING-FR-SOR-STR-GP-PRCHG                  03400003
                      TO DL1-ERROR-MESSAGE                              03410001
                   MOVE DL1-DETAIL-LINE-1 TO PV-PRINT-RECORD            03420001
                   ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                03430001
                   ADD +1 TO PV-XM-EXTRACT-REC-UNIQUE                   03440001
                   ADD +1 TO PV-TOTAL-ERROR-CNT                         03450001
                   PERFORM 8900-WRITE-REPORT-LINE                       03460001
                   PERFORM 7100-READ-XM-STR-GP-PRCHG-EXT                03470002
           END-EVALUATE.                                                03480001
                                                                        03490001
      ******************************************************************03500001
      *2100-COMPARE-FIELDS.                                             03510001
      * COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   03520001
      ******************************************************************03530001
       2100-COMPARE-FIELDS.                                             03540001
                                                                        03550001
           SET PS-NO-ERROR-FOUND                   TO TRUE.             03570022
                                                                        03580001
           INITIALIZE DL1-DETAIL-LINE-1.                                03590001
                                                                        03600001
      *    EFFECTIVE DATE                                               03601001
           MOVE XM011-PRCHG-EFF-DTE TO PV-XM-EFF-DTE                    03602001
           MOVE PV-XM-EFF-CC        TO PV-RFMT-CC                       03602210
           MOVE PV-XM-EFF-YY        TO PV-RFMT-YY                       03602310
           MOVE PV-XM-EFF-MM        TO PV-RFMT-MM                       03602410
           MOVE PV-XM-EFF-DD        TO PV-RFMT-DD                       03602510
                                                                        03602610
           IF  PE104-PRCHG-EFF-DTE NOT EQUAL PV-RFMT-DTE                03800001
               SET PS-ERROR-FOUND  TO TRUE                              03810001
               ADD +1                           TO PV-TOTAL-ERROR-CNT   03820001
               MOVE PE104-PRCHG-ID              TO DL1-PRCHG-ID         03830001
               MOVE PE104-STR-GP-TYP-CDE        TO DL1-STR-GP-TYP-CDE   03840001
               MOVE PE104-STR-GP-ID             TO DL1-STR-GP-ID        03841001
               MOVE PE104-PRCHG-EFF-DTE         TO DL1-SOR-VALUE        03850008
               MOVE XM011-PRCHG-EFF-DTE         TO DL1-XM-VALUE         03851008
               MOVE 'PRCHG_EFF_DTE            ' TO DL1-ERROR-FIELD      03860020
               MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG        03870001
               MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD      03880001
               PERFORM 8900-WRITE-REPORT-LINE                           03890001
               INITIALIZE DL1-DETAIL-LINE-1                             03891016
           END-IF.                                                      03900001
                                                                        03930001
           IF  PE104-OH-AUTO-CNT-IND NOT EQUAL XM011-OH-AUTO-CNT-IND    03940001
               SET PS-ERROR-FOUND  TO TRUE                              03950001
               ADD +1                           TO PV-TOTAL-ERROR-CNT   03960001
               MOVE PE104-PRCHG-ID              TO DL1-PRCHG-ID         03971001
               MOVE PE104-STR-GP-TYP-CDE        TO DL1-STR-GP-TYP-CDE   03972001
               MOVE PE104-STR-GP-ID             TO DL1-STR-GP-ID        03973001
               MOVE PE104-OH-AUTO-CNT-IND       TO DL1-SOR-VALUE        03980001
               MOVE XM011-OH-AUTO-CNT-IND       TO DL1-XM-VALUE         03990001
               MOVE 'OH_AUTO_CNT_IND          ' TO DL1-ERROR-FIELD      04000027
               MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG        04010001
               MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD      04020001
               PERFORM 8900-WRITE-REPORT-LINE                           04030001
               INITIALIZE DL1-DETAIL-LINE-1                             04031016
           END-IF.                                                      04040001
                                                                        04070031
039200* 07/26/2010 - COMMENTED OUT AS PART OF MDO SPRING 2010 PROJECT   04071034
039200*                                                                 04072033
039200*    IF  PE104-SOR-CRE-TMST NOT EQUAL XM011-SOR-CRE-TMST          04080033
039200*        SET PS-ERROR-FOUND  TO TRUE                              04090033
039200*        ADD +1                           TO PV-TOTAL-ERROR-CNT   04100033
039200*        MOVE PE104-PRCHG-ID              TO DL1-PRCHG-ID         04110133
039200*        MOVE PE104-STR-GP-TYP-CDE        TO DL1-STR-GP-TYP-CDE   04110233
039200*        MOVE PE104-STR-GP-ID             TO DL1-STR-GP-ID        04110333
039200*        MOVE PE104-SOR-CRE-TMST          TO DL1-SOR-VALUE        04111033
039200*        MOVE XM011-SOR-CRE-TMST          TO DL1-XM-VALUE         04112033
039200*        MOVE 'SOR_CRE_TMST             ' TO DL1-ERROR-FIELD      04113033
039200*        MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG        04114033
039200*        MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD      04115033
039200*        PERFORM 8900-WRITE-REPORT-LINE                           04116033
039200*        INITIALIZE DL1-DETAIL-LINE-1                             04116133
039200*    END-IF.                                                      04117033
                                                                        04120031
039200* 07/26/2010 - COMMENTED OUT AS PART OF MDO SPRING 2010 PROJECT   04121034
      *    IF  PE104-SOR-LAST-UPD-TMST NOT EQUAL XM011-SOR-LAST-UPD-TMST04130031
      *        SET PS-ERROR-FOUND  TO TRUE                              04140031
      *        ADD +1                           TO PV-TOTAL-ERROR-CNT   04150031
      *        MOVE PE104-PRCHG-ID              TO DL1-PRCHG-ID         04160031
      *        MOVE PE104-STR-GP-TYP-CDE        TO DL1-STR-GP-TYP-CDE   04170031
      *        MOVE PE104-STR-GP-ID             TO DL1-STR-GP-ID        04180031
      *        MOVE PE104-SOR-LAST-UPD-TMST     TO DL1-SOR-VALUE        04190031
      *        MOVE XM011-SOR-LAST-UPD-TMST     TO DL1-XM-VALUE         04200031
      *        MOVE 'SOR_LAST_UPD_TMST        ' TO DL1-ERROR-FIELD      04210031
      *        MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG        04220031
      *        MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD      04230031
      *        PERFORM 8900-WRITE-REPORT-LINE                           04240031
      *    END-IF.                                                      04250031
                                                                        04280001
           IF  PS-ERROR-FOUND                                           04290001
               ADD +1 TO PV-RECORD-ERR-CNT                              04300001
               ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                    04310001
           ELSE                                                         04320001
               ADD +1 TO PV-VALID-MATCH-CNT                             04330001
           END-IF.                                                      04340001
                                                                        04350001
      ******************************************************************04360001
      **  PROCESSES THRU END OF FILE FOR REMAINING RECORDS              04370001
      ******************************************************************04380001
       2200-PROCESS-XTRA-RECORDS.                                       04390004
                                                                        04400001
           INITIALIZE DL1-DETAIL-LINE-1.                                04410001
                                                                        04420001
           IF  PS-NOT-END-OF-SOR-STR-GP-PRCHG                           04430002
               IF PV-PEKEY-PE104 NOT = PV-XMKEY-XM011                   04440002
                  PERFORM 2400-MOVE-SOR-VALUES                          04450001
                  MOVE PC-XTRA-FOUND-SOR-STR-GP-PRCHG                   04460004
                     TO DL1-ERROR-MESSAGE                               04470001
                  MOVE DL1-DETAIL-LINE-1   TO PV-PRINT-RECORD           04480001
                  ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                 04490001
                  ADD +1 TO PV-SOR-STR-GP-PRCHG-RECS-UNIQU              04500004
                  ADD +1 TO PV-TOTAL-ERROR-CNT                          04510001
                  PERFORM 8900-WRITE-REPORT-LINE                        04520001
               END-IF                                                   04530001
               PERFORM 7000-READ-SOR-STR-GP-PRCHG-EXT                   04540002
           END-IF.                                                      04550001
                                                                        04560001
           IF  PS-NOT-END-OF-XM-STR-GP-PRCHG                            04570002
               IF PV-PEKEY-PE104 NOT = PV-XMKEY-XM011                   04580002
                  PERFORM 2500-MOVE-XM-VALUES                           04590001
                  MOVE PC-XTRA-FOUND-XM-STR-GP-PRCHG                    04600004
                      TO DL1-ERROR-MESSAGE                              04610001
                  MOVE DL1-DETAIL-LINE-1   TO PV-PRINT-RECORD           04620001
                  ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                 04630001
                  ADD +1 TO PV-XM-EXTRACT-REC-UNIQUE                    04640001
                  ADD +1 TO PV-TOTAL-ERROR-CNT                          04650001
                  PERFORM 8900-WRITE-REPORT-LINE                        04660001
               END-IF                                                   04670001
               PERFORM 7100-READ-XM-STR-GP-PRCHG-EXT                    04680002
           END-IF.                                                      04690001
                                                                        04700001
      ******************************************************************04710001
      * 2300-CHECK-IF-NEW-PG-NEEDED.                                    04720001
      *     CHECKS TO SEE IF A NEW PAGE IS NEEDED.                      04730001
      ***************************************************************** 04740001
       2300-CHECK-IF-NEW-PG-NEEDED.                                     04750001
                                                                        04760001
           IF PV-LINE-CNT > PC-MAX-REPORT-LINES                         04770001
              MOVE PV-PRINT-RECORD        TO PV-SAVE-PRINT-RECORD       04771011
              PERFORM 8700-REPORT-HEADER-ROUTINE                        04780001
              MOVE PV-SAVE-PRINT-RECORD   TO PV-PRINT-RECORD            04781011
           END-IF.                                                      04790001
                                                                        04800001
      ******************************************************************04810001
      * 2400-MOVE-SOR-VALUES.                                           04820001
      *     MOVES THE SOR ITEM PRCHG VALUES TO THE ERROR RECORD.        04830001
      ***************************************************************** 04840001
       2400-MOVE-SOR-VALUES.                                            04850001
                                                                        04860001
           MOVE PE104-PRCHG-ID              TO DL1-PRCHG-ID.            04871002
           MOVE PE104-STR-GP-TYP-CDE        TO DL1-STR-GP-TYP-CDE.      04872002
           MOVE PE104-STR-GP-ID             TO DL1-STR-GP-ID.           04873002
                                                                        04890001
      ******************************************************************04900001
      * 2500-MOVE-XM-VALUES.                                            04910001
      *     MOVES THE XM PRCHG VALUES TO THE ERROR RECORD.              04920001
      ***************************************************************** 04930001
       2500-MOVE-XM-VALUES.                                             04940001
                                                                        04950001
           MOVE XM011-PRCHG-ID              TO DL1-PRCHG-ID.            04961002
           MOVE XM011-STR-GP-TYP-CDE        TO DL1-STR-GP-TYP-CDE.      04962002
           MOVE XM011-STR-GP-ID             TO DL1-STR-GP-ID.           04963002
                                                                        04980001
      ***************************************************************** 04990001
      *7000-READ-SOR-STR-GP-PRCHG-EXT.                                  05000002
      *  READ SOR STR-GP-PRCHG EXTRACT FILE                             05010002
      ***************************************************************** 05020001
       7000-READ-SOR-STR-GP-PRCHG-EXT.                                  05030002
                                                                        05040001
           IF  PS-NOT-END-OF-SOR-STR-GP-PRCHG                           05050002
               READ SOR-STR-GP-PRCHG-EXT-FILE                           05060002
                  INTO PE104-RECORD                                     05070001
                    AT END                                              05080001
                       SET PS-END-OF-SOR-STR-GP-PRCHG TO TRUE           05090002
                       MOVE HIGH-VALUES TO PV-PEKEY-PE104               05090130
                    NOT AT END                                          05091026
                       ADD +1 TO PV-SOR-STR-GP-PRCHG-RECS-READ          05092026
                       MOVE PE104-PRCHG-ID  TO PV-PEKEY-PRCHG-ID        05092130
                       MOVE PE104-STR-GP-TYP-CDE                        05092230
                                            TO PV-PEKEY-STR-GP-TYP-CDE  05092330
                       MOVE PE104-STR-GP-ID TO PV-PEKEY-STR-GP-ID       05092430
               END-READ                                                 05100001
           END-IF.                                                      05110001
                                                                        05120001
      ***************************************************************** 05190001
      *7100-READ-XM-STR-GP-PRCHG-EXT.                                   05200002
      * READ XM EXTRACT FILE                                            05210001
      ***************************************************************** 05220001
       7100-READ-XM-STR-GP-PRCHG-EXT.                                   05230002
                                                                        05240001
           IF  PS-NOT-END-OF-XM-STR-GP-PRCHG                            05250002
               READ XM-STR-GP-PRCHG-EXT-FILE                            05260002
                 INTO XM011-STR-GP-PRCHG-UNLOAD                         05270002
                    AT END                                              05280026
                       SET PS-END-OF-XM-STR-GP-PRCHG TO TRUE            05290026
                       MOVE HIGH-VALUES TO PV-XMKEY-XM011               05290130
                    NOT AT END                                          05291026
                       ADD +1 TO PV-XM-EXTRACT-REC-READ-CNT             05292026
                       MOVE XM011-PRCHG-ID  TO PV-XMKEY-PRCHG-ID        05292130
                       MOVE XM011-STR-GP-TYP-CDE                        05294030
                                            TO PV-XMKEY-STR-GP-TYP-CDE  05294130
                       MOVE XM011-STR-GP-ID TO PV-XMKEY-STR-GP-ID       05295030
               END-READ                                                 05300001
           END-IF.                                                      05310001
                                                                        05320001
      ******************************************************************05390001
      * 8700-REPORT-HEADER-ROUTINE.                                     05400001
      *     PRINT THE HEADINGS FOR THE REPORT.                          05410001
      ******************************************************************05420001
       8700-REPORT-HEADER-ROUTINE.                                      05430001
                                                                        05440001
           MOVE ZEROS                  TO PV-LINE-CNT.                  05450001
           ADD +1                      TO PV-PAGE-CNT.                  05460001
           MOVE PV-PAGE-CNT            TO DP200-PAGE-NUMBER.            05470001
                                                                        05480001
           MOVE PC-REPORT-NBR-1        TO DP200-REPORT-NUMBER.          05490001
           MOVE DP200-STANDRD-HEADER-200-COLS                           05500001
                                       TO PV-PRINT-RECORD.              05510001
                                                                        05520001
           PERFORM 8800-WRITE-RPT-TOP-OF-PAGE.                          05530001
                                                                        05540001
           MOVE HL2-HEADING-LINE-2     TO PV-PRINT-RECORD.              05550001
           MOVE +2                     TO PV-LINE-SPACING.              05560001
           PERFORM 8900-WRITE-REPORT-LINE.                              05570001
                                                                        05580001
           MOVE HL3-HEADING-LINE-3     TO PV-PRINT-RECORD.              05590001
           MOVE +3                     TO PV-LINE-SPACING.              05600001
           PERFORM 8900-WRITE-REPORT-LINE.                              05610001
                                                                        05620001
           MOVE HL4-HEADING-LINE-4     TO PV-PRINT-RECORD.              05630001
           MOVE +1                     TO PV-LINE-SPACING.              05640001
           PERFORM 8900-WRITE-REPORT-LINE.                              05650001
                                                                        05660001
      ******************************************************************05670001
      * 8800-WRITE-RPT-TOP-OF-PAGE                                      05680001
      *     WRITE FIRST REPORT LINE AFTER PAGE BREAK.                   05690001
      ******************************************************************05700001
       8800-WRITE-RPT-TOP-OF-PAGE.                                      05710001
                                                                        05720001
           WRITE XM-STR-GP-PRCHG-INTEG-RPT-REC                          05730003
             FROM PV-PRINT-RECORD                                       05740001
               AFTER ADVANCING PAGE.                                    05750001
                                                                        05760001
           MOVE +1 TO PV-LINE-CNT.                                      05770001
                                                                        05780001
      ******************************************************************05790001
      * 8900-WRITE-REPORT-LINE                                          05800001
      *     WRITE REPORT LINE AFTER ADVANCING LINE SPACING.             05810001
      ******************************************************************05820001
       8900-WRITE-REPORT-LINE.                                          05830001
                                                                        05840001
           PERFORM 2300-CHECK-IF-NEW-PG-NEEDED.                         05850001
                                                                        05860001
           WRITE XM-STR-GP-PRCHG-INTEG-RPT-REC                          05870003
             FROM PV-PRINT-RECORD                                       05880001
               AFTER ADVANCING PV-LINE-SPACING.                         05890001
                                                                        05900001
           ADD PV-LINE-SPACING TO PV-LINE-CNT.                          05910001
                                                                        05920001
      ****************************************************************  05930001
      *END OF PROGRAM ROUTINE                                           05940001
      ****************************************************************  05950001
       9000-END-OF-PGM.                                                 05960001
                                                                        05970001
           DISPLAY '******** XMKRP011 COUNTS *********'.                05980001
           DISPLAY ' INTEGRITY CHECK - FOR XMT_STR-GP-PRCHG '.          05990002
           DISPLAY '   SOR REC READ CNT= '                              06000001
                           PV-SOR-STR-GP-PRCHG-RECS-READ.               06010004
           DISPLAY 'XM STR-GP-PRCHG READ COUNT= '                       06020002
                                           PV-XM-EXTRACT-REC-READ-CNT.  06021002
           DISPLAY '  =============================== '                 06030001
           DISPLAY 'UNIQUE TO SOR ONLY  - ',                            06040002
                    PV-SOR-STR-GP-PRCHG-RECS-UNIQU                      06041004
           DISPLAY 'UNIQUE TO XM  ONLY  - ', PV-XM-EXTRACT-REC-UNIQUE   06050001
           DISPLAY 'COMMON TO BOTH FILES- ',  PV-SOR-XM-COMMON-CNT      06060001
           COMPUTE PV-SOR-XM-TOTAL-CNT =  PV-SOR-XM-COMMON-CNT          06070001
            + PV-SOR-STR-GP-PRCHG-RECS-UNIQU  + PV-XM-EXTRACT-REC-UNIQUE06080004
           DISPLAY 'TOTAL COMMON + UNIQUE=', PV-SOR-XM-TOTAL-CNT        06090001
           DISPLAY '  =============================== '                 06100001
           DISPLAY ' ERROR MSGS WRITTEN= ' PV-TOTAL-ERROR-CNT.          06110001
           DISPLAY ' TOTAL RECORDS ERROR= ' PV-ERROR-RECORD-WRITTEN-CNT.06120001
           DISPLAY '  VALID MATCH COUNT= ' PV-VALID-MATCH-CNT.          06130001
           DISPLAY '                              '.                    06140001
           DISPLAY '**********************************'.                06150001
                                                                        06160001
           IF  PV-ERROR-RECORD-WRITTEN-CNT > 0                          06170001
               MOVE 04               TO RETURN-CODE                     06180001
           END-IF.                                                      06190001
                                                                        06200001
           CLOSE SOR-STR-GP-PRCHG-EXT-FILE                              06210002
                 XM-STR-GP-PRCHG-EXT-FILE                               06220002
                 XM-STR-GP-PRCHG-INTEG-RPT-FILE.                        06230002
                                                                        06240001
