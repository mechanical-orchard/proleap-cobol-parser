000100 TITLE  'SHIP DOOR TEMPLATE UPDATE'.                                      
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    DLKCS406.                                                 
000400 AUTHOR.        DENNIS STEFFEN.                                           
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN.  DECEMBER 2003.                                            
000700 DATE-COMPILED.                                                           
000800                                                                          
263627*----------------------------------------------------------------*        
263627* CHANGED 11/04/2021    BY JIM HUNTER      WR: CHG0263627                 
263627* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263627* MAKING THE CALL DYNAMIC VS STATIC.                                      
      *                                                                         
009461* CHANGED 10/24/2022    BY BOB MCASEY      WR: CHG0279137                 
009491* ADD COPYBOOK DPWS930 CHANGE CICS ARCHITECTURE CALL TO                   
009491* CHANGE CICS ARCHITECTURE CALL TO USE DP930-CICS-ARCH-API                
263627*----------------------------------------------------------------*        
000900 ENVIRONMENT DIVISION.                                                    
001000 DATA DIVISION.                                                           
001100 WORKING-STORAGE SECTION.                                                 
001200                                                                          
001300 01  DK-DB2-KEYS.                                                         
001400     05  DK-DC-NBR                    PIC S9(04) COMP VALUE ZERO.         
001500                                                                          
001600                                                                          
001700 01  PC-PROGRAM-CONSTANTS.                                                
001800     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
001900                                                    'DLK406  '.           
002000     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
002100                                                    'DLKM406 '.           
002200     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
002300                                                    'DLKCS406'.           
002400     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
002500     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
002600     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
002700     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +65             
002800                                                    COMP SYNC.            
002900     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
003000                                                    COMP SYNC.            
003100     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
003200                                                    +1000000              
003300                                                    COMP-3.               
003400     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
003500                                                    COMP SYNC.            
003600     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
003700                                                    COMP-3.               
003800     05  PC-PERCENT                     PIC  X(01)  VALUE '%'.            
003900     05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
004000     05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.          
004100     05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.          
004200     05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
004300*                                                                         
004400     05  PC-TSYMSG-NUMBERS.                                               
004500         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
004600         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
004700         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
004800         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
004900         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
005000         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
005100         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
005200         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
005300         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
005400         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
005500         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
005600         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
005700         10  PC-TSYMSG-01305            PIC  9(05)  VALUE 01305.          
005800         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
005900         10  PC-TSYMSG-00984            PIC  9(05)  VALUE 00984.          
006000         10  PC-TSYMSG-03181            PIC  9(05)  VALUE 03181.          
006100         10  PC-TSYMSG-03182            PIC  9(05)  VALUE 03182.          
006200         10  PC-TSYMSG-03183            PIC  9(05)  VALUE 03183.          
006300         10  PC-TSYMSG-03185            PIC  9(05)  VALUE 03185.          
006400         10  PC-TSYMSG-03186            PIC  9(05)  VALUE 03186.          
006500         10  PC-TSYMSG-03187            PIC  9(05)  VALUE 03187.          
006600         10  PC-TSYMSG-03199            PIC  9(05)  VALUE 03199.          
006700         10  PC-TSYMSG-03201            PIC  9(05)  VALUE 03201.          
006800         10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
006900*                                                                         
007000 01  PS-PROGRAM-SWITCHES.                                                 
007100     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
007200         88  PS-NO-ERROR                            VALUE 'Y'.            
007300         88  PS-ERROR                               VALUE 'N'.            
007400     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
007500         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
007600         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
007700     05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.            
007800         88  PS-DONE                                VALUE 'Y'.            
007900         88  PS-NOT-DONE                            VALUE 'N'.            
008000     05  PS-ACCESS-SW                   PIC  X(01)  VALUE 'N'.            
008100         88  PS-ACCESS-ALLOWED                      VALUE 'Y'.            
008200         88  PS-ACCESS-NOT-ALLOWED                  VALUE 'N'.            
008300     05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.           
008400         88  PS-LIST-END                             VALUE 'Y'.           
008500         88  PS-NO-LIST-END                          VALUE 'N'.           
008600                                                                          
008700     05  PS-SOFT-ABORT-ERROR-SW         PIC  X(01)  VALUE 'N'.            
008800         88  PS-SOFT-ABORT-ERROR                    VALUE 'Y'.            
008900         88  PS-NO-SOFT-ABORT-ERROR                 VALUE 'N'.            
009000*                                                                         
009100 01  PV-PROGRAM-VARIABLES.                                                
009200     05  PV-SUB                         PIC S9(04)  VALUE +0              
009300                                                    COMP SYNC.            
009400     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
009500                                                    COMP SYNC.            
009600     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
009700                                                    COMP-3.               
009800     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
009900                                                    COMP-3.               
010000     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
010100                                                    COMP-3.               
010200     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
010300                                                    COMP-3.               
010400     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
010500                                                    COMP-3.               
010600     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
010700                                                    COMP-3.               
010800     05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
010900                                                    COMP-3.               
011000     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
011100                                                    COMP SYNC.            
011200     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
011300                                                    COMP SYNC.            
011400     05  PV-ITEM                        PIC S9(04)  VALUE +0              
011500                                                    COMP SYNC.            
011600     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
011700                                                    COMP SYNC.            
011800     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
011900                                                    COMP SYNC.            
012000     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
012100                                                    COMP SYNC.            
012200     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
012300                                                    COMP SYNC.            
012400     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
012500                                                    COMP SYNC.            
012600     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
012700                                                    COMP SYNC.            
012800     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
012900                                                    COMP SYNC.            
013000     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
013100                                                    COMP SYNC.            
013200     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
013300                                                    COMP SYNC.            
013400     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
013500                                                    COMP SYNC.            
013600     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
013700                                                    COMP SYNC.            
013800     05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
013900                                                    COMP SYNC.            
014000     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
014100                                                    COMP SYNC.            
014200                                                                          
014300                                                                          
014400     05  DN-NULL-IND                  PIC S9(04) COMP.                    
014500     05  PV-TEMPLATE-STATUS           PIC X(08) VALUE SPACE.              
014600     05  PV-LENGTH                    PIC S9(05) COMP-3.                  
014700     05  PV-END                       PIC S9(05) COMP-3.                  
014800     05  PV-TEMPLATE-DOOR-COUNT       PIC S9(05) COMP-3.                  
014900     05  PV-DOOR-COUNT                PIC S9(05) COMP-3.                  
015000     05  PV-GROUP-COUNT               PIC S9(05) COMP-3.                  
015100     05  PV-COMMAREA-LENGTH           PIC S9(04) COMP VALUE ZERO.         
015200     05  PV-SOFT-ABORT-MSG.                                               
015300         10  FILLER            PIC X(12) VALUE ' DLKCS406 - '.            
015400         10  FILLER            PIC X(09) VALUE 'SQLCODE: '.               
015500         10  PV-SOFT-SQLCODE   PIC ZZZZZZZZ9-.                            
015600         10  FILLER            PIC X(12) VALUE ' PARAGRAPH: '.            
015700         10  PV-SOFT-PARAGRAPH PIC X(30).                                 
015800     05  PV-CICS-MSG-AREA2            PIC  X(100) VALUE SPACE.            
015900     05  PT-DESC-AREA.                                                    
016000         10  PT-DESC-BYTE OCCURS 40 TIMES PIC X(01).                      
016100                                                                          
016200******************************************************************        
016300**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
016400**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
016500**  APPLICATION-SPECIFIC COMMAREA.                              **        
016600******************************************************************        
016700 01  LQW-LIST-QUEUE-WORK-AREA.                                            
016800     05  LQW-BLOCK-MODIFIED-SW        PIC  X(01)  VALUE SPACE.            
016900         88  LQW-BLOCK-NOT-MODIFIED      VALUE 'N'.                       
017000         88  LQW-BLOCK-MODIFIED          VALUE 'Y'.                       
017100                                                                          
017200     05  LQW-ITEM-ENTRIES             OCCURS 65 TIMES                     
017300                                      INDEXED BY LQW-IDX.                 
017400         10  LQW-LIST-ITEM-NUMBER     PIC S9(09)  COMP-3.                 
017500                                                                          
017600         10  LQW-ERROR-SW             PIC X(01).                          
017700             88  LQW-ERROR-YES                  VALUE 'Y'.                
017800             88  LQW-ERROR-NO                   VALUE SPACE.              
017900                                                                          
018000         10  LQW-NUMERIC-ERROR-SW     PIC X(01).                          
018100             88  LQW-NUMERIC-ERROR-YES           VALUE 'Y'.               
018200             88  LQW-NUMERIC-ERROR-NO            VALUE SPACE.             
018300                                                                          
018400         10  LQW-GROUP-CHANGED-SW     PIC X(01).                          
018500             88  LQW-GROUP-CHANGED-YES           VALUE 'Y'.               
018600             88  LQW-GROUP-CHANGED-NO            VALUE SPACE.             
018700                                                                          
018800         10  LQW-DOOR-ID              PIC  X(04).                         
018900         10  LQW-GROUP-NBR            PIC  X(04).                         
019000         10  LQW-GROUP-NBR-N REDEFINES LQW-GROUP-NBR                      
019100                                      PIC 9999.                           
019200/                                                                         
019300*---------------------------------------------------------------*         
019400*  RECORD LAYOUT FOR THE MESSAGE SELECTION TEMP STORAGE QUEUE.  *         
019500*---------------------------------------------------------------*         
019600                                                                          
019700     COPY DLWS006.                                                        
019800/                                                                         
019900*---------------------------------------------------------------*         
020000*    MAP LAYOUT                                                 *         
020100*---------------------------------------------------------------*         
020200                                                                          
020300     COPY DLKM406.                                                        
020400*                                                                         
020500 01  MR-OCCURS-LAYOUT                   REDEFINES  DLK406O.               
020600     05  FILLER                         PIC X(172).                       
020700     05  MR-SCROLL-AREA                 OCCURS 65 TIMES.                  
020800                                                                          
020900         10  MR-DOOR-NBR-L            PIC S9(04) COMP.                    
021000         10  MR-DOOR-NBRA             PIC  X(01).                         
021100         10  MR-DOOR-NBRC             PIC  X(01).                         
021200         10  MR-DOOR-NBRH             PIC  X(01).                         
021300         10  MR-DOOR-NBR              PIC  X(04).                         
021400                                                                          
021500         10  MR-GROUP-NBR-L           PIC S9(04) COMP.                    
021600         10  MR-GROUP-NBRA            PIC  X(01).                         
021700         10  MR-GROUP-NBRC            PIC  X(01).                         
021800         10  MR-GROUP-NBRH            PIC  X(01).                         
021900         10  MR-GROUP-NBR.                                                
022000             15  MR-GROUP-NBR-N       PIC Z(04).                          
022100                                                                          
022200/                                                                         
022300     COPY DPWS015.                                                        
022400     COPY DPWS016.                                                        
022500                                                                          
022600*---------------------------------------------------------------*         
022700*    NUMERIC EDIT LAYOUT.                                       *         
022800*---------------------------------------------------------------*         
022900                                                                          
023000     COPY DPWS010I.                                                       
023100                                                                          
023200*---------------------------------------------------------------*         
023300*    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   *         
023400*---------------------------------------------------------------*         
023500                                                                          
023600     COPY DPWS030.                                                        
1022RM     COPY DPWS930.                                                        
023700                                                                          
023800*---------------------------------------------------------------*         
023900*    STANDARD COMMAREA.                                         *         
024000*---------------------------------------------------------------*         
024100                                                                          
024200     COPY DPWS020.                                                        
024300     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
024400                                                                          
024500         10  INTER-APPL-COMM-AREA       PIC X(200).                       
024600     COPY DLWS002.                                                        
024700*---------------------------------------------------------------*         
024800*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
024900*---------------------------------------------------------------*         
025000         10  ASC-SPECIFIC-COMMAREA.                                       
025100             15  ASC-LIST-KEYS.                                           
025200                 20  ASC-KEY-OPER-UNT-ID                                  
025300                                      PIC S9(04) COMP.                    
025400                 20  ASC-KEY-OPER-DESC                                    
025500                                      PIC X(40).                          
025600                 20  ASC-KEY-TEMPLATE-DESC                                
025700                                      PIC X(40).                          
025800                 20  ASC-KEY-DOOR-COUNT                                   
025900                                      PIC S9(04) COMP.                    
026000                 20 ASC-DOOR-GP-TMPL-ID                                   
026100                                      PIC  S9(9) COMP.                    
026200                                                                          
026300             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
026400                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
026500                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
026600                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
026700                                                                          
026800                 20  ASC-ITEM-ARRAY.                                      
026900                     25  ASC-ITEM-ENTRIES OCCURS 65 TIMES.                
027000                         30  ASC-LIST-ITEM-NUMBER                         
027100                                                     PIC S9(09)           
027200                                                     COMP-3.              
027300                         30  ASC-ERROR-SW            PIC X(01).           
027400                             88   ASC-ERROR-YES      VALUE 'Y'.           
027500                             88   ASC-ERROR-NO       VALUE SPACE.         
027600                                                                          
027700                         30  ASC-NUMERIC-ERROR-SW     PIC X(01).          
027800                             88  ASC-NUMERIC-ERROR-YES VALUE 'Y'.         
027900                             88  ASC-NUMERIC-ERROR-NO VALUE SPACE.        
028000                                                                          
028100                         30  ASC-GROUP-CHANGED-SW     PIC X(01).          
028200                             88  ASC-GROUP-CHANGED-YES VALUE 'Y'.         
028300                             88  ASC-GROUP-CHANGED-NO VALUE SPACE.        
028400                                                                          
028500                         30  ASC-DOOR-ID             PIC  X(04).          
028600                         30  ASC-GROUP-NBR-X.                             
028700                             35 ASC-GROUP-NBR        PIC  9(04).          
028800                                                                          
028900             15  ASC-BLK-SUB                         PIC S9(04)           
029000                                                          COMP            
029100                                                          SYNC.           
029200             15  ASC-ITEM-SUB                        PIC S9(04)           
029300                                                          COMP            
029400                                                          SYNC.           
029500             15  ASC-SEL-SUB                         PIC S9(04)           
029600                                                         COMP.            
029700             15  ASC-SEL-ITEM                        PIC S9(04)           
029800                                                         COMP.            
029900             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
030000                                                          COMP-3.         
030100             15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(09)           
030200                                                          COMP-3.         
030300             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
030400                                                          COMP-3.         
030500             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
030600                                                          COMP-3.         
030700             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
030800                                                          COMP-3.         
030900             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
031000                                                          COMP-3.         
031100             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
031200                                                          COMP            
031300                                                          SYNC.           
031400             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
031500                                                          COMP-3.         
031600             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
031700                                                          COMP-3.         
031800             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
031900                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
032000                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
032100             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
032200                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
032300                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
032400             15  ASC-LIST-QUEUE-NAME.                                     
032500                 20  ASC-LIST-TERM-ID                PIC  X(04).          
032600                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
032700                                                          COMP-3.         
032800                 20  ASC-LIST-SEQ                    PIC  9(01).          
032900             15  ASC-SEL-QUEUE-NAME.                                      
033000                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
033100                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
033200                                                          COMP-3.         
033300                 20  ASC-SEL-SEQ                     PIC  9(01).          
033400                                                                          
033500             15  ASC-CURSOR-SW                       PIC  X(01).          
033600                 88  ASC-DOOR-SEQUENCE               VALUE 'D'.           
033700                 88  ASC-GROUP-SEQUENCE              VALUE 'G'.           
033800             15  ASC-START-ITEM-REQUEST.                                  
033900                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
034000             15  ASC-PROTECT-ALL-FIELDS-SW           PIC X.               
034100                 88  ASC-PROTECT-YES         VALUE 'Y'.                   
034200                 88  ASC-PROTECT-NO          VALUE 'N'.                   
034300/                                                                         
034400*---------------------------------------------------------------*         
034500*    ABEND PROCESSING COPYBOOK.                                 *         
034600*---------------------------------------------------------------*         
034700                                                                          
034800     COPY DPWS013.                                                        
034900*----------------------------------------------------------------*        
035000**  SECURITY SUBROUTINE PARAMETERS                                        
035100*----------------------------------------------------------------*        
035200     COPY DPWS008.                                                        
035300                                                                          
035400/                                                                         
035500*    DLT_DOOR_GP_TMPL                                                     
035600     EXEC SQL                                                             
035700          INCLUDE DLT00000                                                
035800     END-EXEC.                                                            
035900                                                                          
036000*    DLT_DOOR_GP                                                          
036100     EXEC SQL                                                             
036200          INCLUDE DLT00001                                                
036300     END-EXEC.                                                            
036400                                                                          
036500*    DC DOOR MASTER                                                       
036600     EXEC SQL                                                             
036700          INCLUDE TCNDCDR                                                 
036800     END-EXEC.                                                            
036900                                                                          
037000*    XLT_LOC                                                              
037100     EXEC SQL                                                             
037200          INCLUDE XLT00010                                                
037300     END-EXEC.                                                            
037400                                                                          
037500*    DB2 AREA FOR COMMUNICATIONS                                *         
037600     EXEC SQL                                                             
037700          INCLUDE SQLCA                                                   
037800     END-EXEC.                                                            
037900/                                                                         
038000*    DOOR TEMPLATE CURSOR                                                 
038100     EXEC SQL                                                             
038200         DECLARE DOOR-CSR CURSOR FOR                                      
038300         SELECT                                                           
038400            T1.DOOR                                                       
038500         ,  T2.GROUP_ID                                                   
038600                                                                          
038700         FROM                                                             
038800                                                                          
038900         (  SELECT                                                        
039000               DR.DOOR_ID AS DOOR                                         
039100         FROM                                                             
039200            TCNDCDR        DR                                             
039300           WHERE DR.OPER_UNT_ID = :ASC-KEY-OPER-UNT-ID                    
039400           AND DR.DOOR_TYP_CDE IN ('SH', 'DS', 'ST')                      
039500           ) AS T1                                                        
039600                                                                          
039700          LEFT OUTER JOIN                                                 
039800                                                                          
039900            ( SELECT                                                      
040000              GP.DOOR_ID     AS DOOR_ID                                   
040100            , GP.DOOR_GP_NBR AS GROUP_ID                                  
040200              FROM DLT_DOOR_GP GP                                         
040300              WHERE                                                       
040400               GP.DOOR_GP_TMPL_ID = :ASC-DOOR-GP-TMPL-ID                  
040500               ) AS T2                                                    
040600            ON T1.DOOR = T2.DOOR_ID                                       
040700           ORDER BY                                                       
040800              T1.DOOR                                                     
040900     END-EXEC.                                                            
041000                                                                          
041100*    GROUP TEMPLATE CURSOR                                                
041200     EXEC SQL                                                             
041300         DECLARE GROUP-CSR CURSOR FOR                                     
041400         SELECT                                                           
041500            T1.DOOR                                                       
041600         ,  T2.GROUP_ID                                                   
041700                                                                          
041800         FROM                                                             
041900                                                                          
042000         (  SELECT                                                        
042100               DR.DOOR_ID AS DOOR                                         
042200         FROM                                                             
042300            TCNDCDR        DR                                             
042400           WHERE DR.OPER_UNT_ID = :ASC-KEY-OPER-UNT-ID                    
042500           AND DR.DOOR_TYP_CDE IN ('SH', 'DS', 'ST')                      
042600           ) AS T1                                                        
042700                                                                          
042800          LEFT OUTER JOIN                                                 
042900                                                                          
043000            ( SELECT                                                      
043100              GP.DOOR_ID     AS DOOR_ID                                   
043200            , GP.DOOR_GP_NBR AS GROUP_ID                                  
043300              FROM DLT_DOOR_GP GP                                         
043400              WHERE                                                       
043500               GP.DOOR_GP_TMPL_ID = :ASC-DOOR-GP-TMPL-ID                  
043600               ) AS T2                                                    
043700            ON T1.DOOR = T2.DOOR_ID                                       
043800           ORDER BY                                                       
043900              T2.GROUP_ID                                                 
044000     END-EXEC.                                                            
044100/                                                                         
044200 LINKAGE SECTION.                                                         
044300                                                                          
044400 01  DFHCOMMAREA.                                                         
044500     05  FILLER                         OCCURS  1 TO 4072 TIMES           
044600                                        DEPENDING ON EIBCALEN.            
044700         10  FILLER                     PIC X.                            
044800                                                                          
044900/                                                                         
045000 PROCEDURE DIVISION.                                                      
045100                                                                          
045200 0000-MAIN-MODULE.                                                        
045300     INITIALIZE DP030-CICS-API-FIELDS.                                    
045400     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
045500     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
045600     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
045700     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
045800     MOVE LENGTH OF DLK406I        TO DP030-MAP-LENGTH (1).               
045900     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
046000     PERFORM 0001-CALL-CICS-ARCH-API.                                     
046100*                                                                         
046200     PERFORM 1000-CONTROL-PROCESSING                                      
046300*                                                                         
046400     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
046500     PERFORM 0001-CALL-CICS-ARCH-API.                                     
046600                                                                          
046700******************************************************************        
046800**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
046900**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
047000******************************************************************        
047100 0001-CALL-CICS-ARCH-API.                                                 
047200     CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
047300                                    DFHCOMMAREA                           
047400                                    DP030-CICS-API-FIELDS                 
047500                                    DP020-STANDARD-COMMAREA               
047600                                    DLK406I.                              
047700*                                                                         
047800     IF  DP030-RC-CALL-SUCCESSFUL                                         
047900         CONTINUE                                                         
048000     ELSE                                                                 
048100         SET DP013-NO-ROLLBACK                                            
048200             DP013-XCTL-DISPLAY-RESTART                                   
048300             DP013-CICS-ABEND      TO TRUE                                
048400         MOVE '0001-CALL-CICS-ARCH-API'                                   
048500                                   TO DP013-PARAGRAPH                     
1022RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1022RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
048800         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
048900         PERFORM DP013-0000-PROCESS-ABEND                                 
049000     END-IF.                                                              
049100                                                                          
049200 1000-CONTROL-PROCESSING.                                                 
049300                                                                          
049400     EVALUATE TRUE                                                        
049500         WHEN DP020-NEXT-ACT-INITIAL                                      
049600             INITIALIZE ASC-SPECIFIC-COMMAREA                             
049700             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
049800                                      ASC-SEL-TERM-ID                     
049900             MOVE DP020-SRC-TASK-NUMBER                                   
050000                                   TO ASC-LIST-TASK-NUMBER                
050100                                      ASC-SEL-TASK-NUMBER                 
050200             MOVE PC-LIST-QUEUE-SEQ                                       
050300                                   TO ASC-LIST-SEQ                        
050400             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
050500             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
050600             IF  PS-COMMAREA-NOT-VALID                                    
050700                 SET DP020-NEXT-ACT-APPL-ERROR                            
050800                                   TO TRUE                                
050900             ELSE                                                         
051000                 SET ASC-DOOR-SEQUENCE TO TRUE                            
051100                 PERFORM 4000-BUILD-INITIAL-PANEL                         
051200             END-IF                                                       
051300                                                                          
051400*            MOVE -1                  TO ASTRTITL                         
051500             SET DP030-SET-CURSOR-APPL-1                                  
051600                                      TO TRUE                             
124500             MOVE -1                  TO MR-GROUP-NBR-L (1)               
051700                                                                          
051800         WHEN DP020-NEXT-ACT-READ-MAP                                     
051900             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
052000                                   TO PV-TOP-ITEM                         
052100             PERFORM 2000-PROCESS-PANEL                                   
052200                                                                          
052300         WHEN DP020-NEXT-ACT-RETURN                                       
052400             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
052500                                   TO PV-TOP-ITEM                         
052600             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
052700             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
052800             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
052900*            PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
053000                                                                          
053100         WHEN OTHER                                                       
053200             SET DP013-LOGIC-ABEND TO TRUE                                
053300             SET DP013-NO-ROLLBACK TO TRUE                                
053400             MOVE '1000-CONTROL-PROCESSING'                               
053500                                   TO DP013-PARAGRAPH                     
053600             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
053700                                   TO DP013-MESSAGE-TEXT(1)               
053800             PERFORM DP013-0000-PROCESS-ABEND                             
053900     END-EVALUATE.                                                        
054000                                                                          
054100/                                                                         
054200 1100-PROCESS-INTER-APPL-COMM.                                            
054300                                                                          
054400     SET PS-COMMAREA-VALID         TO TRUE.                               
054500     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
054600                                                                          
054700     MOVE DP020-LOCATION-NBR          TO DK-DC-NBR                        
054800                                         ADCO                             
054900                                         ASC-KEY-OPER-UNT-ID.             
055000                                                                          
055100     PERFORM 7000-LOCATION-DESC.                                          
055200     MOVE ASC-KEY-OPER-DESC           TO ADCNMEO.                         
055300                                                                          
055400     MOVE DL002-DOOR-COUNT            TO ASC-KEY-DOOR-COUNT               
055500                                         ATDOORO.                         
055600                                                                          
055700     MOVE DL002-NUMBER-OF-TS-ITEMS    TO ASC-NUMBER-OF-TS-ITEMS           
055800     MOVE DL002-SEL-QUEUE-NAME        TO ASC-SEL-QUEUE-NAME               
055900     PERFORM 1120-PREPARE-LIST-ENTRY.                                     
056000                                                                          
056100 1120-PREPARE-LIST-ENTRY.                                                 
056200                                                                          
056300     MOVE DL006-MAX-ITEMS             TO ASC-SEL-SUB.                     
056400     MOVE ZERO                        TO ASC-SEL-ITEM.                    
056500     PERFORM 2140-GET-NEXT-MESSAGE.                                       
056600/                                                                         
056700                                                                          
056800 2000-PROCESS-PANEL.                                                      
056900     EVALUATE TRUE                                                        
057000         WHEN DP020-SRC-AID = DP016-CLEAR                                 
057100             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
057200*            PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
057300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
057400                                                                          
057500         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
057600             MOVE ASC-KEY-OPER-UNT-ID   TO DK-DC-NBR                      
057700             MOVE ASC-KEY-TEMPLATE-DESC TO ADESCO                         
057800             PERFORM 2050-DELETE-LIST-QUEUE                               
057900             PERFORM 4000-BUILD-INITIAL-PANEL                             
058000                                                                          
058100*            MOVE -1                  TO ASTRTITL                         
124500             MOVE -1                  TO MR-GROUP-NBR-L (1)               
058200             SET DP030-SET-CURSOR-APPL-1                                  
058300                                      TO TRUE                             
058400                                                                          
058500         WHEN OTHER                                                       
058600             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
058700*            PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
058800             IF  PS-NO-ERROR                                              
058900                 SET DP030-CONTINUE-GO                                    
059000                                 TO TRUE                                  
059100                 PERFORM 2100-CHECK-FUNCTION-KEY                          
059200             ELSE                                                         
059300                 SET DP030-OVERRIDE-GO                                    
059400                                 TO TRUE                                  
059500             END-IF                                                       
059600     END-EVALUATE.                                                        
059700/                                                                         
059800 2001-NUM-EDIT.                                                           
059900     MOVE ASC-GROUP-NBR-X (ASC-BLK-SUB ASC-ITEM-SUB)                      
060000                                      TO DP010I-UNEDITED-FIELD            
060100     MOVE +4                          TO DP010I-MAXIMUM-DIGITS            
060200     MOVE ZERO                        TO DP010I-MAXIMUM-DECIMALS          
060300     SET DP010I-NEGATIVE-NOT-ALLOWED  TO TRUE                             
060400                                                                          
263627     CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
263627          USING DP010I-NUMERIC-EDIT-AREA                                  
060600                                                                          
060700     IF NOT DP010I-ERROR-DETECTED                                         
060800         MOVE DP010I-NUMERIC-FIELD    TO ASC-GROUP-NBR                    
060900                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
061000     END-IF.                                                              
061100                                                                          
061200*----------------------------------------------------------------*        
061300*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
061400*----------------------------------------------------------------*        
061500 2050-DELETE-LIST-QUEUE.                                                  
061600                                                                          
061700     EXEC CICS                                                            
061800         DELETEQ TS                                                       
061900             QUEUE (ASC-LIST-QUEUE-NAME)                                  
062000             RESP  (PV-CICS-RESP)                                         
062100     END-EXEC.                                                            
062200                                                                          
062300     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
062400        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
062500         CONTINUE                                                         
062600     ELSE                                                                 
062700         SET DP013-CICS-ABEND      TO TRUE                                
062800         SET DP013-NO-ROLLBACK     TO TRUE                                
062900                                                                          
063000         MOVE '2050-DELETE-LIST-QUEUE'                                    
063100                                   TO DP013-PARAGRAPH                     
063200         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
063300                                   TO DP013-MESSAGE-TEXT (1)              
063400         PERFORM DP013-0000-PROCESS-ABEND                                 
063500     END-IF.                                                              
063600                                                                          
063700     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
063800/                                                                         
063900*----------------------------------------------------------------*        
064000* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
064100* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
064200* CICS ARCHITECTURE API.                                         *        
064300*----------------------------------------------------------------*        
064400 2100-CHECK-FUNCTION-KEY.                                                 
064500     EVALUATE TRUE                                                        
064600         WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
064700*            (UPDATE DOOR GROUPS)                                         
064800                                                                          
064900             PERFORM 2100A-FUNC01-EDITS                                   
065000             IF PS-NO-ERROR                                               
065100                 PERFORM 2100B-DO-FUNC01                                  
065200             END-IF                                                       
065300             IF PS-NO-ERROR                                               
065400                 MOVE PC-TSYMSG-01305 TO DP020-MSG-NUMBER                 
065500                 MOVE -1              TO ASTRTITL                         
065600             END-IF                                                       
065700                                                                          
065800         WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                      
065900*            (TOGGLE LIST SEQUENCE)                                       
066000             PERFORM 2050-DELETE-LIST-QUEUE                               
066100             IF ASC-DOOR-SEQUENCE                                         
066200                 SET ASC-GROUP-SEQUENCE   TO TRUE                         
066300             ELSE                                                         
066400                 SET ASC-DOOR-SEQUENCE   TO TRUE                          
066500             END-IF                                                       
066600             PERFORM 4000-BUILD-INITIAL-PANEL                             
066700             MOVE -1                  TO ASTRTITL                         
066800                                                                          
066900     WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                         
067000         PERFORM 2140-GET-NEXT-MESSAGE                                    
067100         IF PS-LIST-END                                                   
067200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
067300             SET PS-NO-LIST-END TO TRUE                                   
067400         ELSE                                                             
067500             PERFORM 2050-DELETE-LIST-QUEUE                               
067600             PERFORM 4000-BUILD-INITIAL-PANEL                             
067700         END-IF                                                           
067800                                                                          
067900     WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                         
068000         PERFORM 2150-GET-PREV-MESSAGE                                    
068100         IF PS-LIST-END                                                   
068200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
068300             SET PS-NO-LIST-END TO TRUE                                   
068400         ELSE                                                             
068500             PERFORM 2050-DELETE-LIST-QUEUE                               
068600             PERFORM 4000-BUILD-INITIAL-PANEL                             
068700         END-IF                                                           
068800         PERFORM 3200-RESET-ATTRIBUTES                                    
068900                                                                          
069000         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
069100             MOVE +1               TO PV-TOP-ITEM                         
069200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
069300             MOVE -1                  TO ASTRTITL                         
069400                                                                          
069500         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
069600             PERFORM 2110-BROWSE-BACKWARD                                 
069700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
069800             MOVE -1                  TO ASTRTITL                         
069900                                                                          
070000         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
070100             PERFORM 2120-BROWSE-FORWARD                                  
070200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
070300             MOVE -1                  TO ASTRTITL                         
070400                                                                          
070500         WHEN DP020-SRC-AID = DP016-ENTER                                 
070600             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
070700             IF  PS-ERROR                                                 
070800                 CONTINUE                                                 
070900             ELSE                                                         
071000                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
124500                 MOVE -1              TO MR-GROUP-NBR-L (1)               
071100             END-IF                                                       
071200                                                                          
071300         WHEN OTHER                                                       
071400             SET DP013-NO-ROLLBACK                                        
071500                 DP013-XCTL-DISPLAY-RESTART                               
071600                 DP013-CICS-ABEND  TO TRUE                                
071700             MOVE '2100-CHECK-FUNCTION-KEY'                               
071800                                   TO DP013-PARAGRAPH                     
071900             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
072000                                   TO DP013-MESSAGE-TEXT (1)              
072100             PERFORM DP013-0000-PROCESS-ABEND                             
072200     END-EVALUATE.                                                        
072300                                                                          
072400     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
072500     IF ASC-DOOR-SEQUENCE                                                 
072600         MOVE '** DOOR SEQUENCE **'   TO ASEQO                            
072700     ELSE                                                                 
072800         MOVE '** GROUP SEQUENCE **'   TO ASEQO                           
072900     END-IF                                                               
073000     MOVE ASC-DOOR-GP-TMPL-ID         TO ATMPLO                           
073100     MOVE ASC-KEY-TEMPLATE-DESC       TO ADESCO.                          
073200*    MOVE -1                          TO ASTRTITL.                        
073300     SET DP030-SET-CURSOR-APPL-1      TO TRUE.                            
073400/                                                                         
073500 2100A-FUNC01-EDITS.                                                      
073600                                                                          
073700     IF ASC-KEY-TEMPLATE-DESC = SPACES OR LOW-VALUES                      
073800         SET PS-ERROR                                                     
073900             DP020-MSG-FATAL                                              
074000             DP030-SET-CURSOR-APPL-1                                      
074100             DP030-OVERRIDE-GO       TO TRUE                              
074200         MOVE -1                  TO ADESCL                               
074300*              'MUST KEY TEMPLATE DESCRIPTION AND                         
074400*                           ASSIGN ALL DOORS TO GROUP'                    
074500         MOVE PC-TSYMSG-03182 TO DP020-MSG-NUMBER                         
074600     END-IF                                                               
074700                                                                          
074800     PERFORM 4600-EDIT-EACH-GROUP                                         
074900             VARYING PV-ITEM  FROM 1 BY 1                                 
075000             UNTIL PV-ITEM > ASC-HIGHEST-BLOCK-WRITTEN                    
075100*            OR PS-ERROR                                                  
075200                                                                          
075300     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
075400/                                                                         
075500 2100B-DO-FUNC01.                                                         
075600     MOVE LENGTH OF ASC-KEY-TEMPLATE-DESC TO PV-LENGTH                    
075700                                             PV-END                       
075800     MOVE ASC-KEY-TEMPLATE-DESC       TO                                  
075900                                DLT00000-DOOR-GP-TMPL-DESC-TEX            
076000                                PT-DESC-AREA                              
076100                                                                          
076200     PERFORM                                                              
076300             UNTIL PT-DESC-BYTE (PV-LENGTH)                               
076400                                   NOT = SPACE                            
076500                OR PV-LENGTH < ZERO                                       
076600             SUBTRACT 1 FROM PV-LENGTH                                    
076700      END-PERFORM                                                         
076800                                                                          
076900     IF PV-LENGTH < ZERO                                                  
077000         MOVE ZERO      TO      DLT00000-DOOR-GP-TMPL-DESC-LEN            
077100     ELSE                                                                 
077200         MOVE PV-LENGTH TO      DLT00000-DOOR-GP-TMPL-DESC-LEN            
077300     END-IF                                                               
077400                                                                          
077500     MOVE ASC-DOOR-GP-TMPL-ID         TO DLT00000-DOOR-GP-TMPL-ID         
077600     MOVE DP020-LOCATION-NBR          TO DLT00000-DC-NBR                  
077700     MOVE DP020-USERID                TO DLT00000-CRE-BY-USR-ID           
077800                                                                          
077900     PERFORM 7300-UPDATE-TEMPLATE-HDR.                                    
078000                                                                          
078100     PERFORM 4601-CREATE-GROUPS                                           
078200             VARYING PV-ITEM  FROM 1 BY 1                                 
078300             UNTIL PV-ITEM > ASC-HIGHEST-BLOCK-WRITTEN.                   
078400*            OR PS-ERROR                                                  
078500/                                                                         
078600 4600-EDIT-EACH-GROUP.                                                    
078700                                                                          
078800     MOVE +1                       TO ASC-BLK-SUB.                        
078900     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
079000     ADD +1                        TO ASC-BLK-SUB.                        
079100     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
079200                                                                          
079300     PERFORM 6112-READ-LIST-QUEUE.                                        
079400                                                                          
079500     SET  LQW-IDX  TO   1.                                                
079600                                                                          
079700     PERFORM 6112-READ-LIST-QUEUE.                                        
079800                                                                          
079900     IF PV-CICS-RESP = DFHRESP (NORMAL)                                   
080000         PERFORM                                                          
080100         VARYING LQW-IDX  FROM 1 BY 1                                     
080200         UNTIL   LQW-IDX > PC-ITEMS-PER-PANEL                             
080300            OR   LQW-LIST-ITEM-NUMBER (LQW-IDX) = ZERO                    
080400*           OR   PS-ERROR                                                 
080500                 SET LQW-BLOCK-MODIFIED                                   
080600                                      TO TRUE                             
080700                 SET LQW-ERROR-NO  (LQW-IDX)                              
080800                     LQW-NUMERIC-ERROR-NO  (LQW-IDX) TO TRUE              
080900                 MOVE LQW-GROUP-NBR (LQW-IDX)                             
081000                                      TO DP010I-UNEDITED-FIELD            
081100                 MOVE +4              TO DP010I-MAXIMUM-DIGITS            
081200                 MOVE ZERO            TO DP010I-MAXIMUM-DECIMALS          
081300                 SET DP010I-NEGATIVE-NOT-ALLOWED                          
081400                                      TO TRUE                             
081500                                                                          
263627                 CALL DP010I-NUMERIC-EDIT-ROUTINE                         
263627                      USING DP010I-NUMERIC-EDIT-AREA                      
081700                                                                          
081800                 IF NOT DP010I-ERROR-DETECTED                             
081900                     MOVE DP010I-NUMERIC-FIELD                            
082000                                      TO LQW-GROUP-NBR-N (LQW-IDX)        
082100                     IF LQW-GROUP-NBR-N (LQW-IDX) = ZERO                  
082200                         SET PS-ERROR                                     
082300                             DP020-MSG-FATAL                              
082400                             DP030-SET-CURSOR-APPL-1                      
082500                             DP030-OVERRIDE-GO       TO TRUE              
082600                         SET LQW-ERROR-YES (LQW-IDX) TO TRUE              
082700*                           'MUST KEY TEMPLATE DESCRIPTION AND            
082800*                                 ASSIGN ALL DOORS TO GROUP'              
082900                         MOVE PC-TSYMSG-03182 TO DP020-MSG-NUMBER         
083000                     END-IF                                               
083100                 ELSE                                                     
083200                     SET PS-ERROR                                         
083300                         DP020-MSG-FATAL                                  
083400                         DP030-SET-CURSOR-APPL-1                          
083500                         DP030-OVERRIDE-GO       TO TRUE                  
083600                     SET LQW-NUMERIC-ERROR-YES (LQW-IDX) TO TRUE          
083700*                           'GROUP NUMBERS MUST BE NUMERIC'               
083800                         MOVE PC-TSYMSG-03187 TO DP020-MSG-NUMBER         
083900                 END-IF                                                   
084000         END-PERFORM                                                      
084100     END-IF                                                               
084200                                                                          
084300     MOVE PV-ITEM      TO PV-SAVE-ITEM.                                   
084400     PERFORM 6111-WRITE-LIST-QUEUE.                                       
084500                                                                          
084600     MOVE +1                     TO  ASC-BLK-SUB.                         
084700     PERFORM 4410-READ-TEMP-STORAGE.                                      
084800                                                                          
084900     ADD +1                      TO  ASC-BLK-SUB                          
085000                                     PV-BLOCK-NUMBER.                     
085100                                                                          
085200     PERFORM 4410-READ-TEMP-STORAGE.                                      
085300                                                                          
085400 4600A-CHECK-DOOR-TOTALS.                                                 
085500                                                                          
085600     PERFORM 7010-COUNT-TEMPLATE-DOORS                                    
085700                                                                          
085800     IF PV-TEMPLATE-DOOR-COUNT = ASC-KEY-DOOR-COUNT                       
085900         CONTINUE                                                         
086000     ELSE                                                                 
086100         SET PS-ERROR                                                     
086200             DP020-MSG-FATAL                                              
086300             DP030-SET-CURSOR-APPL-1                                      
086400             DP030-OVERRIDE-GO       TO TRUE                              
086500*                'TEMPLATE MUST BE UPDATED - NOT ALL                      
086600*                      DOORS ARE GROUPED IN TEMPLATE'                     
086700             MOVE PC-TSYMSG-03181 TO DP020-MSG-NUMBER                     
086800     END-IF.                                                              
086900                                                                          
087000 4601-CREATE-GROUPS.                                                      
087100                                                                          
087200     MOVE +1                       TO ASC-BLK-SUB.                        
087300     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
087400     ADD +1                        TO ASC-BLK-SUB.                        
087500     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
087600                                                                          
087700     PERFORM 6112-READ-LIST-QUEUE.                                        
087800                                                                          
087900     SET  LQW-IDX  TO   1.                                                
088000                                                                          
088100     PERFORM 6112-READ-LIST-QUEUE.                                        
088200                                                                          
088300     IF PV-CICS-RESP = DFHRESP (NORMAL)                                   
088400         PERFORM                                                          
088500         VARYING LQW-IDX  FROM 1 BY 1                                     
088600         UNTIL   LQW-IDX > PC-ITEMS-PER-PANEL                             
088700            OR   LQW-LIST-ITEM-NUMBER (LQW-IDX) = ZERO                    
088800            OR   PS-ERROR                                                 
088900                                                                          
089000                 IF LQW-GROUP-CHANGED-YES (LQW-IDX)                       
089100                     MOVE DLT00000-DOOR-GP-TMPL-ID                        
089200                                      TO DLT00001-DOOR-GP-TMPL-ID         
089300                                         ASC-DOOR-GP-TMPL-ID              
089400                     MOVE LQW-DOOR-ID  (LQW-IDX)                          
089500                                      TO DLT00001-DOOR-ID                 
089600                     MOVE LQW-GROUP-NBR-N (LQW-IDX)                       
089700                                      TO DLT00001-DOOR-GP-NBR             
089800                     PERFORM 7350-UPDATE-DOOR-GRP                         
089900                 END-IF                                                   
090000         END-PERFORM                                                      
090100     END-IF                                                               
090200                                                                          
090300     MOVE PV-ITEM      TO PV-SAVE-ITEM.                                   
090400     PERFORM 6111-WRITE-LIST-QUEUE.                                       
090500                                                                          
090600     MOVE +1                     TO  ASC-BLK-SUB.                         
090700     PERFORM 4410-READ-TEMP-STORAGE.                                      
090800                                                                          
090900     ADD +1                      TO  ASC-BLK-SUB                          
091000                                     PV-BLOCK-NUMBER.                     
091100                                                                          
091200     PERFORM 4410-READ-TEMP-STORAGE.                                      
091300                                                                          
091400/                                                                         
091500 2140-GET-NEXT-MESSAGE.                                                   
091600                                                                          
091700     ADD +1                       TO ASC-SEL-SUB.                         
091800     IF  ASC-SEL-SUB > DL006-MAX-ITEMS                                    
091900         ADD +1                   TO ASC-SEL-ITEM                         
092000         MOVE +1                  TO ASC-SEL-SUB                          
092100     END-IF.                                                              
092200                                                                          
092300     IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
092400         PERFORM 9100-READ-SEL-QUEUE                                      
092500         IF  DL006-ITEM (ASC-SEL-SUB) > ZERO                              
092600             MOVE DL006-GP-TMPL-ID (ASC-SEL-SUB)                          
092700                                      TO DLT00001-DOOR-GP-TMPL-ID         
092800                                         ASC-DOOR-GP-TMPL-ID              
092900             MOVE DL006-TEMPLATE-DESC (ASC-SEL-SUB)                       
093000                                      TO ASC-KEY-TEMPLATE-DESC            
093100             MOVE DL006-DOOR-COUNT (ASC-SEL-SUB)                          
093200                                      TO ASC-KEY-DOOR-COUNT               
093300                                         ATDOORO                          
093400             SET DL006-INQ (ASC-SEL-SUB) TO TRUE                          
093500             PERFORM 9200-REWRITE-SEL-QUEUE                               
093600         ELSE                                                             
093700             SUBTRACT +1 FROM ASC-SEL-SUB                                 
093800             MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                     
093900             SET DP020-MSG-INFORMATIONAL TO TRUE                          
094000             SET PS-LIST-END             TO TRUE                          
094100         END-IF                                                           
094200     ELSE                                                                 
094300         SUBTRACT +1 FROM ASC-SEL-ITEM                                    
094400         MOVE DL006-MAX-ITEMS     TO ASC-SEL-SUB                          
094500         MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER                     
094600         SET DP020-MSG-INFORMATIONAL TO TRUE                              
094700         SET PS-LIST-END             TO TRUE                              
094800     END-IF.                                                              
094900                                                                          
095000*----------------------------------------------------------------*        
095100*    GET THE PREVIOUS MESSAGE IF A LIST OF MESSAGES IS BEING     *        
095200*    PROCESSED.                                                  *        
095300*----------------------------------------------------------------*        
095400                                                                          
095500 2150-GET-PREV-MESSAGE.                                                   
095600                                                                          
095700     SUBTRACT +1 FROM ASC-SEL-SUB.                                        
095800     IF  ASC-SEL-SUB < +1                                                 
095900         SUBTRACT +1 FROM ASC-SEL-ITEM                                    
096000         MOVE DL006-MAX-ITEMS     TO ASC-SEL-SUB                          
096100     END-IF.                                                              
096200                                                                          
096300     IF  ASC-SEL-ITEM > ZERO                                              
096400         PERFORM 9100-READ-SEL-QUEUE                                      
096500         MOVE DL006-GP-TMPL-ID (ASC-SEL-SUB)                              
096600                                      TO DLT00001-DOOR-GP-TMPL-ID         
096700                                         ASC-DOOR-GP-TMPL-ID              
096800         MOVE DL006-TEMPLATE-DESC (ASC-SEL-SUB)                           
096900                                      TO ASC-KEY-TEMPLATE-DESC            
097000         MOVE DL006-DOOR-COUNT (ASC-SEL-SUB)                              
097100                                      TO ASC-KEY-DOOR-COUNT               
097200                                         ATDOORO                          
097300                                                                          
097400     ELSE                                                                 
097500         MOVE +1                  TO ASC-SEL-ITEM                         
097600                                     ASC-SEL-SUB                          
097700         MOVE PC-TSYMSG-00069     TO DP020-MSG-NUMBER                     
097800         SET DP020-MSG-INFORMATIONAL TO TRUE                              
097900         SET PS-LIST-END             TO TRUE                              
098000     END-IF.                                                              
098100/                                                                         
098200*                                                                         
098300*----------------------------------------------------------------*        
098400*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
098500*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
098600*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
098700*    MESSAGE.                                                    *        
098800*----------------------------------------------------------------*        
098900                                                                          
099000 2110-BROWSE-BACKWARD.                                                    
099100     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
099200         GIVING PV-TOP-ITEM.                                              
099300*                                                                         
099400     IF  ((PV-TOP-ITEM < +1)                                              
099500       OR (PV-TOP-ITEM = +1))                                             
099600         MOVE +1                   TO PV-TOP-ITEM                         
099700*        -- TOP OF LIST --                                                
099800         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
099900         SET DP020-MSG-INFORMATIONAL TO TRUE                              
100000     END-IF.                                                              
100100                                                                          
100200/                                                                         
100300*----------------------------------------------------------------*        
100400*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
100500*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
100600*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
100700*----------------------------------------------------------------*        
100800                                                                          
100900 2120-BROWSE-FORWARD.                                                     
101000                                                                          
101100     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
101200         GIVING PV-TOP-ITEM.                                              
101300     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
101400         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
101500                                   TO PV-TOP-ITEM                         
101600*        -- BOTTOM OF LIST --                                             
101700         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
101800         SET DP020-MSG-INFORMATIONAL                                      
101900                                   TO TRUE                                
102000     ELSE                                                                 
102100         COMPUTE PV-BOTTOM-ITEM =                                         
102200             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
102300         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
102400*            -- BOTTOM OF LIST --                                         
102500             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
102600             SET DP020-MSG-INFORMATIONAL                                  
102700                                   TO TRUE                                
102800         END-IF                                                           
102900     END-IF.                                                              
103000 EJECT                                                                    
103100*----------------------------------------------------------------*        
103200*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
103300*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
103400*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
103500*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
103600*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
103700*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
103800*----------------------------------------------------------------*        
103900                                                                          
104000 2130-CHECK-FOR-JUMP-BROWSE.                                              
104100     SET PS-NO-ERROR               TO TRUE.                               
104200     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
104300         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
104400                                   TO PV-TOP-ITEM                         
104500     ELSE                                                                 
104600         MOVE ASC-START-ITEM-REQUEST                                      
104700                                   TO DP010I-UNEDITED-FIELD               
104800         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
104900         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
105000         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
105100                                   TO TRUE                                
263627         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263627              USING DP010I-NUMERIC-EDIT-AREA                              
105300                                                                          
105400         IF  NOT DP010I-ERROR-DETECTED                                    
105500             MOVE DP010I-NUMERIC-FIELD                                    
105600                                   TO  ASC-START-ITEM-REQUEST-N           
105700             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
105800                 MOVE -1              TO ASTRTITL                         
105900                 IF  ASC-START-ITEM-REQUEST-N >                           
106000                                          ASC-NUMBER-OF-ITEMS-READ        
106100                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
106200                     COMPUTE PV-TOP-ITEM =                                
106300                             ASC-NUMBER-OF-ITEMS-READ                     
106400                             - PC-ITEMS-PER-PANEL + 1                     
106500                     IF  PV-TOP-ITEM < +1                                 
106600                         MOVE +1   TO PV-TOP-ITEM                         
106700                         MOVE PC-TSYMSG-00070                             
106800                                   TO DP020-MSG-NUMBER                    
106900                         SET DP020-MSG-INFORMATIONAL                      
107000                                   TO TRUE                                
107100                     END-IF                                               
107200                 ELSE                                                     
107300                     MOVE ASC-START-ITEM-REQUEST-N                        
107400                                   TO PV-TOP-ITEM                         
107500                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
107600                     COMPUTE PV-BOTTOM-ITEM =                             
107700                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
107800                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
107900*                        -- BOTTOM OF LIST --                             
108000                         MOVE PC-TSYMSG-00070                             
108100                                   TO DP020-MSG-NUMBER                    
108200                         SET DP020-MSG-INFORMATIONAL                      
108300                                   TO TRUE                                
108400                     END-IF                                               
108500                 END-IF                                                   
108600             ELSE                                                         
108700*                --- VALUE OUT OF RANGE ----                              
108800                 MOVE PC-TSYMSG-00101                                     
108900                                   TO DP020-MSG-NUMBER                    
109000                 MOVE -1           TO ASTRTITL                            
109100                 SET DP030-SET-CURSOR-APPL-1                              
109200                                   TO TRUE                                
109300                 MOVE DP015-REVERSE                                       
109400                                   TO ASTRTITH                            
109500                 MOVE DP015-RED    TO ASTRTITC                            
109600                 MOVE DP015-UNP-NUM-BRT-MDT                               
109700                                   TO ASTRTITA                            
109800                 SET DP020-MSG-FATAL                                      
109900                                   TO TRUE                                
110000                 SET PS-ERROR      TO TRUE                                
110100             END-IF                                                       
110200                                                                          
110300         ELSE                                                             
110400             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
110500             MOVE -1               TO ASTRTITL                            
110600             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
110700             MOVE DP015-REVERSE    TO ASTRTITH                            
110800             MOVE DP015-RED        TO ASTRTITC                            
110900             MOVE DP015-UNP-NUM-BRT-MDT                                   
111000                                   TO ASTRTITA                            
111100             SET DP020-MSG-FATAL   TO TRUE                                
111200             SET PS-ERROR          TO TRUE                                
111300         END-IF                                                           
111400     END-IF.                                                              
111500 EJECT                                                                    
111600******************************************************************        
111700** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
111800** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
111900******************************************************************        
112000 2200-MOVE-SCREEN-TO-COMMAREA.                                            
112100                                                                          
112200     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
112300     MOVE +1                       TO PV-SUB.                             
112400*                                                                         
112500     PERFORM 3100-SET-SUBSCRIPTS.                                         
112600     IF  ASTRTITL > ZERO                                                  
112700         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
112800     ELSE                                                                 
112900         IF  ASTRTITF = DP015-ERASE-EOF                                   
113000             INITIALIZE ASC-START-ITEM-REQUEST                            
113100         END-IF                                                           
113200     END-IF.                                                              
113300                                                                          
113400     IF  ADESCL > ZERO                                                    
113500         MOVE ADESCI           TO ASC-KEY-TEMPLATE-DESC                   
113600     ELSE                                                                 
113700         IF  ADESCF = DP015-ERASE-EOF                                     
113800             INITIALIZE ASC-KEY-TEMPLATE-DESC                             
113900         END-IF                                                           
114000     END-IF.                                                              
114100                                                                          
114200     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
114300         VARYING PV-SUB                                                   
114400         FROM 1 BY 1                                                      
114500         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
114600                                                                          
114700 2210-MOVE-LINES-TO-COMMAREA.                                             
114800                                                                          
114900     IF  MR-GROUP-NBR-L (PV-SUB) > ZERO                                   
115000         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB)                             
115100                                      TO TRUE                             
115200         SET ASC-GROUP-CHANGED-YES (ASC-BLK-SUB, ASC-ITEM-SUB)            
115300                                      TO TRUE                             
115400         MOVE MR-GROUP-NBR (PV-SUB)                                       
115500                                   TO ASC-GROUP-NBR-X                     
115600                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
115700     ELSE                                                                 
115800         IF  MR-GROUP-NBRA (PV-SUB) = DP015-ERASE-EOF                     
115900             SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB)                         
116000                                      TO TRUE                             
116100             SET ASC-GROUP-CHANGED-YES (ASC-BLK-SUB, ASC-ITEM-SUB)        
116200                                      TO TRUE                             
116300             MOVE SPACE               TO ASC-GROUP-NBR-X                  
116400                                     (ASC-BLK-SUB, ASC-ITEM-SUB)          
116500         END-IF                                                           
116600     END-IF.                                                              
116700                                                                          
116800                                                                          
116900     ADD +1                        TO ASC-ITEM-SUB.                       
117000                                                                          
117100     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
117200         ADD +1                    TO ASC-BLK-SUB                         
117300         MOVE +1                   TO ASC-ITEM-SUB                        
117400     END-IF.                                                              
117500/                                                                         
117600*----------------------------------------------------------------*        
117700*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
117800*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
117900*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
118000*                                                                *        
118100*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
118200*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
118300*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
118400*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
118500*----------------------------------------------------------------*        
118600                                                                          
118700 3100-SET-SUBSCRIPTS.                                                     
118800                                                                          
118900     COMPUTE ASC-ITEM-SUB =                                               
119000         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
119100                                                                          
119200     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
119300         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
119400                                                                          
119500         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
119600     ELSE                                                                 
119700         MOVE +1                     TO ASC-BLK-SUB                       
119800     END-IF.                                                              
119900******************************************************************        
120000** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
120100******************************************************************        
120200                                                                          
120300 3200-RESET-ATTRIBUTES.                                                   
120400     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
120500     MOVE DP015-GREEN              TO ASTRTITC.                           
120600     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
120700                                                                          
120800     PERFORM                                                              
120900         VARYING PV-SUB                                                   
121000         FROM 1 BY 1                                                      
121100         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
121200         MOVE DP015-UNP-NUM-NOR-OFF                                       
121300                                   TO MR-GROUP-NBRA (PV-SUB)              
121400         MOVE DP015-GREEN          TO MR-GROUP-NBRC (PV-SUB)              
121500         MOVE DP015-UNDERLINE      TO MR-GROUP-NBRH (PV-SUB)              
121600     END-PERFORM.                                                         
121700******************************************************************        
121800** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
121900** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
122000******************************************************************        
122100 3400-CHECK-RANGE-SEQUENCE.                                               
122200     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
122300       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
122400       AND  ASC-START-OF-SELECTED-RANGE >                                 
122500                                         ASC-END-OF-SELECTED-RANGE        
122600*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
122700         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
122800         SET DP020-MSG-FATAL TO TRUE                                      
122900         SET PS-ERROR TO TRUE                                             
123000         PERFORM 3410-POSITION-AT-ERROR                                   
123100     END-IF.                                                              
123200******************************************************************        
123300** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
123400** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
123500** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
123600******************************************************************        
123700 3410-POSITION-AT-ERROR.                                                  
123800     SET DP030-SET-CURSOR-APPL-1                                          
123900                                   TO TRUE.                               
124000     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
124100                    - PV-TOP-ITEM + 1.                                    
124200     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
124300         CONTINUE                                                         
124400     ELSE                                                                 
124500         MOVE -1                   TO MR-GROUP-NBR-L (PV-SUB)             
124600         MOVE DP015-REVERSE        TO MR-GROUP-NBRH (PV-SUB)              
124700         MOVE DP015-RED            TO MR-GROUP-NBRC (PV-SUB)              
124800     END-IF.                                                              
124900*                                                                         
125000     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
125100                    - PV-TOP-ITEM + 1.                                    
125200     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
125300         CONTINUE                                                         
125400     ELSE                                                                 
125500         MOVE -1                   TO MR-GROUP-NBR-L (PV-SUB)             
125600         MOVE DP015-REVERSE        TO MR-GROUP-NBRH (PV-SUB)              
125700         MOVE DP015-RED            TO MR-GROUP-NBRC (PV-SUB)              
125800     END-IF.                                                              
125900/                                                                         
126000*----------------------------------------------------------------*        
126100*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
126200*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
126300*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
126400*    DISPLAY THE PANEL TO USER.                                  *        
126500*----------------------------------------------------------------*        
126600                                                                          
126700 4000-BUILD-INITIAL-PANEL.                                                
126800                                                                          
126900     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
127000                                 ASC-BLOCK-ENTRIES (2).                   
127100     MOVE +1                 TO ASC-BLK-SUB.                              
127200     SET ASC-ITEMS-LEFT-ON-DB                                             
127300                             TO TRUE.                                     
127400                                                                          
127500     MOVE ZERO               TO ASC-ITEM-SUB                              
127600                                ASC-NUMBER-OF-ITEMS-READ                  
127700                                ASC-NUMBER-OF-SELECTED-ITEMS              
127800                                ASC-HIGHEST-BLOCK-WRITTEN                 
127900                                ASC-START-OF-SELECTED-RANGE               
128000                                ASC-END-OF-SELECTED-RANGE                 
128100                                ASC-ITEM-AT-TOP-OF-PANEL.                 
128200*    PERFORM 4100-PREPARE-CURSOR                                          
128300                                                                          
128400     PERFORM 4010-OPEN-CURSOR.                                            
128500                                                                          
128600     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
128700                                                                          
128800     PERFORM 4020-CLOSE-CURSOR.                                           
128900                                                                          
129000     MOVE +1                   TO  PV-TOP-ITEM.                           
129100     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
129200         MOVE -1                      TO ASTRTITO                         
129300         MOVE ASC-KEY-OPER-UNT-ID     TO ADCO                             
129400         MOVE ASC-KEY-OPER-DESC       TO ADCNMEO                          
129500*        PERFORM VARYING PV-SUB                                           
129600*                FROM PC-ITEMS-PER-PANEL BY -1                            
129700*                UNTIL PV-SUB < +1                                        
129800*            MOVE DP015-ASK-DRK-OFF  TO MR-GROUP-NBRA (PV-SUB)            
129900*                                       MR-DOOR-NBRA (PV-SUB)             
130000*        END-PERFORM                                                      
130100         SET DP020-MSG-FATAL                                              
130200             DP030-SET-CURSOR-APPL-1                                      
130300             DP030-OVERRIDE-APPL-ERROR TO TRUE                            
130400         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
130500     ELSE                                                                 
130600         MOVE -1                      TO ASTRTITO                         
130700         SET DP030-SET-CURSOR-APPL-1                                      
130800                               TO TRUE                                    
130900         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
131000     END-IF.                                                              
131100/                                                                         
131200 4010-OPEN-CURSOR.                                                        
131300                                                                          
131400     IF ASC-DOOR-SEQUENCE                                                 
131500         PERFORM 7100-OPEN-DOOR-CSR                                       
131600     ELSE                                                                 
131700         PERFORM 7200-OPEN-GROUP-CSR                                      
131800     END-IF.                                                              
131900/                                                                         
132000 4020-CLOSE-CURSOR.                                                       
132100     IF ASC-DOOR-SEQUENCE                                                 
132200         PERFORM 7120-CLOSE-DOOR-CSR                                      
132300     ELSE                                                                 
132400         PERFORM 7220-CLOSE-GROUP-CSR                                     
132500     END-IF.                                                              
132600/                                                                         
132700*----------------------------------------------------------------*        
132800*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
132900*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
133000*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
133100*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
133200*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
133300*                                                                *        
133400*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
133500*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
133600*    UPON INITIALIZATION.                                        *        
133700*----------------------------------------------------------------*        
133800                                                                          
133900 4300-READ-ITEMS-FROM-DB.                                                 
134000                                                                          
134100     PERFORM                                                              
134200         VARYING PV-READ-COUNT                                            
134300         FROM 1 BY 1                                                      
134400         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
134500            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
134600                                                                          
134700         PERFORM 4310-FETCH                                               
134800                                                                          
134900         IF  ASC-ITEMS-LEFT-ON-DB                                         
135000             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
135100                 ADD +1            TO ASC-ITEM-SUB                        
135200             ELSE                                                         
135300                 ADD +1            TO ASC-BLK-SUB                         
135400                 MOVE +1           TO ASC-ITEM-SUB                        
135500                                                                          
135600                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
135700                     MOVE +1       TO ASC-BLK-SUB                         
135800                     SET ASC-BLOCK-MODIFIED (1)                           
135900                                   TO TRUE                                
136000                     PERFORM 5100-WRITE-TEMP-STORAGE                      
136100                     MOVE ASC-BLOCK-ENTRIES                               
136200                               (PC-MAX-BLOCKS-IN-ARRAY)                   
136300                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
136400                     ADD +1        TO ASC-BLK-SUB                         
136500                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
136600                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
136700                                   TO TRUE                                
136800                 END-IF                                                   
136900             END-IF                                                       
137000             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
137100             MOVE ASC-NUMBER-OF-ITEMS-READ                                
137200                                   TO ASC-LIST-ITEM-NUMBER                
137300                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
137400*            LOAD CURSOR FIELDS TO ASC                                    
137500             MOVE DLT00001-DOOR-ID    TO ASC-DOOR-ID                      
137600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
137700             MOVE DLT00001-DOOR-GP-NBR TO ASC-GROUP-NBR                   
137800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
137900         END-IF                                                           
138000     END-PERFORM.                                                         
138100                                                                          
138200     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
138300         MOVE +1               TO ASC-BLK-SUB                             
138400         PERFORM 5100-WRITE-TEMP-STORAGE                                  
138500         ADD +1                TO ASC-BLK-SUB                             
138600         PERFORM 5100-WRITE-TEMP-STORAGE                                  
138700                                                                          
138800         MOVE 1                TO ASC-BLK-SUB                             
138900                                  PV-BLOCK-NUMBER                         
139000         PERFORM 4410-READ-TEMP-STORAGE                                   
139100         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
139200             MOVE 2            TO ASC-BLK-SUB                             
139300                                  PV-BLOCK-NUMBER                         
139400             PERFORM 4410-READ-TEMP-STORAGE                               
139500         END-IF                                                           
139600     END-IF.                                                              
139700/                                                                         
139800 4310-FETCH.                                                              
139900                                                                          
140000     IF ASC-DOOR-SEQUENCE                                                 
140100         PERFORM 7110-FETCH-DOOR-CSR                                      
140200     ELSE                                                                 
140300         PERFORM 7210-FETCH-GROUP-CSR                                     
140400     END-IF.                                                              
140500/                                                                         
140600*----------------------------------------------------------------*        
140700* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
140800*----------------------------------------------------------------*        
140900                                                                          
141000 4400-MOVE-COMMAREA-TO-SCREEN.                                            
141100     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
141200         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
141300         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
141400             MOVE +1    TO ASC-BLK-SUB                                    
141500             PERFORM 5100-WRITE-TEMP-STORAGE                              
141600             ADD +1     TO ASC-BLK-SUB                                    
141700             PERFORM 5100-WRITE-TEMP-STORAGE                              
141800             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
141900                 GIVING PV-BLOCK-NUMBER                                   
142000                 REMAINDER PV-REMAINDER                                   
142100             IF  PV-REMAINDER > ZERO                                      
142200                 ADD +1 TO PV-BLOCK-NUMBER                                
142300             END-IF                                                       
142400             MOVE +1    TO ASC-BLK-SUB                                    
142500             PERFORM 4410-READ-TEMP-STORAGE                               
142600             ADD +1     TO ASC-BLK-SUB                                    
142700                           PV-BLOCK-NUMBER                                
142800             PERFORM 4410-READ-TEMP-STORAGE                               
142900     END-IF.                                                              
143000     MOVE +1            TO ASC-BLK-SUB.                                   
143100     COMPUTE ASC-ITEM-SUB =                                               
143200         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
143300         + PC-ITEMS-PER-PANEL).                                           
143400*                                                                         
143500     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
143600         ADD +1         TO ASC-BLK-SUB                                    
143700         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
143800     END-IF.                                                              
143900*                                                                         
144000     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
144100         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
144200     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
144300     PERFORM 4420-FORMAT-LIST-LINES                                       
144400         VARYING PV-SUB                                                   
144500         FROM PC-ITEMS-PER-PANEL BY -1                                    
144600         UNTIL PV-SUB < +1.                                               
144700                                                                          
144800     COMPUTE ASC-ITEMS-DISPLAYED =                                        
144900         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
145000     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
145100                                      ASTRTITO.                           
145200     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
145300     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
145400                                                                          
145500*    HEADINGS                                                             
145600                                                                          
145700     MOVE ASC-KEY-OPER-UNT-ID         TO ADCO                             
145800     MOVE ASC-KEY-OPER-DESC           TO ADCNMEO.                         
145900     MOVE ASC-KEY-DOOR-COUNT          TO ATDOORO.                         
146000                                                                          
146100     IF ASC-DOOR-SEQUENCE                                                 
146200         MOVE '** DOOR SEQUENCE **'   TO ASEQO                            
146300     ELSE                                                                 
146400         MOVE '** GROUP SEQUENCE **'   TO ASEQO                           
146500     END-IF                                                               
146600     MOVE ASC-DOOR-GP-TMPL-ID         TO ATMPLO.                          
146700     MOVE ASC-KEY-TEMPLATE-DESC       TO ADESCO.                          
146800*                                                                         
146900     IF  DP020-MSG-NUMBER-X = SPACE                                       
147000         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
147100*            --- MORE ITEMS TO DISPLAY ----                               
147200             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
147300             SET DP020-MSG-INFORMATIONAL TO TRUE                          
147400         END-IF                                                           
147500     END-IF.                                                              
147600*----------------------------------------------------------------*        
147700*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
147800*----------------------------------------------------------------*        
147900                                                                          
148000 4410-READ-TEMP-STORAGE.                                                  
148100                                                                          
148200     EXEC CICS                                                            
148300         READQ TS                                                         
148400             QUEUE (ASC-LIST-QUEUE-NAME)                                  
148500             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
148600             ITEM  (PV-BLOCK-NUMBER)                                      
148700             RESP  (PV-CICS-RESP)                                         
148800     END-EXEC.                                                            
148900                                                                          
149000     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
149100       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
149200         CONTINUE                                                         
149300     ELSE                                                                 
149400         SET DP013-CICS-ABEND     TO TRUE                                 
149500         SET DP013-NO-ROLLBACK    TO TRUE                                 
149600         MOVE '4410-READ-TEMP-STORAGE'                                    
149700                                  TO DP013-PARAGRAPH                      
149800         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
149900                                  TO DP013-MESSAGE-TEXT(1)                
150000         PERFORM DP013-0000-PROCESS-ABEND                                 
150100     END-IF.                                                              
150200                                                                          
150300     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
150400*                                                                         
150500     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
150600         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
150700     END-IF.                                                              
150800 EJECT                                                                    
150900*----------------------------------------------------------------*        
151000*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
151100*----------------------------------------------------------------*        
151200                                                                          
151300 4420-FORMAT-LIST-LINES.                                                  
151400     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
151500         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
151600         MOVE DP015-ASK-DRK-OFF  TO MR-GROUP-NBRA (PV-SUB)                
151700                                    MR-DOOR-NBRA (PV-SUB)                 
151800         MOVE DP015-HL-OFF       TO MR-GROUP-NBRH (PV-SUB)                
151900                                    MR-DOOR-NBRH (PV-SUB)                 
152000         MOVE SPACE              TO MR-GROUP-NBR (PV-SUB)                 
152100                                    MR-DOOR-NBR (PV-SUB)                  
152200     ELSE                                                                 
152300                                                                          
152400         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
152500             MOVE ASC-DOOR-ID (ASC-BLK-SUB ASC-ITEM-SUB)                  
152600                                      TO MR-DOOR-NBR (PV-SUB)             
152700             IF ASC-GROUP-NBR (ASC-BLK-SUB ASC-ITEM-SUB)                  
152800                     NUMERIC                                              
152900                 MOVE ASC-GROUP-NBR (ASC-BLK-SUB ASC-ITEM-SUB)            
153000                                      TO MR-GROUP-NBR-N (PV-SUB)          
153100             ELSE                                                         
153200                 MOVE ASC-GROUP-NBR-X (ASC-BLK-SUB ASC-ITEM-SUB)          
153300                                      TO MR-GROUP-NBR (PV-SUB)            
153400             END-IF                                                       
153500                                                                          
153600             MOVE DP015-UNP-ALP-NOR-OFF                                   
153700                                      TO MR-GROUP-NBRA (PV-SUB)           
153800             MOVE DP015-GREEN         TO MR-GROUP-NBRC (PV-SUB)           
153900             MOVE DP015-UNDERLINE     TO MR-GROUP-NBRH (PV-SUB)           
154000                                                                          
154100             MOVE DP015-ASK-NOR-OFF   TO MR-DOOR-NBRA (PV-SUB)            
154200             MOVE DP015-BLUE          TO MR-DOOR-NBRC (PV-SUB)            
154300             MOVE DP015-UNDERLINE     TO MR-GROUP-NBRH (PV-SUB)           
154400                                                                          
154500         END-IF                                                           
154600                                                                          
154700         IF ASC-NUMERIC-ERROR-YES (ASC-BLK-SUB ASC-ITEM-SUB)              
154800                 OR ASC-ERROR-YES (ASC-BLK-SUB ASC-ITEM-SUB)              
154900             MOVE -1                  TO MR-GROUP-NBR-L (PV-SUB)          
155000             MOVE DP015-RED           TO MR-GROUP-NBRC  (PV-SUB)          
155100*                                       MR-DOOR-NBRC  (PV-SUB)            
155200             SET DP030-SET-CURSOR-APPL-1                                  
155300                                      TO TRUE                             
155400         ELSE                                                             
155500             MOVE DP015-GREEN         TO MR-GROUP-NBRC  (PV-SUB)          
155600             MOVE DP015-BLUE          TO MR-DOOR-NBRC  (PV-SUB)           
155700         END-IF                                                           
155800     END-IF.                                                              
155900*                                                                         
156000     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
156100     IF  ASC-ITEM-SUB < + 1                                               
156200         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
156300         MOVE +1                 TO ASC-BLK-SUB                           
156400     END-IF.                                                              
156500******************************************************************        
156600** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
156700** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
156800** ISSUE A WARNING.                                             **        
156900******************************************************************        
157000 4430-CHECK-FOR-BEGIN-AND-END.                                            
157100     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
157200         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
157300*            -- NO END-OF-RANGE SPECFIED ---                              
157400             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
157500             SET DP020-MSG-WARNING TO TRUE                                
157600             PERFORM 4431-POSITION-AT-WARNING                             
157700         END-IF                                                           
157800     ELSE                                                                 
157900         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
158000*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
158100             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
158200             SET DP020-MSG-WARNING TO TRUE                                
158300             PERFORM 4431-POSITION-AT-WARNING                             
158400         END-IF                                                           
158500     END-IF.                                                              
158600******************************************************************        
158700** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
158800** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
158900** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
159000** POSITION THE CURSOR THERE.                                   **        
159100******************************************************************        
159200 4431-POSITION-AT-WARNING.                                                
159300     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
159400                    - PV-TOP-ITEM + 1.                                    
159500     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
159600         CONTINUE                                                         
159700     ELSE                                                                 
159800         SET DP030-SET-CURSOR-APPL-1                                      
159900                                   TO TRUE                                
160000         MOVE -1                   TO MR-GROUP-NBR-L (PV-SUB)             
160100         MOVE DP015-REVERSE        TO MR-GROUP-NBRH (PV-SUB)              
160200         MOVE DP015-YELLOW         TO MR-GROUP-NBRC (PV-SUB)              
160300     END-IF.                                                              
160400*                                                                         
160500     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
160600                    - PV-TOP-ITEM + 1.                                    
160700     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
160800         CONTINUE                                                         
160900     ELSE                                                                 
161000         SET DP030-SET-CURSOR-APPL-1                                      
161100                                   TO TRUE                                
161200         MOVE -1                   TO MR-GROUP-NBR-L (PV-SUB)             
161300         MOVE DP015-REVERSE        TO MR-GROUP-NBRH (PV-SUB)              
161400         MOVE DP015-YELLOW         TO MR-GROUP-NBRC (PV-SUB)              
161500     END-IF.                                                              
161600 EJECT                                                                    
161700*----------------------------------------------------------------*        
161800*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
161900*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
162000*----------------------------------------------------------------*        
162100                                                                          
162200 5100-WRITE-TEMP-STORAGE.                                                 
162300     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
162400                                   TO PV-LIST-ITEM-NUMBER.                
162500     COMPUTE PV-BLOCK-NUMBER =                                            
162600         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
162700                                                                          
162800     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
162900     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
163000         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
163100             PERFORM 5105-REWRITE-ASC-LISTQ                               
163200         END-IF                                                           
163300     ELSE                                                                 
163400         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
163500         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
163600         PERFORM 5110-WRITE-ASC-LISTQ                                     
163700         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
163800     END-IF.                                                              
163900                                                                          
164000     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
164100******************************************************************        
164200** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
164300** SPECIFIC COMMAREA.                                           **        
164400******************************************************************        
164500 5105-REWRITE-ASC-LISTQ.                                                  
164600     EXEC CICS                                                            
164700         WRITEQ TS                                                        
164800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
164900             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
165000             ITEM  (PV-BLOCK-NUMBER)                                      
165100             REWRITE                                                      
165200             RESP  (PV-CICS-RESP)                                         
165300     END-EXEC.                                                            
165400                                                                          
165500     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
165600         CONTINUE                                                         
165700     ELSE                                                                 
165800         SET DP013-CICS-ABEND            TO TRUE                          
165900         SET DP013-NO-ROLLBACK           TO TRUE                          
166000         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
166100         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
166200                                         TO DP013-MESSAGE-TEXT(1)         
166300         PERFORM DP013-0000-PROCESS-ABEND                                 
166400     END-IF.                                                              
166500                                                                          
166600******************************************************************        
166700** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
166800** SPECIFIC COMMAREA.                                           **        
166900******************************************************************        
167000 5110-WRITE-ASC-LISTQ.                                                    
167100     EXEC CICS                                                            
167200         WRITEQ TS                                                        
167300             QUEUE (ASC-LIST-QUEUE-NAME)                                  
167400             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
167500             ITEM  (PV-BLOCK-NUMBER)                                      
167600             RESP  (PV-CICS-RESP)                                         
167700     END-EXEC.                                                            
167800     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
167900         SET DP013-CICS-ABEND      TO TRUE                                
168000         SET DP013-NO-ROLLBACK     TO TRUE                                
168100         MOVE '5110-WRITE-ASC-LISTQ'                                      
168200                                   TO DP013-PARAGRAPH                     
168300         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
168400                                   TO DP013-MESSAGE-TEXT(1)               
168500         PERFORM DP013-0000-PROCESS-ABEND                                 
168600     END-IF.                                                              
168700/                                                                         
168800*----------------------------------------------------------------*        
168900*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
169000*----------------------------------------------------------------*        
169100                                                                          
169200 5205-WRITE-SEL-QUEUE.                                                    
169300                                                                          
169400     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
169500                                   TO DL006-SELECTED-ITEMS.               
169600                                                                          
169700     EXEC CICS                                                            
169800          WRITEQ TS                                                       
169900              QUEUE (ASC-SEL-QUEUE-NAME)                                  
170000              FROM  (DL006-SELECT-RECORD)                                 
170100              ITEM  (PV-SEL-QUEUE-ITEM)                                   
170200              RESP  (PV-CICS-RESP)                                        
170300     END-EXEC.                                                            
170400                                                                          
170500     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
170600         CONTINUE                                                         
170700     ELSE                                                                 
170800         SET DP013-CICS-ABEND      TO TRUE                                
170900         SET DP013-NO-ROLLBACK     TO TRUE                                
171000         MOVE '5205-WRITE-SEL-QUEUE'                                      
171100                                   TO DP013-PARAGRAPH                     
171200         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
171300                                   TO DP013-MESSAGE-TEXT(1)               
171400         PERFORM DP013-0000-PROCESS-ABEND                                 
171500     END-IF.                                                              
171600 EJECT                                                                    
171700******************************************************************        
171800** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
171900** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
172000** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
172100** THE USER.                                                    **        
172200******************************************************************        
172300 6000-REFRESH-PANEL-FROM-TEMP.                                            
172400     IF  ASC-UPDATE-TSQ                                                   
172500         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
172600         MOVE ZERO                 TO PV-SAVE-ITEM                        
172700                                      PV-SEL-QUEUE-ITEM                   
172800         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
172900                                   TO PV-HOLD-SELECTED-ITEMS              
173000         MOVE +1                   TO PV-ITEMS-PROCESSED                  
173100         PERFORM 6100-UPDATE-TSQ                                          
173200             UNTIL PV-ITEMS-PROCESSED >                                   
173300                                     PV-HOLD-SELECTED-ITEMS               
173400         PERFORM 6010-DELETE-SEL-QUEUE                                    
173500         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
173600                    ASC-BLOCK-ENTRIES (2)                                 
173700     END-IF.                                                              
173800                                                                          
173900*----------------------------------------------------------------*        
174000*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
174100*----------------------------------------------------------------*        
174200 6010-DELETE-SEL-QUEUE.                                                   
174300                                                                          
174400     EXEC CICS                                                            
174500         DELETEQ TS                                                       
174600             QUEUE (ASC-SEL-QUEUE-NAME)                                   
174700             RESP  (PV-CICS-RESP)                                         
174800     END-EXEC.                                                            
174900*                                                                         
175000     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
175100        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
175200         CONTINUE                                                         
175300     ELSE                                                                 
175400         SET DP013-CICS-ABEND TO TRUE                                     
175500         SET DP013-NO-ROLLBACK TO TRUE                                    
175600         MOVE '6010-DELETE-SEL-QUEUE'                                     
175700                        TO DP013-PARAGRAPH                                
175800         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
175900                        TO DP013-MESSAGE-TEXT (1)                         
176000         PERFORM DP013-0000-PROCESS-ABEND                                 
176100     END-IF.                                                              
176200                                                                          
176300*----------------------------------------------------------------*        
176400*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
176500*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
176600*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
176700*    THE TEMP QUEUE.                                             *        
176800*----------------------------------------------------------------*        
176900                                                                          
177000 6100-UPDATE-TSQ.                                                         
177100                                                                          
177200     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
177300     PERFORM 6105-READ-SEL-QUEUE.                                         
177400     PERFORM                                                              
177500         VARYING DL006-IDX                                                
177600         FROM 1 BY 1                                                      
177700         UNTIL ((PV-ITEMS-PROCESSED >                                     
177800                              PV-HOLD-SELECTED-ITEMS)                     
177900         OR (DL006-IDX GREATER THAN DL006-MAX-ITEMS))                     
178000         IF  DL006-ACTION-SW (DL006-IDX) > SPACE                          
178100             PERFORM 6110-PROCESS-ITEMS                                   
178200         END-IF                                                           
178300         ADD +1                TO PV-ITEMS-PROCESSED                      
178400     END-PERFORM.                                                         
178500     PERFORM 6111-WRITE-LIST-QUEUE.                                       
178600*----------------------------------------------------------------*        
178700*    READ THE LIST QUEUE RECORD.                                 *        
178800*----------------------------------------------------------------*        
178900                                                                          
179000 6105-READ-SEL-QUEUE.                                                     
179100                                                                          
179200     EXEC CICS                                                            
179300          READQ TS                                                        
179400              QUEUE (ASC-SEL-QUEUE-NAME)                                  
179500              INTO  (DL006-SELECT-RECORD)                                 
179600              ITEM  (PV-SEL-QUEUE-ITEM)                                   
179700              RESP  (PV-CICS-RESP)                                        
179800     END-EXEC.                                                            
179900*                                                                         
180000     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
180100         CONTINUE                                                         
180200     ELSE                                                                 
180300         SET DP013-CICS-ABEND      TO  TRUE                               
180400         SET DP013-NO-ROLLBACK     TO  TRUE                               
180500         MOVE '6105-READ-SEL-QUEUE'                                       
180600                                   TO  DP013-PARAGRAPH                    
180700         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
180800                                   TO  DP013-MESSAGE-TEXT(1)              
180900         PERFORM DP013-0000-PROCESS-ABEND                                 
181000     END-IF.                                                              
181100 EJECT                                                                    
181200                                                                          
181300 6110-PROCESS-ITEMS.                                                      
181400                                                                          
181500     COMPUTE PV-ITEM = 1 + ((DL006-ITEM (DL006-IDX) - 1)                  
181600                     / PC-ITEMS-PER-PANEL).                               
181700                                                                          
181800     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
181900         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
182000             PERFORM 6111-WRITE-LIST-QUEUE                                
182100         END-IF                                                           
182200         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
182300                                                                          
182400         PERFORM 6112-READ-LIST-QUEUE                                     
182500         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
182600             CONTINUE                                                     
182700         END-IF                                                           
182800     END-IF.                                                              
182900                                                                          
183000     COMPUTE PV-SUB = (DL006-ITEM (DL006-IDX)                             
183100                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
183200                                                                          
183300     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
183400                                                                          
183500*----------------------------------------------------------------*        
183600*    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
183700*----------------------------------------------------------------*        
183800                                                                          
183900 6111-WRITE-LIST-QUEUE.                                                   
184000                                                                          
184100     EXEC CICS                                                            
184200         WRITEQ TS                                                        
184300             QUEUE (ASC-LIST-QUEUE-NAME)                                  
184400             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
184500             ITEM  (PV-SAVE-ITEM)                                         
184600             REWRITE                                                      
184700             RESP  (PV-CICS-RESP)                                         
184800     END-EXEC.                                                            
184900*                                                                         
185000     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
185100         CONTINUE                                                         
185200     ELSE                                                                 
185300         SET DP013-CICS-ABEND  TO  TRUE                                   
185400         SET DP013-NO-ROLLBACK TO  TRUE                                   
185500         MOVE '6111-WRITE-LIST-QUEUE'                                     
185600                               TO  DP013-PARAGRAPH                        
185700         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
185800                               TO  DP013-MESSAGE-TEXT(1)                  
185900         PERFORM DP013-0000-PROCESS-ABEND                                 
186000     END-IF.                                                              
186100                                                                          
186200 EJECT                                                                    
186300*----------------------------------------------------------------*        
186400*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
186500*----------------------------------------------------------------*        
186600                                                                          
186700 6112-READ-LIST-QUEUE.                                                    
186800                                                                          
186900     EXEC CICS                                                            
187000         READQ TS                                                         
187100             QUEUE (ASC-LIST-QUEUE-NAME)                                  
187200             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
187300             ITEM  (PV-ITEM)                                              
187400             RESP  (PV-CICS-RESP)                                         
187500     END-EXEC.                                                            
187600*                                                                         
187700     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
187800         CONTINUE                                                         
187900     ELSE                                                                 
188000         SET DP013-CICS-ABEND        TO TRUE                              
188100         SET DP013-NO-ROLLBACK       TO TRUE                              
188200         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
188300         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
188400                                     TO DP013-MESSAGE-TEXT(1)             
188500         PERFORM DP013-0000-PROCESS-ABEND                                 
188600     END-IF.                                                              
188700/                                                                         
188800 7000-LOCATION-DESC.                                                      
188900     MOVE DP020-LOCATION-NBR          TO XLT00010-LOC-NBR                 
189000                                                                          
189100     EXEC SQL                                                             
189200         SELECT  LOC_NM                                                   
189300           INTO  :XLT00010-LOC-NM                                         
189400           FROM  XLT_LOC                                                  
189500         WHERE LOC_NBR        = :XLT00010-LOC-NBR                         
189600     END-EXEC                                                             
189700                                                                          
189800     EVALUATE TRUE                                                        
189900         WHEN SQLCODE = ZERO                                              
190000             MOVE XLT00010-LOC-NM     TO ASC-KEY-OPER-DESC                
190100         WHEN SQLCODE = +100                                              
190200             MOVE ALL '?'             TO ASC-KEY-OPER-DESC                
190300         WHEN SQLCODE = -904                                              
190400         WHEN SQLCODE = -911                                              
190500         WHEN SQLCODE = -913                                              
190600             MOVE SQLCODE                   TO PV-SOFT-SQLCODE            
190700             MOVE '7000-LOCATION-DESC'      TO PV-SOFT-PARAGRAPH          
190800             MOVE PV-SOFT-ABORT-MSG         TO PV-CICS-MSG-AREA2          
190900             MOVE LENGTH OF PV-CICS-MSG-AREA2                             
191000               TO PV-COMMAREA-LENGTH                                      
191100             EXEC CICS                                                    
191200                  WRITEQ TD                                               
191300                   QUEUE  ('CSSL')                                        
191400                    FROM  (PV-CICS-MSG-AREA2)                             
191500                  LENGTH  (PV-COMMAREA-LENGTH)                            
191600                    RESP  (PV-CICS-RESP)                                  
191700             END-EXEC                                                     
191800             MOVE  PC-TSYMSG-00984  TO  DP020-MSG-NUMBER                  
191900             SET  DP020-NEXT-ACT-APPL-ERROR  TO TRUE                      
192000             PERFORM 9900-SOFT-ABORT                                      
192100         WHEN OTHER                                                       
192200              MOVE '7000-LOCATION-DESC'                                   
192300                               TO DP013-PARAGRAPH                         
192400              MOVE 'GET LOCATION DESCRIPTION'                             
192500                               TO DP013-MESSAGE-TEXT (1)                  
192600              MOVE SQLCA       TO DP013-SQLCA                             
192700              MOVE 'XLT_LOC '  TO DP013-DB2-TABLE-NAME (1)                
192800              SET DP013-DB2-ABEND                                         
192900                  DP013-XCTL-DISPLAY-RESTART                              
193000                                   TO TRUE                                
193100                  PERFORM DP013-0000-PROCESS-ABEND                        
193200     END-EVALUATE.                                                        
193300                                                                          
193400 7010-COUNT-TEMPLATE-DOORS.                                               
193500                                                                          
193600     MOVE ZERO                        TO PV-TEMPLATE-DOOR-COUNT           
193700     EXEC SQL                                                             
193800         SELECT  COUNT(*)                                                 
193900           INTO  :PV-TEMPLATE-DOOR-COUNT                                  
194000           FROM  DLT_DOOR_GP                                              
194100         WHERE DOOR_GP_TMPL_ID = :DLT00001-DOOR-GP-TMPL-ID                
194200     END-EXEC                                                             
194300                                                                          
194400     EVALUATE TRUE                                                        
194500         WHEN SQLCODE = ZERO                                              
194600         WHEN SQLCODE = +100                                              
194700             CONTINUE                                                     
194800         WHEN SQLCODE = -904                                              
194900         WHEN SQLCODE = -911                                              
195000         WHEN SQLCODE = -913                                              
195100             MOVE SQLCODE             TO PV-SOFT-SQLCODE                  
195200             MOVE '7010-COUNT-TEMPLATE-DOORS'                             
195300                                      TO PV-SOFT-PARAGRAPH                
195400             MOVE PV-SOFT-ABORT-MSG   TO PV-CICS-MSG-AREA2                
195500             MOVE LENGTH OF PV-CICS-MSG-AREA2                             
195600               TO PV-COMMAREA-LENGTH                                      
195700             EXEC CICS                                                    
195800                  WRITEQ TD                                               
195900                   QUEUE  ('CSSL')                                        
196000                    FROM  (PV-CICS-MSG-AREA2)                             
196100                  LENGTH  (PV-COMMAREA-LENGTH)                            
196200                    RESP  (PV-CICS-RESP)                                  
196300             END-EXEC                                                     
196400             MOVE  PC-TSYMSG-00984  TO  DP020-MSG-NUMBER                  
196500             SET  DP020-NEXT-ACT-APPL-ERROR  TO TRUE                      
196600             PERFORM 9900-SOFT-ABORT                                      
196700         WHEN OTHER                                                       
196800              MOVE '7010-COUNT-TEMPLATE-DOORS'                            
196900                                      TO DP013-PARAGRAPH                  
197000              MOVE 'COUNT GROUPS WITHIN TEMPLATE'                         
197100                                      TO DP013-MESSAGE-TEXT (1)           
197200              MOVE SQLCA              TO DP013-SQLCA                      
197300              MOVE 'DLT_DOOR_GP'      TO DP013-DB2-TABLE-NAME (1)         
197400              SET DP013-DB2-ABEND                                         
197500                  DP013-XCTL-DISPLAY-RESTART                              
197600                                   TO TRUE                                
197700                  PERFORM DP013-0000-PROCESS-ABEND                        
197800     END-EVALUATE.                                                        
197900/                                                                         
198000 7100-OPEN-DOOR-CSR.                                                      
198100                                                                          
198200     EXEC SQL                                                             
198300         OPEN DOOR-CSR                                                    
198400     END-EXEC                                                             
198500                                                                          
198600     EVALUATE TRUE                                                        
198700         WHEN SQLCODE = ZERO                                              
198800             CONTINUE                                                     
198900         WHEN SQLCODE = -904                                              
199000         WHEN SQLCODE = -911                                              
199100         WHEN SQLCODE = -913                                              
199200             MOVE '7100-OPEN-DOOR-CSR'                                    
199300                                      TO DP013-PARAGRAPH                  
199400             MOVE 'DB2 TIMEOUT ATTEMPTING TO OPEN CSR'                    
199500                                      TO DP013-MESSAGE-TEXT (1)           
199600             MOVE SQLCA               TO DP013-SQLCA                      
199700             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
199800             SET DP013-DB2-ABEND                                          
199900                 DP013-XCTL-DISPLAY-RESTART                               
200000                                      TO TRUE                             
200100             PERFORM DP013-0000-PROCESS-ABEND                             
200200         WHEN SQLWARN0 NOT = SPACES                                       
200300         WHEN SQLCODE < ZERO                                              
200400         WHEN SQLCODE > ZERO                                              
200500             MOVE '7100-OPEN-DOOR-CSR'                                    
200600                                      TO DP013-PARAGRAPH                  
200700             MOVE 'OPEN DOOR CURSOR - '                                   
200800                                      TO DP013-MESSAGE-TEXT (1)           
200900             MOVE SQLCA               TO DP013-SQLCA                      
201000             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
201100             SET DP013-DB2-ABEND                                          
201200                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
201300             PERFORM DP013-0000-PROCESS-ABEND                             
201400     END-EVALUATE.                                                        
201500*                                                                         
201600 7110-FETCH-DOOR-CSR.                                                     
201700                                                                          
201800     EXEC SQL                                                             
201900         FETCH DOOR-CSR                                                   
202000         INTO                                                             
202100             :DLT00001-DOOR-ID                                            
202200         ,   :DLT00001-DOOR-GP-NBR:DN-NULL-IND                            
202300     END-EXEC                                                             
202400                                                                          
202500     EVALUATE TRUE                                                        
202600         WHEN SQLCODE = ZERO                                              
202700             IF DN-NULL-IND = -1                                          
202800                 MOVE ZERO TO DLT00001-DOOR-GP-NBR                        
202900             END-IF                                                       
203000         WHEN SQLCODE = +100                                              
203100              SET ASC-NO-ITEMS-LEFT-ON-DB TO TRUE                         
203200                                                                          
203300         WHEN SQLCODE = -904                                              
203400         WHEN SQLCODE = -911                                              
203500         WHEN SQLCODE = -913                                              
203600             MOVE '7110-FETCH-DOOR-CSR'                                   
203700                                      TO DP013-PARAGRAPH                  
203800             MOVE 'DB2 TIMEOUT ATTEMPTING TO FETCH CSR'                   
203900                                      TO DP013-MESSAGE-TEXT (1)           
204000             MOVE SQLCA               TO DP013-SQLCA                      
204100             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
204200             SET DP013-DB2-ABEND                                          
204300                 DP013-XCTL-DISPLAY-RESTART                               
204400                                      TO TRUE                             
204500             PERFORM DP013-0000-PROCESS-ABEND                             
204600         WHEN SQLWARN0 NOT = SPACE                                        
204700         WHEN SQLCODE NOT = ZERO                                          
204800              MOVE '7110-FETCH-DOOR-CSR'                                  
204900                               TO DP013-PARAGRAPH                         
205000              MOVE 'FETCH TEMPLATE CURSOR'                                
205100                               TO DP013-MESSAGE-TEXT (1)                  
205200              MOVE SQLCA       TO DP013-SQLCA                             
205300              SET DP013-DB2-ABEND                                         
205400                               TO TRUE                                    
205500              PERFORM DP013-0000-PROCESS-ABEND                            
205600     END-EVALUATE.                                                        
205700                                                                          
205800 7120-CLOSE-DOOR-CSR.                                                     
205900                                                                          
206000     EXEC SQL                                                             
206100         CLOSE DOOR-CSR                                                   
206200     END-EXEC                                                             
206300                                                                          
206400     EVALUATE TRUE                                                        
206500         WHEN SQLCODE = ZERO                                              
206600             CONTINUE                                                     
206700         WHEN SQLWARN0 NOT = SPACES                                       
206800         WHEN SQLCODE < ZERO                                              
206900         WHEN SQLCODE > ZERO                                              
207000             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
207100             MOVE '7120-CLOSE-DOOR-CSR'                                   
207200                                      TO DP013-PARAGRAPH                  
207300             MOVE 'CLOSE TEMPLATE CURSOR '                                
207400                                      TO DP013-MESSAGE-TEXT (1)           
207500             MOVE SQLCA               TO DP013-SQLCA                      
207600             SET DP013-DB2-ABEND                                          
207700                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
207800             PERFORM DP013-0000-PROCESS-ABEND                             
207900     END-EVALUATE.                                                        
208000/                                                                         
208100 7200-OPEN-GROUP-CSR.                                                     
208200                                                                          
208300     EXEC SQL                                                             
208400         OPEN GROUP-CSR                                                   
208500     END-EXEC                                                             
208600                                                                          
208700     EVALUATE TRUE                                                        
208800         WHEN SQLCODE = ZERO                                              
208900             CONTINUE                                                     
209000         WHEN SQLCODE = -904                                              
209100         WHEN SQLCODE = -911                                              
209200         WHEN SQLCODE = -913                                              
209300             MOVE '7200-OPEN-GROUP-CSR'                                   
209400                                      TO DP013-PARAGRAPH                  
209500             MOVE 'DB2 TIMEOUT ATTEMPTING TO OPEN CSR'                    
209600                                      TO DP013-MESSAGE-TEXT (1)           
209700             MOVE SQLCA               TO DP013-SQLCA                      
209800             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
209900             SET DP013-DB2-ABEND                                          
210000                 DP013-XCTL-DISPLAY-RESTART                               
210100                                      TO TRUE                             
210200             PERFORM DP013-0000-PROCESS-ABEND                             
210300         WHEN SQLWARN0 NOT = SPACES                                       
210400         WHEN SQLCODE < ZERO                                              
210500         WHEN SQLCODE > ZERO                                              
210600             MOVE '7200-OPEN-GROUP-CSR'                                   
210700                                      TO DP013-PARAGRAPH                  
210800             MOVE 'OPEN GROUP CURSOR - '                                  
210900                                      TO DP013-MESSAGE-TEXT (1)           
211000             MOVE SQLCA               TO DP013-SQLCA                      
211100             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
211200             SET DP013-DB2-ABEND                                          
211300                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
211400             PERFORM DP013-0000-PROCESS-ABEND                             
211500     END-EVALUATE.                                                        
211600*                                                                         
211700 7210-FETCH-GROUP-CSR.                                                    
211800                                                                          
211900     EXEC SQL                                                             
212000         FETCH GROUP-CSR                                                  
212100         INTO                                                             
212200             :DLT00001-DOOR-ID                                            
212300         ,   :DLT00001-DOOR-GP-NBR:DN-NULL-IND                            
212400     END-EXEC                                                             
212500                                                                          
212600     EVALUATE TRUE                                                        
212700         WHEN SQLCODE = ZERO                                              
212800             IF DN-NULL-IND = -1                                          
212900                 MOVE ZERO TO DLT00001-DOOR-GP-NBR                        
213000             END-IF                                                       
213100         WHEN SQLCODE = +100                                              
213200              SET ASC-NO-ITEMS-LEFT-ON-DB TO TRUE                         
213300                                                                          
213400         WHEN SQLCODE = -904                                              
213500         WHEN SQLCODE = -911                                              
213600         WHEN SQLCODE = -913                                              
213700             MOVE '7210-FETCH-GROUP-CSR'                                  
213800                                      TO DP013-PARAGRAPH                  
213900             MOVE 'DB2 TIMEOUT ATTEMPTING TO FETCH CSR'                   
214000                                      TO DP013-MESSAGE-TEXT (1)           
214100             MOVE SQLCA               TO DP013-SQLCA                      
214200             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
214300             SET DP013-DB2-ABEND                                          
214400                 DP013-XCTL-DISPLAY-RESTART                               
214500                                      TO TRUE                             
214600             PERFORM DP013-0000-PROCESS-ABEND                             
214700         WHEN SQLWARN0 NOT = SPACE                                        
214800         WHEN SQLCODE NOT = ZERO                                          
214900              MOVE '7210-FETCH-GROUP-CSR'                                 
215000                               TO DP013-PARAGRAPH                         
215100              MOVE 'FETCH TEMPLATE CURSOR'                                
215200                               TO DP013-MESSAGE-TEXT (1)                  
215300              MOVE SQLCA       TO DP013-SQLCA                             
215400              SET DP013-DB2-ABEND                                         
215500                               TO TRUE                                    
215600              PERFORM DP013-0000-PROCESS-ABEND                            
215700     END-EVALUATE.                                                        
215800                                                                          
215900 7220-CLOSE-GROUP-CSR.                                                    
216000                                                                          
216100     EXEC SQL                                                             
216200         CLOSE GROUP-CSR                                                  
216300     END-EXEC                                                             
216400                                                                          
216500     EVALUATE TRUE                                                        
216600         WHEN SQLCODE = ZERO                                              
216700             CONTINUE                                                     
216800         WHEN SQLWARN0 NOT = SPACES                                       
216900         WHEN SQLCODE < ZERO                                              
217000         WHEN SQLCODE > ZERO                                              
217100             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
217200             MOVE '7220-CLOSE-GROUP-CSR'                                  
217300                                      TO DP013-PARAGRAPH                  
217400             MOVE 'CLOSE TEMPLATE CURSOR '                                
217500                                      TO DP013-MESSAGE-TEXT (1)           
217600             MOVE SQLCA               TO DP013-SQLCA                      
217700             SET DP013-DB2-ABEND                                          
217800                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
217900             PERFORM DP013-0000-PROCESS-ABEND                             
218000     END-EVALUATE.                                                        
218100/                                                                         
218200 7300-UPDATE-TEMPLATE-HDR.                                                
218300     EXEC SQL                                                             
218400         UPDATE                                                           
218500             DLT_DOOR_GP_TMPL                                             
218600         SET                                                              
218700             DOOR_GP_TMPL_DESC  = :DLT00000-DOOR-GP-TMPL-DESC             
218800         ,   LAST_GP_MNT_TMST   = CURRENT TIMESTAMP                       
218900         ,   LAST_GP_MNT_USR_ID = :DP020-USERID                           
219000         WHERE                                                            
219100             DOOR_GP_TMPL_ID  = :DLT00000-DOOR-GP-TMPL-ID                 
219200     END-EXEC                                                             
219300                                                                          
219400     EVALUATE TRUE                                                        
219500         WHEN SQLCODE = ZERO                                              
219600         WHEN SQLCODE = +100                                              
219700             CONTINUE                                                     
219800         WHEN SQLCODE = -904                                              
219900         WHEN SQLCODE = -911                                              
220000         WHEN SQLCODE = -913                                              
220100             MOVE '7300-UPDATE-TEMPLATE-HDR'                              
220200                                      TO DP013-PARAGRAPH                  
220300             MOVE 'DB2 TIMEOUT ATTEMPTING TO UPDATE TEMPLATE'             
220400                                      TO DP013-MESSAGE-TEXT (1)           
220500             MOVE SQLCA               TO DP013-SQLCA                      
220600             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
220700             SET DP013-DB2-ABEND                                          
220800                 DP013-XCTL-DISPLAY-RESTART                               
220900                                      TO TRUE                             
221000             PERFORM DP013-0000-PROCESS-ABEND                             
221100         WHEN SQLWARN0 NOT = SPACES                                       
221200         WHEN SQLCODE < ZERO                                              
221300         WHEN SQLCODE > ZERO                                              
221400             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
221500             MOVE '7300-UPDATE-TEMPLATE-HDR'                              
221600                                      TO DP013-PARAGRAPH                  
221700             MOVE 'UPDATE TEMPLATE'                                       
221800                                      TO DP013-MESSAGE-TEXT (1)           
221900             MOVE SQLCA               TO DP013-SQLCA                      
222000             SET DP013-DB2-ABEND                                          
222100                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
222200             PERFORM DP013-0000-PROCESS-ABEND                             
222300     END-EVALUATE.                                                        
222400/                                                                         
222500 7350-UPDATE-DOOR-GRP.                                                    
222600                                                                          
222700     EXEC SQL                                                             
222800         UPDATE                                                           
222900            DLT_DOOR_GP                                                   
223000         SET                                                              
223100             DOOR_GP_NBR =   :DLT00001-DOOR-GP-NBR                        
223200                                                                          
223300         WHERE                                                            
223400             DOOR_GP_TMPL_ID = :DLT00001-DOOR-GP-TMPL-ID                  
223500         AND DOOR_ID         = :DLT00001-DOOR-ID                          
223600*        AND DOOR_GP_NBR    <> :DLT00001-DOOR-GP-NBR                      
223700     END-EXEC                                                             
223800                                                                          
223900     EVALUATE TRUE                                                        
224000         WHEN SQLCODE = ZERO                                              
224100             CONTINUE                                                     
224200         WHEN SQLCODE = +100                                              
224300             PERFORM 7400-INSERT-DOOR-GRP                                 
224400         WHEN SQLCODE = -904                                              
224500         WHEN SQLCODE = -911                                              
224600         WHEN SQLCODE = -913                                              
224700             MOVE '7350-UPDATE-DOOR-GRP'                                  
224800                                      TO DP013-PARAGRAPH                  
224900             MOVE 'DB2 TIMEOUT ATTEMPTING TO UPDATE GROUP'                
225000                                      TO DP013-MESSAGE-TEXT (1)           
225100             MOVE SQLCA               TO DP013-SQLCA                      
225200             MOVE 'DLT_DOOR_GP'       TO DP013-DB2-TABLE-NAME (1)         
225300             SET DP013-DB2-ABEND                                          
225400                 DP013-XCTL-DISPLAY-RESTART                               
225500                                      TO TRUE                             
225600             PERFORM DP013-0000-PROCESS-ABEND                             
225700         WHEN SQLWARN0 NOT = SPACES                                       
225800         WHEN SQLCODE < ZERO                                              
225900         WHEN SQLCODE > ZERO                                              
226000             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
226100             MOVE '7350-UPDATE-DOOR-GRP'                                  
226200                                      TO DP013-PARAGRAPH                  
226300             MOVE 'UPDATE DOOR GROUP'                                     
226400                                      TO DP013-MESSAGE-TEXT (1)           
226500             MOVE SQLCA               TO DP013-SQLCA                      
226600             SET DP013-DB2-ABEND                                          
226700                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
226800             PERFORM DP013-0000-PROCESS-ABEND                             
226900     END-EVALUATE.                                                        
227000/                                                                         
227100 7400-INSERT-DOOR-GRP.                                                    
227200                                                                          
227300     EXEC SQL                                                             
227400         INSERT INTO DLT_DOOR_GP                                          
227500           (                                                              
227600             DOOR_GP_TMPL_ID                                              
227700            ,DOOR_ID                                                      
227800            ,DOOR_GP_NBR                                                  
227900           )                                                              
228000         VALUES                                                           
228100         (                                                                
228200             :DLT00001-DOOR-GP-TMPL-ID                                    
228300           , :DLT00001-DOOR-ID                                            
228400           , :DLT00001-DOOR-GP-NBR                                        
228500           )                                                              
228600     END-EXEC                                                             
228700                                                                          
228800     EVALUATE TRUE                                                        
228900         WHEN SQLCODE = ZERO                                              
229000             CONTINUE                                                     
229100         WHEN SQLCODE = -904                                              
229200         WHEN SQLCODE = -911                                              
229300         WHEN SQLCODE = -913                                              
229400             MOVE '7400-INSERT-DOOR-GRP'                                  
229500                                      TO DP013-PARAGRAPH                  
229600             MOVE 'DB2 TIMEOUT ATTEMPTING TO INSERT TEMPLATE'             
229700                                      TO DP013-MESSAGE-TEXT (1)           
229800             MOVE SQLCA               TO DP013-SQLCA                      
229900             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
230000             SET DP013-DB2-ABEND                                          
230100                 DP013-XCTL-DISPLAY-RESTART                               
230200                                      TO TRUE                             
230300             PERFORM DP013-0000-PROCESS-ABEND                             
230400         WHEN SQLWARN0 NOT = SPACES                                       
230500         WHEN SQLCODE < ZERO                                              
230600         WHEN SQLCODE > ZERO                                              
230700             MOVE 'DLT_DOOR_GP_TMPL'  TO DP013-DB2-TABLE-NAME (1)         
230800             MOVE '7400-INSERT-DOOR-GRP'                                  
230900                                      TO DP013-PARAGRAPH                  
231000             MOVE 'INSERT NEW DOOR GROUP'                                 
231100                                      TO DP013-MESSAGE-TEXT (1)           
231200             MOVE SQLCA               TO DP013-SQLCA                      
231300             SET DP013-DB2-ABEND                                          
231400                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
231500             PERFORM DP013-0000-PROCESS-ABEND                             
231600     END-EVALUATE.                                                        
231700/                                                                         
231800 9100-READ-SEL-QUEUE.                                                     
231900     EXEC CICS                                                            
232000         READQ TS                                                         
232100             QUEUE (ASC-SEL-QUEUE-NAME)                                   
232200             INTO  (DL006-SELECT-RECORD)                                  
232300             ITEM  (ASC-SEL-ITEM)                                         
232400             RESP  (PV-CICS-RESP)                                         
232500     END-EXEC.                                                            
232600                                                                          
232700     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
232800         CONTINUE                                                         
232900     ELSE                                                                 
233000         SET DP013-LOGIC-ABEND                                            
233100             DP013-NO-ROLLBACK     TO TRUE                                
233200         MOVE '9100-READ-SEL-QUEUE'                                       
233300                                   TO DP013-PARAGRAPH                     
233400         MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
233500                                   TO DP013-MESSAGE-TEXT (1)              
233600         PERFORM DP013-0000-PROCESS-ABEND                                 
233700     END-IF.                                                              
233800*----------------------------------------------------------------*        
233900* UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
234000*----------------------------------------------------------------*        
234100 9200-REWRITE-SEL-QUEUE.                                                  
234200     EXEC CICS                                                            
234300          WRITEQ TS                                                       
234400              QUEUE  (ASC-SEL-QUEUE-NAME)                                 
234500              FROM   (DL006-SELECT-RECORD)                                
234600              ITEM   (ASC-SEL-ITEM)                                       
234700              RESP   (PV-CICS-RESP)                                       
234800              REWRITE                                                     
234900     END-EXEC.                                                            
235000                                                                          
235100     IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                           
235200         SET DP013-CICS-ABEND                                             
235300             DP013-NO-ROLLBACK     TO TRUE                                
235400         MOVE '9200-REWRITE-SEL-QUEUE'                                    
235500                                   TO DP013-PARAGRAPH                     
235600         MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
235700                                   TO DP013-MESSAGE-TEXT (1)              
235800         PERFORM DP013-0000-PROCESS-ABEND                                 
235900     END-IF.                                                              
236000/                                                                         
236100 9900-SOFT-ABORT.                                                         
236200                                                                          
236300     EXEC CICS SYNCPOINT                                                  
236400          ROLLBACK                                                        
236500     END-EXEC.                                                            
236600                                                                          
236700     SET  PS-ERROR                                                        
236800          PS-SOFT-ABORT-ERROR                                             
236900          DP020-MSG-FATAL       TO  TRUE.                                 
237000/                                                                         
237100*----------------------------------------------------------------*        
237200*    ABEND PROCESSOR MODULE                                      *        
237300*----------------------------------------------------------------*        
237400                                                                          
237500     COPY DPPD013.                                                        
