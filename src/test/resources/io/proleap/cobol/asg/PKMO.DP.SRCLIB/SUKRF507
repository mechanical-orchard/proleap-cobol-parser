000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.             SUKRF507.                                        
000600 AUTHOR.                 LARRY SCHLEICHER - STRATAGEM                     
000700 INSTALLATION.           KOHLS.                                           
000800 DATE-WRITTEN            JUNE, 1996.                                      
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200* THIS PROGRAM DISPLAYS THE WRONG FACILITY SCREEN ON THE                  
001300* LRT (LASER RADIO TERMINAL) AND ACCEPTS AN F-KEY.                        
001400******************************************************************        
000597* 09/26/14    CHERI PARMLEY  CHG0094517     TKID: TKMAF76        *        
000598*             COMPILE ONLY TO GET THE NEW APO MAP FIELD FROM 7   *        
000598*             TO 8 DIGITS                                        *        
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000******************************************************************        
002100 DATA DIVISION.                                                           
002200******************************************************************        
002300                                                                          
002400******************************************************************        
002500 WORKING-STORAGE SECTION.                                                 
002600******************************************************************        
002700 01  CA-COMMAREA.                                                         
002800     COPY SUWS001.                                                        
002900                                                                          
003000******************************************************************        
003100 01  PC-PROGRAM-CONSTANTS.                                                
003200*    05  PC-COMMAREA-LENGTH              PIC S9(04)  VALUE +300           
003300     05  PV-COMMAREA-LENGTH              PIC S9(04)  VALUE +0             
003400                                                     COMP SYNC.           
003500     05  PC-CURRENT-PROGRAM-NAME         PIC  X(08)  VALUE                
003600         'SUKRF507'.                                                      
003700     05  PC-DC-RF-APPL-PGM               PIC  X(08)  VALUE                
003800         'MPKCS027'.                                                      
003900     05  PC-SU-WRONG-FCLTY               PIC  X(04)  VALUE 'S507'.        
004000     05  PC-SU-CHECK-IN-PGM              PIC  X(08)  VALUE                
004100         'SUKRF502'.                                                      
004200     05  PC-END-OF-SESSION-MSG           PIC  X(19)  VALUE                
004300         'END OF SESSION     '.                                           
004400     05  PC-INVALID-SELECTION-MSG        PIC  X(19)  VALUE                
004500         'INVALID SELECTION  '.                                           
004600     05  PC-INVALID-VALUE-MSG            PIC  X(19)  VALUE                
004700         'INVALID VALUE      '.                                           
004800     05  PC-UNRECOGNIZED-PF-KEY-MSG      PIC  X(19)  VALUE                
004900         'UNRECOGNIZED PF KEY'.                                           
005000     05  PC-ACCESS-NOT-ALLOWED-MSG       PIC  X(19)  VALUE                
005100         'USER NOT AUTHORIZED'.                                           
005200     05  PC-NON-NUMERIC-SELECTION-MSG    PIC  X(19)  VALUE                
005300         'NUMERIC VALUE REQ. '.                                           
005400     05  PC-TRANS-UNAVAILABLE-MSG        PIC  X(19)  VALUE                
005500         'TRANS NOT AVAILABLE'.                                           
005600     05  PC-CICS-ABEND-MSG               PIC  X(19)  VALUE                
005700         'CICS ABEND -CALL DP'.                                           
005800     05  PC-CICSAUTH-MSG                 PIC  X(19)  VALUE                
005900         'CICSAUTH BAD RETURN'.                                           
006000     05  PC-UNAVAIL-SELECTION-MSG        PIC X(19)   VALUE                
006100         'SELECTION NOT AVAIL'.                                           
006200     05  PC-N                            PIC X(01)   VALUE 'N'.           
006300     05  PC-Y                            PIC X(01)   VALUE 'Y'.           
006400     05  PC-F4-IGNORE                    PIC X(08)   VALUE                
006500         'IGNORE  '.                                                      
006600     05  PC-F4-IGNORE-PRINT              PIC X(08)   VALUE                
006700         'IGNR&PRT'.                                                      
006800                                                                          
006900                                                                          
007000 01  PS-PROGRAM-SWITCHES.                                                 
007100     05  PS-ACCESS-ALLOWED-IND           PIC  X(01)  VALUE 'N'.           
007200         88  PS-NO-ACCESS-ALLOWED                    VALUE 'N'.           
007300         88  PS-ACCESS-ALLOWED                       VALUE 'Y'.           
007400                                                                          
007500                                                                          
007600 01  PV-PROGRAM-VARIABLES.                                                
007700     05  PV-MESSAGE-AREA                 PIC  X(19)  VALUE SPACE.         
007800     05  PV-CICS-RESPONSE                PIC S9(08)  VALUE ZEROS          
007900                                                     COMP SYNC.           
008000     05  PV-TRANSFER-PROGRAM             PIC  X(08)  VALUE SPACE.         
008100     05  PV-DUPLICATE-IND                PIC  X(01)  VALUE SPACE.         
008200         88 PV-VALID-DUP-IND             VALUE 'Y', 'N'.                  
008300     05  PV-LABEL-IND                    PIC  X(01)  VALUE SPACE.         
008400         88 PV-VALID-LBL-IND             VALUE 'Y', 'N'.                  
008500     05  PV-FKEY-MSG.                                                     
008600         10  FILLER                      PIC  X(11)  VALUE                
008700             'F2-ADD F4-'.                                                
008800         10  PV-F4MSG                    PIC  X(08).                      
008900     05  PV-DB2-DATE                     PIC  X(10).                      
009000     05  PV-DB2-REFORMAT-DATE.                                            
009100         10  PV-DB2-DATE-CC              PIC  X(02).                      
009200         10  PV-DB2-DATE-YY              PIC  X(02).                      
009300         10  FILLER                      PIC  X(01).                      
009400         10  PV-DB2-DATE-MM              PIC  X(02).                      
009500         10  FILLER                      PIC  X(01).                      
009600         10  PV-DB2-DATE-DD              PIC  X(02).                      
009700     05  PV-DISPLAY-DATE.                                                 
009800         10  PV-DISPLAY-DATE-MM          PIC  X(02).                      
009900         10  FILLER                      PIC  X(01)  VALUE '/'.           
010000         10  PV-DISPLAY-DATE-DD          PIC  X(02).                      
010100         10  FILLER                      PIC  X(01)  VALUE '/'.           
010200         10  PV-DISPLAY-DATE-YY          PIC  X(02).                      
010300     05  PV-DB2-TIME                     PIC  X(08).                      
010400                                                                          
010500******************************************************************        
010600*  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA.                       
010700******************************************************************        
010800     COPY DPWS013.                                                        
010900                                                                          
011000******************************************************************        
011100*    COPY BOOK FOR THE CICSAUTH SUBROUTINE                                
011200******************************************************************        
011300     COPY DPWS008.                                                        
011400                                                                          
011500******************************************************************        
011600*  SENDING/RECEIVING AREA FOR THE NUMERIC EDIT ROUTINE (DPKUT100)         
011700******************************************************************        
011800     COPY DPWS010I.                                                       
011900                                                                          
012000******************************************************************        
012100*  GENERATED DSECT FOR THE MPKM027 MAPSET                                 
012200******************************************************************        
012300     COPY SUKM507.                                                        
012400                                                                          
012500******************************************************************        
012600*  KOHL'S STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST           
012700******************************************************************        
012800     COPY DPWS015.                                                        
012900                                                                          
013000******************************************************************        
013100*  KOHL'S STANDARD ATTENTION IDENTIFIER LIST                              
013200******************************************************************        
013300     COPY DFHAID.                                                         
013400                                                                          
013500*                                                                         
013600*  DCLGEN LAYOUT AND COBOL DECLARATION FOR THE SHIPPING UNIT              
013700*  TABLE                                                                  
013800*                                                                         
013900     EXEC SQL                                                             
014000         INCLUDE TSHPUNT                                                  
014100     END-EXEC.                                                            
014200                                                                          
014300*                                                                         
014400*  COMMUNICATIONS AREA FOR DB2                                            
014500*                                                                         
014600     EXEC SQL                                                             
014700         INCLUDE SQLCA                                                    
014800     END-EXEC.                                                            
014900                                                                          
015000******************************************************************        
015100 LINKAGE SECTION.                                                         
015200******************************************************************        
015300*01  DFHCOMMAREA                         PIC  X(300).                     
015400 01  DFHCOMMAREA.                                                         
015500     05  FILLER                       OCCURS 1 TO 4072 TIMES              
015600                                      DEPENDING ON EIBCALEN.              
015700         10  FILLER                   PIC X.                              
015800                                                                          
015900                                                                          
016000******************************************************************        
016100 PROCEDURE DIVISION.                                                      
016200******************************************************************        
016300 0000-PROGRAM-MAINLINE.                                                   
016400*LMM                                                                      
016500     INITIALIZE CA-COMMAREA                                               
016600     MOVE DFHCOMMAREA                TO CA-COMMAREA.                      
016700     MOVE LOW-VALUES                 TO SU507AI.                          
016800                                                                          
016900     EVALUATE TRUE                                                        
017000         WHEN EIBCALEN = ZERO                                             
017100         WHEN EIBAID = DFHCLEAR                                           
017200         WHEN SU001-INITIALIZE-SCREEN                                     
017300              MOVE SU001-CARTON TO ACARTNO                                
017400              MOVE SU001-PO     TO APOO                                   
017500              MOVE SU001-STORE  TO ASTRO                                  
017600              MOVE SU001-CARTON TO SHPUNT-SHIPT-UNT-ID                    
017700              PERFORM 2000-SELECT-SHPUNT                                  
017800              MOVE SHPUNT-ORGN-OPER-UNT-ID                                
017900                                TO AFCLTYO                                
018000              MOVE PC-N         TO PV-LABEL-IND                           
018100              SET SU001-PROCESS-SCREEN TO TRUE                            
018200              PERFORM 8000-SEND-MAP                                       
018300              PERFORM 8200-RETURN-TRANID                                  
018400         WHEN EIBAID = DFHPF2                                             
018500*GB          MOVE PC-Y               TO SU001-DUPLICATE-IND               
018600             IF SU001-PRINTER-IND = PC-N                                  
018700                 MOVE PC-N           TO SU001-LABEL-IND                   
018800             ELSE                                                         
018900                 MOVE PC-Y           TO SU001-LABEL-IND                   
019000             END-IF                                                       
019100             MOVE PC-SU-CHECK-IN-PGM TO PV-TRANSFER-PROGRAM               
019200             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
019300                                                                          
019400         WHEN EIBAID = DFHENTER                                           
019500              PERFORM 8100-SEND-DATA-ONLY                                 
019600              PERFORM 8200-RETURN-TRANID                                  
019700         WHEN OTHER                                                       
019800              MOVE PC-UNRECOGNIZED-PF-KEY-MSG                             
019900                                     TO AMSGLNO                           
020000              PERFORM 8100-SEND-DATA-ONLY                                 
020100              PERFORM 8200-RETURN-TRANID                                  
020200     END-EVALUATE.                                                        
020300                                                                          
020400     GOBACK.                                                              
020500*                                                                         
020600*                                                                         
020700 1000-RECEIVE-MAP.                                                        
020800     EXEC CICS                                                            
020900         RECEIVE MAP    ('SU507A')                                        
021000                 MAPSET ('SUKM507')                                       
021100                 RESP   (PV-CICS-RESPONSE)                                
021200     END-EXEC.                                                            
021300                                                                          
021400     EVALUATE TRUE                                                        
021500         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
021600         WHEN PV-CICS-RESPONSE = DFHRESP (MAPFAIL)                        
021700              CONTINUE                                                    
021800         WHEN OTHER                                                       
021900              MOVE '1000-RECEIVE-MAP'                                     
022000                                     TO DP013-PARAGRAPH                   
022100              MOVE 'UNABLE TO RECEIVE MAP'                                
022200                                     TO DP013-MESSAGE-TEXT (1)            
022300              SET DP013-LOGIC-ABEND  TO TRUE                              
022400              SET DP013-NO-ROLLBACK  TO TRUE                              
022500              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
022600     END-EVALUATE.                                                        
022700*                                                                         
022800 2000-SELECT-SHPUNT.                                                      
022900                                                                          
023000     EXEC SQL                                                             
023100         SELECT ORGN_OPER_UNT_ID                                          
023200         INTO   :SHPUNT-ORGN-OPER-UNT-ID                                  
023300         FROM   TSHPUNT                                                   
023400         WHERE  SHIPT_UNT_ID   = :SHPUNT-SHIPT-UNT-ID                     
023500     END-EXEC.                                                            
023600                                                                          
023700     EVALUATE TRUE                                                        
023800         WHEN SQLCODE = ZERO                                              
023900             CONTINUE                                                     
024000         WHEN SQLWARN0 NOT = SPACES                                       
024100         WHEN SQLCODE NOT EQUAL ZERO                                      
024200             MOVE '2000-SELECT-SHPUNT'                                    
024300                             TO DP013-PARAGRAPH                           
024400             MOVE 'CHECK VALID CARTON'                                    
024500                             TO DP013-MESSAGE-TEXT(1)                     
024600             MOVE SHPUNT-SHIPT-UNT-ID                                     
024700                             TO DP013-MESSAGE-TEXT(2)                     
024800             MOVE SQLCA      TO DP013-SQLCA                               
024900             MOVE 'TSHPUNT'  TO DP013-DB2-TABLE-NAME(1)                   
025000             SET DP013-DB2-ABEND                                          
025100                             TO TRUE                                      
025200             SET DP013-NO-ROLLBACK                                        
025300                             TO TRUE                                      
025400             SET DP013-XCTL-DISPLAY-RESTART                               
025500                             TO TRUE                                      
025600             PERFORM DP014-0000-PROCESS-ABEND                             
025700     END-EVALUATE.                                                        
025800                                                                          
025900*                                                                         
026000 2100-XCTL-TO-SELECTED-PROGRAM.                                           
026100     SET SU001-INITIALIZE-SCREEN     TO TRUE.                             
026200     MOVE PC-CURRENT-PROGRAM-NAME    TO SU001-LAST-PROGRAM.               
026300                                                                          
026400*LMM                                                                      
026500     MOVE LENGTH OF CA-COMMAREA   TO PV-COMMAREA-LENGTH                   
026600     EXEC CICS                                                            
026700         XCTL                                                             
026800             PROGRAM   (PV-TRANSFER-PROGRAM)                              
026900             COMMAREA  (CA-COMMAREA)                                      
027000*LMM         LENGTH    (PC-COMMAREA-LENGTH)                               
027100             LENGTH    (PV-COMMAREA-LENGTH)                               
027200             RESP      (PV-CICS-RESPONSE)                                 
027300     END-EXEC.                                                            
027400                                                                          
027500     EVALUATE TRUE                                                        
027600         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
027700              CONTINUE                                                    
027800         WHEN OTHER                                                       
027900              MOVE '2100-XCTL-TO-SELECTED-PROGRAM'                        
028000                                     TO DP013-PARAGRAPH                   
028100              MOVE 'UNABLE TO XCTL THE FOLLOWING PROGRAM: '               
028200                                     TO DP013-MESSAGE-TEXT (1)            
028300              MOVE PV-TRANSFER-PROGRAM                                    
028400                                     TO DP013-MESSAGE-TEXT (2)            
028500              SET DP013-CICS-ABEND   TO TRUE                              
028600              SET DP013-NO-ROLLBACK  TO TRUE                              
028700              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
028800              MOVE PC-TRANS-UNAVAILABLE-MSG                               
028900                                     TO AMSGLNO                           
029000              SET SU001-PROCESS-SCREEN TO TRUE                            
029100              PERFORM 8100-SEND-DATA-ONLY                                 
029200              PERFORM 8200-RETURN-TRANID                                  
029300     END-EVALUATE.                                                        
029400*                                                                         
029500 8000-SEND-MAP.                                                           
029600     EXEC CICS                                                            
029700         SEND MAP       ('SU507A')                                        
029800              MAPSET    ('SUKM507')                                       
029900              ERASE                                                       
030000              RESP      (PV-CICS-RESPONSE)                                
030100     END-EXEC.                                                            
030200                                                                          
030300     EVALUATE TRUE                                                        
030400         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
030500              CONTINUE                                                    
030600         WHEN OTHER                                                       
030700              MOVE '8000-SEND-MAP'   TO DP013-PARAGRAPH                   
030800              MOVE 'UNABLE TO SEND MAP'                                   
030900                                     TO DP013-MESSAGE-TEXT (1)            
031000              SET DP013-CICS-ABEND   TO TRUE                              
031100              SET DP013-NO-ROLLBACK  TO TRUE                              
031200              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
031300     END-EVALUATE.                                                        
031400*                                                                         
031500 8100-SEND-DATA-ONLY.                                                     
031600                                                                          
031700     EXEC CICS                                                            
031800         SEND MAP       ('SU507A')                                        
031900              MAPSET    ('SUKM507')                                       
032000              CURSOR                                                      
032100              DATAONLY                                                    
032200              RESP      (PV-CICS-RESPONSE)                                
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
032700              CONTINUE                                                    
032800         WHEN OTHER                                                       
032900              MOVE '8100-SEND-DATA-ONLY'                                  
033000                                     TO DP013-PARAGRAPH                   
033100              MOVE 'UNABLE TO SEND DATA'                                  
033200                                     TO DP013-MESSAGE-TEXT (1)            
033300              SET DP013-CICS-ABEND   TO TRUE                              
033400              SET DP013-NO-ROLLBACK  TO TRUE                              
033500              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
033600     END-EVALUATE.                                                        
033700*                                                                         
033800*                                                                         
033900 8200-RETURN-TRANID.                                                      
034000*LMM                                                                      
034100     MOVE LENGTH OF CA-COMMAREA TO PV-COMMAREA-LENGTH                     
034200     EXEC CICS                                                            
034300         RETURN TRANSID   (PC-SU-WRONG-FCLTY)                             
034400                COMMAREA  (CA-COMMAREA)                                   
034500*LMM            LENGTH    (PC-COMMAREA-LENGTH)                            
034600                LENGTH    (PV-COMMAREA-LENGTH)                            
034700                RESP      (PV-CICS-RESPONSE)                              
034800     END-EXEC.                                                            
034900                                                                          
035000     EVALUATE TRUE                                                        
035100         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
035200              CONTINUE                                                    
035300         WHEN OTHER                                                       
035400              MOVE '8200-RETURN-TRANID'                                   
035500                                     TO DP013-PARAGRAPH                   
035600              MOVE 'UNABLE TO RETURN TO TRANID'                           
035700                                     TO DP013-MESSAGE-TEXT (1)            
035800              SET DP013-CICS-ABEND   TO TRUE                              
035900              SET DP013-NO-ROLLBACK  TO TRUE                              
036000              PERFORM 9999-WRITE-MESSAGE-TO-CICS-LOG                      
036100     END-EVALUATE.                                                        
036200*                                                                         
036300 9999-WRITE-MESSAGE-TO-CICS-LOG.                                          
036400     MOVE PC-CURRENT-PROGRAM-NAME    TO DP013-PROGRAM.                    
036500     MOVE DFHEIBLK                   TO DP013-DFHEIBLK.                   
036600     INITIALIZE DP013-SRC-TASK-NUMBER                                     
036700                DP013-STACK.                                              
036800     SET DP013-LINK-RETURN           TO TRUE.                             
036900                                                                          
037000     EXEC CICS                                                            
037100         LINK                                                             
037200             PROGRAM   (DP013-NAME)                                       
037300             COMMAREA  (DP013-PARAMETERS)                                 
037400             LENGTH    (DP013-LENGTH)                                     
037500     END-EXEC.                                                            
037600*----------------------------------------------------------------*      25
037700*    ABEND PROCESSOR MODULE                                      *      25
037800*----------------------------------------------------------------*      25
037900                                                                        25
038000     COPY DPPD014.                                                      25
038100                                                                        25
