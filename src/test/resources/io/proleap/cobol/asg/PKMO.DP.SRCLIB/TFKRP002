       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      TFKRP002.                                       00020000
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    08/22/2005.                                     00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **DAILY TRANSFER REPORTS                                          00080000
      ******************************************************************00090000
      **PROGRAM CREATES THE DAILY TRANSFER DETAIL AND SUMMARY REPORTS   00100000
      **FOR RECEIPTS AND ADJUSTMENTS.                                   00110000
      **     THERE ARE 4 VERSIONS OF THIS REPORT INDICATED BY THE CC:   00120000
      **                - ADJUSTMENTS/RECEIPTS                          00130000
      **                - BLOCKED                                       00140000
      **                - UNBLOCKED                                     00150000
      **                - INVENTORY SNAPSHOT                            00160000
      ******************************************************************00170000
      *DATE:08/22/2005   MADE BY:RAY GRAF INITIAL PROGRAM WRITE         00180000
      ******************************************************************00190000
021006*DATE:02/10/2006   MADE BY:JULIE TEWES FIX TO HOW POSITIVES AND   00200000
021006*                  NEGATIVES ADJUSTMENTS ARE HANDLED              00210000
      ******************************************************************00220000
021006*DATE:10/27/2006   MADE BY:JOYCE KNOKE FIXED PROBLEM NOT SHOWING  00230000
021006*                  DETAIL DOLLAR AMOUNTS                          00240000
      ******************************************************************00250003
092811*DATE:09/28/2011   MADE BY:CHERI PARMLEY - GET BATCH # FOR        00260003
092811*                  HEADINGS FROM THE TFRD052 RECORD. 000 WAS      00270003
092811*                  DISPLAYING WHEN DETAIL ADJUSMENT FILE WAS      00280003
092811*                  EMPTY. OCCURRED IN TESTING.                    00290003
092811*                                             WR# 40189           00300003
      ******************************************************************00310003
      * 03/28/2012 CHANGED BY:  DEANNA BRANTNER    PR#3403              00311009
      *            DESCRIPTION: COMPILE ONLY CHANGES TO TFRD054 COPYBK  00312009
      *                         FOR LARGE RETAIL PROJECT.               00312110
      ******************************************************************00313009
       ENVIRONMENT DIVISION.                                            00320000
       CONFIGURATION SECTION.                                           00330000
       SOURCE-COMPUTER.        IBM-3090.                                00340000
       OBJECT-COMPUTER.        IBM-3090.                                00350000
       INPUT-OUTPUT SECTION.                                            00360000
       FILE-CONTROL.                                                    00370000
                                                                        00380000
           SELECT INPUT-FILE                                            00390000
                  ASSIGN TO INP01.                                      00400000
           SELECT HEADING-CARD                                          00410000
                  ASSIGN TO INP02.                                      00420000
           SELECT BATCH-NBR-CARD                                        00430000
                  ASSIGN TO INP03.                                      00440000
           SELECT REPORT-FILE                                           00450000
                  ASSIGN TO OUT01.                                      00460000
                                                                        00470000
       DATA DIVISION.                                                   00480000
       FILE SECTION.                                                    00490000
                                                                        00500000
       FD  INPUT-FILE                                                   00510000
           RECORDING MODE F                                             00520000
           BLOCK CONTAINS 0 RECORDS                                     00530000
           LABEL RECORDS ARE OMITTED                                    00540000
           DATA RECORD IS INPUT-RECORD.                                 00550000
       01  INPUT-RECORD                        PIC X(120).              00560000
                                                                        00570000
       FD  HEADING-CARD                                                 00580000
           RECORDING MODE F                                             00590000
           BLOCK CONTAINS 0 RECORDS                                     00600000
           LABEL RECORDS ARE OMITTED                                    00610000
           DATA RECORD IS HEADING-RECORD.                               00620000
       01  HEADING-RECORD                      PIC X(080).              00630000
                                                                        00640000
       FD  BATCH-NBR-CARD                                               00650000
           RECORDING MODE IS F                                          00660000
           LABEL RECORDS OMITTED                                        00670000
           BLOCK CONTAINS 0 RECORDS                                     00680000
           DATA RECORD IS CONTROL-CARD-RECORD.                          00690000
       01  BATCH-NBR-RECORD                    PIC X(080).              00700000
                                                                        00710000
       FD  REPORT-FILE                                                  00720000
           RECORDING MODE F                                             00730000
           BLOCK CONTAINS 0 RECORDS                                     00740000
           LABEL RECORDS ARE OMITTED                                    00750000
           DATA RECORD IS REPORT-RECORD.                                00760000
       01  REPORT-RECORD                       PIC X(133).              00770000
                                                                        00780000
       WORKING-STORAGE SECTION.                                         00790000
      ******************************************************************00800000
      **SWITCHES                                                        00810000
      ******************************************************************00820000
       01  PS-PROGRAM-SWITCHES.                                         00830000
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)   VALUE 'N'.   00840000
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.   00850000
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.   00860000
      ******************************************************************00870000
      **HEADING CARD LAYOUTS                                            00880000
      ******************************************************************00890000
       01  CC-HEADING-CARD-LAYOUT.                                      00900000
           05  CC-REPORT-NUMBER                PIC 9(001) VALUE ZEROS.  00910000
           05  CC-HEADING                      PIC X(079) VALUE SPACES. 00920000
      ******************************************************************00930000
      **PROGRAM ARRAY FOR SUMMARY REPORT                                00940000
      ******************************************************************00950000
       01  PA-SUMMARY-REPORT-LINE.                                      00960000
           05  PA-SUMMARY-LINE OCCURS 20 TIMES.                         00970000
               10  PA-TRANSFER-TYPE            PIC X(21)   VALUE SPACES.00980000
               10  PA-TOTAL-RETAIL             PIC 9(09)V99 VALUE ZEROS.00990000
               10  PA-FR-BEF-INV               PIC 9(09)V99 VALUE ZEROS.01000000
               10  PA-FR-AFT-INV               PIC 9(09)V99 VALUE ZEROS.01010000
               10  PA-TO-BEF-INV               PIC 9(09)V99 VALUE ZEROS.01020000
               10  PA-TO-AFT-INV               PIC 9(09)V99 VALUE ZEROS.01030000
               10  PA-ADJ-RSN-CDE              PIC X(12)   VALUE SPACES.01040000
                                                                        01050000
           05  PA-SUM-TBL-SUB                  PIC S9(04) VALUE +0      01060000
                                                       COMP SYNC.       01070000
           05  PA-COUNTER                      PIC S9(04) VALUE +0      01080000
                                                       COMP SYNC.       01090000
      ******************************************************************01100000
      **PROGRAM HOLDS                                                   01110000
      ******************************************************************01120000
       01  PH-PROGRAM-HOLD-DETAIL-FIELDS.                               01130000
           05  PH-TYP-1ST-LNE-DESC             PIC X(18) VALUE SPACES.  01140000
           05  PH-TYP-2ND-LNE-DESC             PIC X(18) VALUE SPACES.  01150000
           05  PH-ADJ-RSN-CDE                  PIC X(02) VALUE SPACES.  01160000
           05  PH-ADJ-RSN-CDE-12               PIC X(12) VALUE SPACES.  01170000
           05  PH-TRANSFER-NBR-FRM-STR         PIC 9(05) VALUE ZEROS.   01180000
           05  PH-TRANSFER-FRM-STR             PIC 9(05) VALUE ZEROS.   01190000
           05  PH-TRANSFER-TO-STR              PIC 9(05) VALUE ZEROS.   01200000
           05  PH-TRANSFER-NBR-SEQ-NBR         PIC 9(07) VALUE ZEROS.   01210000
       01  PH-PROGRAM-HOLD-DETAIL-TOTAL.                                01220000
           05  PH-DETAIL-TOTAL                 PIC 9(09)V99 VALUE ZEROS.01230000
       01  PH-PROGRAM-HOLD-TOTALS.                                      01240000
           05  PH-TOTAL-RETAIL-SUBTOTAL        PIC 9(09)V99 VALUE ZEROS.01250000
           05  PH-TOTAL-RETAIL-TOTAL           PIC 9(09)V99 VALUE ZEROS.01260000
           05  PH-FR-BEF-INV-SUBTOTAL          PIC 9(09)V99 VALUE ZEROS.01270000
           05  PH-FR-AFT-INV-SUBTOTAL          PIC 9(09)V99 VALUE ZEROS.01280000
           05  PH-TO-BEF-INV-SUBTOTAL          PIC 9(09)V99 VALUE ZEROS.01290000
           05  PH-TO-AFT-INV-SUBTOTAL          PIC 9(09)V99 VALUE ZEROS.01300000
           05  PH-FR-BEF-INV-TOTAL             PIC 9(09)V99 VALUE ZEROS.01310000
           05  PH-FR-AFT-INV-TOTAL             PIC 9(09)V99 VALUE ZEROS.01320000
           05  PH-TO-BEF-INV-TOTAL             PIC 9(09)V99 VALUE ZEROS.01330000
           05  PH-TO-AFT-INV-TOTAL             PIC 9(09)V99 VALUE ZEROS.01340000
      ******************************************************************01350000
      **TIMESTAMP VARIABLES                                             01360000
      ******************************************************************01370000
       01 DF-DATE-TIME-FIELDS.                                          01380000
          05 DF-ACCEPT-DATE.                                            01390000
             10  DF-ACCEPT-YR                  PIC 9(02) VALUE ZEROS.   01400000
             10  DF-ACCEPT-MO                  PIC 9(02) VALUE ZEROS.   01410000
             10  DF-ACCEPT-DY                  PIC 9(02) VALUE ZEROS.   01420000
          05 DF-ACCEPT-TIME.                                            01430000
             10  DF-ACCEPT-HR                  PIC 9(02) VALUE ZEROS.   01440000
             10  DF-ACCEPT-MN                  PIC 9(02) VALUE ZEROS.   01450000
             10  DF-ACCEPT-SS                  PIC 9(04) VALUE ZEROS.   01460000
          05 DF-FORMAT-DATE.                                            01470000
             10  DF-FORMAT-DATE-MM             PIC 9(02) VALUE ZEROS.   01480000
             10  FILLER                        PIC X(01) VALUE '/'.     01490000
             10  DF-FORMAT-DATE-DD             PIC 9(02) VALUE ZEROS.   01500000
             10  FILLER                        PIC X(01) VALUE '/'.     01510000
             10  DF-FORMAT-DATE-YY             PIC 9(02) VALUE ZEROS.   01520000
      ******************************************************************01530000
      **PROGRAM CONSTANTS                                               01540000
      ******************************************************************01550000
       01  PC-PROGRAM-CONSTANTS.                                        01560000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                01570000
               VALUE 'TFKRP002'.                                        01580000
           05  PC-COMPLETED                    PIC X(02) VALUE 'CM'.    01590000
           05  PC-ASTERISK                     PIC X(01) VALUE '*'.     01600000
           05  PC-SUMMARY-HEADER               PIC X(13) VALUE          01610000
                                               '     SUMMARY '.         01620000
           05  PC-DETAIL-HEADER                PIC X(13) VALUE          01630000
                                               '      DETAIL '.         01640000
           05  PC-ASSUMPTIVE-HEADER            PIC X(13) VALUE          01650000
                                               'ASSUMPTIVELY '.         01660000
           05  PC-RECEIVED                     PIC X(12) VALUE          01670000
                                                         'RECEIVED:'.   01680000
           05  PC-ADJUSTMENTS                  PIC X(12) VALUE          01690000
                                                         'ADJUSTMENTS:'.01700000
      ******************************************************************01710003
092811**PROGRAM VARIABLES                                               01720003
092911******************************************************************01730003
092811 01  PC-PROGRAM-VARIABLES.                                        01740003
092811     05  PV-BATCH-NBR                    PIC 9(04) VALUE ZERO.    01750004
                                                                        01760003
      ******************************************************************01770000
      **PROGRAM COUNTERS                                                01780000
      ******************************************************************01790000
       01  PC-PROGRAM-COUNTER.                                          01800000
           05  PC-SUMMARY-RECORDS-READ         PIC 9(09) VALUE ZEROS.   01810000
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROS.   01820000
           05  PC-DETAIL-RECORDS-WRITTEN       PIC 9(09) VALUE ZEROS.   01830000
           05  PC-SUMMARY-RECORDS-WRITTEN      PIC 9(09) VALUE ZEROS.   01840000
           05  PC-REPORT-LINE-COUNTER          PIC 9(03) VALUE ZEROS.   01850000
           05  PC-PAGE-COUNTER                 PIC 9(03) VALUE ZEROS.   01860000
      ******************************************************************01870000
      **LAYOUT FOR THE TRANSFER DAILY SUMMARY REPORTS                   01880000
      ******************************************************************01890000
       01  HL-HEADER-LINE1.                                             01900000
           05  FILLER                          PIC X(57) VALUE SPACES.  01910000
           05  FILLER                          PIC X(22)                01920000
               VALUE 'TRANSFERS DAILY REPORT'.                          01930000
       01  HL-HEADER-LINE2.                                             01940000
           05  FILLER                          PIC X(57) VALUE SPACES.  01950000
           05  HL2-REPORT-TITLE                PIC X(40) VALUE SPACES.  01960000
       01  HL-HEADER-LINE2A.                                            01970000
           05  FILLER                          PIC X(64) VALUE SPACES.  01980000
           05  FILLER                          PIC X(07)                01990000
               VALUE 'SUMMARY'.                                         02000000
       01  HL-HEADER-LINE3.                                             02010000
           05  FILLER                          PIC X(05) VALUE SPACES.  02020000
           05  FILLER                          PIC X(06)                02030000
               VALUE 'DATE: '.                                          02040000
           05  HL3-REPORT-DATE                 PIC X(10) VALUE SPACES.  02050000
       01  HL-HEADER-LINE4.                                             02060000
           05  FILLER                          PIC X(11)                02070000
               VALUE 'BATCH NBR: '.                                     02080000
           05  HL4-BATCH-NBR                   PIC 9(03) VALUE ZEROES.  02090000
       01  HL-HEADER-LINE5.                                             02100000
           05  FILLER                          PIC X(11) VALUE SPACES.  02110000
           05  HL5-REPORT-TYPE                 PIC X(13) VALUE SPACES.  02120000
           05  HL5-ADJ-RSN                     PIC X(12) VALUE SPACES.  02130000
           05  FILLER                          PIC X(24) VALUE SPACES.  02140000
           05  HL5-SNAPSHOT-NOTE.                                       02150000
               10 FILLER                       PIC X(56) VALUE          02160000
           '* INDICATES TRANSFER WAS PREVIOUSLY PASSED TO INVENTORY '.  02170000
               10 FILLER                       PIC X(16)                02180000
                 VALUE 'SNAPSHOT PROCESS'.                              02190000
       01  HL-HEADER-LINE6.                                             02200000
           05  FILLER                          PIC X(23)                02210000
               VALUE 'TRANSFER TYPE          '.                         02220000
           05  FILLER                          PIC X(36)                02230000
               VALUE 'TOTAL RETAIL    FROM BEFORE INV     '.            02240000
           05  FILLER                          PIC X(45)                02250000
               VALUE 'AFTER INV       TO   BEFORE INV     AFTER INV'.   02260000
                                                                        02270000
       01  HL-DOTTED-HEADER-LINE1.                                      02280000
           05  FILLER                          PIC X(23)                02290000
               VALUE '---------------------  '.                         02300000
           05  FILLER                          PIC X(36)                02310000
               VALUE '--------------  ---- -------------- '.            02320000
           05  FILLER                          PIC  X(36)               02330000
               VALUE '--------------  ---- -------------- '.            02340000
           05  FILLER                          PIC  X(14)               02350000
               VALUE '--------------'.                                  02360000
                                                                        02370000
       01  DL-DETAIL-LINE1.                                             02380000
           05  DL1-TRANSFER-TYPE               PIC X(20)  VALUE SPACES. 02390000
           05  FILLER                          PIC X(03)  VALUE SPACES. 02400000
           05  DL1-TOTAL-RETAIL                PIC ZZZ,ZZZ,ZZ9.99       02410000
                                                          VALUE ZEROES. 02420000
           05  FILLER                          PIC X(07)  VALUE SPACES. 02430000
           05  DL1-FR-BEF-INV-STR              PIC ZZZ,ZZZ,ZZ9.99       02440000
                                                          VALUE ZEROES. 02450000
           05  FILLER                          PIC X(01)  VALUE SPACES. 02460000
           05  DL1-FR-AFT-INV-STR              PIC ZZZ,ZZZ,ZZ9.99       02470000
                                                          VALUE ZEROES. 02480000
           05  FILLER                          PIC X(07)  VALUE SPACES. 02490000
           05  DL1-TO-BEF-INV-STR              PIC  ZZZ,ZZZ,ZZ9.99      02500000
                                                          VALUE ZEROES. 02510000
           05  FILLER                          PIC  X(01) VALUE SPACES. 02520000
           05  DL1-TO-AFT-INV-STR              PIC  ZZZ,ZZZ,ZZ9.99      02530000
                                                          VALUE ZEROES. 02540000
      ******************************************************************02550000
      **LAYOUT FOR THE TRANSFER DAILY DETAIL REPORTS                    02560000
      ******************************************************************02570000
       01  HL-HEADER-LINE6A.                                            02580000
           05  FILLER                          PIC X(37)                02590000
               VALUE 'TRANSFER TYPE         DOCUMENT NBR   '.           02600000
           05  FILLER                          PIC X(36)                02610000
               VALUE 'TOTAL RETAIL    FROM BEFORE INV     '.            02620000
           05  FILLER                          PIC X(45)                02630000
               VALUE 'AFTER INV       TO   BEFORE INV     AFTER INV'.   02640000
                                                                        02650000
       01  HL-DOTTED-HEADER-LINE1A.                                     02660000
           05  FILLER                          PIC X(37)                02670000
               VALUE '--------------------  -------------  '.           02680000
           05  FILLER                          PIC X(36)                02690000
               VALUE '--------------  ---- -------------- '.            02700000
           05  FILLER                          PIC X(36)                02710000
               VALUE '--------------  ---- -------------- '.            02720000
           05  FILLER                          PIC X(14)                02730000
               VALUE '--------------'.                                  02740000
                                                                        02750000
       01  DL-DETAIL-LINE1A.                                            02760000
           05  DL1A-TRANSFER-TYPE              PIC X(20)  VALUE SPACES. 02770000
           05  FILLER                          PIC X(01)  VALUE SPACES. 02780000
           05  DL1A-ASTERISK                   PIC X(01)  VALUE SPACES. 02790000
           05  DL1A-DOC-FROM-LOC-NBR           PIC X(05) VALUE SPACES.  02800000
           05  DL1A-DASH                       PIC X(01) VALUE '-'.     02810000
           05  DL1A-DOC-NBR                    PIC X(07) VALUE SPACES.  02820000
           05  FILLER                          PIC X(02) VALUE SPACES.  02830000
           05  DL1A-TOTAL-RETAIL               PIC ZZZ,ZZZ,ZZ9.99       02840000
                                                          VALUE ZEROES. 02850000
           05  FILLER                          PIC X(02)  VALUE SPACES. 02860000
           05  DL1A-FR-STR                     PIC X(05)  VALUE SPACES. 02870000
           05  DL1A-FR-BEF-INV-STR             PIC ZZZ,ZZZ,ZZ9.99       02880000
                                                          VALUE ZEROES. 02890000
           05  FILLER                          PIC X(01)  VALUE SPACES. 02900000
           05  DL1A-FR-AFT-INV-STR             PIC ZZZ,ZZZ,ZZ9.99       02910000
                                                          VALUE ZEROES. 02920000
           05  FILLER                          PIC X(01)  VALUE SPACES. 02930000
           05  DL1A-TO-STR                     PIC X(05)  VALUE SPACES. 02940000
           05  DL1A-TO-BEF-INV-STR             PIC ZZZ,ZZZ,ZZ9.99       02950000
                                                          VALUE ZEROES. 02960000
           05  FILLER                          PIC X(01)  VALUE SPACES. 02970000
           05  DL1A-TO-AFT-INV-STR             PIC ZZZ,ZZZ,ZZ9.99       02980000
                                                          VALUE ZEROES. 02990000
       01  BL-BLANK-LINE.                                               03000000
           05  FILLER                          PIC X(132) VALUE SPACES. 03010000
                                                                        03020000
       01  ND-NO-DATA-LINE.                                             03030000
           05  FILLER                          PIC X(050) VALUE SPACES. 03040000
           05  FILLER                          PIC X(082)               03050000
               VALUE '****  NO DATA TO PROCESS FOR THIS REPORT  ***'.   03060000
                                                                        03070000
      ******************************************************************03080000
      **COPYBOOKS                                                       03090000
      ******************************************************************03100000
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        03110000
           COPY DPWS004.                                                03120000
      ****CONTROL CARD BATCH NBR RECORD LAYOUT                          03130000
           COPY TFRD052.                                                03140000
      ***INPUT FILE FORMAT                                              03150000
           COPY TFRD054.                                                03160000
      ***STANDARD KOHLS HEADING FOR 132 BYTE REPORT                     03170000
           COPY DPWS132O.                                               03180000
      **PARAMETER LIST FOR THE CALENDAR ROUTINE                         03190000
           COPY DPWS500.                                                03200000
      ******************************************************************03210000
      **ERROR HANDLING                                                  03220000
      ******************************************************************03230000
       01  PV-MISC-ABEND-FIELDS.                                        03240000
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   03250000
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   03260000
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   03270000
                                                                        03280000
       PROCEDURE DIVISION.                                              03290000
      ***************************************************************** 03300000
                                                                        03310000
           PERFORM 1000-INITIALIZATION.                                 03320000
           PERFORM 2000-PROCESS-RECORDS                                 03330000
                   UNTIL PS-END-OF-INPUT-FILE.                          03340000
           PERFORM 9999-WRAPUP.                                         03350000
           GOBACK.                                                      03360000
                                                                        03370000
       EJECT.                                                           03380000
      ******************************************************************03390000
      **INITIALIZATION AREA                                             03400000
      ******************************************************************03410000
       1000-INITIALIZATION.                                             03420000
                                                                        03430000
           MOVE '1000-INITIALIZATION'    TO PV-PARAGRAPH-NAME.          03440000
                                                                        03450000
           OPEN INPUT  INPUT-FILE                                       03460000
                       HEADING-CARD                                     03470000
                       BATCH-NBR-CARD                                   03480000
                OUTPUT REPORT-FILE.                                     03490000
                                                                        03500000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             03510000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             03520000
                                                                        03530000
           MOVE DF-ACCEPT-YR             TO DF-FORMAT-DATE-YY.          03540000
           MOVE DF-ACCEPT-MO             TO DF-FORMAT-DATE-MM.          03550000
           MOVE DF-ACCEPT-DY             TO DF-FORMAT-DATE-DD.          03560000
                                                                        03570000
           PERFORM 1998-READ-HEADING-CARD.                              03580007
           PERFORM 1999-READ-BATCH-NBR-CARD.                            03590000
                                                                        03600005
092811     MOVE TFRD052-JULIAN-DAY       TO PV-BATCH-NBR.               03610005
                                                                        03620005
           PERFORM 5000-READ-INPUT-FILE.                                03630007
                                                                        03640000
           MOVE TFRD054-DOC-FROM-LOC-NBR TO PH-TRANSFER-NBR-FRM-STR.    03650000
           MOVE TFRD054-DOC-SEQ-NBR      TO PH-TRANSFER-NBR-SEQ-NBR.    03660000
           MOVE TFRD054-FROM-LOC-NBR     TO PH-TRANSFER-FRM-STR.        03670000
           MOVE TFRD054-TO-LOC-NBR       TO PH-TRANSFER-TO-STR.         03680000
           MOVE TFRD054-TYP-1ST-LNE-DESC TO PH-TYP-1ST-LNE-DESC.        03690000
           MOVE TFRD054-TYP-2ND-LNE-DESC TO PH-TYP-2ND-LNE-DESC.        03700000
           MOVE TFRD054-ADJ-RSN-CDE      TO PH-ADJ-RSN-CDE.             03710000
                                                                        03720000
           INITIALIZE DL-DETAIL-LINE1A.                                 03730000
           MOVE PH-TRANSFER-NBR-FRM-STR  TO DL1A-DOC-FROM-LOC-NBR.      03740000
           MOVE PH-TRANSFER-NBR-SEQ-NBR  TO DL1A-DOC-NBR.               03750000
           MOVE PH-TRANSFER-FRM-STR      TO DL1A-FR-STR.                03760000
           MOVE PH-TRANSFER-TO-STR       TO DL1A-TO-STR.                03770000
                                                                        03780000
       EJECT.                                                           03790000
      ***************************************************************** 03800000
      **READ HEADING CONTROL CARD                                       03810000
      ***************************************************************** 03820000
       1998-READ-HEADING-CARD.                                          03830007
                                                                        03840000
           READ HEADING-CARD INTO CC-HEADING-CARD-LAYOUT.               03850000
                                                                        03860000
       EJECT.                                                           03870000
      ******************************************************************03880000
      **READ BATCH NBR CARD                                             03890000
      ******************************************************************03900000
       1999-READ-BATCH-NBR-CARD.                                        03910000
                                                                        03920000
           READ BATCH-NBR-CARD INTO TFRD052-BATCH-CONTROL-FILE.         03930000
                                                                        03940000
       EJECT.                                                           03950000
      ******************************************************************03960000
      **PROCESS INPUT FILE                                              03970000
      ******************************************************************03980000
       2000-PROCESS-RECORDS.                                            03990000
                                                                        04000000
           MOVE '2000-PROCESS-RECORDS'  TO PV-PARAGRAPH-NAME.           04010000
                                                                        04020000
           PERFORM 2100-PROCESS-COMPLETES.                              04030000
           PERFORM 2200-PROCESS-ADJUSTMENTS.                            04040000
                                                                        04050000
      ***WRITE FINAL PAGE SUMMARY                                       04060000
           INITIALIZE PH-PROGRAM-HOLD-TOTALS.                           04070000
           MOVE PC-RECEIVED             TO HL5-ADJ-RSN                  04080000
                                           PH-ADJ-RSN-CDE-12.           04090000
           MOVE SPACES                  TO HL5-SNAPSHOT-NOTE.           04100000
           ADD +1                       TO DP132O-REPORT-NUMBER.        04110000
           PERFORM 4100-WRITE-SUMMARY-HEADINGS.                         04120000
           PERFORM 4200-WRITE-SUMMARY-LINE.                             04130000
           PERFORM 4500-WRITE-FINAL-SUMMARY-TOTAL.                      04140000
           PERFORM 4600-WRITE-SUMMARY-GRANDTOTAL.                       04150000
                                                                        04160000
       EJECT.                                                           04170000
      ******************************************************************04180000
      **PROCESS COMPLETES                                               04190000
      ******************************************************************04200000
       2100-PROCESS-COMPLETES.                                          04210000
                                                                        04220000
           MOVE '2100-PROCESS-COMPLETES'  TO PV-PARAGRAPH-NAME.         04230000
                                                                        04240000
           PERFORM 6100-HEADING-PREP.                                   04250007
           PERFORM 3100-WRITE-DETAIL-HEADINGS.                          04260000
                                                                        04270000
           PERFORM 3200-WRITE-DETAIL-LINE                               04280000
      **ONLY PROCESS THE COMPLETES FOR THIS PORTION OR UNTIL END OF FILE04290000
                   UNTIL TFRD054-ADJ-RSN-CDE NOT = PC-COMPLETED         04300000
                      OR PS-END-OF-INPUT-FILE.                          04310000
                                                                        04320000
           PERFORM 3300-WRITE-DETAIL-LNE-SUBTOTAL.                      04330000
           PERFORM 3500-WRITE-DETAIL-LNE-TOTAL.                         04340000
                                                                        04350000
       EJECT.                                                           04360000
      ******************************************************************04370000
      **PROCESS ADJUSTMENTS                                             04380000
      ******************************************************************04390000
       2200-PROCESS-ADJUSTMENTS.                                        04400000
                                                                        04410000
           MOVE '2200-PROCESS-ADJUSTMENTS'  TO PV-PARAGRAPH-NAME.       04420000
                                                                        04430000
           INITIALIZE PH-TOTAL-RETAIL-TOTAL                             04440000
                      PH-FR-AFT-INV-TOTAL                               04450000
                      PH-FR-BEF-INV-TOTAL                               04460000
                      PH-TO-AFT-INV-TOTAL                               04470000
                      PH-TO-BEF-INV-TOTAL.                              04480000
                                                                        04490000
           MOVE TFRD054-TYP-1ST-LNE-DESC TO PH-TYP-1ST-LNE-DESC.        04500000
           MOVE TFRD054-TYP-2ND-LNE-DESC TO PH-TYP-2ND-LNE-DESC.        04510000
           MOVE TFRD054-ADJ-RSN-CDE      TO PH-ADJ-RSN-CDE.             04520000
                                                                        04530000
           PERFORM 6100-HEADING-PREP.                                   04540007
           PERFORM 3100-WRITE-DETAIL-HEADINGS.                          04550000
                                                                        04560000
           PERFORM 3200-WRITE-DETAIL-LINE                               04570000
      **ALL OTHER RECORDS IN FILE ARE ADJUSTMENTS, SO JUST UNTIL END OF 04580000
      **    FILES                                                       04590000
                   UNTIL PS-END-OF-INPUT-FILE.                          04600000
                                                                        04610000
           PERFORM 3300-WRITE-DETAIL-LNE-SUBTOTAL.                      04620000
           PERFORM 3500-WRITE-DETAIL-LNE-TOTAL.                         04630000
                                                                        04640000
       EJECT.                                                           04650000
      ***************************************************************** 04660000
      **WRITE DETAIL REPORT HEADINGS                                    04670000
      ***************************************************************** 04680000
       3100-WRITE-DETAIL-HEADINGS.                                      04690000
                                                                        04700000
           MOVE '3100-WRITE-DETAIL-HEADINGS'  TO PV-PARAGRAPH-NAME.     04710000
                                                                        04720000
           INITIALIZE PC-REPORT-LINE-COUNTER.                           04730000
                                                                        04740000
           ADD +1                        TO PC-PAGE-COUNTER.            04750000
           MOVE PC-PAGE-COUNTER          TO DP132O-PAGE-NUMBER.         04760000
                                                                        04770000
           MOVE TFRD052-BTCH-CRE-DTE     TO HL3-REPORT-DATE             04780000
092811     MOVE PV-BATCH-NBR             TO HL4-BATCH-NBR.              04790008
                                                                        04800000
           IF TFRD054-ADJ-RSN-CDE =  PC-COMPLETED                       04810000
               MOVE PC-ASSUMPTIVE-HEADER TO HL5-REPORT-TYPE             04820000
               MOVE PC-RECEIVED          TO HL5-ADJ-RSN                 04830000
           ELSE                                                         04840000
               MOVE PC-DETAIL-HEADER     TO HL5-REPORT-TYPE             04850000
               MOVE PC-ADJUSTMENTS       TO HL5-ADJ-RSN                 04860000
           END-IF.                                                      04870000
                                                                        04880000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      04890000
                               AFTER ADVANCING PAGE.                    04900000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE1.                    04910000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE2.                    04920000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE3.                    04930000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE4.                    04940000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE5.                    04950000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE6A                    04960000
                               AFTER ADVANCING 2 LINES.                 04970000
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE1A.            04980000
                                                                        04990000
           ADD +9              TO PC-REPORT-LINE-COUNTER.               05000000
                                                                        05010000
       EJECT.                                                           05020000
      ***************************************************************** 05030000
      **WRITE DETAIL REPORT LINE                                        05040000
      ***************************************************************** 05050000
       3200-WRITE-DETAIL-LINE.                                          05060000
                                                                        05070000
           MOVE '3200-WRITE-DETAIL-LINE'      TO PV-PARAGRAPH-NAME.     05080000
                                                                        05090000
           IF  PC-REPORT-LINE-COUNTER > 59                              05100000
               PERFORM 3100-WRITE-DETAIL-HEADINGS                       05110000
           END-IF.                                                      05120000
                                                                        05130000
           IF  TFRD054-TYP-1ST-LNE-DESC NOT = PH-TYP-1ST-LNE-DESC       05140000
      **WRITE SUBTOTAL LINE WHEN TRANSFER TYPE CHANGES                  05150000
            OR TFRD054-TYP-2ND-LNE-DESC NOT = PH-TYP-2ND-LNE-DESC       05160000
               PERFORM 3300-WRITE-DETAIL-LNE-SUBTOTAL                   05170000
               MOVE TFRD054-TYP-1ST-LNE-DESC  TO PH-TYP-1ST-LNE-DESC    05180000
               MOVE TFRD054-TYP-2ND-LNE-DESC  TO PH-TYP-2ND-LNE-DESC    05190000
           END-IF.                                                      05200000
                                                                        05210000
           IF  TFRD054-INVENTORY-SNAP-YES                               05220000
               MOVE PC-ASTERISK TO DL1A-ASTERISK                        05230000
           END-IF.                                                      05240000
                                                                        05250000
           IF  CC-REPORT-NUMBER = 7                                     05260000
               PERFORM 3250-MOVE-DETAIL-DATA-TYPE-7                     05270000
           ELSE                                                         05280000
               PERFORM 3225-MOVE-DETAIL-DATA                            05290000
           END-IF.                                                      05300000
                                                                        05310000
           MOVE TFRD054-EXTENDED-RETAIL     TO DL1A-TOTAL-RETAIL.       05320000
           ADD TFRD054-EXTENDED-RETAIL      TO PH-DETAIL-TOTAL.         05330000
           ADD TFRD054-EXTENDED-RETAIL      TO PH-TOTAL-RETAIL-SUBTOTAL.05340000
                                                                        05350000
           MOVE PH-TRANSFER-NBR-FRM-STR     TO DL1A-DOC-FROM-LOC-NBR.   05360000
           MOVE PH-TRANSFER-NBR-SEQ-NBR     TO DL1A-DOC-NBR.            05370000
           MOVE TFRD054-FROM-LOC-NBR        TO DL1A-FR-STR.             05380000
           MOVE TFRD054-TO-LOC-NBR          TO DL1A-TO-STR.             05390000
           STRING PH-TYP-1ST-LNE-DESC DELIMITED BY '  '                 05400000
                  PH-TYP-2ND-LNE-DESC DELIMITED BY '  '                 05410000
           INTO DL1A-TRANSFER-TYPE.                                     05420000
                                                                        05430000
           PERFORM 5000-READ-INPUT-FILE.                                05440007
                                                                        05450000
           IF   TFRD054-DOC-FROM-LOC-NBR NOT = PH-TRANSFER-NBR-FRM-STR  05460000
             OR TFRD054-DOC-SEQ-NBR      NOT = PH-TRANSFER-NBR-SEQ-NBR  05470000
                MOVE PH-DETAIL-TOTAL        TO DL1A-TOTAL-RETAIL        05480000
                MOVE TFRD054-DOC-FROM-LOC-NBR                           05490000
                                            TO PH-TRANSFER-NBR-FRM-STR  05500000
                MOVE TFRD054-DOC-SEQ-NBR    TO PH-TRANSFER-NBR-SEQ-NBR  05510000
                MOVE TFRD054-FROM-LOC-NBR   TO PH-TRANSFER-FRM-STR      05520000
                MOVE TFRD054-TO-LOC-NBR     TO PH-TRANSFER-TO-STR       05530000
                INITIALIZE PH-DETAIL-TOTAL                              05540000
           END-IF.                                                      05550000
                                                                        05560000
           WRITE REPORT-RECORD FROM DL-DETAIL-LINE1A.                   05570000
           ADD +1                           TO PC-REPORT-LINE-COUNTER.  05580000
           ADD +1                          TO PC-DETAIL-RECORDS-WRITTEN.05590000
                                                                        05600000
           INITIALIZE DL-DETAIL-LINE1A.                                 05610000
                                                                        05620000
       EJECT.                                                           05630000
      ***************************************************************** 05640000
      **MOVE DATA FOR DETAIL LINES                                      05650000
      ***************************************************************** 05660000
       3225-MOVE-DETAIL-DATA.                                           05670000
                                                                        05680000
           MOVE '3225-MOVE-DETAIL-DATA'   TO PV-PARAGRAPH-NAME.         05690000
                                                                        05700000
           EVALUATE TRUE                                                05710000
              WHEN  TFRD054-FROM-LOC-INV-AFTER                          05720000
                    ADD TFRD054-EXTENDED-RETAIL                         05730000
                                          TO PH-FR-AFT-INV-SUBTOTAL     05740000
                    MOVE TFRD054-EXTENDED-RETAIL                        05750000
                                            TO DL1A-FR-AFT-INV-STR      05760000
              WHEN  TFRD054-FROM-LOC-INV-BEFORE                         05770000
                    ADD TFRD054-EXTENDED-RETAIL                         05780000
                                          TO PH-FR-BEF-INV-SUBTOTAL     05790000
                    MOVE TFRD054-EXTENDED-RETAIL                        05800000
                                            TO DL1A-FR-BEF-INV-STR      05810000
              WHEN  TFRD054-FROM-LOC-INV-NORMAL                         05820000
                    CONTINUE                                            05830000
           END-EVALUATE.                                                05840000
                                                                        05850000
           EVALUATE TRUE                                                05860000
              WHEN  TFRD054-TO-LOC-INV-AFTER                            05870000
                    ADD TFRD054-EXTENDED-RETAIL                         05880000
                                    TO PH-TO-AFT-INV-SUBTOTAL           05890000
                    MOVE TFRD054-EXTENDED-RETAIL                        05900000
                                    TO DL1A-TO-AFT-INV-STR              05910000
              WHEN  TFRD054-TO-LOC-INV-BEFORE                           05920000
                    ADD TFRD054-EXTENDED-RETAIL                         05930000
                                    TO PH-TO-BEF-INV-SUBTOTAL           05940000
                    MOVE TFRD054-EXTENDED-RETAIL                        05950000
                                    TO DL1A-TO-BEF-INV-STR              05960000
              WHEN  TFRD054-TO-LOC-INV-NORMAL                           05970000
                    CONTINUE                                            05980000
           END-EVALUATE.                                                05990000
                                                                        06000000
       EJECT.                                                           06010000
      ***************************************************************** 06020000
      **MOVE DATA FOR DETAIL LINES FOR REPORT TYPE 7                    06030000
      ***************************************************************** 06040000
       3250-MOVE-DETAIL-DATA-TYPE-7.                                    06050000
                                                                        06060000
           MOVE '3250-MOVE-DETAIL-DATA-TYPE-7' TO PV-PARAGRAPH-NAME.    06070000
                                                                        06080000
           IF  TFRD054-ORIG-TO-LOC-NBR = TFRD054-FROM-LOC-NBR           06090000
               EVALUATE TRUE                                            06100000
                  WHEN  TFRD054-FROM-LOC-INV-AFTER                      06110000
                        ADD TFRD054-EXTENDED-RETAIL                     06120000
                                          TO PH-FR-AFT-INV-SUBTOTAL     06130000
                        MOVE TFRD054-EXTENDED-RETAIL                    06140000
                                          TO DL1A-FR-AFT-INV-STR        06150000
                  WHEN  TFRD054-FROM-LOC-INV-BEFORE                     06160000
                        ADD TFRD054-EXTENDED-RETAIL                     06170000
                                          TO PH-FR-BEF-INV-SUBTOTAL     06180000
                        MOVE TFRD054-EXTENDED-RETAIL                    06190000
                                          TO DL1A-FR-BEF-INV-STR        06200000
                  WHEN  TFRD054-FROM-LOC-INV-NORMAL                     06210000
                        CONTINUE                                        06220000
               END-EVALUATE                                             06230000
           END-IF.                                                      06240000
                                                                        06250000
           IF  TFRD054-ORIG-TO-LOC-NBR = TFRD054-TO-LOC-NBR             06260000
               EVALUATE TRUE                                            06270000
                  WHEN  TFRD054-TO-LOC-INV-AFTER                        06280000
                        ADD TFRD054-EXTENDED-RETAIL                     06290000
                                          TO PH-TO-AFT-INV-SUBTOTAL     06300000
                        MOVE TFRD054-EXTENDED-RETAIL                    06310000
                                          TO DL1A-TO-AFT-INV-STR        06320000
                  WHEN  TFRD054-TO-LOC-INV-BEFORE                       06330000
                        ADD TFRD054-EXTENDED-RETAIL                     06340000
                                          TO PH-TO-BEF-INV-SUBTOTAL     06350000
                        MOVE TFRD054-EXTENDED-RETAIL                    06360000
                                          TO DL1A-TO-BEF-INV-STR        06370000
                  WHEN  TFRD054-TO-LOC-INV-NORMAL                       06380000
                        CONTINUE                                        06390000
                END-EVALUATE                                            06400000
           END-IF.                                                      06410000
                                                                        06420000
       EJECT.                                                           06430000
      ***************************************************************** 06440000
      **WRITE DETAIL LINE SUBTOTAL                                      06450000
      ***************************************************************** 06460000
       3300-WRITE-DETAIL-LNE-SUBTOTAL.                                  06470000
                                                                        06480000
           MOVE '3300-WRITE-DETAIL-LNE-SUBTOTAL' TO PV-PARAGRAPH-NAME.  06490000
                                                                        06500000
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE1A.            06510000
                                                                        06520000
           MOVE 'TOTALS'                 TO DL1A-DOC-NBR.               06530000
           MOVE SPACES                   TO DL1A-DASH.                  06540000
           MOVE SPACES                   TO DL1A-DOC-FROM-LOC-NBR.      06550000
           MOVE SPACES TO DL1A-FR-STR                                   06560000
                          DL1A-TO-STR                                   06570000
                          DL1A-ASTERISK.                                06580000
                                                                        06590000
           MOVE PH-TOTAL-RETAIL-SUBTOTAL TO DL1A-TOTAL-RETAIL.          06600000
           MOVE PH-FR-AFT-INV-SUBTOTAL   TO DL1A-FR-AFT-INV-STR.        06610000
           MOVE PH-FR-BEF-INV-SUBTOTAL   TO DL1A-FR-BEF-INV-STR.        06620000
           MOVE PH-TO-AFT-INV-SUBTOTAL   TO DL1A-TO-AFT-INV-STR.        06630000
           MOVE PH-TO-BEF-INV-SUBTOTAL   TO DL1A-TO-BEF-INV-STR.        06640000
                                                                        06650000
           ADD  PH-TOTAL-RETAIL-SUBTOTAL TO PH-TOTAL-RETAIL-TOTAL.      06660000
           ADD  PH-FR-AFT-INV-SUBTOTAL   TO PH-FR-AFT-INV-TOTAL         06670000
           ADD  PH-FR-BEF-INV-SUBTOTAL   TO PH-FR-BEF-INV-TOTAL         06680000
           ADD  PH-TO-AFT-INV-SUBTOTAL   TO PH-TO-AFT-INV-TOTAL         06690000
           ADD  PH-TO-BEF-INV-SUBTOTAL   TO PH-TO-BEF-INV-TOTAL         06700000
                                                                        06710000
           WRITE REPORT-RECORD FROM DL-DETAIL-LINE1A.                   06720000
           WRITE REPORT-RECORD FROM BL-BLANK-LINE.                      06730000
                                                                        06740000
           IF  PC-INPUT-RECORDS-READ > 1                                06750000
               PERFORM 3400-SAVE-SUBTOTAL-FOR-SUMMARY                   06760000
           END-IF.                                                      06770000
                                                                        06780000
           INITIALIZE PH-TOTAL-RETAIL-SUBTOTAL                          06790000
021006                DL-DETAIL-LINE1A                                  06800000
                      PH-FR-AFT-INV-SUBTOTAL                            06810000
                      PH-FR-BEF-INV-SUBTOTAL                            06820000
                      PH-TO-AFT-INV-SUBTOTAL                            06830000
                      PH-TO-BEF-INV-SUBTOTAL.                           06840000
                                                                        06850000
           ADD +3                        TO PC-REPORT-LINE-COUNTER.     06860000
                                                                        06870000
       EJECT.                                                           06880000
                                                                        06890000
      ***************************************************************** 06900000
      **SAVE SUBTOTAL LINE TO GENERATE SUMMARY REPORT FOR GIVEN BATCH   06910000
      **NUMBER.                                                         06920000
      ***************************************************************** 06930000
       3400-SAVE-SUBTOTAL-FOR-SUMMARY.                                  06940000
                                                                        06950000
           ADD 1 TO PA-SUM-TBL-SUB.                                     06960000
                                                                        06970000
           MOVE HL5-ADJ-RSN              TO PA-ADJ-RSN-CDE              06980000
                                            (PA-SUM-TBL-SUB).           06990000
           STRING PH-TYP-1ST-LNE-DESC DELIMITED BY '  '                 07000000
                  PH-TYP-2ND-LNE-DESC DELIMITED BY '  '                 07010000
           INTO PA-TRANSFER-TYPE (PA-SUM-TBL-SUB).                      07020000
           MOVE PH-TOTAL-RETAIL-SUBTOTAL TO PA-TOTAL-RETAIL             07030000
                                            (PA-SUM-TBL-SUB).           07040000
           MOVE PH-FR-BEF-INV-SUBTOTAL   TO PA-FR-BEF-INV               07050000
                                            (PA-SUM-TBL-SUB).           07060000
           MOVE PH-FR-AFT-INV-SUBTOTAL   TO PA-FR-AFT-INV               07070000
                                            (PA-SUM-TBL-SUB).           07080000
           MOVE PH-TO-BEF-INV-SUBTOTAL   TO PA-TO-BEF-INV               07090000
                                            (PA-SUM-TBL-SUB).           07100000
           MOVE PH-TO-AFT-INV-SUBTOTAL   TO PA-TO-AFT-INV               07110000
                                            (PA-SUM-TBL-SUB).           07120000
                                                                        07130000
      ***************************************************************** 07140000
      **WRITE DETAIL LINE TOTAL                                         07150000
      ***************************************************************** 07160000
       3500-WRITE-DETAIL-LNE-TOTAL.                                     07170000
                                                                        07180000
           MOVE '3500-WRITE-DETAIL-LNE-TOTAL' TO PV-PARAGRAPH-NAME.     07190000
                                                                        07200000
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE1A.            07210000
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE1A.            07220000
                                                                        07230000
           MOVE 'TOTALS'                 TO DL1A-DOC-NBR.               07240000
           MOVE SPACES                   TO DL1A-DASH.                  07250000
           MOVE SPACES                   TO DL1A-DOC-FROM-LOC-NBR.      07260000
           MOVE SPACES TO DL1A-TRANSFER-TYPE                            07270000
                          DL1A-FR-STR                                   07280000
                          DL1A-TO-STR                                   07290000
                          DL1A-ASTERISK.                                07300000
           MOVE PH-TOTAL-RETAIL-TOTAL    TO DL1A-TOTAL-RETAIL.          07310000
           MOVE PH-FR-AFT-INV-TOTAL      TO DL1A-FR-AFT-INV-STR.        07320000
           MOVE PH-FR-BEF-INV-TOTAL      TO DL1A-FR-BEF-INV-STR.        07330000
           MOVE PH-TO-AFT-INV-TOTAL      TO DL1A-TO-AFT-INV-STR.        07340000
           MOVE PH-TO-BEF-INV-TOTAL      TO DL1A-TO-BEF-INV-STR.        07350000
                                                                        07360000
           WRITE REPORT-RECORD FROM DL-DETAIL-LINE1A.                   07370000
                                                                        07380000
021006     INITIALIZE DL-DETAIL-LINE1A.                                 07390000
                                                                        07400000
           ADD +3                        TO PC-REPORT-LINE-COUNTER.     07410000
                                                                        07420000
       EJECT.                                                           07430000
      ***************************************************************** 07440000
      **WRITE SUMMARY REPORT HEADINGS                                   07450000
      ***************************************************************** 07460000
       4100-WRITE-SUMMARY-HEADINGS.                                     07470000
                                                                        07480000
           MOVE '4100-WRITE-SUMMARY-HEADINGS' TO PV-PARAGRAPH-NAME.     07490000
                                                                        07500000
           INITIALIZE PC-REPORT-LINE-COUNTER.                           07510000
                                                                        07520000
           ADD +1                      TO PC-PAGE-COUNTER.              07530000
           MOVE PC-PAGE-COUNTER        TO DP132O-PAGE-NUMBER.           07540000
                                                                        07550000
           MOVE PC-SUMMARY-HEADER      TO HL5-REPORT-TYPE.              07560000
                                                                        07570000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      07580000
                               AFTER ADVANCING PAGE.                    07590000
                                                                        07600000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE1.                    07610000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE2.                    07620000
      ****WRITE HEADING LINE WITH THE WORD SUMMARY IN IT SO RDS CAN     07630000
      ****BREAK ON THIS PAGE.                                           07640000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE2A.                   07650000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE3.                    07660000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE4.                    07670000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE5.                    07680000
                                                                        07690000
           WRITE REPORT-RECORD FROM HL-HEADER-LINE6                     07700000
                               AFTER ADVANCING 2 LINES.                 07710000
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE1.             07720000
                                                                        07730000
           ADD +10             TO PC-REPORT-LINE-COUNTER.               07740000
                                                                        07750000
       EJECT.                                                           07760000
      ***************************************************************** 07770000
      **WRITE SUMMARY REPORT LINES                                      07780000
      ***************************************************************** 07790000
       4200-WRITE-SUMMARY-LINE.                                         07800000
                                                                        07810000
           MOVE '4200-WRITE-SUMMARY-LINE' TO PV-PARAGRAPH-NAME.         07820000
                                                                        07830000
           PERFORM VARYING PA-COUNTER                                   07840000
               FROM 1 BY 1                                              07850000
                   UNTIL PA-COUNTER > PA-SUM-TBL-SUB                    07860000
                       PERFORM 4300-WRITE-SUMMARY-LINE                  07870000
           END-PERFORM.                                                 07880000
                                                                        07890000
           INITIALIZE PA-SUMMARY-REPORT-LINE.                           07900000
                                                                        07910000
       EJECT.                                                           07920000
      ***************************************************************** 07930000
      **WRITE SUMMARY REPORT LINES                                      07940000
      ***************************************************************** 07950000
       4300-WRITE-SUMMARY-LINE.                                         07960000
                                                                        07970000
           MOVE '4300-WRITE-SUMMARY-LINE' TO PV-PARAGRAPH-NAME.         07980000
                                                                        07990000
           IF   PA-ADJ-RSN-CDE (PA-COUNTER) NOT = PH-ADJ-RSN-CDE-12     08000000
               PERFORM 4400-WRITE-SUMMARY-LNE-TOTAL                     08010000
               MOVE SPACES TO HL5-SNAPSHOT-NOTE                         08020000
               MOVE PC-ADJUSTMENTS              TO HL5-ADJ-RSN          08030000
               MOVE PA-ADJ-RSN-CDE (PA-COUNTER) TO PH-ADJ-RSN-CDE-12    08040000
               WRITE REPORT-RECORD FROM HL-HEADER-LINE5                 08050000
                   AFTER ADVANCING 3 LINES                              08060000
           END-IF.                                                      08070000
                                                                        08080000
           MOVE PA-TRANSFER-TYPE (PA-COUNTER) TO DL1-TRANSFER-TYPE.     08090000
           MOVE PA-TOTAL-RETAIL  (PA-COUNTER) TO DL1-TOTAL-RETAIL.      08100000
           MOVE PA-FR-BEF-INV    (PA-COUNTER) TO DL1-FR-BEF-INV-STR.    08110000
           MOVE PA-FR-AFT-INV    (PA-COUNTER) TO DL1-FR-AFT-INV-STR.    08120000
           MOVE PA-TO-BEF-INV    (PA-COUNTER) TO DL1-TO-BEF-INV-STR.    08130000
           MOVE PA-TO-AFT-INV    (PA-COUNTER) TO DL1-TO-AFT-INV-STR.    08140000
                                                                        08150000
           WRITE REPORT-RECORD FROM DL-DETAIL-LINE1.                    08160000
                                                                        08170000
           ADD PA-TOTAL-RETAIL (PA-COUNTER) TO PH-TOTAL-RETAIL-SUBTOTAL.08180000
           ADD PA-FR-BEF-INV   (PA-COUNTER) TO PH-FR-BEF-INV-SUBTOTAL.  08190000
           ADD PA-FR-AFT-INV   (PA-COUNTER) TO PH-FR-AFT-INV-SUBTOTAL.  08200000
           ADD PA-TO-BEF-INV   (PA-COUNTER) TO PH-TO-BEF-INV-SUBTOTAL.  08210000
           ADD PA-TO-AFT-INV   (PA-COUNTER) TO PH-TO-AFT-INV-SUBTOTAL.  08220000
                                                                        08230000
           ADD +1                      TO PC-SUMMARY-RECORDS-WRITTEN.   08240000
                                                                        08250000
       EJECT.                                                           08260000
      ***************************************************************** 08270000
      **WRITE SUMMARY REPORT TOTAL LINE                                 08280000
      ***************************************************************** 08290000
       4400-WRITE-SUMMARY-LNE-TOTAL.                                    08300000
                                                                        08310000
           MOVE '4400-WRITE-SUMMARY-LNE-TOTAL' TO PV-PARAGRAPH-NAME.    08320000
                                                                        08330000
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE1.             08340000
           MOVE 'TOTAL'                      TO DL1-TRANSFER-TYPE.      08350000
           MOVE PH-TOTAL-RETAIL-SUBTOTAL     TO DL1-TOTAL-RETAIL.       08360000
           MOVE PH-FR-BEF-INV-SUBTOTAL       TO DL1-FR-BEF-INV-STR.     08370000
           MOVE PH-FR-AFT-INV-SUBTOTAL       TO DL1-FR-AFT-INV-STR.     08380000
           MOVE PH-TO-BEF-INV-SUBTOTAL       TO DL1-TO-BEF-INV-STR.     08390000
           MOVE PH-TO-AFT-INV-SUBTOTAL       TO DL1-TO-AFT-INV-STR.     08400000
                                                                        08410000
           WRITE REPORT-RECORD FROM DL-DETAIL-LINE1.                    08420000
                                                                        08430000
           ADD PH-TOTAL-RETAIL-SUBTOTAL      TO PH-TOTAL-RETAIL-TOTAL.  08440000
           ADD PH-FR-BEF-INV-SUBTOTAL        TO PH-FR-BEF-INV-TOTAL.    08450000
           ADD PH-FR-AFT-INV-SUBTOTAL        TO PH-FR-AFT-INV-TOTAL.    08460000
           ADD PH-TO-BEF-INV-SUBTOTAL        TO PH-TO-BEF-INV-TOTAL.    08470000
           ADD PH-TO-AFT-INV-SUBTOTAL        TO PH-TO-AFT-INV-TOTAL.    08480000
                                                                        08490000
           INITIALIZE PH-TOTAL-RETAIL-SUBTOTAL                          08500000
                      PH-FR-AFT-INV-SUBTOTAL                            08510000
                      PH-FR-BEF-INV-SUBTOTAL                            08520000
                      PH-TO-AFT-INV-SUBTOTAL                            08530000
                      PH-TO-BEF-INV-SUBTOTAL.                           08540000
                                                                        08550000
       EJECT.                                                           08560000
      ***************************************************************** 08570000
      **WRITE THE LAST SUMMARY TOTAL LINE                               08580000
      ***************************************************************** 08590000
       4500-WRITE-FINAL-SUMMARY-TOTAL.                                  08600000
                                                                        08610000
           MOVE '4500-WRITE-FINAL-SUMMARY-TOTAL' TO PV-PARAGRAPH-NAME.  08620000
                                                                        08630000
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE1.             08640000
           MOVE 'TOTAL'                      TO DL1-TRANSFER-TYPE.      08650000
           MOVE PH-TOTAL-RETAIL-SUBTOTAL     TO DL1-TOTAL-RETAIL.       08660000
           MOVE PH-FR-BEF-INV-SUBTOTAL       TO DL1-FR-BEF-INV-STR.     08670000
           MOVE PH-FR-AFT-INV-SUBTOTAL       TO DL1-FR-AFT-INV-STR.     08680000
           MOVE PH-TO-BEF-INV-SUBTOTAL       TO DL1-TO-BEF-INV-STR.     08690000
           MOVE PH-TO-AFT-INV-SUBTOTAL       TO DL1-TO-AFT-INV-STR.     08700000
                                                                        08710000
           WRITE REPORT-RECORD FROM DL-DETAIL-LINE1.                    08720000
                                                                        08730000
       EJECT.                                                           08740000
      ***************************************************************** 08750000
      **WRITE SUMMARY REPORT GRAND TOTAL LINE                           08760000
      ***************************************************************** 08770000
       4600-WRITE-SUMMARY-GRANDTOTAL.                                   08780000
                                                                        08790000
           MOVE '4600-WRITE-SUMMARY-GRANDTOTAL' TO PV-PARAGRAPH-NAME.   08800000
      **** ADD ADJUSTMENTS TO RECEIPTS                                  08810000
           ADD PH-TOTAL-RETAIL-SUBTOTAL TO PH-TOTAL-RETAIL-TOTAL        08820000
               GIVING PH-TOTAL-RETAIL-TOTAL.                            08830000
           ADD PH-FR-BEF-INV-SUBTOTAL   TO PH-FR-BEF-INV-TOTAL          08840000
               GIVING PH-FR-BEF-INV-TOTAL.                              08850000
           ADD PH-FR-AFT-INV-SUBTOTAL   TO PH-FR-AFT-INV-TOTAL          08860000
               GIVING PH-FR-AFT-INV-TOTAL.                              08870000
           ADD PH-TO-BEF-INV-SUBTOTAL   TO PH-TO-BEF-INV-TOTAL          08880000
               GIVING PH-TO-BEF-INV-TOTAL.                              08890000
           ADD PH-TO-AFT-INV-SUBTOTAL   TO PH-TO-AFT-INV-TOTAL          08900000
               GIVING PH-TO-AFT-INV-TOTAL.                              08910000
           MOVE 'GRANDTOTAL'                 TO DL1-TRANSFER-TYPE.      08920000
           MOVE PH-TOTAL-RETAIL-TOTAL        TO DL1-TOTAL-RETAIL.       08930000
           MOVE PH-FR-BEF-INV-TOTAL          TO DL1-FR-BEF-INV-STR.     08940000
           MOVE PH-FR-AFT-INV-TOTAL          TO DL1-FR-AFT-INV-STR.     08950000
           MOVE PH-TO-BEF-INV-TOTAL          TO DL1-TO-BEF-INV-STR.     08960000
           MOVE PH-TO-AFT-INV-TOTAL          TO DL1-TO-AFT-INV-STR.     08970000
           WRITE REPORT-RECORD FROM DL-DETAIL-LINE1                     08980000
               AFTER ADVANCING 2 LINES.                                 08990000
                                                                        09000000
       EJECT.                                                           09010000
      ***************************************************************** 09020000
      **READ INPUT FILE                                                 09030000
      ***************************************************************** 09040000
       5000-READ-INPUT-FILE.                                            09050007
                                                                        09060000
           READ INPUT-FILE INTO TFRD054-ML-DAILY-TRANSFER-FILE          09070000
                AT END SET PS-END-OF-INPUT-FILE TO TRUE                 09080000
           END-READ.                                                    09090000
                                                                        09100000
           IF  PS-NOT-END-OF-INPUT-FILE                                 09110000
               ADD +1                           TO PC-INPUT-RECORDS-READ09120000
           ELSE                                                         09130000
               INITIALIZE TFRD054-ML-DAILY-TRANSFER-FILE                09140006
           END-IF.                                                      09150000
                                                                        09160000
       EJECT.                                                           09170000
      ****************************************************************  09180007
      *GET TRANSFER CREATE DATE FOR PREVIOUS DAYS TRANSFERS             09190007
      ****************************************************************  09200007
       5100-GET-PREVIOUS-DAY.                                           09210007
                                                                        09220007
           MOVE '5100-GET-PREVIOUS-DAY'     TO PV-PARAGRAPH-NAME.       09230007
                                                                        09240007
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              09250007
           MOVE DF-FORMAT-DATE               TO DPG52-LK-DATE-INPUT.    09260007
           SET DPG51-ACTUAL-CALENDAR-ONLY    TO TRUE.                   09270007
           SET DPG51-DECREMENT-DATE          TO TRUE.                   09280007
           SET DPG52-LK-DTE-GREG-FMT         TO TRUE.                   09290007
           MOVE 1                            TO DPG51-INCR-DECR-DAYS-9. 09300007
                                                                        09310007
           CALL DP500-KOHLS-CALENDAR-ROUTINE                            09320007
                      USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.        09330007
                                                                        09340007
           EVALUATE TRUE                                                09350007
                WHEN DPG54-NO-ERROR                                     09360007
                   MOVE DPG55-GREG-DATE-FMT    TO TFRD054-CREATE-DATE   09370007
                WHEN OTHER                                              09380007
                   MOVE DF-FORMAT-DATE         TO TFRD054-CREATE-DATE   09390007
           END-EVALUATE.                                                09400007
                                                                        09410007
       EJECT.                                                           09420007
      ******************************************************************09430007
      **MOVE DATA TO FIELDS IN HEADING FOR REPORT                       09440007
      ******************************************************************09450007
       6100-HEADING-PREP.                                               09460007
                                                                        09470007
           MOVE '1100-HEADING-PREP'     TO PV-PARAGRAPH-NAME.           09480007
                                                                        09490007
      ***PREPS KOHLS STANDARD HEADING                                   09500007
           MOVE PC-CURRENT-PROGRAM-NAME TO DP132O-PROGRAM-NAME.         09510007
           MOVE DF-FORMAT-DATE          TO DP132O-RUN-DATE.             09520007
           MOVE DF-ACCEPT-HR            TO DP132O-RUN-HOUR.             09530007
           MOVE DF-ACCEPT-MN            TO DP132O-RUN-MINUTE.           09540007
           MOVE CC-REPORT-NUMBER        TO DP132O-REPORT-NUMBER.        09550007
           MOVE CC-HEADING              TO HL2-REPORT-TITLE.            09560007
                                                                        09570007
       EJECT.                                                           09580007
      ****************************************************************  09590000
      *END OF PROGRAM ROUTINE                                           09600000
      ****************************************************************  09610000
       9999-WRAPUP.                                                     09620000
                                                                        09630000
           MOVE '9999-WRAPUP'               TO PV-PARAGRAPH-NAME.       09640000
                                                                        09650000
      *IF NO REPORT DETAIL, INDICATE THAT THERE IS NO DATA TO           09660000
      *PROCESS FOR BOTH DETAIL AND SUMMARY REPORTS                      09670000
           IF PC-INPUT-RECORDS-READ = 0                                 09680000
               PERFORM 5100-GET-PREVIOUS-DAY                            09690007
               PERFORM 6100-HEADING-PREP                                09700007
               MOVE TFRD052-JULIAN-DAY TO TFRD054-BATCH-NBR             09710006
      **** WRITE DETAIL RECEIPTS REPORT                                 09720000
               MOVE PC-COMPLETED       TO TFRD054-ADJ-RSN-CDE           09730000
               PERFORM 3100-WRITE-DETAIL-HEADINGS                       09740000
               WRITE REPORT-RECORD FROM ND-NO-DATA-LINE                 09750000
      **** WRITE DETAIL ADJUSTMENTS REPORT                              09760000
               MOVE SPACES             TO TFRD054-ADJ-RSN-CDE           09770000
               PERFORM 3100-WRITE-DETAIL-HEADINGS                       09780000
               WRITE REPORT-RECORD FROM ND-NO-DATA-LINE                 09790000
      **** WRITE SUMMARY REPORT                                         09800000
               ADD 1 TO DP132O-REPORT-NUMBER                            09810000
               PERFORM 4100-WRITE-SUMMARY-HEADINGS                      09820000
               WRITE REPORT-RECORD FROM ND-NO-DATA-LINE                 09830000
           END-IF.                                                      09840000
                                                                        09850000
           CLOSE INPUT-FILE                                             09860000
                 HEADING-CARD                                           09870000
                 BATCH-NBR-CARD                                         09880000
                 REPORT-FILE.                                           09890000
                                                                        09900000
           PERFORM 9999-DISPLAY-STATISTICS.                             09910000
                                                                        09920000
       EJECT.                                                           09930000
      ****************************************************************  09940000
      *DISPLAY PROGRAM STATISTICS                                       09950000
      ****************************************************************  09960000
       9999-DISPLAY-STATISTICS.                                         09970000
                                                                        09980000
           DISPLAY '*************************************************'. 09990000
           DISPLAY '* PROGRAM...............: ' PC-CURRENT-PROGRAM-NAME.10000000
           DISPLAY '* DATE..................: ' DF-ACCEPT-DATE.         10010000
           DISPLAY '* TIMESTAMP.............: ' DF-ACCEPT-TIME.         10020000
           DISPLAY '*************************************************'. 10030000
           DISPLAY '* INPUT RECORDS READ.........: '                    10040000
                                          PC-INPUT-RECORDS-READ.        10050000
           DISPLAY '* DETAIL RECORDS WRITTEN.....: '                    10060000
                                          PC-DETAIL-RECORDS-WRITTEN.    10070000
           DISPLAY '* SUMMARY RECORDS WRITTEN....: '                    10080000
                                          PC-SUMMARY-RECORDS-WRITTEN.   10090000
           DISPLAY '*************************************************'. 10100000
                                                                        10110000
       EJECT.                                                           10120000
