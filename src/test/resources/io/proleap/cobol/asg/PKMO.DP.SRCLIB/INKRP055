       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKRP055                                                  
       AUTHOR.        JIMMY/INFOSYS.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10/14/08.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM WAS COPIED FROM MLKRP055.                                  
      *                                                                         
      * THIS PROGRAM READS THE EDIT SPLIT REPORT DATA FILE AND                  
      * REPORTS MERCHANDISE LEDGER AND INVENTORY LEDGER COST AND RETAIL         
      * AMOUNTS.                                                                
      *                                                                         
      * THE REPORT SEQUENCE IS DOCUMENT NUMBER WITHIN BATCH WITHIN              
      * FINANCIAL LIABILITY DATE WITHIN TRANSACTION CODE WITHIN STORE.          
      * TOTALS ARE SHOWN AT EACH LEVEL EXCEPT THE BATCH LEVEL.                  
      * ALL TOTALS ARE FOR THE CURRENT FISCAL PERIOD                            
      * AMOUNTS, AND A FINAL TOTAL PAGE IS PRINTED AT THE END.                  
      *                                                                         
      * WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES                  
      * ------- ---------- ------------ --------------------------------        
      * W33304  10-14-2008 JIMMY  /      NEW PROGRAM CREATED AS PART            
      *                    INFOSYS       OF HOLDING INVENTORY LEDGER            
      *                                  OPEN - HILO                            
      ******************************************************************        
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT EDIT-SPLIT-FILE                                               
               ASSIGN TO INP01.                                                 
           SELECT STORE-SPLIT-REPORT                                            
               ASSIGN TO RPT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  EDIT-SPLIT-FILE                                                      
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F                                                 
           DATA RECORD IS EDIT-SPLIT-RECORD.                                    
       01  EDIT-SPLIT-RECORD           PIC X(110).                              
                                                                                
       FD  STORE-SPLIT-REPORT                                                   
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 133 CHARACTERS                                       
           RECORDING  MODE IS F                                                 
           DATA RECORD IS STORE-SPLIT-REPORT-LINE.                              
       01  STORE-SPLIT-REPORT-LINE      PIC X(133).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES               PIC S9(04)    VALUE +3              
                                                              COMP SYNC.        
           05  PC-LINE-LIMIT-FOR-DTL        PIC S9(04)    VALUE +57             
                                                              COMP SYNC.        
           05  PC-LINE-LIMIT-FOR-HD-TC      PIC S9(04)    VALUE +54             
                                                              COMP SYNC.        
           05  PC-OPER-CO                   PIC  X(02)    VALUE '03'.           
           05  PC-EFF-DATE-HD-LIT           PIC  X(08)    VALUE                 
                                                             'EFF DATE'.        
           05  PC-BATCH-HD-LIT              PIC  X(05)    VALUE                 
                                                             'BATCH'.           
           05  PC-DOCUMENT-HD-LIT           PIC  X(08)    VALUE                 
                                                             'DOCUMENT'.        
           05  PC-INVENTORY-ONLY-MODE       PIC  X(20)    VALUE                 
                                                     '(INVENTORY ONLY)'.        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-EDIT-LOC-NBR              PIC 9(05)     VALUE ZEROS.          
           05  PV-EDIT-LOC-NBR-ALPH REDEFINES                                   
               PV-EDIT-LOC-NBR              PIC X(05).                          
           05  PV-ADVANCE                   PIC S9(03)    VALUE ZERO            
                                                          COMP-3.               
           05  PV-PAGE-COUNT                PIC S9(05)    VALUE ZERO.           
           05  PV-LINE-COUNT                PIC S9(04)    VALUE ZERO            
                                                          COMP SYNC.            
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACE.          
           05  PV-ABEND-MESSAGE             PIC  X(75)    VALUE SPACE.          
           05  PV-SQL-SUB                   PIC  9(03)    VALUE ZERO.           
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30)    VALUE SPACE.          
                                                                                
           05  PV-YYMMDD-DATE.                                                  
               10  PV-YYMMDD-YY             PIC  X(02)    VALUE SPACE.          
               10  PV-YYMMDD-MM             PIC  X(02)    VALUE SPACE.          
               10  PV-YYMMDD-DD             PIC  X(02)    VALUE SPACE.          
           05  PV-ISO-FMT-DATE.                                                 
               10  PV-ISO-FMT-CC            PIC  X(02).                         
               10  PV-ISO-FMT-YY            PIC  X(02).                         
               10  FILLER                   PIC  X.                             
               10  PV-ISO-FMT-MM            PIC  X(02).                         
               10  FILLER                   PIC  X.                             
               10  PV-ISO-FMT-DD            PIC  X(02).                         
           05  PV-TIME-HHMMSS.                                                  
               10  PV-TIME-HHMMSS-HH        PIC  X(02)    VALUE SPACES.         
               10  PV-TIME-HHMMSS-MM        PIC  X(02)    VALUE SPACES.         
               10  PV-TIME-HHMMSS-SS        PIC  X(02)    VALUE SPACES.         
           05  PV-LAST-CNTL-CARD-READ       PIC  X(80).                         
           05  PV-EDIT-STORE                PIC X(05)     VALUE SPACES.         
           05  PV-EDIT-STORE-NUM REDEFINES                                      
               PV-EDIT-STORE                PIC 9(05).                          
                                                                                
       01  TOT-TOTAL-FIELDS.                                                    
           05  TOT-DOC-NBR-TOTALS.                                              
               10  TOT-DOC-CURR-ML-COST      PIC  S9(13)V99 COMP-3.             
               10  TOT-DOC-CURR-ML-RETAIL    PIC  S9(13)V99 COMP-3.             
               10  TOT-DOC-CURR-INV-COST     PIC  S9(13)V99 COMP-3.             
               10  TOT-DOC-CURR-INV-RETAIL   PIC  S9(13)V99 COMP-3.             
           05  TOT-BCH-BATCH-NBR-TOTALS.                                        
               10  TOT-BCH-CURR-ML-COST      PIC  S9(13)V99 COMP-3.             
               10  TOT-BCH-CURR-ML-RETAIL    PIC  S9(13)V99 COMP-3.             
               10  TOT-BCH-CURR-INV-COST     PIC  S9(13)V99 COMP-3.             
               10  TOT-BCH-CURR-INV-RETAIL   PIC  S9(13)V99 COMP-3.             
           05  TOT-FLB-FIN-LIAB-DTE-TOTALS.                                     
               10  TOT-FLB-CURR-ML-COST      PIC  S9(13)V99 COMP-3.             
               10  TOT-FLB-CURR-ML-RETAIL    PIC  S9(13)V99 COMP-3.             
               10  TOT-FLB-CURR-INV-COST     PIC  S9(13)V99 COMP-3.             
               10  TOT-FLB-CURR-INV-RETAIL   PIC  S9(13)V99 COMP-3.             
           05  TOT-TCD-TRANS-CDE-TOTALS.                                        
               10  TOT-TCD-CURR-ML-COST      PIC  S9(13)V99 COMP-3.             
               10  TOT-TCD-CURR-ML-RETAIL    PIC  S9(13)V99 COMP-3.             
               10  TOT-TCD-CURR-INV-COST     PIC  S9(13)V99 COMP-3.             
               10  TOT-TCD-CURR-INV-RETAIL   PIC  S9(13)V99 COMP-3.             
           05  TOT-STR-STORE-TOTALS.                                            
               10  TOT-STR-CURR-ML-COST      PIC  S9(13)V99 COMP-3.             
               10  TOT-STR-CURR-ML-RETAIL    PIC  S9(13)V99 COMP-3.             
               10  TOT-STR-CURR-INV-COST     PIC  S9(13)V99 COMP-3.             
               10  TOT-STR-CURR-INV-RETAIL   PIC  S9(13)V99 COMP-3.             
           05  TOT-FNL-FINAL-TOTALS.                                            
               10  TOT-FNL-CURR-ML-COST      PIC  S9(13)V99 COMP-3.             
               10  TOT-FNL-CURR-ML-RETAIL    PIC  S9(13)V99 COMP-3.             
               10  TOT-FNL-CURR-INV-COST     PIC  S9(13)V99 COMP-3.             
               10  TOT-FNL-CURR-INV-RETAIL   PIC  S9(13)V99 COMP-3.             
                                                                                
       01  SA-SAVE-AREAS.                                                       
           05  SA-PREV-STORE-NBR       PIC X(05)   VALUE SPACE.                 
           05  SA-PREV-TRANS-CDE       PIC X(02)   VALUE SPACE.                 
               88  SA-PREV-TRANS-PURCHASE          VALUE '10'.                  
               88  SA-PREV-TRANS-SALE              VALUE '20'.                  
               88  SA-PREV-TRANS-TRANSFER-FROM     VALUE '30'.                  
               88  SA-PREV-TRANS-TRANSFER-TO       VALUE '31'.                  
               88  SA-PREV-TRANS-MARKDOWN          VALUE '40'.                  
               88  SA-PREV-TRANS-MARKUP            VALUE '50'.                  
               88  SA-PREV-TRANS-ADJUSTMENT        VALUE '80'.                  
           05  SA-PREV-SYST-ORIG-CDE   PIC X(02)   VALUE SPACES.                
               88  SA-PREV-PURCH-RECEIPT           VALUE '08'.                  
           05  SA-PREV-FIN-LIAB-DTE    PIC X(10)   VALUE SPACE.                 
           05  SA-PREV-BATCH-NBR       PIC X(04)   VALUE SPACE.                 
           05  SA-PREV-DOC-NBR              PIC X(15).                          
           05  SA-PREV-FAST-NBR  REDEFINES  SA-PREV-DOC-NBR.                    
               10  SA-PREV-DOC-FAST-LIT     PIC X(04).                          
               10  SA-PREV-DOC-STORE        PIC X(04).                          
               10  SA-PREV-DOC-HYPHEN       PIC X.                              
               10  SA-PREV-DOC-SEQ          PIC X(06).                          
           05  SA-CURR-FSCL-YR-PER     PIC X(06)   VALUE SPACE.                 
                                                                                
           05  ED-DOC-NBR                   PIC X(15).                          
           05  ED-FAST-NBR       REDEFINES  ED-DOC-NBR.                         
               10  ED-DOC-FAST-LIT          PIC  X(04).                         
               10  ED-DOC-STORE             PIC  X(04).                         
               10  ED-DOC-HYPHEN            PIC  X.                             
               10  ED-DOC-SEQ               PIC  X(06).                         
       01  SW-SWITCHES.                                                         
           05  SW-END-OF-EDIT-SPLIT-FILE-SW PIC X         VALUE 'N'.            
               88  SW-END-OF-EDIT-SPLIT-FILE              VALUE 'Y'.            
               88  SW-NOT-END-OF-EDIT-SPLIT-FILE          VALUE 'N'.            
           05  SW-EMPTY-EDIT-SPLIT-FILE-SW  PIC X         VALUE 'N'.            
               88  SW-EMPTY-EDIT-SPLIT-FILE               VALUE 'Y'.            
               88  SW-NOT-EMPTY-EDIT-SPLIT-FILE           VALUE 'N'.            
           05  SW-STORE-BREAK-SW            PIC X         VALUE 'N'.            
               88  SW-STORE-BREAK                         VALUE 'Y'.            
               88  SW-NOT-STORE-BREAK                     VALUE 'N'.            
           05  SW-TRANS-CDE-BREAK-SW        PIC X         VALUE 'N'.            
               88  SW-TRANS-CDE-BREAK                     VALUE 'Y'.            
               88  SW-NOT-TRANS-CDE-BREAK                 VALUE 'N'.            
           05  SW-FIN-LIAB-BREAK-SW         PIC X         VALUE 'N'.            
               88  SW-FIN-LIAB-BREAK                      VALUE 'Y'.            
               88  SW-NOT-FIN-LIAB-BREAK                  VALUE 'N'.            
           05  SW-BATCH-NBR-BREAK-SW        PIC X         VALUE 'N'.            
               88  SW-BATCH-NBR-BREAK                     VALUE 'Y'.            
               88  SW-NOT-BATCH-NBR-BREAK                 VALUE 'N'.            
           05  SW-DETAIL-REPORTED-SW        PIC X         VALUE 'N'.            
               88  SW-DETAIL-REPORTED                     VALUE 'Y'.            
               88  SW-DETAIL-NOT-REPORTED                 VALUE 'N'.            
                                                                                
       01  FPN-FSCL-PER-NAME-TABLE-DATA.                                        
               15  FPN-ENTRY01-DATA         PIC X(09) VALUE 'FEBRUARY '.        
               15  FPN-ENTRY02-DATA         PIC X(09) VALUE 'MARCH    '.        
               15  FPN-ENTRY03-DATA         PIC X(09) VALUE 'APRIL    '.        
               15  FPN-ENTRY04-DATA         PIC X(09) VALUE 'MAY      '.        
               15  FPN-ENTRY05-DATA         PIC X(09) VALUE 'JUNE     '.        
               15  FPN-ENTRY06-DATA         PIC X(09) VALUE 'JULY     '.        
               15  FPN-ENTRY07-DATA         PIC X(09) VALUE 'AUGUST   '.        
               15  FPN-ENTRY08-DATA         PIC X(09) VALUE 'SEPTEMBER'.        
               15  FPN-ENTRY09-DATA         PIC X(09) VALUE 'OCTOBER  '.        
               15  FPN-ENTRY10-DATA         PIC X(09) VALUE 'NOVEMBER '.        
               15  FPN-ENTRY11-DATA         PIC X(09) VALUE 'DECEMBER '.        
               15  FPN-ENTRY12-DATA         PIC X(09) VALUE 'JANUARY  '.        
                                                                                
       01  FPN-FSCL-PER-NAME-TABLE          REDEFINES                           
           FPN-FSCL-PER-NAME-TABLE-DATA.                                        
           05  FPN-ENTRY                    OCCURS 12.                          
               10  FPN-FSCL-PER-NAME        PIC X(09).                          
                                                                                
       01  FPN-SUB-X.                                                           
           05  FPN-SUB                      PIC 9(02).                          
                                                                                
       01  TCT-TRANS-CDE-TABLE-DATA.                                            
           10  TCT-ENTRY1-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '10  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'PURCH ADJST'.          
                                                                                
           10  TCT-ENTRY8-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '1008'.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'RECEIPTS   '.          
                                                                                
           10  TCT-ENTRY2-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '20  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'SALES'.                
           10  TCT-ENTRY3-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '30  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'TRANSFERS OUT'.        
           10  TCT-ENTRY4-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '31  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'TRANSFERS IN'.         
           10  TCT-ENTRY5-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '40  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'MARKDOWNS'.            
           10  TCT-ENTRY6-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '50  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'MARKUPS'.              
           10  TCT-ENTRY7-DATA.                                                 
               15  FILLER                   PIC  X(04)    VALUE '80  '.         
               15  FILLER                   PIC  X(15)    VALUE                 
                                                        'ADJUSTMENTS'.          
                                                                                
       01  TCT-TRANS-CDE-TABLE              REDEFINES                           
           TCT-TRANS-CDE-TABLE-DATA.                                            
      *    *------------------------------------------------------------        
      *    *                !!! NOTE !!!                                        
      *    *------------------------------------------------------------        
      *    * IF ANY CHANGES ARE MADE TO THIS TABLE, THE HARD-CODED              
      *    * TABLE DATA (TCT-TRANS-CDE-TABLE-DATA) MUST BE CHANGED              
      *    * ACCORDINGLY.                                                       
      *    *                                                                    
      *    * IF THE  NUMBER OF ENTRIES IS CHANGED, TCT-MAX-ENTRIES              
      *    * MUST BE CHANGED TO REFLECT THE CORRECT NUMBER OF ENTRIES.          
      *    *------------------------------------------------------------        
           05  TCT-ENTRY                    OCCURS   8                          
                                            INDEXED BY TCT-IDX.                 
               10  TCT-TRANS-CDE            PIC  X(02).                         
               10  TCT-SYS-ORIG-CDE         PIC  X(02).                         
               10  TCT-TRANS-CDE-DESC       PIC  X(15).                         
                                                                                
       01  TCT-MAX-ENTRIES                  PIC S9(04)    VALUE +8              
                                                          COMP SYNC.            
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
      *****************************************************************         
      * EDIT-SPLIT-RECORD LAYOUT.                                               
      *****************************************************************         
                                                                                
           COPY INRD035.                                                        
                                                                                
      ******************************************************************        
      * STANDARD REPORT HEADING                                                 
      ******************************************************************        
                                                                                
           COPY DPWS133O.                                                       
                                                                                
       01  HD-ST-STORE-HEADING.                                                 
           05  FILLER                       PIC  X(44)    VALUE SPACE.          
           05  FILLER                       PIC  X(11)    VALUE                 
                                                          'EDIT SPLIT '.        
           05  FILLER                       PIC  X(11)    VALUE                 
                                                          'TOTALS FOR '.        
           05  FILLER                       PIC  X(06)    VALUE                 
                                                               'STORE '.        
           05  HD-ST-STORE-NBR              PIC  X(05).                         
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  HD-ST-STORE-NAME             PIC  X(25).                         
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  HD-ST-PROCESSING-MODE        PIC  X(20)    VALUE SPACE.          
                                                                                
       01  HD-FT-FINAL-TOTAL-HEADING.                                           
           05  FILLER                       PIC  X(56)    VALUE SPACE.          
           05  FILLER                       PIC  X(11)    VALUE                 
                                                          'EDIT SPLIT '.        
           05  FILLER                       PIC  X(12)    VALUE                 
                                                         'FINAL TOTALS'.        
           05  HD-FT-PROCESSING-MODE        PIC  X(20)    VALUE SPACE.          
                                                                                
       01  HD-ED-EDIT-DATE-HEADING.                                             
           05  FILLER                       PIC  X(58)    VALUE SPACE.          
           05  FILLER                       PIC  X(03)    VALUE 'FOR'.          
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  HD-ED-DATE.                                                      
               15  HD-ED-DATE-MM            PIC  X(02).                         
               15  FILLER                   PIC  X        VALUE '/'.            
               15  HD-ED-DATE-DD            PIC  X(02).                         
               15  FILLER                   PIC  X        VALUE '/'.            
               15  HD-ED-DATE-YY            PIC  X(02).                         
                                                                                
       01  HD-FP-FISCAL-PER-HEADING.                                            
           05  FILLER                       PIC  X(36)    VALUE SPACE.          
           05  FILLER                       PIC  X(10)    VALUE ALL '-'.        
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(04)    VALUE 'ML: '.         
           05  HD-FP-CURR-FISC-PERIOD-NAME  PIC  X(09).                         
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(10)    VALUE ALL '-'.        
           05  FILLER                       PIC  X(08)    VALUE SPACES.         
           05  FILLER                       PIC  X(09)    VALUE ALL '-'.        
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(04)    VALUE 'IN: '.         
           05  HD-FP-INV-FISC-PERIOD-NAME   PIC  X(09).                         
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(08)    VALUE ALL '-'.        
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
                                                                                
       01  HD-MI-ML-INV-HEADING.                                                
           05  FILLER                       PIC  X(45)    VALUE SPACE.          
           05  FILLER                       PIC  X(18)    VALUE                 
                                                   'MERCHANDISE LEDGER'.        
           05  FILLER                       PIC  X(24)    VALUE SPACE.          
           05  FILLER                       PIC  X(16)    VALUE                 
                                                     'INVENTORY LEDGER'.        
           05  FILLER                       PIC  X(63)    VALUE SPACE.          
       01  HD-CL-COLUMN-HEADING.                                                
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  HD-CL-EFF-DATE-HD-LIT        PIC  X(08)    VALUE SPACE.          
           05  FILLER                       PIC  X(03)    VALUE SPACE.          
           05  HD-CL-BATCH-HD-LIT           PIC  X(05)    VALUE SPACE.          
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
           05  HD-CL-DOCUMENT-HD-LIT        PIC  X(08)    VALUE SPACE.          
           05  FILLER                       PIC  X(15)    VALUE SPACE.          
           05  FILLER                       PIC  X(04)    VALUE 'COST'.         
           05  FILLER                       PIC  X(15)    VALUE SPACE.          
           05  FILLER                       PIC  X(06)    VALUE                 
                                                               'RETAIL'.        
           05  FILLER                       PIC  X(17)    VALUE SPACE.          
           05  FILLER                       PIC  X(04)    VALUE 'COST'.         
           05  FILLER                       PIC  X(16)    VALUE SPACE.          
           05  FILLER                       PIC  X(06)    VALUE                 
                                                               'RETAIL'.        
           05  FILLER                       PIC  X(16)    VALUE SPACE.          
                                                                                
       01  HD-TC-TRANS-CDE-HEADING.                                             
           05  HD-TC-TRANS-CDE-DESC         PIC  X(15).                         
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  DL-EFF-DATE.                                                     
               10  DL-EFF-DATE-MM           PIC  X(02).                         
               10  DL-EFF-DATE-SLASH1       PIC  X.                             
               10  DL-EFF-DATE-DD           PIC  X(02).                         
               10  DL-EFF-DATE-SLASH2       PIC  X.                             
               10  DL-EFF-DATE-YY           PIC  X(02).                         
           05  FILLER                       PIC  X(04)    VALUE SPACE.          
           05  DL-BATCH-NBR                 PIC  X(04).                         
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
           05  DL-DOC-NBR.                                                      
               10  DL-DOC-FAST-LIT          PIC  X(04)    VALUE 'FAST'.         
               10  DL-DOC-STORE             PIC  X(04).                         
               10  DL-DOC-HYPHEN            PIC  X        VALUE '-'.            
               10  DL-DOC-SEQ               PIC  X(06).                         
           05  FILLER                       PIC  X(03)    VALUE SPACE.          
           05  DL-CURR-ML-COST              PIC  ZZZ,ZZZ,ZZZ.99-.               
           05  FILLER                       PIC  X(05)    VALUE SPACE.          
           05  DL-CURR-ML-RETAIL            PIC  ZZZ,ZZZ,ZZZ.99-.               
           05  FILLER                       PIC  X(05)    VALUE SPACE.          
           05  DL-CURR-INV-COST             PIC  ZZZ,ZZZ,ZZZ.99-.               
           05  FILLER                       PIC  X(05)    VALUE SPACE.          
           05  DL-CURR-INV-RETAIL           PIC  ZZZ,ZZZ,ZZZ.99-.               
           05  FILLER                       PIC  X(05)    VALUE SPACE.          
                                                                                
       01  TL-FL1-FIN-LIAB-TOTAL-LINE-1.                                        
           05  FILLER                       PIC  X(2)     VALUE SPACE.          
           05  TL-FL1-EFF-DATE-MM           PIC  X(2).                          
           05  FILLER                       PIC  X        VALUE '/'.            
           05  TL-FL1-EFF-DATE-DD           PIC  X(2).                          
           05  FILLER                       PIC  X        VALUE '/'.            
           05  TL-FL1-EFF-DATE-YY           PIC  X(2).                          
                                                                                
       01  TL-FL2-FIN-LIAB-TOTAL-LINE-2.                                        
           05  FILLER                       PIC  X(02)    VALUE SPACE.          
           05  FILLER                       PIC  X(05)    VALUE 'TOTAL'.        
           05  FILLER                       PIC  X(26)    VALUE SPACE.          
           05  TL-FL2-CURR-ML-COST          PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-FL2-CURR-ML-RETAIL        PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-FL2-CURR-INV-COST         PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-FL2-CURR-INV-RETAIL       PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
                                                                                
       01  TL-TC1-TRANS-CDE-TOTAL-LINE-1.                                       
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-TC1-TRANS-CDE-DESC        PIC  X(15).                         
                                                                                
       01  TL-TC2-TRANS-CDE-TOTAL-LINE-2.                                       
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(05)    VALUE 'TOTAL'.        
           05  FILLER                       PIC  X(27)    VALUE SPACE.          
           05  TL-TC2-CURR-ML-COST          PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-TC2-CURR-ML-RETAIL        PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-TC2-CURR-INV-COST         PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-TC2-CURR-INV-RETAIL       PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
                                                                                
       01  TL-ST1-STORE-TOTAL-LINE-1.                                           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(05)    VALUE 'STORE'.        
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-ST1-STORE-NBR             PIC  X(05).                         
                                                                                
       01  TL-ST2-STORE-TOTAL-LINE-2.                                           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(05)    VALUE 'TOTAL'.        
           05  FILLER                       PIC  X(27)    VALUE SPACE.          
           05  TL-ST2-CURR-ML-COST          PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-ST2-CURR-ML-RETAIL        PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-ST2-CURR-INV-COST         PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-ST2-CURR-INV-RETAIL       PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
                                                                                
       01  TL-FN1-FINAL-TOTAL-LINE-1.                                           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(05)    VALUE 'FINAL'.        
                                                                                
       01  TL-FN2-FINAL-TOTAL-LINE-2.                                           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  FILLER                       PIC  X(05)    VALUE 'TOTAL'.        
           05  FILLER                       PIC  X(27)    VALUE SPACE.          
           05  TL-FN2-CURR-ML-COST          PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-FN2-CURR-ML-RETAIL        PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-FN2-CURR-INV-COST         PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
           05  TL-FN2-CURR-INV-RETAIL       PIC  ZZZ,ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                       PIC  X        VALUE SPACE.          
                                                                                
       01  ER-END-OF-REPORT-LINE.                                               
           05  FILLER                       PIC  X(21)    VALUE                 
                                                '*** END OF REPORT ***'.        
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DB2 STORE TABLE DCLGEN.                                                 
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
           OPEN INPUT  EDIT-SPLIT-FILE                                          
                OUTPUT STORE-SPLIT-REPORT.                                      
                                                                                
           PERFORM B100-PROCESS-CONTROL-CARD.                                   
                                                                                
           PERFORM B200-CREATE-REPORT.                                          
                                                                                
           CLOSE EDIT-SPLIT-FILE                                                
                 STORE-SPLIT-REPORT.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * MAKE SURE THAT THE EDIT-SPLIT FILE CONTAINS A CONTROL CARD AS           
      * THE FIRST RECORD. IF THE CONTROL CARD IS PRESENT, USE ITS               
      * FIELDS TO BUILD SOME OF THE HEADING FIELDS.                             
      ******************************************************************        
       B100-PROCESS-CONTROL-CARD.                                               
                                                                                
           MOVE 'B100-PROCESS-CONTROL-CARD' TO PV-PARAGRAPH-NAME.               
                                                                                
           PERFORM Z100-READ-EDIT-SPLIT-FILE.                                   
                                                                                
           IF SW-END-OF-EDIT-SPLIT-FILE                                         
               MOVE 'NO CONTROL CARD ON EDIT-SPLIT-FILE'                        
                          TO PV-ABEND-MESSAGE                                   
               MOVE SPACE TO EDIT-SPLIT-RECORD                                  
               MOVE +4003 TO ABEND-CODE                                         
               PERFORM Z999-ABEND                                               
           ELSE                                                                 
               IF NOT IN035-RPT1-CNTL-CARD-HDR-REC                              
                   MOVE '1ST RECORD ON EDIT-SPLIT-FILE NOT CONTROL CARD'        
                              TO PV-ABEND-MESSAGE                               
                   MOVE +4003 TO ABEND-CODE                                     
                   PERFORM Z999-ABEND                                           
               ELSE                                                             
                   PERFORM UNTIL IN035-RPT1-DETAIL-REC                          
                              OR SW-END-OF-EDIT-SPLIT-FILE                      
                       MOVE IN035-RPT1-CNTL-CARD-RECORD                         
                                          TO PV-LAST-CNTL-CARD-READ             
                       MOVE IN035-RPT1-EDIT-DTE      TO PV-ISO-FMT-DATE         
                       MOVE PV-ISO-FMT-MM            TO HD-ED-DATE-MM           
                       MOVE PV-ISO-FMT-DD            TO HD-ED-DATE-DD           
                       MOVE PV-ISO-FMT-YY            TO HD-ED-DATE-YY           
                       MOVE IN035-RPT1-CURR-FSCL-YR-PER                         
                                         TO SA-CURR-FSCL-YR-PER                 
                       MOVE IN035-RPT1-CURR-FSCL-PER-CDE                        
                                         TO FPN-SUB-X                           
                       MOVE FPN-FSCL-PER-NAME (FPN-SUB)                         
                                         TO HD-FP-CURR-FISC-PERIOD-NAME         
                       IF IN035-RPT1-MODE-INV-ONLY                              
                           MOVE PC-INVENTORY-ONLY-MODE                          
                                         TO HD-ST-PROCESSING-MODE               
                                            HD-FT-PROCESSING-MODE               
                       END-IF                                                   
                       PERFORM Z100-READ-EDIT-SPLIT-FILE                        
                   END-PERFORM                                                  
                   DISPLAY '** INKRP055 INPUT CONTROL CARD ='                   
                            PV-LAST-CNTL-CARD-READ                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * CREATE THE STORE EDIT SPLIT REPORT BY DOCUMENT NUMBER WITHIN            
      * BATCH WITHIN FINANCIAL LIABILITY DATE WITHIN TRANS CDE WITHIN           
      * STORE.                                                                  
      ******************************************************************        
       B200-CREATE-REPORT.                                                      
                                                                                
           PERFORM C200-INITIALIZATION.                                         
           PERFORM Z300-PRINT-STD-RPT-HEADING.                                  
                                                                                
           IF SW-NOT-END-OF-EDIT-SPLIT-FILE                                     
               PERFORM Z700-BUILD-EDITED-DOC-NBR                                
               MOVE ED-DOC-NBR               TO SA-PREV-DOC-NBR                 
               MOVE IN035-RPT1-BATCH-NBR     TO SA-PREV-BATCH-NBR               
               MOVE IN035-RPT1-FIN-LIAB-DTE  TO SA-PREV-FIN-LIAB-DTE            
               MOVE IN035-RPT1-TRANS-CDE     TO SA-PREV-TRANS-CDE               
               MOVE IN035-RPT1-SYST-ORIG-CDE TO SA-PREV-SYST-ORIG-CDE           
               MOVE IN035-RPT1-STORE-NBR     TO SA-PREV-STORE-NBR               
               MOVE IN035-RPT1-STORE-NBR     TO HD-ST-STORE-NBR                 
                                                TL-ST1-STORE-NBR                
               PERFORM Z600-READ-XMTLOC                                         
               MOVE XMT00001-LOC-NM          TO HD-ST-STORE-NAME                
                                                                                
               PERFORM Z350-PRINT-STORE-HEADING                                 
               PERFORM Z400-PRINT-CONSTANT-HEADINGS                             
               PERFORM Z500-PRINT-TRANS-CDE-HEADING                             
                                                                                
               MOVE IN035-RPT1-FIN-LIAB-DTE TO PV-ISO-FMT-DATE                  
               MOVE PV-ISO-FMT-MM           TO DL-EFF-DATE-MM                   
                                               TL-FL1-EFF-DATE-MM               
               MOVE PV-ISO-FMT-DD           TO DL-EFF-DATE-DD                   
                                               TL-FL1-EFF-DATE-DD               
               MOVE PV-ISO-FMT-YY           TO DL-EFF-DATE-YY                   
                                               TL-FL1-EFF-DATE-YY               
               MOVE '/'                     TO DL-EFF-DATE-SLASH1               
                                               DL-EFF-DATE-SLASH2               
               MOVE IN035-RPT1-BATCH-NBR    TO DL-BATCH-NBR                     
                                                                                
               PERFORM C300-PROCESS-EDIT-SPLIT-FILE                             
                   UNTIL SW-END-OF-EDIT-SPLIT-FILE                              
                                                                                
               PERFORM D050-DOC-NBR-BREAK                                       
               PERFORM D100-BATCH-NBR-BREAK                                     
               PERFORM D200-FIN-LIAB-BREAK                                      
               PERFORM D300-TRANS-CDE-BREAK                                     
               PERFORM D400-STORE-NBR-BREAK                                     
               MOVE SPACE TO HD-ST-STORE-NBR                                    
                             HD-ST-STORE-NAME                                   
               PERFORM C400-PRINT-FINAL-TOTALS                                  
           ELSE                                                                 
               SET SW-EMPTY-EDIT-SPLIT-FILE TO TRUE                             
           END-IF.                                                              
                                                                                
           MOVE ER-END-OF-REPORT-LINE TO STORE-SPLIT-REPORT-LINE.               
           MOVE 3 TO PV-ADVANCE.                                                
           PERFORM Z200-WRITE-REPORT-LINE.                                      
                                                                                
                                                                                
      ******************************************************************        
      * INITIALIZE FIELDS AS NECESSARY FOR THE STORE SPLIT REPORT.     *        
      ******************************************************************        
       C200-INITIALIZATION.                                                     
                                                                                
           INITIALIZE PV-PAGE-COUNT                                             
                      TOT-TOTAL-FIELDS.                                         
                                                                                
           SET SW-NOT-EMPTY-EDIT-SPLIT-FILE                                     
               SW-NOT-STORE-BREAK                                               
               SW-NOT-TRANS-CDE-BREAK                                           
               SW-NOT-FIN-LIAB-BREAK                                            
               SW-NOT-BATCH-NBR-BREAK TO TRUE.                                  
                                                                                
           ACCEPT PV-YYMMDD-DATE FROM DATE.                                     
           MOVE   PV-YYMMDD-YY TO DP133O-RUN-YEAR.                              
           MOVE   PV-YYMMDD-MM TO DP133O-RUN-MONTH.                             
           MOVE   PV-YYMMDD-DD TO DP133O-RUN-DAY.                               
           ACCEPT PV-TIME-HHMMSS FROM TIME.                                     
           MOVE PV-TIME-HHMMSS-HH   TO DP133O-RUN-HOUR.                         
           MOVE PV-TIME-HHMMSS-MM   TO DP133O-RUN-MINUTE.                       
           MOVE 'INKRP055'          TO DP133O-PROGRAM-NAME.                     
           MOVE 1 TO DP133O-REPORT-NUMBER.                                      
                                                                                
           MOVE PC-EFF-DATE-HD-LIT   TO HD-CL-EFF-DATE-HD-LIT.                  
           MOVE PC-BATCH-HD-LIT      TO HD-CL-BATCH-HD-LIT.                     
           MOVE PC-DOCUMENT-HD-LIT   TO HD-CL-DOCUMENT-HD-LIT.                  
                                                                                
                                                                                
      ******************************************************************        
      * READ AND PROCESS THE INPUT FILE. BEFORE EACH RECORD IS         *        
      * PROCESSED, CHECK FOR CONTROL BREAKS, AND DO ANY NECESSARY      *        
      * CONTROL BREAK PROCESSING.                                      *        
      ******************************************************************        
       C300-PROCESS-EDIT-SPLIT-FILE.                                            
                                                                                
           PERFORM Z700-BUILD-EDITED-DOC-NBR.                                   
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN IN035-RPT1-STORE-NBR NOT = SA-PREV-STORE-NBR                
                   PERFORM D050-DOC-NBR-BREAK                                   
                   PERFORM D100-BATCH-NBR-BREAK                                 
                   PERFORM D200-FIN-LIAB-BREAK                                  
                   PERFORM D300-TRANS-CDE-BREAK                                 
                   PERFORM D400-STORE-NBR-BREAK                                 
                   SET SW-STORE-BREAK TO TRUE                                   
                                                                                
               WHEN IN035-RPT1-TRANS-CDE NOT = SA-PREV-TRANS-CDE                
                 OR IN035-RPT1-SYST-ORIG-CDE NOT = SA-PREV-SYST-ORIG-CDE        
                   PERFORM D050-DOC-NBR-BREAK                                   
                   PERFORM D100-BATCH-NBR-BREAK                                 
                   PERFORM D200-FIN-LIAB-BREAK                                  
                   PERFORM D300-TRANS-CDE-BREAK                                 
                   SET SW-TRANS-CDE-BREAK TO TRUE                               
                                                                                
               WHEN IN035-RPT1-FIN-LIAB-DTE NOT = SA-PREV-FIN-LIAB-DTE          
                   PERFORM D050-DOC-NBR-BREAK                                   
                   PERFORM D100-BATCH-NBR-BREAK                                 
                   PERFORM D200-FIN-LIAB-BREAK                                  
                   SET SW-FIN-LIAB-BREAK TO TRUE                                
                                                                                
               WHEN IN035-RPT1-BATCH-NBR NOT = SA-PREV-BATCH-NBR                
                   PERFORM D050-DOC-NBR-BREAK                                   
                   PERFORM D100-BATCH-NBR-BREAK                                 
                   SET SW-BATCH-NBR-BREAK TO TRUE                               
                                                                                
               WHEN ED-DOC-NBR NOT = SA-PREV-DOC-NBR                            
                   PERFORM D050-DOC-NBR-BREAK                                   
                                                                                
           END-EVALUATE.                                                        
                                                                                
           PERFORM D500-PROCESS-EDIT-SPLIT-REC.                                 
           PERFORM Z100-READ-EDIT-SPLIT-FILE.                                   
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL PAGE FOR THE STORE SPLIT REPORT.                  
      ******************************************************************        
       C400-PRINT-FINAL-TOTALS.                                                 
                                                                                
           PERFORM Z300-PRINT-STD-RPT-HEADING.                                  
           MOVE SPACE TO HD-CL-EFF-DATE-HD-LIT                                  
                         HD-CL-BATCH-HD-LIT                                     
                         HD-CL-DOCUMENT-HD-LIT.                                 
           PERFORM Z375-PRINT-FINAL-TOT-HEADING.                                
           PERFORM Z400-PRINT-CONSTANT-HEADINGS.                                
           MOVE TL-FN1-FINAL-TOTAL-LINE-1 TO STORE-SPLIT-REPORT-LINE.           
           PERFORM Z200-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE TOT-FNL-CURR-ML-COST    TO TL-FN2-CURR-ML-COST.                 
           MOVE TOT-FNL-CURR-ML-RETAIL  TO TL-FN2-CURR-ML-RETAIL.               
           MOVE TOT-FNL-CURR-INV-COST   TO TL-FN2-CURR-INV-COST.                
           MOVE TOT-FNL-CURR-INV-RETAIL TO TL-FN2-CURR-INV-RETAIL.              
                                                                                
           MOVE TL-FN2-FINAL-TOTAL-LINE-2 TO STORE-SPLIT-REPORT-LINE.           
           MOVE 1 TO PV-ADVANCE.                                                
           PERFORM Z200-WRITE-REPORT-LINE.                                      
                                                                                
                                                                                
      ******************************************************************        
      * CHECK THE TOTALS FOR THE DOC-NBR. IF THEY ARE ALL ZERO, SKIP            
      * THE BREAK PROCESSING SO THAT THE DETAIL LINE WILL NOT BE                
      * PRODUCED.  IF ANY ARE NOT ZERO, CONTINUE THE BREAK PROCESSING.          
      ******************************************************************        
       D050-DOC-NBR-BREAK.                                                      
                                                                                
           IF  TOT-DOC-CURR-ML-COST     = ZERO                                  
           AND TOT-DOC-CURR-ML-RETAIL   = ZERO                                  
           AND TOT-DOC-CURR-INV-COST    = ZERO                                  
           AND TOT-DOC-CURR-INV-RETAIL  = ZERO                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET SW-DETAIL-REPORTED TO TRUE                                   
               PERFORM E100-CONTINUE-DOC-NBR-BREAK                              
           END-IF.                                                              
                                                                                
           PERFORM Z700-BUILD-EDITED-DOC-NBR.                                   
           MOVE ED-DOC-NBR             TO SA-PREV-DOC-NBR.                      
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS A BREAK ON BATCH-NBR: ADD THE BATCH-NBR TOTALS TO               
      * THE NEXT HIGHER LEVEL TOTALS, AND INITIALIZE THE BATCH-NBR              
      * TOTALS. (BATCH NUMBER TOTALS ARE NOT PRINTED ON THE REPORT.)            
      ******************************************************************        
       D100-BATCH-NBR-BREAK.                                                    
                                                                                
           ADD TOT-BCH-CURR-ML-COST    TO TOT-FLB-CURR-ML-COST.                 
           ADD TOT-BCH-CURR-ML-RETAIL  TO TOT-FLB-CURR-ML-RETAIL.               
           ADD TOT-BCH-CURR-INV-COST   TO TOT-FLB-CURR-INV-COST.                
           ADD TOT-BCH-CURR-INV-RETAIL TO TOT-FLB-CURR-INV-RETAIL.              
                                                                                
           INITIALIZE TOT-BCH-BATCH-NBR-TOTALS.                                 
           MOVE IN035-RPT1-BATCH-NBR    TO SA-PREV-BATCH-NBR.                   
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS A BREAK ON FIN-LIAB-DTE: FORMAT AND PRINT THE TOTAL             
      * LINE, ADD THE FIN-LIAB TOTALS TO THE NEXT HIGHER LEVEL TOTALS,          
      * AND INITIALIZE THE FIN-LIAB TOTALS. NOTE THAT THE TOTAL LINE IS         
      * ONLY PRINTED IF THERE WERE DETAIL LINES PRINTED.                        
      ******************************************************************        
       D200-FIN-LIAB-BREAK.                                                     
                                                                                
           IF SW-DETAIL-REPORTED                                                
               SET SW-DETAIL-NOT-REPORTED TO TRUE                               
               MOVE TL-FL1-FIN-LIAB-TOTAL-LINE-1                                
                                        TO STORE-SPLIT-REPORT-LINE              
               MOVE 2 TO PV-ADVANCE                                             
               PERFORM Z200-WRITE-REPORT-LINE                                   
                                                                                
               MOVE TOT-FLB-CURR-ML-COST    TO TL-FL2-CURR-ML-COST              
               MOVE TOT-FLB-CURR-ML-RETAIL  TO TL-FL2-CURR-ML-RETAIL            
               MOVE TOT-FLB-CURR-INV-COST   TO TL-FL2-CURR-INV-COST             
               MOVE TOT-FLB-CURR-INV-RETAIL TO TL-FL2-CURR-INV-RETAIL           
                                                                                
               MOVE TL-FL2-FIN-LIAB-TOTAL-LINE-2                                
                                        TO STORE-SPLIT-REPORT-LINE              
               MOVE 1 TO PV-ADVANCE                                             
               PERFORM Z200-WRITE-REPORT-LINE                                   
           END-IF.                                                              
                                                                                
           MOVE 2 TO PV-ADVANCE.                                                
                                                                                
           ADD TOT-FLB-CURR-ML-COST    TO TOT-TCD-CURR-ML-COST.                 
           ADD TOT-FLB-CURR-ML-RETAIL  TO TOT-TCD-CURR-ML-RETAIL.               
           ADD TOT-FLB-CURR-INV-COST   TO TOT-TCD-CURR-INV-COST.                
           ADD TOT-FLB-CURR-INV-RETAIL TO TOT-TCD-CURR-INV-RETAIL.              
                                                                                
           INITIALIZE TOT-FLB-FIN-LIAB-DTE-TOTALS.                              
                                                                                
           MOVE IN035-RPT1-FIN-LIAB-DTE TO SA-PREV-FIN-LIAB-DTE.                
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS A BREAK ON TRANS-CDE: FORMAT AND PRINT THE TOTAL                
      * LINE, ADD THE TRANS-CDE TOTALS TO THE NEXT HIGHER LEVEL TOTALS,         
      * AND INITIALIZE THE TRANS-CDE TOTALS.                                    
      ******************************************************************        
       D300-TRANS-CDE-BREAK.                                                    
                                                                                
           MOVE TL-TC1-TRANS-CDE-TOTAL-LINE-1                                   
                                        TO STORE-SPLIT-REPORT-LINE.             
           MOVE 2 TO PV-ADVANCE.                                                
           PERFORM Z200-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE TOT-TCD-CURR-ML-COST    TO TL-TC2-CURR-ML-COST.                 
           MOVE TOT-TCD-CURR-ML-RETAIL  TO TL-TC2-CURR-ML-RETAIL.               
           MOVE TOT-TCD-CURR-INV-COST   TO TL-TC2-CURR-INV-COST.                
           MOVE TOT-TCD-CURR-INV-RETAIL TO TL-TC2-CURR-INV-RETAIL.              
                                                                                
           MOVE TL-TC2-TRANS-CDE-TOTAL-LINE-2                                   
                                        TO STORE-SPLIT-REPORT-LINE.             
           MOVE 1 TO PV-ADVANCE.                                                
           PERFORM Z200-WRITE-REPORT-LINE.                                      
                                                                                
           IF SA-PREV-TRANS-PURCHASE                                            
           OR SA-PREV-TRANS-TRANSFER-TO                                         
           OR SA-PREV-TRANS-MARKUP                                              
           OR SA-PREV-TRANS-ADJUSTMENT                                          
               ADD TOT-TCD-CURR-ML-COST    TO TOT-STR-CURR-ML-COST              
               ADD TOT-TCD-CURR-ML-RETAIL  TO TOT-STR-CURR-ML-RETAIL            
               ADD TOT-TCD-CURR-INV-COST   TO TOT-STR-CURR-INV-COST             
               ADD TOT-TCD-CURR-INV-RETAIL TO TOT-STR-CURR-INV-RETAIL           
           ELSE                                                                 
               SUBTRACT TOT-TCD-CURR-ML-COST                                    
                                            FROM TOT-STR-CURR-ML-COST           
               SUBTRACT TOT-TCD-CURR-ML-RETAIL                                  
                                            FROM TOT-STR-CURR-ML-RETAIL         
               SUBTRACT TOT-TCD-CURR-INV-COST                                   
                                            FROM TOT-STR-CURR-INV-COST          
               SUBTRACT TOT-TCD-CURR-INV-RETAIL                                 
                                            FROM TOT-STR-CURR-INV-RETAIL        
           END-IF.                                                              
                                                                                
           INITIALIZE TOT-TCD-TRANS-CDE-TOTALS.                                 
           MOVE IN035-RPT1-TRANS-CDE    TO SA-PREV-TRANS-CDE.                   
           MOVE IN035-RPT1-SYST-ORIG-CDE TO SA-PREV-SYST-ORIG-CDE.              
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS A BREAK ON STORE-NBR: FORMAT AND PRINT THE TOTAL                
      * LINE, ADD THE STORE TOTALS TO THE NEXT HIGHER LEVEL TOTALS,             
      * AND INITIALIZE THE STORE TOTALS.                                        
      ******************************************************************        
       D400-STORE-NBR-BREAK.                                                    
                                                                                
           MOVE TL-ST1-STORE-TOTAL-LINE-1 TO STORE-SPLIT-REPORT-LINE.           
           MOVE 3 TO PV-ADVANCE.                                                
           PERFORM Z200-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE TOT-STR-CURR-ML-COST    TO TL-ST2-CURR-ML-COST.                 
           MOVE TOT-STR-CURR-ML-RETAIL  TO TL-ST2-CURR-ML-RETAIL.               
           MOVE TOT-STR-CURR-INV-COST   TO TL-ST2-CURR-INV-COST.                
           MOVE TOT-STR-CURR-INV-RETAIL TO TL-ST2-CURR-INV-RETAIL.              
                                                                                
           MOVE TL-ST2-STORE-TOTAL-LINE-2 TO STORE-SPLIT-REPORT-LINE.           
           MOVE 1 TO PV-ADVANCE.                                                
           PERFORM Z200-WRITE-REPORT-LINE.                                      
                                                                                
           ADD TOT-STR-CURR-ML-COST    TO TOT-FNL-CURR-ML-COST.                 
           ADD TOT-STR-CURR-ML-RETAIL  TO TOT-FNL-CURR-ML-RETAIL.               
           ADD TOT-STR-CURR-INV-COST   TO TOT-FNL-CURR-INV-COST.                
           ADD TOT-STR-CURR-INV-RETAIL TO TOT-FNL-CURR-INV-RETAIL.              
           INITIALIZE TOT-STR-STORE-TOTALS.                                     
           MOVE IN035-RPT1-STORE-NBR TO SA-PREV-STORE-NBR.                      
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS EACH INPUT RECORD: PRINT HEADINGS AS NECESSARY DUE TO           
      * CONTROL BREAKS; IF THE FINANCIAL LIABILITY DATE HAS CHANGED;            
      * ADD THE COST AND RETAIL AMOUNTS TO THE LOWEST LEVEL TOTALS.             
      ******************************************************************        
       D500-PROCESS-EDIT-SPLIT-REC.                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SW-STORE-BREAK                                              
                   PERFORM Z300-PRINT-STD-RPT-HEADING                           
                   MOVE IN035-RPT1-STORE-NBR    TO HD-ST-STORE-NBR              
                                                   TL-ST1-STORE-NBR             
                   PERFORM Z600-READ-XMTLOC                                     
                   MOVE XMT00001-LOC-NM         TO HD-ST-STORE-NAME             
                   PERFORM Z350-PRINT-STORE-HEADING                             
                   PERFORM Z400-PRINT-CONSTANT-HEADINGS                         
                   PERFORM Z500-PRINT-TRANS-CDE-HEADING                         
                   MOVE SA-PREV-FIN-LIAB-DTE    TO PV-ISO-FMT-DATE              
                   MOVE PV-ISO-FMT-MM           TO DL-EFF-DATE-MM               
                                                   TL-FL1-EFF-DATE-MM           
                   MOVE PV-ISO-FMT-DD           TO DL-EFF-DATE-DD               
                                                   TL-FL1-EFF-DATE-DD           
                   MOVE PV-ISO-FMT-YY           TO DL-EFF-DATE-YY               
                                                   TL-FL1-EFF-DATE-YY           
                   MOVE '/'                     TO DL-EFF-DATE-SLASH1           
                                                   DL-EFF-DATE-SLASH2           
                   MOVE IN035-RPT1-BATCH-NBR    TO DL-BATCH-NBR                 
                   SET SW-NOT-STORE-BREAK TO TRUE                               
               WHEN SW-TRANS-CDE-BREAK                                          
                   PERFORM Z500-PRINT-TRANS-CDE-HEADING                         
                   MOVE SA-PREV-FIN-LIAB-DTE    TO PV-ISO-FMT-DATE              
                   MOVE PV-ISO-FMT-MM           TO DL-EFF-DATE-MM               
                                                   TL-FL1-EFF-DATE-MM           
                   MOVE PV-ISO-FMT-DD           TO DL-EFF-DATE-DD               
                                                   TL-FL1-EFF-DATE-DD           
                   MOVE PV-ISO-FMT-YY           TO DL-EFF-DATE-YY               
                                                   TL-FL1-EFF-DATE-YY           
                   MOVE '/'                     TO DL-EFF-DATE-SLASH1           
                                                   DL-EFF-DATE-SLASH2           
                   MOVE IN035-RPT1-BATCH-NBR    TO DL-BATCH-NBR                 
                   SET SW-NOT-TRANS-CDE-BREAK TO TRUE                           
               WHEN SW-FIN-LIAB-BREAK                                           
                   MOVE SA-PREV-FIN-LIAB-DTE    TO PV-ISO-FMT-DATE              
                   MOVE PV-ISO-FMT-MM           TO DL-EFF-DATE-MM               
                                                   TL-FL1-EFF-DATE-MM           
                   MOVE PV-ISO-FMT-DD           TO DL-EFF-DATE-DD               
                                                   TL-FL1-EFF-DATE-DD           
                   MOVE PV-ISO-FMT-YY           TO DL-EFF-DATE-YY               
                                                   TL-FL1-EFF-DATE-YY           
                   MOVE '/'                     TO DL-EFF-DATE-SLASH1           
                                                   DL-EFF-DATE-SLASH2           
                   MOVE IN035-RPT1-BATCH-NBR    TO DL-BATCH-NBR                 
                   SET SW-NOT-FIN-LIAB-BREAK TO TRUE                            
               WHEN SW-BATCH-NBR-BREAK                                          
                   MOVE IN035-RPT1-BATCH-NBR    TO DL-BATCH-NBR                 
                   SET SW-NOT-BATCH-NBR-BREAK TO TRUE                           
           END-EVALUATE.                                                        
                                                                                
           IF IN035-RPT1-INCLUDE-IN-ML                                          
               ADD IN035-RPT1-EXTD-COST-AMT                                     
                                          TO TOT-DOC-CURR-ML-COST               
               ADD IN035-RPT1-EXTD-RTL-AMT                                      
                                          TO TOT-DOC-CURR-ML-RETAIL             
           END-IF.                                                              
           IF IN035-RPT1-INCLUDE-IN-INV                                         
              AND IN035-INV-ID NOT = ZEROES                                     
               ADD IN035-RPT1-EXTD-COST-AMT                                     
                                          TO TOT-DOC-CURR-INV-COST              
               ADD IN035-RPT1-EXTD-RTL-AMT                                      
                                          TO TOT-DOC-CURR-INV-RETAIL            
           ELSE                                                                 
               MOVE ZEROES                TO TOT-DOC-CURR-INV-COST              
               MOVE ZEROES                TO TOT-DOC-CURR-INV-RETAIL            
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS A BREAK ON DOC-NBR: FORMAT AND PRINT A DETAIL LINE,             
      * ADD THE DOC-NBR TOTALS TO THE NEXT HIGHER LEVEL TOTALS, AND             
      * INITIALIZE THE DOC-NBR TOTALS.                                          
      ******************************************************************        
       E100-CONTINUE-DOC-NBR-BREAK.                                             
                                                                                
           MOVE SA-PREV-DOC-NBR         TO DL-DOC-NBR.                          
                                                                                
           MOVE TOT-DOC-CURR-ML-COST    TO DL-CURR-ML-COST.                     
           MOVE TOT-DOC-CURR-ML-RETAIL  TO DL-CURR-ML-RETAIL.                   
           MOVE TOT-DOC-CURR-INV-COST   TO DL-CURR-INV-COST.                    
           MOVE TOT-DOC-CURR-INV-RETAIL TO DL-CURR-INV-RETAIL.                  
                                                                                
           IF PV-LINE-COUNT > PC-LINE-LIMIT-FOR-DTL                             
               PERFORM Z300-PRINT-STD-RPT-HEADING                               
               PERFORM Z350-PRINT-STORE-HEADING                                 
               PERFORM Z400-PRINT-CONSTANT-HEADINGS                             
           END-IF.                                                              
                                                                                
           MOVE DL-DETAIL-LINE TO STORE-SPLIT-REPORT-LINE.                      
           PERFORM Z200-WRITE-REPORT-LINE.                                      
           MOVE 1 TO PV-ADVANCE.                                                
                                                                                
           ADD TOT-DOC-CURR-ML-COST    TO TOT-BCH-CURR-ML-COST.                 
           ADD TOT-DOC-CURR-ML-RETAIL  TO TOT-BCH-CURR-ML-RETAIL.               
           ADD TOT-DOC-CURR-INV-COST   TO TOT-BCH-CURR-INV-COST.                
           ADD TOT-DOC-CURR-INV-RETAIL TO TOT-BCH-CURR-INV-RETAIL.              
                                                                                
           INITIALIZE TOT-DOC-NBR-TOTALS                                        
                      DL-DETAIL-LINE.                                           
                                                                                
                                                                                
       Z100-READ-EDIT-SPLIT-FILE.                                               
                                                                                
           READ EDIT-SPLIT-FILE                                                 
               INTO IN035-RPT1-RECORD                                           
                   AT END SET SW-END-OF-EDIT-SPLIT-FILE TO TRUE.                
                                                                                
                                                                                
       Z200-WRITE-REPORT-LINE.                                                  
                                                                                
           WRITE STORE-SPLIT-REPORT-LINE                                        
               AFTER ADVANCING PV-ADVANCE.                                      
           ADD PV-ADVANCE TO PV-LINE-COUNT.                                     
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE STANDARD REPORT HEADING LINE FOR THE STORE REPORT.            
      ******************************************************************        
       Z300-PRINT-STD-RPT-HEADING.                                              
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP133O-PAGE-NUMBER.                            
           MOVE DP133O-STANDRD-HEADER-133-COLS                                  
               TO STORE-SPLIT-REPORT-LINE                                       
           MOVE SPACES TO STORE-SPLIT-REPORT-LINE(133:1)                        
           WRITE STORE-SPLIT-REPORT-LINE                                        
               AFTER ADVANCING PAGE.                                            
           MOVE +1 TO PV-LINE-COUNT.                                            
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE STORE HEADING LINE.                                           
      ******************************************************************        
       Z350-PRINT-STORE-HEADING.                                                
                                                                                
           WRITE STORE-SPLIT-REPORT-LINE FROM HD-ST-STORE-HEADING               
               AFTER ADVANCING 2 LINES.                                         
           ADD +2 TO PV-LINE-COUNT.                                             
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE FINAL TOTAL HEADING LINE FOR THE STORE REPORT.                
      ******************************************************************        
       Z375-PRINT-FINAL-TOT-HEADING.                                            
                                                                                
           WRITE STORE-SPLIT-REPORT-LINE FROM HD-FT-FINAL-TOTAL-HEADING         
                   AFTER ADVANCING 2 LINES.                                     
           ADD +2 TO PV-LINE-COUNT.                                             
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE STORE REPORT HEADINGS THAT ARE THE SAME FOR BOTH THE          
      * DETAIL PAGES AND THE FINAL TOTAL PAGE. THE TRANS-CDE HEADING IS         
      * NOT PRINTED HERE. IT IS PRINTED SPEARATELY BECAUSE UNLIKE THE           
      * HEADINGS PRINTED HERE, IT IS NOT REPRINTED UPON A PAGE BREAK.           
      ******************************************************************        
       Z400-PRINT-CONSTANT-HEADINGS.                                            
                                                                                
           IF  IN035-RPT1-INV-FSCL-PER-CDE NOT = SPACES                         
               OR  IN035-RPT1-INV-FSCL-PER-CDE NOT = '  '                       
               MOVE IN035-RPT1-INV-FSCL-PER-CDE                                 
                                         TO FPN-SUB-X                           
               MOVE FPN-FSCL-PER-NAME (FPN-SUB)                                 
                                         TO                                     
                                     HD-FP-INV-FISC-PERIOD-NAME                 
           ELSE                                                                 
               MOVE HD-FP-CURR-FISC-PERIOD-NAME                                 
                                         TO                                     
                                     HD-FP-INV-FISC-PERIOD-NAME                 
           END-IF.                                                              
                                                                                
           WRITE STORE-SPLIT-REPORT-LINE FROM HD-ED-EDIT-DATE-HEADING           
               AFTER ADVANCING 1 LINES.                                         
           WRITE STORE-SPLIT-REPORT-LINE FROM HD-FP-FISCAL-PER-HEADING          
               AFTER ADVANCING 2 LINES.                                         
           WRITE STORE-SPLIT-REPORT-LINE FROM HD-MI-ML-INV-HEADING              
               AFTER ADVANCING 2 LINES.                                         
           WRITE STORE-SPLIT-REPORT-LINE FROM HD-CL-COLUMN-HEADING              
               AFTER ADVANCING 1 LINE.                                          
           ADD +6 TO PV-LINE-COUNT.                                             
           MOVE 2  TO PV-ADVANCE.                                               
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE TRANS-CDE HEADING. TRANS-CDE-TABLE IS SEARCHED TO FIND        
      * THE DESCRIPTION THAT WILL BE USED IN THE HEADING.                       
      ******************************************************************        
       Z500-PRINT-TRANS-CDE-HEADING.                                            
                                                                                
           MOVE 'Z500-PRINT-TRANS-CDE-HEADING' TO PV-PARAGRAPH-NAME.            
                                                                                
           SET TCT-IDX TO 1.                                                    
                                                                                
           SEARCH TCT-ENTRY                                                     
               AT END                                                           
                   MOVE 'TRANS-CDE ON INPUT RECORD NOT FOUND IN TCT-            
      -                    'TRANS-CDE-TABLE.' TO PV-ABEND-MESSAGE               
                   MOVE +4008 TO ABEND-CODE                                     
                   PERFORM Z999-ABEND                                           
               WHEN TCT-TRANS-CDE (TCT-IDX) = IN035-RPT1-TRANS-CDE              
                AND TCT-SYS-ORIG-CDE (TCT-IDX)                                  
                                    = IN035-RPT1-SYST-ORIG-CDE                  
                   MOVE TCT-TRANS-CDE-DESC (TCT-IDX)                            
                                            TO HD-TC-TRANS-CDE-DESC             
                                               TL-TC1-TRANS-CDE-DESC            
           END-SEARCH.                                                          
                                                                                
           IF PV-LINE-COUNT > PC-LINE-LIMIT-FOR-HD-TC                           
               PERFORM Z300-PRINT-STD-RPT-HEADING                               
               PERFORM Z350-PRINT-STORE-HEADING                                 
               PERFORM Z400-PRINT-CONSTANT-HEADINGS                             
           END-IF.                                                              
                                                                                
           WRITE STORE-SPLIT-REPORT-LINE                                        
               FROM HD-TC-TRANS-CDE-HEADING                                     
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           ADD +2 TO PV-LINE-COUNT.                                             
           MOVE 2 TO PV-ADVANCE.                                                
                                                                                
                                                                                
      *****************************************************************         
      * THIS PARAGRAPH RETRIEVES THE STORE NAME.                                
      *****************************************************************         
       Z600-READ-XMTLOC.                                                        
                                                                                
           MOVE 'Z600-READ-XMTLOC' TO PV-PARAGRAPH-NAME.                        
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                                
                  OR SQLCODE = 0                                                
                                                                                
               MOVE IN035-RPT1-STORE-NBR TO PV-EDIT-STORE                       
               MOVE PV-EDIT-STORE-NUM    TO XMT00001-LOC-NBR                    
                                                                                
               EXEC SQL                                                         
                    SELECT LOC_NM                                               
                    INTO :XMT00001-LOC-NM                                       
                    FROM XMT_LOC                                                
                    WHERE LOC_NBR = :XMT00001-LOC-NBR                           
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                       DISPLAY '*** INKRP055 ABEND ***'                         
                       DISPLAY '**** NO MATCH FOR STORE '                       
                                                IN035-RPT1-STORE-NBR            
                       DISPLAY '**** ON DB2 STORE TABLE'                        
                       PERFORM Z900-SQL-ABEND                                   
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'READING STORE TABLE'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE 'READ RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  THE DOCUMENT NBR HAS EXPANDED FROM 5 TO 15 CHARACTERS DUE TO           
      *  REPORTING CHANGINGS FOR THE NEW FAST TRANSFER SYSTEM.                  
      *                                                                         
      *  NOTE:  NON-FAST TRANSACTIONS WILL NOT CHANGE OTHER THAN HAVING         
      *         10 SPACES PADDED AFTER IT.                                      
      ******************************************************************        
       Z700-BUILD-EDITED-DOC-NBR.                                               
                                                                                
           IF  IN035-RPT1-DOC-5-NBRX = 'FAST '                                  
               MOVE IN035-RPT1-DOC-5-NBR (1:4)          TO                      
                    ED-DOC-FAST-LIT                                             
               MOVE '-'                                 TO                      
                    ED-DOC-HYPHEN                                               
               IF  IN035-RPT1-XFER-DOC-STORE NUMERIC AND                        
                   IN035-RPT1-XFER-DOC-SEQ NUMERIC                              
                   MOVE IN035-RPT1-XFER-DOC-STORE (2:4) TO                      
                        PV-EDIT-LOC-NBR                                         
                   MOVE PV-EDIT-LOC-NBR-ALPH (2:4) TO                           
                        ED-DOC-STORE                                            
                   MOVE IN035-RPT1-XFER-DOC-SEQ         TO                      
                        ED-DOC-SEQ                                              
               ELSE                                                             
                   MOVE IN035-RPT1-XFER-DOC-NBR        TO                       
                        ED-DOC-STORE                                            
                   MOVE IN035-RPT1-XFER-DOC-NBR (5:6)  TO                       
                        ED-DOC-SEQ                                              
               END-IF                                                           
           ELSE                                                                 
               MOVE IN035-RPT1-DOC-5-NBRX               TO                      
                    ED-DOC-NBR                                                  
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP055'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
       Z999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP055'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  ' PV-ABEND-MESSAGE.                                     
           DISPLAY '**  INPUT RECORD: ' EDIT-SPLIT-RECORD.                      
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
