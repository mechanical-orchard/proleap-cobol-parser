      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.             FSKUT065.                                        
       AUTHOR.                 JOHN PRICE.                                      
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            JULY 2000.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                *        
      *  THIS PROGRAM WILL READ THE POINT OF SALES FILE TRANSACTIONS   *        
      *  AND CREATE AN EXTRACT FILE TO BE USED TO VALIDATE TOTALS      *        
      *  PRIOR TO RUNNING DAILY SALES DB/2 UPDATES                     *        
      *                                                                *        
JP1200******************************************************************        
JP1200* 12/20/2000 JOHN PRICE   - ELIMINATE THE PROCESSING OF          *        
JP1200*                           DEPARTMENT 850. (SEE PARAG. 2000-)   *        
JP1200*                                           (WR# 99999)          *        
JP1200******************************************************************        
LM0601* 06/01/2001 LINDA MOYER  - CHANGED RETURN TYPE 'SALE-TYPE'      *        
      *                           TO BE 11 OR 12 DUE TO POS SPLIT      *        
      *                           TENDER PROCESS.                      *        
      ******************************************************************        
      * 07/22/2002 MARIA KLAMIK - RECOMPLIE PROGRAM WITH CHANGES TO    *        
      *       KDU1806 FOR NEW SKU.                                     *        
      *                                           (WR# 21568)          *        
      ******************************************************************        
207210* 08/24/2018 JIM HUNTER  - REPLACE COPYBOOK KDU1806 WITH PSWS010 *        
207210*                                           (CHG0207210)         *        
207210******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT DAILY-SALES-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT EXTRACT-FILE                                                  
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DAILY-SALES-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 65 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS DAILY-SALES-RECORD.                                   
                                                                                
       01  DS-DAILY-SALES-RECORD            PIC X(65).                          
                                                                                
       FD  EXTRACT-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 65 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS EXTRACT-RECORD.                                       
                                                                                
       01  EXTRACT-RECORD                   PIC X(65).                          
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'FSKUT065'.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-DAILY-SALES-FILE-SW      PIC X(01)   VALUE 'N'.               
               88  PS-END-OF-DAILY-SALES-FILE          VALUE 'Y'.               
           05  PS-EXTRACT-FILE-SW          PIC X(01)   VALUE 'N'.               
               88  PS-END-OF-EXTRACT-FILE              VALUE 'Y'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DISPLAY-RCDS-READ         PIC Z,ZZZ,ZZ9.                      
           05  PV-DISPLAY-MISC-BYPASSED     PIC Z,ZZZ,ZZ9.                      
           05  PV-DISPLAY-SEQ-000-BYPASSED  PIC Z,ZZZ,ZZ9.                      
           05  PV-DISPLAY-RCDS-WRITTEN      PIC Z,ZZZ,ZZ9.                      
                                                                                
       01 PA-PROGRAM-ACCUMULATORE.                                              
          05  PA-DAILY-SALES-RCDS-READ      PIC 9(07) COMP-3 VALUE ZERO.        
          05  PA-MISC-RCDS-BYPASSED         PIC 9(07) COMP-3 VALUE ZERO.        
          05  PA-SEQ-000-RCDS-BYPASSED      PIC 9(07) COMP-3 VALUE ZERO.        
          05  PA-EXTRACT-RCDS-WRITEN        PIC 9(07) COMP-3 VALUE ZERO.        
                                                                                
      ******************************************************************        
      *    THIS IS THE COPYBOOK FOR BOTH THE INPUT AND OUT FILES       *        
      ******************************************************************        
                                                                                
207210     COPY PSWS010.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL PS-END-OF-DAILY-SALES-FILE.                                
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                                   
      *      1.  OPEN I/O FILES                                                 
      *      2.  READ FIRST DAILY SALES INPUT RECORD                            
      ******************************************************************        
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  DAILY-SALES-FILE                                         
                OUTPUT EXTRACT-FILE.                                            
                                                                                
           PERFORM 8100-READ-DAILY-SALES.                                       
                                                                                
           IF  PS-END-OF-DAILY-SALES-FILE                                       
               DISPLAY SPACE                                                    
               DISPLAY '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'          
               DISPLAY SPACE                                                    
               DISPLAY 'PROGRAM      = ' PC-PROGRAM-NAME                        
               DISPLAY '** ABEND **  = DAILY SALES INPUT FILE IS EMPTY'         
               DISPLAY SPACE                                                    
               DISPLAY '@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@'          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                                   
      *      1.  PERFORM VARIOUS ROUTINES TO PROCESS THE INPUT RECORDS          
      *          AND WRITE THE MODIFIED RECORDS TO THE OUTPUT FILE              
JP1200*                                                                         
JP1200* 12/19/00 JOHN PRICE CHANGES:                                            
JP1200*     DEPARTMENTS 850 AND 983 WILL BE USED FOR E-COMMERCE SERVICE         
JP1200*     ITEMS SUCH AS WRAPPING PAPER AND PACKING SUPPLIES. THE              
JP1200*     ONHAND QUANTITIES SHOULD NOT BE UPDATED FOR THESE DEPTS.            
      ******************************************************************        
                                                                                
       2000-MAIN-ROUTINE.                                                       
                                                                                
           IF  D040-DEPT = (099 OR 148 OR 149 OR                                
JP1200                     555 OR 800 OR 850 OR 981 OR 983)                     
               ADD +1                    TO PA-MISC-RCDS-BYPASSED               
           ELSE                                                                 
               IF  D040-SEQNO = ZERO                                            
                   ADD +1                TO PA-SEQ-000-RCDS-BYPASSED            
               ELSE                                                             
                   PERFORM 2010-PROCESS-OUTPUT-FILE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 8100-READ-DAILY-SALES.                                       
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                          *        
      *      1. CALCULATE THE SALES-UNITS                              *        
      *      2. PERFORM A ROUTINE TO WRITE THE OUTPUT RECORDS          *        
      ******************************************************************        
                                                                                
       2010-PROCESS-OUTPUT-FILE.                                                
                                                                                
           IF  D040-SALE-AMT < ZERO                                             
               COMPUTE D040-SALE-UNITS = D040-SALE-UNITS * -1                   
           ELSE                                                                 
               IF  ((D040-SALE-AMT = ZERO) AND                                  
LM                 (D040-SALE-TYPE = 11 OR 12))                                 
                   COMPUTE D040-SALE-UNITS = D040-SALE-UNITS * -1               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 8200-WRITE-EXTRACT-FILE.                                     
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                          *        
      *      1. READ THE INPUT DAILY-SALES FILE                        *        
      ******************************************************************        
       8100-READ-DAILY-SALES.                                                   
                                                                                
           READ DAILY-SALES-FILE INTO D040-RECORD                               
               AT END                                                           
                   SET PS-END-OF-DAILY-SALES-FILE  TO  TRUE.                    
                                                                                
           IF  PS-END-OF-DAILY-SALES-FILE                                       
               NEXT SENTENCE                                                    
           ELSE                                                                 
               ADD +1                    TO PA-DAILY-SALES-RCDS-READ            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                          *        
      *      1. WRITE THE OUTPUT EXTRACT FILE                          *        
      ******************************************************************        
                                                                                
       8200-WRITE-EXTRACT-FILE.                                                 
                                                                                
           WRITE EXTRACT-RECORD                                                 
               FROM D040-RECORD.                                                
                                                                                
           ADD +1                        TO PA-EXTRACT-RCDS-WRITEN.             
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WILL:                                          *        
      *      1. CLOSE INPUT & OUTPUT FILES                             *        
      *      2. DISPLAY VARIOUS TOTALS                                 *        
      ******************************************************************        
                                                                                
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE DAILY-SALES-FILE                                               
                 EXTRACT-FILE.                                                  
                                                                                
           MOVE PA-DAILY-SALES-RCDS-READ  TO PV-DISPLAY-RCDS-READ               
           MOVE PA-MISC-RCDS-BYPASSED     TO PV-DISPLAY-MISC-BYPASSED           
           MOVE PA-SEQ-000-RCDS-BYPASSED  TO PV-DISPLAY-SEQ-000-BYPASSED        
           MOVE PA-EXTRACT-RCDS-WRITEN    TO PV-DISPLAY-RCDS-WRITTEN            
                                                                                
           DISPLAY SPACE                                                        
           DISPLAY '@@@@@@@@@@ PROGRAM FSKUT065 TOTALS @@@@@@@@@@'.             
           DISPLAY SPACE                                                        
           DISPLAY '   INPUT RECORDS READ             = '                       
                                            PV-DISPLAY-RCDS-READ                
           DISPLAY '   MISCELLANEOUS RECORDS BYPASSED = '                       
                                            PV-DISPLAY-MISC-BYPASSED            
           DISPLAY '   SEQUENCE 000 RECORDS BYPASSED  = '                       
                                            PV-DISPLAY-SEQ-000-BYPASSED         
           DISPLAY '   OUTPUT RECORDS WRITTEN         = '                       
                                            PV-DISPLAY-RCDS-WRITTEN             
           DISPLAY SPACE                                                        
           DISPLAY '@@@@@@@@@@ PROGRAM FSKUT065 TOTALS @@@@@@@@@@'.             
           DISPLAY SPACE.                                                       
