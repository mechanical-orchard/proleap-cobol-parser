       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    IMKUT077.                                         00020000
       AUTHOR.        PAUL KEBER.                                       00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  06-29-06.                                         00050000
       DATE-COMPILED.                                                   00060000
                                                                        00061000
      ******************************************************************00062000
      *     IMKUT077 - PERM PRICING TPPCPRC CHANGES FILE SPLIT          00063000
      ******************************************************************00064000
      *  THIS PROGRAM WILL INPUT NON-CONTINUED CLEARANCE PERM PRICE     00065000
      *  CHANGES FILE RECORDS THAT ARE SORTED IN ITEM NBR SEQUENCE AND  00066000
      *  SPLIT THE FILE INTO EITHER ONE FILE OR SIX EQUAL FILES BASED   00067002
      *  ON THE NUMBER OF INPUT FILE RECORDS AND A SPLIT START POINT    00067100
      *  COUNT INPUT CONTROL CARD FILE COUNT.  IF THE TOTAL INPUT FILE  00067200
      *  RECORD COUNT IS LESS THAN THE SPLIT START POINT COUNT FILE     00067300
      *  COUNT, ALL INPUT FILE RECORDS WILL BE WRITTEN TO THE FIRST     00067400
      *  OUTPUT FILE.  OTHERWISE, THIS PROGRAM WILL SPLIT THE INPUT     00067501
      *  FILE INTO SIX EQUAL FILES BASED ON THE NUMBER OF INPUT FILE    00067602
      *  RECORDS.                                                       00067701
      *                                                                 00068100
      *  AN INPUT CONTROL CARD FILE WILL CONTAIN TPPCPRC PARTITIONS BY  00068200
      *  ITM_NBR RECORDS.  THIS FILE WILL BE USED TO PREVENT THE START  00069000
      *  OF A NEW OUTPUT FILE IN THE MIDDLE OF A TPPCPRC PARTITION.     00070000
      ******************************************************************00080000
      *  WR29434   06/29/06 P. KEBER    INITIAL CREATION.              *00081000
      ******************************************************************00082000
PK0208*  WR34307   02/15/08 P. KEBER    CHANGE TO SPLIT FROM 4 TO 6    *00082102
PK0208*                                 EQUAL FILES.                   *00082202
      ******************************************************************00082302
      *                                                                 00083000
       ENVIRONMENT DIVISION.                                            00084000
      *                                                                 00085000
       CONFIGURATION SECTION.                                           00086000
      *                                                                 00087000
       SOURCE-COMPUTER.        IBM-3090.                                00088000
       OBJECT-COMPUTER.        IBM-3090.                                00089000
      *                                                                 00090000
       INPUT-OUTPUT SECTION.                                            00091000
       FILE-CONTROL.                                                    00092000
                                                                        00093000
           SELECT PRICE-UPDATES-FILE                                    00094000
               ASSIGN TO INP01.                                         00095000
                                                                        00095100
           SELECT PRICE-UPDATES-COUNT-FILE                              00095200
               ASSIGN TO INP02.                                         00095300
                                                                        00095400
           SELECT PARTITIONS-CONTROL-FILE                               00095500
               ASSIGN TO INP03.                                         00095600
                                                                        00095700
           SELECT SPLIT-START-POINT-CC-FILE                             00095800
               ASSIGN TO INP04.                                         00095900
                                                                        00096000
           SELECT SPLIT-UPDATES-FILE-1                                  00097000
               ASSIGN TO OUT01.                                         00097100
                                                                        00097200
           SELECT SPLIT-UPDATES-FILE-2                                  00097300
               ASSIGN TO OUT02.                                         00097400
                                                                        00097500
           SELECT SPLIT-UPDATES-FILE-3                                  00097600
               ASSIGN TO OUT03.                                         00097700
                                                                        00097800
           SELECT SPLIT-UPDATES-FILE-4                                  00097900
               ASSIGN TO OUT04.                                         00098000
                                                                        00098100
PK0208     SELECT SPLIT-UPDATES-FILE-5                                  00098202
PK0208         ASSIGN TO OUT05.                                         00098302
                                                                        00098402
PK0208     SELECT SPLIT-UPDATES-FILE-6                                  00098502
PK0208         ASSIGN TO OUT06.                                         00098602
                                                                        00098702
      ******************************************************************00098800
       DATA DIVISION.                                                   00098900
      ******************************************************************00099000
                                                                        00099100
       FILE SECTION.                                                    00099200
                                                                        00099300
       FD  PRICE-UPDATES-FILE                                           00099400
           RECORDING MODE IS F                                          00099500
           LABEL RECORDS ARE STANDARD                                   00099600
           BLOCK CONTAINS 0 RECORDS.                                    00100000
                                                                        00110000
       01  PRICE-UPDATES-IN-RECORD     PIC  X(56).                      00120000
                                                                        00130000
       FD  PRICE-UPDATES-COUNT-FILE                                     00140000
           RECORDING MODE IS F                                          00150000
           LABEL RECORDS ARE STANDARD                                   00160000
           BLOCK CONTAINS 0 RECORDS.                                    00170000
                                                                        00180000
       01  PRICE-UPDATES-COUNT-REC     PIC  X(9).                       00181000
                                                                        00182000
       FD  PARTITIONS-CONTROL-FILE                                      00183000
           RECORDING MODE IS F                                          00184000
           LABEL RECORDS ARE STANDARD                                   00185000
           BLOCK CONTAINS 0 RECORDS.                                    00186000
                                                                        00187000
       01  PARTITIONS-CONTROL-RECORD   PIC X(80).                       00188000
                                                                        00189000
       FD  SPLIT-START-POINT-CC-FILE                                    00190000
           RECORDING MODE IS F                                          00191000
           LABEL RECORDS ARE STANDARD                                   00192000
           BLOCK CONTAINS 0 RECORDS.                                    00193000
                                                                        00194000
       01  SPLIT-START-POINT-CC-RECORD PIC X(80).                       00195000
                                                                        00196000
       FD  SPLIT-UPDATES-FILE-1                                         00197000
           RECORDING MODE IS F                                          00198000
           LABEL RECORDS ARE STANDARD                                   00199000
           BLOCK CONTAINS 0 RECORDS.                                    00200000
                                                                        00210000
       01  SPLIT-UPDATES-FILE-1-REC    PIC  X(56).                      00220000
                                                                        00230000
       FD  SPLIT-UPDATES-FILE-2                                         00240000
           RECORDING MODE IS F                                          00250000
           LABEL RECORDS ARE STANDARD                                   00260000
           BLOCK CONTAINS 0 RECORDS.                                    00261000
                                                                        00262000
       01  SPLIT-UPDATES-FILE-2-REC    PIC  X(56).                      00263000
                                                                        00264000
       FD  SPLIT-UPDATES-FILE-3                                         00265000
           RECORDING MODE IS F                                          00266000
           LABEL RECORDS ARE STANDARD                                   00266100
           BLOCK CONTAINS 0 RECORDS.                                    00266200
                                                                        00266300
       01  SPLIT-UPDATES-FILE-3-REC    PIC  X(56).                      00266400
                                                                        00266500
       FD  SPLIT-UPDATES-FILE-4                                         00266600
           RECORDING MODE IS F                                          00266700
           LABEL RECORDS ARE STANDARD                                   00266800
           BLOCK CONTAINS 0 RECORDS.                                    00266900
                                                                        00267000
       01  SPLIT-UPDATES-FILE-4-REC    PIC  X(56).                      00267100
                                                                        00267200
PK0208 FD  SPLIT-UPDATES-FILE-5                                         00267302
PK0208     RECORDING MODE IS F                                          00267402
PK0208     LABEL RECORDS ARE STANDARD                                   00267502
PK0208     BLOCK CONTAINS 0 RECORDS.                                    00267602
PK0208                                                                  00267702
PK0208 01  SPLIT-UPDATES-FILE-5-REC    PIC  X(56).                      00267802
PK0208                                                                  00267902
PK0208 FD  SPLIT-UPDATES-FILE-6                                         00268002
PK0208     RECORDING MODE IS F                                          00268102
PK0208     LABEL RECORDS ARE STANDARD                                   00268202
PK0208     BLOCK CONTAINS 0 RECORDS.                                    00268302
PK0208                                                                  00268402
PK0208 01  SPLIT-UPDATES-FILE-6-REC    PIC  X(56).                      00268502
                                                                        00268602
      *                                                                 00268700
      ******************************************************************00268800
       WORKING-STORAGE SECTION.                                         00268900
      ******************************************************************00269000
                                                                        00269100
       01  PC-PROGRAM-CONSTANTS.                                        00269200
           05  PC-MAX-PARTITIONS-CNT   PIC S9(04)     VALUE +500 COMP.  00269300
                                                                        00269400
       01  PV-PROGRAM-VARIABLES.                                        00269500
           05  PV-PRICE-UPDTS-READ-CNT PIC S9(09)     VALUE +0  COMP.   00269600
           05  PV-SPLIT-FILE-RECS-CNT  PIC S9(09)     VALUE +0  COMP.   00269700
           05  PV-SPLIT-FILE-COUNT     PIC S9(09)     VALUE +0  COMP.   00269800
           05  PV-OUTPUT-1-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00269900
           05  PV-OUTPUT-2-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00270000
           05  PV-OUTPUT-3-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00270100
           05  PV-OUTPUT-4-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00270200
PK0208     05  PV-OUTPUT-5-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00270302
PK0208     05  PV-OUTPUT-6-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00270402
           05  PV-PARTITIONS-CNT       PIC S9(04)     VALUE +0  COMP.   00270500
           05  PV-PARTITIONS-SUB       PIC S9(04)     VALUE +0  COMP.   00271000
           05  PV-SWITCH-PARTITION-SUB PIC S9(09)     VALUE +0  COMP.   00280000
           05  PV-ABEND-CODE           PIC S9(04)     VALUE +0  COMP.   00290000
               88  PV-ABEND-4001                      VALUE 4001.       00300000
      *            EMPTY PRICE UPDATES COUNT FILE                       00310000
               88  PV-ABEND-4002                      VALUE 4002.       00320000
      *            NON-NUMERIC PRICE UPDATES COUNT                      00330000
               88  PV-ABEND-4003                      VALUE 4003.       00340000
      *            MORE THAN 500 PARTITIONS ON PARTITIONS CONTROL FILE  00350000
               88  PV-ABEND-4004                      VALUE 4004.       00360000
      *            NON-NUMERIC PARTITIONS CONTROL RECORD ITEM NUMBER    00370000
               88  PV-ABEND-4005                      VALUE 4005.       00380000
      *            EMPTY PARTITIONS CONTROL FILE                        00390000
               88  PV-ABEND-4006                      VALUE 4006.       00400000
      *            PARTITIONS CONTROL RECORD OUT OF SEQUENCE            00410000
               88  PV-ABEND-4007                      VALUE 4007.       00420000
      *            PARTITIONS SUB OVERFLOW                              00430000
               88  PV-ABEND-4008                      VALUE 4008.       00431000
      *            EMPTY SPLIT START POINT COUNT CONTROL CARD           00431100
                                                                        00431200
       01  PS-PROGRAM-SWITCHES.                                         00431300
           05  PS-END-OF-PRICE-UPDATES-SW  PIC  X(01) VALUE 'N'.        00431400
               88  PS-END-OF-PRICE-UPDATES            VALUE 'Y'.        00431500
           05  PS-END-OF-PARTITIONS-SW     PIC  X(01) VALUE 'N'.        00431600
               88  PS-END-OF-PARTITIONS               VALUE 'Y'.        00431700
           05  PS-PARTITION-SWITCH-SW      PIC  X(01) VALUE 'N'.        00431800
               88  PS-PARTITION-SWITCH                VALUE 'Y'.        00431900
               88  PS-NO-PARTITION-SWITCH             VALUE 'N'.        00432000
           05  PS-SPLIT-FILE-REACHED-SW    PIC  X(01) VALUE 'N'.        00433000
               88  PS-SPLIT-FILE-REACHED              VALUE 'Y'.        00434000
               88  PS-SPLIT-FILE-NOT-REACHED          VALUE 'N'.        00435000
           05  PS-PRICE-UPDATES-OUT-SW     PIC  9(01) VALUE 1.          00436000
                                                                        00437000
      *    INTERNAL PARTITIONS TABLE                                    00438000
       01  PT-PARTITIONS-TABLE.                                         00439000
           05  PT-PARTITIONS-ITEM-NBR  OCCURS 500 TIMES                 00439100
                                           PIC S9(15) COMP-3.           00439200
                                                                        00450000
      ******************************************************************00460000
      *  RECORD LAYOUT FOR THE NON-CONTINUED CLEARANCE PERM SKU PRICE   00470000
      *  UPDATES INPUT FILE                                             00480000
      ******************************************************************00490000
           COPY IMRD048.                                                00491000
                                                                        00492000
      ***************************************************************** 00493000
      *  RECORD LAYOUT FOR THE PRICE UPDATES COUNT FILE                 00494000
      ***************************************************************** 00494100
       01  WS-PRICE-UPDATES-COUNT-REC.                                  00494200
           05  WS-PRICE-UPDATES-COUNT       PIC 9(09) VALUE ZERO.       00494300
                                                                        00494400
      ***************************************************************** 00494500
      *  RECORD LAYOUT FOR THE PARTITIONS CONTROL FILE                  00494600
      ***************************************************************** 00494700
       01  WS-PARTITIONS-CTRL-RECORD.                                   00494800
           05  FILLER                       PIC X(07) VALUE SPACES.     00494900
           05  WS-PARTITIONS-ITEM-NBR       PIC 9(15) VALUE ZERO.       00495000
           05  FILLER                       PIC X(58) VALUE SPACES.     00495100
                                                                        00495200
      ***************************************************************** 00495300
      *  RECORD LAYOUT FOR THE SPLIT START POINT COUNT CONTROL CARD     00495400
      ***************************************************************** 00495500
       01  WS-SPLIT-START-POINT-REC.                                    00495600
           05  WS-SPLIT-START-POINT-COUNT   PIC 9(09) VALUE ZERO.       00495700
                                                                        00495800
       EJECT                                                            00495900
                                                                        00496000
      ******************************************************************00497000
       PROCEDURE DIVISION.                                              00498000
      ******************************************************************00499000
                                                                        00500000
       0100-MAINLINE-IMKUT077.                                          00510000
      *                                                                 00520000
           PERFORM 1000-INITIALIZATION.                                 00530000
                                                                        00540000
           IF PS-END-OF-PRICE-UPDATES                                   00550000
               DISPLAY '*** IMKUT077 - EMPTY PRICE UPDATES INPUT FILE'  00560000
               MOVE 4095 TO RETURN-CODE                                 00570000
           ELSE                                                         00580000
               IF WS-PRICE-UPDATES-COUNT < WS-SPLIT-START-POINT-COUNT   00581000
                   DISPLAY '*** IMKUT077 - ALL RECORDS WRITTEN TO SPLIT'00582000
                           ' FILE 1'                                    00583000
                   DISPLAY '********************************************00583100
      -            '**********'                                         00583200
                   MOVE 4094 TO RETURN-CODE                             00583300
               ELSE                                                     00583400
                   MOVE 0 TO RETURN-CODE                                00583500
               END-IF                                                   00583600
           END-IF.                                                      00583700
                                                                        00583800
           IF RETURN-CODE = 0                                           00583900
               PERFORM 2000-PROCESS-PRICE-UPDTS-SPLIT                   00584000
                   UNTIL PS-END-OF-PRICE-UPDATES                        00585000
           ELSE                                                         00586000
               PERFORM 3000-WRITE-PRICE-UPDTS-SPLIT1                    00587000
                   UNTIL PS-END-OF-PRICE-UPDATES                        00588000
           END-IF.                                                      00589000
                                                                        00590000
           PERFORM 8000-EOJ.                                            00600000
                                                                        00610000
           GOBACK.                                                      00620000
                                                                        00630000
       EJECT                                                            00640000
                                                                        00650000
      ******************************************************************00660000
      *  OPEN FILES AND READ THE FIRST RECORD OF THE PRICE UPDATES      00670000
      *  INPUT FILE.  ALSO READ THE PRICE UPDATES COUNT FILE AND LOAD   00680000
      *  THE PARTITIONS CONTROL FILE RECORDS INTO THE PARTITIONS TABLE. 00690000
      ******************************************************************00700000
       1000-INITIALIZATION.                                             00710000
      *                                                                 00720000
           OPEN INPUT  PRICE-UPDATES-FILE                               00730000
                       PRICE-UPDATES-COUNT-FILE                         00740000
                       PARTITIONS-CONTROL-FILE                          00750000
                       SPLIT-START-POINT-CC-FILE.                       00760000
           OPEN OUTPUT SPLIT-UPDATES-FILE-1                             00770000
                       SPLIT-UPDATES-FILE-2                             00780000
                       SPLIT-UPDATES-FILE-3                             00790000
                       SPLIT-UPDATES-FILE-4                             00800002
PK0208                 SPLIT-UPDATES-FILE-5                             00801002
PK0208                 SPLIT-UPDATES-FILE-6.                            00802002
                                                                        00810000
           DISPLAY '****************************************************00820000
      -            '**'.                                                00830000
                                                                        00831000
           PERFORM 4000-READ-PRICE-UPDATES-IN.                          00832000
                                                                        00833000
           PERFORM 4100-READ-PRICE-UPDATES-COUNT.                       00834000
                                                                        00835000
           IF WS-PRICE-UPDATES-COUNT NOT NUMERIC                        00836000
               DISPLAY '*** IMKUT077 - NON-NUMERIC PRICE UPDATES COUNT' 00837000
               SET PV-ABEND-4002 TO TRUE                                00838000
               PERFORM 9999-ABEND                                       00839000
           ELSE                                                         00840000
PK0208         DIVIDE WS-PRICE-UPDATES-COUNT BY 6 GIVING                00840102
                     PV-SPLIT-FILE-COUNT ROUNDED                        00840200
               DISPLAY '*** IMKUT077 - PRICE UPDATES FILE COUNT: '      00840300
                     WS-PRICE-UPDATES-COUNT                             00840400
               DISPLAY '*** IMKUT077 - SPLIT FILE COUNT: '              00840500
                     PV-SPLIT-FILE-COUNT                                00840600
           END-IF.                                                      00840700
                                                                        00840800
           MOVE LOW-VALUES TO PT-PARTITIONS-TABLE.                      00840900
                                                                        00841000
           PERFORM 4200-READ-PARTITIONS-CNTL                            00841100
               UNTIL PS-END-OF-PARTITIONS.                              00841200
                                                                        00841300
           IF PV-PARTITIONS-CNT = ZERO                                  00841400
               DISPLAY '*** EMPTY PARTITIONS CONTROL FILE'              00841500
               SET PV-ABEND-4005 TO TRUE                                00841600
               PERFORM 9999-ABEND                                       00841700
           END-IF.                                                      00841800
                                                                        00841900
           MOVE +1 TO PV-PARTITIONS-SUB.                                00842000
                                                                        00843000
           PERFORM 4300-SPLIT-START-POINT-COUNT.                        00844000
                                                                        00845000
           DISPLAY '****************************************************00846000
      -            '**'.                                                00847000
                                                                        00848000
       EJECT                                                            00849000
                                                                        00850000
      ******************************************************************00860000
      *  WRITE PRICE UPDATES RECORDS TO THE PROPER SPLIT FILE           00870000
      ******************************************************************00880000
       2000-PROCESS-PRICE-UPDTS-SPLIT.                                  00890000
      *                                                                 00900000
           ADD +1 TO PV-SPLIT-FILE-RECS-CNT.                            00910000
                                                                        00920000
           IF IM048-UIR-ITM-NBR >                                       00930000
                 PT-PARTITIONS-ITEM-NBR (PV-PARTITIONS-SUB)             00940000
               SET PS-NO-PARTITION-SWITCH TO TRUE                       00950000
               PERFORM 2010-PARTITIONS-SUB-CHG                          00960000
                   UNTIL PS-PARTITION-SWITCH                            00970000
           END-IF.                                                      00980000
                                                                        00980100
           IF PV-SPLIT-FILE-RECS-CNT >= PV-SPLIT-FILE-COUNT             00980200
               IF PS-SPLIT-FILE-NOT-REACHED                             00980300
                   MOVE PV-PARTITIONS-SUB TO PV-SWITCH-PARTITION-SUB    00980400
                   SET PS-SPLIT-FILE-REACHED TO TRUE                    00980500
               ELSE                                                     00980600
                   IF PV-PARTITIONS-SUB > PV-SWITCH-PARTITION-SUB       00980700
                       ADD 1 TO PS-PRICE-UPDATES-OUT-SW                 00980800
                       MOVE +1 TO PV-SPLIT-FILE-RECS-CNT                00980903
                       SET PS-SPLIT-FILE-NOT-REACHED TO TRUE            00981000
                   END-IF                                               00981100
               END-IF                                                   00981200
           END-IF.                                                      00981300
                                                                        00981400
           EVALUATE PS-PRICE-UPDATES-OUT-SW                             00981500
               WHEN 1                                                   00981600
                   PERFORM 5000-CREATE-PRICE-UPDTS-1-REC                00981700
               WHEN 2                                                   00981800
                   PERFORM 5010-CREATE-PRICE-UPDTS-2-REC                00981900
               WHEN 3                                                   00982000
                   PERFORM 5020-CREATE-PRICE-UPDTS-3-REC                00983000
               WHEN 4                                                   00984000
                   PERFORM 5030-CREATE-PRICE-UPDTS-4-REC                00985000
PK0208         WHEN 5                                                   00985102
PK0208             PERFORM 5040-CREATE-PRICE-UPDTS-5-REC                00985202
PK0208         WHEN 6                                                   00985302
PK0208             PERFORM 5050-CREATE-PRICE-UPDTS-6-REC                00985402
           END-EVALUATE.                                                00986000
                                                                        00987000
           PERFORM 4000-READ-PRICE-UPDATES-IN.                          00988000
                                                                        00989000
      ******************************************************************00990000
      *  FIND THE NEXT PARTITIONS CONTROL ITEM NUMBER RECORD            01000000
      ******************************************************************01010000
       2010-PARTITIONS-SUB-CHG.                                         01020000
      *                                                                 01030000
           ADD +1 TO PV-PARTITIONS-SUB.                                 01040000
                                                                        01050000
           IF PV-PARTITIONS-SUB > PV-PARTITIONS-CNT                     01060000
               DISPLAY '*** IMKUT077 - PARTITIONS SUB OVERFLOW FOR ITEM'01070000
                       ' ID: ' IM048-UIR-ITM-NBR                        01080000
               SET PV-ABEND-4007 TO TRUE                                01090000
               PERFORM 9999-ABEND                                       01100000
           END-IF.                                                      01110000
                                                                        01120000
           IF IM048-UIR-ITM-NBR <=                                      01130000
                 PT-PARTITIONS-ITEM-NBR (PV-PARTITIONS-SUB)             01140000
               SET PS-PARTITION-SWITCH TO TRUE                          01150000
           END-IF.                                                      01160000
                                                                        01170000
      ******************************************************************01180000
      *  WRITE PRICE UPDATES RECORDS TO THE FIRST SPLIT FILE            01181000
      ******************************************************************01182000
       3000-WRITE-PRICE-UPDTS-SPLIT1.                                   01183000
      *                                                                 01184000
           PERFORM 5000-CREATE-PRICE-UPDTS-1-REC.                       01185000
           PERFORM 4000-READ-PRICE-UPDATES-IN.                          01186000
                                                                        01187000
      ***************************************************************** 01187100
      *  READ A PRICE UPDATES INPUT FILE RECORD.                        01187200
      ***************************************************************** 01187300
       4000-READ-PRICE-UPDATES-IN.                                      01187400
      *                                                                 01187500
           READ PRICE-UPDATES-FILE INTO IM048-UPC-ID-PRICE-UPDATE-REC   01187600
             AT END                                                     01187700
                 SET PS-END-OF-PRICE-UPDATES TO TRUE                    01187800
             NOT AT END                                                 01187900
                 ADD +1 TO PV-PRICE-UPDTS-READ-CNT                      01188000
           END-READ.                                                    01189000
                                                                        01190000
      ***************************************************************** 01200000
      *  READ THE PRICE UPDATES COUNT FILE RECORD.                      01210000
      ***************************************************************** 01220000
       4100-READ-PRICE-UPDATES-COUNT.                                   01230000
      *                                                                 01240000
           READ PRICE-UPDATES-COUNT-FILE INTO WS-PRICE-UPDATES-COUNT-REC01250000
             AT END                                                     01260000
                 DISPLAY '*** IMKUT077 - EMPTY PRICE UPDATES COUNT FILE'01270000
                 SET PV-ABEND-4001 TO TRUE                              01280000
                 PERFORM 9999-ABEND                                     01290000
           END-READ.                                                    01300000
                                                                        01310000
      ***************************************************************** 01320000
      *  READ A PARTITIONS CONTROL FILE RECORD.                         01321000
      ***************************************************************** 01322000
       4200-READ-PARTITIONS-CNTL.                                       01323000
                                                                        01324000
           READ PARTITIONS-CONTROL-FILE INTO WS-PARTITIONS-CTRL-RECORD  01325000
             AT END                                                     01326000
                 SET PS-END-OF-PARTITIONS TO TRUE                       01327000
                 DISPLAY '*** IMKUT077 - PARTITIONS CONTROL FILE '      01328000
                         'COUNT: ' PV-PARTITIONS-CNT                    01329000
             NOT AT END                                                 01330000
                 ADD +1 TO PV-PARTITIONS-CNT                            01340000
                 IF PV-PARTITIONS-CNT > PC-MAX-PARTITIONS-CNT           01350000
                     DISPLAY '*** MORE THAN ' PC-MAX-PARTITIONS-CNT     01360000
                             ' PARTITIONS ON PARTITIONS CONTROL FILE'   01370000
                     SET PV-ABEND-4003 TO TRUE                          01380000
                     PERFORM 9999-ABEND                                 01390000
                 END-IF                                                 01400000
                 IF WS-PARTITIONS-ITEM-NBR NUMERIC                      01401000
                     MOVE WS-PARTITIONS-ITEM-NBR TO                     01402000
                           PT-PARTITIONS-ITEM-NBR (PV-PARTITIONS-CNT)   01403000
                     IF PV-PARTITIONS-CNT > +1                          01404000
                         IF PT-PARTITIONS-ITEM-NBR (PV-PARTITIONS-CNT)  01405000
                               <= PT-PARTITIONS-ITEM-NBR                01406000
                                        (PV-PARTITIONS-CNT - 1)         01407000
                             DISPLAY '*** PARTITIONS CONTROL RECORD OUT'01408000
                                     ' OF SEQUENCE FOR RECORD NUMBER: ' 01409000
                                     PV-PARTITIONS-CNT                  01409100
                             DISPLAY '*** PARTITIONS CONTROL RECORD: '  01409200
                                     WS-PARTITIONS-CTRL-RECORD          01409300
                             SET PV-ABEND-4006 TO TRUE                  01409400
                             PERFORM 9999-ABEND                         01409500
                         END-IF                                         01409600
                     END-IF                                             01409700
                 ELSE                                                   01423400
                     DISPLAY '*** NON-NUMERIC PARTITIONS CONTROL RECORD'01423500
                             ' ITEM NUMBER FOR RECORD NUMBER: '         01423600
                             PV-PARTITIONS-CNT                          01423700
                     DISPLAY '*** PARTITIONS CONTROL RECORD: '          01423800
                             WS-PARTITIONS-CTRL-RECORD                  01423900
                     SET PV-ABEND-4004 TO TRUE                          01424000
                     PERFORM 9999-ABEND                                 01424100
                 END-IF                                                 01424200
           END-READ.                                                    01424300
                                                                        01424400
      ***************************************************************** 01424500
      *  READ THE SPLIT START POINT COUNT CONTROL CARD RECORD.          01424600
      ***************************************************************** 01424700
       4300-SPLIT-START-POINT-COUNT.                                    01424800
      *                                                                 01424900
           READ SPLIT-START-POINT-CC-FILE INTO WS-SPLIT-START-POINT-REC 01425000
             AT END                                                     01426000
                 DISPLAY '*** IMKUT077 - EMPTY SPLIT START POINT COUNT '01427000
                         'CONTROL CARD'                                 01428000
                 SET PV-ABEND-4008 TO TRUE                              01428100
                 PERFORM 9999-ABEND                                     01428200
             NOT AT END                                                 01428300
               DISPLAY '*** IMKUT077 - SPLIT START POINT COUNT: '       01428400
                     WS-SPLIT-START-POINT-COUNT                         01428500
           END-READ.                                                    01428600
                                                                        01428700
       EJECT                                                            01428800
                                                                        01428900
      ******************************************************************01429000
      *  CREATE A PRICE UPDATES FILE 1 OUTPUT RECORD                    01430000
      ******************************************************************01440000
       5000-CREATE-PRICE-UPDTS-1-REC.                                   01450000
      *                                                                 01460000
           WRITE SPLIT-UPDATES-FILE-1-REC                               01470000
               FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01480000
                                                                        01490000
           ADD 1 TO PV-OUTPUT-1-WRITTEN-CNT.                            01500000
                                                                        01510000
      ******************************************************************01520000
      *  CREATE A PRICE UPDATES FILE 2 OUTPUT RECORD                    01530000
      ******************************************************************01540000
       5010-CREATE-PRICE-UPDTS-2-REC.                                   01550000
      *                                                                 01560000
           WRITE SPLIT-UPDATES-FILE-2-REC                               01570000
               FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01580000
                                                                        01590000
           ADD 1 TO PV-OUTPUT-2-WRITTEN-CNT.                            01600000
                                                                        01610000
      ******************************************************************01620000
      *  CREATE A PRICE UPDATES FILE 3 OUTPUT RECORD                    01630000
      ******************************************************************01640000
       5020-CREATE-PRICE-UPDTS-3-REC.                                   01650000
      *                                                                 01660000
           WRITE SPLIT-UPDATES-FILE-3-REC                               01670000
               FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01680000
                                                                        01681000
           ADD 1 TO PV-OUTPUT-3-WRITTEN-CNT.                            01682000
                                                                        01683000
      ******************************************************************01684000
      *  CREATE A PRICE UPDATES FILE 4 OUTPUT RECORD                    01685000
      ******************************************************************01686000
       5030-CREATE-PRICE-UPDTS-4-REC.                                   01687000
      *                                                                 01688000
           WRITE SPLIT-UPDATES-FILE-4-REC                               01689000
               FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01689100
                                                                        01689200
           ADD 1 TO PV-OUTPUT-4-WRITTEN-CNT.                            01689300
                                                                        01689400
PK0208******************************************************************01689502
PK0208*  CREATE A PRICE UPDATES FILE 5 OUTPUT RECORD                    01689602
PK0208******************************************************************01689702
PK0208 5040-CREATE-PRICE-UPDTS-5-REC.                                   01689802
PK0208*                                                                 01689902
PK0208     WRITE SPLIT-UPDATES-FILE-5-REC                               01690002
PK0208         FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01690102
PK0208                                                                  01690202
PK0208     ADD 1 TO PV-OUTPUT-5-WRITTEN-CNT.                            01690302
PK0208                                                                  01690402
PK0208******************************************************************01690502
PK0208*  CREATE A PRICE UPDATES FILE 6 OUTPUT RECORD                    01690602
PK0208******************************************************************01690702
PK0208 5050-CREATE-PRICE-UPDTS-6-REC.                                   01690802
PK0208*                                                                 01690902
PK0208     WRITE SPLIT-UPDATES-FILE-6-REC                               01691002
PK0208         FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01691102
PK0208                                                                  01691202
PK0208     ADD 1 TO PV-OUTPUT-6-WRITTEN-CNT.                            01691302
PK0208                                                                  01691402
      ******************************************************************01691500
      *  FINAL PROCESSING FOR THE PROGRAM.                              01691600
      ******************************************************************01691700
       8000-EOJ.                                                        01691800
      *                                                                 01691900
           DISPLAY '*** RECORD COUNT SUMMARY'.                          01692000
           DISPLAY 'PRICE UPDATES RECORDS READ = '                      01692100
                 PV-PRICE-UPDTS-READ-CNT.                               01692200
           DISPLAY 'PARTITION CNTRL RECS READ  = '                      01693000
                 PV-PARTITIONS-CNT                                      01694000
           DISPLAY 'OUTPUT FILE 1 RECS WRITTEN = '                      01695000
                 PV-OUTPUT-1-WRITTEN-CNT.                               01696000
           DISPLAY 'OUTPUT FILE 2 RECS WRITTEN = '                      01697000
                 PV-OUTPUT-2-WRITTEN-CNT.                               01698000
           DISPLAY 'OUTPUT FILE 3 RECS WRITTEN = '                      01699000
                 PV-OUTPUT-3-WRITTEN-CNT.                               01700000
           DISPLAY 'OUTPUT FILE 4 RECS WRITTEN = '                      01710000
                 PV-OUTPUT-4-WRITTEN-CNT.                               01720000
PK0208     DISPLAY 'OUTPUT FILE 5 RECS WRITTEN = '                      01721002
PK0208           PV-OUTPUT-5-WRITTEN-CNT.                               01722002
PK0208     DISPLAY 'OUTPUT FILE 6 RECS WRITTEN = '                      01723002
PK0208           PV-OUTPUT-6-WRITTEN-CNT.                               01724002
           DISPLAY '****************************************************01730000
      -            '**'.                                                01740000
                                                                        01750000
           CLOSE PRICE-UPDATES-FILE                                     01760000
                 PRICE-UPDATES-COUNT-FILE                               01770000
                 PARTITIONS-CONTROL-FILE                                01780000
                 SPLIT-START-POINT-CC-FILE                              01790000
                 SPLIT-UPDATES-FILE-1                                   01800000
                 SPLIT-UPDATES-FILE-2                                   01801000
                 SPLIT-UPDATES-FILE-3                                   01802000
                 SPLIT-UPDATES-FILE-4                                   01803002
PK0208           SPLIT-UPDATES-FILE-5                                   01803102
PK0208           SPLIT-UPDATES-FILE-6.                                  01803202
                                                                        01804000
                                                                        01805000
       9999-ABEND.                                                      01806000
      *                                                                 01807000
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                        01808000
