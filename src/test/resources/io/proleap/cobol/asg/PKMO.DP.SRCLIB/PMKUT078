      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.             PMKUT078.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           FEBRUARY 2003.                                   
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  PMKUT078 - PRICING BUSINESS EXCLUSIONS EVENT INTEGRITY CHECK. *        
      *                                                                *        
      *    -CURRENTLY CHECK THREE DIFFERENT AREAS FOR PRICING (CLRNC,  *        
      *     GRP, SIMPLE).  A FUTURE CHANGE WOULD BE TO INCLUDE TWO     *        
      *     MORE (ADDLAMTOFF AND BWPBUY).                              *        
      *                                                                *        
      *     THIS PROGRAM CREATES THE PRICING EXCLUSIONS BUSINESS       *        
      *     FILE USED IN THE DATA INTEGRITY PROCESS.  FIRST OPEN THE   *        
      *     TWO INPUT FILES AND ONE OUPUT FILE.  WHEN THE TWO INPUT    *        
      *     FILES ARE READ AND NOTHING IS IN EITHER OF THEM ABEND      *        
      *     THE PROGRAM NEXT LOG ON TO ORACLE.  IF SUCCESSFUL THEN     *        
      *     FIND THE CURRENT SYSDATE USING A SELECT STATEMENT.  ONCE   *        
      *     THE SYSDATE HAS BEEN FOUND THEN SELECT THE PRICING EXLUSION*        
      *     RECORDS ON THE PMV_SIMP_PRICG_EXCLN THAT FALL WITHIN THE   *        
      *     DATE RANGE AND HAVE NOT BEEN UPDATED SINCE THE TIMESTAMP   *        
      *     FROM THE TIMESTAMP FILE.  MOVE THE SELECTED VARIABLES TO A *        
      *     COPYBOOK AND IF THE VS_ID IS GREATER THEN ZERO THEN CALL   *        
      *     PMKUT080 TO GET THE STYLE ID.  ONCE DONE MOVE THE COPYBOOK *        
      *     TO THE OUTPUT FILE.  THEN DO THE SAME THING TO             *        
      *     PMV_GP_PRICG_EXCLN, PMV_CLRNC_PRICG_EXCLN, AND             *        
      *     PMV_PROMO_ITM_TIER_PRICG_EXCL.  WHEN ALL RECORDS HAVE BEEN *        
      *     FOUND AND PUT IN THE OUTPUT FILE WRITE THE TOTAL LINE TO   *        
      *     THE OUTPUT FILE.  FINALLY CLOSE THE TWO INPUT AND ONE      *        
      *     OUTPUT FILE.                                               *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      * DATE:         10/25/2004                                                
      * WR/IR#:       P000704857                                                
      * PROGRAMMER:   COLLEEN CUMMINGS                                          
      * DESCRIPTION:  REMOVED PMKUT080 - CHANGED TO LOOK AT TMDARVS.            
      *               CANNOT CONVERT TO STYL_ID FROM OUT PROMO TABLES           
      *               UNTIL INTERFACE CONVERTS OFF OF THAT SO WE ARE            
      *               IN SYNCH WITH WHAT XS IS GETTING.                         
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-RANGE-FILE                                               
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT TMST-FILE                                                     
               ASSIGN                       TO INP02.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
                                                                                
           SELECT PRCG-EXCL-FILE                                                
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DATE-RANGE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  DATE-RANGE-LINE                  PIC  X(80).                         
                                                                                
       FD  TMST-FILE                                                            
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  TMST-LINE                        PIC  X(80).                         
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
       FD  PRCG-EXCL-FILE                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PRCG-EXCL-LINE                   PIC X(250).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-DATE-RANGE-FILE.                                                  
           05  CC-START-DATE                PIC X(10).                          
           05  FILLER                       PIC X(01).                          
           05  CC-END-DATE                  PIC X(10).                          
                                                                                
       01  CC-TMST-FILE.                                                        
           05  CC-LAST-UPD-TMST             PIC X(19).                          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW          PIC X(01)  VALUE 'N'.           
070941         88  PS-END-OF-LOGON-FILE                    VALUE 'Y'.           
070941     05  PS-ORA-SRC-CTL-DONE-SW           PIC X(01)  VALUE 'N'.           
070941         88  PS-ORA-SRC-EMPTY                        VALUE 'Y'.           
           05  PS-DATE-RANGE-FILE-SW            PIC X(01)  VALUE 'N'.           
               88  PS-DATE-RANGE-FILE-END                  VALUE 'Y'.           
               88  PS-DATE-RANGE-FILE-START                VALUE 'N'.           
           05  PS-TMST-FILE-SW                  PIC X(01)  VALUE 'N'.           
               88  PS-TMST-FILE-END                        VALUE 'Y'.           
               88  PS-TMST-FILE-START                      VALUE 'N'.           
           05  PS-END-OF-PRCG-SIMP-CSR-SW       PIC X(01)  VALUE 'N'.           
               88  PS-END-OF-PRCG-SIMP-CSR                 VALUE 'Y'.           
               88  PS-NOT-END-OF-PRCG-SIMP-CSR             VALUE 'N'.           
           05  PS-END-OF-PRCG-GP-CSR-SW         PIC X(01)  VALUE 'N'.           
               88  PS-END-OF-PRCG-GP-CSR                   VALUE 'Y'.           
               88  PS-NOT-END-OF-PRCG-GP-CSR               VALUE 'N'.           
           05  PS-END-OF-PRCG-CLRNC-CSR-SW      PIC X(01)  VALUE 'N'.           
               88  PS-END-OF-PRCG-CLRNC-CSR                VALUE 'Y'.           
               88  PS-NOT-END-OF-PRCG-CLRNC-CSR            VALUE 'N'.           
           05  PS-END-OF-PRCG-TIER-CSR-SW       PIC X(01)  VALUE 'N'.           
               88  PS-END-OF-PRCG-TIER-CSR                 VALUE 'Y'.           
               88  PS-NOT-END-OF-PRCG-TIER-CSR             VALUE 'N'.           
           05  PS-TYPE                          PIC X(01).                      
               88  PS-CLRNC-TYP                            VALUE 'C'.           
               88  PS-SIMP-TYP                             VALUE 'S'.           
               88  PS-GRP-TYP                              VALUE 'G'.           
               88  PS-TIER-TYP                             VALUE 'T'.           
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                         VALUE '/'.              
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
070941*    05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
070941     05  PC-AUTO-LOGON                PIC  X(1)   VALUE '/'.              
070941     05  PC-PROGRAM-NAME              PIC  X(08)  VALUE                   
                                                           'PMKUT078'.          
           05  PC-BUSINESS-EVNT-ID          PIC  9(03)  VALUE 110.              
           05  PC-ACTION-CDE                PIC  X(01)  VALUE 'U'.              
           05  PC-PMKUT080-PGM              PIC  X(08)  VALUE                   
                                                           'PMKUT080'.          
           05  PC-DASH                      PIC  X(01)  VALUE '-'.              
           05  PC-SPACE                     PIC  X(01)  VALUE ' '.              
           05  PC-BEG-TMST                  PIC  X(08)  VALUE                   
                                                          '00:00:00'.           
           05  PC-END-TMST                  PIC  X(08)  VALUE                   
                                                          '23:59:59'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC  S9(4)     VALUE ZEROS          
070941                                                     COMP SYNC.           
070941     05  PV-ORACLE-PASSWORD-LEN       PIC  S9(4)     VALUE ZEROS          
070941                                                     COMP SYNC.           
070941     05  PC-AUTO-LOGON                PIC  X(01)     VALUE '/'.           
           05  PV-SQL                       PIC S9(04)     VALUE ZEROS.         
           05  PV-TABLE-NAME                PIC  X(12)     VALUE SPACES.        
           05  PV-OPERATION-MSG             PIC  X(72)     VALUE SPACES.        
           05  PV-OPERATION-MSG-1           PIC  X(40)     VALUE SPACES.        
           05  PV-PARAGRAPH-NAME            PIC  X(30)     VALUE SPACES.        
           05  PV-SYS-DATE                  PIC  X(26)     VALUE SPACES.        
           05  PV-SYS-DATE-XS REDEFINES PV-SYS-DATE.                            
               10  PV-SYS-DATE-DATE         PIC  X(10).                         
               10  PV-PERIOD                PIC  X(01).                         
               10  PV-SYS-DATE-TIME         PIC  X(15).                         
           05  PV-NEW-STRT-DATE             PIC  X(26)     VALUE SPACES.        
           05  PV-NEW-END-DATE              PIC  X(26)     VALUE SPACES.        
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-PROMO-INTFC-ID            PIC  9(10)     VALUE ZEROS.         
           05  PV-PROMO-INTFC-SUB-ID        PIC  9(10)     VALUE ZEROS.         
           05  PV-PRICG-TYP-CDE             PIC  X(03)     VALUE SPACES.        
           05  PV-PROMO-ITM-ID              PIC  9(10)     VALUE ZEROS.         
           05  PV-MDSE-LNE-ID               PIC  9(10)     VALUE ZEROS.         
           05  PV-MDSE-LNE-EXCLN-ID         PIC  9(10)     VALUE ZERO.          
           05  PV-MDSE-HIER-CDE             PIC  X(03)     VALUE SPACES.        
           05  PV-DEPT-NBR                  PIC  9(04)     VALUE ZEROS.         
           05  PV-MAJ-CL-NBR                PIC  9(04)     VALUE ZEROS.         
           05  PV-SUB-CL-NBR                PIC  9(04)     VALUE ZEROS.         
           05  PV-VS-ID                     PIC  9(09)     VALUE ZEROS.         
           05  PV-STYL-ID                   PIC  9(09)     VALUE ZEROS.         
           05  PV-SKU-NBR                   PIC  9(08)     VALUE ZEROS.         
           05  PV-STAT-CDE                  PIC  X(01)     VALUE SPACES.        
           05  PV-LAST-UPD-TMST             PIC  X(19)     VALUE SPACES.        
           05  PV-INTFC-SUB-ID-NULL         PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-DEPT-NBR-NULL             PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-MAJ-CL-NBR-NULL           PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-SUB-CL-NBR-NULL           PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-VS-ID-NULL                PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-STYL-ID-NULL              PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-SKU-NBR-NULL              PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
       01  PA-IND-PROGRAM-ACCUMULATORS.                                         
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-PRCG-SIMP-CNT             PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-PRCG-GP-CNT               PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-PRCG-CLRNC-CNT            PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-PRCG-TIER-CNT             PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE PRICING BUSINESS EVENT                              
      ******************************************************************        
           COPY PMRD708.                                                        
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE TOTAL RECORD                                        
      ******************************************************************        
           COPY PMRD700.                                                        
                                                                                
      *****************************************************************         
      *    THE LAYOUT FOR THE GET STYLE ID                            *         
      *    PROGRAM PMKUT080 PARAMETER LIST.                           *         
      *****************************************************************         
           COPY PMRD800.                                                        
                                                                                
           EJECT                                                                
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - RESET SWITCHES                                             *         
      *  - OPEN INPUT AND OUTPUT FILES                                *         
      *  - READ THE DATE RANGE AND TIMESTAMP FILE                     *         
      *  - SET UP ORACLE                                              *         
      *  - FIND THE CURRENT SYS DATE                                  *         
      *  - RETRIEVE SIMPLE PRICING INFORMATION                        *         
      *  - RETRIEVE GROUP PRICING INFORMATION                         *         
      *  - RETRIEVE CLEARANCE PRICING INFORMATION                     *         
      *  - RETRIEVE TIERED PRICING INFORMATION                        *         
      *  - WRITE THE TOTAL LINES                                      *         
      *  - CLOSE THE INPUT AND OUTPUT FILES                           *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           SET PS-DATE-RANGE-FILE-START TO TRUE.                                
           SET PS-TMST-FILE-START TO TRUE.                                      
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-DATE-RANGE-FILE.                                   
           PERFORM B300-READ-TMST-FILE.                                         
                                                                                
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
                                                                                
           PERFORM C100-FIND-SYSDATE.                                           
                                                                                
           PERFORM C200-DECLARE-PRCG-SIMP-CSR.                                  
           PERFORM C300-OPEN-PRCG-SIMP-CSR.                                     
           PERFORM C400-FETCH-PRCG-SIMP-CSR.                                    
           PERFORM C500-PROCESS-PRCG-SIMP-CSR                                   
               UNTIL PS-END-OF-PRCG-SIMP-CSR                                    
           PERFORM C600-CLOSE-PRCG-SIMP-CSR.                                    
                                                                                
           PERFORM C210-DECLARE-PRCG-GP-CSR.                                    
           PERFORM C310-OPEN-PRCG-GP-CSR.                                       
           PERFORM C410-FETCH-PRCG-GP-CSR.                                      
           PERFORM C510-PROCESS-PRCG-GP-CSR                                     
               UNTIL PS-END-OF-PRCG-GP-CSR                                      
           PERFORM C610-CLOSE-PRCG-GP-CSR.                                      
                                                                                
           PERFORM C220-DECLARE-PRCG-CLRNC-CSR.                                 
           PERFORM C320-OPEN-PRCG-CLRNC-CSR.                                    
           PERFORM C420-FETCH-PRCG-CLRNC-CSR.                                   
           PERFORM C520-PROCESS-PRCG-CLRNC-CSR                                  
               UNTIL PS-END-OF-PRCG-CLRNC-CSR                                   
           PERFORM C620-CLOSE-PRCG-CLRNC-CSR.                                   
                                                                                
           PERFORM C230-DECLARE-PRCG-TIER-CSR.                                  
           PERFORM C330-OPEN-PRCG-TIER-CSR.                                     
           PERFORM C430-FETCH-PRCG-TIER-CSR.                                    
           PERFORM C530-PROCESS-PRCG-TIER-CSR                                   
               UNTIL PS-END-OF-PRCG-TIER-CSR                                    
           PERFORM C630-CLOSE-PRCG-TIER-CSR.                                    
                                                                                
           PERFORM W200-WRITE-TOTAL-LINE.                                       
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           DISPLAY 'RECORDS FROM PRICING SIMPLE CSR = '                 LE-CNT  
                                           PA-PRCG-SIMP-CNT.                    
           DISPLAY 'RECORDS FROM PRICING GROUP CSR = '                  LE-CNT  
                                           PA-PRCG-GP-CNT.                      
           DISPLAY 'RECORDS FROM PRICING CLRNC CSR = '                  LE-CNT  
                                           PA-PRCG-CLRNC-CNT.                   
           DISPLAY 'RECORDS FROM PRICING TIER CSR = '                   LE-CNT  
                                           PA-PRCG-TIER-CNT.                    
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN TWO INPUT FILES AND ONE OUTPUT FILE                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM 9900-ORACLE-ABEND                                        
070941     END-IF.                                                              
                                                                                
           OPEN INPUT  DATE-RANGE-FILE                                          
                INPUT  TMST-FILE                                                
                OUTPUT PRCG-EXCL-FILE.                                          
                                                                                
      *****************************************************************         
      * B200-READ-DATE-RANGE-FILE.                                    *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE DATE RANGE FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-DATE-RANGE-FILE.                                               
                                                                                
           READ DATE-RANGE-FILE INTO CC-DATE-RANGE-FILE                         
              AT END                                                            
                 SET PS-DATE-RANGE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF PS-DATE-RANGE-FILE-END                                            
               MOVE 'NO DATE RANGE FILE TO PROCESS'                             
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-START-DATE = ' CC-START-DATE                         
               DISPLAY 'CC-END-DATE   = ' CC-END-DATE                           
                                                                                
               STRING CC-START-DATE DELIMITED BY SPACE                          
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-BEG-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-STRT-DATE                                     
                                                                                
               STRING CC-END-DATE DELIMITED BY SPACE                            
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-END-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-END-DATE                                      
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B300-READ-TMST-FILE.                                          *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE TMST FILE                                      *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B300-READ-TMST-FILE.                                                     
                                                                                
           READ TMST-FILE INTO CC-TMST-FILE                                     
              AT END                                                            
                 SET PS-TMST-FILE-END TO TRUE                                   
           END-READ.                                                            
                                                                                
           IF PS-TMST-FILE-END                                                  
               MOVE 'NO TMST FILE TO PROCESS'                                   
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-LAST-UPD-TMST = ' CC-LAST-UPD-TMST                   
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE TWO INPUT FILES AND ONE OUTPUT FILE              *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  DATE-RANGE-FILE                                               
                  TMST-FILE                                                     
                  PRCG-EXCL-FILE.                                               
                                                                                
      *****************************************************************         
      * C100-FIND-SYSDATE:                                            *         
      * ==> PROCESSES:                                                *         
      *  - SINCE NO TABLE IS ACTUALLY USED WE USE THE DUAL TABLE      *         
      *  - FIND THE CURRENT DATE AND TIME                                       
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C100-FIND-SYSDATE.                                                       
           EXEC SQL                                                             
               SELECT                                                           
                   SYSDATE                                                      
               INTO :PV-SYS-DATE                                                
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 MOVE PC-DASH TO PV-PERIOD                                      
                 STRING PV-SYS-DATE-XS DELIMITED BY SPACE                       
                        '.000000'      DELIMITED BY SIZE                        
                        INTO PV-SYS-DATE-XS                                     
                 DISPLAY 'SYS DATE = ' PV-SYS-DATE-XS                           
                 CONTINUE                                                       
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-FIND-SYSDATE'   TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C200-DECLARE-PRCG-SIMP-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - USE A VIEW AND TABLES                                      *         
      *  - DECLARE CSR THAT RETRIEVES SIMPLE PRICING                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C200-DECLARE-PRCG-SIMP-CSR.                                              
                                                                                
           EXEC SQL                                                             
                DECLARE PRICESIMPLECSR                                          
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.SIMP_PROMO_INTFC_ID                                  
                        ,A.MDSE_SELN_LNE_ID                                     
                        ,A.MDSE_SELN_LNE_EXCLN_ID                               
                        ,A.MDSE_HIER_CDE                                        
                        ,A.DEPT_NBR                                             
                        ,A.MAJ_CL_NBR                                           
                        ,A.SUB_CL_NBR                                           
                        ,A.VS_ID                                                
                        ,F.RMS_STYL_ID                                          
                        ,A.SKU_NBR                                              
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM PMV_SIMP_PRICG_EXCLN A                                 
                        ,PMT_MDSE_SELN_LNE B                                    
                        ,PMT_MDSE_SELN C                                        
                        ,PMT_PROMO_ITM D                                        
                        ,PMT_PROMO E                                            
                        ,K_DBLINKS.V_TMDARVS F                                  
                    WHERE                                                       
                          A.MDSE_SELN_LNE_ID  = B.MDSE_SELN_LNE_ID              
                      AND B.MDSE_SELN_ID = C.MDSE_SELN_ID                       
                      AND C.PROMO_ITM_ID = D.PROMO_ITM_ID                       
                      AND D.PROMO_ID = E.PROMO_ID                               
                      AND A.DEPT_NBR       = F.DEPT_NBR(+)                      
                      AND A.MAJ_CL_NBR     = F.MAJ_CL_NBR(+)                    
                      AND A.SUB_CL_NBR     = F.SUB_CL_NBR(+)                    
                      AND A.VS_ID          = F.VS_ID(+)                         
                      AND E.DFLT_END_TMST >= TO_DATE(                           
                          :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')           
                      AND E.DFLT_STRT_TMST <= TO_DATE(                          
                          :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')            
                      AND A.LAST_UPD_TMST <= TO_DATE(                           
                          :CC-LAST-UPD-TMST, 'YYYY-MM-DD-HH24.MI.SS')           
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C210-DECLARE-PRCG-GP-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - USE A VIEW AND TABLES                                      *         
      *  - DECLARE CSR THAT RETRIEVES GROUP PRICING                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C210-DECLARE-PRCG-GP-CSR.                                                
                                                                                
           EXEC SQL                                                             
                DECLARE PRICEGROUPCSR                                           
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.GP_PROMO_INTFC_ID                                    
                        ,A.MDSE_SELN_LNE_ID                                     
                        ,A.MDSE_SELN_LNE_EXCLN_ID                               
                        ,A.MDSE_HIER_CDE                                        
                        ,A.DEPT_NBR                                             
                        ,A.MAJ_CL_NBR                                           
                        ,A.SUB_CL_NBR                                           
                        ,A.VS_ID                                                
                        ,F.RMS_STYL_ID                                          
                        ,A.SKU_NBR                                              
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM PMV_GP_PRICG_EXCLN A                                   
                        ,PMT_MDSE_SELN_LNE B                                    
                        ,PMT_MDSE_SELN C                                        
                        ,PMT_PROMO_ITM D                                        
                        ,PMT_PROMO E                                            
                        ,K_DBLINKS.V_TMDARVS F                                  
                    WHERE                                                       
                          A.MDSE_SELN_LNE_ID  = B.MDSE_SELN_LNE_ID              
                      AND B.MDSE_SELN_ID = C.MDSE_SELN_ID                       
                      AND C.PROMO_ITM_ID = D.PROMO_ITM_ID                       
                      AND D.PROMO_ID = E.PROMO_ID                               
                      AND A.DEPT_NBR       = F.DEPT_NBR(+)                      
                      AND A.MAJ_CL_NBR     = F.MAJ_CL_NBR(+)                    
                      AND A.SUB_CL_NBR     = F.SUB_CL_NBR(+)                    
                      AND A.VS_ID          = F.VS_ID(+)                         
                      AND E.DFLT_END_TMST >= TO_DATE(                           
                          :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')           
                      AND E.DFLT_STRT_TMST <= TO_DATE(                          
                          :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')            
                      AND A.LAST_UPD_TMST <= TO_DATE(                           
                          :CC-LAST-UPD-TMST, 'YYYY-MM-DD-HH24.MI.SS')           
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C220-DECLARE-PRCG-CLRNC-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - USE A VIEW AND TABLES                                      *         
      *  - DECLARE CSR THAT RETRIEVES CLRNC PRICING                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C220-DECLARE-PRCG-CLRNC-CSR.                                             
                                                                                
           EXEC SQL                                                             
                DECLARE PRICECLRNCCSR                                           
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.CLRNC_PROMO_INTFC_ID                                 
                        ,A.MDSE_SELN_LNE_ID                                     
                        ,A.MDSE_SELN_LNE_EXCLN_ID                               
                        ,A.MDSE_HIER_CDE                                        
                        ,A.DEPT_NBR                                             
                        ,A.MAJ_CL_NBR                                           
                        ,A.SUB_CL_NBR                                           
                        ,A.VS_ID                                                
                        ,F.RMS_STYL_ID                                          
                        ,A.SKU_NBR                                              
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM PMV_CLRNC_PRICG_EXCLN A                                
                        ,PMT_CLRNC_MDSE_LNE B                                   
                        ,PMT_CLRNC_PROMO C                                      
                        ,K_DBLINKS.V_TMDARVS F                                  
                    WHERE A.MDSE_SELN_LNE_ID = B.MDSE_SELN_LNE_ID               
                      AND B.CLRNC_PROMO_ID = C.CLRNC_PROMO_ID                   
                      AND A.DEPT_NBR       = F.DEPT_NBR(+)                      
                      AND A.MAJ_CL_NBR     = F.MAJ_CL_NBR(+)                    
                      AND A.SUB_CL_NBR     = F.SUB_CL_NBR(+)                    
                      AND A.VS_ID          = F.VS_ID(+)                         
                      AND C.END_TMST >= TO_DATE(                                
                          :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')           
                      AND C.STRT_TMST <= TO_DATE(                               
                          :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')            
                      AND A.LAST_UPD_TMST <= TO_DATE(                           
                          :CC-LAST-UPD-TMST, 'YYYY-MM-DD-HH24.MI.SS')           
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C230-DECLARE-PRCG-TIER-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - USE A VIEW AND TABLES                                      *         
      *  - DECLARE CSR THAT RETRIEVES TIERED PRICING                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C230-DECLARE-PRCG-TIER-CSR.                                              
                                                                                
           EXEC SQL                                                             
                DECLARE PRICETIERCSR                                            
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.TIER_PROMO_INTFC_ID                                  
                        ,A.TIER_PROMO_INTFC_SUB_ID                              
                        ,A.MDSE_SELN_LNE_ID                                     
                        ,A.MDSE_SELN_LNE_EXCLN_ID                               
                        ,A.MDSE_HIER_CDE                                        
                        ,A.DEPT_NBR                                             
                        ,A.MAJ_CL_NBR                                           
                        ,A.SUB_CL_NBR                                           
                        ,A.VS_ID                                                
                        ,F.RMS_STYL_ID                                          
                        ,A.SKU_NBR                                              
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM PMV_PROMO_ITM_TIER_PRICG_EXCLN A                       
                        ,PMT_MDSE_SELN_LNE B                                    
                        ,PMT_MDSE_SELN C                                        
                        ,PMT_PROMO_ITM D                                        
                        ,PMT_PROMO E                                            
                        ,K_DBLINKS.V_TMDARVS F                                  
                    WHERE                                                       
                          A.MDSE_SELN_LNE_ID  = B.MDSE_SELN_LNE_ID              
                      AND B.MDSE_SELN_ID = C.MDSE_SELN_ID                       
                      AND C.PROMO_ITM_ID = D.PROMO_ITM_ID                       
                      AND D.PROMO_ID = E.PROMO_ID                               
                      AND A.DEPT_NBR       = F.DEPT_NBR(+)                      
                      AND A.MAJ_CL_NBR     = F.MAJ_CL_NBR(+)                    
                      AND A.SUB_CL_NBR     = F.SUB_CL_NBR(+)                    
                      AND A.VS_ID          = F.VS_ID(+)                         
                      AND E.DFLT_END_TMST >= TO_DATE(                           
                          :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')           
                      AND E.DFLT_STRT_TMST <= TO_DATE(                          
                          :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')            
                      AND A.LAST_UPD_TMST <= TO_DATE(                           
                          :CC-LAST-UPD-TMST, 'YYYY-MM-DD-HH24.MI.SS')           
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C300-OPEN-PRCG-SIMP-CSR.                                      *         
      * ==> PROCESSES:                                                *         
      *  - OPEN PRICESIMPLECSR                                        *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C300-OPEN-PRCG-SIMP-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                 OPEN PRICESIMPLECSR                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-PRCG-SIMP-CSR      '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PRICING-SIMP-CSR '                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C310-OPEN-PRCG-GP-CSR.                                        *         
      * ==> PROCESSES:                                                *         
      *  - OPEN PRICEGROUPCSR                                         *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C310-OPEN-PRCG-GP-CSR.                                                   
                                                                                
           EXEC SQL                                                             
                 OPEN PRICEGROUPCSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C310-OPEN-PRCG-GP-CSR      '                           
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PRICING-GP-CSR '                         
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C320-OPEN-PRCG-CLRNC-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - OPEN PRICECLRNCCSR                                         *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C320-OPEN-PRCG-CLRNC-CSR.                                                
                                                                                
           EXEC SQL                                                             
                 OPEN PRICECLRNCCSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C320-OPEN-PRCG-CLRNC-CSR      '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PRICING-CLRNC-CSR '                      
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C330-OPEN-PRCG-TIER-CSR.                                      *         
      * ==> PROCESSES:                                                *         
      *  - OPEN PRICETIERCSR                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C330-OPEN-PRCG-TIER-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                 OPEN PRICETIERCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C330-OPEN-PRCG-TIER-CSR      '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PRICING-TIER-CSR '                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C400-FETCH-PRCG-SIMP-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C500-PROCESS-PRCG-SIMP-CSR *         
      *****************************************************************         
       C400-FETCH-PRCG-SIMP-CSR.                                                
                                                                                
           EXEC SQL                                                             
               FETCH PRICESIMPLECSR                                             
                INTO  :PV-PROMO-INTFC-ID                                        
                    , :PV-MDSE-LNE-ID                                           
                    , :PV-MDSE-LNE-EXCLN-ID                                     
                    , :PV-MDSE-HIER-CDE                                         
                    , :PV-DEPT-NBR           :PV-DEPT-NBR-NULL                  
                    , :PV-MAJ-CL-NBR         :PV-MAJ-CL-NBR-NULL                
                    , :PV-SUB-CL-NBR         :PV-SUB-CL-NBR-NULL                
                    , :PV-VS-ID              :PV-VS-ID-NULL                     
                    , :PV-STYL-ID            :PV-STYL-ID-NULL                   
                    , :PV-SKU-NBR            :PV-SKU-NBR-NULL                   
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-PRCG-SIMP-CNT                                    
                   SET PS-SIMP-TYP TO TRUE                                      
                   PERFORM D100-MOVE-TO-CPYBK                                   
              WHEN SQLCODE = 1403                                               
                   DISPLAY 'PRICING SIMPLE COUNT FROM FETCH = '         LE-CNT  
                                                  PA-PRCG-SIMP-CNT              
                   DISPLAY 'END OF PRICESIMPLECSR'                              
                   SET PS-END-OF-PRCG-SIMP-CSR TO TRUE                          
               WHEN OTHER                                                       
                   MOVE 'C400-FETCH-PRCG-SIMP-CSR        '                      
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PRICING-SIMP-CSR   '                    
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C410-FETCH-PRCG-GP-CSR.                                       *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C510-PROCESS-PRCG-GP-CSR   *         
      *****************************************************************         
       C410-FETCH-PRCG-GP-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               FETCH PRICEGROUPCSR                                              
                INTO  :PV-PROMO-INTFC-ID                                        
                    , :PV-MDSE-LNE-ID                                           
                    , :PV-MDSE-LNE-EXCLN-ID                                     
                    , :PV-MDSE-HIER-CDE                                         
                    , :PV-DEPT-NBR           :PV-DEPT-NBR-NULL                  
                    , :PV-MAJ-CL-NBR         :PV-MAJ-CL-NBR-NULL                
                    , :PV-SUB-CL-NBR         :PV-SUB-CL-NBR-NULL                
                    , :PV-VS-ID              :PV-VS-ID-NULL                     
                    , :PV-STYL-ID            :PV-STYL-ID-NULL                   
                    , :PV-SKU-NBR            :PV-SKU-NBR-NULL                   
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-PRCG-GP-CNT                                      
                   SET PS-GRP-TYP TO TRUE                                       
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'PRICING GROUP COUNT FROM FETCH = '          LE-CNT  
                                                  PA-PRCG-GP-CNT                
                   DISPLAY 'END OF PRICEGROUPCSR'                               
                   SET PS-END-OF-PRCG-GP-CSR TO TRUE                            
               WHEN OTHER                                                       
                   MOVE 'C410-FETCH-PRCG-GP-CSR        '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PRICING-GP-CSR   '                      
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C420-FETCH-PRCG-CLRNC-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C520-PROCESS-PRCG-CLRNC-CSR*         
      *****************************************************************         
       C420-FETCH-PRCG-CLRNC-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH PRICECLRNCCSR                                              
                INTO  :PV-PROMO-INTFC-ID                                        
                    , :PV-MDSE-LNE-ID                                           
                    , :PV-MDSE-LNE-EXCLN-ID                                     
                    , :PV-MDSE-HIER-CDE                                         
                    , :PV-DEPT-NBR                :PV-DEPT-NBR-NULL             
                    , :PV-MAJ-CL-NBR              :PV-MAJ-CL-NBR-NULL           
                    , :PV-SUB-CL-NBR              :PV-SUB-CL-NBR-NULL           
                    , :PV-VS-ID                   :PV-VS-ID-NULL                
                    , :PV-STYL-ID                 :PV-STYL-ID-NULL              
                    , :PV-SKU-NBR                 :PV-SKU-NBR-NULL              
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-PRCG-CLRNC-CNT                                   
                   SET PS-CLRNC-TYP TO TRUE                                     
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'PRICING CLRNC COUNT FROM FETCH = '          LE-CNT  
                                                  PA-PRCG-CLRNC-CNT             
                   DISPLAY 'END OF PRICECLRNCCSR'                               
                   SET PS-END-OF-PRCG-CLRNC-CSR TO TRUE                         
               WHEN OTHER                                                       
                   MOVE 'C420-FETCH-PRCG-CLRNC-CSR        '                     
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PRICING-CLRNC-CSR   '                   
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C430-FETCH-PRCG-TIER-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C530-PROCESS-PRCG-TIER-CSR *         
      *****************************************************************         
       C430-FETCH-PRCG-TIER-CSR.                                                
                                                                                
           EXEC SQL                                                             
               FETCH PRICETIERCSR                                               
                INTO  :PV-PROMO-INTFC-ID                                        
                    , :PV-PROMO-INTFC-SUB-ID                                    
                    , :PV-MDSE-LNE-ID                                           
                    , :PV-MDSE-LNE-EXCLN-ID                                     
                    , :PV-MDSE-HIER-CDE                                         
                    , :PV-DEPT-NBR                :PV-DEPT-NBR-NULL             
                    , :PV-MAJ-CL-NBR              :PV-MAJ-CL-NBR-NULL           
                    , :PV-SUB-CL-NBR              :PV-SUB-CL-NBR-NULL           
                    , :PV-VS-ID                   :PV-VS-ID-NULL                
                    , :PV-STYL-ID                 :PV-STYL-ID-NULL              
                    , :PV-SKU-NBR                 :PV-SKU-NBR-NULL              
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-PRCG-TIER-CNT                                    
                   SET PS-TIER-TYP TO TRUE                                      
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'PRICING TIER COUNT FROM FETCH = '           LE-CNT  
                                                  PA-PRCG-TIER-CNT              
                   DISPLAY 'END OF PRICETIERCSR'                                
                   SET PS-END-OF-PRCG-TIER-CSR TO TRUE                          
               WHEN OTHER                                                       
                   MOVE 'C430-FETCH-PRCG-TIER-CSR        '                      
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PRICING-TIER-CSR   '                    
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C500-PROCESS-PRCG-SIMP-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C500-PROCESS-PRCG-SIMP-CSR.                                              
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C400-FETCH-PRCG-SIMP-CSR.                                    
                                                                                
      *****************************************************************         
      * C510-PROCESS-PRCG-GP-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C510-PROCESS-PRCG-GP-CSR.                                                
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C410-FETCH-PRCG-GP-CSR.                                      
                                                                                
      *****************************************************************         
      * C520-PROCESS-PRCG-CLRNC-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C520-PROCESS-PRCG-CLRNC-CSR.                                             
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C420-FETCH-PRCG-CLRNC-CSR.                                   
                                                                                
      *****************************************************************         
      * C530-PROCESS-PRCG-TIER-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C530-PROCESS-PRCG-TIER-CSR.                                              
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C430-FETCH-PRCG-TIER-CSR.                                    
                                                                                
      *****************************************************************         
      * C600-CLOSE-PRCG-SIMP-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PRICESIMPLECSR                                   *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C600-CLOSE-PRCG-SIMP-CSR.                                                
                                                                                
           EXEC SQL                                                             
                CLOSE PRICESIMPLECSR                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C600-CLOSE-PRCG-SIMP-CSR      '                         
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PRICESIMPLECSR     '                     
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C610-CLOSE-PRCG-GP-CSR.                                       *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PRICEGROUPCSRR                                   *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C610-CLOSE-PRCG-GP-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                CLOSE PRICEGROUPCSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C610-CLOSE-PRCG-GP-CSR      '                           
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PRICEGROUPCSR     '                      
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C620-CLOSE-PRCG-CLRNC-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PRICECLRNCCSR                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C620-CLOSE-PRCG-CLRNC-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE PRICECLRNCCSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C620-CLOSE-PRCG-CLRNC-CSR      '                        
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PRICECLRNCCSR     '                      
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C630-CLOSE-PRCG-TIER-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PRICETIERCSR                                     *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C630-CLOSE-PRCG-TIER-CSR.                                                
                                                                                
           EXEC SQL                                                             
                CLOSE PRICETIERCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C630-CLOSE-PRCG-TIER-CSR      '                         
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PRICETIERCSR     '                       
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D100-MOVE-TO-CPYBK                                            *         
      * ==> PROCESSES:                                                *         
      *  - MOVE THE VARIABLES TO THE COPYBOOK                         *         
      * ==> PERFORMED FROM: C400-FETCH-PRCG-SIMP-CSR,                 *         
      *       C410-FETCH-PRCG-GP-CSR, C420-FETCH-PRCG-CLRNC-CSR       *         
      *       C430-FETCH-PRCG-TIER-CSR                                *         
      *****************************************************************         
       D100-MOVE-TO-CPYBK.                                                      
                                                                                
           INITIALIZE PM708-PRICING-EXCLSNS-RECORD.                             
           INITIALIZE PM800-PARAMETERS.                                         
           MOVE PC-BUSINESS-EVNT-ID    TO PM708-BUSINESS-EVNT-ID.               
           MOVE PC-ACTION-CDE          TO PM708-ACTION-CDE.                     
           MOVE PV-SYS-DATE-XS         TO PM708-BUSINESS-EVNT-CRE-TMST.         
           MOVE PV-PROMO-INTFC-ID      TO PM708-PROMO-INTFC-ID.                 
           MOVE PV-MDSE-LNE-ID         TO PM708-MDSE-LNE-ID.                    
           MOVE PV-MDSE-LNE-EXCLN-ID   TO PM708-MDSE-LNE-EXCLN-ID.              
           MOVE PV-MDSE-HIER-CDE       TO PM708-MDSE-HIER-CDE.                  
           MOVE PV-DEPT-NBR            TO PM708-DEPT-NBR.                       
           MOVE PV-MAJ-CL-NBR          TO PM708-MAJ-CL-NBR.                     
           MOVE PV-SUB-CL-NBR          TO PM708-SUB-CL-NBR.                     
           MOVE PV-STYL-ID             TO PM708-STYLE-ID.                       
           MOVE PV-SKU-NBR             TO PM708-SKU-NBR.                        
           MOVE PV-STAT-CDE            TO PM708-STAT-CDE.                       
           STRING PV-LAST-UPD-TMST DELIMITED BY SPACE                           
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PM708-LAST-UPD-TMST.                                     
                                                                                
           EVALUATE TRUE                                                        
               WHEN PS-TIER-TYP                                                 
                   MOVE PV-PROMO-INTFC-SUB-ID                                   
                                         TO PM708-PROMO-INTFC-SUB-ID            
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * W100-WRITE-DETAIL.                                            *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DETAIL LINE TO THE PRICING EXCLST OUTPUT FILE    *         
      * ==> PERFORMED FROM: C500-PROCESS-PRCG-SIMP-CSR                *         
      *      C510-PROCESS-PRCG-GP-CSR, C520-PROCESS-PRCG-CLRNC-CSR    *         
      *      C530-PROCESS-PRCG-TIER-CSR                               *         
      *****************************************************************         
       W100-WRITE-DETAIL.                                                       
                                                                                
           WRITE PRCG-EXCL-LINE FROM PM708-PRICING-EXCLSNS-RECORD.              
                                                                                
           ADD +1                    TO PA-RECS-WRITTEN.                        
                                                                                
      *****************************************************************         
      * W200-WRITE-TOTAL-LINE                                         *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE TOTAL LINE TO THE PRICING EXCLUSN OUTPUT FILE    *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       W200-WRITE-TOTAL-LINE.                                                   
                                                                                
           MOVE PC-BUSINESS-EVNT-ID TO PM700-BUSINESS-EVNT-ID.                  
           MOVE PA-RECS-WRITTEN TO PM700-TOTAL-NBR-OF-RECS.                     
           WRITE PRCG-EXCL-LINE FROM PM700-TOTAL-RECORD.                        
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
070941*    DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
070941     DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL.                            
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
070941*    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
