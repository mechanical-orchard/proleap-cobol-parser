000100******************************************************************04/11/95
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************   LV002
000400                                                                          
000500 PROGRAM-ID.                VLKCS506.                                     
000600 AUTHOR.                    MARY SARDINA.                                 
000700 INSTALLATION.              KOHLS DEPARTMENT STORES.                      
000800 DATE-WRITTEN.              07-28-96.                                     
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  CICS PROGRAM - VLKCS506 - V506                                *        
001300*                                                                *        
001400*       PO SYSTEM PRETICKET INQUIRE LIST SCREEN                  *        
001500*                                                                *        
001600*  THIS SCREEN WILL DISPLAY ALL FO THE PRE/TICKET/LOCKER STOCK   *        
001700*  REQUESTS THAT HAVE BEEN PRINTED AND ARE WAITING TO BE SHIPPED *        
001800******************************************************************        
      *  CHANGED 11/03/14 BY C. PARMLEY          WR#: CHG0094517       *        
      *   EXPANDED PO# FROM 7 TO 8 DIGITS                              *        
001800******************************************************************        
263627*----------------------------------------------------------------*        
263627* CHANGED 11/05/2021    BY JIM HUNTER      WR: CHG0263627                 
263627* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263627* MAKING THE CALL DYNAMIC VS STATIC.                                      
263627*----------------------------------------------------------------*        
CICS  * CHANGED 11/11/2022    BY JIM HUNTER      WR: CHG0278236                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *****************************************************************         
001900 ENVIRONMENT DIVISION.                                                    
002000                                                                          
002100 DATA DIVISION.                                                           
002200                                                                          
002300 WORKING-STORAGE SECTION.                                                 
002400                                                                          
003200 01  PC-PROGRAM-CONSTANTS.                                                
003300     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
003400                                                    'VL506A  '.           
003500     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
003600                                                    'VLKM506 '.           
003700     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
003800                                                    'VLKCS506'.           
003900     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
004000     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
004100     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
004200     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +12             
004300                                                    COMP SYNC.            
004400     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
004500                                                    COMP SYNC.            
004600     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
004700                                                    +1000000              
004800                                                    COMP-3.               
004900     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
005000                                                    COMP SYNC.            
005100     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
005200                                                    COMP-3.               
005300     05  PC-PERCENT                     PIC  X(01)  VALUE '%'.            
005400     05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
005410     05  PC-SHP                         PIC  X(03)  VALUE 'SHP'.          
005500     05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.          
005600     05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.          
005700     05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
005710     05  PC-P                           PIC  X(01)  VALUE 'P'.            
005720     05  PC-SLASH                       PIC  X(01)  VALUE '/'.            
005800*                                                                         
005900     05  PC-TSYMSG-NUMBERS.                                               
006000         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
006100         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
006200         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
006210         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
006300         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
006400         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
006500         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
006600         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
006700         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
006800         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
006900         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
007000         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
007100         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
007200         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
007201         10  PC-TSYMSG-01362            PIC  9(05)  VALUE 01362.          
007202         10  PC-TSYMSG-01363            PIC  9(05)  VALUE 01363.          
007202         10  PC-TSYMSG-01384            PIC  9(05)  VALUE 01384.          
007300         10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
007400*                                                                         
007500 01  PS-PROGRAM-SWITCHES.                                                 
007600     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
007700         88  PS-NO-ERROR                            VALUE 'Y'.            
007800         88  PS-ERROR                               VALUE 'N'.            
007900     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
008000         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
008100         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
008200*                                                                         
008300 01  PV-PROGRAM-VARIABLES.                                                
008400     05  PV-COLOR                       PIC X(1).                         
008400     05  PV-CURRENT-DATE                PIC X(10).                        
008400     05  PV-COMPARE-DATE                PIC X(10).                        
008400     05  PV-WORK-DATE.                                                    
008500         10  PV-WORK-CC                 PIC XX.                           
008600         10  PV-WORK-YY                 PIC XX.                           
008700         10  FILLER                     PIC X.                            
008800         10  PV-DISPLAY-DATE.                                             
008900             15 PV-WORK-MM              PIC XX.                           
009000             15 PV-WORK-SLASH           PIC X.                            
009100             15 PV-WORK-DD              PIC XX.                           
009110     05  PV-WORK-TIME.                                                    
009120         10  PV-TIME-HH                 PIC XX.                           
009121         10  FILLER                     PIC X.                            
009130         10  PV-TIME-MM                 PIC XX.                           
009140         10  FILLER                     PIC X.                            
009150         10  PV-TIME-SS                 PIC XX.                           
009160     05  PV-DISPLAY-TIME.                                                 
009170         10  PV-DISP-HH                 PIC XX.                           
009180         10  FILLER                     PIC X VALUE ':'.                  
009190         10  PV-DISP-MM                 PIC XX.                           
009200     05  PV-REQUEST-TICKET-TOTAL        PIC S9(09)  VALUE +0              
009300                                                    COMP SYNC.            
009310     05  PV-TICKET-QTY                  PIC S9(09)  VALUE +0              
009320                                                    COMP SYNC.            
009400     05  PV-SUB                         PIC S9(04)  VALUE +0              
009500                                                    COMP SYNC.            
009600     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
009700                                                    COMP SYNC.            
009800     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
009900                                                    COMP-3.               
010000     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
010100                                                    COMP-3.               
010200     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
010300                                                    COMP-3.               
010400     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
010500                                                    COMP-3.               
010600     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
010700                                                    COMP-3.               
010800     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
010900                                                    COMP-3.               
011000     05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
011100                                                    COMP-3.               
011200     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
011300                                                    COMP SYNC.            
011400     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
011500                                                    COMP SYNC.            
011600     05  PV-ITEM                        PIC S9(04)  VALUE +0              
011700                                                    COMP SYNC.            
011800     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
011900                                                    COMP SYNC.            
012000     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
012100                                                    COMP SYNC.            
012200     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
012300                                                    COMP SYNC.            
012400     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
012500                                                    COMP SYNC.            
012600     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
012700                                                    COMP SYNC.            
012800     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
012900                                                    COMP SYNC.            
013000     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
013100                                                    COMP SYNC.            
013200     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
013300                                                    COMP SYNC.            
013400     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
013500                                                    COMP SYNC.            
013600     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
013700                                                    COMP SYNC.            
013800     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
013900                                                    COMP SYNC.            
014000     05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
014100                                                    COMP SYNC.            
014200     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
014300                                                    COMP SYNC.            
014400******************************************************************        
014500**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
014600**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
014700**  APPLICATION-SPECIFIC COMMAREA.                              **        
014800******************************************************************        
014900 01  LQW-LIST-QUEUE-WORK-AREA.                                            
015000     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
015100     05  LQW-ITEM-ENTRIES               OCCURS 12 TIMES                   
015200                                        INDEXED BY LQW-IDX.               
015300         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
015400         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
015500         10  LQW-SELECTED-SW            PIC  X(01).                       
015600             88  LQW-SELECT                         VALUE 'S'.            
015700             88  LQW-DESELECT                       VALUE ' '.            
015800         10  LQW-ACTION                 PIC  X(03).                       
015900         10  LQW-PO-REQ-NBR             PIC  9(09) COMP.                  
015901         10  LQW-PO-NBR                 PIC  9(09) COMP.                  
015910         10  LQW-REQ-TYP-CDE            PIC  X(01).                       
015920         10  LQW-TICKET-QTY             PIC S9(07) COMP.                  
015930         10  LQW-VND-NAME               PIC  X(15).                       
031000         10  LQW-ENT-ID                 PIC S9(09) COMP.                  
031001         10  LQW-ADDR-SEQ-NBR           PIC S9(09) COMP.                  
031002         10  LQW-REQUEST-DTE            PIC  X(10).                       
031003         10  LQW-VND-DUE-DTE            PIC  X(10).                       
031004         10  LQW-PRT-BY-DTE             PIC  X(10).                       
031005         10  LQW-ACTL-PRT-DTE           PIC  X(10).                       
031006         10  LQW-PRT-TM                 PIC  X(05).                       
031008                                                                          
031009     EJECT                                                                
031010*---------------------------------------------------------------*         
031011*  RECORD LAYOUT FOR THE MESSAGE SELECTION TEMP STORAGE QUEUE.  *         
031012*---------------------------------------------------------------*         
031013                                                                          
031014     COPY VLWS003.                                                        
031015                                                                          
031016     EJECT                                                                
031017*---------------------------------------------------------------*         
031018*    MAP LAYOUT                                                 *         
031019*---------------------------------------------------------------*         
031020                                                                          
031021     COPY VLKM506.                                                        
031022*                                                                         
031023 01  MR-OCCURS-LAYOUT                   REDEFINES  VL506AO.               
031024     05  FILLER                         PIC X(42).                        
031025     05  MR-SCROLL-AREA                 OCCURS 12 TIMES.                  
031026         10  MR-SELECTL                 PIC S9(04) COMP.                  
031027         10  MR-SELECTA                 PIC  X(01).                       
031028         10  MR-SELECTC                 PIC  X(01).                       
031029         10  MR-SELECTH                 PIC  X(01).                       
031030         10  MR-SELECTI                 PIC  X(01).                       
031031                                                                          
031032         10  MR-ACTIONL                 PIC S9(04) COMP.                  
031033         10  MR-ACTIONA                 PIC  X(01).                       
031034         10  MR-ACTIONC                 PIC  X(01).                       
031035         10  MR-ACTIONH                 PIC  X(01).                       
031036         10  MR-ACTION                  PIC  X(03).                       
031037                                                                          
031032         10  MR-PO-NBRL                 PIC S9(04) COMP.                  
031033         10  MR-PO-NBRA                 PIC  X(01).                       
031034         10  MR-PO-NBRC                 PIC  X(01).                       
031035         10  MR-PO-NBRH                 PIC  X(01).                       
031039         10  MR-PO-NBR.                                                   
      *CLP 11/03/14                                                             
031040             15  MR-PO-NBR-N            PIC  9(08).                       
031041                                                                          
031032         10  MR-REQ-TYP-CDEL            PIC S9(04) COMP.                  
031033         10  MR-REQ-TYP-CDEA            PIC  X(01).                       
031034         10  MR-REQ-TYP-CDEC            PIC  X(01).                       
031035         10  MR-REQ-TYP-CDEH            PIC  X(01).                       
031043         10  MR-REQ-TYP-CDE             PIC  X(01).                       
031044                                                                          
031032         10  MR-TICKET-QTYL             PIC S9(04) COMP.                  
031033         10  MR-TICKET-QTYA             PIC  X(01).                       
031034         10  MR-TICKET-QTYC             PIC  X(01).                       
031035         10  MR-TICKET-QTYH             PIC  X(01).                       
031046         10  MR-TICKET-QTY-X.                                             
031047             15  MR-TICKET-QTY          PIC  Z,ZZZ,ZZ9.                   
031048                                                                          
031032         10  MR-VND-NAMEL               PIC S9(04) COMP.                  
031033         10  MR-VND-NAMEA               PIC  X(01).                       
031034         10  MR-VND-NAMEC               PIC  X(01).                       
031035         10  MR-VND-NAMEH               PIC  X(01).                       
031050         10  MR-VND-NAME                PIC  X(15).                       
031051                                                                          
031032         10  MR-VND-DUE-DTEL            PIC S9(04) COMP.                  
031033         10  MR-VND-DUE-DTEA            PIC  X(01).                       
031034         10  MR-VND-DUE-DTEC            PIC  X(01).                       
031035         10  MR-VND-DUE-DTEH            PIC  X(01).                       
031053         10  MR-VND-DUE-DTE             PIC  X(05).                       
031054                                                                          
031032         10  MR-PRT-BY-DTEL             PIC S9(04) COMP.                  
031033         10  MR-PRT-BY-DTEA             PIC  X(01).                       
031034         10  MR-PRT-BY-DTEC             PIC  X(01).                       
031035         10  MR-PRT-BY-DTEH             PIC  X(01).                       
031056         10  MR-PRT-BY-DTE              PIC  X(05).                       
031057                                                                          
031032         10  MR-ACTL-PRT-DTEL           PIC S9(04) COMP.                  
031033         10  MR-ACTL-PRT-DTEA           PIC  X(01).                       
031034         10  MR-ACTL-PRT-DTEC           PIC  X(01).                       
031035         10  MR-ACTL-PRT-DTEH           PIC  X(01).                       
031059         10  MR-ACTL-PRT-DTE            PIC  X(05).                       
031060                                                                          
031032         10  MR-PRT-TML                 PIC S9(04) COMP.                  
031033         10  MR-PRT-TMA                 PIC  X(01).                       
031034         10  MR-PRT-TMC                 PIC  X(01).                       
031035         10  MR-PRT-TMH                 PIC  X(01).                       
031062         10  MR-PRT-TM                  PIC  X(05).                       
031063                                                                          
031064     EJECT                                                                
031065*---------------------------------------------------------------*         
031066*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
031067*---------------------------------------------------------------*         
031068                                                                          
031069     COPY DPWS015.                                                        
031070                                                                          
031071*---------------------------------------------------------------*         
031072*    FUNCTION KEYS COPYBOOK.                                    *         
031073*---------------------------------------------------------------*         
031074                                                                          
031075     COPY DPWS016.                                                        
031076                                                                          
031077*---------------------------------------------------------------*         
031078*    NUMERIC EDIT LAYOUT.                                       *         
031079*---------------------------------------------------------------*         
031080                                                                          
031081     COPY DPWS010I.                                                       
031082 EJECT                                                                    
031083*---------------------------------------------------------------*         
017100*    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API           *         
031085*---------------------------------------------------------------*         
031086                                                                          
031087     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
031088 EJECT                                                                    
031089*---------------------------------------------------------------*         
031090*    STANDAND COMMAREA.                                         *         
031091*---------------------------------------------------------------*         
031092                                                                          
031093     COPY DPWS020.                                                        
031094     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
031095                                                                          
031096*---------------------------------------------------------------*         
031097* INTER-APPLICATION COMMAREA.                                   *         
031099*   VLWS002 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
031100*             THIS LIST PROGRAMMING FOR A LOCAL-GO FUNCTION.    *         
031101*---------------------------------------------------------------*         
031102                                                                          
031103         10  INTER-APPL-COMM-AREA       PIC X(200).                       
031104     COPY VLWS002.                                                        
031105*---------------------------------------------------------------*         
031106*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
031107*---------------------------------------------------------------*         
031108         10  ASC-SPECIFIC-COMMAREA.                                       
031109             15  ASC-LIST-KEYS.                                           
031115                 20  ASC-CURRENT-DATE                PIC  X(10).          
031115                 20  ASC-COMPARE-DATE                PIC  X(10).          
031116             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
031117                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
031118                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
031119                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
031120                                                                          
031121                 20  ASC-ITEM-ARRAY.                                      
031122                     25  ASC-ITEM-ENTRIES OCCURS 12 TIMES.                
031123                         30  ASC-LIST-ITEM-NUMBER                         
031124                                                     PIC S9(09)           
031125                                                     COMP-3.              
031126                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
031127                             88  ASC-VALID-SELECT    VALUE ' '            
031128                                                           'S'            
031129                                                           'B'            
031130                                                           'E'.           
031131                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
031132                             88  ASC-TEMP-SELECT     VALUE 'S'.           
031133                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
031134                             88  ASC-TEMP-END        VALUE 'E'.           
031135                         30  ASC-SELECTED-SW         PIC  X(01).          
031136                             88  ASC-NO-SELECT       VALUE ' '.           
031137                             88  ASC-SELECT          VALUE 'S'.           
031138                             88  ASC-BEGIN           VALUE 'B'.           
031139                             88  ASC-END             VALUE 'E'.           
031140                         30  ASC-ACTION              PIC  X(03).          
031141                         30  ASC-PO-REQ-NBR          PIC  9(09)           
031142                                                          COMP.           
031143                         30  ASC-PO-NBR              PIC  9(09)           
031144                                                          COMP.           
031145                         30  ASC-REQ-TYP-CDE         PIC  X(01).          
031146                         30  ASC-TICKET-QTY          PIC S9(07)           
031147                                                         COMP.            
031148                         30  ASC-VND-NAME            PIC  X(15).          
031149                         30  ASC-ENT-ID              PIC S9(09)           
031150                                                         COMP.            
031151                         30  ASC-ADDR-SEQ-NBR        PIC S9(09)           
031152                                                         COMP.            
031153                         30  ASC-REQUEST-DTE         PIC  X(10).          
031154                         30  ASC-VND-DUE-DTE         PIC  X(10).          
031155                         30  ASC-PRT-BY-DTE          PIC  X(10).          
031200                         30  ASC-ACTL-PRT-DTE        PIC  X(10).          
031400                         30  ASC-PRT-TM              PIC  X(05).          
031700                                                                          
031800             15  ASC-BLK-SUB                         PIC S9(04)           
031900                                                          COMP            
032000                                                          SYNC.           
032100             15  ASC-ITEM-SUB                        PIC S9(04)           
032200                                                          COMP            
032300                                                          SYNC.           
032301             15  ASC-COMMON-ENT-ID                   PIC S9(09)           
032302                                                         COMP.            
032303             15  ASC-COMMON-ADDR-SEQ-NBR             PIC S9(09)           
032304                                                         COMP.            
032400             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
032500                                                          COMP-3.         
032600             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
032700                                                          COMP-3.         
032800             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
032900                                                          COMP-3.         
033000             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
033100                                                          COMP-3.         
033200             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
033300                                                          COMP-3.         
033400             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
033500                                                          COMP            
033600                                                          SYNC.           
033700             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
033800                                                          COMP-3.         
033900             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
034000                                                          COMP-3.         
034100             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
034200                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
034300                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
034400             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
034500                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
034600                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
034700             15  ASC-LIST-QUEUE-NAME.                                     
034800                 20  ASC-LIST-TERM-ID                PIC  X(04).          
034900                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
035000                                                          COMP-3.         
035100                 20  ASC-LIST-SEQ                    PIC  9(01).          
035200             15  ASC-SEL-QUEUE-NAME.                                      
035300                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
035400                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
035500                                                          COMP-3.         
035600                 20  ASC-SEL-SEQ                     PIC  9(01).          
035700                                                                          
035800             15  ASC-CURSOR-SW                       PIC  X(01).          
035900                 88  ASC-NO-CURSOR                   VALUE ' '.           
036000                 88  ASC-PO-CURSOR                   VALUE 'P'.           
036100                 88  ASC-DPT-CURSOR                  VALUE 'D'.           
036200                 88  ASC-VND-CURSOR                  VALUE 'V'.           
036300                 88  ASC-DPTVND-CURSOR               VALUE 'B'.           
036400             15  ASC-START-ITEM-REQUEST.                                  
036500                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
036600     EJECT                                                                
036700*---------------------------------------------------------------*         
036800*    ABEND PROCESSING COPYBOOK.                                 *         
036900*---------------------------------------------------------------*         
037000                                                                          
037100     COPY DPWS013.                                                        
037200                                                                          
037300 EJECT                                                                    
037400*---------------------------------------------------------------*         
037500*    DB2 AREA FOR TMESSAGE                                      *         
037600*---------------------------------------------------------------*         
037700     EXEC SQL                                                             
037800          INCLUDE TSYMSG                                                  
037900     END-EXEC.                                                            
038000*---------------------------------------------------------------*         
038100*    DB2 AREA FOR COMMUNICATIONS                                *         
038200*---------------------------------------------------------------*         
038300     EXEC SQL                                                             
038400          INCLUDE SQLCA                                                   
038500     END-EXEC.                                                            
038600                                                                          
038700*---------------------------------------------------------------*         
038800*    DB2 AREA THE PRETICKET REQUEST TABLE                       *         
038900*---------------------------------------------------------------*         
039000     EXEC SQL                                                             
039100          INCLUDE TPTKREQ                                                 
039200     END-EXEC.                                                            
039300                                                                          
039400*---------------------------------------------------------------*         
039500*    DB2 AREA THE PRETICKET REQUEST LINE TABLE                  *         
039600*---------------------------------------------------------------*         
039700     EXEC SQL                                                             
039800          INCLUDE TPTKLNE                                                 
039900     END-EXEC.                                                            
040000                                                                          
040001                                                                          
040002*---------------------------------------------------------------*         
040003*    DB2 AREA THE PRETICKET REQUEST COMMENT TABLE               *         
040004*---------------------------------------------------------------*         
040005     EXEC SQL                                                             
040006          INCLUDE TPTKCMT                                                 
040007     END-EXEC.                                                            
040008                                                                          
040700                                                                          
040800*---------------------------------------------------------------*         
040900*    DB2 AREA THE ENTERPRISE NAME TABLE                         *         
041000*---------------------------------------------------------------*         
041100     EXEC SQL                                                             
041200          INCLUDE TENTNME                                                 
041300     END-EXEC.                                                            
041310                                                                          
041400                                                                          
041500*---------------------------------------------------------------*         
041600*    REQUEST CURSOR WHEN THE DEPARTMENT NUMBER IS SUPPLIED      *         
041700*---------------------------------------------------------------*         
041800                                                                          
041900     EXEC SQL                                                             
042000          DECLARE TKT_PRINT_CURSOR CURSOR                               86
042100          FOR SELECT                                                      
042200                A.PO_REQ_NBR                                              
042300              , A.PO_NBR                                                  
042500              , A.REQ_TYP_CDE                                             
042900              , A.REQ_CREN_DTE                                            
042901              , A.VND_DUE_DTE                                             
042910              , A.PRT_BY_DTE                                              
043000              , A.ACTL_PRT_DTE                                            
043010              , A.PRT_TM                                                  
043020              , A.ENT_ID                                                  
043021              , A.ADDR_SEQ_NBR                                            
043030              , C.ENT_NAME_DESC                                           
043100              , SUM(B.TKT_QTY)                                            
                    , CURRENT DATE                                              
                    , (CURRENT DATE + 5 DAYS)                                   
043200          FROM                                                            
043300                TPTKREQ A                                               86
043400              , TPTKLNE B                                               86
043500              , TENTNME C                                               86
043700          WHERE                                                           
043800                A.PO_REQ_NBR = B.PO_REQ_NBR                               
043910            AND A.REQ_STAT_CDE  = :PC-P                                   
043911            AND A.ENT_ID  = C.ENT_ID                                      
043920            AND C.TYPE_CDE      = '01'                                    
044210          GROUP BY                                                        
044300                A.PO_REQ_NBR                                              
044400              , A.PO_NBR                                                  
044500              , A.REQ_TYP_CDE                                             
044510              , A.REQ_CREN_DTE                                            
044600              , A.VND_DUE_DTE                                             
044700              , A.PRT_BY_DTE                                              
044800              , A.ACTL_PRT_DTE                                            
044900              , A.PRT_TM                                                  
045000              , A.ENT_ID                                                  
045010              , A.ADDR_SEQ_NBR                                            
045100              , C.ENT_NAME_DESC                                           
045300          ORDER BY                                                        
045310                C.ENT_NAME_DESC, A.VND_DUE_DTE                            
045500     END-EXEC.                                                            
058200                                                                          
058300 EJECT                                                                    
058400 LINKAGE SECTION.                                                         
058500                                                                          
058600 01  DFHCOMMAREA.                                                         
058700     05  FILLER                         OCCURS  1 TO 4072 TIMES           
058800                                        DEPENDING ON EIBCALEN.            
058900         10  FILLER                     PIC X.                            
059000                                                                          
059100 EJECT                                                                    
059200 PROCEDURE DIVISION.                                                      
059300******************************************************************        
059400**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
059500**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
059600**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
059700******************************************************************        
059800 0000-MAIN-MODULE.                                                        
059900     INITIALIZE DP030-CICS-API-FIELDS.                                    
060000     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
060100     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
060200     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
060300     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
060400     MOVE LENGTH OF VL506AI        TO DP030-MAP-LENGTH (1).               
060500     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
060600     PERFORM 0001-CALL-CICS-ARCH-API.                                     
060700*                                                                         
060800     PERFORM 1000-CONTROL-PROCESSING                                      
060900*                                                                         
061000     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
061100     PERFORM 0001-CALL-CICS-ARCH-API.                                     
061200                                                                          
061300******************************************************************        
061400**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
061500**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
061600******************************************************************        
061700 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
061900                                    DFHCOMMAREA                           
062000                                    DP030-CICS-API-FIELDS                 
062100                                    DP020-STANDARD-COMMAREA               
062200                                    VL506AI.                              
062300*                                                                         
062400     IF  DP030-RC-CALL-SUCCESSFUL                                         
062500         CONTINUE                                                         
062600     ELSE                                                                 
062700         SET DP013-NO-ROLLBACK                                            
062800             DP013-XCTL-DISPLAY-RESTART                                   
062900             DP013-CICS-ABEND      TO TRUE                                
063000         MOVE '0001-CALL-CICS-ARCH-API'                                   
063100                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
063400         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
063500         PERFORM DP013-0000-PROCESS-ABEND                                 
063600     END-IF.                                                              
063700*----------------------------------------------------------------*        
063800* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
063900* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
064000*                                                                *        
064100* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
064200*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
064300*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
064400*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
064500*----------------------------------------------------------------*        
064600                                                                          
064700 1000-CONTROL-PROCESSING.                                                 
064800                                                                          
064900     EVALUATE TRUE                                                        
065000         WHEN DP020-NEXT-ACT-INITIAL                                      
065100             INITIALIZE ASC-SPECIFIC-COMMAREA                             
065200             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
065300                                      ASC-SEL-TERM-ID                     
065400             MOVE DP020-SRC-TASK-NUMBER                                   
065500                                   TO ASC-LIST-TASK-NUMBER                
065600                                      ASC-SEL-TASK-NUMBER                 
065700             MOVE PC-LIST-QUEUE-SEQ                                       
065800                                   TO ASC-LIST-SEQ                        
065900             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
066000             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
066100***-----$TEMPLATE NOTE$-------------------------------------------        
066200*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
066300*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
066400*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
066500***---------------------------------------------------------------        
066600             IF  PS-COMMAREA-NOT-VALID                                    
067100                 SET DP020-NEXT-ACT-APPL-ERROR                            
067200                                   TO TRUE                                
067300             ELSE                                                         
067400                 PERFORM 1200-GET-PROGRAM-DATES                           
067400                 PERFORM 4000-BUILD-INITIAL-PANEL                         
067500             END-IF                                                       
067600                                                                          
067700         WHEN DP020-NEXT-ACT-READ-MAP                                     
067800             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
067900                                   TO PV-TOP-ITEM                         
068000             PERFORM 2000-PROCESS-PANEL                                   
068100                                                                          
068200         WHEN DP020-NEXT-ACT-RETURN                                       
068300             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
068400                                   TO PV-TOP-ITEM                         
068500             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
068710             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
068700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
068800             PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**2
068900                                                                          
069000         WHEN OTHER                                                       
069100             SET DP013-LOGIC-ABEND TO TRUE                                
069200             SET DP013-NO-ROLLBACK TO TRUE                                
069300             MOVE '1000-CONTROL-PROCESSING'                               
069400                                   TO DP013-PARAGRAPH                     
069500             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
069600                                   TO DP013-MESSAGE-TEXT(1)               
069700             PERFORM DP013-0000-PROCESS-ABEND                             
069800     END-EVALUATE.                                                        
069900                                                                          
070000 EJECT                                                                    
070100*----------------------------------------------------------------*        
070200* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
070300* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
070400*----------------------------------------------------------------*        
070500                                                                          
070600 1100-PROCESS-INTER-APPL-COMM.                                            
070700                                                                          
070800     SET PS-COMMAREA-VALID         TO TRUE.                               
070900     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
071900                                                                          
072000 EJECT                                                                    
072100*----------------------------------------------------------------*        
072200* 1200-GET-COMPARE-DATES - FIRST GET THE CURRENT DATE - THEN     *        
072300* USE THE CALENDAR ROUTINE TO GET THE CURRENT DATE - 5 DAYS.     *        
072400* THESE DATES ARE KEPT IN THE COMMAREA FOR COMPARISON TO EACH    *        
072500* LINE TO DETERMINE WHICH HIGHLIGHTING (IF ANY) WILL BE DONE.    *        
072700*----------------------------------------------------------------*        
072800                                                                          
072900 1200-GET-PROGRAM-DATES.                                                  
072000 EJECT                                                                    
072100*----------------------------------------------------------------*        
072200* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
072300* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
072400* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
072500* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
072600* FUNCTION KEYS CAN GET THIS FAR.                                *        
072700*----------------------------------------------------------------*        
072800                                                                          
072900 2000-PROCESS-PANEL.                                                      
073000     EVALUATE TRUE                                                        
073100         WHEN DP020-SRC-AID = DP016-CLEAR                                 
073200             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
073300             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
073400             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
073500                                                                          
073600         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
074000             PERFORM 2050-DELETE-LIST-QUEUE                               
074100             PERFORM 4000-BUILD-INITIAL-PANEL                             
074200                                                                          
074300         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
074400             PERFORM 2140-DESELECT-ALL-ITEMS                              
074500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
074600                                                                          
074700         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
074800             PERFORM 2150-SELECT-ALL-ITEMS                                
074900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
075000                                                                          
075100         WHEN OTHER                                                       
075200             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
075300             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
075400             IF  PS-NO-ERROR                                              
075500                 SET DP030-CONTINUE-GO                                    
075600                                 TO TRUE                                  
075700                 PERFORM 2100-CHECK-FUNCTION-KEY                          
075800             ELSE                                                         
075900                 SET DP030-OVERRIDE-GO                                    
076000                                 TO TRUE                                  
076100             END-IF                                                       
076200     END-EVALUATE.                                                        
076300 EJECT                                                                    
076400*----------------------------------------------------------------*        
076500*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
076600*----------------------------------------------------------------*        
076700 2050-DELETE-LIST-QUEUE.                                                  
076800                                                                          
076900     EXEC CICS                                                            
077000         DELETEQ TS                                                       
077100             QUEUE (ASC-LIST-QUEUE-NAME)                                  
077200             RESP  (PV-CICS-RESP)                                         
077300     END-EXEC.                                                            
077400                                                                          
077500     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
077600        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
077700         CONTINUE                                                         
077800     ELSE                                                                 
077900         SET DP013-CICS-ABEND      TO TRUE                                
078000         SET DP013-NO-ROLLBACK     TO TRUE                                
078100                                                                          
078200         MOVE '2050-DELETE-LIST-QUEUE'                                    
078300                                   TO DP013-PARAGRAPH                     
078400         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
078500                                   TO DP013-MESSAGE-TEXT (1)              
078600         PERFORM DP013-0000-PROCESS-ABEND                                 
078700     END-IF.                                                              
078800                                                                          
078900     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
079000 EJECT                                                                    
079100*----------------------------------------------------------------*        
079200* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
079300* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
079400* CICS ARCHITECTURE API.                                         *        
079500*----------------------------------------------------------------*        
079600 2100-CHECK-FUNCTION-KEY.                                                 
079700     EVALUATE TRUE                                                        
079800         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
079900             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
080100                 INITIALIZE VL002-INTER-APPL-COMMAREA                     
080200                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
080300                      TO VL002-NUMBER-OF-SELECTED-ITEMS                   
080400                 PERFORM 5000-PREPARE-SEL-QUEUE                           
080400                 IF PS-ERROR                                              
081400                     SET DP030-OVERRIDE-GO                                
081500                                   TO TRUE                                
00774                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN         PCKCS152
080400                 END-IF                                                   
081300             ELSE                                                         
081400                 SET DP030-OVERRIDE-GO                                    
081500                                   TO TRUE                                
081600                 MOVE PC-TSYMSG-00057                                     
081700                                   TO DP020-MSG-NUMBER                    
081800                 SET DP020-MSG-WARNING                                    
081900                                   TO TRUE                                
082000             END-IF                                                       
082010         WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
082100             CONTINUE                                                     
082110                                                                          
082200         WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                          
082300             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
082400                 INITIALIZE VL002-INTER-APPL-COMMAREA                     
082500                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
082600                                TO VL002-NUMBER-OF-SELECTED-ITEMS         
082700                 PERFORM 5000-PREPARE-SEL-QUEUE                           
082800             ELSE                                                         
082900                 SET DP030-OVERRIDE-GO                                    
083000                                   TO TRUE                                
083100                 MOVE PC-TSYMSG-00057                                     
083200                                   TO DP020-MSG-NUMBER                    
083300                 SET DP020-MSG-WARNING                                    
083400                                   TO TRUE                                
083500             END-IF                                                       
083600                                                                          
083700         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
083800             MOVE +1               TO PV-TOP-ITEM                         
083900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
084000                                                                          
084100         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
084200             PERFORM 2110-BROWSE-BACKWARD                                 
084300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
084400                                                                          
084500         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
084600             PERFORM 2120-BROWSE-FORWARD                                  
084700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
084800                                                                          
084900         WHEN DP020-SRC-AID = DP016-ENTER                                 
085000             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
085100             IF  PS-ERROR                                                 
085200                 CONTINUE                                                 
085300             ELSE                                                         
085400                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
085500             END-IF                                                       
085600                                                                          
085700         WHEN OTHER                                                       
085800             SET DP013-NO-ROLLBACK                                        
085900                 DP013-XCTL-DISPLAY-RESTART                               
086000                 DP013-CICS-ABEND  TO TRUE                                
086100             MOVE '2100-CHECK-FUNCTION-KEY'                               
086200                                   TO DP013-PARAGRAPH                     
086300             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
086400                                   TO DP013-MESSAGE-TEXT (1)              
086500             PERFORM DP013-0000-PROCESS-ABEND                             
086600     END-EVALUATE.                                                        
086700*                                                                         
086800*----------------------------------------------------------------*        
086900*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
087000*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
087100*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
087200*    MESSAGE.                                                    *        
087300*----------------------------------------------------------------*        
087400                                                                          
087500 2110-BROWSE-BACKWARD.                                                    
087600     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
087700         GIVING PV-TOP-ITEM.                                              
087800*                                                                         
087900     IF  ((PV-TOP-ITEM < +1)                                              
088000       OR (PV-TOP-ITEM = +1))                                             
088100         MOVE +1                   TO PV-TOP-ITEM                         
088200*        -- TOP OF LIST --                                                
088300         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
088400         SET DP020-MSG-INFORMATIONAL TO TRUE                              
088500     END-IF.                                                              
088600                                                                          
088700 EJECT                                                                    
088800*----------------------------------------------------------------*        
088900*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
089000*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
089100*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
089200*----------------------------------------------------------------*        
089300                                                                          
089400 2120-BROWSE-FORWARD.                                                     
089500                                                                          
089600     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
089700         GIVING PV-TOP-ITEM.                                              
089800     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
089900         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
090000                                   TO PV-TOP-ITEM                         
090100*        -- BOTTOM OF LIST --                                             
090200         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
090300         SET DP020-MSG-INFORMATIONAL                                      
090400                                   TO TRUE                                
090500     ELSE                                                                 
090600         COMPUTE PV-BOTTOM-ITEM =                                         
090700             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
090800         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
090900*            -- BOTTOM OF LIST --                                         
091000             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
091100             SET DP020-MSG-INFORMATIONAL                                  
091200                                   TO TRUE                                
091300         END-IF                                                           
091400     END-IF.                                                              
091500 EJECT                                                                    
091600*----------------------------------------------------------------*        
091700*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
091800*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
091900*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
092000*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
092100*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
092200*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
092300*----------------------------------------------------------------*        
092400                                                                          
092500 2130-CHECK-FOR-JUMP-BROWSE.                                              
092600     SET PS-NO-ERROR               TO TRUE.                               
092700     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
092800         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
092900                                   TO PV-TOP-ITEM                         
093000     ELSE                                                                 
093100         MOVE ASC-START-ITEM-REQUEST                                      
093200                                   TO DP010I-UNEDITED-FIELD               
093300         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
093400         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
093500         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
093600                                   TO TRUE                                
263627         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263627              USING DP010I-NUMERIC-EDIT-AREA                              
093800                                                                          
093900         IF  NOT DP010I-ERROR-DETECTED                                    
094000             MOVE DP010I-NUMERIC-FIELD                                    
094100                                   TO  ASC-START-ITEM-REQUEST-N           
094200             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
094300                 IF  ASC-START-ITEM-REQUEST-N >                           
094400                                          ASC-NUMBER-OF-ITEMS-READ        
094500                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
094600                     COMPUTE PV-TOP-ITEM =                                
094700                             ASC-NUMBER-OF-ITEMS-READ                     
094800                             - PC-ITEMS-PER-PANEL + 1                     
094900                     IF  PV-TOP-ITEM < +1                                 
095000                         MOVE +1   TO PV-TOP-ITEM                         
095100                         MOVE PC-TSYMSG-00070                             
095200                                   TO DP020-MSG-NUMBER                    
095300                         SET DP020-MSG-INFORMATIONAL                      
095400                                   TO TRUE                                
095500                     END-IF                                               
095600                 ELSE                                                     
095700                     MOVE ASC-START-ITEM-REQUEST-N                        
095800                                   TO PV-TOP-ITEM                         
095900                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
096000                     COMPUTE PV-BOTTOM-ITEM =                             
096100                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
096200                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
096300*                        -- BOTTOM OF LIST --                             
096400                         MOVE PC-TSYMSG-00070                             
096500                                   TO DP020-MSG-NUMBER                    
096600                         SET DP020-MSG-INFORMATIONAL                      
096700                                   TO TRUE                                
096800                     END-IF                                               
096900                 END-IF                                                   
097000             ELSE                                                         
097100*                --- VALUE OUT OF RANGE ----                              
097200                 MOVE PC-TSYMSG-00101                                     
097300                                   TO DP020-MSG-NUMBER                    
097400                 MOVE -1           TO ASTRTITL                            
097500                 SET DP030-SET-CURSOR-APPL-1                              
097600                                   TO TRUE                                
097700                 MOVE DP015-REVERSE                                       
097800                                   TO ASTRTITH                            
097900                 MOVE DP015-RED    TO ASTRTITC                            
098000                 MOVE DP015-UNP-NUM-BRT-MDT                               
098100                                   TO ASTRTITA                            
098200                 SET DP020-MSG-FATAL                                      
098300                                   TO TRUE                                
098400                 SET PS-ERROR      TO TRUE                                
098500             END-IF                                                       
098600                                                                          
098700         ELSE                                                             
098800             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
098900             MOVE -1               TO ASTRTITL                            
099000             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
099100             MOVE DP015-REVERSE    TO ASTRTITH                            
099200             MOVE DP015-RED        TO ASTRTITC                            
099300             MOVE DP015-UNP-NUM-BRT-MDT                                   
099400                                   TO ASTRTITA                            
099500             SET DP020-MSG-FATAL   TO TRUE                                
099600             SET PS-ERROR          TO TRUE                                
099700         END-IF                                                           
099800     END-IF.                                                              
099900 EJECT                                                                    
100000*----------------------------------------------------------------*        
100100*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
100200*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
100300*----------------------------------------------------------------*        
100400 2140-DESELECT-ALL-ITEMS.                                                 
100500     MOVE +1                       TO ASC-BLK-SUB.                        
100600                                                                          
100700     PERFORM 2141-CLEAR-SELECTED-INDS                                     
100800         VARYING ASC-ITEM-SUB                                             
100900         FROM 1 BY 1                                                      
101000         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
101100                                                                          
101200     ADD +1                        TO ASC-BLK-SUB.                        
101300                                                                          
101400     PERFORM 2141-CLEAR-SELECTED-INDS                                     
101500         VARYING ASC-ITEM-SUB                                             
101600         FROM 1 BY 1                                                      
101700         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
101800                                                                          
101900     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
102000                                   TO PV-LIST-ITEM-NUMBER.                
102100                                                                          
102200     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
102300         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
102400                                                                          
102500     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
102600        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
102700         MOVE +1                   TO ASC-BLK-SUB                         
102800                                                                          
102900         PERFORM 5100-WRITE-TEMP-STORAGE                                  
103000                                                                          
103100         MOVE +1                   TO PV-BLOCK-NUMBER                     
103200                                                                          
103300         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
103400             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
103500                                                                          
103600         PERFORM                                                          
103700             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
103800                                                                          
103900             PERFORM 4410-READ-TEMP-STORAGE                               
104000                                                                          
104100             PERFORM 2141-CLEAR-SELECTED-INDS                             
104200                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
104300                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
104400                                                                          
104500             PERFORM 5100-WRITE-TEMP-STORAGE                              
104600                                                                          
104700             ADD +1                TO PV-BLOCK-NUMBER                     
104800                                                                          
104900         END-PERFORM                                                      
105000                                                                          
105100         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
105200             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
105300                                                                          
105400         PERFORM                                                          
105500             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
105600                   ASC-HIGHEST-BLOCK-WRITTEN                              
105700                                                                          
105800             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
105900                                   TO PV-BLOCK-NUMBER                     
106000                                                                          
106100             PERFORM 4410-READ-TEMP-STORAGE                               
106200                                                                          
106300             PERFORM 2141-CLEAR-SELECTED-INDS                             
106400                 VARYING ASC-ITEM-SUB                                     
106500                 FROM 1 BY 1                                              
106600                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
106700                                                                          
106800             PERFORM 5100-WRITE-TEMP-STORAGE                              
106900                                                                          
107000             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
107100                                                                          
107200         END-PERFORM                                                      
107300                                                                          
107400         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
107500                                   TO PV-BLOCK-NUMBER                     
107600                                                                          
107700         PERFORM 4410-READ-TEMP-STORAGE                                   
107800     END-IF.                                                              
107900                                                                          
108000                                                                          
108100     MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
108200     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
108300     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
108400                                                                          
108500     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
108600                                                                          
108700 EJECT                                                                    
108800*----------------------------------------------------------------*        
108900*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
109000*----------------------------------------------------------------*        
109100                                                                          
109200 2141-CLEAR-SELECTED-INDS.                                                
109300                                                                          
109400     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
109500             NOT EQUAL ZERO                                               
109600         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
109700             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
109800                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
109900                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
110000                                   TO TRUE                                
110100         END-IF                                                           
110200     END-IF.                                                              
110300                                                                          
110400 EJECT                                                                    
110500*----------------------------------------------------------------*        
110600*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
110700*  FOR ALL ITEMS.                                                *        
110800*----------------------------------------------------------------*        
110900                                                                          
111000 2150-SELECT-ALL-ITEMS.                                                   
111100                                                                          
111200     MOVE +1                       TO ASC-BLK-SUB.                        
111300                                                                          
111400     PERFORM 2151-SET-SELECTED-INDS                                       
111500         VARYING ASC-ITEM-SUB                                             
111600         FROM 1 BY 1                                                      
111700         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
111800                                                                          
111900     ADD +1                        TO ASC-BLK-SUB.                        
112000                                                                          
112100     PERFORM 2151-SET-SELECTED-INDS                                       
112200         VARYING ASC-ITEM-SUB                                             
112300         FROM 1 BY 1                                                      
112400         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
112500                                                                          
112600     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
112700                                   TO PV-LIST-ITEM-NUMBER.                
112800                                                                          
112900     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
113000         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
113100                                                                          
113200     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
113300        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
113400         MOVE +1                   TO ASC-BLK-SUB                         
113500                                                                          
113600         PERFORM 5100-WRITE-TEMP-STORAGE                                  
113700                                                                          
113800         MOVE +1                   TO PV-BLOCK-NUMBER                     
113900                                                                          
114000         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
114100             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
114200                                                                          
114300         PERFORM                                                          
114400             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
114500                                                                          
114600             PERFORM 4410-READ-TEMP-STORAGE                               
114700                                                                          
114800             PERFORM 2151-SET-SELECTED-INDS                               
114900                 VARYING ASC-ITEM-SUB                                     
115000                 FROM 1 BY 1                                              
115100                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
115200                                                                          
115300             PERFORM 5100-WRITE-TEMP-STORAGE                              
115400                                                                          
115500             ADD +1                TO PV-BLOCK-NUMBER                     
115600                                                                          
115700         END-PERFORM                                                      
115800                                                                          
115900         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
116000             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
116100                                                                          
116200         PERFORM                                                          
116300             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
116400                   ASC-HIGHEST-BLOCK-WRITTEN                              
116500                                                                          
116600             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
116700                                   TO PV-BLOCK-NUMBER                     
116800                                                                          
116900             PERFORM 4410-READ-TEMP-STORAGE                               
117000                                                                          
117100             PERFORM 2151-SET-SELECTED-INDS                               
117200                 VARYING ASC-ITEM-SUB                                     
117300                 FROM 1 BY 1                                              
117400                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
117500                                                                          
117600             PERFORM 5100-WRITE-TEMP-STORAGE                              
117700                                                                          
117800             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
117900                                                                          
118000         END-PERFORM                                                      
118100                                                                          
118200         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
118300                                   TO PV-BLOCK-NUMBER                     
118400                                                                          
118500         PERFORM 4410-READ-TEMP-STORAGE                                   
118600     END-IF.                                                              
118700                                                                          
118800                                                                          
118900     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
119000                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
119100                                                                          
119200     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
119300                                                                          
119400 EJECT                                                                    
119500*----------------------------------------------------------------*        
119600*    MARK EACH ITEM AS BEING SELECTED.                           *        
119700*----------------------------------------------------------------*        
119800                                                                          
119900 2151-SET-SELECTED-INDS.                                                  
120000                                                                          
120100     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
120200             NOT EQUAL ZERO                                               
120300         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
120400             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
120500                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
120600                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
120700                               TO  TRUE                                   
120800         END-IF                                                           
120900     END-IF.                                                              
121000******************************************************************        
121100** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
121200** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
121300******************************************************************        
121400 2200-MOVE-SCREEN-TO-COMMAREA.                                            
121500                                                                          
121600     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
121700     MOVE +1                       TO PV-SUB.                             
121800*                                                                         
121900     PERFORM 3100-SET-SUBSCRIPTS.                                         
122000     IF  ASTRTITL > ZERO                                                  
122100         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
122200     ELSE                                                                 
122300         IF  ASTRTITF = DP015-ERASE-EOF                                   
122400             INITIALIZE ASC-START-ITEM-REQUEST                            
122500         END-IF                                                           
122600     END-IF.                                                              
122700     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
122800         VARYING PV-SUB                                                   
122900         FROM 1 BY 1                                                      
123000         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
123100******************************************************************        
123200** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
123300******************************************************************        
123400 2210-MOVE-LINES-TO-COMMAREA.                                             
123500     IF  MR-SELECTL (PV-SUB) > ZERO                                       
123600         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
123700         MOVE MR-SELECTI (PV-SUB)                                         
123800                                   TO ASC-TEMP-SELECTED-SW                
123900                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
124000     ELSE                                                                 
124100         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
124200                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
124300                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
124400                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
124500         END-IF                                                           
124600     END-IF.                                                              
124700     ADD +1                        TO ASC-ITEM-SUB.                       
124800     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
124900         ADD +1                    TO ASC-BLK-SUB                         
125000         MOVE +1                   TO ASC-ITEM-SUB                        
125100     END-IF.                                                              
125200******************************************************************        
125300** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
125400** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
125500******************************************************************        
125600 3000-EDIT-DATA-IN-COMMAREA.                                              
125700     SET PS-NO-ERROR               TO TRUE.                               
125800     PERFORM 3200-RESET-ATTRIBUTES.                                       
125900     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
126000**                                                                        
126100* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
126200* EDITTING IS DONE FROM THE BOTTOM UP.                                    
126300**                                                                        
126400     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
126500         ASC-ITEMS-DISPLAYED - 1.                                         
126600     MOVE +0 TO ASC-COMMON-ENT-ID                                         
126700                ASC-COMMON-ADDR-SEQ-NBR.                                  
126701                                                                          
126702     PERFORM 3100-SET-SUBSCRIPTS.                                         
126703     PERFORM 3300-EDIT-SELECTIONS                                         
126800         VARYING PV-SUB                                                   
126900         FROM PV-SUB BY -1                                                
127000         UNTIL PV-SUB < +1                                                
127100*                                                                         
127200     IF  PS-NO-ERROR                                                      
127300         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
127400     END-IF.                                                              
127500*                                                                         
127600     IF  ((PS-NO-ERROR)                                                   
127700       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
127800       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
127900             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
128000     END-IF.                                                              
128100 EJECT                                                                    
128200*----------------------------------------------------------------*        
128300*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
128400*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
128500*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
128600*                                                                *        
128700*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
128800*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
128900*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
129000*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
129100*----------------------------------------------------------------*        
129200                                                                          
129300 3100-SET-SUBSCRIPTS.                                                     
129400                                                                          
129500     COMPUTE ASC-ITEM-SUB =                                               
129600         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
129700                                                                          
129800     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
129900         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
130000                                                                          
130100         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
130200     ELSE                                                                 
130300         MOVE +1                     TO ASC-BLK-SUB                       
130400     END-IF.                                                              
130500******************************************************************        
130600** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
130700******************************************************************        
130800                                                                          
130900 3200-RESET-ATTRIBUTES.                                                   
131000     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
131100     MOVE DP015-GREEN              TO ASTRTITC.                           
131200     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
131300*                                                                         
131400     PERFORM                                                              
131500         VARYING PV-SUB                                                   
131600         FROM 1 BY 1                                                      
131700         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
131800         MOVE DP015-UNP-ALP-NOR-OFF                                       
131900                                   TO MR-SELECTA (PV-SUB)                 
132000         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
132100         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
132200     END-PERFORM.                                                         
132300******************************************************************        
132400** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
132500** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
132600** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
132700** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
132800******************************************************************        
132900 3300-EDIT-SELECTIONS.                                                    
133000     EVALUATE TRUE                                                        
133100         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
133200             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
133300                 CONTINUE                                                 
133400         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
133500             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
133600                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
133700                 MOVE ASC-TEMP-SELECTED-SW                                
133800                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
133900                                TO ASC-SELECTED-SW                        
134000                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
134100             END-IF                                                       
134200         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
134300                 = SPACE                                                  
134400             EVALUATE TRUE                                                
134500                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
134600                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
134700                     MOVE SPACE TO ASC-SELECTED-SW                        
134800                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
134900                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
135000                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
135100                     MOVE SPACE TO ASC-SELECTED-SW                        
135200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
135300                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
135400                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
135500                     MOVE SPACE TO ASC-SELECTED-SW                        
135600                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
135700             END-EVALUATE                                                 
135800         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
135900             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
136000                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
136100             END-IF                                                       
136200             PERFORM 3310-EDIT-RANGE                                      
136300         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
136400             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
136500                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
136600             END-IF                                                       
136700             PERFORM 3310-EDIT-RANGE                                      
136800         WHEN OTHER                                                       
136900             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
137000             SET DP020-MSG-FATAL    TO TRUE                               
137100             MOVE -1                TO MR-SELECTL (PV-SUB)                
137200             SET DP030-SET-CURSOR-APPL-1                                  
137300                                    TO TRUE                               
137400             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
137500             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
137600             SET PS-ERROR TO TRUE                                         
137700             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
137800                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
137900                 MOVE SPACE         TO ASC-SELECTED-SW                    
138000                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
138100             END-IF                                                       
138200     END-EVALUATE.                                                        
138300                                                                          
138400     IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                            
138500         IF ASC-COMMON-ENT-ID = +0                                        
138600             MOVE ASC-ENT-ID (ASC-BLK-SUB ASC-ITEM-SUB)                   
138700                  TO ASC-COMMON-ENT-ID                                    
138800         ELSE                                                             
138900             IF ASC-ENT-ID (ASC-BLK-SUB ASC-ITEM-SUB)                     
139000                  = ASC-COMMON-ENT-ID                                     
139100                 CONTINUE                                                 
139101             ELSE                                                         
139102                 MOVE PC-TSYMSG-01362   TO DP020-MSG-NUMBER               
139103                 MOVE -1                TO MR-SELECTL (PV-SUB)            
139104                 SET DP030-SET-CURSOR-APPL-1                              
139105                     DP020-MSG-FATAL                                      
139106                     PS-ERROR           TO TRUE                           
139107             END-IF                                                       
139108         END-IF                                                           
139109         IF ASC-COMMON-ADDR-SEQ-NBR = +0                                  
139110             MOVE ASC-ADDR-SEQ-NBR (ASC-BLK-SUB ASC-ITEM-SUB)             
139120                  TO ASC-COMMON-ADDR-SEQ-NBR                              
139130         ELSE                                                             
139140             IF ASC-ADDR-SEQ-NBR (ASC-BLK-SUB ASC-ITEM-SUB)               
139150                  =  ASC-COMMON-ADDR-SEQ-NBR                              
139160                 CONTINUE                                                 
139161             ELSE                                                         
139162                 MOVE PC-TSYMSG-01363   TO DP020-MSG-NUMBER               
139163                 MOVE -1                TO MR-SELECTL (PV-SUB)            
139164                 SET DP020-MSG-FATAL                                      
139165                     DP030-SET-CURSOR-APPL-1                              
139166                     PS-ERROR           TO TRUE                           
139167             END-IF                                                       
139168         END-IF                                                           
               IF  ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB) = PC-SHP               
               AND DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                       
139102             MOVE PC-TSYMSG-01384       TO DP020-MSG-NUMBER               
139103             MOVE -1                    TO MR-SELECTL (PV-SUB)            
139104             SET DP030-SET-CURSOR-APPL-1                                  
139105                 DP020-MSG-FATAL                                          
139106                 PS-ERROR               TO TRUE                           
139168         END-IF                                                           
139169     END-IF.                                                              
139170                                                                          
139171     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
139172     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
139173     IF  ASC-ITEM-SUB < 1                                                 
139174         SUBTRACT +1 FROM ASC-BLK-SUB                                     
139175         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
139176     END-IF.                                                              
139177******************************************************************        
139178** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
139200** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
139300** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
139400** THE ENDING.                                                  **        
139500******************************************************************        
139600 3310-EDIT-RANGE.                                                         
139700     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
139800         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
139900             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
140000                                   TO ASC-SELECTED-SW                     
140100                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
140200             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
140300         ELSE                                                             
140400             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
140500                 CONTINUE                                                 
140600             ELSE                                                         
140700*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
140800                 MOVE PC-TSYMSG-00054                                     
140900                                   TO DP020-MSG-NUMBER                    
141000                 SET DP020-MSG-FATAL                                      
141100                                   TO TRUE                                
141200                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
141300                 SET DP030-SET-CURSOR-APPL-1                              
141400                                   TO TRUE                                
141500                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
141600                 MOVE DP015-UNDERLINE                                     
141700                                   TO MR-SELECTH (PV-SUB)                 
141800                 SET PS-ERROR      TO TRUE                                
141900             END-IF                                                       
142000         END-IF                                                           
142100     ELSE                                                                 
142200         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
142300             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
142400                                   TO ASC-SELECTED-SW                     
142500                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
142600             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
142700         ELSE                                                             
142800             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
142900                 CONTINUE                                                 
143000             ELSE                                                         
143100*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
143200                 MOVE PC-TSYMSG-00055                                     
143300                                   TO DP020-MSG-NUMBER                    
143400                 SET DP020-MSG-FATAL                                      
143500                                   TO TRUE                                
143600                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
143700                 SET DP030-SET-CURSOR-APPL-1                              
143800                                   TO TRUE                                
143900                 SET PS-ERROR      TO TRUE                                
144000             END-IF                                                       
144100         END-IF                                                           
144200     END-IF.                                                              
144300*                                                                         
144400******************************************************************        
144500** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
144600** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
144700******************************************************************        
144800 3400-CHECK-RANGE-SEQUENCE.                                               
144900     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
145000       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
145100       AND  ASC-START-OF-SELECTED-RANGE >                                 
145200                                         ASC-END-OF-SELECTED-RANGE        
145300*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
145400         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
145500         SET DP020-MSG-FATAL TO TRUE                                      
145600         SET PS-ERROR TO TRUE                                             
145700         PERFORM 3410-POSITION-AT-ERROR                                   
145800     END-IF.                                                              
145900******************************************************************        
146000** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
146100** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
146200** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
146300******************************************************************        
146400 3410-POSITION-AT-ERROR.                                                  
146500     SET DP030-SET-CURSOR-APPL-1                                          
146600                                   TO TRUE.                               
146700     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
146800                    - PV-TOP-ITEM + 1.                                    
146900     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
147000         CONTINUE                                                         
147100     ELSE                                                                 
147200         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
147300         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
147400         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
147500     END-IF.                                                              
147600*                                                                         
147700     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
147800                    - PV-TOP-ITEM + 1.                                    
147900     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
148000         CONTINUE                                                         
148100     ELSE                                                                 
148200         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
148300         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
148400         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
148500     END-IF.                                                              
148600 EJECT                                                                    
148700*----------------------------------------------------------------*        
148800*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
148900*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
149000*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
149100*----------------------------------------------------------------*        
149200                                                                          
149300 3500-MARK-RANGE-AS-SELECTED.                                             
149400     MOVE +1                       TO  ASC-BLK-SUB.                       
149500     PERFORM 3510-MARK-SELECTED-INDS                                      
149600         VARYING ASC-ITEM-SUB                                             
149700         FROM 1 BY 1                                                      
149800         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
149900                                                                          
150000     ADD +1                        TO ASC-BLK-SUB.                        
150100     PERFORM 3510-MARK-SELECTED-INDS                                      
150200         VARYING ASC-ITEM-SUB                                             
150300         FROM 1 BY 1                                                      
150400         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
150500                                                                          
150600     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
150700                                   TO  PV-LIST-ITEM-NUMBER.               
150800     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
150900         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
151000               PC-ITEMS-PER-PANEL).                                       
151100                                                                          
151200     MOVE ASC-START-OF-SELECTED-RANGE                                     
151300                                   TO PV-START-OF-SELECTED-RANGE.         
151400     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
151500         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
151600               PC-ITEMS-PER-PANEL).                                       
151700                                                                          
151800     MOVE ASC-END-OF-SELECTED-RANGE                                       
151900                                   TO  PV-END-OF-SELECTED-RANGE.          
152000     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
152100         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
152200               PC-ITEMS-PER-PANEL).                                       
152300     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
152400         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
152500                                                                          
152600     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
152700        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
152800         MOVE +1                   TO ASC-BLK-SUB                         
152900         PERFORM 5100-WRITE-TEMP-STORAGE                                  
153000         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
153100             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
153200                                                                          
153300         PERFORM                                                          
153400             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
153500                   PV-PREV-BLOCK-IN-ARRAY                                 
153600             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
153700                                   TO PV-BLOCK-NUMBER                     
153800             PERFORM 4410-READ-TEMP-STORAGE                               
153900             PERFORM 3510-MARK-SELECTED-INDS                              
154000                 VARYING ASC-ITEM-SUB                                     
154100                 FROM 1 BY 1                                              
154200                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
154300             PERFORM 5100-WRITE-TEMP-STORAGE                              
154400             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
154500         END-PERFORM                                                      
154600                                                                          
154700         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
154800             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
154900         PERFORM                                                          
155000             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
155100                   PV-LAST-BLOCK-IN-RANGE                                 
155200             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
155300                                   TO PV-BLOCK-NUMBER                     
155400             PERFORM 4410-READ-TEMP-STORAGE                               
155500             PERFORM 3510-MARK-SELECTED-INDS                              
155600                 VARYING ASC-ITEM-SUB                                     
155700                 FROM 1 BY 1                                              
155800                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
155900             PERFORM 5100-WRITE-TEMP-STORAGE                              
156000             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
156100         END-PERFORM                                                      
156200                                                                          
156300         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
156400                                   TO PV-BLOCK-NUMBER                     
156500         PERFORM 4410-READ-TEMP-STORAGE                                   
156600     END-IF.                                                              
156700                                                                          
156800     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
156900                                      ASC-END-OF-SELECTED-RANGE.          
157000                                                                          
157100 EJECT                                                                    
157200*----------------------------------------------------------------*        
157300* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
157400*----------------------------------------------------------------*        
157500                                                                          
157600 3510-MARK-SELECTED-INDS.                                                 
157700                                                                          
157800     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
157900                                ASC-ITEM-SUB) >                           
158000         ASC-START-OF-SELECTED-RANGE)                                     
158100     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
158200                                ASC-ITEM-SUB) =                           
158300         ASC-START-OF-SELECTED-RANGE)                                     
158400                                                                          
158500         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
158600                                    ASC-ITEM-SUB) <                       
158700             ASC-END-OF-SELECTED-RANGE)                                   
158800         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
158900                                    ASC-ITEM-SUB) =                       
159000             ASC-END-OF-SELECTED-RANGE)                                   
159100                                                                          
159200             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
159300                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
159400                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
159500                               TO TRUE                                    
159600                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
159700                               TO TRUE                                    
159800                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
159900             END-IF                                                       
160000         END-IF                                                           
160100     END-IF.                                                              
160200                                                                          
160300 EJECT                                                                    
160400*----------------------------------------------------------------*        
160500*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
160600*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
160700*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
160800*    DISPLAY THE PANEL TO USER.                                  *        
160900*----------------------------------------------------------------*        
161000                                                                          
161100 4000-BUILD-INITIAL-PANEL.                                                
161200                                                                          
161300     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
161400                                 ASC-BLOCK-ENTRIES (2).                   
161500     MOVE +1                 TO ASC-BLK-SUB.                              
161600     SET ASC-ITEMS-LEFT-ON-DB                                             
161700                             TO TRUE.                                     
161800                                                                          
161900     MOVE +0                 TO ASC-ITEM-SUB                              
162000                                ASC-NUMBER-OF-ITEMS-READ                  
162100                                ASC-NUMBER-OF-SELECTED-ITEMS              
162200                                ASC-HIGHEST-BLOCK-WRITTEN                 
162300                                ASC-START-OF-SELECTED-RANGE               
162400                                ASC-END-OF-SELECTED-RANGE                 
162500                                ASC-ITEM-AT-TOP-OF-PANEL.                 
162900                                                                          
162800     PERFORM 4010-OPEN-CURSOR.                                            
162900                                                                          
163000     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
163100                                                                          
163200     PERFORM 4020-CLOSE-CURSOR.                                           
163300                                                                          
163400     MOVE +1                   TO  PV-TOP-ITEM.                           
163500     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
163600         IF DP020-FK-REFRESH (DP020-SRC-AID)                              
163700             CONTINUE                                                     
163701         ELSE                                                             
163702             SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
163703         END-IF                                                           
163704         SET DP020-MSG-FATAL   TO  TRUE                                   
163800         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
163900     ELSE                                                                 
164000         MOVE -1               TO MR-SELECTL (1)                          
164100         SET DP030-SET-CURSOR-APPL-1                                      
164200                               TO TRUE                                    
164300         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
164400     END-IF.                                                              
164500 EJECT                                                                    
164600*----------------------------------------------------------------*        
164700* OPEN THE TICKET PRINT CURSOR.                                  *        
165100*----------------------------------------------------------------*        
165200                                                                          
165300 4010-OPEN-CURSOR.                                                        
165400     PERFORM                                                              
165500         WITH TEST AFTER                                                  
165600         VARYING PV-RETRY-COUNTER                                         
165700         FROM 1 BY 1                                                      
165800         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
165900                   OR (SQLCODE = ZERO))                                   
166000                                                                          
166400         EXEC SQL                                                         
166510             OPEN TKT_PRINT_CURSOR                                      86
166600         END-EXEC                                                         
168900                                                                          
169000         EVALUATE TRUE                                                    
169100             WHEN SQLCODE = ZERO                                          
169200             WHEN SQLCODE = -904                                          
169300             WHEN SQLCODE = -913                                          
169400                 CONTINUE                                                 
169500             WHEN SQLWARN0 NOT = SPACES                                   
169600             WHEN SQLCODE < ZERO                                          
169700             WHEN SQLCODE > ZERO                                          
169800                 MOVE '4010-OPEN-CURSOR'                                  
169900                                   TO DP013-PARAGRAPH                     
170000                 MOVE 'OPEN TICKET PRINT CUSROR      - '                  
170100                                   TO DP013-MESSAGE-TEXT (1)              
170200                 MOVE SQLCA        TO DP013-SQLCA                         
170300                 MOVE 'TPTKREQ '   TO DP013-DB2-TABLE-NAME (1)            
170400                 MOVE 'TPTKLNE '   TO DP013-DB2-TABLE-NAME (2)            
170500                 SET DP013-DB2-ABEND                                      
170600                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
170700                 PERFORM DP013-0000-PROCESS-ABEND                         
170800         END-EVALUATE                                                     
170900     END-PERFORM.                                                         
171000*                                                                         
171100     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
171200         MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH                     
171300         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
171400                                   TO DP013-MESSAGE-TEXT (1)              
171500         MOVE SQLCA                TO DP013-SQLCA                         
171600         MOVE 'TPTKREQ '           TO DP013-DB2-TABLE-NAME (1)            
171700         MOVE 'TPTKLNE '           TO DP013-DB2-TABLE-NAME (2)            
171800         SET DP013-DB2-ABEND                                              
171900             DP013-XCTL-DISPLAY-RESTART                                   
172000                                   TO TRUE                                
172100         PERFORM DP013-0000-PROCESS-ABEND                                 
172200     END-IF.                                                              
172300                                                                          
172400 EJECT                                                                    
172500*----------------------------------------------------------------*        
172600* CLOSE THE TMESSAGE CURSOR.                                     *        
172700*   IN THIS PARTICULAR APPLICATION ONE OF 3 DIFFERENT CURSORS    *        
172800*   WILL BE CLOSED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
172900*   USER.                                                        *        
173000*----------------------------------------------------------------*        
173100                                                                          
173200 4020-CLOSE-CURSOR.                                                       
173500      EXEC SQL                                                            
173600           CLOSE TKT_PRINT_CURSOR                                         
173700      END-EXEC                                                            
173800                                                                          
175400*                                                                         
175500     EVALUATE TRUE                                                        
175600         WHEN SQLCODE = ZERO                                              
175700             CONTINUE                                                     
175800         WHEN SQLWARN0 NOT = SPACES                                       
175900         WHEN SQLCODE < ZERO                                              
176000         WHEN SQLCODE > ZERO                                              
176100             MOVE '4020-CLOSE-CURSOR'                                     
176200                               TO DP013-PARAGRAPH                         
176300             MOVE 'CLOSE SYSTEM MESSAGE CURSOR '                          
176400                               TO DP013-MESSAGE-TEXT (1)                  
176500             MOVE SQLCA        TO DP013-SQLCA                             
176600             MOVE 'TSYMSG  '   TO DP013-DB2-TABLE-NAME (1)                
176700             SET DP013-DB2-ABEND                                          
176800                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
176900             PERFORM DP013-0000-PROCESS-ABEND                             
177000     END-EVALUATE.                                                        
177100                                                                          
177200 EJECT                                                                    
179800*----------------------------------------------------------------*        
179900*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
180000*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
180100*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
180200*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
180300*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
180400*                                                                *        
180500*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
180600*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
180700*    UPON INITIALIZATION.                                        *        
180800*----------------------------------------------------------------*        
180900                                                                          
181000 4300-READ-ITEMS-FROM-DB.                                                 
181100                                                                          
181200     PERFORM                                                              
181300         VARYING PV-READ-COUNT                                            
181400         FROM 1 BY 1                                                      
181500         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
181600            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
181700                                                                          
181800         PERFORM 4310-FETCH                                               
181900                                                                          
182000         IF  ASC-ITEMS-LEFT-ON-DB                                         
                   IF ASC-CURRENT-DATE  = SPACES                                
                       MOVE PV-CURRENT-DATE  TO ASC-CURRENT-DATE                
                       MOVE PV-COMPARE-DATE  TO ASC-COMPARE-DATE                
                   END-IF                                                       
                                                                                
182100             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
182200                 ADD +1            TO ASC-ITEM-SUB                        
182300             ELSE                                                         
182400                 ADD +1            TO ASC-BLK-SUB                         
182500                 MOVE +1           TO ASC-ITEM-SUB                        
182600                                                                          
182700                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
182800                     MOVE +1       TO ASC-BLK-SUB                         
182900                     SET ASC-BLOCK-MODIFIED (1)                           
183000                                   TO TRUE                                
183100                     PERFORM 5100-WRITE-TEMP-STORAGE                      
183200                     MOVE ASC-BLOCK-ENTRIES                               
183300                               (PC-MAX-BLOCKS-IN-ARRAY)                   
183400                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
183500                     ADD +1        TO ASC-BLK-SUB                         
183600                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
183700                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
183800                                   TO TRUE                                
183900                 END-IF                                                   
184000             END-IF                                                       
184100             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
184200             MOVE ASC-NUMBER-OF-ITEMS-READ                                
184300                                   TO ASC-LIST-ITEM-NUMBER                
184400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
184500             MOVE PTKREQ-PO-NBR    TO ASC-PO-NBR                          
184600                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
184700             MOVE PTKREQ-PO-REQ-NBR                                       
184800                                   TO ASC-PO-REQ-NBR                      
184900                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
185000             MOVE PTKREQ-REQ-TYP-CDE                                      
185100                                   TO ASC-REQ-TYP-CDE                     
185200                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
185600             MOVE ENTNME-ENT-NAME-DESC                                    
185700                                   TO ASC-VND-NAME                        
185800                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
186500             MOVE PTKREQ-REQ-CREN-DTE                                     
186600                                   TO ASC-REQUEST-DTE                     
186700                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
186701             MOVE PTKREQ-PRT-BY-DTE                                       
186702                                   TO ASC-PRT-BY-DTE                      
186703                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
186710             MOVE PTKREQ-VND-DUE-DTE                                      
186720                                   TO ASC-VND-DUE-DTE                     
186730                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
186740             MOVE PTKREQ-ACTL-PRT-DTE                                     
186750                                   TO ASC-ACTL-PRT-DTE                    
186760                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
186770             MOVE PTKREQ-PRT-TM                                           
186780                                   TO ASC-PRT-TM                          
186790                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
186791             MOVE PTKREQ-ENT-ID                                           
186792                                   TO ASC-ENT-ID                          
186793                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
186794             MOVE PTKREQ-ADDR-SEQ-NBR                                     
186795                                   TO ASC-ADDR-SEQ-NBR                    
186796                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
186800             MOVE PV-TICKET-QTY                                           
186900                                   TO ASC-TICKET-QTY                      
187000                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
187103         END-IF                                                           
187200     END-PERFORM.                                                         
187300                                                                          
187400     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
187500         MOVE +1               TO ASC-BLK-SUB                             
187600         PERFORM 5100-WRITE-TEMP-STORAGE                                  
187700         ADD +1                TO ASC-BLK-SUB                             
187800         PERFORM 5100-WRITE-TEMP-STORAGE                                  
187900                                                                          
188000         MOVE 1                TO ASC-BLK-SUB                             
188100                                  PV-BLOCK-NUMBER                         
188200         PERFORM 4410-READ-TEMP-STORAGE                                   
188300         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
188400             MOVE 2            TO ASC-BLK-SUB                             
188500                                  PV-BLOCK-NUMBER                         
188600             PERFORM 4410-READ-TEMP-STORAGE                               
188700         END-IF                                                           
188800     END-IF.                                                              
188900 EJECT                                                                    
189000*----------------------------------------------------------------*        
189100*    FETCH THE MESSAGE CURSOR.                                   *        
189200*----------------------------------------------------------------*        
189300                                                                          
189400 4310-FETCH.                                                              
189500                                                                          
189800         EXEC SQL                                                         
189900              FETCH TKT_PRINT_CURSOR                                      
190000              INTO                                                        
190100                :PTKREQ-PO-REQ-NBR                                        
190200               ,:PTKREQ-PO-NBR                                            
190400               ,:PTKREQ-REQ-TYP-CDE                                       
190500               ,:PTKREQ-REQ-CREN-DTE                                      
190501               ,:PTKREQ-VND-DUE-DTE                                       
190510               ,:PTKREQ-PRT-BY-DTE                                        
190520               ,:PTKREQ-ACTL-PRT-DTE                                      
190530               ,:PTKREQ-PRT-TM                                            
190600               ,:PTKREQ-ENT-ID                                            
190610               ,:PTKREQ-ADDR-SEQ-NBR                                      
190700               ,:ENTNME-ENT-NAME-DESC                                     
191000               ,:PV-TICKET-QTY                                            
190700               ,:PV-CURRENT-DATE                                          
191000               ,:PV-COMPARE-DATE                                          
191100         END-EXEC                                                         
196100                                                                          
196200     EVALUATE TRUE                                                        
196300         WHEN SQLCODE = ZERO                                              
196400         WHEN SQLCODE = +100                                              
196500              CONTINUE                                                    
196600         WHEN SQLWARN0 NOT = SPACE                                        
196700         WHEN SQLCODE NOT = ZERO                                          
196800              MOVE '4310-FETCH-CURSOR'                                    
196900                               TO DP013-PARAGRAPH                         
197000              MOVE 'FETCH MESSAGE CURSOR'                                 
197100                               TO DP013-MESSAGE-TEXT (1)                  
197200              MOVE SQLCA       TO DP013-SQLCA                             
197300              SET DP013-DB2-ABEND                                         
197400                               TO TRUE                                    
197500              PERFORM DP013-0000-PROCESS-ABEND                            
197600     END-EVALUATE.                                                        
197700                                                                          
197800     IF  SQLCODE EQUAL +100                                               
197900         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
198000                                   TO TRUE                                
198100     END-IF.                                                              
198150 EJECT                                                                    
198200                                                                          
198400*----------------------------------------------------------------*        
198500* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
198600*----------------------------------------------------------------*        
198700                                                                          
198800 4400-MOVE-COMMAREA-TO-SCREEN.                                            
198900     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
199000         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
199100         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
199200             MOVE +1    TO ASC-BLK-SUB                                    
199300             PERFORM 5100-WRITE-TEMP-STORAGE                              
199400             ADD +1     TO ASC-BLK-SUB                                    
199500             PERFORM 5100-WRITE-TEMP-STORAGE                              
199600             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
199700                 GIVING PV-BLOCK-NUMBER                                   
199800                 REMAINDER PV-REMAINDER                                   
199900             IF  PV-REMAINDER > ZERO                                      
200000                 ADD +1 TO PV-BLOCK-NUMBER                                
200100             END-IF                                                       
200200             MOVE +1    TO ASC-BLK-SUB                                    
200300             PERFORM 4410-READ-TEMP-STORAGE                               
200400             ADD +1     TO ASC-BLK-SUB                                    
200500                           PV-BLOCK-NUMBER                                
200600             PERFORM 4410-READ-TEMP-STORAGE                               
200700     END-IF.                                                              
200800     MOVE +1            TO ASC-BLK-SUB.                                   
200900     COMPUTE ASC-ITEM-SUB =                                               
201000         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
201100         + PC-ITEMS-PER-PANEL).                                           
201200*                                                                         
201300     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
201400         ADD +1         TO ASC-BLK-SUB                                    
201500         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
201600     END-IF.                                                              
201700*                                                                         
201800     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
201900         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
202000     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
202100     PERFORM 4420-FORMAT-LIST-LINES                                       
202200         VARYING PV-SUB                                                   
202300         FROM PC-ITEMS-PER-PANEL BY -1                                    
202400         UNTIL PV-SUB < +1.                                               
202500*                                                                         
202600     COMPUTE ASC-ITEMS-DISPLAYED =                                        
202700         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
202800     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
202900                                      ASTRTITO.                           
203000     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
203100     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
203200     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
203300                                   TO ATOTSELO.                           
203400*                                                                         
203500     IF  DP020-MSG-NUMBER-X = SPACE                                       
203600         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
203700*            --- MORE ITEMS TO DISPLAY ----                               
203800             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
203900             SET DP020-MSG-INFORMATIONAL TO TRUE                          
204000         END-IF                                                           
204100     END-IF.                                                              
204200*----------------------------------------------------------------*        
204300*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
204400*----------------------------------------------------------------*        
204500                                                                          
204600 4410-READ-TEMP-STORAGE.                                                  
204700                                                                          
204800     EXEC CICS                                                            
204900         READQ TS                                                         
205000             QUEUE (ASC-LIST-QUEUE-NAME)                                  
205100             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
205200             ITEM  (PV-BLOCK-NUMBER)                                      
205300             RESP  (PV-CICS-RESP)                                         
205400     END-EXEC.                                                            
205500                                                                          
205600     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
205700       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
205800         CONTINUE                                                         
205900     ELSE                                                                 
206000         SET DP013-CICS-ABEND     TO TRUE                                 
206100         SET DP013-NO-ROLLBACK    TO TRUE                                 
206200         MOVE '4410-READ-TEMP-STORAGE'                                    
206300                                  TO DP013-PARAGRAPH                      
206400         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
206500                                  TO DP013-MESSAGE-TEXT(1)                
206600         PERFORM DP013-0000-PROCESS-ABEND                                 
206700     END-IF.                                                              
206800                                                                          
206900     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
207000*                                                                         
207100     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
207200         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
207300     END-IF.                                                              
207400 EJECT                                                                    
207500*----------------------------------------------------------------*        
207600*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
207700*----------------------------------------------------------------*        
207800                                                                          
207900 4420-FORMAT-LIST-LINES.                                                  
208000     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
208100         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
208200         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
208300         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
208400         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
208500                                    MR-ACTION (PV-SUB)                    
208600                                    MR-PO-NBR (PV-SUB)                    
208700                                    MR-REQ-TYP-CDE (PV-SUB)             86
208900                                    MR-VND-NAME (PV-SUB)                86
209100                                    MR-VND-DUE-DTE (PV-SUB)             86
209200                                    MR-PRT-BY-DTE (PV-SUB)              86
209300                                    MR-ACTL-PRT-DTE (PV-SUB)            86
209300                                    MR-PRT-TM (PV-SUB)                  86
209400                                    MR-TICKET-QTY-X (PV-SUB)            86
209500     ELSE                                                                 
209600         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
209700                                                   > SPACE                
209800             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
209900                                 TO MR-SELECTI (PV-SUB)                   
210000         ELSE                                                             
210100             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
210200                                 TO MR-SELECTI (PV-SUB)                   
210300         END-IF                                                           
210400*                                                                         
210500         IF  DP030-SET-CURSOR-TRAILER                                     
210600             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
210700                 > SPACE)                                                 
210800               OR (PV-SUB = 1))                                           
210900                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
211000                MOVE -1          TO MR-SELECTL (PV-SUB)                   
211100             END-IF                                                       
211200         END-IF                                                           
211300*                                                                         
211400         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
211500             MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)                   
211600                                 TO MR-ACTION (PV-SUB)                    
211700             MOVE ASC-PO-NBR (ASC-BLK-SUB ASC-ITEM-SUB)                   
211800                                 TO  MR-PO-NBR-N (PV-SUB)                 
211900             MOVE ASC-REQ-TYP-CDE (ASC-BLK-SUB ASC-ITEM-SUB)              
212000                                 TO  MR-REQ-TYP-CDE (PV-SUB)            86
212300             MOVE ASC-VND-NAME (ASC-BLK-SUB ASC-ITEM-SUB)                 
212400                                 TO  MR-VND-NAME (PV-SUB)               86
212700             MOVE ASC-VND-DUE-DTE (ASC-BLK-SUB ASC-ITEM-SUB)              
212800                                 TO  PV-WORK-DATE                       86
212810             MOVE PC-SLASH       TO  PV-WORK-SLASH                        
212830                                                                          
212900             MOVE PV-DISPLAY-DATE                                         
213000                                 TO  MR-VND-DUE-DTE (PV-SUB)            86
213100                                                                          
213200             MOVE ASC-PRT-BY-DTE (ASC-BLK-SUB ASC-ITEM-SUB)               
213300                                 TO  PV-WORK-DATE                       86
213310             MOVE PC-SLASH       TO  PV-WORK-SLASH                        
213400             MOVE PV-DISPLAY-DATE                                         
213500                                 TO  MR-PRT-BY-DTE (PV-SUB)             86
213600                                                                          
213700             MOVE ASC-ACTL-PRT-DTE (ASC-BLK-SUB ASC-ITEM-SUB)             
213800                                 TO  PV-WORK-DATE                       86
213810             MOVE PC-SLASH       TO  PV-WORK-SLASH                        
213900             MOVE PV-DISPLAY-DATE                                         
214000                                 TO  MR-ACTL-PRT-DTE (PV-SUB)           86
214010             MOVE ASC-PRT-TM (ASC-BLK-SUB ASC-ITEM-SUB)                   
214020                                 TO  PV-WORK-TIME                       86
214021             MOVE PV-TIME-HH     TO  PV-DISP-HH                           
214022             MOVE PV-TIME-MM     TO  PV-DISP-MM                           
214030             MOVE PV-DISPLAY-TIME                                         
214040                                 TO  MR-PRT-TM (PV-SUB)                 86
214100                                                                          
214200             MOVE ASC-TICKET-QTY    (ASC-BLK-SUB ASC-ITEM-SUB)            
214300                                 TO  MR-TICKET-QTY (PV-SUB)             86
                                                                                
      * HIGHLIGHT LINES IN YELLOW IF THEY ARE WITHIN 5 DAYS, RED IF THEY        
      * ARE PAST DUE (LESS THAN THE CURRENT DATE).                              
                   IF ASC-VND-DUE-DTE (ASC-BLK-SUB ASC-ITEM-SUB)                
                   LESS THAN OR EQUAL TO  ASC-CURRENT-DATE                      
                       MOVE DP015-RED    TO PV-COLOR                            
                   ELSE                                                         
                       IF ASC-VND-DUE-DTE (ASC-BLK-SUB ASC-ITEM-SUB)            
                       LESS THAN OR EQUAL TO ASC-COMPARE-DATE                   
                           MOVE DP015-YELLOW TO PV-COLOR                        
                       ELSE                                                     
                           MOVE DP015-BLUE       TO PV-COLOR                    
                       END-IF                                                   
                   END-IF                                                       
                                                                                
031034             MOVE PV-COLOR           TO MR-PO-NBRC (PV-SUB)               
031034                                        MR-REQ-TYP-CDEC (PV-SUB)          
031034                                        MR-TICKET-QTYC (PV-SUB)           
031034                                        MR-VND-NAMEC (PV-SUB)             
031034                                        MR-VND-DUE-DTEC (PV-SUB)          
031034                                        MR-PRT-BY-DTEC (PV-SUB)           
031034                                        MR-ACTL-PRT-DTEC (PV-SUB)         
031034                                        MR-PRT-TMC (PV-SUB)               
                                                                                
214400         END-IF                                                           
214500     END-IF.                                                              
214600     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
214700     IF  ASC-ITEM-SUB < + 1                                               
214800         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
214900         MOVE +1                 TO ASC-BLK-SUB                           
215000     END-IF.                                                              
215100******************************************************************        
215200** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
215300** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
215400** ISSUE A WARNING.                                             **        
215500******************************************************************        
215600 4430-CHECK-FOR-BEGIN-AND-END.                                            
215700     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
215800         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
215900*            -- NO END-OF-RANGE SPECFIED ---                              
216000             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
216100             SET DP020-MSG-WARNING TO TRUE                                
216200             PERFORM 4431-POSITION-AT-WARNING                             
216300         END-IF                                                           
216400     ELSE                                                                 
216500         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
216600*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
216700             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
216800             SET DP020-MSG-WARNING TO TRUE                                
216900             PERFORM 4431-POSITION-AT-WARNING                             
217000         END-IF                                                           
217100     END-IF.                                                              
217200******************************************************************        
217300** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
217400** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
217500** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
217600** POSITION THE CURSOR THERE.                                   **        
217700******************************************************************        
217800 4431-POSITION-AT-WARNING.                                                
217900     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
218000                    - PV-TOP-ITEM + 1.                                    
218100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
218200         CONTINUE                                                         
218300     ELSE                                                                 
218400         SET DP030-SET-CURSOR-APPL-1                                      
218500                                   TO TRUE                                
218600         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
218700         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
218800         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
218900     END-IF.                                                              
219000*                                                                         
219100     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
219200                    - PV-TOP-ITEM + 1.                                    
219300     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
219400         CONTINUE                                                         
219500     ELSE                                                                 
219600         SET DP030-SET-CURSOR-APPL-1                                      
219700                                   TO TRUE                                
219800         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
219900         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
220000         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
220100     END-IF.                                                              
220200 EJECT                                                                    
220300*----------------------------------------------------------------*        
220400* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
220500* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
220600* FOR THE SELECTED ITEMS QUEUE.                                  *        
220700*                                                                *        
220800* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
220900* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
221000* THE ARRAY IS FULL.                                             *        
221100*----------------------------------------------------------------*        
221200                                                                          
221300 5000-PREPARE-SEL-QUEUE.                                                  
221400                                                                          
221500     MOVE +1                   TO  ASC-BLK-SUB.                           
221600     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
221700     ADD +1                    TO  ASC-BLK-SUB.                           
221800     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
221900                                                                          
222000     MOVE ASC-SEL-QUEUE-NAME   TO  VL002-SEL-QUEUE-NAME.                  
222100     PERFORM 6010-DELETE-SEL-QUEUE.                                       
222200                                                                          
222300     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
222400                                                                          
222500     SET VL003-IDX             TO  1.                                     
222600     MOVE ZERO                 TO VL002-NUMBER-OF-TS-ITEMS.               
222700     INITIALIZE VL003-SELECTED-ITEM-ARRAY.                                
126600     MOVE +0                   TO ASC-COMMON-ENT-ID                       
126700                                  ASC-COMMON-ADDR-SEQ-NBR.                
222800                                                                          
222900     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
223000             VARYING PV-ITEM                                              
223100             FROM 1 BY 1                                                  
223200             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN OR           
223200                   PS-ERROR.                                              
223300                                                                          
223400     IF  VL003-IDX > 1                                                    
223500         PERFORM 5205-WRITE-SEL-QUEUE                                     
223600         ADD +1                TO VL002-NUMBER-OF-TS-ITEMS                
223700     END-IF.                                                              
223800 EJECT                                                                    
223900*----------------------------------------------------------------*        
224000*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
224100*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
224200*----------------------------------------------------------------*        
224300                                                                          
224400 5100-WRITE-TEMP-STORAGE.                                                 
224500     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
224600                                   TO PV-LIST-ITEM-NUMBER.                
224700     COMPUTE PV-BLOCK-NUMBER =                                            
224800         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
224900                                                                          
225000     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
225100     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
225200         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
225300             PERFORM 5105-REWRITE-ASC-LISTQ                               
225400         END-IF                                                           
225500     ELSE                                                                 
225600         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
225700         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
225800         PERFORM 5110-WRITE-ASC-LISTQ                                     
225900         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
226000     END-IF.                                                              
226100                                                                          
226200     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
226300******************************************************************        
226400** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
226500** SPECIFIC COMMAREA.                                           **        
226600******************************************************************        
226700 5105-REWRITE-ASC-LISTQ.                                                  
226800     EXEC CICS                                                            
226900         WRITEQ TS                                                        
227000             QUEUE (ASC-LIST-QUEUE-NAME)                                  
227100             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
227200             ITEM  (PV-BLOCK-NUMBER)                                      
227300             REWRITE                                                      
227400             RESP  (PV-CICS-RESP)                                         
227500     END-EXEC.                                                            
227600                                                                          
227700     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
227800         CONTINUE                                                         
227900     ELSE                                                                 
228000         SET DP013-CICS-ABEND            TO TRUE                          
228100         SET DP013-NO-ROLLBACK           TO TRUE                          
228200         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
228300         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
228400                                         TO DP013-MESSAGE-TEXT(1)         
228500         PERFORM DP013-0000-PROCESS-ABEND                                 
228600     END-IF.                                                              
228700                                                                          
228800******************************************************************        
228900** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
229000** SPECIFIC COMMAREA.                                           **        
229100******************************************************************        
229200 5110-WRITE-ASC-LISTQ.                                                    
229300     EXEC CICS                                                            
229400         WRITEQ TS                                                        
229500             QUEUE (ASC-LIST-QUEUE-NAME)                                  
229600             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
229700             ITEM  (PV-BLOCK-NUMBER)                                      
229800             RESP  (PV-CICS-RESP)                                         
229900     END-EXEC.                                                            
230000     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
230100         SET DP013-CICS-ABEND      TO TRUE                                
230200         SET DP013-NO-ROLLBACK     TO TRUE                                
230300         MOVE '5110-WRITE-ASC-LISTQ'                                      
230400                                   TO DP013-PARAGRAPH                     
230500         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
230600                                   TO DP013-MESSAGE-TEXT(1)               
230700         PERFORM DP013-0000-PROCESS-ABEND                                 
230800     END-IF.                                                              
230900 EJECT                                                                    
231000*----------------------------------------------------------------*        
231100* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
231200* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
231300*----------------------------------------------------------------*        
231400                                                                          
231500 5200-FILL-SEL-QUEUE-RECORD.                                              
231600                                                                          
231700     PERFORM 6112-READ-LIST-QUEUE.                                        
231800                                                                          
231900     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
232000         PERFORM                                                          
232100         VARYING LQW-IDX                                                  
232200         FROM 1 BY 1                                                      
232300         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
232400           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
232500                                                                          
232600             IF  LQW-SELECT (LQW-IDX)                                     
138500                 IF ASC-COMMON-ENT-ID = +0                                
138600                    MOVE LQW-ENT-ID (LQW-IDX)                             
138700                                            TO ASC-COMMON-ENT-ID          
138800                 ELSE                                                     
139000                     IF LQW-ENT-ID (LQW-IDX) = ASC-COMMON-ENT-ID          
139100                         CONTINUE                                         
139101                     ELSE                                                 
                              MOVE PC-TSYMSG-01362                              
                                                  TO DP020-MSG-NUMBER           
139104                        SET PS-ERROR        TO TRUE                       
139107                     END-IF                                               
139107                 END-IF                                                   
138500                 IF ASC-COMMON-ADDR-SEQ-NBR = +0                          
138600                     MOVE LQW-ADDR-SEQ-NBR (LQW-IDX)                      
139150                                        TO ASC-COMMON-ADDR-SEQ-NBR        
138800                 ELSE                                                     
139140                     IF LQW-ADDR-SEQ-NBR (LQW-IDX)                        
139150                                        =  ASC-COMMON-ADDR-SEQ-NBR        
139160                         CONTINUE                                         
139161                     ELSE                                                 
139162                         MOVE PC-TSYMSG-01363                             
                                                  TO DP020-MSG-NUMBER           
139166                         SET PS-ERROR       TO TRUE                       
139167                     END-IF                                               
139167                 END-IF                                                   
139000                 IF PS-NO-ERROR                                           
232700                     MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                  
232800                                   TO VL003-ITEM (VL003-IDX)              
232900                     MOVE LQW-PO-REQ-NBR (LQW-IDX)                        
233000                                   TO VL003-PO-REQ-NBR                    
233100                                        (VL003-IDX)                       
233101                     MOVE LQW-PO-NBR     (LQW-IDX)                        
233102                                   TO VL003-PO-NUMBER                     
233103                                        (VL003-IDX)                       
233104                     MOVE LQW-REQUEST-DTE (LQW-IDX)                       
233105                                   TO VL003-REQUEST-DATE                  
233106                                        (VL003-IDX)                       
233107                     MOVE LQW-ENT-ID (LQW-IDX)                            
233108                                   TO VL002-ENT-ID                        
233111                     MOVE LQW-VND-NAME (LQW-IDX)                          
233112                                   TO VL002-VENDOR-NAME                   
233115                     MOVE LQW-ADDR-SEQ-NBR (LQW-IDX)                      
233116                                   TO VL002-ADDR-SEQ-NBR                  
233117                     MOVE LQW-VND-DUE-DTE (LQW-IDX)                       
233118                                   TO VL003-VND-DUE-DATE                  
233119                                        (VL003-IDX)                       
233120                     MOVE LQW-ACTL-PRT-DTE (LQW-IDX)                      
233121                                   TO VL003-PRINT-DATE                    
233122                                        (VL003-IDX)                       
233200                     MOVE SPACE    TO VL003-ACTION-SW (VL003-IDX)         
233300                     SET VL003-IDX UP BY 1                                
233400                     IF  VL003-IDX > VL003-MAX-ITEMS                      
233500                         PERFORM 5205-WRITE-SEL-QUEUE                     
233600                         ADD +1        TO VL002-NUMBER-OF-TS-ITEMS        
233700                         INITIALIZE VL003-SELECTED-ITEM-ARRAY             
233800                         SET VL003-IDX TO  1                              
233900                     END-IF                                               
233900                 END-IF                                                   
234000             END-IF                                                       
234100         END-PERFORM                                                      
234200     END-IF.                                                              
234300                                                                          
234400*----------------------------------------------------------------*        
234500*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
234600*----------------------------------------------------------------*        
234700                                                                          
234800 5205-WRITE-SEL-QUEUE.                                                    
234900                                                                          
235000     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
235100                                   TO VL003-SELECTED-ITEMS.               
235200                                                                          
235300     EXEC CICS                                                            
235400          WRITEQ TS                                                       
235500              QUEUE (ASC-SEL-QUEUE-NAME)                                  
235600              FROM  (VL003-REQUEST-SEL-REC)                               
235700              ITEM  (PV-SEL-QUEUE-ITEM)                                   
235800              RESP  (PV-CICS-RESP)                                        
235900     END-EXEC.                                                            
236000                                                                          
236100     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
236200         CONTINUE                                                         
236300     ELSE                                                                 
236400         SET DP013-CICS-ABEND      TO TRUE                                
236500         SET DP013-NO-ROLLBACK     TO TRUE                                
236600         MOVE '5205-WRITE-SEL-QUEUE'                                      
236700                                   TO DP013-PARAGRAPH                     
236800         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
236900                                   TO DP013-MESSAGE-TEXT(1)               
237000         PERFORM DP013-0000-PROCESS-ABEND                                 
237100     END-IF.                                                              
237200 EJECT                                                                    
237300******************************************************************        
237400** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
237500** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
237600** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
237700** THE USER.                                                    **        
237800******************************************************************        
237900 6000-REFRESH-PANEL-FROM-TEMP.                                            
238000     IF  ASC-UPDATE-TSQ                                                   
238100         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
238200         MOVE ZERO                 TO PV-SAVE-ITEM                        
238300                                      PV-SEL-QUEUE-ITEM                   
238400         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
238500                                   TO PV-HOLD-SELECTED-ITEMS              
238600         MOVE +1                   TO PV-ITEMS-PROCESSED                  
238700         PERFORM 6100-UPDATE-TSQ                                          
238800             UNTIL PV-ITEMS-PROCESSED >                                   
238900                                     PV-HOLD-SELECTED-ITEMS               
239000         PERFORM 6010-DELETE-SEL-QUEUE                                    
239100         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
239200                    ASC-BLOCK-ENTRIES (2)                                 
239300     END-IF.                                                              
239400                                                                          
239500*----------------------------------------------------------------*        
239600*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
239700*----------------------------------------------------------------*        
239800 6010-DELETE-SEL-QUEUE.                                                   
239900                                                                          
240000     EXEC CICS                                                            
240100         DELETEQ TS                                                       
240200             QUEUE (ASC-SEL-QUEUE-NAME)                                   
240300             RESP  (PV-CICS-RESP)                                         
240400     END-EXEC.                                                            
240500*                                                                         
240600     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
240700        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
240800         CONTINUE                                                         
240900     ELSE                                                                 
241000         SET DP013-CICS-ABEND TO TRUE                                     
241100         SET DP013-NO-ROLLBACK TO TRUE                                    
241200         MOVE '6010-DELETE-SEL-QUEUE'                                     
241300                        TO DP013-PARAGRAPH                                
241400         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
241500                        TO DP013-MESSAGE-TEXT (1)                         
241600         PERFORM DP013-0000-PROCESS-ABEND                                 
241700     END-IF.                                                              
241800                                                                          
241900*----------------------------------------------------------------*        
242000*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
242100*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
242200*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
242300*    THE TEMP QUEUE.                                             *        
242400*----------------------------------------------------------------*        
242500                                                                          
242600 6100-UPDATE-TSQ.                                                         
242700                                                                          
242800     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
242900     PERFORM 6105-READ-SEL-QUEUE.                                         
243000     PERFORM                                                              
243100         VARYING VL003-IDX                                                
243200         FROM 1 BY 1                                                      
243300         UNTIL ((PV-ITEMS-PROCESSED >                                     
243400                              PV-HOLD-SELECTED-ITEMS)                     
243500         OR (VL003-IDX GREATER THAN VL003-MAX-ITEMS))                     
243600         IF  VL003-ACTION-SW (VL003-IDX) > SPACE                          
243700             PERFORM 6110-PROCESS-ITEMS                                   
243800         END-IF                                                           
243900         ADD +1                TO PV-ITEMS-PROCESSED                      
244000     END-PERFORM.                                                         
244100     PERFORM 6111-WRITE-LIST-QUEUE.                                       
244200*----------------------------------------------------------------*        
244300*    READ THE LIST QUEUE RECORD.                                 *        
244400*----------------------------------------------------------------*        
244500                                                                          
244600 6105-READ-SEL-QUEUE.                                                     
244700                                                                          
244800     EXEC CICS                                                            
244900          READQ TS                                                        
245000              QUEUE (ASC-SEL-QUEUE-NAME)                                  
245100              INTO  (VL003-REQUEST-SEL-REC)                               
245200              ITEM  (PV-SEL-QUEUE-ITEM)                                   
245300              RESP  (PV-CICS-RESP)                                        
245400     END-EXEC.                                                            
245500*                                                                         
245600     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
245700         CONTINUE                                                         
245800     ELSE                                                                 
245900         SET DP013-CICS-ABEND      TO  TRUE                               
246000         SET DP013-NO-ROLLBACK     TO  TRUE                               
246100         MOVE '6105-READ-SEL-QUEUE'                                       
246200                                   TO  DP013-PARAGRAPH                    
246300         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
246400                                   TO  DP013-MESSAGE-TEXT(1)              
246500         PERFORM DP013-0000-PROCESS-ABEND                                 
246600     END-IF.                                                              
246700 EJECT                                                                    
246800*----------------------------------------------------------------*        
246900*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
247000*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
247100*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
247200*----------------------------------------------------------------*        
247300                                                                          
247400 6110-PROCESS-ITEMS.                                                      
247500                                                                          
247600     COMPUTE PV-ITEM = 1 + ((VL003-ITEM (VL003-IDX) - 1)                  
247700                     / PC-ITEMS-PER-PANEL).                               
247800                                                                          
247900     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
248000         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
248100             PERFORM 6111-WRITE-LIST-QUEUE                                
248200         END-IF                                                           
248300         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
248400                                                                          
248500         PERFORM 6112-READ-LIST-QUEUE                                     
248600         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
248700             CONTINUE                                                     
248800         END-IF                                                           
248900     END-IF.                                                              
249000                                                                          
249100     COMPUTE PV-SUB = (VL003-ITEM (VL003-IDX)                             
249200                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
249300     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
249400     MOVE LQW-SELECTED-SW (PV-SUB)                                        
249500                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
249600     EVALUATE TRUE                                                        
249700         WHEN VL003-SHP (VL003-IDX)                                       
249800              MOVE PC-SHP      TO LQW-ACTION (PV-SUB)                     
249900                                                                          
249901         WHEN VL003-INQ (VL003-IDX)                                       
249902              MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
249903                                                                          
250000         WHEN VL003-UPD (VL003-IDX)                                       
250100              MOVE PC-UPD      TO LQW-ACTION (PV-SUB)                     
250200                                                                          
250300         WHEN VL003-DEL (VL003-IDX)                                       
250400              MOVE PC-DEL      TO LQW-ACTION (PV-SUB)                     
250500                                                                          
250600         WHEN VL003-ERR (VL003-IDX)                                       
250700              MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
250800     END-EVALUATE.                                                        
250900                                                                          
251000     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
251100                                                                          
251200*----------------------------------------------------------------*        
251300*    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
251400*----------------------------------------------------------------*        
251500                                                                          
251600 6111-WRITE-LIST-QUEUE.                                                   
251700                                                                          
251800     EXEC CICS                                                            
251900         WRITEQ TS                                                        
252000             QUEUE (ASC-LIST-QUEUE-NAME)                                  
252100             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
252200             ITEM  (PV-SAVE-ITEM)                                         
252300             REWRITE                                                      
252400             RESP  (PV-CICS-RESP)                                         
252500     END-EXEC.                                                            
252600*                                                                         
252700     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
252800         CONTINUE                                                         
252900     ELSE                                                                 
253000         SET DP013-CICS-ABEND  TO  TRUE                                   
253100         SET DP013-NO-ROLLBACK TO  TRUE                                   
253200         MOVE '6111-WRITE-LIST-QUEUE'                                     
253300                               TO  DP013-PARAGRAPH                        
253400         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
253500                               TO  DP013-MESSAGE-TEXT(1)                  
253600         PERFORM DP013-0000-PROCESS-ABEND                                 
253700     END-IF.                                                              
253800                                                                          
253900 EJECT                                                                    
254000*----------------------------------------------------------------*        
254100*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
254200*----------------------------------------------------------------*        
254300                                                                          
254400 6112-READ-LIST-QUEUE.                                                    
254500                                                                          
254600     EXEC CICS                                                            
254700         READQ TS                                                         
254800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
254900             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
255000             ITEM  (PV-ITEM)                                              
255100             RESP  (PV-CICS-RESP)                                         
255200     END-EXEC.                                                            
255300*                                                                         
255400     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
255500         CONTINUE                                                         
255600     ELSE                                                                 
255700         SET DP013-CICS-ABEND        TO TRUE                              
255800         SET DP013-NO-ROLLBACK       TO TRUE                              
255900         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
256000         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
256100                                     TO DP013-MESSAGE-TEXT(1)             
256200         PERFORM DP013-0000-PROCESS-ABEND                                 
256300     END-IF.                                                              
256400                                                                          
256500 EJECT                                                                    
256600*----------------------------------------------------------------*        
256700*    ABEND PROCESSOR MODULE                                      *        
256800*----------------------------------------------------------------*        
256900                                                                          
257000     COPY DPPD013.                                                        
