       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    IMKUT110.                                                 
       AUTHOR.        AMANDA JEFFORDS.                                          
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03-16-2009.                                               
       DATE-COMPILED.                                                           
                                                                                
      *******************************************************************       
      * IMKUT110 - CREATE A COMMA DELIMITED FILE OF ALL DIRECT                  
      * SHIP WEB EXCLUSIVE ITEMS EXTRACT.  FTP THE FILE AND EMAIL THE           
      * USER ONCE THE FILE IS HAS BEEN FTP'D.                                   
      ******************************************************************        
      * CHANGE: INC000000750069                          DATE:03/26/09 *        
      * PROGRAMMER: AMANDA JEFFORDS                                    *        
      * CHANGE DESCRIPTION: THE OUT01 WAS RPT01.  CHANGED TO OUT01     *        
      ******************************************************************        
      * CHANGE: INC000000750195                          DATE:03/26/09 *        
      * PROGRAMMER: AMANDA JEFFORDS                                    *        
      * CHANGE DESCRIPTION: SKU WAS RETURNING RENUMBERED SKUS.  ADDED  *        
      *                     WHERE CLAUSE TO NOT RETURN THEM.           *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT PA-UNLOAD-IN-FILE                                             
              ASSIGN TO INP01.                                                  
                                                                                
           SELECT FINAL-EXCLUSIVE-FILE                                          
              ASSIGN TO OUT01.                                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PA-UNLOAD-IN-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 180 CHARACTERS                                       
           DATA RECORD IS PA-UNLOAD-IN-REC.                                     
                                                                                
       01  PA-UNLOAD-IN-REC                    PIC X(180).                      
                                                                                
       FD  FINAL-EXCLUSIVE-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 180 CHARACTERS                                       
           DATA RECORD IS FINAL-EXCLUSIVE-REC.                                  
                                                                                
       01  FINAL-EXCLUSIVE-REC                 PIC  X(180).                     
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW               PIC  X(01)  VALUE 'N'.           
               88 PS-END-OF-FILE                           VALUE 'Y'.           
               88 PS-NOT-END-OF-FILE                       VALUE 'N'.           
           05  PS-WRITE-RECORD-SW              PIC  X(01)  VALUE 'Y'.           
               88 PS-WRITE-RECORD                          VALUE 'Y'.           
               88 PS-NOT-WRITE-RECORD                      VALUE 'N'.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-RETRY-COUNT                  PIC S9(04)  VALUE ZERO           
                                                           COMP SYNC.           
           05  PC-MAXIMUM-RETRY-COUNT          PIC S9(04)  VALUE +2             
                                                           COMP SYNC.           
           05  PC-DPT                          PIC  X(03)  VALUE 'DPT'.         
           05  PC-DMM                          PIC  X(03)  VALUE 'DMM'.         
           05  PC-DMA                          PIC  X(03)  VALUE 'DMA'.         
           05  PC-THREE                        PIC  X(02)  VALUE '03'.          
           05  PC-YES                          PIC  X(01)  VALUE 'Y'.           
           05  PC-NO                           PIC  X(01)  VALUE 'N'.           
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECORD-COUNTS.                                                
               10  PA-RECS-WRITTEN-CT          PIC  9(09)  VALUE ZERO.          
               10  PA-READ-REC                 PIC  9(09)  VALUE ZERO.          
               10  PA-SUB-CL-CT                PIC  9(09)  VALUE ZERO.          
               10  PA-DMM-CT                   PIC  9(09)  VALUE ZERO.          
               10  PA-SKU-CT                   PIC  9(09)  VALUE ZERO.          
               10  PA-VND-STYL-CT              PIC  9(09)  VALUE ZERO.          
               10  PA-VND-NM-CT                PIC  9(09)  VALUE ZERO.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME               PIC  X(30).                      
           05  PV-DB2-OPER-ATTEMPTED           PIC  X(30).                      
           05  PV-VS-ID                        PIC S9(09)  COMP                 
                                                           VALUE ZERO.          
           05  PV-UPC-ID                       PIC S9(9)   COMP                 
                                                           VALUE ZERO.          
           05  PV-DMM-NBR                      PIC  X(02)  VALUE SPACE.         
           05  PV-DMM-DESC                     PIC  X(25)  VALUE SPACE.         
           05  PV-SUB-CL-MDSEAR-ID             PIC  9(06)  VALUE ZERO.          
           05  PV-DEPT-NBR-NBR                 PIC  9(03)  VALUE ZERO.          
           05  PV-DEPT-NBR-TXT                 PIC  X(03)  VALUE SPACE.         
           05  PV-MAJ-CL-NBR-NBR               PIC  9(02)  VALUE ZERO.          
           05  PV-MAJ-CL-NBR-TXT               PIC  X(03)  VALUE SPACE.         
           05  PV-SUB-CL-NBR-NBR               PIC  9(03)  VALUE ZERO.          
           05  PV-SUB-CL-NBR-TXT               PIC  X(03)  VALUE SPACE.         
           05  PV-SKU-NBR                      PIC  9(08)  VALUE ZERO.          
           05  PV-SKU-TXT                      PIC  X(08)  VALUE SPACE.         
           05  PV-PRD-GP-NBR-NBR               PIC  9(07)  VALUE ZERO.          
           05  PV-PRD-GP-NBR-TXT               PIC  X(07)  VALUE SPACE.         
           05  PV-SHIP-GP-SEQ-NBR              PIC  9(04)  VALUE ZERO.          
           05  PV-SHIP-GP-SEQ-NBR-TXT          PIC  X(04)  VALUE SPACE.         
           05  PV-VS-NBR                       PIC  X(20)  VALUE SPACE.         
           05  PV-SKU-CREATE-DTE               PIC  X(10)  VALUE SPACE.         
           05  PV-PREV-SUB-CL-MDSEAR-ID        PIC S9(06)V COMP-3               
                                                           VALUE ZERO.          
           05  PV-PREV-DEPT-NBR                PIC  9(03)  VALUE ZERO.          
                                                                                
       01  ERROR-CODE                          PIC S9(04)  VALUE +0             
                                                           COMP SYNC.           
       01  ABEND-CODE                          PIC S9(04)  VALUE +0             
                                                           COMP SYNC.           
           88 ABEND-4013                                   VALUE +4013.         
           88 ABEND-4003                                   VALUE +4003.         
                                                                                
      ******************************************************************        
      * DIRECT SHIP AND WEB EXCLUSIVE ITEMS - INPUT                             
      ******************************************************************        
           COPY IMRD096.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FOR IMKUT110                                                     
      ******************************************************************        
           COPY IMRD097.                                                        
                                                                                
      ******************************************************************        
      * CALENDAR ROUTINE WORK AREA.                                             
      ******************************************************************        
           COPY DPWS500.                                                        
                                                                                
      ******************************************************************        
      * DB2 ERROR MESSAGES FORMAT AREA.                                         
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
           EXEC SQL                                                             
              INCLUDE TMDARVS                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TMDSEAR                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TRSPBAR                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TSKXREF                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TVNDSTL                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TENTNME                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * MAIN PROCESSING                                                         
      ******************************************************************        
                                                                                
           PERFORM 0000-INITIALIZE.                                             
                                                                                
           PERFORM 1000-PROCESS-INPUT                                           
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'RECORDS READ:        ' PA-READ-REC                          
           DISPLAY 'RECORDS WRITTEN:     ' PA-RECS-WRITTEN-CT                   
           DISPLAY 'DIFFERENT SUB CLASS: ' PA-SUB-CL-CT                         
           DISPLAY 'DIFFERENT DMMS:      ' PA-DMM-CT                            
           DISPLAY 'SKUS FOUND:          ' PA-SKU-CT                            
           DISPLAY 'VENDOR STYLES FOUND: ' PA-VND-STYL-CT                       
           DISPLAY 'VENDOR NAMES FOUND:  ' PA-VND-NM-CT                         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE PA-UNLOAD-IN-FILE                                              
                 FINAL-EXCLUSIVE-FILE.                                          
                                                                                
           IF PA-RECS-WRITTEN-CT = 0                                            
              MOVE 4095 TO ERROR-CODE                                           
           END-IF.                                                              
           MOVE ERROR-CODE TO RETURN-CODE.                                      
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * READ FIRST INPUT REC                                                    
      ******************************************************************        
       0000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT PA-UNLOAD-IN-FILE                                         
                OUTPUT FINAL-EXCLUSIVE-FILE.                                    
                                                                                
           PERFORM Z100-READ-PA-UNLOAD-IN-FILE.                                 
           IF PS-END-OF-FILE                                                    
               MOVE 4095 TO ERROR-CODE                                          
               DISPLAY ' '                                                      
               DISPLAY '***** INPUT FILE IS EMPTY *****'                        
               DISPLAY ' '                                                      
           ELSE                                                                 
               MOVE 0 TO ERROR-CODE                                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * PROCESS INITIATIVE                                                      
      ******************************************************************        
       1000-PROCESS-INPUT.                                                      
           INITIALIZE IM097-RECORD.                                             
           SET PS-WRITE-RECORD TO TRUE.                                         
                                                                                
           IF PV-SUB-CL-MDSEAR-ID NOT EQUAL PV-PREV-SUB-CL-MDSEAR-ID            
               PERFORM 1100-GET-DEPT                                            
           END-IF.                                                              
                                                                                
           IF PS-WRITE-RECORD                                                   
              IF PV-DEPT-NBR-NBR NOT EQUAL PV-PREV-DEPT-NBR                     
                 PERFORM 1200-GET-DMM-INFO                                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF PS-WRITE-RECORD                                                   
              PERFORM 1300-GET-SKU                                              
           END-IF.                                                              
           IF PS-WRITE-RECORD                                                   
              PERFORM 1400-GET-VND-STYL                                         
           END-IF.                                                              
           IF PS-WRITE-RECORD                                                   
              PERFORM 1500-GET-VND-NM                                           
           END-IF.                                                              
                                                                                
           IF PS-WRITE-RECORD                                                   
              PERFORM 2000-MOVE-AND-WRITE                                       
              MOVE PV-SUB-CL-MDSEAR-ID TO PV-PREV-SUB-CL-MDSEAR-ID              
              MOVE PV-DEPT-NBR-NBR  TO PV-PREV-DEPT-NBR                         
           END-IF.                                                              
           PERFORM Z100-READ-PA-UNLOAD-IN-FILE.                                 
                                                                                
      ******************************************************************        
      * GET DEPARTMENT, MAJOR CLASS AND SUBCLASS                                
      ******************************************************************        
       1100-GET-DEPT.                                                           
           MOVE '1100-GET-DEPT'      TO PV-PARAGRAPH-NAME.                      
           MOVE 'GET DEPT'           TO PV-DB2-OPER-ATTEMPTED.                  
           INITIALIZE PC-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = ZERO)                                      
                                OR                                              
                          (SQLCODE = +100)                                      
                                OR                                              
                          (PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT))            
                                                                                
             EXEC SQL                                                           
               SELECT DEPT_NBR                                                  
                     ,MAJ_CL_NBR                                                
                     ,SUB_CL_NBR                                                
               INTO :MDARVS-DEPT-NBR                                            
                   ,:MDARVS-MAJ-CL-NBR                                          
                   ,:MDARVS-SUB-CL-NBR                                          
               FROM TMDARVS                                                     
               WHERE SUB_CL_MDSEAR_ID = :IM096-SUB-CL-MDSEAR-ID                 
                 AND VS_ID   = :IM096-VS-ID                                     
                 AND ACTV_IND = 'Y'                                             
               WITH UR                                                          
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = +000                                            
                     MOVE MDARVS-DEPT-NBR   TO PV-DEPT-NBR-NBR                  
                     MOVE PV-DEPT-NBR-NBR   TO PV-DEPT-NBR-TXT                  
                     MOVE MDARVS-MAJ-CL-NBR TO PV-MAJ-CL-NBR-NBR                
                     MOVE PV-MAJ-CL-NBR-NBR TO PV-MAJ-CL-NBR-TXT                
                     MOVE MDARVS-SUB-CL-NBR TO PV-SUB-CL-NBR-NBR                
                     MOVE PV-SUB-CL-NBR-NBR TO PV-SUB-CL-NBR-TXT                
                     ADD +1 TO PA-SUB-CL-CT                                     
                 WHEN SQLCODE = +100                                            
                     SET PS-NOT-WRITE-RECORD TO TRUE                            
                 WHEN OTHER                                                     
                     PERFORM 9000-SQL-ERROR                                     
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  SQLCODE = -911                                                   
            OR SQLCODE = -913                                                   
               MOVE '1100-GET-DEPT'        TO PV-PARAGRAPH-NAME                 
               MOVE 'SELECT FROM TMDARVS'  TO PV-DB2-OPER-ATTEMPTED        CL**8
               PERFORM 9000-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * GET DMM NAME AND NUMBER                                                 
      ******************************************************************        
       1200-GET-DMM-INFO.                                                       
                                                                                
           MOVE '1200-GET-DMM-INFO'  TO PV-PARAGRAPH-NAME.                      
           MOVE 'GET DMM INFO'       TO PV-DB2-OPER-ATTEMPTED.                  
           INITIALIZE PC-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = ZERO)                                      
                                OR                                              
                          (SQLCODE = +100)                                      
                                OR                                              
                          (PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT))            
                                                                                
             EXEC SQL                                                           
               SELECT A.DMA_NBR,                                                
                      A.MDSEAR_DESC                                             
               INTO :MDSEAR-DMA-NBR                                             
                   ,:MDSEAR-DESC                                                
               FROM TMDSEAR A                                                   
               WHERE A.OPERCO_NBR = :PC-THREE                                   
                 AND A.DMA_NBR = (SELECT C.DMA_NBR                              
                                  FROM TMDSEAR C                                
                                  WHERE C.OPERCO_NBR = :PC-THREE                
                                    AND C.DEPT_NBR = :PV-DEPT-NBR-TXT           
                                    AND C.MDSELV_CDE = :PC-DPT                  
                                    AND (DATE(CURRENT DATE)                     
                                         BETWEEN C.STRT_DTE                     
                                         AND C.END_DTE))                        
                 AND A.MDSELV_CDE = :PC-DMA                                     
                 AND (DATE(CURRENT DATE)                                        
                      BETWEEN A.STRT_DTE                                        
                      AND A.END_DTE)                                            
                 WITH UR                                                        
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = +000                                            
                     MOVE MDSEAR-DMA-NBR          TO PV-DMM-NBR                 
                     MOVE MDSEAR-DESC             TO PV-DMM-DESC                
                     ADD +1 TO PA-DMM-CT                                        
                 WHEN SQLCODE = +100                                            
                     SET PS-NOT-WRITE-RECORD TO TRUE                            
                 WHEN OTHER                                                     
                     PERFORM 9000-SQL-ERROR                                     
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  SQLCODE = -911                                                   
            OR SQLCODE = -913                                                   
               MOVE '200-GET-DMM-INFO'     TO PV-PARAGRAPH-NAME                 
               MOVE 'SELECT FROM TMDSEAR'  TO PV-DB2-OPER-ATTEMPTED        CL**8
               PERFORM 9000-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * GET SKU                                                                 
      ******************************************************************        
       1300-GET-SKU.                                                            
           MOVE '1300-GET-SKU'       TO PV-PARAGRAPH-NAME.                      
           MOVE 'GET SKU'            TO PV-DB2-OPER-ATTEMPTED.                  
           INITIALIZE PC-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = ZERO)                                      
                                OR                                              
                          (SQLCODE = +100)                                      
                                OR                                              
                          (PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT))            
                                                                                
             EXEC SQL                                                           
               SELECT SKU                                                       
                     ,SKU_STAT_CDE                                              
               INTO :SKXREF-SKU                                                 
                   ,:SKXREF-SKU-STAT-CDE                                        
               FROM TSKXREF                                                     
               WHERE INTR_UPC_ID = :IM096-UPC-ID                                
                 AND SKU_STAT_CDE IN ('10','20','25','30','40','90')            
               WITH UR                                                          
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = +000                                            
                     MOVE SKXREF-SKU TO PV-SKU-NBR                              
                     MOVE PV-SKU-NBR TO PV-SKU-TXT                              
                     ADD +1 TO PA-SKU-CT                                        
                 WHEN SQLCODE = +100                                            
                     SET PS-NOT-WRITE-RECORD TO TRUE                            
                 WHEN OTHER                                                     
                     DISPLAY 'UPC-ID = ' IM096-UPC-ID                           
                     PERFORM 9000-SQL-ERROR                                     
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  SQLCODE = -911                                                   
            OR SQLCODE = -913                                                   
               MOVE '1300-GET-SKU'         TO PV-PARAGRAPH-NAME                 
               MOVE 'SELECT FROM TSKXREF'  TO PV-DB2-OPER-ATTEMPTED        CL**8
               PERFORM 9000-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * GET VND STYLE                                                           
      ******************************************************************        
       1400-GET-VND-STYL.                                                       
           MOVE '1400-GET-VND-STYL'  TO PV-PARAGRAPH-NAME.                      
           MOVE 'GET VND STYL'       TO PV-DB2-OPER-ATTEMPTED.                  
           INITIALIZE PC-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = ZERO)                                      
                                OR                                              
                          (SQLCODE = +100)                                      
                                OR                                              
                          (PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT))            
                                                                                
             EXEC SQL                                                           
               SELECT VS_NBR                                                    
                     ,ENT_ID                                                    
               INTO :VNDSTL-VS-NBR                                              
                   ,:VNDSTL-ENT-ID                                              
               FROM TVNDSTL                                                     
               WHERE VS_ID = :IM096-VS-ID                                       
                 AND EFF_END_DTE IS NULL                                        
               WITH UR                                                          
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = +000                                            
                     MOVE VNDSTL-VS-NBR TO PV-VS-NBR                            
                     ADD +1 TO PA-VND-STYL-CT                                   
                 WHEN SQLCODE = +100                                            
                     SET PS-NOT-WRITE-RECORD TO TRUE                            
                 WHEN OTHER                                                     
                     PERFORM 9000-SQL-ERROR                                     
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  SQLCODE = -911                                                   
            OR SQLCODE = -913                                                   
               MOVE '1400-GET-VND-STYL'                                         
                                     TO PV-PARAGRAPH-NAME                       
               MOVE 'SELECT FROM TVNDSTL'                                  CL**8
                                     TO PV-DB2-OPER-ATTEMPTED              CL**1
               PERFORM 9000-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * GET VENDOR NAME                                                         
      ******************************************************************        
       1500-GET-VND-NM.                                                         
           MOVE '1500-GET-VND-NM'    TO PV-PARAGRAPH-NAME.                      
           MOVE 'GET VND NAME'       TO PV-DB2-OPER-ATTEMPTED.                  
           INITIALIZE PC-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = ZERO)                                      
                                OR                                              
                          (SQLCODE = +100)                                      
                                OR                                              
                          (PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT))            
                                                                                
             EXEC SQL                                                           
               SELECT ENT_NAME_DESC                                             
               INTO :ENTNME-ENT-NAME-DESC                                       
               FROM TENTNME                                                     
               WHERE ENT_ID = :VNDSTL-ENT-ID                                    
                 AND TYPE_CDE = '01'                                            
               WITH UR                                                          
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = +000                                            
                     ADD +1 TO PA-VND-NM-CT                                     
                 WHEN SQLCODE = +100                                            
                     SET PS-NOT-WRITE-RECORD TO TRUE                            
                 WHEN OTHER                                                     
                     PERFORM 9000-SQL-ERROR                                     
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  SQLCODE = -911                                                   
            OR SQLCODE = -913                                                   
               MOVE '1500-GET-VND-NM'                                           
                                     TO PV-PARAGRAPH-NAME                       
               MOVE 'SELECT FROM TENTNME'                                  CL**8
                                     TO PV-DB2-OPER-ATTEMPTED              CL**1
               PERFORM 9000-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * MOVE FIELDS TO OUTPUT REC AND WRITE IT                                  
      *****************************************************************         
       2000-MOVE-AND-WRITE.                                                     
                                                                                
           MOVE PV-DMM-NBR                  TO IM097-DMM-NBR.                   
           MOVE PV-DMM-DESC                 TO IM097-DMM-DESC.                  
           MOVE ENTNME-ENT-NAME-DESC        TO IM097-VENDOR-NAME.               
           MOVE PV-DEPT-NBR-TXT             TO IM097-DEPT-NBR.                  
           MOVE PV-MAJ-CL-NBR-TXT           TO IM097-MAJ-CL-NBR.                
           MOVE PV-SUB-CL-NBR-TXT           TO IM097-SUB-CL-NBR.                
           MOVE PV-VS-NBR                   TO IM097-VS-NBR.                    
           MOVE PV-SKU-TXT                  TO IM097-SKU.                       
           MOVE PV-PRD-GP-NBR-TXT           TO IM097-PRD-GP-NBR.                
           MOVE IM096-INTR-UPC-DESC         TO IM097-INTR-UPC-DESC.             
           IF PV-SHIP-GP-SEQ-NBR = 13 THEN                                      
              MOVE PC-YES TO IM097-DIRECT-SHIP                                  
           ELSE                                                                 
             MOVE PC-NO TO IM097-DIRECT-SHIP                                    
           END-IF.                                                              
           MOVE IM096-WEB-EXCLUSION         TO IM097-WEB-EXCLUSIVE.             
           MOVE SKXREF-SKU-STAT-CDE         TO IM097-STATUS-CODE.               
           MOVE IM096-SKU-CREATE-DTE        TO IM097-SKU-CREATE-DATE.           
                                                                                
           MOVE IM097-RECORD  TO FINAL-EXCLUSIVE-REC.                           
           WRITE FINAL-EXCLUSIVE-REC.                                           
                                                                                
           ADD +1 TO PA-RECS-WRITTEN-CT.                                        
                                                                                
      *****************************************************************         
      * READ INPUT FILE                                                         
      *****************************************************************         
       Z100-READ-PA-UNLOAD-IN-FILE.                                             
                                                                                
           READ PA-UNLOAD-IN-FILE                                               
               INTO IM096-RECORD                                                
               AT END                                                           
                   SET PS-END-OF-FILE TO TRUE.                                  
                                                                                
           IF PS-NOT-END-OF-FILE                                                
               MOVE IM096-SUB-CL-MDSEAR-ID TO PV-SUB-CL-MDSEAR-ID               
               MOVE IM096-VS-ID            TO PV-VS-ID                          
               MOVE IM096-UPC-ID           TO PV-UPC-ID                         
               MOVE IM096-PRD-GP-NBR       TO PV-PRD-GP-NBR-NBR                 
               MOVE PV-PRD-GP-NBR-NBR      TO PV-PRD-GP-NBR-TXT                 
               MOVE IM096-DIRECT-SHIP      TO PV-SHIP-GP-SEQ-NBR                
               MOVE PV-SHIP-GP-SEQ-NBR     TO PV-SHIP-GP-SEQ-NBR-TXT            
               MOVE IM096-SKU-CREATE-DTE   TO PV-SKU-CREATE-DTE                 
               ADD +1 TO PA-READ-REC                                            
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * SQL ERROR                                                               
      *****************************************************************         
       9000-SQL-ERROR.                                                          
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'RECORDS READ:        ' PA-READ-REC                          
           DISPLAY 'RECORDS WRITTEN:     ' PA-RECS-WRITTEN-CT                   
           DISPLAY 'DIFFERENT SUB CLASS: ' PA-SUB-CL-CT                         
           DISPLAY 'DIFFERENT DMMS:      ' PA-DMM-CT                            
           DISPLAY 'SKUS FOUND:          ' PA-SKU-CT                            
           DISPLAY 'VENDOR STYLES FOUND: ' PA-VND-STYL-CT                       
           DISPLAY ' '.                                                         
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
