       IDENTIFICATION DIVISION.                                         00010002
       PROGRAM-ID.    INKUT005.                                         00020002
       AUTHOR.        PAUL KEBER.                                       00030002
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040002
       DATE-WRITTEN.  01-07-2005.                                       00050002
       DATE-COMPILED.                                                   00060002
                                                                        00061002
      ******************************************************************00062002
      *      INKUT005 - CREATE INVENTORY LEDGER CUTOFF REPORT PROGRAM   00063002
      *                 TABLE UNLOADS CONTROL CARDS                     00064002
      ******************************************************************00065002
      *                                                                 00066002
      *  THIS PROGRAM WILL CREATE A PLATINUM UNLOAD WHERE CLAUSE        00067002
      *  PARTITION NUMBER CONTROL CARD TO BE USED IN UNLOADING          00067102
      *  MLT_MNLY_SUM RECS FOR THE INVENTORY LEDGER CUTOFF REPORT       00067202
      *  PROGRAM.  IT WILL ALSO A PLATINUM UNLOAD WHERE CLAUSE MONTH    00067302
      *  AND DATE CONTROL CARD TO BE USED IN UNLOADING TINSCLS RECS     00067402
      *  FOR THE INVENTORY LEDGER CUTOFF REPORT PROGRAM.                00067502
      *                                                                 00067602
      * CHANGE HISTORY:                                                 00067702
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00067802
      * ------- ---------- ----------- -------------------------------- 00067902
      * WR20445 01-07-2005 PAUL KEBER  INITIAL CREATION                 00068003
W35378* W35378  04-15-2009 T KONTAROVICH BMC TO DSNTIAUL CONVERSION             
W35378*                    CHANGE 'PART' TO 'WHERE PARTN_NBR IN' CLAUSE.        
      * WR33304 10-13-2008 D.THEEL     HILO CHANGES                     00068004
      *                                ADDED OUTPUT3 AND THE CREATION OF00068005
      *                                THE CUTOFF CONTROL FILE. ADDED   00068006
      *                                SELECT OF OPEN FISCAL WEEK TO    00068007
      *                                7000 TO POPULATE CUTOFF          00068008
      *                                TIMESTAMP.  WE WILL UNLOAD FROM  00068009
      *                                INT_SUB_CL_INV_LDGR INSTEAD OF   00068010
      *                                TINSCLS.                         00068020
W34557* WR34557 09-13-2011 KARTHIKEYAN M THE PROGRAM IS MODIFIED TO             
W34557*         ALLOW CUT OVER OF STORES TAKING INVENTORY COUNT DURING          
W34557*         CLOSING WEEK. ALTERNATE DATE CONTROL CARD IS ADDED FOR          
W34557*         LOOKING AHEAD INTO THE NEXT WEEK DURING CLOSE WEEK.             
W34557*         WHEN DOING THE CUTOVER FOR STORES COUNTING DURING CLOSE         
W34557*         WEEK, THE ALTERNATE DATE CONTROL CARD WILL CONTAIN THE          
W34557*         MONTH END OF THE NEW ACCOUNTING MONTH, NOT THE MONTH            
W34557*         END OF THE CLOSING MONTH. IN OTHER WEEKS, THE MONTH DATE        
W34557*         IS THE SAME FOR MLCNTL AND THE ALTERNATE DATE CONTROL           
W34557*         CARD. THE MONTH END DATE IS USED IN THE UNLOAD CARDS            
W34557*         FOR TABLE INT_SUB_CL_INV_LDGR CREATED BY THE PROGRAM.           
W34557*         THE UNLOAD INFORMATION FOR THE MLT_MNLY_SUM CONTINUES           
W34557*         TO USE THE TMLCNTL INFORMATION.                                 
W34557*                                                                         
W34557* PLEASE SEE INRD001 FOR EXAMPLES OF THE ALTERNATE DATE CARD              
W34557* AND ITS VALUES                                                          
W34557*                                                                         
      ******************************************************************00069002
      *                                                                 00070002
       ENVIRONMENT DIVISION.                                            00080002
       CONFIGURATION SECTION.                                           00090002
       SOURCE-COMPUTER.        IBM-3090.                                00100002
       OBJECT-COMPUTER.        IBM-3090.                                00110002
       INPUT-OUTPUT SECTION.                                            00120002
       FILE-CONTROL.                                                    00130002
                                                                        00140002
W34557     SELECT ALTERNATE-DATE-CNTL-FILE                                      
W34557         ASSIGN TO INP01.                                                 
W34557                                                                          
           SELECT MLTMNLYSUM-PARTITION-FILE                             00150002
               ASSIGN TO OUT01.                                         00160002
                                                                        00170002
W33304     SELECT INV-SCL-DATE-CNTL-FILE                                00180002
               ASSIGN TO OUT02.                                         00190002
                                                                        00200002
W33304     SELECT INV-SCL-CUTOFF-CNTL-FILE                              00200003
W33304         ASSIGN TO OUT03.                                         00200004
                                                                        00200005
       DATA DIVISION.                                                   00210002
       FILE SECTION.                                                    00220002
                                                                        00230002
       FD  MLTMNLYSUM-PARTITION-FILE                                    00240002
           RECORDING MODE IS F                                          00250002
           LABEL RECORDS ARE STANDARD                                   00260002
           BLOCK CONTAINS 0 RECORDS.                                    00270002
                                                                        00280002
       01  MLTMNLYSUM-PARTITION-RECORD PIC X(80).                       00290002
      *                                                                 00300002
W33304 FD  INV-SCL-DATE-CNTL-FILE                                       00310002
           RECORDING MODE IS F                                          00320002
           LABEL RECORDS ARE STANDARD                                   00330002
           BLOCK CONTAINS 0 RECORDS.                                    00340002
                                                                        00350002
W33304 01  INV-SCL-DATE-CNTL-RECORD    PIC X(80).                       00360002
      *                                                                 00370002
W33304 FD  INV-SCL-CUTOFF-CNTL-FILE                                     00370003
W33304     RECORDING MODE IS F                                          00370004
W33304     LABEL RECORDS ARE STANDARD                                   00370005
W33304     BLOCK CONTAINS 0 RECORDS.                                    00370006
                                                                        00370007
W33304 01  INV-SCL-CUTOFF-CNTL-RECORD  PIC X(80).                       00370008
W34557                                                                          
W34557 FD  ALTERNATE-DATE-CNTL-FILE                                             
W34557     LABEL RECORDS ARE STANDARD                                           
W34557     RECORDING MODE F                                                     
W34557     BLOCK CONTAINS 0 RECORDS.                                            
W34557                                                                          
W34557 01  ALTERNATE-DATE-CNTL-REC       PIC X(80).                             
W34557                                                                          
      ******************************************************************00380002
       WORKING-STORAGE SECTION.                                         00390002
      ******************************************************************00400002
                                                                        00410002
       01  PC-PROGRAM-COUNTERS.                                         00420002
           05  PC-MAX-RETRIES          PIC S9(4)   VALUE +2 COMP.       00430002
                                                                        00440002
       01  PS-PROGRAM-SWITCHES.                                         00450002
           05  PS-PARTN-NBR-DONE-SW      PIC X(01)   VALUE 'N'.         00460002
               88  PS-PARTN-NBR-COMPLETE             VALUE 'Y'.         00470002
           05  PS-FIRST-TIME-SW          PIC X(01)   VALUE 'N'.         00480002
               88  PS-FIRST-TIME-THRU                VALUE 'N'.         00490002
               88  PS-CONTINUED-PROCESSING           VALUE 'Y'.         00500002
                                                                        00510002
       01  PROGRAM-VARIABLES.                                           00520002
           05  PV-RETRY-COUNT          PIC S9(4)   VALUE +0 COMP.       00530002
           05  PV-PARTN-NBR-COUNT      PIC S9(5)   VALUE +0 COMP-3.     00540002
                                                                        00550002
       01  MLTMNLYSUM-PART-VERBIAGE-1.                                  00560002
           05  FILLER                  PIC X(22)   VALUE                00570002
W35378         '  WHERE PARTN_NBR IN ('.                                        
           05  FILLER                  PIC X(58)   VALUE SPACES.        00590002
                                                                        00600002
       01  MLTMNLYSUM-PART-VERBIAGE-2.                                  00610002
           05  FILLER                  PIC X(04)   VALUE SPACES.        00620002
           05  MLTMNLYSUMM-COMMA       PIC X(01)   VALUE SPACES.        00630002
           05  MLTMNLYSUM-PARTN-NBR    PIC Z99     VALUE ZEROES.        00640002
           05  FILLER                  PIC X(72)   VALUE SPACES.        00650002
                                                                        00660002
W33304 01  PV-INV-SCL-DATE-CNTL-RECORD.                                 00670002
           05  FILLER                  PIC X(23)   VALUE                00680002
W33304         '    AND FSCL_MN_NBR = '''.                              00690002
W33304     05  PV-INV-SCL-FSCL-MN-NBR  PIC X(02)   VALUE SPACES.        00700002
           05  FILLER                  PIC X(06)   VALUE ''' AND '.     00710002
           05  FILLER                  PIC X(19)   VALUE                00720002
               'FSCL_MN_END_DTE = '''.                                  00730002
W33304     05  PV-INV-SCL-MN-END-DATE  PIC X(10)   VALUE SPACES.        00740002
W33304     05  FILLER                  PIC X(01)   VALUE "'".           00740003
W33304     05  FILLER                  PIC X(18)   VALUE SPACES.        00760002
W33304 01  PV-INV-SCL-CUTOFF-CNTL-RECORD.                               00760003
W33304     05  FILLER                  PIC X(32)   VALUE                00760004
W33304         '    AND B.ACTL_LDG_CTOF_TMST = '''.                     00760006
W33304     05  PV-CUTOFF-TIMESTAMP     PIC X(26)   VALUE SPACES.        00760007
W33304     05  FILLER                  PIC X(02)   VALUE ''';'.         00760020
W33304     05  FILLER                  PIC X(20)   VALUE SPACES.        00760030
                                                                        00770002
                                                                        00780002
       01 ABEND-AREAS.                                                  00790002
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00800002
                                                   COMP SYNC.           00810002
               88  AC-BAD-DATA                     VALUE +4003.         00820002
               88  AC-DB2-ERROR                    VALUE +4013.         00830002
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00840002
                 '*****       ABEND'.                                   00850002
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00860002
                   '*****   PROGRAM: INKUT005'.                         00870002
           05  AA-PARAGRAPH-LIT.                                        00880002
               10  FILLER              PIC X(17)   VALUE                00890002
                   '***** PARAGRAPH: '.                                 00900002
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00910002
           05  AA-MESSAGE-LINE.                                         00920002
               10  FILLER              PIC X(06)   VALUE '*****'.       00930002
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00940002
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00950002
                   '*****    DB2 ERROR'.                                00960002
           05  AA-DB2-OPERATION-LIT.                                    00970002
               10  FILLER              PIC X(17)   VALUE                00980002
                   '***** OPERATION: '.                                 00990002
               10  AA-DB2-OPERATION.                                    01000002
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        01010002
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        01020002
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        01030002
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        01040002
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        01050002
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        01060002
                                                                        01070002
      ******************************************************************01080002
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01090002
      ******************************************************************01100002
           COPY DPWS004.                                                01110002
                                                                        01120002
      ******************************************************************01130002
      *  DB2 ERRORS                                                     01140002
      ******************************************************************01150002
           COPY SQLCA2.                                                 01160002
                                                                        01170002
      ******************************************************************01180002
      *  DB2 MLCNTL TABLE LAYOUT                                        01190002
      ******************************************************************01200002
           EXEC SQL                                                     01210002
                INCLUDE TMLCNTL                                         01220002
           END-EXEC.                                                    01230002
                                                                        01240002
      ******************************************************************01250002
      *  DB2 - M/L DETAIL LEDGER PARTITION BALANCE TBL (MLT_PARTN_BAL)  01260002
      ******************************************************************01270002
           EXEC SQL                                                     01280002
               INCLUDE MLT00002                                         01290002
           END-EXEC.                                                    01300002
                                                                        01310002
      ******************************************************************01320002
      *  DB2 - M/L DETAIL LEDGER PARTITION CONTROL TBL (MLT_PARTN_CNTL) 01330002
      ******************************************************************01340002
           EXEC SQL                                                     01350002
               INCLUDE MLT00003                                         01360002
           END-EXEC.                                                    01370002
                                                                        01380002
      ******************************************************************01390002
      *  DB2 COMMUNICATION AREA                                         01400002
      ******************************************************************01410002
           EXEC SQL                                                     01420002
                INCLUDE SQLCA                                           01430002
           END-EXEC.                                                    01440002
                                                                        01450002
W34557*****************************************************************         
W34557*  DB2 DATE ATTRIBUTES                                                    
W34557*****************************************************************         
W34557                                                                          
W34557     EXEC SQL                                                             
W34557         INCLUDE TDTATTR                                                  
W34557     END-EXEC.                                                            
W34557                                                                          
W34557*****************************************************************         
W34557*  COPYBOOK OF INPUT ALTERNATE DATE CONTROL CARD RECORD LAYOUT  *         
W34557*****************************************************************         
W34557*                                                                         
W34557     COPY INRD001.                                                        
W34557                                                                          
W34557*****************************************************************         
W34557*  COPYBOOK OF FIELDS USED IN THE EDIT / VALIDATE PROCESS.      *         
W34557*****************************************************************         
W34557                                                                          
W34557     COPY INWS050.                                                        
W34557                                                                          
      ******************************************************************01460002
      *  DB2 - M/L DETAIL LEDGER PARTITION CURSOR.                      01470002
      ******************************************************************01480002
           EXEC SQL                                                     01490002
             DECLARE PARTN-NBR-CSR CURSOR FOR                           01500002
               SELECT DISTINCT PARTN_NBR                                01510002
               FROM MLT_PARTN_CNTL A,                                   01520002
                    MLT_PARTN_BAL B                                     01530002
               WHERE A.TBL_TM_LVL_CDE = 'M'                             01540002
                 AND A.FSCL_DTE       = :MLCNTL-OPN-FSCL-MN-DTE         01550002
                 AND B.TBL_TM_LVL_CDE = A.TBL_TM_LVL_CDE                01560002
                 AND B.EFF_BEG_DTE   <= :MLCNTL-OPN-FSCL-MN-DTE         01570002
                 AND B.EFF_END_DTE   >= :MLCNTL-OPN-FSCL-MN-DTE         01580002
                 AND B.DEPT_GP_CDE    = A.DEPT_GP_CDE                   01590002
           END-EXEC.                                                    01600002
                                                                        01610002
      /*****************************************************************01620002
       PROCEDURE DIVISION.                                              01630002
      ******************************************************************01640002
       0100-MAINLINE.                                                   01650002
      *                                                                 01660002
           DISPLAY '********************************'.                  01660104
           DISPLAY 'INKUT005 PROGRAM RUN INFORMATION'                   01660204
           DISPLAY '********************************'.                  01660304
                                                                        01660404
W34557     PERFORM 1000-INIT.                                                   
W34557                                                                          
W33304*** DETERMINE THE FISCAL MONTH AND FISCAL WEEK TO BE PROCESSED....01661002
           PERFORM 7000-SELECT-TMLCNTL.                                 01662002
W33304     STRING MLCNTL-OPN-FSCL-WK-DTE DELIMITED BY SIZE              01662003
W33304      '-23.59.59.999999' DELIMITED BY SIZE                        01662004
W33304        INTO PV-CUTOFF-TIMESTAMP.                                 01662005
                                                                        01663002
           DISPLAY ' '.                                                 01664002
W34557                                                                  01665002
W34557     DISPLAY '*** ML CONTROL OPEN FISCAL MONTH DATE IS:  '        01665002
W34557                  MLCNTL-OPN-FSCL-MN-DTE.                         01666002
W34557     DISPLAY '*** ML CONTROL OPEN FISCAL WEEK DATE IS:  '         01665002
W34557                  MLCNTL-OPN-FSCL-WK-DTE.                         01666002
W34557     DISPLAY '*** ML CONTROL OPEN FISCAL MONTH NUMBER IS:  '      01668002
W34557                  MLCNTL-OPN-FSCL-MN-NBR.                         01669002
W34557                                                                  01667002
W34557     DISPLAY '*** ALT. DATE CONTROL OPEN FISCAL MONTH DATE IS:  ' 01665002
W34557                  IN001-OPN-FSCL-MN-DTE.                          01666002
W34557     DISPLAY '*** ALT. DATE CONTROL OPEN FISCAL WEEK DATE IS:  '  01665002
W34557                  IN001-OPN-FSCL-WK-DTE.                          01666002
W34557     DISPLAY '*** ALT. DATE CONTROL OPEN FISCAL MONTH NUMBER IS: '01668002
W34557                  IN001-OPN-FSCL-MN-NBR.                          01669002
W34557                                                                  01670002
W33304     DISPLAY 'CUTOFF TIMESTAMP = ' PV-CUTOFF-TIMESTAMP.           01670003
      *** OPEN FILES....                                                01690002
           OPEN OUTPUT MLTMNLYSUM-PARTITION-FILE                        01700002
W33304                 INV-SCL-DATE-CNTL-FILE                           01710002
W33304                 INV-SCL-CUTOFF-CNTL-FILE.                        01710003
                                                                        01720002
      *** DETERMINE THE FISCAL PERIOD'S PARTITION NUMBERS FOR THE TABLE 01730002
      *** MLT_MNLY_SUM....                                              01740002
           MOVE ZEROS          TO PV-PARTN-NBR-COUNT.                   01750002
           MOVE 'N'            TO PS-PARTN-NBR-DONE-SW.                 01760002
                                                                        01770002
           PERFORM 7100-OPEN-PARTN-NBR-CSR.                             01780002
           PERFORM 7200-FETCH-PARTN-NBR-CSR.                            01790002
                                                                        01800002
           PERFORM 2000-BUILD-PARTN-NBR                                 01810002
               UNTIL PS-PARTN-NBR-COMPLETE.                             01820002
                                                                        01830002
           PERFORM 7300-CLOSE-PARTN-NBR-CSR.                            01840002
                                                                        01850002
           IF PV-PARTN-NBR-COUNT IS EQUAL ZERO                          01860002
               DISPLAY AA-ABEND-LIT                                     01870002
               DISPLAY AA-PROGRAM-LIT                                   01880002
               DISPLAY 'NO PARTN-NBR-CSR CURSOR PARTITIONS FOUND'       01890002
               SET AC-BAD-DATA TO TRUE                                  01900002
               CALL 'ILBOABN0' USING ABEND-CODE                         01910002
           END-IF.                                                      01920002
                                                                        01930002
W33304*** INT_SUB_CL_INV_LDGR FISCAL PERIOD SETUP...                    01940002
W34557*W33304     MOVE MLCNTL-OPN-FSCL-MN-NBR TO PV-INV-SCL-FSCL-MN-NBR 01950002
W34557*W33304     MOVE MLCNTL-OPN-FSCL-MN-DTE TO PV-INV-SCL-MN-END-DATE.01960002
                                                                        01970002
W34557     MOVE IN001-OPN-FSCL-MN-NBR  TO PV-INV-SCL-FSCL-MN-NBR.       01950002
W34557     MOVE IN001-OPN-FSCL-MN-DTE  TO PV-INV-SCL-MN-END-DATE.       01960002
W34557                                                                          
W33304     WRITE INV-SCL-DATE-CNTL-RECORD                               01980002
W33304         FROM  PV-INV-SCL-DATE-CNTL-RECORD.                       01990002
W33304     WRITE INV-SCL-CUTOFF-CNTL-RECORD                             01990003
W33304         FROM  PV-INV-SCL-CUTOFF-CNTL-RECORD.                     01990004
                                                                        02000002
      *** CLOSE FILES....                                               02010002
           CLOSE MLTMNLYSUM-PARTITION-FILE                              02020002
W33304           INV-SCL-DATE-CNTL-FILE                                 02030002
W33304           INV-SCL-CUTOFF-CNTL-FILE.                              02030003
W34557                                                                          
W34557     DISPLAY '******************************************'.                
W34557     DISPLAY '************INKUT005 ENDING***************'.                
W34557     DISPLAY '******************************************'.                
                                                                        02040002
           STOP RUN.                                                    02050002
                                                                        02060002
W34557*****************************************************************         
W34557* 1000-INIT.                                                              
W34557* PERFORMS DATE VALIDATION FOR ALTERNATE DATE CONTROL FILE                
W34557*                                                                         
W34557*****************************************************************         
W34557 1000-INIT.                                                               
W34557                                                                          
W34557     DISPLAY '******************************************'.                
W34557     DISPLAY '************INKUT005 STARTING*************'.                
W34557     DISPLAY '******************************************'.                
W34557                                                                          
W34557     OPEN INPUT ALTERNATE-DATE-CNTL-FILE.                                 
W34557                                                                          
W34557     INITIALIZE IN001-RECORD.                                             
W34557                                                                          
W34557     MOVE 'INKUT005'                  TO PV-IN050-PGM-NUMBER              
W34557     PERFORM 1100-EDIT-VALIDATE-CNTL.                                     
W34557                                                                          
W34557     CLOSE ALTERNATE-DATE-CNTL-FILE.                                      
W34557                                                                          
      /*****************************************************************02070002
      * 2000-BUILD-PARTN-NBR.                                           02080002
      *     THE ROUTINE BUILDS THE SELECT STATMENT FOR MLT_MNLY_SUM. THE02090002
      * TABLE IS SETUP TO USE PARTITION NUMBER. THE ROUTINE BUILDS THE  02100002
      * PARTITION NUMBER SELECT STATMENTS FOR THE OPEN FISCAL MONTH...  02110002
      ******************************************************************02120002
       2000-BUILD-PARTN-NBR.                                            02130002
                                                                        02140002
           IF PS-FIRST-TIME-THRU                                        02150002
               WRITE MLTMNLYSUM-PARTITION-RECORD                        02160002
                                    FROM MLTMNLYSUM-PART-VERBIAGE-1     02170002
               MOVE 'Y'             TO PS-FIRST-TIME-SW                 02180002
           END-IF.                                                      02181002
                                                                        02182002
           IF PV-PARTN-NBR-COUNT IS GREATER THAN ZERO                   02182102
               MOVE ','             TO MLTMNLYSUMM-COMMA                02182202
           ELSE                                                         02182302
               MOVE SPACE           TO MLTMNLYSUMM-COMMA                02182402
           END-IF.                                                      02182502
                                                                        02182602
           MOVE MLT00003-PARTN-NBR  TO MLTMNLYSUM-PARTN-NBR             02182702
                                                                        02182802
           WRITE MLTMNLYSUM-PARTITION-RECORD                            02182902
                                    FROM MLTMNLYSUM-PART-VERBIAGE-2.    02183002
                                                                        02183102
           ADD 1                    TO PV-PARTN-NBR-COUNT.              02183202
                                                                        02183302
           PERFORM 7200-FETCH-PARTN-NBR-CSR.                            02183402
                                                                        02183502
      ******************************************************************02183602
       7000-SELECT-TMLCNTL.                                             02183702
      ******************************************************************02183802
                                                                        02183902
           PERFORM                                                      02184002
               WITH TEST AFTER                                          02185002
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       02186002
                 UNTIL SQLCODE = +000                                   02187002
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  02188002
                                                                        02189002
               EXEC SQL                                                 02189102
                    SELECT                                              02189202
                        OPN_FSCL_MN_DTE                                 02189302
                       ,OPN_FSCL_MN_NBR                                 02189402
W33304                 ,OPN_FSCL_WK_DTE                                 02189403
                    INTO                                                02189502
                       :MLCNTL-OPN-FSCL-MN-DTE                          02189602
                      ,:MLCNTL-OPN-FSCL-MN-NBR                          02189702
W33304                ,:MLCNTL-OPN-FSCL-WK-DTE                          02189703
                    FROM                                                02189802
                        TMLCNTL                                         02189902
                    WITH UR                                             02190002
               END-EXEC                                                 02190102
                                                                        02190202
               EVALUATE TRUE                                            02190302
                   WHEN SQLCODE        = +000                           02190402
                   WHEN SQLCODE        = -911                           02190502
                   WHEN SQLCODE        = -913                           02190602
                       CONTINUE                                         02190702
                   WHEN OTHER                                           02190802
                       MOVE '7000-SELECT-TMLCNTL'                       02190902
                                                TO AA-PARAGRAPH-NAME    02191002
                       MOVE 'TMLCNTL ROW NOT FOUND OR ERROR OCCURRED'   02191102
                                                TO AA-MESSAGE           02191202
                       MOVE 'SELECT ROW FROM TMLCNTL'                   02191302
                                                TO AA-DB2-OPERATION     02191402
                       PERFORM 9901-SQL-ERROR                           02191502
               END-EVALUATE                                             02191602
                                                                        02191702
           END-PERFORM.                                                 02191802
                                                                        02191902
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02192002
               MOVE '7000-SELECT-TMLCNTL'       TO AA-PARAGRAPH-NAME    02193002
               MOVE 'MAX RETRIES EXCEEDED ON TMLCNTL'                   02193102
                                                TO AA-MESSAGE           02193202
               MOVE 'SELECT ROW FROM TMLCNTL'   TO AA-DB2-OPERATION     02193302
               PERFORM 9901-SQL-ERROR                                   02193402
           END-IF.                                                      02193502
                                                                        02193602
      ***************************************************************** 02193702
      * 7100-OPEN-PARTN-NBR-CSR.                                        02193802
      ***************************************************************** 02193902
       7100-OPEN-PARTN-NBR-CSR.                                         02194002
                                                                        02195002
           PERFORM                                                      02196002
               WITH TEST AFTER                                          02197002
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       02198002
                 UNTIL SQLCODE = +000                                   02199002
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  02200002
                                                                        02210002
               EXEC SQL                                                 02220002
                   OPEN PARTN-NBR-CSR                                   02230002
               END-EXEC                                                 02240002
                                                                        02250002
               EVALUATE TRUE                                            02260002
                   WHEN SQLCODE        = +000                           02270002
                   WHEN SQLCODE        = -911                           02280002
                   WHEN SQLCODE        = -913                           02290002
                       CONTINUE                                         02300002
                   WHEN OTHER                                           02310002
                       MOVE '7100-OPEN-PARTN-NBR-CSR'                   02320002
                                                TO AA-PARAGRAPH-NAME    02330002
                       MOVE 'PARTN-NBR-CSR OPEN CURSOR ERROR OCCURRED'  02340002
                                                TO AA-MESSAGE           02350002
                       MOVE 'OPEN PARTITION NUMBER CURSOR'              02360002
                                                TO AA-DB2-OPERATION     02370002
                       PERFORM 9901-SQL-ERROR                           02380002
               END-EVALUATE                                             02390002
                                                                        02400002
           END-PERFORM.                                                 02410002
                                                                        02420002
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02430002
               MOVE '7100-OPEN-PARTN-NBR-CSR'   TO AA-PARAGRAPH-NAME    02440002
               MOVE 'PARTN-NBR-CSR OPEN CURSOR RETRIES EXCEEDED'        02450002
                                                TO AA-MESSAGE           02460002
               MOVE 'OPEN PARTITION NUMBER CURSOR'                      02470002
                                                TO AA-DB2-OPERATION     02480002
               PERFORM 9901-SQL-ERROR                                   02490002
           END-IF.                                                      02500002
                                                                        02510002
      ***************************************************************** 02520002
      * 7200-FETCH-PARTN-NBR-CSR.                                       02530002
      ***************************************************************** 02540002
        7200-FETCH-PARTN-NBR-CSR.                                       02550002
                                                                        02560002
           PERFORM                                                      02570002
               WITH TEST AFTER                                          02580002
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       02590002
                 UNTIL SQLCODE = +000                                   02600002
                    OR SQLCODE = +100                                   02610002
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  02620002
                                                                        02630002
               EXEC SQL                                                 02640002
                   FETCH PARTN-NBR-CSR                                  02650002
                   INTO :MLT00003-PARTN-NBR                             02651002
               END-EXEC                                                 02652002
                                                                        02653002
               EVALUATE TRUE                                            02654002
                   WHEN SQLCODE        = +000                           02655002
                       CONTINUE                                         02655102
                   WHEN SQLCODE = +100                                  02655202
                       SET PS-PARTN-NBR-COMPLETE TO TRUE                02655302
                   WHEN SQLCODE        = -911                           02655402
                   WHEN SQLCODE        = -913                           02655502
                       CONTINUE                                         02655602
                   WHEN OTHER                                           02655702
                       MOVE '7200-FETCH-PARTN-NBR-CSR'                  02655802
                                                TO AA-PARAGRAPH-NAME    02655902
                       MOVE 'PARTN-NBR-CSR FETCH CURSOR ERROR OCCURRED' 02656002
                                                TO AA-MESSAGE           02656102
                       MOVE 'FETCH PARTITION NUMBER CURSOR'             02656202
                                                TO AA-DB2-OPERATION     02656302
                       PERFORM 9901-SQL-ERROR                           02656402
               END-EVALUATE                                             02656502
                                                                        02656602
           END-PERFORM.                                                 02656702
                                                                        02656802
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02656902
               MOVE '7200-FETCH-PARTN-NBR-CSR'  TO AA-PARAGRAPH-NAME    02657002
               MOVE 'PARTN-NBR-CSR FETCH CURSOR RETRIES EXCEEDED'       02657102
                                                TO AA-MESSAGE           02657202
               MOVE 'FETCH PARTITION NUMBER CURSOR'                     02657302
                                                TO AA-DB2-OPERATION     02657402
               PERFORM 9901-SQL-ERROR                                   02657502
           END-IF.                                                      02657602
                                                                        02657702
      ***************************************************************** 02657802
      * 7300-CLOSE-PARTN-NBR-CSR.                                       02657902
      ***************************************************************** 02658002
        7300-CLOSE-PARTN-NBR-CSR.                                       02659002
                                                                        02659102
           PERFORM                                                      02659202
               WITH TEST AFTER                                          02659302
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       02659402
                 UNTIL SQLCODE = +000                                   02659502
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  02659602
                                                                        02659702
               EXEC SQL                                                 02659802
                   CLOSE PARTN-NBR-CSR                                  02659902
               END-EXEC                                                 02660002
                                                                        02660102
               EVALUATE TRUE                                            02660202
                   WHEN SQLCODE        = +000                           02660302
                   WHEN SQLCODE        = -911                           02660402
                   WHEN SQLCODE        = -913                           02660502
                       CONTINUE                                         02660602
                   WHEN OTHER                                           02660702
                       MOVE '7300-CLOSE-PARTN-NBR-CSR'                  02660802
                                                TO AA-PARAGRAPH-NAME    02660902
                       MOVE 'PARTN-NBR-CSR CLOSE CURSOR ERROR OCCURRED' 02661002
                                                TO AA-MESSAGE           02661102
                       MOVE 'CLOSE PARTITION NUMBER CURSOR'             02661202
                                                TO AA-DB2-OPERATION     02661302
                       PERFORM 9901-SQL-ERROR                           02661402
               END-EVALUATE                                             02661502
                                                                        02661602
           END-PERFORM.                                                 02661702
                                                                        02661802
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02661902
               MOVE '7300-CLOSE-PARTN-NBR-CSR'  TO AA-PARAGRAPH-NAME    02662002
               MOVE 'PARTN-NBR-CSR CLOSE CURSOR RETRIES EXCEEDED'       02662102
                                                TO AA-MESSAGE           02662202
               MOVE 'CLOSE PARTITION NUMBER CURSOR'                     02662302
                                                TO AA-DB2-OPERATION     02662402
               PERFORM 9901-SQL-ERROR                                   02662502
           END-IF.                                                      02662602
                                                                        02662702
      ******************************************************************02662802
       9901-SQL-ERROR.                                                  02662902
      ******************************************************************02663002
                                                                        02663102
                                                                        02663202
           DISPLAY AA-ABEND-LIT.                                        02663302
           DISPLAY AA-DB2-ERROR-LIT.                                    02663402
           DISPLAY AA-PROGRAM-LIT.                                      02663502
           DISPLAY AA-PARAGRAPH-LIT.                                    02663602
           DISPLAY AA-MESSAGE-LINE.                                     02663702
           DISPLAY AA-DB2-OPERATION-LIT.                                02663802
           SET AC-DB2-ERROR TO TRUE.                                    02663902
           MOVE +4013                  TO ABEND-CODE.                   02664002
                                                                        02665002
           COPY DPPD004.                                                02666002
           CALL 'ILBOABN0' USING ABEND-CODE.                            02667002
W34557*****************************************************************         
W34557*  COPYBOOK OF DATE EDIT LOGIC FOR INPUT ALTERNATE DATES        *         
W34557*****************************************************************         
W34557     COPY INPD050.                                                        
W34557*****************************************************************         
