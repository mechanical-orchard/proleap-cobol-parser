       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      PXKRP126.                                       00020000
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    06/18/2012.                                     00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **NEW STORE YELLOW TICKET PRICE CHANGE SUMMARY REPORT             00080000
      ******************************************************************00090000
      *DATE: 06/18/2012 CHANGED BY: DAVID WELLER                        00100000
      *      INITIAL DESIGN                                             00110000
      ******************************************************************00120000
       ENVIRONMENT DIVISION.                                            00130000
       CONFIGURATION SECTION.                                           00140000
       SOURCE-COMPUTER.        IBM-3090.                                00150000
       OBJECT-COMPUTER.        IBM-3090.                                00160000
       INPUT-OUTPUT SECTION.                                            00170000
       FILE-CONTROL.                                                    00180000
                                                                        00190000
           SELECT INPUT-FILE                                            00200000
                  ASSIGN TO INP01.                                      00210000
           SELECT REPORT-FILE                                           00220000
                  ASSIGN TO OUT01.                                      00230000
                                                                        00240000
       DATA DIVISION.                                                   00250000
       FILE SECTION.                                                    00260000
                                                                        00270000
       FD  INPUT-FILE                                                   00280000
           RECORDING MODE F                                             00290000
           BLOCK CONTAINS 0 RECORDS                                     00300000
           LABEL RECORDS ARE OMITTED                                    00310000
           DATA RECORD IS INPUT-RECORD.                                 00320000
       01  INPUT-RECORD                    PIC X(250).                  00330000
                                                                        00340000
       FD  REPORT-FILE                                                  00350000
           RECORDING MODE F                                             00360000
           BLOCK CONTAINS 0 RECORDS                                     00370000
           LABEL RECORDS ARE OMITTED                                    00380000
           DATA RECORD IS REPORT-RECORD.                                00390000
       01  REPORT-RECORD                   PIC X(133).                  00400000
                                                                        00410000
       WORKING-STORAGE SECTION.                                         00420000
      ******************************************************************00430000
      **SWITCHES                                                        00440000
      ******************************************************************00450000
       01  PS-PROGRAM-SWITCHES.                                         00460000
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)   VALUE 'N'.   00470000
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.   00480000
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.   00490000
      ******************************************************************00500000
      **PROGRAM HOLDS                                                   00510000
      ******************************************************************00520000
       01  PH-PROGRAM-HOLDS-1.                                          00530000
           05  PH-STR-NBR                    PIC S9(04) COMP            00540000
                                                        VALUE ZEROS.    00550000
           05  PH-STR-TOTAL-QTY              PIC  9(09) VALUE ZEROS.    00560000
      ******************************************************************00570000
      **TIMESTAMP VARIABLES                                             00580000
      ******************************************************************00590000
       01 DF-DATE-TIME-FIELDS.                                          00600000
          05 DF-ACCEPT-DATE.                                            00610000
             10  DF-ACCEPT-YR                  PIC 9(02) VALUE ZEROS.   00620000
             10  DF-ACCEPT-MO                  PIC 9(02) VALUE ZEROS.   00630000
             10  DF-ACCEPT-DY                  PIC 9(02) VALUE ZEROS.   00640000
          05 DF-ACCEPT-TIME.                                            00650000
             10  DF-ACCEPT-HR                  PIC 9(02) VALUE ZEROS.   00660000
             10  DF-ACCEPT-MN                  PIC 9(02) VALUE ZEROS.   00670000
             10  DF-ACCEPT-SS                  PIC 9(04) VALUE ZEROS.   00680000
          05 DF-FORMAT-DATE.                                            00690000
             10  DF-FORMAT-DATE-MM             PIC 9(02) VALUE ZEROS.   00700000
             10  FILLER                        PIC X(01) VALUE '/'.     00710000
             10  DF-FORMAT-DATE-DD             PIC 9(02) VALUE ZEROS.   00720000
             10  FILLER                        PIC X(01) VALUE '/'.     00730000
             10  DF-FORMAT-DATE-YY             PIC 9(02) VALUE ZEROS.   00740000
      ******************************************************************00750000
      **PROGRAM CONSTANTS                                               00760000
      ******************************************************************00770000
       01  PC-PROGRAM-CONSTANTS.                                        00780000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                00790000
               VALUE 'PXKRP126'.                                        00800000
      ******************************************************************00810000
      **PROGRAM COUNTERS                                                00820000
      ******************************************************************00830000
       01  PC-PROGRAM-COUNTER.                                          00840000
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROS.   00850000
           05  PC-REPORT-RECORDS-WRITTEN       PIC 9(09) VALUE ZEROS.   00860000
           05  PC-REPORT-LINE-COUNTER          PIC 9(03) VALUE ZEROS.   00870000
           05  PC-PAGE-COUNTER                 PIC 9(03) VALUE ZEROS.   00880000
      ******************************************************************00890000
      **REPORT HEADINGS                                                 00900000
      ******************************************************************00910000
       01  RH-TITLE-1.                                                  00920000
           05  FILLER                          PIC X(45)  VALUE SPACES. 00930000
           05  FILLER                          PIC X(43)                00940000
                   VALUE 'NEW STORE YELLOW TICKET PRICE CHANGE REPORT'. 00950000
           05  FILLER                          PIC X(45)  VALUE SPACES. 00960000
       01  RH-TITLE-2.                                                  00970000
           05  FILLER                          PIC X(55)  VALUE SPACES. 00980000
           05  FILLER                          PIC X(23)                00990000
                 VALUE ' SUMMARY BY DEPARTMENT '.                       01000000
           05  FILLER                          PIC X(55)  VALUE SPACES. 01010000
       01  RH-TITLE-3.                                                  01020000
           05  FILLER                          PIC X(55)  VALUE SPACES. 01030000
           05  FILLER                          PIC X(23)                01040000
                 VALUE '  MARKUPS / MARKDOWNS  '.                       01050000
           05  FILLER                          PIC X(55)  VALUE SPACES. 01060000
       01  RH-HEADINGS-1.                                               01070000
           05  FILLER                          PIC X(02)  VALUE SPACES. 01080000
           05  FILLER                          PIC X(14)                01090000
                             VALUE ' STORE NUMBER:'.                    01100000
           05  RH-HEADINGS-1-STR-NBR           PIC Z9999  VALUE ZEROS.  01110000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01120000
           05  RH-HEADING-1-STR-NAME           PIC X(20)  VALUE SPACES. 01130000
           05  FILLER                          PIC X(90)  VALUE SPACES. 01140000
       01  RH-HEADINGS-2.                                               01150000
           05  FILLER                          PIC X(17)                01160000
                             VALUE ' EFFECTIVE DATE: '.                 01170000
           05  RH-HEADINGS-2-EFF-DATE          PIC X(10)  VALUE SPACES. 01180000
       01  RH-HEADINGS-4.                                               01190000
           05  FILLER                          PIC X(04)                01200000
               VALUE 'DEPT'.                                            01210000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01220000
           05  FILLER                          PIC X(20)                01230000
               VALUE 'DEPT'.                                            01240000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01250000
           05  FILLER                          PIC X(11)                01260000
               VALUE '      TOTAL'.                                     01270000
       01  RH-HEADINGS-5.                                               01280000
           05  FILLER                          PIC X(04)                01290000
               VALUE 'NBR '.                                            01300000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01310000
           05  FILLER                          PIC X(20)                01320000
               VALUE 'NAME'.                                            01330000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01340000
           05  FILLER                          PIC X(11)                01350000
               VALUE '      UNITS'.                                     01360000
       01  RH-HEADINGS-6.                                               01370000
           05  FILLER                          PIC X(04)                01380000
               VALUE '----'.                                            01390000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01400000
           05  FILLER                          PIC X(20)                01410000
               VALUE '--------------------'.                            01420000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01430000
           05  FILLER                          PIC X(11)                01440000
               VALUE '-----------'.                                     01450000
       01  RH-DETAIL-LINE.                                              01460000
           05  RH-DEPT-NBR                     PIC ZZZZ   VALUE ZEROS.  01470000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01480000
           05  RH-DEPT-NM                      PIC X(20)  VALUE SPACES. 01490000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01500000
           05  RH-STR-OH-QTY                   PIC ZZZZZZZZZZ9.         01510000
       01  RH-TOTAL-1.                                                  01520000
           05  FILLER                          PIC X(37)                01530000
               VALUE '-------------------------------------'.           01540000
       01  RH-TOTAL-2.                                                  01550000
           05  FILLER                          PIC X(26)                01560000
               VALUE 'TOTAL STORE UNITS'.                               01570000
           05  RH-STR-TOTAL-QTY                PIC ZZZZZZZZZZ9.         01580000
       01  RH-FOOTERS-1.                                                01590000
           05  FILLER                          PIC X(54)  VALUE SPACES. 01600000
           05  FILLER                          PIC X(19)                01610000
               VALUE '***END OF REPORT***'.                             01620000
      ******************************************************************01630000
      **COPYBOOKS                                                       01640000
      ******************************************************************01650000
      ***INPUT FILE FORMAT                                              01660000
           COPY PXRD142.                                                01670000
      ***STANDARD KOHLS HEADING FOR 132 BYTE REPORT                     01680000
           COPY DPWS132O.                                               01690000
      ******************************************************************01700000
      **ERROR HANDLING                                                  01710000
      ******************************************************************01720000
       01  PV-MISC-ABEND-FIELDS.                                        01730000
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   01740000
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   01750000
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   01760000
                                                                        01770000
       PROCEDURE DIVISION.                                              01780000
      ***************************************************************** 01790000
                                                                        01800000
           PERFORM 1000-INITIALIZATION.                                 01810000
           PERFORM 2000-PROCESS-RECORDS                                 01820000
                   UNTIL PS-END-OF-INPUT-FILE.                          01830000
           PERFORM 9999-WRAPUP.                                         01840000
           GOBACK.                                                      01850000
                                                                        01860000
      ******************************************************************01870000
      **INITIALIZATION AREA                                             01880000
      ******************************************************************01890000
       1000-INITIALIZATION.                                             01900000
                                                                        01910000
           OPEN INPUT  INPUT-FILE                                       01920000
                OUTPUT REPORT-FILE.                                     01930000
                                                                        01940000
      **RETRIEVE DATE FROM SYSTEM                                       01950000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01960000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01970000
                                                                        01980000
           MOVE DF-ACCEPT-YR          TO DF-FORMAT-DATE-YY.             01990000
           MOVE DF-ACCEPT-MO          TO DF-FORMAT-DATE-MM.             02000000
           MOVE DF-ACCEPT-DY          TO DF-FORMAT-DATE-DD.             02010000
                                                                        02020000
           MOVE ZEROS                 TO PH-STR-TOTAL-QTY.              02030000
           PERFORM 2999-READ-INPUT-FILE.                                02040000
                                                                        02050000
           IF  PS-NOT-END-OF-INPUT-FILE                                 02060000
               PERFORM 1100-HEADING-PREP                                02070000
               PERFORM 2100-WRITE-HEADINGS                              02080000
               MOVE PXRD142-STR-NBR      TO PH-STR-NBR                  02090000
           END-IF.                                                      02100000
                                                                        02110000
      ******************************************************************02120000
      **MOVE DATA TO FIELDS IN HEADING FOR REPORT                       02130000
      ******************************************************************02140000
       1100-HEADING-PREP.                                               02150000
                                                                        02160000
      ***PREPS KOHLS STANDARD HEADING                                   02170000
           MOVE PC-CURRENT-PROGRAM-NAME                                 02180000
                                     TO DP132O-PROGRAM-NAME.            02190000
           MOVE DF-FORMAT-DATE       TO DP132O-RUN-DATE.                02200000
           MOVE DF-ACCEPT-HR         TO DP132O-RUN-HOUR.                02210000
           MOVE DF-ACCEPT-MN         TO DP132O-RUN-MINUTE.              02220000
           MOVE 1                    TO DP132O-REPORT-NUMBER.           02230000
                                                                        02240000
      ******************************************************************02250000
      **PROCESSES INPUT FILE                                            02260000
      ******************************************************************02270000
       2000-PROCESS-RECORDS.                                            02280000
                                                                        02290000
           EVALUATE TRUE                                                02300000
              WHEN PXRD142-STR-NBR EQUAL PH-STR-NBR                     02310000
                                                                        02320000
                   IF  PC-REPORT-LINE-COUNTER > 57                      02330000
                       PERFORM 2300-WRITE-FOOTERS                       02340000
                       PERFORM 2100-WRITE-HEADINGS                      02350000
                   END-IF                                               02360000
                                                                        02370000
                   PERFORM 2200-WRITE-DETAIL                            02380000
                                                                        02390000
              WHEN PXRD142-STR-NBR NOT EQUAL PH-STR-NBR                 02400000
                   INITIALIZE PC-PAGE-COUNTER                           02410000
                   PERFORM 2400-WRITE-TOTALS                            02420000
                   PERFORM 2300-WRITE-FOOTERS                           02430000
                   PERFORM 1100-HEADING-PREP                            02440000
                   PERFORM 2100-WRITE-HEADINGS                          02450000
                   PERFORM 2200-WRITE-DETAIL                            02460000
                   MOVE PXRD142-STR-NBR TO PH-STR-NBR                   02470000
           END-EVALUATE.                                                02480000
                                                                        02490000
           PERFORM 2999-READ-INPUT-FILE.                                02500000
           IF  PS-END-OF-INPUT-FILE                                     02510000
               PERFORM 2400-WRITE-TOTALS                                02520000
           END-IF.                                                      02530000
                                                                        02540000
      ***************************************************************** 02550000
      **WRITE REPORT HEADERS                                            02560000
      ***************************************************************** 02570000
       2100-WRITE-HEADINGS.                                             02580000
                                                                        02590000
           INITIALIZE PC-REPORT-LINE-COUNTER.                           02600000
                                                                        02610000
           ADD +1                      TO PC-PAGE-COUNTER.              02620000
           MOVE PC-PAGE-COUNTER        TO DP132O-PAGE-NUMBER.           02630000
                                                                        02640000
           MOVE PXRD142-STR-NBR        TO RH-HEADINGS-1-STR-NBR.        02650000
           MOVE PXRD142-LOC-NM         TO RH-HEADING-1-STR-NAME.        02660000
           MOVE PXRD142-EFF-DATE       TO RH-HEADINGS-2-EFF-DATE.       02670000
                                                                        02680000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      02690000
                               AFTER ADVANCING PAGE.                    02700000
                                                                        02710000
           WRITE REPORT-RECORD FROM RH-TITLE-1 AFTER ADVANCING 1 LINE.  02720000
           WRITE REPORT-RECORD FROM RH-TITLE-2 AFTER ADVANCING 1 LINE.  02730000
           WRITE REPORT-RECORD FROM RH-TITLE-3 AFTER ADVANCING 1 LINE.  02740000
                                                                        02750000
           WRITE REPORT-RECORD FROM RH-HEADINGS-1                       02760000
                               AFTER ADVANCING 2 LINES.                 02770000
           WRITE REPORT-RECORD FROM RH-HEADINGS-2                       02780000
                               AFTER ADVANCING 1 LINES.                 02790000
                                                                        02800000
           WRITE REPORT-RECORD FROM RH-HEADINGS-4                       02810000
                               AFTER ADVANCING 2 LINES.                 02820000
           WRITE REPORT-RECORD FROM RH-HEADINGS-5                       02830000
                               AFTER ADVANCING 1 LINES.                 02840000
           WRITE REPORT-RECORD FROM RH-HEADINGS-6                       02850000
                               AFTER ADVANCING 1 LINES.                 02860000
                                                                        02870000
           ADD +11             TO PC-REPORT-LINE-COUNTER.               02880000
                                                                        02890000
      ***************************************************************** 02900000
      **WRITE DETAIL LINE                                               02910000
      ***************************************************************** 02920000
       2200-WRITE-DETAIL.                                               02930000
                                                                        02940000
           INITIALIZE RH-DETAIL-LINE.                                   02950000
                                                                        02960000
           MOVE PXRD142-DEPT-NBR       TO RH-DEPT-NBR.                  02970000
                                                                        02980000
           MOVE PXRD142-DEPT-NM        TO RH-DEPT-NM.                   02990000
           MOVE PXRD142-STR-OH-QTY     TO RH-STR-OH-QTY.                03000000
                                                                        03010000
           ADD +1                      TO PC-REPORT-LINE-COUNTER.       03020000
           ADD PXRD142-STR-OH-QTY      TO PH-STR-TOTAL-QTY.             03030000
                                                                        03040000
           WRITE REPORT-RECORD FROM RH-DETAIL-LINE.                     03050000
                                                                        03060000
           ADD +1                      TO PC-REPORT-RECORDS-WRITTEN.    03070000
                                                                        03080000
      ***************************************************************** 03090000
      **WRITE FOOTERS                                                   03100000
      ***************************************************************** 03110000
       2300-WRITE-FOOTERS.                                              03120000
                                                                        03130000
           WRITE REPORT-RECORD FROM RH-FOOTERS-1.                       03140000
                                                                        03150000
      ***************************************************************** 03160000
      **WRITE TOTALS                                                    03170000
      ***************************************************************** 03180000
       2400-WRITE-TOTALS.                                               03190000
                                                                        03200000
           MOVE PH-STR-TOTAL-QTY    TO RH-STR-TOTAL-QTY.                03210000
           WRITE REPORT-RECORD FROM RH-TOTAL-1                          03220000
               AFTER ADVANCING 2 LINES.                                 03230000
           WRITE REPORT-RECORD FROM RH-TOTAL-2                          03240000
               AFTER ADVANCING 1 LINES.                                 03250000
           MOVE ZEROS               TO PH-STR-TOTAL-QTY.                03260000
                                                                        03270000
      ***************************************************************** 03280000
      **READ INPUT FILE                                                 03290000
      ***************************************************************** 03300000
       2999-READ-INPUT-FILE.                                            03310000
                                                                        03320000
           READ INPUT-FILE INTO PXRD142-YELLOW-TICKET-REC               03330000
                AT END SET PS-END-OF-INPUT-FILE TO TRUE                 03340000
           END-READ.                                                    03350000
                                                                        03360000
           IF  PS-NOT-END-OF-INPUT-FILE                                 03370000
               ADD +1 TO PC-INPUT-RECORDS-READ                          03380000
           END-IF.                                                      03390000
                                                                        03400000
      ****************************************************************  03410000
      *END OF PROGRAM ROUTINE                                           03420000
      ****************************************************************  03430000
       9999-WRAPUP.                                                     03440000
                                                                        03450000
           IF  PC-REPORT-RECORDS-WRITTEN > 0                            03460000
               PERFORM 2300-WRITE-FOOTERS                               03470000
           END-IF.                                                      03480000
                                                                        03490000
           CLOSE INPUT-FILE                                             03500000
                 REPORT-FILE.                                           03510000
                                                                        03520000
           PERFORM 9999-DISPLAY-STATISTICS.                             03530000
                                                                        03540000
      ****************************************************************  03550000
      *DISPLAY PROGRAM STATISTICS                                       03560000
      ****************************************************************  03570000
       9999-DISPLAY-STATISTICS.                                         03580000
                                                                        03590000
           DISPLAY '*************************************************'. 03600000
           DISPLAY '* PROGRAM...............: ' PC-CURRENT-PROGRAM-NAME.03610000
           DISPLAY '* DATE..................: ' DF-ACCEPT-DATE.         03620000
           DISPLAY '* TIMESTAMP.............: ' DF-ACCEPT-TIME.         03630000
           DISPLAY '*************************************************'. 03640000
           DISPLAY '* INPUT RECORDS READ.........: '                    03650000
                                          PC-INPUT-RECORDS-READ.        03660000
           DISPLAY '* REPORT RECORDS WRITTEN.....: '                    03670000
                                          PC-REPORT-RECORDS-WRITTEN.    03680000
           DISPLAY '*************************************************'. 03690000
                                                                        03700000
