      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
                                                                                
       PROGRAM-ID.             ARKUT131.                                        
       AUTHOR.                 COGNIZANT.                                       
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            JULY 2019.                                       
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  PURPOSE:                                                               
      *  THIS PROGRAM WILL TOKENIZE THE ACCOUNT NUMBER FIELD IN THE             
      *  INPUT FILE.                                                            
      *  PROTEGRITY MODULE WILL BE INVOKED TO TOKENIZE THE ACCOUNT              
      *  NUMBER                                                                 
      *                                                                         
      *  INPUT FILES :   PSTDMON-RDT-FILE          DDNAME = INP01               
      *  OUTPUT FILES:   PSTDMON-RDT-TOKEN-FILE    DDNAME = OUT01               
      *                                                                         
      *****************************************************************         
      *----------------------------------------------------------------*        
      *                                                                *        
      * DATE:        07/24/2019                                        *        
      * WR/IR#:      CHG0230275                                        *        
      * PROGRAMMER:  COGNIZANT                                         *        
      * DESCRIPTION: INITIAL VERSION CREATED FOR FD FILE TRANSFER      *        
      *              PROJECT                                           *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
                                                                                
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT PSTDMON-RDT-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT PSTDMON-RDT-TOKEN-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PSTDMON-RDT-FILE                                                     
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  PSTDMON-RDT-RECORD          PIC X(772).                              
                                                                                
       FD  PSTDMON-RDT-TOKEN-FILE                                               
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  PSTDMON-RDT-TOKEN-RECORD    PIC X(772).                              
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
       01  FILLER                            PIC X(50)  VALUE                   
           ' *** WORKING STORAGE STARTS HERE FOR ARKUT131 *** '.                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW          PIC X(01)  VALUE 'N'.                 
               88  PS-NOT-END-OF-FILE                VALUE 'N'.                 
               88  PS-END-OF-FILE                    VALUE 'Y'.                 
           05  PS-HDR-TRLR-REC-SW         PIC X(01)  VALUE 'N'.                 
               88  PS-NOT-HDR-TRLR-REC               VALUE 'N'.                 
               88  PS-HDR-TRLR-REC                   VALUE 'Y'.                 
           05  PS-ACCT-SSN                PIC X(08)  VALUE SPACES.              
               88  PS-ACCT-NBR                       VALUE 'ACCT NBR'.          
                                                                                
       01  PC-CONSTANTS.                                                        
           05  PC-PROGRAM-NAME               PIC  X(08) VALUE                   
                                                       'ARKUT131'.              
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-READ-COUNT                 PIC  9(09) VALUE ZEROES.           
           05  PV-WRITE-COUNT                PIC  9(09) VALUE ZEROES.           
           05  PV-INPUT-LENGTH               PIC  9(09) VALUE ZEROES.           
           05  PV-OUTPUT-RECORD              PIC  X(772) VALUE SPACES.          
           05  PV-ACCT-NBR                   PIC  X(16) VALUE SPACES.           
                                                                                
       01  ABEND-CODE                        PIC S9(04) VALUE ZERO              
                                                        COMP SYNC.              
                                                                                
      ******************************************************************        
      * RECORD LAYOUT FOR DATALINK ACCOUNT FILE - INPUT AND OUTPUT     *        
      ******************************************************************        
           COPY ARRD131.                                                        
                                                                                
      ******************************************************************        
      * LAYOUT THAT DEFINES THE PARAMETERS NEEDED TO INVOKE PROTEGRITY *        
      * MODULE                                                         *        
      ******************************************************************        
           COPY PTYCCSIP.                                                       
                                                                                
      ******************************************************************        
      * LAYOUT THAT DEFINES THE WORKING VARIABLES TO INVOKE PROTEGRITY *        
      * MODULE                                                         *        
      ******************************************************************        
           COPY DPWS031.                                                        
                                                                                
      ******************************************************************        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * 0000-MAINLINE-PARA                                                      
      *   1. PROCESS THE RECORDS IN THE INPUT FILE                              
      *   2. CALL TOKENIZE PARA TO TOKENIZE RDT-CHD-ACCOUNT-NUMBER              
      *   3. WRITE OUTPUT RECORDS                                               
      ******************************************************************        
       0000-MAINLINE-PARA.                                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-PARA                                            
                 UNTIL PS-END-OF-FILE.                                          
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZE                                                         
      *     INITIALIZE THE VARIABLES, OPEN THE INPUT AND OUTPUT FILES           
      *     AND READ THE INPUT FILE                                             
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           DISPLAY 'PROGRAM - ' PC-PROGRAM-NAME.                                
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
           MOVE PC-PROGRAM-NAME               TO DP031-PROGRAM-NAME.            
                                                                                
           OPEN INPUT  PSTDMON-RDT-FILE                                         
                OUTPUT PSTDMON-RDT-TOKEN-FILE.                                  
                                                                                
           PERFORM 2100-READ-FILE.                                              
                                                                                
      ******************************************************************        
      * 2000-PROCESS-PARA                                                       
      *   1. PROCESS THE RECORDS IN THE INPUT FILE                              
      *   2. CALL TOKENIZE PARA TO TOKENIZE ACCOUNT NUMBER                      
      *   3. WRITE OUTPUT RECORDS                                               
      ******************************************************************        
       2000-PROCESS-PARA.                                                       
                                                                                
           INITIALIZE PV-ACCT-NBR                                               
                      PV-INPUT-LENGTH                                           
                                                                                
           IF PS-NOT-HDR-TRLR-REC                                               
              IF AR131-RDT-CHD-ACCOUNT-NUMBER NOT EQUAL SPACES                  
                 SET PS-ACCT-NBR          TO TRUE                               
                 MOVE AR131-RDT-CHD-ACCOUNT-NUMBER  TO PV-ACCT-NBR              
                                                                                
                 INSPECT FUNCTION REVERSE (PV-ACCT-NBR)                         
                     TALLYING PV-INPUT-LENGTH FOR LEADING SPACES                
                 COMPUTE PV-INPUT-LENGTH =                                      
                   LENGTH OF PV-ACCT-NBR - PV-INPUT-LENGTH                      
                                                                                
                 PERFORM 2300-TOKENIZE-RDT-CHD-ACCT-NBR                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2200-WRITE-FILE.                                             
           PERFORM 2100-READ-FILE.                                              
                                                                                
      ******************************************************************        
      * 2100-READ-FILE                                                          
      *      READ THE INPUT RECORDS                                             
      ******************************************************************        
       2100-READ-FILE.                                                          
                                                                                
           READ PSTDMON-RDT-FILE INTO AR131-RECORDS                             
              AT END                                                            
                 SET PS-END-OF-FILE  TO TRUE                                    
              NOT AT END                                                        
                 ADD 1               TO PV-READ-COUNT                           
           END-READ.                                                            
                                                                                
           IF AR131-RECORDS(1:3) = 'TRL' OR 'HDR'                               
              SET PS-HDR-TRLR-REC     TO TRUE                                   
           ELSE                                                                 
              SET PS-NOT-HDR-TRLR-REC TO TRUE                                   
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2200-WRITE-FILE                                                         
      *      WRITE THE RECORDS TO OUTPUT                                        
      ******************************************************************        
       2200-WRITE-FILE.                                                         
                                                                                
           INITIALIZE PV-OUTPUT-RECORD.                                         
           MOVE AR131-RECORDS            TO PV-OUTPUT-RECORD.                   
                                                                                
           WRITE PSTDMON-RDT-TOKEN-RECORD FROM PV-OUTPUT-RECORD.                
           ADD 1                         TO PV-WRITE-COUNT.                     
                                                                                
      ******************************************************************        
      * 2300-TOKENIZE-RDT-CHD-ACCT-NBR                                          
      *    TOKENIZE DR-CHD-ACCOUNT-NUMBER                                       
      ******************************************************************        
       2300-TOKENIZE-RDT-CHD-ACCT-NBR.                                          
                                                                                
           INITIALIZE CSI-PARAMETERS                                            
                      DP031-PROTEG-PGM-PARAMETERS.                              
                                                                                
           MOVE 'ACCT-NBR'              TO DP031-FIELD                          
           MOVE PV-ACCT-NBR             TO DP031-CLEAR-TEXT                     
           MOVE PV-INPUT-LENGTH         TO DP031-INPUT-LEN                      
           SET DP031-ACCTNBR            TO TRUE                                 
                                                                                
           SET CSI-ENCRYPT-FUNCTION     TO TRUE.                                
           PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                              
                                                                                
           IF CSI-RETURN-CODE = ZERO                                            
              MOVE DP031-CIPHER-TEXT  TO PV-ACCT-NBR                            
              MOVE PV-ACCT-NBR        TO AR131-RDT-CHD-ACCOUNT-NUMBER           
           ELSE                                                                 
              DISPLAY 'ISSUE IN RECORD# : ' PV-READ-COUNT                       
              PERFORM DP031-9999-ABEND                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 9000-EOJ-ROUTINE                                                        
      *      CLOSE THE INPUT AND OUTPUT FILES, DISPLAY THE MESSAGES             
      *      AND TERMINATE THE PROCESS.                                         
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE PSTDMON-RDT-FILE                                               
                 PSTDMON-RDT-TOKEN-FILE.                                        
                                                                                
           IF PV-READ-COUNT = 0                                                 
              DISPLAY 'INPUT FILE IS EMPTY'                                     
           ELSE                                                                 
              DISPLAY 'RECORDS READ   : ' PV-READ-COUNT                         
              DISPLAY 'RECORDS WRITTEN: ' PV-WRITE-COUNT                        
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *      COPY MEMBER DPPD031 CONTAINS STATEMENTS NEEDED TO INVOKE           
      *      PROTEGRITY MODULE FOR PROTECTION/UNPROTECTION                      
      ******************************************************************        
            COPY DPPD031.                                                       
                                                                                
      ******************************************************************        
