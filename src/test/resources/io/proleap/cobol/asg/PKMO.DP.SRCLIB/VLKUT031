000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.             VLKUT031.                                        
000400 AUTHOR.                 VIRGENE LENNEMAN.                                
000500 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000600 DATE-WRITTEN.           FEBRUARY 2005.                                   
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*                                                                         
001000*  PROGRAM NARRATIVE:                                                     
001100*  THIS PROGRAM WILL CREATE A FILE CONTAINING VENDOR                      
001200*  INFORMATION FOR COMPLIANCE NETWORK                                     
001300*                                                                         
194891*  01/03/2018 - JIM HUNTER - CHG0194891                                   
194891*  ADD RECORDS FOR THE FACTORY AND PARENT ENTITY ID INFORMATION           
194891*  TO THE VL-VNDR-FILE GOING TO COMPLIANCE NETWORKS.                      
194891*  SET THE EIN FIELD TO INDICATE IF THE ENTITY ID REPRESENTS A            
194891*  FACTORY OR PARENT.                                                     
194891*                                                                         
001500 ENVIRONMENT DIVISION.                                                    
001600                                                                          
001700 CONFIGURATION SECTION.                                                   
001800                                                                          
001900 SOURCE-COMPUTER.                        IBM-3090.                        
002000 OBJECT-COMPUTER.                        IBM-3090.                        
002100                                                                          
002200 INPUT-OUTPUT SECTION.                                                    
002300                                                                          
002400 FILE-CONTROL.                                                            
002500                                                                          
002600     SELECT  VL-VNDR-FILE                                                 
002700         ASSIGN TO OUT01.                                                 
002800                                                                          
002900     SELECT  VL-MARKER-FILE                                               
003000         ASSIGN TO OUT02.                                                 
003100                                                                          
003100****** THIS FILE IS FOR GENCO,  SCAC IS ADDED TO CARRIER VENDORS          
002600     SELECT  RV-VNDR-FILE                                                 
002700         ASSIGN TO OUT03.                                                 
002800                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700*                                                                         
003800 FD  VL-VNDR-FILE                                                         
003900     RECORDING MODE IS F                                                  
004000     LABEL RECORDS ARE STANDARD                                           
004100     BLOCK CONTAINS 0 RECORDS.                                            
004200 01  VL-VNDR-RECORD                   PIC  X(110).                        
004300*                                                                         
004400*                                                                         
004500 FD  VL-MARKER-FILE                                                       
004600     RECORDING MODE IS F                                                  
004700     LABEL RECORDS ARE STANDARD                                           
004800     BLOCK CONTAINS 0 RECORDS.                                            
004900 01  VL-MARKER-RECORD                   PIC  X(78).                       
005000*                                                                         
003800 FD  RV-VNDR-FILE                                                         
003900     RECORDING MODE IS F                                                  
004000     LABEL RECORDS ARE STANDARD                                           
004100     BLOCK CONTAINS 0 RECORDS.                                            
004200 01  RV-VNDR-RECORD                   PIC  X(114).                        
004300*                                                                         
005100*                                                                         
005200 WORKING-STORAGE SECTION.                                                 
000250***************************************************************** 00025015
000260*    INPUT CONTROL CARD                                           00026015
000270***************************************************************** 00027015
000054 01  CC-CONTROL-CARD.                                                     
000055     05  CC-SCHEDULE-DATE.                                                
000056         10  CC-CCYY                 PIC X(04).                           
000057         10  CC-MM                   PIC X(02).                           
000058         10  CC-DD                   PIC X(02).                           
000059                                                                          
005300*****************************************************************         
005400*    PROGRAM CONSTANTS                                                    
005500*****************************************************************         
005600 01  PC-CONSTANTS.                                                        
005700     05  PC-CURRENT-PGM-VLKUT031      PIC  X(08) VALUE 'VLKUT031'.        
005800     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
005900                                                 COMP SYNC.               
006000                                                                          
006100*****************************************************************         
006200*    PROGRAM SWITCHES                                                     
006300*****************************************************************         
006400 01  PS-SWITCHES.                                                         
006500     05  PS-END-CURSOR-SWITCH         PIC  X(01) VALUE 'N'.               
006600         88  PS-END-CSR                          VALUE 'Y'.               
006700         88  PS-NOT-END-CSR                      VALUE 'N'.               
006500     05  PS-END-FP-CURSOR-SWITCH      PIC  X(01) VALUE 'N'.               
006600         88  PS-END-FP-CSR                       VALUE 'Y'.               
006700         88  PS-NOT-END-FP-CSR                   VALUE 'N'.               
006800                                                                          
006900*****************************************************************         
007000*    PROGRAM VARIABLES                                                    
007100*****************************************************************         
007200 01  PV-VARIABLES.                                                        
007300     05  PV-RETRY-COUNT               PIC S9(04) VALUE +0                 
007400                                                 COMP SYNC.               
007500     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
007600                                                 COMP SYNC.               
007700     05  PV-PARAGRAPH                 PIC  X(30) VALUE SPACES.            
007800     05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.            
007900     05  PV-ABEND-STRUCTURE-1         PIC  X(20) VALUE SPACES.            
008000     05  PV-ABEND-STRUCTURE-2         PIC  X(20) VALUE SPACES.            
008100     05  PV-ABEND-STRUCTURE-3         PIC  X(20) VALUE SPACES.            
008200     05  PV-RECS-WRITTEN              PIC  9(09) VALUE ZERO.              
194891     05  PV-GENCO-RECS-WRITTEN        PIC  9(09) VALUE ZERO.              
009000     05  PV-RUN-DATE-CN               PIC  X(08) VALUE SPACES.            
009400     05  PV-RUN-TIME-CN               PIC  X(06) VALUE SPACES.            
010500***** FILE NAME CONSISTS OF RUN-DATE-CN_VENDOR.DAT                        
010600*****     EXAMPLE: 20050317_VENDOR.DAT                                    
010700     05  PV-FILE-NAME.                                                    
010800         10  PV-CREATE-DATE           PIC  X(08) VALUE SPACES.            
010900         10  PV-UNDERSCORE            PIC  X(01) VALUE '_'.               
011000         10  PV-VENDOR-DAT            PIC  X(10) VALUE SPACES.            
011100                                                                          
011200***  MARKER FILE LAYOUT                                                   
011300     COPY VLRD029.                                                        
011400                                                                          
011500***  VENDOR FILE LAYOUT                                                   
011600     COPY VLRD031.                                                        
011700                                                                          
011500***  VENDOR FILE LAYOUT FOR GENCO                                         
011600     COPY RVRD040.                                                        
011700                                                                          
000418***  DATE ROUTINE COPYBOOK                                        00041815
000419     COPY DPWS500.                                                00041915
000420                                                                  00042015
011800***  DB2 ABEND COPYBOOK                                                   
011900     COPY DPWS004.                                                        
012000                                                                          
012100     EXEC SQL                                                             
012200         INCLUDE SQLCA                                                    
012300     END-EXEC.                                                            
012400                                                                          
012500     COPY SQLCA2.                                                         
012600                                                                          
012700***  EXTERNAL ENTITY                                                      
012800     EXEC SQL                                                             
012900         INCLUDE TEXTENT                                                  
013000     END-EXEC.                                                            
013100                                                                          
013200***  ENTITY NAME                                                          
013300     EXEC SQL                                                             
013400         INCLUDE TENTNME                                                  
013500     END-EXEC.                                                            
013600                                                                          
013700***  EXTRACT CURSOR                                                       
013800     EXEC SQL                                                             
013900         DECLARE EXTRACT-CSR CURSOR FOR                                   
014000         SELECT A.ENT_ID                                                  
014100              , A.DUN_NBR                                                 
014100              , A.SCAC_CDE                                                
014100              , A.CARRIER_IND                                             
014200              , B.ENT_NAME_DESC                                           
014300           FROM                                                           
014400                TEXTENT  A                                                
014500              , TENTNME  B                                                
014600           WHERE A.ENT_ID   = B.ENT_ID                                    
014700             AND (A.MDSE_VENDOR_IND = 'Y' OR                              
014800                  A.CARRIER_IND = 'Y')                                    
014900             AND B.TYPE_CDE     = '01'                                    
015000        ORDER BY A.ENT_ID                                                 
015000        WITH UR                                                           
015100     END-EXEC.                                                            
013600                                                                          
194891***  FACTORY / PARENT CURSOR                                              
194891     EXEC SQL                                                             
194891         DECLARE FACTORY-PARENT-CSR CURSOR FOR                            
194891         SELECT A.ENT_ID                                                  
194891              , A.DUN_NBR                                                 
194891              , B.ENT_NAME_DESC                                           
194891              , A.PRTNR_TYP_CDE                                           
194891              , A.STATUS_CDE                                              
194891           FROM                                                           
194891                TEXTENT  A                                                
194891              , TENTNME  B                                                
194891           WHERE A.PRTNR_TYP_CDE IN('S2','FA')                            
194891             AND A.VND_IN_RMS_IND = 'Y'                                   
194891             AND A.ENT_ID         = B.ENT_ID                              
194891             AND B.TYPE_CDE       = '01'                                  
194891        ORDER BY A.ENT_ID                                                 
194891        WITH UR                                                           
194891     END-EXEC.                                                            
194891                                                                          
015300 LINKAGE SECTION.                                                         
015400                                                                          
015500 PROCEDURE DIVISION.                                                      
015600*****************************************************************         
015700*  MAINLINE LOGIC FOR PROGRAM                                             
015800*****************************************************************         
015900 0000-MAINLINE.                                                           
016000     PERFORM 1000-INITIALIZE.                                             
016100                                                                          
016200     PERFORM 2000-PROCESS                                                 
016300         UNTIL PS-END-CSR.                                                
016100                                                                          
194891     PERFORM 7500-OPEN-FACTORY-PARENT-CSR.                                
194891     PERFORM 7700-FETCH-FACTORY-PARENT-CSR.                               
194891                                                                          
194891     PERFORM 3000-PROCESS-FP-CSR                                          
194891         UNTIL PS-END-FP-CSR.                                             
194891                                                                          
016500     PERFORM 9000-EOJ.                                                    
016600                                                                          
016700     STOP RUN.                                                            
016800                                                                          
016900*****************************************************************         
017000*  INITIALIZATION PARAGRAPH                                               
017100*****************************************************************         
017200 1000-INITIALIZE.                                                         
017300     OPEN OUTPUT VL-VNDR-FILE                                             
017300                 RV-VNDR-FILE                                             
017400                 VL-MARKER-FILE.                                          
017500                                                                          
000126     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
000127                                                                          
000128     DISPLAY 'CONTROL CARD ====>  ' CC-CONTROL-CARD.                      
000129                                                                          
000130     PERFORM 1100-VALIDATE-CONTROL-CARD.                                  
000131                                                                          
000132******* TIME IS CONTAINED CURRENT-DATE IN POSITION 9 FOR 8                
000133     MOVE FUNCTION CURRENT-DATE(9:8) TO PV-RUN-TIME-CN.                   
000135                                                                          
000136     DISPLAY '******  RUN TIME  => '  PV-RUN-TIME-CN.                     
000137                                                                          
019500     PERFORM 7000-OPEN-EXTRACT-CSR.                                       
019600     PERFORM 7200-FETCH-EXTRACT-CSR.                                      
000137                                                                          
000142******************************************************************        
000143**  VALIDATE THE DATE FROM THE SCHEDULE                                   
000144******************************************************************        
000145 1100-VALIDATE-CONTROL-CARD.                                              
000146     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000147     SET DPG51-FISCAL-454-CALENDAR                                        
000148         DPG51-DO-NOT-INCR-DECR-DATE                                      
000149         DPG52-LK-DTE-ISO           TO TRUE.                              
000150                                                                          
000151     MOVE CC-SCHEDULE-DATE     TO DPG52-LK-DATE-INPUT.                    
000152                                                                          
000153     CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
000154             DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
000155                                                                          
000156     IF DPG54-NO-ERROR                                                    
000157         MOVE CC-SCHEDULE-DATE     TO PV-RUN-DATE-CN                      
000158     ELSE                                                                 
000159         DISPLAY '** ABEND **'                                            
000160         DISPLAY '** PROGRAM = VLKUT031 -- CALLED MODULE '                
000161                  'DPKUT500 **'                                           
000162         DISPLAY '** PARAGRAPH = 1100-VALIDATE-CONTROL-CARD **'           
000163         DISPLAY '** INVALID SCHEDULE DATE: ' CC-SCHEDULE-DATE            
000164         DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '                
000165         DISPLAY DPG54-ERROR-MESSAGE                                      
000166         MOVE +4003 TO PV-ABEND-CODE                                      
000167         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
000168     END-IF.                                                              
000169                                                                          
000169                                                                          
019800*****************************************************************         
019900* PROCESS DATA FROM CURSOR AND WRITE OUTPUT RECORD.                       
194891* THESE RECORDS ARE ALSO WRITTEN TO THE GENCO OUTPUT FILE.                
020000*****************************************************************         
020100 2000-PROCESS.                                                            
020200     INITIALIZE VL031-VENDOR-RECORD                                       
020200                RV040-VENDOR-RECORD.                                      
020310     MOVE 'A'                     TO VL031-ACD-INDICATOR                  
020310                                     RV040-ACD-INDICATOR.                 
020400     MOVE EXTENT-ENT-ID           TO VL031-VENDOR-NUMBER                  
020400                                     RV040-VENDOR-NUMBER.                 
020500     MOVE ENTNME-ENT-NAME-DESC    TO VL031-VENDOR-NAME                    
020500                                     RV040-VENDOR-NAME.                   
020600     MOVE SPACES                  TO VL031-EIN                            
020600                                     VL031-EIN.                           
020700     MOVE EXTENT-DUN-NBR          TO VL031-DUN-NUMBER                     
020700                                     RV040-DUN-NUMBER.                    
020800     MOVE SPACES                  TO VL031-FACTOR-NUMBER                  
020800                                     RV040-FACTOR-NUMBER.                 
020900     MOVE SPACES                  TO VL031-FRT-PAYMENT-TYPE               
020900                                     RV040-FRT-PAYMENT-TYPE.              
021000     MOVE 'USD'                   TO VL031-CURRENCY-CODE                  
021000                                     RV040-CURRENCY-CODE.                 
021100     IF EXTENT-CARRIER-IND  = 'Y'                                         
021100         MOVE EXTENT-SCAC-CDE     TO RV040-SCAC                           
021100     END-IF.                                                              
021100     MOVE 'X'                     TO VL031-ANCHOR-CONSTANT                
021100                                     RV040-ANCHOR-CONSTANT.               
021200                                                                          
021300     PERFORM 8000-WRITE-OUTPUT.                                           
021200                                                                          
194891     PERFORM 8100-WRITE-GENCO-OUTPUT.                                     
194891                                                                          
021500     PERFORM 7200-FETCH-EXTRACT-CSR.                                      
021600                                                                          
000169                                                                          
194891*****************************************************************         
194891* PROCESS DATA FROM FACTORY / PARENT CURSOR AND WRITE OUTPUT              
194891* RECORD.  THESE RECORDS ARE NOT WRITTEN TO THE GENCO OUTPUT              
194891* FILE.                                                                   
194891*****************************************************************         
194891 3000-PROCESS-FP-CSR.                                                     
194891                                                                          
194891     INITIALIZE VL031-VENDOR-RECORD.                                      
194891     MOVE 'A'                     TO VL031-ACD-INDICATOR.                 
194891     MOVE EXTENT-ENT-ID           TO VL031-VENDOR-NUMBER.                 
194891     MOVE ENTNME-ENT-NAME-DESC    TO VL031-VENDOR-NAME.                   
194891     MOVE EXTENT-PRTNR-TYP-CDE    TO VL031-EIN.                           
194891*         'FA' = FACTORY,   'S2' = PARENT                                 
194891     MOVE EXTENT-DUN-NBR          TO VL031-DUN-NUMBER.                    
194891     MOVE SPACES                  TO VL031-FACTOR-NUMBER.                 
194891     MOVE SPACES                  TO VL031-FRT-PAYMENT-TYPE.              
194891     MOVE 'USD'                   TO VL031-CURRENCY-CODE.                 
194891     MOVE 'X'                     TO VL031-ANCHOR-CONSTANT.               
194891                                                                          
194891     PERFORM 8000-WRITE-OUTPUT.                                           
194891                                                                          
194891     PERFORM 7700-FETCH-FACTORY-PARENT-CSR.                               
194891                                                                          
021700                                                                          
021800******************************************************************        
021900* OPEN CURSOR                                                             
022000******************************************************************        
022100 7000-OPEN-EXTRACT-CSR.                                                   
022200     PERFORM                                                              
022300         WITH TEST AFTER                                                  
022400         VARYING PV-RETRY-COUNT                                           
022500         FROM 1 BY 1                                                      
022600         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
022700         OR  (SQLCODE = +0))                                              
022800                                                                          
022900         EXEC SQL                                                         
023000              OPEN EXTRACT-CSR                                            
023100         END-EXEC                                                         
023200                                                                          
023300         EVALUATE TRUE                                                    
023400             WHEN SQLCODE = +0                                            
023500             WHEN SQLCODE = -904                                          
023600             WHEN SQLCODE = -911                                          
023700             WHEN SQLCODE = -913                                          
023800                 CONTINUE                                                 
023900             WHEN SQLWARN0 NOT EQUAL SPACE                                
024000             WHEN SQLCODE NOT EQUAL ZERO                                  
024100                 DISPLAY '*****  ABEND *****'                             
024200                 DISPLAY '** PROGRAM = VLKUT031 '                         
024300                 DISPLAY '** PARAGRAPH = 7000-OPEN-EXTRACT-CSR'           
024400                 MOVE '7000-OPEN-EXTRACT-CSR' TO PV-PARAGRAPH             
024500                 MOVE 'OPEN CURSOR          ' TO PV-DB2-FUNCTION          
024600                 MOVE 'TEXTENT'         TO PV-ABEND-STRUCTURE-1           
024700                 MOVE 'TENTNME'         TO PV-ABEND-STRUCTURE-2           
024800                 PERFORM 9200-SQL-ERROR                                   
024900         END-EVALUATE                                                     
025000     END-PERFORM.                                                         
025100                                                                          
025200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025300         MOVE '7000-OPEN-EXTRACT-CSR'  TO PV-PARAGRAPH                    
025400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
025500         MOVE 'TEXTENT'                TO PV-ABEND-STRUCTURE-1            
025600         MOVE 'TENTNME'                TO PV-ABEND-STRUCTURE-2            
025700         PERFORM 9200-SQL-ERROR                                           
025800     END-IF.                                                              
025900                                                                          
026000                                                                          
026100******************************************************************        
026200* CLOSE CURSOR                                                            
026300******************************************************************        
026400 7100-CLOSE-EXTRACT-CSR.                                                  
026500     PERFORM                                                              
026600         WITH TEST AFTER                                                  
026700         VARYING PV-RETRY-COUNT                                           
026800         FROM 1 BY 1                                                      
026900         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
027000         OR  (SQLCODE = +0))                                              
027100                                                                          
027200         EXEC SQL                                                         
027300             CLOSE EXTRACT-CSR                                            
027400         END-EXEC                                                         
027500                                                                          
027600         EVALUATE TRUE                                                    
027700             WHEN SQLCODE = +0                                            
027800             WHEN SQLCODE = -904                                          
027900             WHEN SQLCODE = -911                                          
028000             WHEN SQLCODE = -913                                          
028100                 CONTINUE                                                 
028200             WHEN SQLWARN0 NOT EQUAL SPACE                                
028300             WHEN SQLCODE NOT EQUAL ZERO                                  
028400                 DISPLAY '** ABEND **'                                    
028500                 DISPLAY '** PROGRAM = VLKUT031 **'                       
028600                 DISPLAY '** PARAGRAPH = 7100-CLOSE-EXTRACT-CSR'          
028700                 MOVE '7100-CLOSE-EXTRACT-CSR' TO PV-PARAGRAPH            
028800                 MOVE 'CLOSE CURSOR          ' TO PV-DB2-FUNCTION         
028900                 MOVE 'TEXTENT'         TO PV-ABEND-STRUCTURE-1           
029000                 MOVE 'TENTNME'         TO PV-ABEND-STRUCTURE-2           
029100                 PERFORM 9200-SQL-ERROR                                   
029200         END-EVALUATE                                                     
029300     END-PERFORM.                                                         
029400                                                                          
029500     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
029600         MOVE '7100-CLOSE-EXTRACT-CSR' TO PV-PARAGRAPH                    
029700         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
029800         MOVE 'TEXTENT'                TO PV-ABEND-STRUCTURE-1            
029900         MOVE 'TENTNME'                TO PV-ABEND-STRUCTURE-2            
030000         PERFORM 9200-SQL-ERROR                                           
030100     END-IF.                                                              
030200                                                                          
030300******************************************************************        
030400*  FETCH CURSOR                                                           
030500******************************************************************        
030600 7200-FETCH-EXTRACT-CSR.                                                  
030700     PERFORM                                                              
030800         WITH TEST AFTER                                                  
030900         VARYING PV-RETRY-COUNT                                           
031000         FROM 1 BY 1                                                      
031100         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
031200         OR  (SQLCODE = +0) OR (PS-END-CSR))                              
031300                                                                          
031400         EXEC SQL                                                         
031500              FETCH EXTRACT-CSR                                           
031600              INTO :EXTENT-ENT-ID                                         
031700                    , :EXTENT-DUN-NBR                                     
031700                    , :EXTENT-SCAC-CDE                                    
031700                    , :EXTENT-CARRIER-IND                                 
031800                    , :ENTNME-ENT-NAME-DESC                               
031900         END-EXEC                                                         
032000                                                                          
032100         EVALUATE TRUE                                                    
032200             WHEN SQLCODE = ZERO                                          
032300             WHEN SQLCODE = -904                                          
032400             WHEN SQLCODE = -911                                          
032500             WHEN SQLCODE = -913                                          
032600                 CONTINUE                                                 
032700             WHEN SQLCODE = +100                                          
032800                 SET PS-END-CSR TO TRUE                                   
032900             WHEN SQLWARN0 NOT EQUAL SPACE                                
033000             WHEN SQLCODE NOT EQUAL ZERO                                  
033100                 DISPLAY '** ABEND **'                                    
033200                 DISPLAY '** PROGRAM = VLKUT031 **'                       
033300                 DISPLAY '** PARAGRAPH = 7200-FETCH-EXTRACT-CSR'          
033400                 MOVE '7200-FETCH-EXTRACT-CSR' TO PV-PARAGRAPH            
033500                 MOVE 'FETCH FROM CURSOR     ' TO PV-DB2-FUNCTION         
033600                 MOVE 'TEXTENT'         TO PV-ABEND-STRUCTURE-1           
033700                 MOVE 'TENTNME'         TO PV-ABEND-STRUCTURE-2           
033800                 PERFORM 9200-SQL-ERROR                                   
033900         END-EVALUATE                                                     
034000     END-PERFORM.                                                         
034100                                                                          
034200     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
034300         MOVE '7200-FETCH-EXTRACT-CSR' TO PV-PARAGRAPH                    
034400         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
034500         MOVE 'TEXTENT'                TO PV-ABEND-STRUCTURE-1            
034600         MOVE 'TENTNME'                TO PV-ABEND-STRUCTURE-2            
034700         PERFORM 9200-SQL-ERROR                                           
034800     END-IF.                                                              
034900                                                                          
021700                                                                          
194891******************************************************************        
194891* OPEN CURSOR                                                             
194891******************************************************************        
194891 7500-OPEN-FACTORY-PARENT-CSR.                                            
194891     PERFORM                                                              
194891         WITH TEST AFTER                                                  
194891         VARYING PV-RETRY-COUNT                                           
194891         FROM 1 BY 1                                                      
194891         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
194891         OR  (SQLCODE = +0))                                              
194891                                                                          
194891         EXEC SQL                                                         
194891              OPEN FACTORY-PARENT-CSR                                     
194891         END-EXEC                                                         
194891                                                                          
194891         EVALUATE TRUE                                                    
194891             WHEN SQLCODE = +0                                            
194891             WHEN SQLCODE = -904                                          
194891             WHEN SQLCODE = -911                                          
194891             WHEN SQLCODE = -913                                          
194891                 CONTINUE                                                 
194891             WHEN SQLWARN0 NOT EQUAL SPACE                                
194891             WHEN SQLCODE NOT EQUAL ZERO                                  
194891                 DISPLAY '*****  ABEND *****'                             
194891                 DISPLAY '** PROGRAM = VLKUT031 '                         
194891                 DISPLAY '** PARAGRAPH = 7500-OPEN-FP-CSR'                
194891                 MOVE '7500-OPEN-FP-CSR' TO PV-PARAGRAPH                  
194891                 MOVE 'OPEN CURSOR     ' TO PV-DB2-FUNCTION               
194891                 MOVE 'TEXTENT'          TO PV-ABEND-STRUCTURE-1          
194891                 MOVE 'TENTNME'          TO PV-ABEND-STRUCTURE-2          
194891                 PERFORM 9200-SQL-ERROR                                   
194891         END-EVALUATE                                                     
194891     END-PERFORM.                                                         
194891                                                                          
194891     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
194891         MOVE '7500-OPEN-FP-CSR '      TO PV-PARAGRAPH                    
194891         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
194891         MOVE 'TEXTENT'                TO PV-ABEND-STRUCTURE-1            
194891         MOVE 'TENTNME'                TO PV-ABEND-STRUCTURE-2            
194891         PERFORM 9200-SQL-ERROR                                           
194891     END-IF.                                                              
194891                                                                          
194891                                                                          
194891******************************************************************        
194891* CLOSE FACTORY PARENT CURSOR                                             
194891******************************************************************        
194891 7600-CLOSE-FACTORY-PARENT-CSR.                                           
194891     PERFORM                                                              
194891         WITH TEST AFTER                                                  
194891         VARYING PV-RETRY-COUNT                                           
194891         FROM 1 BY 1                                                      
194891         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
194891         OR  (SQLCODE = +0))                                              
194891                                                                          
194891         EXEC SQL                                                         
194891             CLOSE FACTORY-PARENT-CSR                                     
194891         END-EXEC                                                         
194891                                                                          
194891         EVALUATE TRUE                                                    
194891             WHEN SQLCODE = +0                                            
194891             WHEN SQLCODE = -904                                          
194891             WHEN SQLCODE = -911                                          
194891             WHEN SQLCODE = -913                                          
194891                 CONTINUE                                                 
194891             WHEN SQLWARN0 NOT EQUAL SPACE                                
194891             WHEN SQLCODE NOT EQUAL ZERO                                  
194891                 DISPLAY '** ABEND **'                                    
194891                 DISPLAY '** PROGRAM = VLKUT031 **'                       
194891                 DISPLAY '** PARAGRAPH = 7600-CLOSE-FP-CSR'               
194891                 MOVE '7600-CLOSE-FP-CSR' TO PV-PARAGRAPH                 
194891                 MOVE 'CLOSE CURSOR     ' TO PV-DB2-FUNCTION              
194891                 MOVE 'TEXTENT'           TO PV-ABEND-STRUCTURE-1         
194891                 MOVE 'TENTNME'           TO PV-ABEND-STRUCTURE-2         
194891                 PERFORM 9200-SQL-ERROR                                   
194891         END-EVALUATE                                                     
194891     END-PERFORM.                                                         
194891                                                                          
194891     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
194891         MOVE '7600-CLOSE-FP-CSR'      TO PV-PARAGRAPH                    
194891         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
194891         MOVE 'TEXTENT'                TO PV-ABEND-STRUCTURE-1            
194891         MOVE 'TENTNME'                TO PV-ABEND-STRUCTURE-2            
194891         PERFORM 9200-SQL-ERROR                                           
194891     END-IF.                                                              
194891                                                                          
194891******************************************************************        
194891*  FETCH FACTORY PARENT CURSOR                                            
194891******************************************************************        
194891 7700-FETCH-FACTORY-PARENT-CSR.                                           
194891     PERFORM                                                              
194891         WITH TEST AFTER                                                  
194891         VARYING PV-RETRY-COUNT                                           
194891         FROM 1 BY 1                                                      
194891         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
194891         OR  (SQLCODE = +0) OR (PS-END-CSR))                              
194891                                                                          
194891         EXEC SQL                                                         
194891              FETCH FACTORY-PARENT-CSR                                    
194891              INTO :EXTENT-ENT-ID                                         
194891                 , :EXTENT-DUN-NBR                                        
194891                 , :ENTNME-ENT-NAME-DESC                                  
194891                 , :EXTENT-PRTNR-TYP-CDE                                  
194891                 , :EXTENT-STATUS-CDE                                     
194891         END-EXEC                                                         
194891                                                                          
194891         EVALUATE TRUE                                                    
194891             WHEN SQLCODE = ZERO                                          
194891             WHEN SQLCODE = -904                                          
194891             WHEN SQLCODE = -911                                          
194891             WHEN SQLCODE = -913                                          
194891                 CONTINUE                                                 
194891             WHEN SQLCODE = +100                                          
194891                 SET PS-END-FP-CSR TO TRUE                                
194891             WHEN SQLWARN0 NOT EQUAL SPACE                                
194891             WHEN SQLCODE NOT EQUAL ZERO                                  
194891                 DISPLAY '** ABEND **'                                    
194891                 DISPLAY '** PROGRAM = VLKUT031 **'                       
194891                 DISPLAY '** PARAGRAPH = 7700-FETCH-FP-CSR'               
194891                 MOVE '7700-FETCH-FP-CSR ' TO PV-PARAGRAPH                
194891                 MOVE 'FETCH FROM CURSOR ' TO PV-DB2-FUNCTION             
194891                 MOVE 'TEXTENT'           TO PV-ABEND-STRUCTURE-1         
194891                 MOVE 'TENTNME'           TO PV-ABEND-STRUCTURE-2         
194891                 PERFORM 9200-SQL-ERROR                                   
194891         END-EVALUATE                                                     
194891     END-PERFORM.                                                         
194891                                                                          
194891     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
194891         MOVE '7700-FETCH-FP-CSR '     TO PV-PARAGRAPH                    
194891         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
194891         MOVE 'TEXTENT'                TO PV-ABEND-STRUCTURE-1            
194891         MOVE 'TENTNME'                TO PV-ABEND-STRUCTURE-2            
194891         PERFORM 9200-SQL-ERROR                                           
194891     END-IF.                                                              
034900                                                                          
035000*****************************************************************         
035100*  WRITE OUTPUT FILE                                                      
035200*****************************************************************         
035300 8000-WRITE-OUTPUT.                                                       
035400     WRITE VL-VNDR-RECORD FROM VL031-VENDOR-RECORD.                       
035500                                                                          
035600     ADD 1                TO PV-RECS-WRITTEN.                             
035700                                                                          
194891*****************************************************************         
194891*  WRITE OUTPUT FILE                                                      
194891*****************************************************************         
194891 8100-WRITE-GENCO-OUTPUT.                                                 
194891     WRITE RV-VNDR-RECORD FROM RV040-VENDOR-RECORD.                       
194891                                                                          
194891     ADD 1                TO PV-GENCO-RECS-WRITTEN.                       
194891                                                                          
035800*****************************************************************         
035900*  CREATE MARKER FILE                                                     
036000*****************************************************************         
036100 8500-CREATE-MARKER.                                                      
036200     MOVE PV-RUN-DATE-CN  TO PV-CREATE-DATE.                              
036300     MOVE 'VENDOR.DAT'    TO PV-VENDOR-DAT.                               
036400     MOVE PV-FILE-NAME    TO VL029-FILE-NAME.                             
036500     MOVE PV-RECS-WRITTEN TO VL029-RECORD-COUNT.                          
036600     MOVE PV-RUN-DATE-CN  TO VL029-BUILD-DATE.                            
036700     MOVE PV-RUN-TIME-CN  TO VL029-BUILD-TIME.                            
036800     MOVE 'X'             TO VL029-ANCHOR-CONSTANT.                       
036900                                                                          
037000     WRITE VL-MARKER-RECORD FROM VL029-MARKER-RECORD.                     
037100                                                                          
037200*****************************************************************         
037300*  END OF JOB PROCESSING                                                  
037400*****************************************************************         
037500 9000-EOJ.                                                                
037600     DISPLAY 'RECORDS WRITTEN       ===> ' PV-RECS-WRITTEN.               
194891     DISPLAY 'GENCO RECORDS WRITTEN ===> ' PV-GENCO-RECS-WRITTEN.         
037700                                                                          
037800     PERFORM 8500-CREATE-MARKER.                                          
037900                                                                          
038000     PERFORM 7100-CLOSE-EXTRACT-CSR.                                      
038000     PERFORM 7600-CLOSE-FACTORY-PARENT-CSR.                               
038100                                                                          
038200     CLOSE VL-VNDR-FILE                                                   
038200           RV-VNDR-FILE                                                   
038300           VL-MARKER-FILE.                                                
038400                                                                          
038500                                                                          
038600*****************************************************************         
038700*  PARAGRAPH EXECUTED WHEN SERIOUS DB2 ERROR OCCURRED                     
038800*****************************************************************         
038900 9200-SQL-ERROR.                                                          
039000     CLOSE VL-VNDR-FILE                                                   
038200           RV-VNDR-FILE                                                   
038300           VL-MARKER-FILE.                                                
039100                                                                          
039200     MOVE SQLCODE                     TO SQLCODE2.                        
039300     MOVE SQLERRD(3)                  TO SQLERRD3.                        
039400                                                                          
039500     DISPLAY '** ABEND **         ** = SQLERROR'.                         
039600     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PGM-VLKUT031.                
039700     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                           
039800     DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                        
039900                                                                          
040000     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
040100     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
040200     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
040300     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
040400                                                                          
040500     COPY DPPD004.                                                        
040600                                                                          
040700     MOVE +4013                       TO PV-ABEND-CODE.                   
040800     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
040900                                                                          
041000*************  END OF VLKUT031  ********************************          
