       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT013.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  06-02-2008.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * INKUT013 - PHYSICAL INVENTORY EXCEPTIONS SYSTEM PRICING ADDITION        
      ******************************************************************        
      * THIS PROGRAM READS THE PHYSICAL INVENTORY EXCEPTIONS MERGED FILE        
      * AND ADDS SYSTEM PRICING INFORMATION. SYSTEM PRICING IS OBTAINED         
      * FROM TUPCPLS AND TMITGPL (FIRST AT THE STORE LEVEL, THEN AT             
      * CORP).  FOR DELETED SKUS, PRICING INFORMATION IS OBTAINED FROM          
      * INT_SKU_STYL_DLT.  THE INPUT FILE IS SORTED IN INTR-UPC-ID,             
      * LOC NBR, AREA SEQUENCE.                                                 
      ******************************************************************        
      * CHANGE HISTORY:                                                         
      * W34894   06-02-2008  INITIAL CREATION.    PAUL KEBER                    
      ******************************************************************        
W33304*  10/27/2008    PATTY JAEGER-CODEWORKS  CHANGE ID:  W33304               
W33304*  HILO CHANGES                                                           
W33304******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INV-EXCEPTS-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT MERGE-FILE-OUT                                                
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT MERGE-FILE-2-OUT                                              
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INV-EXCEPTS-FILE                                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INV-EXCEPTS-REC       PIC X(100).                                    
                                                                                
       FD  MERGE-FILE-OUT                                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W33304 01  MERGE-REC-OUT         PIC X(150).                                    
                                                                                
       FD  MERGE-FILE-2-OUT                                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  MERGE-REC-F2-OUT      PIC X(100).                                    
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE               PIC S9(04) VALUE ZERO COMP SYNC.            
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-AREA-HEADER          PIC  X(06)     VALUE '000000'.           
           05  PC-DEFAULT-LOC          PIC S9(04)     VALUE +0 COMP.            
           05  PC-MAX-RETRIES          PIC S9(03)     VALUE +02.        INKUT102
           05  PC-NEG-VALUE            PIC S9(01)     VALUE -1.                 
           05  PC-REGIONAL-STAT        PIC X(02)      VALUE '25'.               
           05  PC-CLEARANCE-STAT       PIC X(02)      VALUE '30'.               
           05  PC-GWP-CEILING          PIC 9V99       VALUE 0.02.               
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-INV-EXCEPTS-READ     PIC S9(09) VALUE +0  COMP.               
           05  PV-TOTAL-EXTR-WRITTEN   PIC S9(09) VALUE +0  COMP.               
           05  PV-TOTAL-EXTR-F2-WRITTEN                                         
                                       PIC S9(09) VALUE +0  COMP.               
           05  PV-INTSKUSD-ROWS-READ   PIC S9(09) VALUE +0  COMP.               
           05  PV-TUPCPLS-CORP-READ    PIC S9(09) VALUE +0  COMP.               
           05  PV-TUPCPLS-STRS-READ    PIC S9(09) VALUE +0  COMP.               
           05  PV-SKUS-NOT-FOUND       PIC S9(09) VALUE +0  COMP.               
           05  PV-GROUP-UNIT-RTL       PIC S9(07)V99  VALUE +0 COMP.            
           05  PV-VARIANCE-PRICE       PIC S9(07)V99  VALUE +0 COMP.            
           05  PV-ABSOLUTE-VAR-PRICE   PIC S9(07)V99  VALUE +0 COMP.            
           05  PV-STR-NDX              PIC S9(04) COMP   VALUE ZEROS.           
           05  PV-PARAGRAPH-NAME       PIC X(30)  VALUE SPACES.                 
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)  VALUE SPACES.                 
           05  PV-PROGRAM-NAME         PIC X(20)  VALUE 'INKUT013'.             
           05  PV-ABEND-MESSAGE        PIC X(50)  VALUE SPACE.                  
           05  PV-UPC-STAT-CDE         PIC X(2)   VALUE SPACES.         INKUT102
           05  PV-SQL-SUB              PIC 9(03)  VALUE ZERO.           INKUT102
           05  PV-UNT-RTL-AMT          PIC S9(7)V9(2) COMP-3  VALUE +0.         
           05  PV-F2-UNT-RTL-AMT       PIC S9(7)V9(2) COMP-3  VALUE +0.         
           05  PV-PLND-INV-TMST.                                                
               10  PV-PLND-INV-DTE     PIC  X(10) VALUE SPACES.                 
               10  FILLER              PIC  X(16) VALUE                         
                                                 '-23.59.59.999999'.            
           05  PV-SAVE-INTR-UPC-ID     PIC S9(09)     COMP    VALUE +0.         
           05  PV-SAVE-CORP-INTR-UPC-ID                                         
                                       PIC S9(09)     COMP    VALUE +0.         
           05  PV-SAVE-CORP-UNT-RTL-AMT                                         
                                       PIC S9(7)V9(2) COMP-3  VALUE +0.         
           05  PV-SAVE-CORP-MEITGP-NBR PIC S9(09)     COMP    VALUE +0.         
           05  PV-SAVE-CORP-UPC-STAT-CDE                                        
                                       PIC X(2)       VALUE SPACES.             
           05  PV-SAVE-LOC             PIC S9(4)      COMP    VALUE +0.         
           05  PV-SAVE-SYS-RTL         PIC S9(7)V9(2) COMP-3  VALUE +0.         
           05  PV-SAVE-F2-SYS-RTL      PIC S9(7)V9(2) COMP-3  VALUE +0.         
                                                                                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-INV-EXCEPTS-DONE     PIC  X(01)      VALUE 'N'.               
               88  PS-INV-EXCEPTS-IS-DONE              VALUE 'Y'.               
               88  PS-INV-EXCEPTS-IS-NOT-DONE          VALUE 'N'.               
           05  PS-TINVPAR-DONE-SW         PIC X(01)    VALUE 'N'.               
               88  TINVPAR-PROCESS-COMPLETE            VALUE 'Y'.               
           05  PS-TUPCPLS-RTL-SW          PIC X(01)    VALUE 'N'.               
               88  PS-STORE-RTL-FOUND                  VALUE 'Y'.               
               88  PS-STORE-RTL-NOT-FOUND              VALUE 'N'.               
               88  PS-CORP-RTL-FOUND                   VALUE 'C'.               
               88  PS-CORP-RTL-NOT-FOUND               VALUE 'X'.               
W33304     05  PS-TINVPAR-TINCNTL-SW      PIC X(01)    VALUE 'N'.               
W33304         88  PS-STORE-FOUND                      VALUE 'Y'.               
W33304         88  PS-STORE-NOT-FOUND                  VALUE 'N'.               
                                                                                
       01  WS-TINVPAR-TABLE.                                                    
           05  WS-TINVPAR-TABLE-ELEMENTS   OCCURS 9999 TIMES.                   
               10  WS-PLND-INV-DTE  PIC X(10)  VALUE SPACES.                    
                                                                                
      *****************************************************************         
      *  INVENTORY EXCEPTIONS BY STORE EXTRACT FILE LAYOUT - IN/OUT             
      *****************************************************************         
           COPY INRD024.                                                        
W33304                                                                          
W33304*****************************************************************         
W33304*  INVENTORY EXCEPTIONS BY STORE EXTRACT FILE LAYOUT - IN/OUT             
W33304*****************************************************************         
W33304     COPY INRD032.                                                        
                                                                                
      *****************************************************************         
      * ERROR MESSAGE AREA FOR DB2                                              
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * INT_SKU_STYL_DLT DCLGEN                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE INT00003                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TMITGPL DCLGEN  - FOR GROUP PRICING                                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMITGPL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TUPCPLS DCLGEN                                                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
                                                                                
W33304*****************************************************************         
W33304* TINCNTL DCLGEN                                                          
W33304*****************************************************************         
W33304     EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
      *****************************************************************         
      * TINVPAR DCLGEN                                                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TINVPAR CURSOR.                                                         
      ******************************************************************        
           EXEC SQL                                                             
             DECLARE TINVPAR_CURSOR CURSOR FOR                                  
W33304         SELECT A.LOC_NBR,                                                
W33304                A.PLND_INV_DTE                                            
W33304         FROM TINVPAR A,                                                  
W33304              TINCNTL B                                                   
W33304         WHERE A.ACTL_FIN_BK_DTE = '9999-09-09'                           
W33304           AND A.INV_ID = B.INV_ID                                        
W33304           AND A.LOC_INV_STAT_CDE = 'IN'                                  
W33304           AND B.ACTV_IND = 'Y'                                           
               FOR FETCH ONLY                                                   
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * SQL ERROR HANDLING.                                                     
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      /*****************************************************************        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0100-MAINLINE-INKUT013.                                                  
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INV-EXCEPTS-RECS                                
               UNTIL PS-INV-EXCEPTS-IS-DONE.                                    
                                                                                
           PERFORM 8000-EOJ.                                                    
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /*****************************************************************        
      *  OPEN FILES AND READ THE FIRST RECORD OF EACH INPUT FILE.               
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  INV-EXCEPTS-FILE                                         
                OUTPUT MERGE-FILE-OUT                                           
                       MERGE-FILE-2-OUT.                                        
                                                                                
      *** LOAD THE TINVPAR TABLE....                                            
           INITIALIZE      WS-TINVPAR-TABLE.                                    
           MOVE ZEROS      TO PV-STR-NDX.                                       
           MOVE 'N'        TO PS-TINVPAR-DONE-SW.                               
           PERFORM 8200-OPEN-TINVPAR.                                           
           PERFORM 8300-LOAD-TINVPAR-TABLE                                      
               UNTIL TINVPAR-PROCESS-COMPLETE  OR                               
                     PV-STR-NDX IS GREATER THAN 9999.                           
           PERFORM 8400-CLOSE-TINVPAR.                                          
                                                                                
W33304     PERFORM 5000-READ-INV-EXCEPTS                                        
W33304         UNTIL PS-STORE-FOUND                                             
W33304            OR PS-INV-EXCEPTS-IS-DONE.                                    
                                                                                
           DISPLAY '***********************************************'.           
           DISPLAY '*  START OF INKUT013                          *'.           
           DISPLAY '***********************************************'.           
      *                                                                         
      ******************************************************************        
      * - 2000-PROCESS-INV-EXCEPTS-RECS.                                        
      * - OBTAIN RETAIL AMOUNT FOR EACH UNIQUE SKU/STORE RECORD AND             
      *   CREATE A RECORD FOR BOTH OUTPUT FILES.                                
      ******************************************************************        
       2000-PROCESS-INV-EXCEPTS-RECS.                                           
                                                                                
           IF IN024-INTR-UPC-ID = PV-SAVE-INTR-UPC-ID                           
             AND IN024-STORE-NUMBER = PV-SAVE-LOC                               
               MOVE PV-SAVE-SYS-RTL        TO IN024-SYSTEM-PRICE        INRD024 
               MOVE PV-SAVE-SYS-RTL        TO PV-UNT-RTL-AMT            INRD024 
           ELSE                                                                 
               PERFORM 3000-GET-SYSTEM-RTL                                      
               MOVE IN024-INTR-UPC-ID      TO PV-SAVE-INTR-UPC-ID               
               MOVE IN024-STORE-NUMBER     TO PV-SAVE-LOC                       
               MOVE PV-UNT-RTL-AMT         TO IN024-SYSTEM-PRICE        INRD024 
                                              PV-SAVE-SYS-RTL           INRD024 
               MOVE PV-F2-UNT-RTL-AMT      TO PV-SAVE-F2-SYS-RTL        INRD024 
           END-IF.                                                              
                                                                                
           PERFORM 4000-CALC-RTL-VARIANCE.                                      
           MOVE PV-VARIANCE-PRICE        TO IN024-VARIANCE-PRICE.               
           MOVE PV-ABSOLUTE-VAR-PRICE    TO IN024-ABSOLUTE-VAR-PRICE.           
                                                                                
           PERFORM 6000-WRITE-EXTRACT-REC.                                      
                                                                                
           MOVE PV-SAVE-F2-SYS-RTL         TO IN024-SYSTEM-PRICE.       INRD024 
           MOVE PV-SAVE-F2-SYS-RTL         TO PV-UNT-RTL-AMT.           INRD024 
                                                                                
           PERFORM 4000-CALC-RTL-VARIANCE.                                      
           MOVE PV-VARIANCE-PRICE          TO IN024-VARIANCE-PRICE.             
           MOVE PV-ABSOLUTE-VAR-PRICE      TO IN024-ABSOLUTE-VAR-PRICE.         
                                                                                
           PERFORM 6010-WRITE-EXTRACT-F2-REC.                                   
                                                                        INRD024 
W33304     SET PS-STORE-NOT-FOUND TO TRUE.                                      
W33304     PERFORM 5000-READ-INV-EXCEPTS                                        
W33304         UNTIL PS-STORE-FOUND                                             
W33304            OR PS-INV-EXCEPTS-IS-DONE.                                    
                                                                                
      ******************************************************************INKUT102
      * GET THE SYSTEM PRICING FROM TUPCPLS.  FIND INDIVIDUAL STORE     INKUT102
      * RETAIL (IF IT EXISTS).  GET SYSTEM PRICING FROM INT_SKU_STYL_DLTINKUT102
      * IF THE SKU IS NOT FOUND ON TUPCPLS.                                     
      ******************************************************************INKUT102
       3000-GET-SYSTEM-RTL.                                                     
                                                                        INKUT102
           MOVE ZEROS TO PV-UNT-RTL-AMT                                         
                         PV-F2-UNT-RTL-AMT                                      
                         PV-SAVE-SYS-RTL                                        
                         PV-SAVE-F2-SYS-RTL.                                    
                                                                                
           MOVE IN024-INTR-UPC-ID            TO UPCPLS-UPC-ID.          INRD024 
           MOVE IN024-STORE-NUMBER           TO UPCPLS-STR-NBR          INRD024 
                                                PV-STR-NDX.             INRD024 
           MOVE WS-PLND-INV-DTE (PV-STR-NDX) TO PV-PLND-INV-DTE.                
           MOVE PV-PLND-INV-TMST             TO UPCPLS-EFF-BEG-TMST     INKUT102
                                                UPCPLS-EFF-END-TMST.    INKUT102
                                                                                
      ***  ONLY GET PRICE IF UPC ID IS VALID ELSE DEFAULT TO ZERO               
           IF IN024-INTR-UPC-ID > 0                                             
      *** I.   STORE SPECIFIC PRICING                                           
               PERFORM 3300-GET-STORE-SPECIFIC-RTL                              
               IF  PS-STORE-RTL-FOUND                                           
                   PERFORM 3010-CALC-STORE-RTL-AMT                              
               ELSE                                                             
      *** II.      DEFAULT STORE PRICING <CORPORATE>                            
                   IF IN024-INTR-UPC-ID = PV-SAVE-CORP-INTR-UPC-ID              
                       SET  PS-CORP-RTL-FOUND    TO TRUE                 INKUT10
                       MOVE PV-SAVE-CORP-UNT-RTL-AMT TO PV-UNT-RTL-AMT  INKUT102
                       MOVE PV-SAVE-CORP-MEITGP-NBR TO MITGPL-MEITGP-NBRINKUT102
                       MOVE PV-SAVE-CORP-UPC-STAT-CDE TO PV-UPC-STAT-CDEINKUT102
                   ELSE                                                         
                       PERFORM 3400-GET-DEFAULT-STORE-RTL                       
                   END-IF                                                       
                   IF  PS-CORP-RTL-FOUND                                        
                       PERFORM 3020-CALC-CORP-RTL-AMT                           
                   ELSE                                                         
      *** III.         DELETED SKU STORE SPECIFIC PRICING                       
                       PERFORM 3700-GET-DLT-SKU-STORE-RTL                       
                       IF  PS-STORE-RTL-FOUND                                   
                           PERFORM 3010-CALC-STORE-RTL-AMT                      
                       ELSE                                                     
      *** IV.              DELETED SKU DEFAULT STORE PRICING <CORPORATE>        
                           PERFORM 3800-GET-DLT-DEFAULT-STORE-RTL               
                           IF  PS-CORP-RTL-FOUND                                
                               PERFORM 3020-CALC-CORP-RTL-AMT                   
                           ELSE                                                 
      *** V.                   DELETED SKU DEFAULT STORE ROW NOT FOUND          
                               MOVE ZEROES TO PV-UNT-RTL-AMT                    
                                              PV-F2-UNT-RTL-AMT                 
                               ADD +1 TO PV-SKUS-NOT-FOUND                      
                               DISPLAY '** SKU RETAIL NOT FOUND FOR '           
                                       'SKU: ' IN024-SKU                        
                                       '  UPC ID: ' IN024-INTR-UPC-ID           
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************INKUT102
      * CALCULATE THE UNIT RETAIL AMOUNT FOR AN INDIVIDUAL STORE.       INKUT102
      ******************************************************************INKUT102
       3010-CALC-STORE-RTL-AMT.                                                 
                                                                                
           MOVE PV-UNT-RTL-AMT TO PV-F2-UNT-RTL-AMT.                            
                                                                                
      ***  A.  BYPASS - GIFT WITH PURCHASE                                      
           IF  PV-UNT-RTL-AMT LESS PC-GWP-CEILING                               
               MOVE ZEROS  TO PV-UNT-RTL-AMT                                    
           ELSE                                                                 
      ***      B.  BYPASS - CLEARANCE MERCHANDISE                               
               IF  PV-UPC-STAT-CDE EQUAL PC-CLEARANCE-STAT                      
                   MOVE ZEROS  TO PV-UNT-RTL-AMT                                
               END-IF                                                           
           END-IF.                                                              
                                                                        INKUT102
      ******************************************************************INKUT102
      * CALCULATE THE UNIT RETAIL AMOUNT FOR THE CORPORATE STORE.       INKUT102
      ******************************************************************INKUT102
       3020-CALC-CORP-RTL-AMT.                                                  
                                                                                
           PERFORM 3500-CHECK-GROUP-PRICING.                                    
           MOVE PV-UNT-RTL-AMT TO PV-F2-UNT-RTL-AMT.                            
                                                                                
      ***  A.  BYPASS - GIFT WITH PURCHASE                                      
           IF  PV-UNT-RTL-AMT LESS PC-GWP-CEILING                               
               MOVE ZEROS  TO PV-UNT-RTL-AMT                                    
           ELSE                                                                 
      ***      B.  BYPASS - CLEARANCE MERCHANDISE                               
               IF  PV-UPC-STAT-CDE EQUAL PC-CLEARANCE-STAT                      
                 OR PC-REGIONAL-STAT                                            
                   MOVE ZEROS  TO PV-UNT-RTL-AMT                                
               END-IF                                                           
           END-IF.                                                              
                                                                        INKUT102
      ******************************************************************INKUT102
      * USE UPC ID FROM THE INPUT FILE TO FIND CORRESPONDING SKU ON     INKUT102
      * TUPCPLS. FIND INDIVIDUAL STORE RETAIL (IF IT EXISTS).           INKUT102
      ******************************************************************INKUT102
       3300-GET-STORE-SPECIFIC-RTL.                                             
      *                                                                 INKUT102
           PERFORM                                                      INKUT102
               WITH TEST AFTER                                          INKUT102
               VARYING PV-SQL-SUB FROM 1 BY 1                           INKUT102
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES)                      INKUT102
                   OR (SQLCODE = 0)                                     INKUT102
                   OR (SQLCODE = +100)                                  INKUT102
                                                                        INKUT102
               EXEC SQL                                                 INKUT102
                    SELECT UNT_RTL_AMT                                  INKUT102
                          ,UPC_STAT_CDE                                         
                    INTO   :UPCPLS-UNT-RTL-AMT                          INKUT102
                          ,:UPCPLS-UPC-STAT-CDE                                 
                    FROM  TUPCPLS                                       INKUT102
                 WHERE  UPC_ID        = :UPCPLS-UPC-ID                          
                 AND    STR_NBR       = :UPCPLS-STR-NBR                         
                 AND    EFF_BEG_TMST <= :UPCPLS-EFF-BEG-TMST                    
                 AND   (EFF_END_TMST >= :UPCPLS-EFF-END-TMST                    
                 OR     EFF_END_TMST IS NULL)                                   
               WITH UR                                                          
               END-EXEC                                                 INKUT102
                                                                        INKUT102
               EVALUATE TRUE                                            INKUT102
                   WHEN SQLCODE = 0                                     INKUT102
                      SET  PS-STORE-RTL-FOUND   TO TRUE                 INKUT102
                      MOVE UPCPLS-UNT-RTL-AMT   TO PV-UNT-RTL-AMT       INKUT102
                      MOVE UPCPLS-UPC-STAT-CDE  TO PV-UPC-STAT-CDE      INKUT102
                      ADD +1                    TO PV-TUPCPLS-STRS-READ         
                   WHEN SQLCODE = +100                                  INKUT102
                      SET  PS-STORE-RTL-NOT-FOUND TO TRUE               INKUT102
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                      CONTINUE                                                  
                   WHEN OTHER                                           INKUT102
                      MOVE    '3300-GET-STORE-SPECIFIC-RTL'             INKUT102
                                          TO PV-PARAGRAPH-NAME          INKUT102
                      MOVE    'READING TUPCPLS TABLE'                   INKUT102
                                          TO  PV-DB2-OPER-ATTEMPTED     INKUT102
                      DISPLAY 'SKU:       '  IN024-SKU                          
                      DISPLAY 'UPC ID:    '  UPCPLS-UPC-ID                      
                      DISPLAY 'INV STORE: '  IN024-STORE-NUMBER                 
                      DISPLAY 'PLANNED INV DATE TIMESTAMP: '                    
                                              PV-PLND-INV-DTE                   
                      PERFORM 9000-SQL-ABEND                            INKUT102
               END-EVALUATE                                             INKUT102
           END-PERFORM.                                                 INKUT102
                                                                        INKUT102
           IF  PV-SQL-SUB > PC-MAX-RETRIES                              INKUT102
               MOVE '3300-GET-STORE-SPECIFIC-RTL'                       INKUT102
                                            TO PV-PARAGRAPH-NAME        INKUT102
               MOVE 'MAX RETRIES EXCEEDED'  TO PV-DB2-OPER-ATTEMPTED    INKUT102
               DISPLAY 'SKU:       '  IN024-SKU                                 
               DISPLAY 'UPC ID:    '  UPCPLS-UPC-ID                             
               DISPLAY 'INV STORE: '  IN024-STORE-NUMBER                        
               DISPLAY 'PLANNED INV DATE TIMESTAMP: ' PV-PLND-INV-DTE           
               PERFORM 9000-SQL-ABEND                                   INKUT102
           END-IF.                                                      INKUT102
                                                                                
      ******************************************************************INKUT102
      * USE UPC ID FROM THE INPUT FILE TO FIND CORRESPONDING SKU ON     INKUT102
      * TUPCPLS. FIND CORPORATE STORE RETAIL (IF IT EXISTS).            INKUT102
      ******************************************************************INKUT102
       3400-GET-DEFAULT-STORE-RTL.                                              
      *                                                                 INKUT102
           PERFORM                                                      INKUT102
               WITH TEST AFTER                                          INKUT102
               VARYING PV-SQL-SUB FROM 1 BY 1                           INKUT102
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES)                      INKUT102
                   OR (SQLCODE = 0)                                     INKUT102
                   OR (SQLCODE = +100)                                  INKUT102
                                                                        INKUT102
               EXEC SQL                                                 INKUT102
                    SELECT UNT_RTL_AMT                                  INKUT102
                          ,MEITGP_NBR                                           
                          ,UPC_STAT_CDE                                         
                    INTO   :UPCPLS-UNT-RTL-AMT                          INKUT102
                          ,:UPCPLS-MEITGP-NBR                                   
                          ,:UPCPLS-UPC-STAT-CDE                                 
                    FROM  TUPCPLS                                       INKUT102
                 WHERE  UPC_ID        = :UPCPLS-UPC-ID                          
                 AND    STR_NBR       = :PC-DEFAULT-LOC                         
                 AND    EFF_BEG_TMST <= :UPCPLS-EFF-BEG-TMST                    
                 AND   (EFF_END_TMST >= :UPCPLS-EFF-END-TMST                    
                 OR     EFF_END_TMST IS NULL)                                   
               WITH UR                                                          
               END-EXEC                                                 INKUT102
                                                                        INKUT102
               EVALUATE TRUE                                            INKUT102
                   WHEN SQLCODE = 0                                     INKUT102
                      SET  PS-CORP-RTL-FOUND    TO TRUE                 INKUT102
                      MOVE IN024-INTR-UPC-ID                                    
                            TO PV-SAVE-CORP-INTR-UPC-ID                         
                      MOVE UPCPLS-UNT-RTL-AMT   TO PV-UNT-RTL-AMT       INKUT102
                                                PV-SAVE-CORP-UNT-RTL-AMT        
                      MOVE UPCPLS-MEITGP-NBR    TO MITGPL-MEITGP-NBR    INKUT102
                                                PV-SAVE-CORP-MEITGP-NBR         
                      MOVE UPCPLS-UPC-STAT-CDE  TO PV-UPC-STAT-CDE      INKUT102
                                               PV-SAVE-CORP-UPC-STAT-CDE        
                      ADD +1                    TO PV-TUPCPLS-CORP-READ         
                   WHEN SQLCODE = +100                                  INKUT102
                      SET PS-CORP-RTL-NOT-FOUND TO TRUE                 INKUT102
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                      CONTINUE                                                  
                   WHEN OTHER                                           INKUT102
                      MOVE    '3400-GET-DEFAULT-STORE-RTL'              INKUT102
                                          TO PV-PARAGRAPH-NAME          INKUT102
                      MOVE    'READING UPCPLS TABLE'                    INKUT102
                                          TO  PV-DB2-OPER-ATTEMPTED     INKUT102
                      DISPLAY 'SKU:       '  IN024-SKU                          
                      DISPLAY 'UPC ID:    '  UPCPLS-UPC-ID                      
                      DISPLAY 'INV STORE: '  IN024-STORE-NUMBER                 
                      DISPLAY 'PLANNED INV DATE TIMESTAMP: '                    
                                              PV-PLND-INV-DTE                   
                      PERFORM 9000-SQL-ABEND                            INKUT102
               END-EVALUATE                                             INKUT102
           END-PERFORM.                                                 INKUT102
                                                                        INKUT102
           IF  PV-SQL-SUB > PC-MAX-RETRIES                              INKUT102
               MOVE '3400-GET-DEFAULT-STORE-RTL'                        INKUT102
                                            TO PV-PARAGRAPH-NAME        INKUT102
               MOVE 'MAX RETRIES EXCEEDED'  TO PV-DB2-OPER-ATTEMPTED    INKUT102
               DISPLAY 'SKU:       '  IN024-SKU                                 
               DISPLAY 'UPC ID:    '  UPCPLS-UPC-ID                             
               DISPLAY 'INV STORE: '  IN024-STORE-NUMBER                        
               DISPLAY 'PLANNED INV DATE TIMESTAMP: ' PV-PLND-INV-DTE           
               PERFORM 9000-SQL-ABEND                                   INKUT102
           END-IF.                                                      INKUT102
                                                                        INKUT102
                                                                                
      ******************************************************************        
      *  CALCULATE THE DIFFERENCE BETWEEN THE PHYSICAL INVENTORY RETAIL         
      *  AND THE SYSTEM RETAIL RECORD                                           
      ******************************************************************        
       3500-CHECK-GROUP-PRICING.                                                
                                                                                
           IF MITGPL-MEITGP-NBR NOT EQUAL 0                                     
               PERFORM 3600-CHECK-GRP-PRC                                       
               IF SQLCODE = 0                                                   
                   COMPUTE PV-GROUP-UNIT-RTL ROUNDED =                          
                                              (MITGPL-GP-AMT /                  
                                               MITGPL-MITGPL-QTY )              
                   MOVE PV-GROUP-UNIT-RTL  TO PV-UNT-RTL-AMT                    
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * USE ITEM NUMBER RETRIEVED FROM TSK AND FIND CORRESPONDING ITEM  INKUT102
      * ON TSKST. FIND INDIVIDUAL STORE 'STATUS CODE' AND 'CUTOFF PRICE'INKUT102
      ******************************************************************INKUT102
       3600-CHECK-GRP-PRC.                                                      
                                                                        INKUT102
           PERFORM                                                      INKUT102
               WITH TEST AFTER                                          INKUT102
               VARYING PV-SQL-SUB FROM 1 BY 1                           INKUT102
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES)                      INKUT102
                   OR (SQLCODE = 0)                                     INKUT102
                   OR (SQLCODE = +100)                                  INKUT102
                                                                        INKUT102
               EXEC SQL                                                         
                   SELECT  MITGPL_QTY                                           
                          ,GP_AMT                                               
                   INTO    :MITGPL-MITGPL-QTY                                   
                          ,:MITGPL-GP-AMT                                       
                   FROM   TMITGPL                                               
                   WHERE  OPERCO_NBR    = '03'                                  
                     AND  MEITGP_NBR    = :MITGPL-MEITGP-NBR                    
                     AND  ADLOC_ID      = '   '                                 
                     AND  (CURRENT DATE BETWEEN                                 
                           BEG_DTE AND END_DTE)                                 
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                      CONTINUE                                                  
                   WHEN OTHER                                           INKUT102
                       MOVE '3600-CHECK-GRP-PRC'                                
                                          TO PV-PARAGRAPH-NAME                  
                       MOVE 'GET GROUP PRICING'                                 
                                          TO  PV-DB2-OPER-ATTEMPTED             
                       DISPLAY 'SKU:        '  IN024-SKU                        
                       DISPLAY 'MEITGP NBR: '  MITGPL-MEITGP-NBR                
                       PERFORM 9000-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                 INKUT102
                                                                        INKUT102
           IF  PV-SQL-SUB > PC-MAX-RETRIES                              INKUT102
               MOVE '3600-CHECK-GRP-PRC'    TO PV-PARAGRAPH-NAME        INKUT102
               MOVE 'MAX RETRIES EXCEEDED'  TO PV-DB2-OPER-ATTEMPTED    INKUT102
               DISPLAY 'SKU:        '  IN024-SKU                                
               DISPLAY 'MEITGP NBR: '  MITGPL-MEITGP-NBR                        
               PERFORM 9000-SQL-ABEND                                   INKUT102
           END-IF.                                                      INKUT102
                                                                                
      ******************************************************************INKUT102
      * USE UPC ID FROM THE INPUT FILE TO FIND CORRESPONDING SKU ON     INKUT102
      * INT_SKU_STYL_DLT. FIND INDIVIDUAL STORE RETAIL (IF IT EXISTS).  INKUT102
      ******************************************************************INKUT102
       3700-GET-DLT-SKU-STORE-RTL.                                              
      *                                                                 INKUT102
           MOVE IN024-INTR-UPC-ID            TO INT00003-UPC-ID.        INRD024 
           MOVE IN024-STORE-NUMBER           TO INT00003-LOC-NBR.       INRD024 
           MOVE IN024-SKU                    TO INT00003-SKU-NBR.       INRD024 
                                                                                
           PERFORM                                                      INKUT102
               WITH TEST AFTER                                          INKUT102
               VARYING PV-SQL-SUB FROM 1 BY 1                           INKUT102
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES)                      INKUT102
                   OR (SQLCODE = 0)                                     INKUT102
                   OR (SQLCODE = +100)                                  INKUT102
                                                                        INKUT102
               EXEC SQL                                                 INKUT102
                    SELECT UNT_RTL_AMT                                  INKUT102
                          ,UPC_STAT_CDE                                         
                    INTO   :INT00003-UNT-RTL-AMT                        INKUT102
                          ,:INT00003-UPC-STAT-CDE                               
                    FROM  INT_SKU_STYL_DLT                              INKUT102
                 WHERE  UPC_ID        = :INT00003-UPC-ID                        
                 AND    LOC_NBR       = :INT00003-LOC-NBR                       
                 AND    SKU_NBR       = :INT00003-SKU-NBR                       
               WITH UR                                                          
               END-EXEC                                                 INKUT102
                                                                        INKUT102
               EVALUATE TRUE                                            INKUT102
                   WHEN SQLCODE = 0                                     INKUT102
                      SET  PS-STORE-RTL-FOUND    TO TRUE                INKUT102
                      MOVE INT00003-UNT-RTL-AMT  TO PV-UNT-RTL-AMT      INKUT102
                      MOVE INT00003-UPC-STAT-CDE TO PV-UPC-STAT-CDE     INKUT102
                      ADD +1 TO PV-INTSKUSD-ROWS-READ                           
                   WHEN SQLCODE = +100                                  INKUT102
                      SET  PS-STORE-RTL-NOT-FOUND TO TRUE               INKUT102
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                      CONTINUE                                                  
                   WHEN OTHER                                           INKUT102
                      MOVE    '3700-GET-DLT-SKU-STORE-RTL'              INKUT102
                                          TO PV-PARAGRAPH-NAME          INKUT102
                      MOVE    'READING INT_SKU_STYL_DLT TABLE'          INKUT102
                                          TO  PV-DB2-OPER-ATTEMPTED     INKUT102
                      DISPLAY 'SKU:       '  IN024-SKU                          
                      DISPLAY 'UPC ID:    '  INT00003-UPC-ID                    
                      DISPLAY 'INV STORE: '  IN024-STORE-NUMBER                 
                      PERFORM 9000-SQL-ABEND                            INKUT102
               END-EVALUATE                                             INKUT102
           END-PERFORM.                                                 INKUT102
                                                                        INKUT102
           IF  PV-SQL-SUB > PC-MAX-RETRIES                              INKUT102
               MOVE '3700-GET-DLT-SKU-STORE-RTL'                        INKUT102
                                            TO PV-PARAGRAPH-NAME        INKUT102
               MOVE 'MAX RETRIES EXCEEDED'  TO PV-DB2-OPER-ATTEMPTED    INKUT102
               DISPLAY 'SKU:       '  IN024-SKU                                 
               DISPLAY 'UPC ID:    '  INT00003-UPC-ID                           
               DISPLAY 'INV STORE: '  IN024-STORE-NUMBER                        
               PERFORM 9000-SQL-ABEND                                   INKUT102
           END-IF.                                                      INKUT102
                                                                                
      ******************************************************************INKUT102
      * USE UPC ID FROM THE INPUT FILE TO FIND CORRESPONDING SKU ON     INKUT102
      * INT_SKU_STYL_DLT. FIND CORPORATE STORE RETAIL (IF IT EXISTS).   INKUT102
      ******************************************************************INKUT102
       3800-GET-DLT-DEFAULT-STORE-RTL.                                          
      *                                                                 INKUT102
           PERFORM                                                      INKUT102
               WITH TEST AFTER                                          INKUT102
               VARYING PV-SQL-SUB FROM 1 BY 1                           INKUT102
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES)                      INKUT102
                   OR (SQLCODE = 0)                                     INKUT102
                   OR (SQLCODE = +100)                                  INKUT102
                                                                        INKUT102
               EXEC SQL                                                 INKUT102
                    SELECT UNT_RTL_AMT                                  INKUT102
                          ,UPC_STAT_CDE                                         
                          ,MEITGP_NBR                                           
                    INTO   :INT00003-UNT-RTL-AMT                        INKUT102
                          ,:INT00003-UPC-STAT-CDE                               
                          ,:INT00003-MEITGP-NBR                                 
                    FROM  INT_SKU_STYL_DLT                              INKUT102
                 WHERE  UPC_ID        = :INT00003-UPC-ID                        
                 AND    LOC_NBR       = :PC-DEFAULT-LOC                         
                 AND    SKU_NBR       = :INT00003-SKU-NBR                       
               WITH UR                                                          
               END-EXEC                                                 INKUT102
                                                                        INKUT102
               EVALUATE TRUE                                            INKUT102
                   WHEN SQLCODE = 0                                     INKUT102
                      SET  PS-CORP-RTL-FOUND     TO TRUE                INKUT102
                      MOVE IN024-INTR-UPC-ID                                    
                            TO PV-SAVE-CORP-INTR-UPC-ID                         
                      MOVE INT00003-UNT-RTL-AMT  TO PV-UNT-RTL-AMT      INKUT102
                                                PV-SAVE-CORP-UNT-RTL-AMT        
                      MOVE INT00003-UPC-STAT-CDE TO PV-UPC-STAT-CDE     INKUT102
                                               PV-SAVE-CORP-UPC-STAT-CDE        
                      MOVE INT00003-MEITGP-NBR   TO MITGPL-MEITGP-NBR   INKUT102
                                                PV-SAVE-CORP-MEITGP-NBR         
                      ADD +1 TO PV-INTSKUSD-ROWS-READ                           
                   WHEN SQLCODE = +100                                  INKUT102
                      SET  PS-CORP-RTL-NOT-FOUND TO TRUE                INKUT102
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                      CONTINUE                                                  
                   WHEN OTHER                                           INKUT102
                      MOVE    '3800-GET-DLT-DEFAULT-STORE-RTL'          INKUT102
                                          TO PV-PARAGRAPH-NAME          INKUT102
                      MOVE    'READING INT_SKU_STYL_DLT TABLE'          INKUT102
                                          TO  PV-DB2-OPER-ATTEMPTED     INKUT102
                      DISPLAY 'SKU:       '  IN024-SKU                          
                      DISPLAY 'UPC ID:    '  INT00003-UPC-ID                    
                      DISPLAY 'INV STORE: '  IN024-STORE-NUMBER                 
                      PERFORM 9000-SQL-ABEND                            INKUT102
               END-EVALUATE                                             INKUT102
           END-PERFORM.                                                 INKUT102
                                                                        INKUT102
           IF  PV-SQL-SUB > PC-MAX-RETRIES                              INKUT102
               MOVE '3800-GET-DLT-DEFAULT-STORE-RTL'                    INKUT102
                                            TO PV-PARAGRAPH-NAME        INKUT102
               MOVE 'MAX RETRIES EXCEEDED'  TO PV-DB2-OPER-ATTEMPTED    INKUT102
               DISPLAY 'SKU:       '  IN024-SKU                                 
               DISPLAY 'UPC ID:    '  INT00003-UPC-ID                           
               DISPLAY 'INV STORE: '  IN024-STORE-NUMBER                        
               PERFORM 9000-SQL-ABEND                                   INKUT102
           END-IF.                                                      INKUT102
                                                                                
                                                                        INKUT102
      ******************************************************************        
      *  CALCULATE THE DIFFERENCE BETWEEN THE PHYSICAL INVENTORY RETAIL         
      *  AND THE SYSTEM RETAIL RECORD                                           
      ******************************************************************        
       4000-CALC-RTL-VARIANCE.                                                  
                                                                                
      * IF ONLY CUTOFF RECORD EXISTS, THEN RETAIL VARIANCE SHOULD = 0           
      *                                                                         
           IF NOT IN024-CTOFF-DB-ONLY                                           
               IF IN024-AREA-NUMBER NOT EQUAL PC-AREA-HEADER                    
      *                                                                         
                   COMPUTE PV-VARIANCE-PRICE  = (IN024-PRICE                    
                                              -  PV-UNT-RTL-AMT)                
                   IF PV-VARIANCE-PRICE < 0                                     
                     COMPUTE PV-ABSOLUTE-VAR-PRICE = (PV-VARIANCE-PRICE         
                                                   *  PC-NEG-VALUE)             
                   ELSE                                                         
                     MOVE PV-VARIANCE-PRICE TO PV-ABSOLUTE-VAR-PRICE            
                   END-IF                                                       
               ELSE                                                             
                   MOVE ZEROES TO PV-VARIANCE-PRICE                             
                                  PV-ABSOLUTE-VAR-PRICE                         
               END-IF                                                           
           ELSE                                                                 
               MOVE ZEROES TO PV-VARIANCE-PRICE                                 
                              PV-ABSOLUTE-VAR-PRICE                             
           END-IF.                                                              
                                                                                
      *                                                                         
      *****************************************************************         
      * 5000-READ-INV-EXCEPTS. READ AN INVENTORY EXCEPTIONS FILE REC.           
      *****************************************************************         
       5000-READ-INV-EXCEPTS.                                                   
                                                                                
           READ INV-EXCEPTS-FILE INTO IN024-INV-EXCEPTS-STR-REC                 
             AT END                                                             
                 MOVE 'Y' TO PS-INV-EXCEPTS-DONE                                
             NOT AT END                                                         
                 ADD +1   TO PV-INV-EXCEPTS-READ                                
W33304           SET PS-STORE-NOT-FOUND TO TRUE                                 
           END-READ.                                                            
                                                                                
W33304     MOVE IN024-STORE-NUMBER TO INVPAR-LOC-NBR.                           
W33304     PERFORM 5010-READ-TINVPAR-TINCNTL.                                   
W33304                                                                          
W33304******************************************************************        
W33304* 5010-READ-TINVPAR-TINCNTL.                                              
W33304*  READ TINVPAR/TINCNTL DATA FOR THIS RECORD TO GET                       
W33304*  STORE AND FIN BOOK DATE DATA NEEDED IN INKRP285 PROGRAM                
W33304******************************************************************        
W33304                                                                          
W33304  5010-READ-TINVPAR-TINCNTL.                                              
W33304     EXEC SQL                                                             
W33304         SELECT B.PLND_FIN_BKG_DTE                                        
W33304               ,B.INV_ID                                                  
W33304           INTO :IN032-FNCL-BKG-DTE                                       
W33304               ,:IN032-INV-ID                                             
W33304         FROM TINVPAR A,                                                  
W33304              TINCNTL B                                                   
W33304         WHERE A.ACTL_FIN_BK_DTE = '9999-09-09'                           
W33304           AND A.INV_ID = B.INV_ID                                        
W33304           AND B.ACTV_IND = 'Y'                                           
W33304           AND A.LOC_INV_STAT_CDE = 'IN'                                  
W33304           AND A.LOC_NBR = :INVPAR-LOC-NBR                                
W33304         WITH UR                                                          
W33304     END-EXEC.                                                            
W33304                                                                          
W33304     EVALUATE SQLCODE                                                     
W33304         WHEN ZERO                                                        
W33304           SET PS-STORE-FOUND     TO TRUE                                 
W33304         WHEN +100                                                        
W33304           SET PS-STORE-NOT-FOUND TO TRUE                                 
W33304         WHEN OTHER                                                       
W33304           SET PS-STORE-NOT-FOUND TO TRUE                                 
W33304           MOVE '5000-READ-INV-EXCEPTS'  TO PV-PARAGRAPH-NAME             
W33304           MOVE 'READING TINVPAR-TINCNTL'                                 
W33304                                         TO PV-DB2-OPER-ATTEMPTED         
W33304           DISPLAY '5000-READ-INV-EXCEPTS'                                
W33304           DISPLAY 'DATABASE READ ERROR'                                  
W33304           DISPLAY 'SKU:       '  IN024-SKU                               
W33304           DISPLAY 'UPC ID:    '  UPCPLS-UPC-ID                           
W33304           DISPLAY 'INV STORE: '  IN024-STORE-NUMBER                      
W33304           DISPLAY 'PLANNED INV DATE TIMESTAMP: ' PV-PLND-INV-DTE         
W33304           DISPLAY 'INV STORE: '  IN024-STORE-NUMBER                      
W33304           PERFORM 9000-SQL-ABEND                                         
W33304     END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 6000-WRITE-EXTRACT-REC.                                                 
      *  CREATE A STORE INVENTORY EXCEPTIONS OUTPUT RECORD CONTAINING           
      *  SYSTEM PRICE INFORMATION.                                              
      ******************************************************************        
       6000-WRITE-EXTRACT-REC.                                                  
                                                                                
W33304     MOVE IN024-INV-EXCEPTS-STR-REC TO                                    
W33304          IN032-FIRST-100-BYTES.                                          
           WRITE MERGE-REC-OUT                                                  
W33304         FROM IN032-INV-EXCEPTS-STR-REC.                                  
                                                                                
           ADD +1 TO PV-TOTAL-EXTR-WRITTEN.                                     
                                                                                
      *                                                                         
      ******************************************************************        
      * 6010-WRITE-EXTRACT-F2-REC.                                              
      *  CREATE A STORE INVENTORY EXCEPTIONS OUTPUT RECORD CONTAINING           
      *  SYSTEM PRICE INFORMATION.                                              
      ******************************************************************        
       6010-WRITE-EXTRACT-F2-REC.                                               
                                                                                
           WRITE MERGE-REC-F2-OUT                                               
               FROM IN024-INV-EXCEPTS-STR-REC.                                  
                                                                                
           ADD +1 TO PV-TOTAL-EXTR-F2-WRITTEN.                                  
                                                                                
      *                                                                         
      ******************************************************************        
      * 8000-EOJ.  FINAL PROCESSING FOR THE PROGRAM.                            
      ******************************************************************        
       8000-EOJ.                                                                
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*** INKUT013 RECORD COUNT SUMMARY'.                         
           DISPLAY 'INVENTORY EXCEPTION RECORDS READ  = '                       
                 PV-INV-EXCEPTS-READ                                            
           DISPLAY 'INVENTORY EXTRACT RECORDS WRITTEN = '                       
                 PV-TOTAL-EXTR-WRITTEN.                                         
           DISPLAY 'INV EXTRACT FILE 2 RECS WRITTEN   = '                       
                 PV-TOTAL-EXTR-F2-WRITTEN.                                      
           DISPLAY 'TUPCPLS STORE ROWS SELECTED       = '                       
                 PV-TUPCPLS-STRS-READ.                                          
           DISPLAY 'TUPCPLS CORPORATE ROWS SELECTED   = '                       
                 PV-TUPCPLS-CORP-READ.                                          
           DISPLAY 'INT_SKU_STYL_DLT ROWS SELECTED    = '                       
                 PV-INTSKUSD-ROWS-READ.                                         
           DISPLAY 'SKUS NOT FOUND                    = '                       
                 PV-SKUS-NOT-FOUND.                                             
                                                                                
           DISPLAY '***********************************************'.           
           DISPLAY '*  END OF INKUT013                            *'.           
           DISPLAY '***********************************************'.           
                                                                                
           CLOSE INV-EXCEPTS-FILE                                               
                 MERGE-FILE-OUT                                                 
                 MERGE-FILE-2-OUT.                                              
                                                                                
      *****************************************************************         
      * 8200-OPEN-TINVPAR.                                                      
      *****************************************************************         
       8200-OPEN-TINVPAR.                                                       
      *                                                                         
           EXEC SQL                                                             
               OPEN TINVPAR_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE '8200-OPEN-TINVPAR' TO PV-PARAGRAPH-NAME                     
              MOVE 'OPEN CURSOR'       TO PV-DB2-OPER-ATTEMPTED                 
              MOVE 'Y'                 TO PS-TINVPAR-DONE-SW                    
              PERFORM 9000-SQL-ABEND                                            
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      * 8300-LOAD-TINVPAR-TABLE.                                                
      *****************************************************************         
       8300-LOAD-TINVPAR-TABLE.                                                 
      *                                                                         
           EXEC SQL                                                             
               FETCH TINVPAR_CURSOR                                             
               INTO :INVPAR-LOC-NBR,                                            
                    :INVPAR-PLND-INV-DTE                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               MOVE INVPAR-LOC-NBR      TO PV-STR-NDX                           
               MOVE INVPAR-PLND-INV-DTE TO WS-PLND-INV-DTE (PV-STR-NDX)         
             WHEN SQLCODE IS EQUAL TO +100                                      
               MOVE 'Y' TO PS-TINVPAR-DONE-SW                                   
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               MOVE '8300-LOAD-TINVPAR-TABLE' TO PV-PARAGRAPH-NAME              
               MOVE 'LOAD TABLE'        TO PV-DB2-OPER-ATTEMPTED                
               MOVE 'Y'        TO PS-TINVPAR-DONE-SW                            
               PERFORM 9000-SQL-ABEND                                           
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      * 8400-CLOSE-TINVPAR.                                                     
      *****************************************************************         
       8400-CLOSE-TINVPAR.                                                      
      *                                                                         
           EXEC SQL                                                             
               CLOSE TINVPAR_CURSOR                                             
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE '8400-CLOSE-TINVPAR' TO PV-PARAGRAPH-NAME                    
              MOVE 'CLOSE CURSOR'       TO PV-DB2-OPER-ATTEMPTED                
              MOVE 'Y'                  TO PS-TINVPAR-DONE-SW                   
              PERFORM 9000-SQL-ABEND                                            
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 9000-SQL-ABEND.  STANDARD DB2 ERROR ABEND ROUTINE                       
      *                  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PBLM.        
      ******************************************************************        
       9000-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
