00001  MATCH-HISTORY.                                                   04/19/88
00002                                                                   FSPD001U
00003 *****************************************************************    LV007
00004 *    TO USE THIS COPY MEMBER, THE PROGRAMMER MUST:                FSPD001U
00005 *                                                                 FSPD001U
00006 *                                                                 FSPD001U
00007 *    A) COPY MEMBER FSWS001U.                                        CL**5
00008 *    B) INITIALIZE MC-RENUM-SKU-TABLE (SEE FSWS001U)                 CL**5
00009 *       AND LOAD THE TABLE FROM PKMO.FS.RENUM.LOG                    CL**5
00010 *    C) READ A RECORD FROM THE FILE WHICH IS TO BE MATCHED AGAINST   CL**5
00011 *       MC-RENUM-SKU-TABLE.                                          CL**5
00012 *    D) MOVE THE SKU NUMBER TO MC-CHECK-SKU                          CL**5
00013 *       MOVE THE INHOUSE NUMBER TO MC-CHECK-INHOUSE (IF NEEDED)      CL**5
00014 *    E) PERFORM THIS PARAGRAPH AS FOLLOWS                            CL**5
00015 *       - PERFORM MATCH-HISTORY                                   FSPD001U
00016 *    F) AFTER THIS PARAGRAPH IS DONE, CHECK MC-SKU-MATCH-SW TO SEE   CL**5
00017 *       IF THE SKU HAS BEEN CHANGED.                                 CL**5
00018 *    G) GO BACK TO STEP C.                                           CL**5
00019 ***************************************************************** FSPD001U
00020                                                                   FSPD001U
00021      SET MC-NO-SKU-MATCH        TO TRUE.                          FSPD001U
00022      SET MC-NOT-DONE-WITH-TABLE TO TRUE.                          FSPD001U
00023      PERFORM FIND-MATCH.                                             CL**5
00024                                                                      CL**5
00025 ***************************************************************** FSPD001U
00026 *    IF THERE IS A MATCH IN THE RENUMBER TABLE, UPDATE THE        FSPD001U
00027 *    CHECK RECORD WITH THE NEW SKU AND VENDOR INHOUSE #              CL**5
00028 *    FROM THE RENUMBER TABLE AND CONTINUE SEARCHING THE              CL**5
00029 *    RENUMBER TABLE, UPDATING WHEN A MATCH IS FOUND ON THE           CL**5
00030 *    CHECK SKU.                                                      CL**5
00031 ***************************************************************** FSPD001U
00032                                                                   FSPD001U
00033      IF MC-SKU-MATCH                                                 CL**5
00034          MOVE MC-SKU(MC-INDEX1)          TO MC-CHECK-SKU             CL**5
00035          MOVE MC-INHOUSE-NMBR(MC-INDEX1) TO MC-CHECK-INHOUSE         CL**5
00036                                                                   FSPD001U
00037          SET MC-INDEX2 TO MC-INDEX1                                  CL**5
00038          SET MC-INDEX2 UP BY 1                                       CL**5
00039                                                                   FSPD001U
00040          PERFORM MULTIPLE-COPIES                                     CL**5
00041              UNTIL MC-DONE-WITH-TABLE                                CL**7
00042      END-IF.                                                         CL**7
00043  EJECT                                                               CL**6
00044  FIND-MATCH.                                                      FSPD001U
00045                                                                   FSPD001U
00046 ***************************************************************** FSPD001U
00047 *    SEARCH THE RENUMBER TABLE UNTIL A MATCH IS FOUND OR THE END     CL**3
00048 *    OF THE TABLE.                                                FSPD001U
00049 ***************************************************************** FSPD001U
00050                                                                   FSPD001U
00051      SET MC-INDEX1 TO 1.                                          FSPD001U
00052      SEARCH MC-TABLE-ENTRIES                                      FSPD001U
00053          VARYING MC-INDEX1                                        FSPD001U
00054          WHEN MC-HIST-SKU(MC-INDEX1) IS EQUAL TO MC-CHECK-SKU        CL**5
00055              SET MC-SKU-MATCH TO TRUE                             FSPD001U
00056          WHEN MC-HIST-SKU(MC-INDEX1) IS EQUAL TO SPACES           FSPD001U
00057              SET MC-NO-SKU-MATCH TO TRUE.                            CL**3
00058                                                                   FSPD001U
00059  MULTIPLE-COPIES.                                                 FSPD001U
00060                                                                   FSPD001U
00061 ***************************************************************** FSPD001U
00062 *    CONTINUE SEARCHING THE THE RENUMBER TABLE, UPDATING THE         CL**3
00063 *    CHECK SKU AND INHOUSE # AS MATCHES ARE FOUND.                   CL**5
00064 *    THE SEARCH WILL STOP AT THE END OF THE TABLE.                   CL**3
00065 ***************************************************************** FSPD001U
00066                                                                   FSPD001U
00067      SEARCH MC-TABLE-ENTRIES                                      FSPD001U
00068          VARYING MC-INDEX2                                        FSPD001U
00069          AT END                                                   FSPD001U
00070              SET MC-DONE-WITH-TABLE TO TRUE                          CL**2
00071          WHEN MC-HIST-SKU(MC-INDEX2) IS EQUAL TO SPACES           FSPD001U
00072              SET MC-DONE-WITH-TABLE TO TRUE                          CL**2
00073          WHEN MC-HIST-SKU(MC-INDEX2) IS EQUAL TO MC-CHECK-SKU        CL**5
00074              MOVE MC-SKU(MC-INDEX2)          TO MC-CHECK-SKU         CL**5
00075              MOVE MC-INHOUSE-NMBR(MC-INDEX2) TO MC-CHECK-INHOUSE     CL**5
00076              SET MC-INDEX2 UP BY 1.                               FSPD001U
00077  EJECT                                                               CL**6
