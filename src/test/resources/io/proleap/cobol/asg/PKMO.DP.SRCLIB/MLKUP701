       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP701                                          00030000
       AUTHOR.        BOB MCASEY.                                       00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  4-09-1999.                                        00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP701 - MAJOR CLASS ENT-ID M/L VENDOR ANALYSIS    *00100000
      *                      TABLE UPDATE (TVAMCL)                     *00110000
      ******************************************************************00110113
      *    WR/PITS#     DATE        DESCRIPTION                        *00110213
      *    --------   --------     ---------------------------------   *00110313
      *    10587      05/19/1999   CHANGED ALL OCCURANCES OF           *00110413
      *                            MKDN-PERM-RTL-AMT TO                *00110513
      *                            MKDN-RGST-RTL-AMT                   *00110613
      *    10587      12/06/1999   MKUP_INIT_AMT AND MKUP_NET_AMT CALCS*00110714
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION     00110816
      ******************************************************************00120000
                                                                        00130000
                                                                        00140000
       ENVIRONMENT DIVISION.                                            00150000
                                                                        00160000
       CONFIGURATION SECTION.                                           00170000
       SOURCE-COMPUTER.        IBM-3090.                                00180000
       OBJECT-COMPUTER.        IBM-3090.                                00190000
       INPUT-OUTPUT SECTION.                                            00200000
       FILE-CONTROL.                                                    00210000
           SELECT CONDENSED-FILE                                        00220000
               ASSIGN TO INP01.                                         00230000
                                                                        00240000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  CONDENSED-FILE                                               00290000
           LABEL RECORDS ARE STANDARD                                   00300000
           RECORDING MODE IS F                                          00310000
           BLOCK CONTAINS 0 RECORDS.                                    00320000
W26603 01  CONDENSED-RECORD            PIC X(200).                      00330016
      *                                                                 00340000
                                                                        00350000
       WORKING-STORAGE SECTION.                                         00360000
                                                                        00370000
       01  FILLER                      PIC X(28)   VALUE                00380000
                'BEGINNING OF WORKING STORAGE'.                         00390000
                                                                        00400000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00410000
                                                                        00420000
       01  PC-PROGRAM-CONSTANTS.                                        00430000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK405'.    00440000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP701'.  00450000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00460000
                                                                        00470000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00480000
           05  PE-MSG-01                       PIC X(50) VALUE          00490000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00500000
           05  PE-MSG-02                       PIC X(50) VALUE          00510000
                'ATTEMPT TO UPDATE ROW ON TABLE TVAMCL FAILED    '.     00520000
           05  PE-MSG-03                       PIC X(50) VALUE          00530000
                'ATTEMPT TO INSERT ROW ON TABLE TVAMCL FAILED    '.     00540000
           05  PE-MSG-04                       PIC X(50) VALUE          00550000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00560000
           05  PE-MSG-05                       PIC X(50) VALUE          00570000
                'UPDATE TO TCKRSTINF FAILED                      '.     00580000
           05  PE-MSG-06                       PIC X(50) VALUE          00590000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00600000
           05  PE-MSG-07                       PIC X(50) VALUE          00610000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00620000
      *                                                                 00630000
       01  PS-PROGRAM-SWITCHES.                                         00640000
           05  PS-EOF-CONDENSED-FILE-SW     PIC X(01)     VALUE 'N'.    00650000
               88  PS-EOF-CONDENSED-FILE                  VALUE 'Y'.    00660000
           05  PS-VAMCL-CURSOR-EOF-SW       PIC X(01)     VALUE 'N'.    00670000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00680000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00690000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00700000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00710000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00720000
                                                                        00730000
       01  PI-PROGRAM-INDICATORS.                                       00740000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00750000
               88  PI-UPDATE-VAMCL-ROW                    VALUE 'U'.    00760000
               88  PI-INSERT-VAMCL-ROW                    VALUE 'I'.    00770000
      *                                                                 00780000
       01  PROGRAM-VARIABLES.                                           00790000
           05  PV-RETRY-COUNT            PIC S9(04) VALUE +0.           00800000
           05  PV-DEADLOCK-COUNT         PIC S9(04) VALUE +0.           00810000
           05  PV-ENT-ID                 PIC 9(09)  VALUE 0.            00820004
      *                                                                 00830000
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00840000
           05  PV-ABEND-MESSAGE          PIC  X(75) VALUE SPACES.       00850000
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00860000
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.       00870000
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.       00880000
W26603     05  PV-MKUP-NET-AMT           PIC S9(11)V99 VALUE +0 COMP-3. 00890016
W26603     05  PV-MKUP-INIT-AMT          PIC S9(11)V99 VALUE +0 COMP-3. 00900016
W26603     05  PV-NET-COST               PIC S9(11)V99 VALUE +0 COMP-3. 00900116
W26603     05  PV-NET-RETAIL             PIC S9(11)V99 VALUE +0 COMP-3. 00901016
      *                                                                 00910000
       01  PA-PROGRAM-ACCUMULATORS.                                     00920000
           05  PA-RECORD-COUNTS.                                        00930000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00940000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      00950000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      00960000
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      00970000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      00980000
                                                                        00990000
       01  PD-PROGRAM-DISPLAYS.                                         01000000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01010000
       01  SAVE-AREA.                                                   01020000
           05  SV-PRIMARY-KEY.                                          01030000
               10  SV-FSCL-MN-NBR        PIC X(02)  VALUE SPACES.       01040000
               10  SV-FSCL-MN-END-DTE    PIC X(10)  VALUE SPACES.       01050000
               10  SV-DEPT-NBR           PIC X(03)  VALUE SPACES.       01060000
               10  SV-MAJ-CL-NBR         PIC X(02)  VALUE SPACES.       01070000
           05  SV-UPDATE-FIELDS.                                        01080000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01090016
W26603         10  SV-RCPT-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01100016
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01110016
W26603         10  SV-PADJ-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01120016
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01130016
W26603         10  SV-SLS-REG-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01140016
W26603         10  SV-SLS-PROMO-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01150016
W26603         10  SV-SLS-PROCLR-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01162016
W26603         10  SV-SLS-CLR-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01170016
W26603         10  SV-SLS-OTH-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01180016
W26603         10  SV-MKDN-PLU-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01190016
W26603*        10  SV-MKDN-PERM-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01200016
W26603         10  SV-MKDN-RGST-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01201016
W26603         10  SV-MKDN-UMTCH-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01210016
W26603         10  SV-MKDN-BYR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01220016
W26603         10  SV-MKDN-STR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01230016
W26603         10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01240016
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01250016
W26603         10  SV-MKUP-NET-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01260016
W26603         10  SV-MKUP-INIT-AMT      PIC S9(11)V99  COMP-3 VALUE +0.01270016
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01280016
W26603         10  SV-XFER-OUT-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01290016
W26603         10  SV-RCPT-GRO-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01300016
W26603         10  SV-PADJ-FRGT-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01310016
W26603         10  SV-PADJ-DISC-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01320016
W26603         10  SV-DR-ALW-RTL-AMT     PIC S9(11)V99  COMP-3 VALUE +0.01330016
W26603         10  SV-RTV-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01340016
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01350016
      *                                                                 01360000
      ******************************************************************01370000
      * MLRD201 - M/L MONTHLY VAMCL DATABASE UPDATE TXN LAYOUT          01380000
      ******************************************************************01390000
W26603     COPY MLRD201.                                                01400016
      *                                                                 01410000
      ******************************************************************01420000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01430000
      ******************************************************************01440000
           COPY DPWS004.                                                01450000
      *                                                                 01460000
      ******************************************************************01470000
      *  USED FOR DB2 ERRORS                                            01480000
      ******************************************************************01490000
           EXEC SQL                                                     01500000
               INCLUDE SQLCA                                            01510000
           END-EXEC.                                                    01520000
      *                                                                 01530000
      ******************************************************************01540000
      *  M/L MONTHLY MAJOR CLASS/ENT-ID TABLE                           01550000
      ******************************************************************01560000
           EXEC SQL                                                     01570000
               INCLUDE TVAMCL                                           01580000
           END-EXEC.                                                    01590000
      *                                                                 01600000
      ***************************************************************** 01610000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01620000
      ***************************************************************** 01630000
           EXEC SQL                                                     01640000
               INCLUDE TCKRSTCN                                         01650000
           END-EXEC.                                                    01660000
      *                                                                 01670000
           EXEC SQL                                                     01680000
               INCLUDE TCKRSTIN                                         01690000
           END-EXEC.                                                    01700000
      *                                                                 01710000
      ******************************************************************01720000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01730000
      ******************************************************************01740000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01750000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +35  COMP. 01760007
           05  CR-CHECKPOINT-RESTART-VAR.                               01770000
               10  CR-PREV-DTL-KEY          PIC  X(26) VALUE SPACES.    01780009
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01790000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01800000
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 01810000
                   15  CR-DEPT-NBR          PIC  X(03).                 01820000
                   15  CR-MAJ-CL-NBR        PIC  X(02).                 01830000
                   15  CR-ENT-ID            PIC  9(09).                 01840000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01850000
      *                                                                 01860000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01870000
           05  CK-SAVE-PREV-KEY         PIC  X(26) VALUE SPACES.        01880009
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01890000
               10  CK-FISCAL-NBR        PIC  X(02).                     01900000
               10  CK-FISCAL-MN-END-DTE PIC  X(10).                     01910000
               10  CK-DEPT-NBR          PIC  X(03).                     01920000
               10  CK-MAJ-CL-NBR        PIC  X(02).                     01930000
               10  CK-ENT-ID            PIC  9(09).                     01940000
                                                                        01950000
                                                                        01960000
      *-------------------*                                             01970000
       PROCEDURE DIVISION.                                              01980000
      *-------------------*                                             01990000
                                                                        02000000
       0000-MLKUP701.                                                   02010000
                                                                        02020000
           PERFORM 1000-INITIALIZATION.                                 02030000
      *                                                                 02040000
           PERFORM 2000-PROCESS-INPUT                                   02050000
                UNTIL PS-EOF-CONDENSED-FILE.                            02060000
      *                                                                 02070000
           PERFORM 8000-EOJ-ROUTINE.                                    02080000
                                                                        02090000
           STOP RUN.                                                    02100000
                                                                        02110000
      ******************************************************************02120000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02130000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02140000
      ******************************************************************02150000
      *                                                                 02160000
       1000-INITIALIZATION.                                             02170000
      *                                                                 02180000
           OPEN INPUT CONDENSED-FILE.                                   02190000
                                                                        02200000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02210000
                                                                        02220000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02230000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02240000
               PERFORM 7500-SELECT-RESTART-INFO                         02250000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02260000
                                            CR-CHECKPOINT-RESTART-VAR   02270000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02280000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02290000
               DISPLAY 'MONTH NBR       ' CR-FISCAL-NBR                 02300000
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE          02310000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02320000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02330000
               DISPLAY 'ENT-ID          ' CR-ENT-ID                     02340000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02350000
           ELSE                                                         02360000
               SET PS-FIRST-COMMIT TO TRUE                              02370000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02380000
           END-IF.                                                      02390000
      *                                                                 02400000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02410000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02420000
            OR PS-EOF-CONDENSED-FILE.                                   02430000
      *                                                                 02440000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02450000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02460000
                        PA-INPUT-COUNT                                  02470000
               DISPLAY 'FISCAL MONTH NBR      ' ML201-FSCL-MN-NBR       02480000
               DISPLAY 'FISCAL MONTH END DATE ' ML201-FSCL-MN-END-DTE   02490000
               DISPLAY 'DEPARTMENT NBR        ' ML201-DEPT-NBR          02500000
               DISPLAY 'MAJOR CLASS NBR       ' ML201-MAJ-CL-NBR        02510000
               MOVE ML201-ENT-ID   TO PV-ENT-ID                         02520000
               DISPLAY 'ENT-ID                ' PV-ENT-ID               02530000
           END-IF.                                                      02540000
                                                                        02550000
           IF PS-EOF-CONDENSED-FILE                                     02560000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02570000
                   CONTINUE                                             02580000
               ELSE                                                     02590000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02600000
           ELSE                                                         02610000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02620000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02630000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02640000
           END-IF.                                                      02650000
                                                                        02660000
      ******************************************************************02670000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02680000
      ******************************************************************02690000
       2000-PROCESS-INPUT.                                              02700000
      *                                                                 02710000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02720000
      *                                                                 02730000
           PERFORM 7000-SELECT-DB-ROW.                                  02740000
      *                                                                 02750000
           IF PS-PROGRAM-DEADLOCKED                                     02760000
               CONTINUE                                                 02770000
           ELSE                                                         02780000
               EVALUATE TRUE                                            02790000
                   WHEN PI-UPDATE-VAMCL-ROW                             02800000
                       PERFORM 7100-UPDATE-VAMCL-ROW                    02810000
                   WHEN PI-INSERT-VAMCL-ROW                             02820000
                       PERFORM 7200-INSERT-VAMCL-ROW                    02830000
                   WHEN OTHER                                           02840000
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02850000
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02860000
                       PERFORM 9999-SQL-ABEND                           02870000
               END-EVALUATE                                             02880000
           END-IF.                                                      02890000
      *                                                                 02900000
           IF PS-PROGRAM-DEADLOCKED                                     02910000
               CONTINUE                                                 02920000
           ELSE                                                         02930000
               PERFORM 7700-COMMIT-PROCESSING                           02940000
               PERFORM 4000-READ-CONDENSED-FILE                         02950000
           END-IF.                                                      02960000
      *                                                                 02970000
      *                                                                 02980000
      ******************************************************************02990000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         03000000
      ******************************************************************03010000
       2100-CALCULATE-UPDATES.                                          03020000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     03030000
      *                                                                 03040000
           COMPUTE SV-SLS-REG-RTL-AMT =                                 03050000
                   VAMCL-SLS-REG-RTL-AMT    + ML201-SLS-REG-RTL-AMT.    03060000
           COMPUTE SV-SLS-PROMO-RTL-AMT =                               03070000
                   VAMCL-SLS-PROMO-RTL-AMT  + ML201-SLS-PROMO-RTL-AMT.  03080000
           COMPUTE SV-SLS-PROCLR-RTL-AMT =                              03090000
                   VAMCL-SLS-PROCLR-RTL-AMT + ML201-SLS-PROCLR-RTL-AMT. 03100000
           COMPUTE SV-SLS-CLR-RTL-AMT =                                 03110000
                   VAMCL-SLS-CLR-RTL-AMT    + ML201-SLS-CLR-RTL-AMT.    03120000
           COMPUTE SV-SLS-OTH-RTL-AMT =                                 03130000
                   VAMCL-SLS-OTH-RTL-AMT    + ML201-SLS-OTH-RTL-AMT.    03140000
                                                                        03150000
           COMPUTE SV-MKDN-PLU-RTL-AMT  =                               03160000
                   VAMCL-MKDN-PLU-RTL-AMT   + ML201-MKDN-PLU-RTL-AMT.   03170000
0599RM*    COMPUTE SV-MKDN-PERM-RTL-AMT =                               03180013
0599RM*            VAMCL-MKDN-PERM-RTL-AMT  + ML201-MKDN-PERM-RTL-AMT.  03190013
0599RM     COMPUTE SV-MKDN-RGST-RTL-AMT =                               03191013
0599RM             VAMCL-MKDN-RGST-RTL-AMT  + ML201-MKDN-RGST-RTL-AMT.  03192013
           COMPUTE SV-MKDN-UMTCH-RTL-AMT =                              03200000
                   VAMCL-MKDN-UMTCH-RTL-AMT + ML201-MKDN-UMTCH-RTL-AMT. 03210000
           COMPUTE SV-MKDN-BYR-RTL-AMT  =                               03220000
                   VAMCL-MKDN-BYR-RTL-AMT   + ML201-MKDN-BYR-RTL-AMT.   03230000
           COMPUTE SV-MKDN-STR-RTL-AMT  =                               03240000
                   VAMCL-MKDN-STR-RTL-AMT   + ML201-MKDN-STR-RTL-AMT.   03250000
                                                                        03260000
           COMPUTE SV-MKUP-RTL-AMT      =                               03270000
                   VAMCL-MKUP-RTL-AMT       + ML201-MKUP-RTL-AMT.       03280000
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03290000
                   VAMCL-XFER-IN-RTL-AMT    + ML201-XFER-IN-RTL-AMT.    03300000
           COMPUTE SV-XFER-OUT-RTL-AMT  =                               03310000
                   VAMCL-XFER-OUT-RTL-AMT   + ML201-XFER-OUT-RTL-AMT.   03320000
           COMPUTE SV-RCPT-RTL-AMT      =                               03330000
                   VAMCL-RCPT-RTL-AMT       + ML201-RCPT-RTL-AMT.       03340000
           COMPUTE SV-RCPT-NET-COST     =                               03350000
                   VAMCL-RCPT-NET-COST-AMT  + ML201-RCPT-NET-COST-AMT.  03360000
           COMPUTE SV-RCPT-GRO-COST-AMT =                               03370000
                   VAMCL-RCPT-GRO-COST-AMT  + ML201-RCPT-GRO-COST-AMT.  03380000
           COMPUTE SV-PADJ-FRGT-COST-AMT =                              03390012
                   VAMCL-PADJ-FRGT-COST-AMT + ML201-PADJ-FRGT-COST-AMT. 03400012
           COMPUTE SV-PADJ-DISC-COST-AMT =                              03410012
                   VAMCL-PADJ-DISC-COST-AMT + ML201-PADJ-DISC-COST-AMT. 03420012
           COMPUTE SV-PADJ-RTL-AMT      =                               03430000
                   VAMCL-PADJ-RTL-AMT       + ML201-PADJ-RTL-AMT.       03440000
           COMPUTE SV-PADJ-NET-COST     =                               03450000
                   VAMCL-PADJ-NET-COST-AMT  + ML201-PADJ-NET-COST-AMT.  03460000
           COMPUTE SV-DR-ALW-RTL-AMT    =                               03470000
                   VAMCL-DR-ALW-RTL-AMT     + ML201-DR-ALW-RTL-AMT.     03480000
           COMPUTE SV-RTV-RTL-AMT       =                               03490000
                   VAMCL-RTV-RTL-AMT        + ML201-RTV-RTL-AMT.        03500000
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03510000
                    VAMCL-INV-ADJ-RTL-AMT  + ML201-INV-ADJ-RTL-AMT.     03520000
                                                                        03521014
      *--- INITIAL MARKUP AMOUNT                                        03530014
           COMPUTE PV-MKUP-INIT-AMT =                                   03540000
1299RM             ML201-RCPT-RTL-AMT - ML201-RCPT-NET-COST-AMT.        03550014
1299RM*            ML201-RCPT-RTL-AMT - ML201-RCPT-GRO-COST-AMT.        03551014
           COMPUTE SV-MKUP-INIT-AMT   =                                 03560000
                   VAMCL-MKUP-INIT-AMT + PV-MKUP-INIT-AMT.              03570002
                                                                        03580000
      *--- NET MARKUP AMOUNT                                            03581014
1299RM*    COMPUTE PV-MKUP-NET-AMT =                                    03590014
1299RM*           (ML201-RCPT-RTL-AMT -                                 03600014
1299RM*           (ML201-RCPT-GRO-COST-AMT +                            03610014
1299RM*            ML201-PADJ-FRGT-COST-AMT) -                          03620014
1299RM*            ML201-PADJ-DISC-COST-AMT).                           03630014
1299RM     COMPUTE PV-NET-RETAIL =                                      03630114
1299RM            (ML201-RCPT-RTL-AMT +                                 03630214
1299RM             ML201-PADJ-RTL-AMT +                                 03630314
1299RM             ML201-MKUP-RTL-AMT +                                 03630414
1299RM            (ML201-XFER-IN-RTL-AMT -                              03630514
1299RM             ML201-XFER-OUT-RTL-AMT))                             03630614
1299RM     END-COMPUTE.                                                 03631014
1299RM     COMPUTE PV-NET-COST = ML201-RCPT-NET-COST-AMT +              03631114
1299RM                           ML201-PADJ-NET-COST-AMT                03631214
1299RM     END-COMPUTE.                                                 03632014
                                                                        03632114
1299RM     COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        03632214
1299RM     END-COMPUTE.                                                 03633014
                                                                        03634014
           COMPUTE SV-MKUP-NET-AMT     =                                03640000
                   VAMCL-MKUP-NET-AMT  + PV-MKUP-NET-AMT.               03650000
                                                                        03660000
           COMPUTE SV-INV-ADJ-RTL-AMT  =                                03670000
                   VAMCL-INV-ADJ-RTL-AMT  + ML201-INV-ADJ-RTL-AMT.      03680000
      *                                                                 03690000
      ******************************************************************03700000
      * READS THE CONDENSED FILE INTO THE TVAMCL LAYOUT                 03710000
      ******************************************************************03720000
       4000-READ-CONDENSED-FILE.                                        03730000
           READ CONDENSED-FILE INTO ML201-UPDATE-REC                    03740000
              AT END                                                    03750000
                MOVE 'Y' TO PS-EOF-CONDENSED-FILE-SW.                   03760000
     *                                                                  03770000
           IF PS-EOF-CONDENSED-FILE                                     03780000
               MOVE ML201-FSCL-MN-NBR         TO CR-FISCAL-NBR          03790000
               MOVE ML201-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   03800000
               MOVE ML201-DEPT-NBR            TO CR-DEPT-NBR            03810000
               MOVE ML201-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03820000
               MOVE ML201-ENT-ID              TO PV-ENT-ID              03830000
               MOVE PV-ENT-ID                 TO CR-ENT-ID              03840000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03850000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03860000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03870000
                                             TCKRSTINF-WORK-STRG-DESC   03880000
               EXEC SQL                                                 03890000
                 UPDATE TCKRSTINF                                       03900000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03910000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03920000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03930000
               END-EXEC                                                 03940000
               IF SQLCODE = 0                                           03950000
                   CONTINUE                                             03960000
               ELSE                                                     03970000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03980000
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  03990000
                   PERFORM 9999-SQL-ABEND                               04000000
               END-IF                                                   04010000
            ELSE                                                        04020000
                ADD 1                        TO PA-INPUT-COUNT          04030000
            END-IF.                                                     04040000
      *                                                                 04050000
      ******************************************************************04060000
      * SELECT A ROW FROM THE MONTHLY MCL  TABLE THAT MATCHES INPUT KEY 04070000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      04080000
      ******************************************************************04090000
       7000-SELECT-DB-ROW.                                              04100000
      *                                                                 04110000
           EXEC SQL                                                     04120000
               SELECT SLS_REG_RTL_AMT                                   04130000
                     ,SLS_PROMO_RTL_AMT                                 04140000
                     ,SLS_PROCLR_RTL_AMT                                04150000
                     ,SLS_CLR_RTL_AMT                                   04160000
                     ,SLS_OTH_RTL_AMT                                   04170000
                     ,MKDN_PLU_RTL_AMT                                  04180000
0599RM*              ,MKDN_PERM_RTL_AMT                                 04190013
0599RM               ,MKDN_RGST_RTL_AMT                                 04191013
                     ,MKDN_UMTCH_RTL_AMT                                04200000
                     ,MKDN_BYR_RTL_AMT                                  04210000
                     ,MKDN_STR_RTL_AMT                                  04220000
                     ,MKUP_RTL_AMT                                      04230000
                     ,XFER_IN_RTL_AMT                                   04240000
                     ,XFER_OUT_RTL_AMT                                  04250000
                     ,RCPT_RTL_AMT                                      04260000
                     ,RCPT_NET_COST_AMT                                 04270000
                     ,RCPT_GRO_COST_AMT                                 04280000
                     ,PADJ_FRGT_COST_AMT                                04290012
                     ,PADJ_DISC_COST_AMT                                04300012
                     ,PADJ_RTL_AMT                                      04310000
                     ,PADJ_NET_COST_AMT                                 04320000
                     ,DR_ALW_RTL_AMT                                    04330000
                     ,RTV_RTL_AMT                                       04340000
                     ,INV_ADJ_RTL_AMT                                   04350000
                     ,MKUP_INIT_AMT                                     04351000
                     ,MKUP_NET_AMT                                      04352000
                 INTO :VAMCL-SLS-REG-RTL-AMT                            04353000
                     ,:VAMCL-SLS-PROMO-RTL-AMT                          04354000
                     ,:VAMCL-SLS-PROCLR-RTL-AMT                         04355000
                     ,:VAMCL-SLS-CLR-RTL-AMT                            04356000
                     ,:VAMCL-SLS-OTH-RTL-AMT                            04357000
                     ,:VAMCL-MKDN-PLU-RTL-AMT                           04358000
0599RM*              ,:VAMCL-MKDN-PERM-RTL-AMT                          04359013
0599RM               ,:VAMCL-MKDN-RGST-RTL-AMT                          04359113
                     ,:VAMCL-MKDN-UMTCH-RTL-AMT                         04360000
                     ,:VAMCL-MKDN-BYR-RTL-AMT                           04370000
                     ,:VAMCL-MKDN-STR-RTL-AMT                           04380000
                     ,:VAMCL-MKUP-RTL-AMT                               04390000
                     ,:VAMCL-XFER-IN-RTL-AMT                            04400000
                     ,:VAMCL-XFER-OUT-RTL-AMT                           04410000
                     ,:VAMCL-RCPT-RTL-AMT                               04420000
                     ,:VAMCL-RCPT-NET-COST-AMT                          04430000
                     ,:VAMCL-RCPT-GRO-COST-AMT                          04440000
                     ,:VAMCL-PADJ-FRGT-COST-AMT                         04450012
                     ,:VAMCL-PADJ-DISC-COST-AMT                         04460012
                     ,:VAMCL-PADJ-RTL-AMT                               04470000
                     ,:VAMCL-PADJ-NET-COST-AMT                          04480000
                     ,:VAMCL-DR-ALW-RTL-AMT                             04490000
                     ,:VAMCL-RTV-RTL-AMT                                04500000
                     ,:VAMCL-INV-ADJ-RTL-AMT                            04510000
                     ,:VAMCL-MKUP-INIT-AMT                              04520000
                     ,:VAMCL-MKUP-NET-AMT                               04530000
               FROM TVAMCL                                              04540000
              WHERE   FSCL_MN_NBR         = :ML201-FSCL-MN-NBR          04550000
                AND   FSCL_MN_END_DTE     = :ML201-FSCL-MN-END-DTE      04560000
                AND   DEPT_NBR            = :ML201-DEPT-NBR             04570000
                AND   MAJ_CL_NBR          = :ML201-MAJ-CL-NBR           04580000
                AND   ENT_ID              = :ML201-ENT-ID               04590000
           END-EXEC.                                                    04600000
      *                                                                 04610000
           EVALUATE SQLCODE                                             04620000
               WHEN 0                                                   04630000
                   SET PI-UPDATE-VAMCL-ROW        TO TRUE               04640000
               WHEN +100                                                04650000
                   SET PI-INSERT-VAMCL-ROW        TO TRUE               04660000
               WHEN -911                                                04670000
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            04680000
                   ADD +1             TO PV-DEADLOCK-COUNT              04690000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04700000
                       MOVE '7000-SELECT-DB-ROW' TO                     04710000
                                                    PV-PARAGRAPH-NAME   04720000
                       PERFORM 9000-DEADLOCK-ERROR                      04730000
                   ELSE                                                 04740000
                       PERFORM 7999-RESTART-PROCESS                     04750000
                   END-IF                                               04760000
               WHEN OTHER                                               04770000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04780000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04790000
                   PERFORM 9999-SQL-ABEND                               04800000
           END-EVALUATE.                                                04810000
      *                                                                 04820000
      *                                                                 04830000
      ***************************************************************** 04840000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  04850000
      * AND MAJOR CLASS. CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES 04860000
      * FROM INPUT REC MLRD105                                          04870000
      ***************************************************************** 04880000
       7100-UPDATE-VAMCL-ROW.                                           04890000
      *                                                                 04900000
           PERFORM 2100-CALCULATE-UPDATES.                              04910000
      *                                                                 04920000
           EXEC SQL                                                     04930000
               UPDATE TVAMCL                                            04940000
                   SET SLS_REG_RTL_AMT     = :SV-SLS-REG-RTL-AMT        04950000
                     , SLS_PROMO_RTL_AMT   = :SV-SLS-PROMO-RTL-AMT      04960000
                     , SLS_PROCLR_RTL_AMT  = :SV-SLS-PROCLR-RTL-AMT     04970000
                     , SLS_CLR_RTL_AMT     = :SV-SLS-CLR-RTL-AMT        04980000
                     , SLS_OTH_RTL_AMT     = :SV-SLS-OTH-RTL-AMT        04990000
                     , MKDN_PLU_RTL_AMT    = :SV-MKDN-PLU-RTL-AMT       05000000
0599RM*              , MKDN_PERM_RTL_AMT   = :SV-MKDN-PERM-RTL-AMT      05010013
0599RM               , MKDN_RGST_RTL_AMT   = :SV-MKDN-RGST-RTL-AMT      05011013
                     , MKDN_UMTCH_RTL_AMT  = :SV-MKDN-UMTCH-RTL-AMT     05020000
                     , MKDN_BYR_RTL_AMT    = :SV-MKDN-BYR-RTL-AMT       05030000
                     , MKDN_STR_RTL_AMT    = :SV-MKDN-STR-RTL-AMT       05040000
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           05050000
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        05060000
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL-AMT       05070000
                     , RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           05080000
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST          05090000
                     , RCPT_GRO_COST_AMT   = :SV-RCPT-GRO-COST-AMT      05100000
                     , PADJ_FRGT_COST_AMT  = :SV-PADJ-FRGT-COST-AMT     05110012
                     , PADJ_DISC_COST_AMT  = :SV-PADJ-DISC-COST-AMT     05120012
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           05130000
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST          05140000
                     , DR_ALW_RTL_AMT      = :SV-DR-ALW-RTL-AMT         05150000
                     , RTV_RTL_AMT         = :SV-RTV-RTL-AMT            05160000
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        05170000
                     , MKUP_INIT_AMT       = :SV-MKUP-INIT-AMT          05180000
                     , MKUP_NET_AMT        = :SV-MKUP-NET-AMT           05190000
              WHERE   FSCL_MN_NBR          = :ML201-FSCL-MN-NBR         05200000
                AND   FSCL_MN_END_DTE      = :ML201-FSCL-MN-END-DTE     05210000
                AND   DEPT_NBR             = :ML201-DEPT-NBR            05220000
                AND   MAJ_CL_NBR           = :ML201-MAJ-CL-NBR          05230000
                AND   ENT_ID               = :ML201-ENT-ID              05240000
           END-EXEC                                                     05250000
      *                                                                 05260000
           EVALUATE SQLCODE                                             05270000
               WHEN 0                                                   05280000
                   CONTINUE                                             05290000
               WHEN -911                                                05300000
                   ADD +1             TO PV-DEADLOCK-COUNT              05310000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            05320000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05330000
                       MOVE '7100-UPDATE-VAMCL-ROW' TO                  05340000
                                                    PV-PARAGRAPH-NAME   05350000
                       PERFORM 9000-DEADLOCK-ERROR                      05360000
                   ELSE                                                 05370000
                       PERFORM 7999-RESTART-PROCESS                     05380000
                   END-IF                                               05390000
               WHEN OTHER                                               05400000
                   MOVE '7100-UPDATE-VAMCL-ROW' TO PV-PARAGRAPH-NAME    05410000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED05420000
                   PERFORM 9999-SQL-ABEND                               05430000
           END-EVALUATE.                                                05440000
      *                                                                 05450000
           ADD 1 TO PA-UPDATE-COUNT.                                    05460000
      *                                                                 05470000
      ***************************************************************** 05480000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY MCL TABLE. INITIAL   * 05490014
      * ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW.             * 05500014
      * CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC* 05510014
      ***************************************************************** 05520000
       7200-INSERT-VAMCL-ROW.                                           05530000
      *                                                                 05540000
           COMPUTE PV-MKUP-INIT-AMT =                                   05541003
1299RM             ML201-RCPT-RTL-AMT - ML201-RCPT-NET-COST-AMT.        05541115
1299RM*            ML201-RCPT-RTL-AMT - ML201-RCPT-GRO-COST-AMT.        05542014
                                                                        05545003
1299RM*    COMPUTE PV-MKUP-NET-AMT =                                    05546014
1299RM*           (ML201-RCPT-RTL-AMT -                                 05547014
1299RM*           (ML201-RCPT-GRO-COST-AMT +                            05548014
1299RM*            ML201-PADJ-FRGT-COST-AMT) -                          05549014
1299RM*            ML201-PADJ-DISC-COST-AMT).                           05549114
      *                                                                 05550000
1299RM     COMPUTE PV-NET-RETAIL =                                      05550114
1299RM            (ML201-RCPT-RTL-AMT +                                 05550215
1299RM             ML201-PADJ-RTL-AMT +                                 05550315
1299RM             ML201-MKUP-RTL-AMT +                                 05550415
1299RM            (ML201-XFER-IN-RTL-AMT -                              05550515
1299RM             ML201-XFER-OUT-RTL-AMT))                             05550615
1299RM     END-COMPUTE.                                                 05550714
1299RM     COMPUTE PV-NET-COST = ML201-RCPT-NET-COST-AMT +              05550815
1299RM                           ML201-PADJ-NET-COST-AMT                05550915
1299RM     END-COMPUTE.                                                 05551014
                                                                        05551114
1299RM     COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        05551214
1299RM     END-COMPUTE.                                                 05552014
           EXEC SQL                                                     05560000
               INSERT INTO TVAMCL                                       05570000
                    (  FSCL_MN_NBR                                      05580000
                     , FSCL_MN_END_DTE                                  05590000
                     , DEPT_NBR                                         05600000
                     , MAJ_CL_NBR                                       05610000
                     , ENT_ID                                           05620000
                     , SLS_REG_RTL_AMT                                  05630000
                     , SLS_PROMO_RTL_AMT                                05640000
                     , SLS_PROCLR_RTL_AMT                               05650000
                     , SLS_CLR_RTL_AMT                                  05660000
                     , SLS_OTH_RTL_AMT                                  05670000
                     , MKDN_PLU_RTL_AMT                                 05680000
0599RM*              , MKDN_PERM_RTL_AMT                                05690013
0599RM               , MKDN_RGST_RTL_AMT                                05695013
                     , MKDN_UMTCH_RTL_AMT                               05700000
                     , MKDN_BYR_RTL_AMT                                 05710000
                     , MKDN_STR_RTL_AMT                                 05720000
                     , MKUP_RTL_AMT                                     05730000
                     , XFER_IN_RTL_AMT                                  05740000
                     , XFER_OUT_RTL_AMT                                 05750000
                     , RCPT_RTL_AMT                                     05760000
                     , RCPT_NET_COST_AMT                                05770000
                     , RCPT_GRO_COST_AMT                                05780000
                     , PADJ_FRGT_COST_AMT                               05790012
                     , PADJ_DISC_COST_AMT                               05800012
                     , PADJ_RTL_AMT                                     05810000
                     , PADJ_NET_COST_AMT                                05820000
                     , DR_ALW_RTL_AMT                                   05830000
                     , RTV_RTL_AMT                                      05840000
                     , INV_ADJ_RTL_AMT                                  05850000
                     , SHNK_RTL_AMT                                     05860000
                     , MKUP_INIT_AMT                                    05870000
                     , MKUP_NET_AMT                                     05880000
                     , END_INV_RTL_AMT                                  05890000
                     , CUM_MKUP_PCT                                     05900000
                     , END_INV_COST_AMT                                 05910000
                     , SLS_COST_AMT                                     05920000
                     , GRO_MRGN_AMT      )                              05930000
               VALUES                                                   05940000
                   (   :ML201-FSCL-MN-NBR                               05950000
                     , :ML201-FSCL-MN-END-DTE                           05960000
                     , :ML201-DEPT-NBR                                  05970000
                     , :ML201-MAJ-CL-NBR                                05980000
                     , :ML201-ENT-ID                                    05990000
                     , :ML201-SLS-REG-RTL-AMT                           06000000
                     , :ML201-SLS-PROMO-RTL-AMT                         06010000
                     , :ML201-SLS-PROCLR-RTL-AMT                        06020000
                     , :ML201-SLS-CLR-RTL-AMT                           06030000
                     , :ML201-SLS-OTH-RTL-AMT                           06040000
                     , :ML201-MKDN-PLU-RTL-AMT                          06050000
0599RM*              , :ML201-MKDN-PERM-RTL-AMT                         06060013
0599RM               , :ML201-MKDN-RGST-RTL-AMT                         06061013
                     , :ML201-MKDN-UMTCH-RTL-AMT                        06070000
                     , :ML201-MKDN-BYR-RTL-AMT                          06080000
                     , :ML201-MKDN-STR-RTL-AMT                          06090000
                     , :ML201-MKUP-RTL-AMT                              06100000
                     , :ML201-XFER-IN-RTL-AMT                           06110000
                     , :ML201-XFER-OUT-RTL-AMT                          06120000
                     , :ML201-RCPT-RTL-AMT                              06130000
                     , :ML201-RCPT-NET-COST-AMT                         06140000
                     , :ML201-RCPT-GRO-COST-AMT                         06150000
                     , :ML201-PADJ-FRGT-COST-AMT                        06160012
                     , :ML201-PADJ-DISC-COST-AMT                        06170012
                     , :ML201-PADJ-RTL-AMT                              06180000
                     , :ML201-PADJ-NET-COST-AMT                         06190000
                     , :ML201-DR-ALW-RTL-AMT                            06200000
                     , :ML201-RTV-RTL-AMT                               06210000
                     , :ML201-INV-ADJ-RTL-AMT                           06220000
                     , 0                                                06230000
                     , :PV-MKUP-INIT-AMT                                06240003
                     , :PV-MKUP-NET-AMT                                 06250003
                     , 0                                                06260000
                     , 0                                                06270000
                     , 0                                                06280000
                     , 0                                                06290000
                     , 0                       )                        06300000
           END-EXEC                                                     06310000
      *                                                                 06320000
           EVALUATE SQLCODE                                             06330000
               WHEN 0                                                   06340000
                   CONTINUE                                             06350000
               WHEN -911                                                06360000
                   ADD +1             TO PV-DEADLOCK-COUNT              06370000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            06380000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              06390000
                       MOVE '7200-INSERT-VAMCL-ROW' TO                  06400000
                                                    PV-PARAGRAPH-NAME   06410000
                       PERFORM 9000-DEADLOCK-ERROR                      06420000
                   ELSE                                                 06430000
                       PERFORM 7999-RESTART-PROCESS                     06440000
                   END-IF                                               06450000
               WHEN OTHER                                               06460000
                   MOVE '7200-INSERT-VAMCL-ROW' TO PV-PARAGRAPH-NAME    06470000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED06480000
                   PERFORM 9999-SQL-ABEND                               06490000
           END-EVALUATE.                                                06500000
                                                                        06510000
           ADD 1 TO PA-INSERT-COUNT.                                    06520000
                                                                        06530000
      ******************************************************************06540000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *06550000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *06560000
      *            CONTROL TABLE                                       *06570000
      ******************************************************************06580000
       7300-GET-COMMIT-TIMESTAMP.                                       06590000
                                                                        06600000
           EXEC SQL                                                     06610000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        06620000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       06630000
               INTO :PV-COMMIT-TIMESTAMP                                06640000
               FROM TCKRSTCNTL                                          06650000
              WHERE JOB_NAME  = :PC-JOB-NAME                            06660000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        06670000
           END-EXEC.                                                    06680000
                                                                        06690000
           EVALUATE TRUE                                                06700000
               WHEN SQLCODE = 0                                         06710000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  06720000
                   CONTINUE                                             06730000
               WHEN SQLCODE NOT EQUAL ZERO                              06740000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME06750000
                   PERFORM 9999-SQL-ABEND                               06760000
           END-EVALUATE.                                                06770000
      *                                                                 06780000
      *                                                                 06790000
      ******************************************************************06800000
      * 7400-INIT-CHECKPOINT-SELECT                                    *06810000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *06820000
      *            IF WE ARE IN A RESTART CONDITION.                   *06830000
      ******************************************************************06840000
       7400-INIT-CHECKPOINT-SELECT.                                     06850000
                                                                        06860000
           EXEC SQL                                                     06870000
             SELECT  CKPT_STAT_CODE                                     06880000
                    ,CKPT_FRQNCY_QTY                                    06890000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         06900000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        06910000
               FROM  TCKRSTCNTL                                         06920000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06930000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06940000
           END-EXEC.                                                    06950000
                                                                        06960000
           IF SQLCODE = 0                                               06970000
               CONTINUE                                                 06980000
           ELSE                                                         06990000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  07000000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     07010000
               PERFORM 9999-SQL-ABEND                                   07020000
           END-IF.                                                      07030000
      *                                                                 07040000
      *                                                                 07050000
      ******************************************************************07060000
      * 7500-SELECT-RESTART-INFO                                       *07070000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *07080000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *07090000
      ******************************************************************07100000
       7500-SELECT-RESTART-INFO.                                        07110000
                                                                        07120000
           EXEC SQL                                                     07130000
             SELECT  WORK_STRG_DESC                                     07140000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          07150000
               FROM  TCKRSTINF                                          07160000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07170000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07180000
           END-EXEC.                                                    07190000
                                                                        07200000
           IF SQLCODE = 0                                               07210000
               CONTINUE                                                 07220000
           ELSE                                                         07230000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   07240000
               PERFORM 9999-SQL-ABEND                                   07250000
           END-IF.                                                      07260000
                                                                        07270000
      ******************************************************************07280000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *07290000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *07300000
      ******************************************************************07310000
       7600-SET-CURRENT-TIMESTAMP.                                      07320000
                                                                        07330000
           EXEC SQL                                                     07340000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           07350000
           END-EXEC.                                                    07360000
                                                                        07370000
           IF SQLCODE = 0                                               07380000
               CONTINUE                                                 07390000
           ELSE                                                         07400000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 07410000
               PERFORM 9999-SQL-ABEND                                   07420000
           END-IF.                                                      07430000
      *                                                                 07440000
      *                                                                 07450000
      ******************************************************************07460000
      * 7700-COMMIT-PROCESSING.                                        *07470000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *07480000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*07490000
      ******************************************************************07500000
       7700-COMMIT-PROCESSING.                                          07510000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     07520000
                                                                        07530000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          07540000
                                                                        07550000
      * PREPARE COMMIT RECORD FOR UPDATE                                07560000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                07570000
               MOVE ML201-FSCL-MN-NBR         TO CR-FISCAL-NBR          07580000
               MOVE ML201-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   07590000
               MOVE ML201-DEPT-NBR            TO CR-DEPT-NBR            07600000
               MOVE ML201-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          07610000
               MOVE ML201-ENT-ID              TO PV-ENT-ID              07620000
               MOVE PV-ENT-ID                 TO CR-ENT-ID              07630000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    07640000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  07650000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       07660000
                                             TCKRSTINF-WORK-STRG-DESC   07670000
               IF PS-FIRST-COMMIT                                       07680000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                07690000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                07700000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       07710000
               END-IF                                                   07720000
               PERFORM 7800-COMMIT-CHANGES                              07730000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        07740000
           END-IF.                                                      07750000
      *                                                                 07760000
      *                                                                 07770000
      ******************************************************************07780000
      * 7800-COMMIT-CHANGES.                                            07790000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      07800000
      *            TAKE A COMMIT.                                       07810000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    07820000
      ******************************************************************07830000
       7800-COMMIT-CHANGES.                                             07840000
                                                                        07850000
           EXEC SQL                                                     07860000
             UPDATE TCKRSTINF                                           07870000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07880000
              WHERE JOB_NAME       = :PC-JOB-NAME                       07890000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07900000
           END-EXEC.                                                    07910000
                                                                        07920000
           IF SQLCODE = 0                                               07930000
               CONTINUE                                                 07941006
           ELSE                                                         07950000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED07960000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07970000
               PERFORM 9999-SQL-ABEND                                   07980000
           END-IF.                                                      07990000
                                                                        08000000
           EXEC SQL                                                     08010000
               COMMIT                                                   08020000
           END-EXEC.                                                    08030000
                                                                        08040000
           IF SQLCODE = 0                                               08050000
               ADD 1 TO PA-COMMIT-COUNT                                 08051006
           ELSE                                                         08070000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED08080000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     08090000
               PERFORM 9999-SQL-ABEND                                   08100000
           END-IF.                                                      08110000
      *                                                                 08120000
      ******************************************************************08130000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   08140000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   08150000
      ******************************************************************08160000
       7900-UPDATE-CHECKPOINT-STATUS.                                   08170000
                                                                        08180000
           EXEC SQL                                                     08190000
             UPDATE  TCKRSTCNTL                                         08200000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        08210000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08220000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08230000
           END-EXEC.                                                    08240000
                                                                        08250000
           IF SQLCODE = 0                                               08260000
               CONTINUE                                                 08270000
           ELSE                                                         08280000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME08290000
               PERFORM 9999-SQL-ABEND                                   08300000
           END-IF.                                                      08310000
                                                                        08320000
           EJECT                                                        08330000
      *                                                                 08340000
      *                                                                 08350000
      ******************************************************************08360000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TVAMCL ROW FOR UPDATE 08370000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  08380000
      ******************************************************************08390000
       7999-RESTART-PROCESS.                                            08400000
      *                                                                 08410000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           08420000
      *                                                                 08430000
           CLOSE CONDENSED-FILE.                                        08440000
      *                                                                 08450000
           PERFORM 7500-SELECT-RESTART-INFO.                            08460000
      *                                                                 08470000
           DISPLAY '**** RESTART **** '.                                08480000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 08490000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   08500000
                                                                        08510000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      08520000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        08530000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       08540000
           IF PS-FIRST-COMMIT                                           08550000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               08560000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     08570000
                      ' BEGINNING'                                      08580000
           ELSE                                                         08590000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           08600000
                        TCKRSTINF-WORK-STRG-DESC (1:35)                 08610010
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 08620000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         08630000
                    CR-CHECKPOINT-RESTART-AREA                          08640000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                08650000
               DISPLAY 'MONTH NBR       ' CR-FISCAL-NBR                 08660000
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE          08670000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   08680000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 08690000
               DISPLAY 'ENT-ID          ' CR-ENT-ID                     08700000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           08710000
           END-IF.                                                      08720000
                                                                        08730000
           OPEN INPUT CONDENSED-FILE.                                   08740000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      08750000
      *                                                                 08760000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          08770000
           PERFORM 4000-READ-CONDENSED-FILE                             08780000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              08790000
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  08800000
                    PA-INPUT-COUNT                                      08810000
           DISPLAY 'FISCAL MONTH NBR      ' ML201-FSCL-MN-NBR.          08820000
           DISPLAY 'FISCAL MONTH END DATE ' ML201-FSCL-MN-END-DTE.      08830000
           DISPLAY 'DEPARTMENT NBR        ' ML201-DEPT-NBR.             08840000
           DISPLAY 'MAJOR CLASS NBR       ' ML201-MAJ-CL-NBR.           08850000
           MOVE ML201-ENT-ID TO PV-ENT-ID.                              08860000
           DISPLAY 'ENT-ID                ' PV-ENT-ID.                  08870000
      ******************************************************************08880000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08890000
      ******************************************************************08900000
       8000-EOJ-ROUTINE.                                                08910000
      *                                                                 08920000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       08930000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       08940000
                                                                        08950000
      *                                                                 08960000
           DISPLAY SPACES.                                              08970000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         08980000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        08990000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        09000000
           DISPLAY SPACES.                                              09010000
      *                                                                 09020000
           CLOSE CONDENSED-FILE.                                        09030000
      *                                                                 09040000
      *                                                                 09050000
      ******************************************************************09060000
      *  PERFORMED BY 7100-UPDATE AND 7200-INSERT                       09070003
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   09080000
      ******************************************************************09090000
       9000-DEADLOCK-ERROR.                                             09100000
      *                                                                 09110000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     09120000
               PERFORM 9999-SQL-ABEND.                                  09130000
      *                                                                 09140000
      ******************************************************************09150000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               09160000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             09170000
      ******************************************************************09180000
       9999-SQL-ABEND.                                                  09190000
                                                                        09200000
           MOVE +4013                 TO ABEND-CODE.                    09210000
           DISPLAY '**  ABEND     **'.                                  09220000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              09230000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            09240000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       09250000
                                                                        09260000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         09270000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        09280000
                                                                        09290000
           COPY DPPD004.                                                09300000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           09310000
