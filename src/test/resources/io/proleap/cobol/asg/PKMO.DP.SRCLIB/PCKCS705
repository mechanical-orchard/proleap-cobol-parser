000010******************************************************************        
000020*  I M P O R T A N T --- WHEN COMPILING THIS PROGRAM THE SOCKET  *        
000030* SWITCH ON THE 2ND COMPILE SCREEN NEEDS TO BE SET TO 'Y'.  THIS          
000040* PROGRAM IS USING "EZASOKET".                                            
000050******************************************************************        
000060                                                                          
000200 IDENTIFICATION DIVISION.                                                 
000300 TITLE 'PACK-TO-LITE DISTRIBUTION CARTON SCAN UPLOAD'.                    
000400                                                                          
000500 PROGRAM-ID.             PCKCS705.                                        
000600 AUTHOR.                 RON KRIER.                                       
000700 INSTALLATION.           KOHLS.                                           
000800 DATE-WRITTEN            NOVEMBER 1998.                                   
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200* THIS PROGRAM IS STARTED FROM THE PACK-TO-LIGHT SYSTEM.                  
001300* ON THAT SYSTEM, THE APPLICATION WILL UPLOAD DATA FROM                   
001400* THE SYSTEM TO THE MAINFRAME WHEN A DISTRIBUTION IS STARTED.             
001500*                                                                         
001600* THIS PROGRAM IS EXECUTED VIA A SOCKET CALL.                             
001700*                                                                         
001800* WHEN THE PROGRAM IS EXECUTED, IT WILL TAKE THE DATA FROM THE            
001900* SOCKET CALL AND ATTEMPT TO UPDATE THE TDISTHD TABLE FOR THE             
002000* DISTRIBUTION TO INCREMENT THE NUMBER OF CARTONS SCANNED FOR             
002100* THE DISTRIBUTION.                                                       
002200*                                                                         
002300* IF THE UPDATE IS SUCCESSFUL, A GOOD CONFIRMATION WILL BE PASSED         
002400* TO THE PACK-TO-LIGHT SYSTEM AND PROCESSING IS COMPLETED.  IF            
002500* THE UPDATE IS NOT SUCCESSFUL, A BAD CONFIRMATION WILL BE                
002600* PASSED TO THE PACK-TO-LIGHT SYSTEM AND PROCESSING WILL BE               
002700* COMPLETED (WHEN A BAD CONFIRMATION IS SENT BACK TO THE PACK-            
002800* TO-LIGHT SYSTEM, THAT SYSTEM WILL ATTEMPT TO RE-SEND THE DATA           
002900* AT A LATER TIME).                                                       
003000******************************************************************        
003100                                                                          
003200 ENVIRONMENT DIVISION.                                                    
003300 DATA DIVISION.                                                           
003400 WORKING-STORAGE SECTION.                                                 
003500                                                                          
003600*                                                                         
003700*  SOCKET COMMANDS CODES NEED FOR THE TCP/IP SOCKET CALLS                 
003800*                                                                         
003900     COPY DPWS081.                                                        
004000                                                                          
004100*                                                                         
004200*  DISTRIBUTION CARTON LAUNCH STATUS RECORD LAYOUT LOADED TO THE          
004300*  MAINFRAME FROM THE QNX BOX ASSOCIATED WITH THE PACK TO                 
004400*  LIGHT SYSTEM PROVIDED BY AL SYSTEMS.                                   
004500*                                                                         
004600     COPY PCWS008.                                                        
004700                                                                          
004800     EJECT                                                                
004900*                                                                         
005000*  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA.                       
005100*                                                                         
005200     COPY DPWS013.                                                        
005300                                                                          
005400 77  TASK-START                     PIC X(40)                             
005500      VALUE IS 'CONFIRMS....CICS-TCPIP INTERFACE START.;'.                
005600 77  TASK-END                       PIC X(40)                             
005700      VALUE IS 'CONFIRMS....CICS-TCPIP INTERFACE END....'.                
005800*                                                                         
005900 01  TCP-BUF-AREA.                                                        
006000     05  TCP-BUF-ALL                     PIC X(50) VALUE SPACES.          
006100     05  TCP-BUF REDEFINES TCP-BUF-ALL                                    
006200                OCCURS 50 TIMES          PIC  X(01).                      
006300 01  PV-BUFFER-SUB                    PIC 9(08) BINARY VALUE ZERO.        
006400*                                                                         
006500 01  PC-PROGRAM-CONSTANTS.                                                
006600                                                                          
006700     05  PV-CONTROL-NUMBER               PIC  X(21)  VALUE SPACES.        
006800     05  PC-MAX-RETRIES                  PIC S9(03)  VALUE +3             
006900                                                     COMP-3.              
007000     05  PC-PREVIOUSLY-VERIFIED-STATUS   PIC  X(01)  VALUE 'P'.           
007100     05  PC-SPLIT-COMP-WAIT-STATUS       PIC  X(01)  VALUE 'B'.           
007200     05  PC-CURRENT-PGM-NAME             PIC  X(08)  VALUE                
007300         'PCKCS705'.                                                      
007400     05  PC-SPACES                       PIC  X(22)  VALUE SPACES.        
007500     05  PC-A                            PIC  X(01)  VALUE 'A'.           
007600     05  PC-TERM-CHARACTER               PIC  X(01)  VALUE X'3B'.         
007700     05  PC-DEFAULT-DATE-1               PIC  X(26)   VALUE               
007800                                   '9999-09-09-09.09.09.999999'.          
007900     05  PC-DEFAULT-DATE-2               PIC  X(26)   VALUE               
008000                                   '0001-01-01-00.00.00.000000'.          
008010     05  PC-MSG-TABLE-SIZE            PIC S9(03) COMP-3 VALUE 200.        
008100*                                                                         
008200 01  DK-DB2-KEYS.                                                         
008300     05  DK-ORD-NBR                      PIC S9(9)   VALUE ZERO           
008400                                                     COMP SYNC.           
008500     05  DK-REC-SEQ-NBR                  PIC S9(9)   VALUE ZERO           
008600                                                     COMP SYNC.           
008700     05  DK-ORD-LNE-NBR                  PIC S9(9)   VALUE ZERO           
008800                                                     COMP SYNC.           
008900     05  DK-WRK-ORD-SEQ-NBR              PIC S9(4)   VALUE ZERO           
009000                                                     COMP SYNC.           
009100     05  DK-STR-NBR                      PIC S9(4)   VALUE ZERO           
009200                                                     COMP SYNC.           
009210     05  DK-OPER-UNT-ID                  PIC S9(4)   VALUE ZERO           
009220                                                     COMP SYNC.           
009300                                                                          
009400 01  PS-PROGRAM-SWITCHES.                                                 
009410     05  PS-SOCKET-ERROR-SW           PIC X(01) VALUE 'N'.                
009420         88  PS-SOCKET-ERROR-NO                 VALUE 'N'.                
009430         88  PS-SOCKET-ERROR                    VALUE 'Y'.                
009500     05  PS-PREVIOUSLY-VERIFIED-SW       PIC  X(01)  VALUE 'N'.           
009600         88  PS-PREVIOUSLY-VERIFIED                  VALUE 'Y'.           
009700         88  PS-NOT-PREVIOUSLY-VERIFIED              VALUE 'N'.           
009800     05  PS-TDISTHD-FOUND-SW             PIC  X(01)  VALUE 'N'.           
009900         88  PS-TDISTHD-FOUND                        VALUE 'Y'.           
010000         88  PS-TDISTHD-NOT-FOUND                    VALUE 'N'.           
010100     05  PS-SOCKET-SW                    PIC  X(01)  VALUE 'N'.           
010200         88  PS-SOCKET-OPEN                          VALUE 'Y'.           
010300         88  PS-SOCKET-CLOSED                        VALUE 'N'.           
010400                                                                          
010500 01  PV-PROGRAM-VARIABLES.                                                
010510     05  PV-DISPLAY-SOCKETS           PIC  9(8)  VALUE ZERO.              
010520     05  PV-DISPLAY-SOCKNO            PIC  9(8)- VALUE ZERO.              
010530     05  PV-COMMAREA-LENGTH              PIC S9(04)  VALUE +0             
010540                                                     COMP SYNC.           
010550     05  PV-PREV-SEQUENCE             PIC S9(05) COMP-3                   
010560                                                     VALUE ZERO.          
010570     05  PV-NEXT-SEQUENCE             PIC S9(05) COMP-3                   
010580                                                     VALUE ZERO.          
010600     05  PV-CICS-RESPONSE                PIC S9(08)  VALUE ZEROS          
010700                                                     COMP SYNC.           
010800     05  PV-RETRY-COUNTER                PIC S9(04)  VALUE ZEROS          
010900                                                     COMP SYNC.           
011000     05  PV-NULL-IND                     PIC S9(04)  VALUE ZEROS          
011100                                                     COMP SYNC.           
011200     05  PV-DB2-TROUBLE-COUNT            PIC S9(09)  VALUE ZEROS          
011300                                                     COMP-3.              
011400     05  PV-DB2-FREIGHT-COUNT            PIC S9(09)  VALUE ZEROS          
011500                                                     COMP-3.              
011600     05  PV-STR-RECS-RECEIVED            PIC  9(03)  VALUE ZEROS.         
011700     05  PV-STR-RECS-SENT                PIC  9(03)  VALUE ZEROS.         
011800     05  PV-TASK-X.                                                       
011900         10  PV-TASK                     PIC 9(07)   VALUE ZERO.          
012000     05  PV-RECEIVE-LENGTH               PIC S9(04)  VALUE +0             
012100                                                     COMP SYNC.           
012200     05  PV-WRITE-LENGTH                 PIC S9(04)  VALUE +0             
012300                                                     COMP SYNC.           
012400     05  PV-STACK-RBA                    PIC S9(09)  VALUE ZEROS          
012500                                                     COMP SYNC.           
012600     05  PV-P2L-SCN-CART-CNT             PIC S9(04)  VALUE ZEROS          
012700                                                     COMP SYNC.           
012800*!!                                                                       
012900*    05  PV-CICS-MSG-AREA                PIC  X(60)  VALUE SPACE.         
013000     05  PV-CICS-MSG-AREA                PIC  X(80)  VALUE SPACE.         
013100     05  PV-PRCG-STAT-CDE                PIC  X(01)  VALUE SPACE.         
013200     05  PV-ERROR-ORD-NBR                PIC  9(08)  VALUE ZEROS.         
013300     05  PV-ERROR-RCVR-SEQ-NBR           PIC  9(04)  VALUE ZEROS.         
013400     05  PV-ERROR-ORD-LNE-NBR            PIC  9(04)  VALUE ZEROS.         
013500     05  PV-ERROR-STR-NBR                PIC  9(04)  VALUE ZEROS.         
013600*!!                                                                       
013700     05  PV-DETAIL-CRTN-QTY-X.                                            
013800         10 PV-DETAIL-CRTN-QTY           PIC 9(04).                       
013900     05  PV-STR-NBR-X.                                                    
014000         10 PV-STR-NBR                   PIC 9(03).                       
014100     05  PV-ORD-NBR-X.                                                    
014200         10 PV-ORD-NBR                   PIC 9(08).                       
014300     05  PV-RECEIVER-NBR-X.                                               
014400         10 PV-RECEIVER-NBR              PIC 9(03).                       
014500     05  PV-LINE-NBR-X.                                                   
014600         10 PV-LINE-NBR                  PIC 9(04).                       
014700     05  PV-WORK-ORD-SEQ-NBR-X.                                           
014800         10 PV-WORK-ORD-SEQ-NBR          PIC 9(03).                       
014801                                                                          
014810     05  PV-ERROR-COUNT                  PIC 9(03) VALUE ZERO.            
014820     05  PV-ERROR-CODE                   PIC X(03) VALUE ZERO.            
014900                                                                          
014910                                                                          
014920* DMH - 03/07/08 - BEGIN                                                  
014930* add select parameter before recv socket command to determine if         
014940* is still active                                                         
014950*                                                                         
014960* SELECT-NUMFDS needs to be set to 3.  1 for each p2l system and          
014970* one for the print management system                                     
014980*                                                                         
014990*                                                                         
014991 01  SELECT-PARMS.                                                        
014992     02  SELECT-NUMFDS          PIC 9(8) COMP VALUE 3.                    
014993     02  SELECT-TIMEOUT.                                                  
014994         03  SELECT-SECS        PIC 9(8) COMP VALUE 05.                   
014995         03  SELECT-MILLSECS    PIC 9(8) COMP VALUE 0.                    
014996     02  SELECT-RD-MASK         PIC X(16).                                
014997     02  SELECT-WR-MASK         PIC X(16).                                
014998     02  SELECT-EX-MASK         PIC X(16).                                
014999     02  SELECT-RR-MASK         PIC X(16).                                
015000     02  SELECT-RW-MASK         PIC X(16).                                
015001     02  SELECT-RE-MASK         PIC X(16).                                
015002                                                                          
015003 01  CHAR-MASK.                                                           
015004     02 CHAR-STRING             PIC  X(100).                              
015005                                                                          
015006 01  CHAR-ARRAY REDEFINES CHAR-MASK.                                      
015007     02  CHAR-ENTRY-TABLE OCCURS 100 TIMES.                               
015008       03  CHAR-ENTRY         PIC  X.                                     
015009                                                                          
015010 01  BIT-MASK.                                                            
015011     02  BIT-ARRAY            PIC  X(16).                                 
015012                                                                          
015013 01  BIT-FUNCTION-CODES.                                                  
015014     02  CTOB                 PIC  X(4) VALUE 'CTOB'.                     
015015     02  BTOC                 PIC  X(4) VALUE 'BTOC'.                     
015016                                                                          
015017 01  BIT-MASK-LENGTH          PIC  9(8) COMP VALUE 100.                   
015018                                                                          
015019 01  BITMASK-TOKEN            PIC X(16) VALUE 'TCPIPBITMASKCOBL'.         
015020 01  SOCKETS                  PIC  9(8) COMP VALUE 100.                   
015021 01  SOCKNO                   PIC S9(8) BINARY.                           
015022 01  EZRETC                   PIC S9(8) COMP.                             
015023                                                                          
015024 01  SOCKET-SELECT-ERR.                                                   
015025     05  FILLER                       PIC X(10)                           
015026         VALUE ' PCKCS706-'.                                              
015027     05  FILLER                       PIC X(14)                           
015028         VALUE 'SOCKET SELECT-'.                                          
015029     05  FILLER                       PIC X(7)                            
015030         VALUE '-RETCD='.                                                 
015031     05  SOCKET-RETCODE               PIC 9(10)-.                         
015032     05  FILLER                       PIC X(1) VALUE SPACES.              
015033     05  SOCKET-ERRNO                 PIC 9(8).                           
015034     05  FILLER                       PIC X(1) VALUE SPACES.              
015035     05  SOCKET-DESCR                 PIC X(10).                          
015036     05  FILLER                       PIC X(1) VALUE SPACES.              
015037     05  SOCKET-MESSAGE               PIC X(100).                         
015038*                                                                         
015039* DMH - 03/07/08 - END                                                    
015040*                                                                         
015041                                                                          
015042                                                                          
015050 01  PT-MSG-TABLE.                                                        
015100     05  PT-MESSAGES OCCURS 200 TIMES INDEXED BY PT-MSG-IDX.              
015210         10 PT-MSG-SEQ               PIC S9(05) COMP-3.                   
015220         10 FILLER                   PIC X(1).                            
015300         10 PT-MESSAGE               PIC X(52).                           
015400         10 FILLER                   PIC X(1).                            
015500         10 PT-TASK                  PIC 9(07).                           
015600/                                                                         
015700*---------------------------------------------------------------*         
015800*    DB2 AREA FOR TDISTHD - DISTRIBUTION TRANSACTION HEADER     *         
015900*---------------------------------------------------------------*         
016000     EXEC SQL                                                             
016100          INCLUDE TDISTHD                                                 
016200     END-EXEC.                                                            
016201                                                                          
016210*---------------------------------------------------------------*         
016220*    DB2 AREA FOR TKRPRPM - PARAMETER TABLE                     *         
016230*---------------------------------------------------------------*         
016240     EXEC SQL                                                             
016250          INCLUDE TKRPRPM                                                 
016260     END-EXEC.                                                            
016300/                                                                         
016310*---------------------------------------------------------------*         
016320*    DB2 AREA FOR TUPLDER - upload error table                  *         
016330*---------------------------------------------------------------*         
016340     EXEC SQL                                                             
016350          INCLUDE TUPLDER                                                 
016360     END-EXEC.                                                            
016370                                                                          
016400*---------------------------------------------------------------*         
016500*    DB2 AREA FOR COMMUNICATIONS                                *         
016600*---------------------------------------------------------------*         
016700     EXEC SQL                                                             
016800          INCLUDE SQLCA                                                   
016900     END-EXEC.                                                            
017000/                                                                         
017100 PROCEDURE DIVISION.                                                      
017200                                                                          
017300 0000-PROGRAM-MAINLINE.                                                   
017400                                                                          
017500     MOVE EIBTASKN               TO PV-TASK                               
017600     MOVE SPACES                 TO PV-CICS-MSG-AREA                      
017700     PERFORM 9990-WRITE-CICS                                              
017800     STRING ' PCKCS705 PCC LAUNCH STARTED - TASK = ' PV-TASK-X            
017900         DELIMITED BY SIZE INTO PV-CICS-MSG-AREA                          
018000     PERFORM 9990-WRITE-CICS                                              
018100                                                                          
018200     MOVE SPACES                 TO PT-MSG-TABLE.                         
018300     SET PT-MSG-IDX              TO 1.                                    
018310     MOVE 1                      TO PT-MSG-SEQ (PT-MSG-IDX).              
018400     MOVE EIBTASKN               TO PT-TASK(PT-MSG-IDX)                   
018500     MOVE ' PCKCS705 - STARTED'  TO PT-MESSAGE(PT-MSG-IDX)                
018510                                                                          
018520     MOVE  PT-MSG-SEQ(PT-MSG-IDX) TO  PV-PREV-SEQUENCE                    
018530     SET   PT-MSG-IDX UP BY 1                                             
018600     COMPUTE PV-NEXT-SEQUENCE = PV-PREV-SEQUENCE + 1.                     
018601     MOVE  PV-NEXT-SEQUENCE  TO PT-MSG-SEQ (PT-MSG-IDX).                  
018613                                                                          
018621     PERFORM 7115-GET-SOCKET-TIMEOUT.                                     
018630                                                                          
018700     PERFORM 1000-GET-COMMAREA-FROM-LSTNR.                                
018800                                                                          
018900*    STRING ' COMMAREA = ' DP081-LSTN-TCPSOCKET-PARM                      
019000*        DELIMITED BY SIZE INTO PV-CICS-MSG-AREA f                        
019100*    PERFORM 9990-WRITE-CICS                                              
019200                                                                          
019400     MOVE EIBTASKN               TO PT-TASK(PT-MSG-IDX)                   
019500     MOVE ' PCKCS705 - AFTER RETRIEVE >>>> '                              
019600                                 TO PT-MESSAGE(PT-MSG-IDX)                
019601                                                                          
019610     MOVE  PT-MSG-SEQ(PT-MSG-IDX) TO  PV-PREV-SEQUENCE                    
019620     SET   PT-MSG-IDX UP BY 1                                             
019630     COMPUTE PV-NEXT-SEQUENCE = PV-PREV-SEQUENCE + 1.                     
019640     MOVE  PV-NEXT-SEQUENCE  TO PT-MSG-SEQ (PT-MSG-IDX).                  
019700                                                                          
019800     PERFORM 1100-TAKE-SOCKET.                                            
019900                                                                          
020000*    MOVE ' TAKE SOCKET COMPLETE ' TO                                     
020100*         PV-CICS-MSG-AREA                                                
020200*    PERFORM 9990-WRITE-CICS                                              
020300                                                                          
020500     MOVE EIBTASKN               TO PT-TASK(PT-MSG-IDX)                   
020600     MOVE ' PCKCS705 - TAKESOCKET OK'                                     
020700                                 TO PT-MESSAGE(PT-MSG-IDX)                
020800                                                                          
020820     MOVE  PT-MSG-SEQ(PT-MSG-IDX) TO  PV-PREV-SEQUENCE                    
020830     SET   PT-MSG-IDX UP BY 1                                             
020840     COMPUTE PV-NEXT-SEQUENCE = PV-PREV-SEQUENCE + 1.                     
020850     MOVE  PV-NEXT-SEQUENCE  TO PT-MSG-SEQ (PT-MSG-IDX).                  
020860                                                                          
020900     PERFORM 1200-CONFIRM-CONNECTION.                                     
021000                                                                          
021200     MOVE EIBTASKN               TO PT-TASK(PT-MSG-IDX)                   
021300     MOVE ' PCKCS705 - WRITE SOCKET OK'                                   
021400                                 TO PT-MESSAGE(PT-MSG-IDX)                
021410                                                                          
021420     MOVE  PT-MSG-SEQ(PT-MSG-IDX) TO  PV-PREV-SEQUENCE                    
021430     SET   PT-MSG-IDX UP BY 1                                             
021440     COMPUTE PV-NEXT-SEQUENCE = PV-PREV-SEQUENCE + 1.                     
021450     MOVE  PV-NEXT-SEQUENCE  TO PT-MSG-SEQ (PT-MSG-IDX).                  
021500                                                                          
021510     PERFORM 1250-CHECK-SOCKET-CONNECTION.                                
021520                                                                          
021600     PERFORM 1300-RECEIVE-DATA-FROM-QNX.                                  
021700                                                                          
021800                                                                          
021900     PERFORM 1400-SEND-CONFIRMATION-QNX.                                  
022000                                                                          
022200     MOVE EIBTASKN               TO PT-TASK(PT-MSG-IDX)                   
022300     MOVE ' PCKCS705 - SEND CONFIRM OK'                                   
022400                                 TO PT-MESSAGE(PT-MSG-IDX)                
022410                                                                          
022420     MOVE  PT-MSG-SEQ(PT-MSG-IDX) TO  PV-PREV-SEQUENCE                    
022430     SET   PT-MSG-IDX UP BY 1                                             
022440     COMPUTE PV-NEXT-SEQUENCE = PV-PREV-SEQUENCE + 1.                     
022450     MOVE  PV-NEXT-SEQUENCE  TO PT-MSG-SEQ (PT-MSG-IDX).                  
022460                                                                          
022600     PERFORM 1500-CLOSE-SOCKET.                                           
022700                                                                          
022900     MOVE EIBTASKN               TO PT-TASK(PT-MSG-IDX)                   
023000     MOVE ' PCKCS705 - CLOSE SOCKET OK'                                   
023100                                 TO PT-MESSAGE(PT-MSG-IDX)                
023110                                                                          
023120     MOVE  PT-MSG-SEQ(PT-MSG-IDX) TO  PV-PREV-SEQUENCE                    
023130     SET   PT-MSG-IDX UP BY 1                                             
023140     COMPUTE PV-NEXT-SEQUENCE = PV-PREV-SEQUENCE + 1.                     
023150     MOVE  PV-NEXT-SEQUENCE  TO PT-MSG-SEQ (PT-MSG-IDX).                  
023200                                                                          
023300     STRING ' PCKCS705 ENDED - TASK = ' PV-TASK-X                         
023301            ' '                                                           
023310            PV-ORD-NBR '-'                                                
023330            PV-RECEIVER-NBR '-'                                           
023350            PV-LINE-NBR                                                   
023400            DELIMITED BY SIZE INTO PV-CICS-MSG-AREA                       
023500     PERFORM 9990-WRITE-CICS                                              
023600                                                                          
023700     GOBACK.                                                              
023800/                                                                         
023900************************************************************              
024000* THE LISTENER HAS BEEN STARTED AND A SOCKET HAS BEEN      *              
024100* ESTABLISHED WITHIN THE LISTENER PROGRAM.  THE LISTENER   *              
024200* PROGRAM IN TURN WILL START THIS PROGRAM, SO WE MUST      *              
024300* DO A RETREIVE TO GET THE COMMAREA WHICH WAS PASSED.      *              
024400************************************************************              
024500 1000-GET-COMMAREA-FROM-LSTNR.                                            
024600     MOVE LENGTH OF DP081-LSTN-TCPSOCKET-PARM                             
024700                                      TO PV-RECEIVE-LENGTH.               
024800                                                                          
024900     EXEC CICS                                                            
025000         RETRIEVE                                                         
025100             INTO    (DP081-LSTN-TCPSOCKET-PARM)                          
025200             LENGTH  (PV-RECEIVE-LENGTH)                                  
025300             RESP    (PV-CICS-RESPONSE)                                   
025400     END-EXEC.                                                            
025500                                                                          
025600     EVALUATE TRUE                                                        
025700         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
025800             CONTINUE                                                     
025900         WHEN OTHER                                                       
026000             MOVE '1000-GET-COMMAREA-FROM-LSTNR'                          
026100                 TO DP013-PARAGRAPH                                       
026200             MOVE 'UNABLE TO RETRIEVE COMMAREA FROM LISTENER'             
026300                 TO DP013-MESSAGE-TEXT (1)                                
026400             SET DP013-CICS-ABEND   TO  TRUE                              
026500             SET DP013-NO-ROLLBACK  TO  TRUE                              
026600             PERFORM 9999-WRITE-CICS-LOG-MSG                              
026700     END-EVALUATE.                                                        
026800/                                                                         
026900*----------------------------------------------------------------*        
027000*  ACQUIRE A SOCKET FROM ANOTHER PROGRAM AND CREATE A NEW SOCKET.*        
027100*----------------------------------------------------------------*        
027200 1100-TAKE-SOCKET.                                                        
027300                                                                          
027400     MOVE DP081-AF-INET               TO DP081-CID-DOMAIN-LSTN.           
027500     MOVE DP081-LSTN-NAME             TO DP081-CID-NAME-LSTN.             
027600     MOVE DP081-LSTN-SUBTASKNAME      TO                                  
027700                                     DP081-CID-SUBTASKNAME-LSTN.          
027800     MOVE DP081-LSTN-GIVE-TAKE-SOCKET      TO                             
027900                                         DP081-SOCK-ID.                   
028000                                                                          
028100     CALL 'EZASOKET'                                                      
028200         USING                                                            
028300                DP081-TAKESOCKET-CMD                                      
028400                DP081-SOCK-ID                                             
028500                DP081-CLIENTID-LSTN                                       
028600                DP081-ERRNO                                               
028700                DP081-RETCODE.                                            
028800                                                                          
028900     IF  DP081-RETCODE >= ZERO                                            
029000         MOVE DP081-RETCODE           TO DP081-SOCK-ID                    
029100     ELSE                                                                 
029200         MOVE PC-CURRENT-PGM-NAME TO DP081-TCP-IP-PGM-NAME                
029300         SET  DP081-TCP-IP-TAKE-ERR     TO TRUE                           
029400         MOVE DP081-ERRNO               TO DP081-TCP-IP-ERR-NBR           
029500         SET  DP013-OTHER-ABEND   TO TRUE                                 
029600         SET  DP013-NO-ROLLBACK   TO TRUE                                 
029700         MOVE '1100-TAKE-SOCKET'  TO DP013-PARAGRAPH                      
029800         MOVE DP081-TCP-IP-LOGGING-MSG  TO DP013-MESSAGE-TEXT (1)         
029900         PERFORM 9999-WRITE-CICS-LOG-MSG                                  
030000     END-IF.                                                              
030100/                                                                         
030200*----------------------------------------------------------------*        
030300*    CONFIRM THE CONNECTION ON THE SOCKET THAT WE RECEIVED FROM  *        
030400*    THE PACK TO LIGHT SYSTEM.                                   *        
030500*----------------------------------------------------------------*        
030600 1200-CONFIRM-CONNECTION.                                                 
030700                                                                          
030800     MOVE SPACES                      TO TCP-BUF-AREA.                    
030900     MOVE TASK-START                  TO TCP-BUF-AREA.                    
031000     MOVE 40                          TO DP081-NBR-BYTES                  
031100                                         DP081-TCPLENG.                   
031200                                                                          
031300* TRANSLATE EBCDIC TO ASCII                                               
031400     CALL 'EZACIC04'                                                      
031500         USING  DP081-TOASCII-TOKEN                                       
031600                TCP-BUF-AREA                                              
031700                DP081-TCPLENG.                                            
031800                                                                          
031900* WRITE DATA ON A CONNECTED SOCKET                                        
032000     CALL 'EZASOKET'                                                      
032100         USING  DP081-WRITE-CMD                                           
032200                DP081-SOCK-ID                                             
032300                DP081-NBR-BYTES                                           
032400                TCP-BUF-AREA                                              
032500                DP081-ERRNO                                               
032600                DP081-RETCODE.                                            
032700                                                                          
032800*                                                                         
032900     IF DP081-RETCODE < ZERO                                              
033000         MOVE PC-CURRENT-PGM-NAME TO DP081-TCP-IP-PGM-NAME                
033100         SET  DP081-TCP-IP-WRITE-ERR    TO TRUE                           
033200         MOVE DP081-ERRNO         TO DP081-TCP-IP-ERR-NBR                 
033300         SET  DP013-OTHER-ABEND   TO TRUE                                 
033400         SET  DP013-NO-ROLLBACK   TO TRUE                                 
033500         MOVE '1200-CONFIRM-CONNECTION'                                   
033600             TO DP013-PARAGRAPH                                           
033700         MOVE DP081-TCP-IP-LOGGING-MSG  TO DP013-MESSAGE-TEXT (1)         
033800         MOVE DP081-TCP-IP-ERR-NBR TO DP013-MESSAGE-TEXT (2)              
033900         PERFORM 9999-WRITE-CICS-LOG-MSG                                  
034000     END-IF.                                                              
034100/                                                                         
034110                                                                          
034120* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
034130*  added Select socket command to check if socket connection    *         
034140*  is still active.  if return code > 0 then socket is active   *         
034150*  if return code = 0 socket is no longer active.               *         
034160* DMH - 03/07/08 - BEGIN                                                  
034170* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
034180 1250-CHECK-SOCKET-CONNECTION.                                            
034190                                                                          
034191     INITIALIZE  PV-CICS-MSG-AREA.                                        
034192     STRING 'PCKCS705 - 1250-CHECK-SOCKET-CONNECT'                        
034193                                              DELIMITED BY SIZE           
034194                                         INTO PV-CICS-MSG-AREA.           
034195     PERFORM 9995-WRITE-CICS.                                             
034196                                                                          
034197* ZERO OUT ALL OUTPUT BIT MASKS FOR SELECT                                
034198     PERFORM VARYING SOCKNO FROM 1 BY 1 UNTIL SOCKNO > SOCKETS            
034199        MOVE '0' TO CHAR-ENTRY(SOCKNO)                                    
034200     END-PERFORM.                                                         
034201     PERFORM 1260-CHAR-TO-BIT.                                            
034202                                                                          
034203        INITIALIZE                       PV-CICS-MSG-AREA                 
034204        MOVE SOCKNO                 TO  PV-DISPLAY-SOCKNO                 
034205        MOVE SOCKETS                TO  PV-DISPLAY-SOCKETS                
034206        STRING ' PCKCS705- 1250-'                                         
034207               '  SOCKNO='   PV-DISPLAY-SOCKNO                            
034208               '  SOCKETS='  PV-DISPLAY-SOCKETS                           
034209                                         DELIMITED BY SIZE                
034210                                    INTO PV-CICS-MSG-AREA                 
034211        MOVE LENGTH OF PV-CICS-MSG-AREA                                   
034212                                      TO PV-COMMAREA-LENGTH               
034213     PERFORM 9995-WRITE-CICS.                                             
034214                                                                          
034215     MOVE BIT-MASK TO SELECT-RR-MASK.                                     
034216     MOVE BIT-MASK TO SELECT-RW-MASK.                                     
034217     MOVE BIT-MASK TO SELECT-RE-MASK.                                     
034218     MOVE BIT-MASK TO SELECT-EX-MASK.                                     
034219     MOVE BIT-MASK TO SELECT-WR-MASK.                                     
034220                                                                          
034221* set all input bit masks for select to 1                                 
034222     PERFORM VARYING SOCKNO FROM 1 BY 1 UNTIL SOCKNO > SOCKETS            
034223        MOVE '1' TO CHAR-ENTRY(SOCKNO)                                    
034224     END-PERFORM.                                                         
034225     PERFORM 1260-CHAR-TO-BIT.                                            
034226     MOVE BIT-MASK TO SELECT-RD-MASK.                                     
034227                                                                          
034228     INITIALIZE                       PV-CICS-MSG-AREA                    
034229     MOVE SOCKNO                 TO  PV-DISPLAY-SOCKNO                    
034230     MOVE SOCKETS                TO  PV-DISPLAY-SOCKETS                   
034231     STRING ' PCKCS705- 1250-'                                            
034232            '  SOCKNO='   PV-DISPLAY-SOCKNO                               
034233            '  SOCKETS='  PV-DISPLAY-SOCKETS                              
034234                                      DELIMITED BY SIZE                   
034235                                 INTO PV-CICS-MSG-AREA                    
034236     MOVE LENGTH OF PV-CICS-MSG-AREA                                      
034237                                   TO PV-COMMAREA-LENGTH                  
034238     PERFORM 9995-WRITE-CICS.                                             
034239                                                                          
034240*                                                                         
034241* ISSUE THE SELECT COMMAND ----------------------------                   
034242* SELECT WILL FLIP A BIT IN THE READ MASK TO '1' FOR ANY                  
034243* SOCKET THAT HAS DATA TO BE PROCESSED.                                   
034244*                                                                         
034245                                                                          
034246     MOVE 0 TO DP081-ERRNO.                                               
034247     MOVE 0 TO DP081-RETCODE.                                             
034248     CALL 'EZASOKET' USING DP081-SELECT-CMD                               
034249         SELECT-NUMFDS SELECT-TIMEOUT                                     
034250         SELECT-RD-MASK SELECT-WR-MASK SELECT-EX-MASK                     
034251         SELECT-RR-MASK SELECT-RW-MASK SELECT-RE-MASK                     
034252         DP081-ERRNO DP081-RETCODE.                                       
034253                                                                          
034254                                                                          
034255     INITIALIZE                       PV-CICS-MSG-AREA                    
034256     MOVE SOCKNO                 TO  PV-DISPLAY-SOCKNO                    
034257     MOVE SOCKETS                TO  PV-DISPLAY-SOCKETS                   
034258     STRING ' PCKCS705- 1250-'                                            
034259            '  RD-MASK=' SELECT-RD-MASK                                   
034260            '  WR-MASK=' SELECT-WR-MASK                                   
034261            '  EX-MASK=' SELECT-EX-MASK                                   
034262                                      DELIMITED BY SIZE                   
034263                                 INTO PV-CICS-MSG-AREA                    
034264     MOVE LENGTH OF PV-CICS-MSG-AREA                                      
034265                                   TO PV-COMMAREA-LENGTH                  
034266     PERFORM 9995-WRITE-CICS.                                             
034267                                                                          
034268     INITIALIZE                       PV-CICS-MSG-AREA                    
034269     MOVE SOCKNO                 TO  PV-DISPLAY-SOCKNO                    
034270     MOVE SOCKETS                TO  PV-DISPLAY-SOCKETS                   
034271     STRING ' PCKCS705  1250-'                                            
034272            '  RR-MASK=' SELECT-RR-MASK                                   
034273            '  RW-MASK=' SELECT-RW-MASK                                   
034274            '  RE-MASK=' SELECT-RE-MASK                                   
034275            '  BIT-MASK=' BIT-MASK                                        
034276                                      DELIMITED BY SIZE                   
034277                                 INTO PV-CICS-MSG-AREA                    
034278     MOVE LENGTH OF PV-CICS-MSG-AREA                                      
034279                                   TO PV-COMMAREA-LENGTH                  
034280     PERFORM 9995-WRITE-CICS.                                             
034281                                                                          
034282                                                                          
034283     IF DP081-RETCODE < 0                                                 
034284         INITIALIZE  PV-CICS-MSG-AREA                                     
034285         SET  PS-SOCKET-ERROR        TO TRUE                              
034286         MOVE PC-CURRENT-PGM-NAME     TO DP081-TCP-IP-PGM-NAME            
034287         SET  DP081-TCP-IP-SEND-TO-ERR TO TRUE                            
034288         MOVE DP081-ERRNO            TO DP081-TCP-IP-ERR-NBR              
034289                                        SOCKET-ERRNO                      
034290         MOVE DP081-RETCODE          TO SOCKET-RETCODE                    
034291         MOVE PC008-STATUS-RECORD     TO SOCKET-MESSAGE                   
034292         MOVE 'SEE ERROR NBR '       TO SOCKET-DESCR                      
034293         MOVE SOCKET-SELECT-ERR      TO PV-CICS-MSG-AREA                  
034294         PERFORM 9995-WRITE-CICS                                          
034295                                                                          
034296         INITIALIZE  PV-CICS-MSG-AREA                                     
034297         STRING ' PCKCS705 -'                                             
034298                 PC008-STATUS-RECORD                                      
034299                                         DELIMITED BY SIZE                
034300                                     INTO PV-CICS-MSG-AREA                
034301         PERFORM 9995-WRITE-CICS                                          
034302                                                                          
034303         INITIALIZE  PV-CICS-MSG-AREA                                     
034304         MOVE DP081-ERRNO            TO DP081-TCP-IP-ERR-NBR              
034305         MOVE DP081-TCP-IP-LOGGING-MSG TO PV-CICS-MSG-AREA                
034306         PERFORM 9995-WRITE-CICS                                          
034307                                                                          
034308     END-IF.                                                              
034309                                                                          
034310     IF DP081-RETCODE = 0                                                 
034311         INITIALIZE  PV-CICS-MSG-AREA                                     
034312         SET  PS-SOCKET-ERROR        TO TRUE                              
034313         MOVE PC-CURRENT-PGM-NAME     TO DP081-TCP-IP-PGM-NAME            
034314         SET  DP081-TCP-IP-SEND-TO-ERR TO TRUE                            
034315         MOVE DP081-ERRNO            TO DP081-TCP-IP-ERR-NBR              
034316                                        SOCKET-ERRNO                      
034317         MOVE DP081-RETCODE          TO SOCKET-RETCODE                    
034318         MOVE PC008-STATUS-RECORD     TO SOCKET-MESSAGE                   
034319         MOVE 'SOCKET TIMEOUT'       TO SOCKET-DESCR                      
034320                                                                          
034321         MOVE SOCKET-SELECT-ERR      TO PV-CICS-MSG-AREA                  
034322         PERFORM 9995-WRITE-CICS                                          
034323                                                                          
034324         INITIALIZE  PV-CICS-MSG-AREA                                     
034325         STRING ' PCKCS705 -'            DELIMITED BY SIZE                
034326                 PC008-STATUS-RECORD                                      
034327                                         DELIMITED BY SIZE                
034328                                     INTO PV-CICS-MSG-AREA                
034329         PERFORM 9995-WRITE-CICS                                          
034330                                                                          
034331         INITIALIZE  PV-CICS-MSG-AREA                                     
034332         MOVE DP081-ERRNO            TO DP081-TCP-IP-ERR-NBR              
034333         MOVE DP081-TCP-IP-LOGGING-MSG TO PV-CICS-MSG-AREA                
034334         PERFORM 9995-WRITE-CICS                                          
034335                                                                          
034336     END-IF.                                                              
034337                                                                          
034338***************************************************************           
034339* CHAR-TO-BIT                                                 *           
034340* CONVERT THE CHARACTER ARRAY TO A SELECT BIT MASK SO THAT THE*           
034341* COBOL PROGRAM CAN WRITE IT.                                 *           
034342***************************************************************           
034343 1260-CHAR-TO-BIT.                                                        
034344                                                                          
034345     INITIALIZE                       PV-CICS-MSG-AREA                    
034346     STRING ' PCKCS706- 1260-CHAR-TO-BIT              '                   
034347                                         DELIMITED BY SIZE                
034348                                    INTO PV-CICS-MSG-AREA                 
034349     MOVE LENGTH OF PV-CICS-MSG-AREA                                      
034350                                      TO PV-COMMAREA-LENGTH               
034351     PERFORM 9995-WRITE-CICS.                                             
034352                                                                          
034353* CHAR-TO-BIT                                                             
034354* CONVERT THE CHARACTER ARRAY TO A SELECT BIT MASK SO THAT THE            
034355* COBOL PROGRAM CAN WRITE IT.                                             
034356*                                                                         
034357     CALL 'EZACIC06' USING BITMASK-TOKEN CTOB                             
034358          BIT-MASK CHAR-MASK BIT-MASK-LENGTH EZRETC                       
034359     IF  EZRETC < 0 THEN                                                  
034360         MOVE '!EZCIC06 - CTOB-EXCP ' TO PV-CICS-MSG-AREA                 
034361         MOVE LENGTH OF PV-CICS-MSG-AREA TO PV-COMMAREA-LENGTH            
034362                                                                          
034363         EXEC CICS                                                        
034364             WRITEQ TD                                                    
034365                 QUEUE   ('CSSL')                                         
034366                 FROM    (PV-CICS-MSG-AREA)                               
034367                 LENGTH  (PV-COMMAREA-LENGTH)                             
034368                 RESP    (PV-CICS-RESPONSE)                               
034369         END-EXEC                                                         
034370                                                                          
034371         INITIALIZE  PV-CICS-MSG-AREA                                     
034372         SET  PS-SOCKET-ERROR        TO TRUE                              
034373         MOVE PC-CURRENT-PGM-NAME     TO DP081-TCP-IP-PGM-NAME            
034374         SET  DP081-TCP-IP-SEND-TO-ERR TO TRUE                            
034375         MOVE DP081-ERRNO            TO DP081-TCP-IP-ERR-NBR              
034376                                        SOCKET-ERRNO                      
034377         MOVE DP081-RETCODE          TO SOCKET-RETCODE                    
034378         MOVE PC008-STATUS-RECORD     TO SOCKET-MESSAGE                   
034379         MOVE 'SEE ERROR NO '        TO SOCKET-DESCR                      
034380                                                                          
034381         MOVE SOCKET-SELECT-ERR      TO PV-CICS-MSG-AREA                  
034382         PERFORM 9995-WRITE-CICS                                          
034383                                                                          
034384         INITIALIZE  PV-CICS-MSG-AREA                                     
034385         STRING ' PCKCS706 -'            DELIMITED BY SIZE                
034386                 PC008-STATUS-RECORD                                      
034387                                         DELIMITED BY SIZE                
034388                                     INTO PV-CICS-MSG-AREA                
034389         PERFORM 9995-WRITE-CICS                                          
034390                                                                          
034391         INITIALIZE  PV-CICS-MSG-AREA                                     
034392         MOVE DP081-ERRNO            TO DP081-TCP-IP-ERR-NBR              
034393         MOVE DP081-TCP-IP-LOGGING-MSG TO PV-CICS-MSG-AREA                
034394         PERFORM 9995-WRITE-CICS                                          
034395                                                                          
034396     END-IF.                                                              
034397                                                                          
034398* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
034399* DMH - 03/07/08 - END                                                    
034400                                                                          
034401*----------------------------------------------------------------*        
034402*    RECEIVE THE DATA FROM THE PACK TO LIGHT SYSTEM.  THIS DATA  *        
034410*    WILL CONSIST OF A HEADER RECORD CONTAINING INFORMATION ON   *        
034500*    WHEN THE LINE WAS COMPLETED IN THE PACK TO LIGHT SYSTEM.    *        
034600*    DETAIL RECORDS ARE ALSO SENT FOR EACH STORE ON THE RECEIVER *        
034700*    LINE CONTAINING THE NUMBER OF PIECES PROCESSED.  A TRAILER  *        
034800*    RECORD IS SENT CONTAINING THE NUMBER OF STORE RECORDS THAT  *        
034900*    WERE SENT TO BE USED FOR CROSS-CHECK VALIDATION.            *        
035000*----------------------------------------------------------------*        
035100 1300-RECEIVE-DATA-FROM-QNX.                                              
035200                                                                          
035300* RECEIVE DATA ON A CONNECTED SOCKET                                      
035400                                                                          
035500     MOVE ZERO                        TO PV-BUFFER-SUB                    
035600     MOVE SPACES                      TO TCP-BUF-AREA.                    
035700     SET PS-SOCKET-OPEN               TO TRUE.                            
035800     MOVE PC008-DL-STAT-DETAIL        TO DP081-TCPLENG.                   
035900*    MOVE 50                          TO DP081-NBR-BYTES                  
036000     MOVE 1                           TO DP081-NBR-BYTES                  
036100                                                                          
036200*    PERFORM UNTIL DP081-NBR-BYTES = ZERO                                 
036300     PERFORM UNTIL TCP-BUF (PV-BUFFER-SUB) = PC-TERM-CHARACTER            
036400                OR PS-SOCKET-CLOSED                                       
036500                                                                          
036600         CALL 'EZASOKET'                                                  
036700             USING                                                        
036800                DP081-RECV-CMD                                            
036900                DP081-SOCK-ID                                             
037000                DP081-FLAG                                                
037100                DP081-NBR-BYTES                                           
037200                TCP-BUF (PV-BUFFER-SUB + 1)                               
037300                DP081-ERRNO                                               
037400                DP081-RETCODE                                             
037500                                                                          
037600         IF  DP081-RETCODE > ZERO                                         
037700             ADD DP081-RETCODE        TO PV-BUFFER-SUB                    
037800*            SUBTRACT DP081-RETCODE FROM DP081-NBR-BYTES                  
037900         ELSE                                                             
038000             IF  DP081-RETCODE = ZERO                                     
038100                 SET PS-SOCKET-CLOSED     TO TRUE                         
038200             ELSE                                                         
038300                 PERFORM 1410-SEND-ERROR-CONFIRM-QNX                      
038400                 PERFORM 1500-CLOSE-SOCKET                                
038500                 MOVE PC-CURRENT-PGM-NAME TO DP081-TCP-IP-PGM-NAME        
038600                 SET  DP081-TCP-IP-READ-ERR                               
038700                                          TO TRUE                         
038800                 MOVE DP081-ERRNO         TO DP081-TCP-IP-ERR-NBR         
038900                 SET  DP013-OTHER-ABEND   TO TRUE                         
039000                 SET  DP013-NO-ROLLBACK   TO TRUE                         
039100                 MOVE '1300-RECEIVE-DATA-FROM-QNX'                        
039200                                         TO DP013-PARAGRAPH               
039300                 MOVE DP081-TCP-IP-LOGGING-MSG                            
039400                                         TO DP013-MESSAGE-TEXT (1)        
039500                 PERFORM 9999-WRITE-CICS-LOG-MSG                          
039600         END-IF                                                           
039700             END-IF                                                       
039800     END-PERFORM.                                                         
039900                                                                          
040000* TRANSLATE ASCII TO EBCDIC                                               
040100     CALL 'EZACIC05'                                                      
040200         USING  DP081-TOEBCDIC-TOKEN                                      
040300                TCP-BUF-AREA                                              
040400                DP081-TCPLENG.                                            
040500                                                                          
040600     INITIALIZE PC008-STATUS-RECORD.                                      
040700                                                                          
040800     MOVE TCP-BUF-AREA           TO PC008-STATUS-RECORD.                  
040900                                                                          
041000*    STRING ' ' PC008-STATUS-RECORD                                       
041100*        DELIMITED BY SIZE INTO PV-CICS-MSG-AREA                          
041200*    PERFORM 9990-WRITE-CICS                                              
041300                                                                          
041400*    CALL 'EZASOKET'                                                      
041500*        USING                                                            
041600*               DP081-RECV-CMD                                            
041700*               DP081-SOCK-ID                                             
041800*               DP081-FLAG                                                
041900*               DP081-NBR-BYTES                                           
042000*               TCP-BUF                                                   
042100*               DP081-ERRNO                                               
042200*               DP081-RETCODE.                                            
042300*                                                                         
042400*    IF DP081-RETCODE < ZERO                                              
042500*        PERFORM 1410-SEND-ERROR-CONFIRM-QNX                              
042600*        PERFORM 1500-CLOSE-SOCKET                                        
042700*        MOVE PC-CURRENT-PGM-NAME TO DP081-TCP-IP-PGM-NAME                
042800*        SET  DP081-TCP-IP-READ-ERR     TO TRUE                           
042900*        MOVE DP081-ERRNO               TO DP081-TCP-IP-ERR-NBR           
043000*        SET  DP013-OTHER-ABEND   TO TRUE                                 
043100*        SET  DP013-NO-ROLLBACK   TO TRUE                                 
043200*        MOVE '1300-RECEIVE-DATA-FROM-QNX'                                
043300*            TO DP013-PARAGRAPH                                           
043400*        MOVE DP081-TCP-IP-LOGGING-MSG  TO DP013-MESSAGE-TEXT (1)         
043500*        PERFORM 9999-WRITE-CICS-LOG-MSG                                  
043600*    END-IF.                                                              
043700*                                                                         
043800* TRANSLATE ASCII TO EBCDIC                                               
043900*    CALL 'EZACIC05'                                                      
044000*        USING  DP081-TCP-TOKEN                                           
044100*               TCP-BUF2                                                  
044200*               DP081-TCPLENG.                                            
044300*                                                                         
044400*    INITIALIZE PC008-STATUS-RECORD.                                      
044500*                                                                         
044600*    MOVE TCP-BUF2               TO PC008-STATUS-RECORD.                  
044700                                                                          
044800**!!                                                                      
044900*                                                                         
045000*    INITIALIZE PV-CICS-MSG-AREA.                                         
045100*                                                                         
045200*    STRING ' PCKCS705:'                                                  
045300*         TCP-BUF2                                                        
045400*        DELIMITED BY SIZE INTO PV-CICS-MSG-AREA.                         
045500*    PERFORM 9990-WRITE-CICS                                              
045600*                                                                         
045700*    INITIALIZE PV-CICS-MSG-AREA.                                         
045800*                                                                         
045900*    STRING ' PCKCS705:'                                                  
046000*         'PO='                                                           
046100*         PC008-DTL-ORD-NBR                                               
046200*         ' RCVR='                                                        
046300*         PC008-DTL-RECEIVER-NBR                                          
046400*         ' LINE='                                                        
046500*         PC008-DTL-LINE-NBR                                              
046600*         ' WO='                                                          
046700*         PC008-DTL-WORK-ORD-SEQ-NBR                                      
046800*        DELIMITED BY SIZE INTO PV-CICS-MSG-AREA.                         
046900*    PERFORM 9990-WRITE-CICS                                              
047000*                                                                         
047100**!!                                                                      
047200                                                                          
047300*-----------------------------------------------------------------        
047301*   dmh - 10/09/2017                                                      
047302*                                                                         
047310*   mainframe hung because multiple PCKCS705 transactions were            
047320*   abending.  Transactions were abending because the MF was              
047330*   not responding in 5 seconds to the transaction starting.              
047340*                                                                         
047350*   need to add a check to make sure the data is valid                    
047360*   if bad data will log to TUPLDER indicating bad response               
047370*   from mainframe for Pack-to-Light inbound launch                       
047380*                                                                         
047390*-----------------------------------------------------------------        
047392                                                                          
047393     IF  PC008-DTL-CNTL-NBR   NUMERIC                                     
047400                                                                          
047500         MOVE PC008-DTL-ORD-NBR      TO PV-ORD-NBR-X                      
047600         MOVE PV-ORD-NBR             TO DK-ORD-NBR                        
047700         MOVE PC008-DTL-RECEIVER-NBR TO PV-RECEIVER-NBR-X                 
047800         MOVE PV-RECEIVER-NBR        TO DK-REC-SEQ-NBR                    
047900         MOVE PC008-DTL-LINE-NBR     TO PV-LINE-NBR-x                     
048000         MOVE PV-LINE-NBR            TO DK-ORD-LNE-NBR                    
048100         MOVE PC008-DTL-WORK-ORD-SEQ-NBR                                  
048200                                     TO PV-WORK-ORD-SEQ-NBR-X             
048300         MOVE PV-WORK-ORD-SEQ-NBR    TO DK-WRK-ORD-SEQ-NBR                
048400                                                                          
048500         IF PC008-DTL-CRTN-QTY NUMERIC                                    
048600             MOVE PC008-DTL-CRTN-QTY TO PV-DETAIL-CRTN-QTY-X              
048700         ELSE                                                             
048800             MOVE ZEROES             TO PV-DETAIL-CRTN-QTY                
048900         END-IF                                                           
049000                                                                          
049100         MOVE PV-DETAIL-CRTN-QTY     TO PV-P2L-SCN-CART-CNT               
049200                                                                          
049300         IF  PV-P2L-SCN-CART-CNT EQUAL ZEROS                              
049400             CONTINUE                                                     
049500         ELSE                                                             
049600             PERFORM 1330-UPDATE-TDISTHD                                  
050300         END-IF                                                           
050310     ELSE                                                                 
050311         MOVE '101'           TO PV-ERROR-CODE                            
050312         PERFORM 7010-DC-NBR-FROM-TRANID                                  
050313         PERFORM 7000-INSERT-TUPLDER                                      
050330     END-IF.                                                              
050400                                                                          
050500                                                                          
050600*----------------------------------------------------------------*        
050700*    UPDATE THE SCAN CARTON COUNT ON THE DISTRIBUTION HEADER ROW *        
050800*    FOR THE SPECIFIED LINE.                                     *        
050900*----------------------------------------------------------------*        
051000 1330-UPDATE-TDISTHD.                                                     
051100                                                                          
051200     PERFORM                                                              
051300         WITH TEST AFTER                                                  
051400         VARYING PV-RETRY-COUNTER                                         
051500         FROM 1 BY 1                                                      
051600         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
051700             OR (SQLCODE = +0)                                            
051800             OR (SQLCODE = +100))                                         
051900                                                                          
052000         EXEC SQL                                                         
052100             UPDATE TDISTHD                                               
052200                 SET P2L_SCN_CART_CNT = :PV-P2L-SCN-CART-CNT              
052300             WHERE                                                        
052400                 ORD_NBR             = :DK-ORD-NBR      AND               
052500                 RCVR_SEQ_NBR        = :DK-REC-SEQ-NBR  AND               
052600                 ORD_LNE_NBR         = :DK-ORD-LNE-NBR  AND               
052700                 WRK_ORD_SEQ_NBR     = :DK-WRK-ORD-SEQ-NBR AND            
052800                 P2L_SCN_CART_CNT    < :PV-P2L-SCN-CART-CNT               
052900         END-EXEC                                                         
053000                                                                          
053100         EVALUATE TRUE                                                    
053200             WHEN SQLCODE = ZERO                                          
053300             WHEN SQLCODE = +100                                          
053400             WHEN SQLCODE = -904                                          
053500             WHEN SQLCODE = -911                                          
053600             WHEN SQLCODE = -913                                          
053700                 CONTINUE                                                 
053800             WHEN SQLWARN0 NOT = SPACES                                   
053900             WHEN SQLCODE < ZERO                                          
054000             WHEN SQLCODE > ZERO                                          
054100                 PERFORM 1410-SEND-ERROR-CONFIRM-QNX                      
054200                 PERFORM 1500-CLOSE-SOCKET                                
054300                 MOVE '1330-UPDATE-TDISTHD'                               
054400                                   TO DP013-PARAGRAPH                     
054500                 MOVE 'ERROR UPDATING TDISTHD'                            
054600                                   TO DP013-MESSAGE-TEXT (1)              
054700                 MOVE DK-ORD-NBR   TO PV-ERROR-ORD-NBR                    
054800                 MOVE DK-REC-SEQ-NBR                                      
054900                                   TO PV-ERROR-RCVR-SEQ-NBR               
055000                 MOVE DK-ORD-LNE-NBR                                      
055100                                   TO PV-ERROR-ORD-LNE-NBR                
055200                 STRING ' RCVR: '            DELIMITED BY SIZE            
055300                      PV-ERROR-ORD-NBR       DELIMITED BY SIZE            
055400                      '-'                    DELIMITED BY SIZE            
055500                      PV-ERROR-RCVR-SEQ-NBR  DELIMITED BY SIZE            
055600                      '  LINE: '             DELIMITED BY SIZE            
055700                      PV-ERROR-ORD-LNE-NBR   DELIMITED BY SIZE            
055800                                 INTO DP013-MESSAGE-TEXT (2)              
055900                 MOVE SQLCA        TO DP013-SQLCA                         
056000                 MOVE 'TDISTHD '   TO DP013-DB2-TABLE-NAME (1)            
056100                 SET DP013-ROLLBACK                                       
056200                     DP013-DB2-ABEND                                      
056300                                   TO TRUE                                
056400                 PERFORM 9999-WRITE-CICS-LOG-MSG                          
056500         END-EVALUATE                                                     
056600     END-PERFORM.                                                         
056700                                                                          
056800     IF  (PV-RETRY-COUNTER > PC-MAX-RETRIES) AND                          
056900         (SQLCODE NOT = +0)                                               
057000         PERFORM 1410-SEND-ERROR-CONFIRM-QNX                              
057100         PERFORM 1500-CLOSE-SOCKET                                        
057200         MOVE '1330-UPDATE-TDISTHD'                                       
057300                                  TO DP013-PARAGRAPH                      
057400         MOVE 'MAX RETRIES EXCEEDED ON UPDATE OF TDISTHD ROW'             
057500                                  TO DP013-MESSAGE-TEXT (1)               
057600         MOVE DK-ORD-NBR          TO PV-ERROR-ORD-NBR                     
057700         MOVE DK-REC-SEQ-NBR      TO PV-ERROR-RCVR-SEQ-NBR                
057800         MOVE DK-ORD-LNE-NBR      TO PV-ERROR-ORD-LNE-NBR                 
057900         STRING ' RCVR: '            DELIMITED BY SIZE                    
058000              PV-ERROR-ORD-NBR       DELIMITED BY SIZE                    
058100              '-'                    DELIMITED BY SIZE                    
058200              PV-ERROR-RCVR-SEQ-NBR  DELIMITED BY SIZE                    
058300              '  LINE: '             DELIMITED BY SIZE                    
058400              PV-ERROR-ORD-LNE-NBR   DELIMITED BY SIZE                    
058500                                INTO DP013-MESSAGE-TEXT (2)               
058600         MOVE SQLCA               TO DP013-SQLCA                          
058700         MOVE 'TDISTHD'           TO DP013-DB2-TABLE-NAME (1)             
058800         SET DP013-DB2-ABEND                                              
058900             DP013-ROLLBACK       TO TRUE                                 
059000         PERFORM 9999-WRITE-CICS-LOG-MSG                                  
059100     END-IF.                                                              
059200                                                                          
059300     EJECT                                                                
059400*                                                                         
059500*----------------------------------------------------------------*        
059600*    SELECT INFORMATION FROM THE DISTRIBUTION HEADER TABLE.      *        
059700*----------------------------------------------------------------*        
059800*1380-SELECT-TDISTHD.                                                     
059900*                                                                         
060000*    INITIALIZE DCLTDISTHD.                                               
060100*                                                                         
060200*    PERFORM                                                              
060300*        WITH TEST AFTER                                                  
060400*        VARYING PV-RETRY-COUNTER                                         
060500*        FROM 1 BY 1                                                      
060600*        UNTIL (PV-RETRY-COUNTER > PC-MAX-RETRIES)                        
060700*            OR (SQLCODE = +0)                                            
060800*                                                                         
060900*        EXEC SQL                                                         
061000*             SELECT                                                      
061100*                   P2L_SCN_CART_CNT                                      
061200*               INTO                                                      
061300*                   :DISTHD-P2L-SCN-CART-CNT                              
061400*               FROM                                                      
061500*                   TDISTHD                                               
061600*              WHERE                                                      
061700*                   ORD_NBR      = :DK-ORD-NBR      AND                   
061800*                   RCVR_SEQ_NBR = :DK-REC-SEQ-NBR  AND                   
061900*                   ORD_LNE_NBR  = :DK-ORD-LNE-NBR  AND                   
062000*                   WRK_ORD_SEQ_NBR     = :DK-WRK-ORD-SEQ-NBR             
062100*        END-EXEC                                                         
062200*                                                                         
062300*        EVALUATE TRUE                                                    
062400*            WHEN SQLCODE = ZERO                                          
062500*            WHEN SQLCODE = -904                                          
062600*            WHEN SQLCODE = -911                                          
062700*            WHEN SQLCODE = -913                                          
062800*                CONTINUE                                                 
062900*            WHEN SQLWARN0 NOT = SPACES                                   
063000*            WHEN SQLCODE < ZERO                                          
063100*            WHEN SQLCODE > ZERO                                          
063200*                PERFORM 1410-SEND-ERROR-CONFIRM-QNX                      
063300*                PERFORM 1500-CLOSE-SOCKET                                
063400*                MOVE '1380-READ-TDISTHD'                                 
063500*                                  TO DP013-PARAGRAPH                     
063600*                MOVE 'UNABLE TO SELECT TDISTHD DATA '                    
063700*                                  TO DP013-MESSAGE-TEXT (1)              
063800*                MOVE DK-ORD-NBR   TO PV-ERROR-ORD-NBR                    
063900*                MOVE DK-REC-SEQ-NBR                                      
064000*                                  TO PV-ERROR-RCVR-SEQ-NBR               
064100*                MOVE DK-ORD-LNE-NBR                                      
064200*                                  TO PV-ERROR-ORD-LNE-NBR                
064300*                STRING ' RCVR: '            DELIMITED BY SIZE            
064400*                     PV-ERROR-ORD-NBR       DELIMITED BY SIZE            
064500*                     '-'                    DELIMITED BY SIZE            
064600*                     PV-ERROR-RCVR-SEQ-NBR  DELIMITED BY SIZE            
064700*                     '  LINE: '             DELIMITED BY SIZE            
064800*                     PV-ERROR-ORD-LNE-NBR   DELIMITED BY SIZE            
064900*                                INTO DP013-MESSAGE-TEXT (2)              
065000*                MOVE SQLCA        TO DP013-SQLCA                         
065100*                MOVE 'TDISTHD '   TO DP013-DB2-TABLE-NAME (1)            
065200*                SET DP013-ROLLBACK                                       
065300*                    DP013-DB2-ABEND                                      
065400*                                  TO TRUE                                
065500*                PERFORM 9999-WRITE-CICS-LOG-MSG                          
065600*        END-EVALUATE                                                     
065700*    END-PERFORM.                                                         
065800*                                                                         
065900*    IF  (PV-RETRY-COUNTER > PC-MAX-RETRIES) AND                          
066000*        (SQLCODE NOT = +0)                                               
066100*        PERFORM 1410-SEND-ERROR-CONFIRM-QNX                              
066200*        PERFORM 1500-CLOSE-SOCKET                                        
066300*        MOVE '1380-READ-TDISTHD'                                         
066400*                                 TO DP013-PARAGRAPH                      
066500*        MOVE 'MAX RETRIES EXCEEDED ON TDISTHD SELECT'                    
066600*                                 TO DP013-MESSAGE-TEXT (1)               
066700*        MOVE DK-ORD-NBR          TO PV-ERROR-ORD-NBR                     
066800*        MOVE DK-REC-SEQ-NBR      TO PV-ERROR-RCVR-SEQ-NBR                
066900*        MOVE DK-ORD-LNE-NBR      TO PV-ERROR-ORD-LNE-NBR                 
067000*        STRING ' RCVR: '            DELIMITED BY SIZE                    
067100*             PV-ERROR-ORD-NBR       DELIMITED BY SIZE                    
067200*             '-'                    DELIMITED BY SIZE                    
067300*             PV-ERROR-RCVR-SEQ-NBR  DELIMITED BY SIZE                    
067400*             '  LINE: '             DELIMITED BY SIZE                    
067500*             PV-ERROR-ORD-LNE-NBR   DELIMITED BY SIZE                    
067600*                               INTO DP013-MESSAGE-TEXT (2)               
067700*        MOVE SQLCA               TO DP013-SQLCA                          
067800*        MOVE 'TDISTHD'           TO DP013-DB2-TABLE-NAME (1)             
067900*        SET DP013-DB2-ABEND                                              
068000*            DP013-ROLLBACK       TO TRUE                                 
068100*        PERFORM 9999-WRITE-CICS-LOG-MSG                                  
068200*    END-IF.                                                              
068300*                                                                         
068400*    EJECT                                                                
068500/                                                                         
068600*----------------------------------------------------------------*        
068700*    SEND A CONFIRMATION RECORD TO THE PACK TO LIGHT SYSTEM TO   *        
068800*    POSITIVELY ACKNOWLEDGE THE RECEIPT OF A RECORD FROM THEM.   *        
068900*----------------------------------------------------------------*        
069000 1400-SEND-CONFIRMATION-QNX.                                              
069100                                                                          
069200     MOVE SPACES              TO TCP-BUF-AREA.                            
069300                                                                          
069400     MOVE PC008-DTL-CNTL-NBR  TO PV-CONTROL-NUMBER.                       
069500                                                                          
069600     INITIALIZE PC008-STATUS-RECORD.                                      
069700     SET PC008-STATUS-CONFIRM TO TRUE.                                    
069800     MOVE PV-CONTROL-NUMBER   TO PC008-CONFIRM-CNTL-NBR.                  
069900     MOVE ';'                 TO PC008-CFM-TERM-CHAR.                     
070000     MOVE PC008-STATUS-RECORD TO TCP-BUF-AREA.                            
070100     MOVE PC008-DL-CONFIRM    TO DP081-TCPLENG                            
070200                                 DP081-NBR-BYTES.                         
070300*    MOVE 50                  TO DP081-TCPLENG.                           
070400                                                                          
070500*    MOVE ' BUFFER AREA DISPLAY' TO PV-CICS-MSG-AREA.                     
070600*    PERFORM 9990-WRITE-CICS.                                             
070700                                                                          
070800*    STRING ' ' TCP-BUF-AREA                                              
070900*        DELIMITED BY SIZE INTO PV-CICS-MSG-AREA                          
071000*    PERFORM 9990-WRITE-CICS                                              
071100                                                                          
071200* TRANSLATE EBCDIC TO ASCII                                               
071300     CALL 'EZACIC04'                                                      
071400         USING  DP081-TOASCII-TOKEN                                       
071500                TCP-BUF-AREA                                              
071600                DP081-TCPLENG.                                            
071700                                                                          
071800*    MOVE ' BUFFER AREA AFT CONV' TO PV-CICS-MSG-AREA.                    
071900*    PERFORM 9990-WRITE-CICS.                                             
072000                                                                          
072100*    STRING ' ' TCP-BUF-AREA                                              
072200*        DELIMITED BY SIZE INTO PV-CICS-MSG-AREA                          
072300*    PERFORM 9990-WRITE-CICS                                              
072400                                                                          
072500     CALL 'EZASOKET'                                                      
072600         USING                                                            
072700                DP081-WRITE-CMD                                           
072800                DP081-SOCK-ID                                             
072900                DP081-NBR-BYTES                                           
073000                TCP-BUF-AREA                                              
073100                DP081-ERRNO                                               
073200                DP081-RETCODE.                                            
073300                                                                          
073400*                                                                         
073500     IF DP081-RETCODE < ZERO                                              
073600         MOVE PC-CURRENT-PGM-NAME TO DP081-TCP-IP-PGM-NAME                
073700         SET  DP081-TCP-IP-SEND-TO-ERR  TO TRUE                           
073800         MOVE DP081-ERRNO         TO DP081-TCP-IP-ERR-NBR                 
073900         SET  DP013-OTHER-ABEND   TO TRUE                                 
074000         SET  DP013-NO-ROLLBACK   TO TRUE                                 
074100         MOVE '1400-SEND-CONFIRMATION-QNX'                                
074200             TO DP013-PARAGRAPH                                           
074300         MOVE DP081-TCP-IP-LOGGING-MSG  TO DP013-MESSAGE-TEXT (1)         
074400         MOVE DP081-TCP-IP-ERR-NBR      TO DP013-MESSAGE-TEXT (2)         
074500         PERFORM 9999-WRITE-CICS-LOG-MSG                                  
074600     END-IF.                                                              
074700/                                                                         
074800*----------------------------------------------------------------*        
074900*    SEND AN ERROR CONFIRMATION RECORD TO THE PACK TO LIGHT      *        
075000*    SYSTEM, INDICATING THERE WAS AN ERROR WITH THE DATA WE      *        
075100*    RECEIVED FROM THEM.                                         *        
075200*----------------------------------------------------------------*        
075300 1410-SEND-ERROR-CONFIRM-QNX.                                             
075400     MOVE SPACES              TO TCP-BUF-AREA                             
075500     SET PC008-STATUS-CONFIRM-ERROR                                       
075600                              TO TRUE                                     
075700     MOVE PC008-STATUS-RECORD TO TCP-BUF-AREA                             
075800*    MOVE 50                  TO DP081-TCPLENG.                           
075900     MOVE PC008-DL-CONFIRM    TO DP081-TCPLENG.                           
075910     MOVE ';'                 TO PC008-CFM-TERM-CHAR.                     
076000                                                                          
076100* TRANSLATE EBCDIC TO ASCII                                               
076200     CALL 'EZACIC04'                                                      
076300         USING  DP081-TOASCII-TOKEN                                       
076400                TCP-BUF-AREA                                              
076500                DP081-TCPLENG.                                            
076600                                                                          
076700* WRITE DATA ON A CONNECTED SOCKET                                        
076800     CALL 'EZASOKET'                                                      
076900         USING  DP081-WRITE-CMD                                           
077000                DP081-SOCK-ID                                             
077100                DP081-NBR-BYTES                                           
077200                TCP-BUF-AREA                                              
077300                DP081-ERRNO                                               
077400                DP081-RETCODE.                                            
077500                                                                          
077600*                                                                         
077700     IF DP081-RETCODE < ZERO                                              
077800         MOVE PC-CURRENT-PGM-NAME TO DP081-TCP-IP-PGM-NAME                
077900         SET  DP081-TCP-IP-SEND-TO-ERR  TO TRUE                           
078000         MOVE DP081-ERRNO         TO DP081-TCP-IP-ERR-NBR                 
078100         SET  DP013-OTHER-ABEND   TO TRUE                                 
078200         SET  DP013-NO-ROLLBACK   TO TRUE                                 
078300         MOVE '1410-SEND-ERROR-CONFIRM-QNX'                               
078400             TO DP013-PARAGRAPH                                           
078500         MOVE DP081-TCP-IP-LOGGING-MSG  TO DP013-MESSAGE-TEXT (1)         
078600         PERFORM 9999-WRITE-CICS-LOG-MSG                                  
078700     END-IF.                                                              
078800/                                                                         
078900*----------------------------------------------------------------*        
079000*    CLOSE THE SOCKET GIVEN TO US BY THE PACK TO LIGHT SYSTEM.   *        
079100*----------------------------------------------------------------*        
079200 1500-CLOSE-SOCKET.                                                       
079300                                                                          
079400* CLOSE THE CONNECTED SOCKET                                              
079500         CALL 'EZASOKET'                                                  
079600             USING                                                        
079700                DP081-CLOSE-CMD                                           
079800                DP081-SOCK-ID                                             
079900                DP081-ERRNO                                               
080000                DP081-RETCODE.                                            
080100                                                                          
080200     IF DP081-RETCODE < ZERO                                              
080300         MOVE PC-CURRENT-PGM-NAME       TO DP081-TCP-IP-PGM-NAME          
080400         SET  DP081-TCP-IP-CLOSE-ERR    TO TRUE                           
080500         MOVE DP081-ERRNO           TO DP081-TCP-IP-ERR-NBR               
080600         SET  DP013-OTHER-ABEND   TO TRUE                                 
080700         SET  DP013-NO-ROLLBACK   TO TRUE                                 
080800         MOVE '1500-CLOSE-SOCKET'                                         
080900             TO DP013-PARAGRAPH                                           
081000         MOVE DP081-TCP-IP-LOGGING-MSG  TO DP013-MESSAGE-TEXT (1)         
081100         PERFORM 9999-WRITE-CICS-LOG-MSG                                  
081200     END-IF.                                                              
081300/                                                                         
081301 7000-INSERT-TUPLDER.                                                     
081302                                                                          
081303*    STRING ' PCKCS705 - 7000-INSERT-TUPLDER'                             
081304*        DELIMITED BY SIZE                                                
081305*        INTO PV-CICS-MSG-AREA.                                           
081306*    PERFORM 9990-WRITE-CICS.                                             
081307                                                                          
081308     INITIALIZE                       UPLDER-PKT-TXT                      
081309                                      UPLDER-UPLD-TRN-CDE.                
081311     ADD  1                        TO PV-ERROR-COUNT.                     
081312     MOVE PV-ERROR-COUNT           TO UPLDER-UPLD-SEQ-NBR.                
081315     INITIALIZE                       UPLDER-PKT-TXT                      
081316     STRING EIBTRNID                                                      
081317            '-'                                                           
081318           PC008-STATUS-RECORD        DELIMITED BY SIZE                   
081319                                 INTO UPLDER-PKT-TXT.                     
081321                                                                          
081322     INITIALIZE PV-CICS-MSG-AREA.                                         
081323     STRING ' PCKCS705'                                                   
081324            ' '  PV-ERROR-CODE ' '                                        
081325            UPLDER-PKT-TXT                                                
081326         DELIMITED BY SIZE                                                
081327         INTO PV-CICS-MSG-AREA.                                           
081328     PERFORM 9990-WRITE-CICS.                                             
081329                                                                          
081330     EXEC SQL                                                             
081331     INSERT INTO TUPLDER                                                  
081332         (                                                                
081333           OPER_UNT_ID                                                    
081334         , UPLD_TRN_CDE                                                   
081335         , ERR_CDE                                                        
081336         , PRC_TMST                                                       
081337         , UPLD_SEQ_NBR                                                   
081338         , PKT_TXT                                                        
081339         )                                                                
081340         VALUES                                                           
081341         (                                                                
081342           :DK-OPER-UNT-ID                                                
081343         , 'J'                                                            
081344         , :PV-ERROR-CODE                                                 
081345         , current timestamp                                              
081346         , 0                                                              
081347         , :UPLDER-PKT-TXT                                                
081348         )                                                                
081349     END-EXEC.                                                            
081350                                                                          
081351     EVALUATE TRUE                                                        
081352         WHEN SQLCODE = ZERO                                              
081353             CONTINUE                                                     
081354         WHEN SQLCODE = -904                                              
081355         WHEN SQLCODE = -911                                              
081356         WHEN SQLCODE = -913                                              
081357                 MOVE '7000-INSERT-TUPLDER'                               
081358                                   TO DP013-PARAGRAPH                     
081359                 MOVE SQLCA        TO DP013-SQLCA                         
081360                 MOVE 'TUPLDER '   TO DP013-DB2-TABLE-NAME (1)            
081361                 SET DP013-ROLLBACK                                       
081362                     DP013-DB2-ABEND                                      
081363                                   TO TRUE                                
081364                 PERFORM 9999-WRITE-CICS-LOG-MSG                          
081370                                                                          
081371         WHEN SQLWARN0 NOT = SPACES                                       
081372         WHEN SQLCODE  NOT = ZERO                                         
081373                 MOVE '7000-INSERT-TUPLDER'                               
081374                                   TO DP013-PARAGRAPH                     
081375                 MOVE SQLCA        TO DP013-SQLCA                         
081376                 MOVE 'TUPLDER '   TO DP013-DB2-TABLE-NAME (1)            
081377                 SET DP013-ROLLBACK                                       
081378                     DP013-DB2-ABEND                                      
081379                                   TO TRUE                                
081380                 PERFORM 9999-WRITE-CICS-LOG-MSG                          
081390     END-EVALUATE.                                                        
081391                                                                          
081392                                                                          
081393*----------------------------------------------------------------*        
081394* SELECT oper unit id based on the transaction id.               *        
081395* DMH - 10/10/17                                                          
081396*----------------------------------------------------------------*        
081397 7010-DC-NBR-FROM-TRANID.                                                 
081398                                                                          
081399     STRING '  PCKCS705 -  7010-DC-NBR-FROM-TRANID'                       
081400         DELIMITED BY SIZE                                                
081401         INTO PV-CICS-MSG-AREA.                                           
081402     PERFORM 9995-WRITE-CICS.                                             
081403                                                                          
081404                                                                          
081405     move eibtrnid  to  KRPRPM-FUNC-TXT                                   
081406                                                                          
081407     EXEC SQL                                                             
081408          SELECT  LOC_ID                                                  
081409            INTO :DK-OPER-UNT-ID                                          
081410            FROM  TKRPRPM                                                 
081411           WHERE                                                          
081412                  INTFC_SYS_CDE     = 'PCC'                               
081413             AND  SYS_PRC_FUNC_CDE  = 'P2LLCH'                            
081414             AND  FUNC_PRC_STAT_CDE = 'AC'                                
081415             AND  FUNC_txt          = :KRPRPM-FUNC-TXT                    
081416     END-EXEC.                                                            
081417                                                                          
081418     EVALUATE TRUE                                                        
081419         WHEN SQLCODE = ZERO                                              
081420              continue                                                    
081421                                                                          
081422         WHEN SQLCODE = +100                                              
081423              MOVE ZEROES  TO  DK-OPER-UNT-ID                             
081424                                                                          
081425         WHEN SQLWARN0 NOT = SPACES                                       
081426         WHEN SQLCODE < ZERO                                              
081427         WHEN SQLCODE > ZERO                                              
081428                 MOVE '7010-DC-NBR-FROM-TRANID'                           
081429                                   TO DP013-PARAGRAPH                     
081430                 MOVE SQLCA        TO DP013-SQLCA                         
081431                 MOVE 'TKRPRPM '   TO DP013-DB2-TABLE-NAME (1)            
081432                 SET DP013-ROLLBACK                                       
081433                     DP013-DB2-ABEND                                      
081434                                   TO TRUE                                
081435                 PERFORM 9999-WRITE-CICS-LOG-MSG                          
081436                                                                          
081437     END-EVALUATE.                                                        
081438                                                                          
081439                                                                          
081440*----------------------------------------------------------------*        
081441* SELECT SOCKET TIMEOUT FROM PARAMETER DATABASE                  *        
081442* DMH - 03/07/08 - BEGIN                                                  
081443*----------------------------------------------------------------*        
081444 7115-GET-SOCKET-TIMEOUT.                                                 
081445     STRING '  PCKCS705 - 7115-GET-SOCKET-TIMEOUT'                        
081446         DELIMITED BY SIZE                                                
081447         INTO PV-CICS-MSG-AREA.                                           
081448     PERFORM 9995-WRITE-CICS.                                             
081449                                                                          
081450                                                                          
081451     EXEC SQL                                                             
081452          SELECT  FUNC_QTY                                                
081453            INTO :KRPRPM-FUNC-QTY                                         
081454            FROM  TKRPRPM                                                 
081455           WHERE  LOC_ID            = 90                                  
081456             AND  INTFC_SYS_CDE    = 'KHL'                                
081457             AND  SYS_PRC_FUNC_CDE = 'SCKTM9'                             
081458             AND  FUNC_PRC_STAT_CDE = 'AC'                                
081459     END-EXEC.                                                            
081460                                                                          
081461     EVALUATE TRUE                                                        
081462         WHEN SQLCODE = ZERO                                              
081463              MOVE KRPRPM-FUNC-QTY  TO  SELECT-SECS                       
081464                                                                          
081465         WHEN SQLCODE = +100                                              
081466              CONTINUE                                                    
081467                                                                          
081468         WHEN SQLWARN0 NOT = SPACES                                       
081469         WHEN SQLCODE < ZERO                                              
081470         WHEN SQLCODE > ZERO                                              
081471                 MOVE '7115-GET-SOCKET-TIMEOUT'                           
081472                                   TO DP013-PARAGRAPH                     
081473                 MOVE SQLCA        TO DP013-SQLCA                         
081474                 MOVE 'TKRPRPM '   TO DP013-DB2-TABLE-NAME (1)            
081475                 SET DP013-ROLLBACK                                       
081476                     DP013-DB2-ABEND                                      
081477                                   TO TRUE                                
081478                 PERFORM 9999-WRITE-CICS-LOG-MSG                          
081479                                                                          
081480     END-EVALUATE.                                                        
081481                                                                          
081482                                                                          
081490 9990-WRITE-CICS.                                                         
081500     EXEC CICS WRITEQ TD                                                  
081600         QUEUE('CSSL')                                                    
081700         FROM(PV-CICS-MSG-AREA)                                           
081800         NOHANDLE                                                         
081900     END-EXEC.                                                            
082000                                                                          
082100     MOVE SPACES TO PV-CICS-MSG-AREA.                                     
082200*                                                                         
082210*                                                                         
082220 9995-WRITE-CICS.                                                         
082230                                                                          
082240* IF YOU'RE AT THE LAST OCCURRENCE OF THE TABLE,                          
082250* THE NEXT MESSAGE WILL GO INTO THE FIRST OCCURRENCE.  KEEP               
082260* INCREMENTING THE SEQUENCE SO THAT IF THE TABLE IS DUMPED TO THE         
082270* LOG YOU WILL BE ABLE TO SEE THE LAST 200 LINES AND HOW THEY             
082280* WRAPPED AROUND.                                                         
082290                                                                          
082291     EVALUATE TRUE                                                        
082292     WHEN PT-MSG-IDX < PC-MSG-TABLE-SIZE                                  
082293         MOVE PV-CICS-MSG-AREA        TO PT-MESSAGE(PT-MSG-IDX)           
082294         MOVE PT-MSG-SEQ (PT-MSG-IDX) TO PV-PREV-SEQUENCE                 
082295         SET PT-MSG-IDX UP BY 1                                           
082296     WHEN PT-MSG-IDX = PC-MSG-TABLE-SIZE                                  
082297         MOVE PV-CICS-MSG-AREA        TO PT-MESSAGE(PT-MSG-IDX)           
082298         MOVE PT-MSG-SEQ (PT-MSG-IDX) TO PV-PREV-SEQUENCE                 
082299         SET PT-MSG-IDX               TO 1                                
082300     END-EVALUATE.                                                        
082301                                                                          
082302     COMPUTE PV-NEXT-SEQUENCE = PV-PREV-SEQUENCE + 1.                     
082303     MOVE PV-NEXT-SEQUENCE            TO PT-MSG-SEQ (PT-MSG-IDX).         
082304                                                                          
082305                                                                          
082310************************************************************              
082400**                                                                        
082500************************************************************              
082600 9999-WRITE-CICS-LOG-MSG.                                                 
082700     MOVE PC-CURRENT-PGM-NAME  TO  DP013-PROGRAM.                         
082800     MOVE DFHEIBLK             TO  DP013-DFHEIBLK.                        
082900                                                                          
083000     INITIALIZE DP013-SRC-TASK-NUMBER                                     
083100                DP013-STACK.                                              
083200     SET DP013-LINK-RETURN     TO TRUE.                                   
083300                                                                          
083400     EXEC CICS                                                            
083500         LINK                                                             
083600             PROGRAM   (DP013-NAME)                                       
083700             COMMAREA  (DP013-PARAMETERS)                                 
083800             LENGTH    (DP013-LENGTH)                                     
083900     END-EXEC.                                                            
084000                                                                          
084100     PERFORM VARYING PT-MSG-IDX FROM 1 BY 1 UNTIL                         
084200                     PT-MESSAGE(PT-MSG-IDX) = SPACES                      
084300                     OR PT-MSG-IDX > 200                                  
084400         MOVE PT-MESSAGES(PT-MSG-IDX) TO PV-CICS-MSG-AREA                 
084500         PERFORM 9990-WRITE-CICS                                          
084600     END-PERFORM.                                                         
084700                                                                          
084800     EXEC CICS                                                            
084900         RETURN                                                           
085000     END-EXEC.                                                            
085100*                                                                         
