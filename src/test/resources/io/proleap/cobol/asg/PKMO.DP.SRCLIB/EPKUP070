      ******************************************************************00010018
       IDENTIFICATION DIVISION.                                         00020018
      ******************************************************************00030018
      *                                                                 00040018
      *    CALL BOX LOAD PROGRAM                                        00050018
      *                                                                 00060018
      ******************************************************************00070018
       PROGRAM-ID.             EPKUP070.                                00080018
       AUTHOR.                 BRYAN BUKOWSKI.                          00090018
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00100018
       DATE-WRITTEN.           10-08-2009.                              00110018
       DATE-COMPILED.                                                   00120018
      ******************************************************************00130018
      * THIS PROGRAM WILL READ MESSAGES OFF OF THE CALL BOX QUEUE AND   00140018
      * LOAD THEM INTO THE CALL BOX DB2 TABLE.  THIS IS A BATCH PROGRAM 00150018
      * THAT WILL BE RUN HOURLY.                                        00160018
      ******************************************************************00170018
IN7319* AUTH:  INC000001857319                                          00180028
IN7319* DATE:  09-23-11                                                 00190028
IN7319* PRGMR: COGNIZANT                                                00200028
IN7319* WHY:   INCLUDED BAD FILE TO WRITE THE BAD DATAS AND SKIP TO READ00210028
IN7319*        NEXT RECORD FROM QUEUE. CODE CHANGES ARE TAGGED WITH     00220028
IN7319*        IN7319.                                                  00230028
      ******************************************************************00241033
IN7466* AUTH:  INC0687466                                               00242033
IN7466* DATE:  05-05-14                                                 00243033
IN7466* PRGMR: COGNIZANT                                                00244033
IN7466* WHY:   INCLUDED TRUNCATE ERROR CHECK FOR WRITTING THE BAD RECORD00245033
IN7466*        INTO ERROR FILE. THE CODE CHANGES ARE TAGGED WITH        00246033
IN7466*        INC7466                                                  00247033
      ******************************************************************00248033
       ENVIRONMENT DIVISION.                                            00250018
      ******************************************************************00260018
       CONFIGURATION SECTION.                                           00270018
       SOURCE-COMPUTER.        IBM-3090.                                00280018
       OBJECT-COMPUTER.        IBM-3090.                                00290018
       INPUT-OUTPUT SECTION.                                            00300018
       FILE-CONTROL.                                                    00310018
           SELECT MQ-ENVIRONMENT-CTLCD                                  00320018
                  ASSIGN TO INP01.                                      00330018
           SELECT MQ-NAME-CTLCD                                         00340018
                  ASSIGN TO INP02.                                      00350018
           SELECT BACKUP-OUTPUT-FILE                                    00360018
                  ASSIGN TO OUT01.                                      00370018
IN7319     SELECT BAD-FILE                                              00380027
IN7319            ASSIGN TO OUT02.                                      00390027
                                                                        00400027
      ******************************************************************00410018
       DATA DIVISION.                                                   00420018
      ******************************************************************00430018
       FILE SECTION.                                                    00440018
                                                                        00450018
       FD  MQ-ENVIRONMENT-CTLCD                                         00460018
           RECORDING MODE F                                             00470018
           BLOCK CONTAINS 0 RECORDS                                     00480018
           LABEL RECORDS ARE OMITTED.                                   00490018
       01  MQ-ENVIRONMENT-CTLCD-LINE        PIC  X(80).                 00500018
                                                                        00510018
       FD  MQ-NAME-CTLCD                                                00520018
           RECORDING MODE F                                             00530018
           BLOCK CONTAINS 0 RECORDS                                     00540018
           LABEL RECORDS ARE OMITTED.                                   00550018
       01  MQ-NAME-CTLCD-LINE               PIC  X(80).                 00560018
                                                                        00570018
       FD  BACKUP-OUTPUT-FILE                                           00580018
           RECORDING MODE IS F                                          00590018
           BLOCK CONTAINS 0  RECORDS                                    00600018
           LABEL RECORDS ARE STANDARD                                   00610018
           RECORD CONTAINS 100 CHARACTERS                               00620018
           DATA RECORD IS BACKUP-OUTPUT-RECORD.                         00630018
                                                                        00640018
       01  BACKUP-OUTPUT-RECORD             PIC  X(100).                00650018
                                                                        00660018
IN7319 FD  BAD-FILE                                                     00670027
IN7319     RECORDING MODE IS F                                          00680027
IN7319     BLOCK CONTAINS 0  RECORDS                                    00690027
IN7319     LABEL RECORDS ARE STANDARD                                   00700027
IN7319     RECORD CONTAINS 200 CHARACTERS                               00710035
IN7319     DATA RECORD IS BAD-REC.                                      00720027
IN7319                                                                  00730027
IN7466 01  BAD-REC                          PIC  X(200).                00740033
                                                                        00750027
       WORKING-STORAGE SECTION.                                         00760018
       01  FILLER                           PIC X(41)                   00770018
           VALUE 'WORKING STORAGE STARTS HERE FOR EPKUP070'.            00780018
       01  MQM-OBJECT-DESCRIPTOR.                                       00790018
           COPY CMQODV.                                                 00800018
       01  MQM-MESSAGE-DESCRIPTOR.                                      00810018
           COPY CMQMDV.                                                 00820018
       01  MQM-GET-MESSAGE-OPTIONS.                                     00830018
           COPY CMQGMOV.                                                00840018
                                                                        00850018
      *    MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)   00860018
      *    AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)          00870018
                                                                        00880018
       01  MQM-CONSTANTS.                                               00890018
           COPY CMQV.                                                   00900018
                                                                        00910018
       01  WS-COUNTER.                                                  00920018
           05  WS-HOLD-TMST             PIC X(26)     VALUE SPACES.     00930018
           05  WS-TMST                  PIC X(26)     VALUE SPACES.     00940018
                                                                        00950018
       01  PS-PGM-SWITCHES.                                             00960018
           05  PS-MQ-ENVIRONMENT-CTLCD-SW PIC X(01) VALUE 'N'.          00970018
               88  PS-MQ-ENVIRONMENT-CTLCD-END      VALUE 'Y'.          00980018
               88  PS-MQ-ENVIRONMENT-CTLCD-START    VALUE 'N'.          00990018
           05  PS-MQ-NAME-CTLCD-SW       PIC  X(01) VALUE 'N'.          01000018
               88  PS-MQ-NAME-CTLCD-END             VALUE 'Y'.          01010018
               88  PS-MQ-NAME-CTLCD-START           VALUE 'N'.          01020018
           05  PS-MORE-MESSAGES          PIC  X(01) VALUE 'Y'.          01030018
               88  PS-MORE-MSG                      VALUE 'Y'.          01040018
               88  PS-NO-MORE-MSG                   VALUE 'N'.          01050018
IN7319     05  PS-BAD-MESSAGES           PIC  X(01) VALUE 'Y'.          01060029
IN7319         88  PS-BAD-MSG                       VALUE 'Y'.          01070029
IN7319         88  PS-NO-BAD-MSG                    VALUE 'N'.          01080029
           05  PS-DEFAULT-CKPT-FEQ-SW    PIC  X(01) VALUE 'Y'.          01090022
               88  PS-DEFAULT-CKPT-FEQ-Y            VALUE 'Y'.          01100022
               88  PS-DEFAULT-CKPT-FEQ-N            VALUE 'N'.          01110022
                                                                        01120018
       01  ABEND-CODE                    PIC S9(4) COMP SYNC VALUE ZERO.01130018
           88  AC-CONTROL-CARD-ERROR                VALUE +4003.        01140018
           88  AC-DB2-ERROR                         VALUE +4013.        01150018
           88  AC-DATE-ERROR                        VALUE +4019.        01160018
           88  AC-MQ-ERROR                          VALUE +4020.        01170018
                                                                        01180018
       01  MQ-WORKING-STORAGE.                                          01190018
           05  WS-MQCONN                 PIC  X(8)  VALUE 'CSQBCONN'.   01200018
           05  WS-MQOPEN                 PIC  X(8)  VALUE 'CSQBOPEN'.   01210018
           05  WS-MQGET                  PIC  X(8)  VALUE 'CSQBGET'.    01220018
           05  WS-MQBACK                 PIC  X(8)  VALUE 'CSQBBACK'.   01230018
           05  WS-MQCMIT                 PIC  X(8)  VALUE 'CSQBCOMM'.   01240018
           05  WS-MQCLOSE                PIC  X(8)  VALUE 'CSQBCLOS'.   01250018
           05  WS-MQDISC                 PIC  X(8)  VALUE 'CSQBDISC'.   01260018
           05  WS-MQCHECK                PIC  X(8)  VALUE 'MQR2C'.      01270018
                                                                        01280018
       01  CC-MQ-ENVIRONMENT-CTLCD.                                     01290018
           05  CC-PC-QMGR-NAME           PIC  X(08).                    01300018
           05  FILLER                    PIC  X(72).                    01310018
                                                                        01320018
       01  CC-MQ-NAME-CTLCD.                                            01330018
           05  CC-PC-QUEUE-NAME          PIC  X(24).                    01340018
           05  FILLER                    PIC  X(56).                    01350018
                                                                        01360018
       01 PC-CONSTANTS.                                                 01370018
           05  PC-PGM-NAME               PIC  X(08) VALUE 'EPKUP070'.   01380018
           05  PC-FIFTY-STARS            PIC  X(50) VALUE ALL '*'.      01390018
           05  PC-MAX-MESSAGE            PIC  9(03) VALUE 100.          01400018
           05  PC-MAX-FIELDS             PIC  9(01) VALUE 6.            01410018
           05  PC-DATE-DASH              PIC  X(01) VALUE '-'.          01420018
           05  PC-TIME-DOT               PIC  X(01) VALUE '.'.          01430018
           05  PC-DATE-FINAL             PIC  X(06) VALUE '000000'.     01440018
           05  PC-ROW-NOT-FOUND          PIC S9(04) VALUE +100          01450022
                                                  COMP SYNC.            01460022
           05  PC-DEFAULT-CKPT-FREQ      PIC S9(05)V VALUE +1000        01470024
                                                  USAGE COMP-3.         01480023
                                                                        01490018
       01 PV-PROGRAM-MQ-VARIABLES.                                      01500018
           05  PV-CKPT-FRQNCY-QTY        PIC S99999V USAGE COMP-3.      01510023
           05  PV-CLOSE-HANDLE           PIC S9(09) BINARY VALUE 0.     01520022
           05  PV-COMPLETION-CODE        PIC S9(09) BINARY.             01530018
           05  PV-CON-REASON             PIC S9(09) BINARY.             01540018
           05  PV-DATA-LENGTH            PIC S9(09) BINARY.             01550018
           05  PV-DB2-OPERATION          PIC  X(30) VALUE SPACE.        01560018
           05  PV-DB2-DUP-ROW-WRITTEN    PIC  9(07) VALUE ZEROS.        01570018
           05  PV-DB2-ROWS-WRITTEN       PIC  9(07) VALUE ZEROS.        01580018
           05  PV-ERROR-MESSAGE          PIC  X(48) VALUE SPACES.       01590018
           05  PV-HANDLE                 PIC S9(09) BINARY VALUE 0.     01600018
           05  PV-HCONN                  PIC S9(09) BINARY VALUE 0.     01610018
           05  PV-INT-OPEN-INPUT-HNDL    PIC S9(09) BINARY VALUE 0.     01620018
           05  PV-MSGBUFFER              PIC X(100) VALUE SPACES.       01630018
           05  PV-MSGLENGTH              PIC S9(09) BINARY VALUE +100.  01640018
           05  PV-MQ-REASON-TEXT         PIC  X(30) VALUE SPACES.       01650018
           05  PV-MQ-ROWS-READ           PIC  9(07) VALUE ZEROS.        01660018
IN7466     05  PV-TRUNC-CNT-FAIL         PIC  9(07) VALUE ZEROS.        01661037
IN7466     05  PV-TRUNC-CNT-ACCP         PIC  9(07) VALUE ZEROS.        01662037
IN7319     05  PA-BAD-WRITTEN            PIC  9(07) VALUE ZEROS.        01670027
           05  PV-OPEN-OPTIONS           PIC S9(09) BINARY.             01680018
           05  PV-PARAGRAPH-NAME         PIC  X(48) VALUE SPACES.       01690018
           05  PV-QM-NAME                PIC  X(48).                    01700018
           05  PV-REASON                 PIC S9(09) BINARY.             01710018
           05  PV-SAVE-COMP-CODE         PIC S9(09) BINARY.             01720018
           05  PV-SAVE-REASON            PIC S9(09) BINARY.             01730018
       01 PV-PROGRAM-VARIABLES.                                         01740018
           05  PV-DATE-TMSTMP            PIC  X(26) VALUE SPACES.       01750018
           05  FILLER REDEFINES PV-DATE-TMSTMP.                         01760018
               10  PV-DATE-CCYY-MM-DD    PIC  X(10).                    01770018
               10  FILLER                PIC  X(16).                    01780018
           05  FILLER REDEFINES PV-DATE-TMSTMP.                         01790018
               10  PV-DATE-CCYY          PIC  X(04).                    01800018
               10  PV-DATE-DASH1         PIC  X(01).                    01810018
               10  PV-DATE-MM            PIC  X(02).                    01820018
               10  PV-DATE-DASH2         PIC  X(01).                    01830018
               10  PV-DATE-DD            PIC  X(02).                    01840018
               10  PV-DATE-DASH3         PIC  X(01).                    01850018
               10  PV-DATE-TIME-HH       PIC  X(02).                    01860018
               10  PV-DATE-TIME-DOT1     PIC  X(01).                    01870018
               10  PV-DATE-TIME-MM       PIC  X(02).                    01880018
               10  PV-DATE-TIME-DOT2     PIC  X(01).                    01890018
               10  PV-DATE-TIME-SS       PIC  X(02).                    01900018
               10  PV-DATE-TIME-DOT3     PIC  X(01).                    01910018
               10  PV-DATE-FINAL         PIC  X(06).                    01920018
           05  PV-TIME                   PIC  X(08) VALUE SPACES.       01930018
           05  FILLER REDEFINES PV-TIME.                                01940018
               10  PV-TIME-HH            PIC  X(02).                    01950018
               10  PV-TIME-DOT1          PIC  X(01).                    01960018
               10  PV-TIME-MM            PIC  X(02).                    01970018
               10  PV-TIME-DOT2          PIC  X(01).                    01980018
               10  PV-TIME-SS            PIC  X(02).                    01990018
           05  PV-LAST-DB2-ACTION        PIC  X(50) VALUE SPACES.       02000018
               88  PV-EPT00021-LOAD-ERROR           VALUE               02010018
                   'INSERTING INTO CALL BOX TABLE, PARA 5000     '.     02020018
                                                                        02030018
       01  PV-CALL-BOX-MESSAGE.                                         02040018
           05  PV-HOLD-MESSAGE           PIC X(100) VALUE SPACES.       02050018
           05  PV-CALL-BOX-FIELD       OCCURS 20 TIMES                  02060018
                                         PIC  X(30) VALUE SPACES.       02070018
           05  PV-CALL-BOX-INDEX         PIC  9(3)  VALUE ZERO.         02080018
           05  PV-WS-CTR                 PIC  9(04) VALUE ZEROS.        02090018
           05  PV-CHK-PT-MESSAGE-CNTR    PIC  9(04) VALUE ZERO.         02100018
                                                                        02110018
       01  IF-INPUT-FIELDS.                                             02120018
           05  IF-01-LOC-NM              PIC  X(04) VALUE SPACES.       02130018
           05  IF-02-REG-NM              PIC  X(04) VALUE SPACES.       02140018
           05  IF-03-MSG-ID              PIC  X(04) VALUE SPACES.       02150018
           05  FILLER REDEFINES IF-03-MSG-ID.                           02160018
               10 FILLER                 PIC  X(03).                    02170018
               10 IF-03-MSG-BTN-ID       PIC  X(01).                    02180018
           05  IF-04-DATE                PIC  X(08) VALUE SPACES.       02190018
           05  FILLER REDEFINES IF-04-DATE.                             02200018
               10  IF-04-DATE-MM         PIC  X(02).                    02210018
               10  IF-04-DATE-DD         PIC  X(02).                    02220018
               10  IF-04-DATE-CCYY       PIC  X(04).                    02230018
           05  IF-05-TIME                PIC  X(06) VALUE SPACES.       02240018
           05  FILLER REDEFINES IF-05-TIME.                             02250018
               10  IF-05-TIME-HH         PIC  X(02).                    02260018
               10  IF-05-TIME-MM         PIC  X(02).                    02270018
               10  IF-05-TIME-SS         PIC  X(02).                    02280018
      *                                                                 02290018
      ******************************************************************02300018
      * DATE ROUTINE COPY MEMBERS                                       02310018
      ******************************************************************02320018
           COPY DPWS500.                                                02330018
                                                                        02340018
      ***************************************************************** 02350018
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            02360018
      ***************************************************************** 02370018
            COPY DPWS004.                                               02380018
                                                                        02390018
      ******************************************************************02400018
      *      TCKRSTCNTL TABLE LAYOUT                                    02410018
      ******************************************************************02420018
               EXEC SQL                                                 02430018
                   INCLUDE TCKRSTCN                                     02440018
               END-EXEC.                                                02450018
                                                                        02460018
      ***************************************************************** 02470018
      *  DB2 COMMUNICATION AREA.                                        02480018
      ***************************************************************** 02490018
              EXEC SQL                                                  02500018
                   INCLUDE SQLCA                                        02510018
              END-EXEC.                                                 02520018
                                                                        02530018
      ***************************************************************** 02540018
      *  DB2 TABLE DB2PDBA.EPT_CALL_BOX - CALL BOX TABLE.               02550018
      ***************************************************************** 02560018
              EXEC SQL                                                  02570018
                   INCLUDE EPT00062                                     02580018
              END-EXEC.                                                 02590018
                                                                        02600018
      ******************************************************************02610018
       PROCEDURE DIVISION.                                              02620018
      ******************************************************************02630018
       0000-MAINLINE.                                                   02640018
                                                                        02650018
           PERFORM 1000-INITIALIZE-PROGRAM.                             02660018
           PERFORM 1500-MQSETUP.                                        02670018
           PERFORM 3000-PROCESS-MSGS                                    02680018
              UNTIL PS-NO-MORE-MSG                                      02690018
           PERFORM 9000-CLOSE-ROUTINE.                                  02700018
           PERFORM 9400-DISCONNECT-QMGR.                                02710018
                                                                        02720018
           GOBACK.                                                      02730018
                                                                        02740018
      *0000-MAINLINE-EXIT.                                              02750018
      ***************************************************************** 02760018
      * 1000-INITIALIZE-PROGRAM                                         02770018
      ***************************************************************** 02780018
       1000-INITIALIZE-PROGRAM.                                         02790018
                                                                        02800018
           OPEN OUTPUT BACKUP-OUTPUT-FILE                               02810027
IN7319                 BAD-FILE.                                        02820027
           PERFORM 1100-GET-CHECKPOINT-FREQ.                            02830020
                                                                        02840022
           IF PS-DEFAULT-CKPT-FEQ-Y                                     02850022
              MOVE PC-DEFAULT-CKPT-FREQ TO PV-CKPT-FRQNCY-QTY.          02860023
                                                                        02870020
      ***************************************************************** 02880020
      * 1100-GET-CHECKPOINT-FREQ                                        02890020
      ***************************************************************** 02900020
       1100-GET-CHECKPOINT-FREQ.                                        02910020
                                                                        02920020
           EXEC SQL                                                     02930020
            SELECT CKPT_FRQNCY_QTY                                      02940021
             INTO :PV-CKPT-FRQNCY-QTY                                   02950023
             FROM TCKRSTCNTL                                            02960020
             WHERE PLAN_NAME = :PC-PGM-NAME                             02970022
           END-EXEC                                                     02980020
                                                                        02990024
            EVALUATE TRUE                                               03000023
                WHEN SQLCODE = ZERO                                     03010023
                WHEN SQLCODE = -904                                     03020023
                WHEN SQLCODE = -913                                     03030023
                     SET PS-DEFAULT-CKPT-FEQ-N TO TRUE                  03040023
                WHEN SQLCODE = PC-ROW-NOT-FOUND                         03050023
                     SET PS-DEFAULT-CKPT-FEQ-Y TO TRUE                  03060023
                WHEN SQLWARN0 NOT = SPACES                              03070023
                WHEN SQLCODE  NOT = ZERO                                03080023
                    SET AC-DATE-ERROR TO TRUE                           03090023
                    MOVE '1100-GET-CHECKPOINT-FREQ' TO PV-PARAGRAPH-NAME03100023
                    PERFORM 9999-ABEND                                  03110023
            END-EVALUATE.                                               03120023
                                                                        03130020
                                                                        03140020
       1500-MQSETUP.                                                    03150018
      * ------------------------------------------------------------- * 03160018
      * READ CONTROL CARD TO GET QUEUE MANAGER NAME FOR CALL BOX        03170018
      * ------------------------------------------------------------- * 03180018
                                                                        03190018
            SET PS-MQ-ENVIRONMENT-CTLCD-START TO TRUE.                  03200018
            OPEN INPUT MQ-ENVIRONMENT-CTLCD.                            03210018
            READ MQ-ENVIRONMENT-CTLCD INTO CC-MQ-ENVIRONMENT-CTLCD      03220018
                AT END                                                  03230018
                    SET PS-MQ-ENVIRONMENT-CTLCD-END                     03240018
                                        TO TRUE.                        03250018
                                                                        03260018
            IF PS-MQ-ENVIRONMENT-CTLCD-END                              03270018
                DISPLAY PC-PGM-NAME                                     03280018
                    'CONTROL CARD 1 MISSING - QMGR NAME FOR CALL BOX'   03290018
                SET AC-CONTROL-CARD-ERROR TO TRUE                       03300018
                CALL 'ILBOABN0' USING ABEND-CODE                        03310018
            ELSE                                                        03320018
                DISPLAY PC-PGM-NAME                                     03330018
                        '- '                                            03340018
                        PC-FIFTY-STARS                                  03350018
                DISPLAY PC-PGM-NAME                                     03360018
                        '- CONTROL CARD: '                              03370018
                        CC-MQ-ENVIRONMENT-CTLCD                         03380018
            END-IF.                                                     03390018
                                                                        03400018
            CLOSE MQ-ENVIRONMENT-CTLCD.                                 03410018
                                                                        03420018
      * ------------------------------------------------------------- * 03430018
      *  READ CONTROL CARD TO GET QUEUE NAME FOR CALL BOX             * 03440018
      * ------------------------------------------------------------- * 03450018
                                                                        03460018
            SET PS-MQ-NAME-CTLCD-START TO TRUE.                         03470018
            OPEN INPUT MQ-NAME-CTLCD.                                   03480018
            READ MQ-NAME-CTLCD INTO CC-MQ-NAME-CTLCD                    03490018
                AT END                                                  03500018
                    SET PS-MQ-NAME-CTLCD-END                            03510018
                                        TO TRUE.                        03520018
                                                                        03530018
            IF PS-MQ-NAME-CTLCD-END                                     03540018
                DISPLAY PC-PGM-NAME                                     03550018
                   'CONTROL CARD 2 MISSING - QUEUE NAME FOR CALL BOX'   03560018
                SET AC-CONTROL-CARD-ERROR TO TRUE                       03570018
                CALL 'ILBOABN0' USING ABEND-CODE                        03580018
            ELSE                                                        03590018
                DISPLAY PC-PGM-NAME                                     03600018
                        '- '                                            03610018
                        PC-FIFTY-STARS                                  03620018
                DISPLAY PC-PGM-NAME                                     03630018
                        '- CONTROL CARD: '                              03640018
                        CC-MQ-NAME-CTLCD                                03650018
            END-IF.                                                     03660018
                                                                        03670018
            CLOSE MQ-NAME-CTLCD.                                        03680018
                                                                        03690018
      * ------------------------------------------------------------- * 03700018
      *    CONNECT TO THE QUEUE MANAGER                                 03710018
      * ------------------------------------------------------------- * 03720018
      * SUPPLY QM-NAME, GET BACK HCONN, COMPLETION CODE AND REASON      03730018
                                                                        03740018
           MOVE CC-PC-QMGR-NAME TO PV-QM-NAME.                          03750018
           CALL WS-MQCONN USING PV-QM-NAME                              03760018
                                PV-HCONN                                03770018
                                PV-COMPLETION-CODE                      03780018
                                PV-REASON.                              03790018
                                                                        03800018
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE              03810018
      *    AND EXIT                                                     03820018
                                                                        03830018
           MOVE PV-REASON TO PV-CON-REASON.                             03840018
           IF (PV-COMPLETION-CODE     NOT = MQCC-OK) THEN               03850018
              MOVE 'MQCONN ERROR'        TO PV-ERROR-MESSAGE            03860018
              MOVE '1500-MQ-SETUP'       TO PV-PARAGRAPH-NAME           03870018
              PERFORM 9200-DISPLAY-MQ-ERROR                             03880018
           END-IF.                                                      03890018
           DISPLAY 'MQCONN SUCCESSFUL    TO ', PV-QM-NAME.              03900018
                                                                        03910018
      * ------------------------------------------------------------- * 03920018
      *    OPEN THE CALL BOX QUEUE FOR INPUT                            03930018
      * ------------------------------------------------------------- * 03940018
                                                                        03950018
           COMPUTE PV-OPEN-OPTIONS = MQOO-INPUT-SHARED +                03960018
                                     MQOO-FAIL-IF-QUIESCING +           03970018
                                     MQOO-SAVE-ALL-CONTEXT.             03980018
           MOVE CC-PC-QUEUE-NAME         TO MQOD-OBJECTNAME.            03990018
           MOVE PV-QM-NAME               TO MQOD-OBJECTQMGRNAME.        04000018
                                                                        04010018
           CALL WS-MQOPEN USING PV-HCONN                                04020018
                                MQM-OBJECT-DESCRIPTOR                   04030018
                                PV-OPEN-OPTIONS                         04040018
                                PV-HANDLE                               04050018
                                PV-COMPLETION-CODE                      04060018
                                PV-REASON.                              04070018
                                                                        04080018
      *    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    04090018
      *    AND EXIT.                                                    04100018
      *                                                                 04110018
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   04120018
              MOVE 'MQOPEN ERROR'        TO PV-ERROR-MESSAGE            04130018
              MOVE 'ERROR ON OPEN OF CALL BOX Q' TO PV-PARAGRAPH-NAME   04140018
              MOVE PV-REASON             TO PV-SAVE-REASON              04150018
              MOVE PV-COMPLETION-CODE    TO PV-SAVE-COMP-CODE           04160018
              PERFORM 9400-DISCONNECT-QMGR                              04170018
              PERFORM 9200-DISPLAY-MQ-ERROR                             04180018
           END-IF.                                                      04190018
           MOVE PV-HANDLE                TO PV-INT-OPEN-INPUT-HNDL.     04200018
           DISPLAY 'MQOPEN OF CALL BOX-INPUT DC QUEUE SUCCESSFUL'.      04210018
                                                                        04220018
      *1500-EXIT.                                                       04230018
      ***************************************************************** 04240018
      * 3000-PROCESS-MSGS                                               04250018
      ***************************************************************** 04260018
       3000-PROCESS-MSGS.                                               04270018
                                                                        04280018
           INITIALIZE  IF-INPUT-FIELDS.                                 04290018
                                                                        04300031
IN7319     SET PS-BAD-MSG      TO TRUE.                                 04310031
                                                                        04320031
           PERFORM 9500-GET-MQ-MESSAGE                                  04330029
IN7319             UNTIL PS-NO-BAD-MSG OR PS-NO-MORE-MSG.               04340030
                                                                        04350029
           IF PS-MORE-MSG                                               04360018
              WRITE BACKUP-OUTPUT-RECORD FROM PV-MSGBUFFER              04370018
              PERFORM 4000-UNSTRING-DETAIL                              04380018
              PERFORM 5000-MOVE-INPUT-FIELDS                            04390018
              PERFORM 6000-CALL-BOX-LOAD                                04400018
              ADD +1                     TO PV-CHK-PT-MESSAGE-CNTR      04410018
              ADD +1                     TO PV-MQ-ROWS-READ             04420018
              IF PV-CHK-PT-MESSAGE-CNTR > PV-CKPT-FRQNCY-QTY            04430023
                 PERFORM 8900-COMMIT-DB2                                04440018
                 PERFORM 9800-COMMIT-MQ                                 04450018
                 MOVE  0                 TO PV-CHK-PT-MESSAGE-CNTR      04460018
              END-IF                                                    04470018
           END-IF.                                                      04480018
                                                                        04490018
                                                                        04500018
      ******************************************************************04510018
       4000-UNSTRING-DETAIL.                                            04520018
      ******************************************************************04530018
           MOVE 1                         TO PV-CALL-BOX-INDEX.         04540018
           MOVE 1                         TO PV-WS-CTR.                 04550018
           PERFORM UNTIL PV-WS-CTR         > PC-MAX-MESSAGE             04560018
                   OR PV-CALL-BOX-INDEX    > PC-MAX-FIELDS              04570018
                UNSTRING PV-HOLD-MESSAGE                                04580018
                   DELIMITED BY ','                                     04590018
                   INTO PV-CALL-BOX-FIELD (PV-CALL-BOX-INDEX)           04600018
                   WITH POINTER PV-WS-CTR                               04610018
                END-UNSTRING                                            04620018
                ADD 1                     TO PV-CALL-BOX-INDEX          04630018
           END-PERFORM.                                                 04640018
                                                                        04650018
      ******************************************************************04660018
       5000-MOVE-INPUT-FIELDS.                                          04670018
      ******************************************************************04680018
                                                                        04690018
           MOVE PV-CALL-BOX-FIELD (01)    TO IF-01-LOC-NM.              04700018
           MOVE PV-CALL-BOX-FIELD (02)    TO IF-02-REG-NM.              04710018
           MOVE PV-CALL-BOX-FIELD (03)    TO IF-03-MSG-ID.              04720018
           MOVE PV-CALL-BOX-FIELD (04)    TO IF-04-DATE.                04730018
           MOVE PC-DATE-FINAL             TO PV-DATE-FINAL.             04740026
           PERFORM 5500-DATE-ROUTINE.                                   04750018
                                                                        04760018
           MOVE PV-CALL-BOX-FIELD (05)    TO IF-05-TIME.                04770018
           MOVE IF-05-TIME-HH             TO PV-TIME-HH                 04780018
                                             PV-DATE-TIME-HH.           04790018
           MOVE IF-05-TIME-MM             TO PV-TIME-MM                 04800018
                                             PV-DATE-TIME-MM.           04810018
           MOVE IF-05-TIME-SS             TO PV-TIME-SS                 04820018
                                             PV-DATE-TIME-SS.           04830018
           MOVE PC-TIME-DOT               TO PV-TIME-DOT1               04840018
                                             PV-TIME-DOT2               04850018
                                             PV-DATE-TIME-DOT1          04860018
                                             PV-DATE-TIME-DOT2          04870018
                                             PV-DATE-TIME-DOT3.         04880018
                                                                        04890018
      ******************************************************************04900018
      * 5500-DATE-ROUTINE.                                              04910025
      * VALIDATES THE DATE INPUT ON THE CONTROL FILE                    04920025
      ******************************************************************04930018
       5500-DATE-ROUTINE.                                               04940018
                                                                        04950018
           INITIALIZE DPG51 DPG52 DPG53                                 04960018
                      DPG54 DPG55 DPG56.                                04970018
                                                                        04980018
           SET DPG51-ACTUAL-CALENDAR-ONLY  TO TRUE.                     04990018
                                                                        05000018
           MOVE IF-04-DATE                TO DPG52-LK-DATE-INPUT.       05010026
           SET DPG52-LK-DTE-GREG-CC       TO TRUE.                      05020026
                                                                        05030018
           CALL DP500-KOHLS-CALENDAR-ROUTINE                            05040018
             USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                 05050018
                                                                        05060018
           EVALUATE TRUE                                                05070018
               WHEN DPG54-SEVERE-ERROR                                  05080018
               WHEN DPG54-DATE-INVALID                                  05090018
                   SET AC-DATE-ERROR TO TRUE                            05100018
                   MOVE '5500-DATE-ROUTINE' TO PV-PARAGRAPH-NAME        05110018
                   PERFORM 9999-ABEND                                   05120018
           END-EVALUATE.                                                05130018
                                                                        05140018
      ******************************************************************05150018
        6000-CALL-BOX-LOAD.                                             05160018
      ******************************************************************05170018
           MOVE IF-01-LOC-NM              TO EPT00062-STR-NBR.          05180018
           MOVE IF-02-REG-NM              TO EPT00062-DEV-ID.           05190018
           MOVE IF-03-MSG-ID              TO EPT00062-MSG-ID.           05200018
           MOVE DPG55-DB2-ISO-DATE-FMT    TO EPT00062-CALL-DTE.         05210026
           MOVE PV-TIME                   TO EPT00062-CALL-TM.          05220018
           MOVE IF-03-MSG-BTN-ID          TO EPT00062-MSG-BTN-ID.       05230018
                                                                        05240018
           EXEC SQL INSERT                                              05250018
                    INTO EPT_CALL_BOX                                   05260018
                        (STR_NBR                                        05270018
                        ,DEV_ID                                         05280018
                        ,CALL_DTE                                       05290018
                        ,CALL_TM                                        05300018
                        ,MSG_ID                                         05310018
                        ,MSG_BTN_ID)                                    05320018
                 VALUES                                                 05330018
                    (:EPT00062-STR-NBR                                  05340018
                    ,:EPT00062-DEV-ID                                   05350018
                    ,:EPT00062-CALL-DTE                                 05360018
                    ,:EPT00062-CALL-TM                                  05370018
                    ,:EPT00062-MSG-ID                                   05380018
                    ,:EPT00062-MSG-BTN-ID)                              05390018
            END-EXEC.                                                   05400018
                                                                        05410018
            EVALUATE TRUE                                               05420018
               WHEN SQLCODE = -803                                      05430018
                  ADD +1 TO PV-DB2-DUP-ROW-WRITTEN                      05440018
               WHEN SQLCODE = ZERO                                      05450018
                  ADD +1 TO PV-DB2-ROWS-WRITTEN                         05460018
               WHEN OTHER                                               05470018
                  MOVE '6000-CALL-BOX-LOAD' TO PV-PARAGRAPH-NAME        05480018
                  SET PV-EPT00021-LOAD-ERROR   TO TRUE                  05490018
                  PERFORM 9100-SQL-ABEND                                05500018
            END-EVALUATE.                                               05510018
      *6000-EXIT.                                                       05520018
                                                                        05530018
IN7319******************************************************************05540027
IN7319* WRITE THE BAD OUTPUT REC                                        05550027
IN7319******************************************************************05560027
IN7319 7000-WRITE-BAD.                                                  05570027
IN7319                                                                  05580027
IN7319     WRITE BAD-REC FROM PV-MSGBUFFER.                             05590027
IN7319                                                                  05600027
IN7319     ADD 1 TO PA-BAD-WRITTEN.                                     05610027
IN7319                                                                  05620027
IN7319*7000-EXIT.                                                       05630027
      ******************************************************************05640018
       8900-COMMIT-DB2.                                                 05650018
      ******************************************************************05660018
           EXEC SQL                                                     05670018
              COMMIT                                                    05680018
           END-EXEC.                                                    05690018
                                                                        05700018
           EVALUATE TRUE                                                05710018
              WHEN SQLCODE = 0                                          05720018
                 CONTINUE                                               05730018
              WHEN OTHER                                                05740018
                  MOVE '8900-COMMIT-DB2' TO PV-PARAGRAPH-NAME           05750018
                  DISPLAY '**  ABEND  **'                               05760018
                  CALL 'ILBOABN0' USING ABEND-CODE                      05770018
           END-EVALUATE.                                                05780018
                                                                        05790018
      *8900-EXIT.                                                       05800018
      ****************************************************************  05810018
       9000-CLOSE-ROUTINE.                                              05820018
      ****************************************************************  05830018
           CALL WS-MQCLOSE USING PV-HCONN                               05840018
                                 PV-CLOSE-HANDLE                        05850018
                                 MQCO-NONE                              05860018
                                 PV-COMPLETION-CODE                     05870018
                                 PV-REASON.                             05880018
                                                                        05890018
           CLOSE BACKUP-OUTPUT-FILE.                                    05900018
                                                                        05910018
           DISPLAY '********************************************'.      05920018
           DISPLAY '* RECRODS READ    =  ' PV-MQ-ROWS-READ.             05930018
IN7319     DISPLAY '* BAD DATA WRITTEN=  ' PA-BAD-WRITTEN.              05940027
           DISPLAY '* RECORDS WRITTEN =  ' PV-DB2-ROWS-WRITTEN.         05950018
           DISPLAY '* DUPLICATE COUNT =  ' PV-DB2-DUP-ROW-WRITTEN.      05960018
IN7466     DISPLAY '* TRUNCATED CNT FAIL=' PV-TRUNC-CNT-FAIL.           05961037
IN7466     DISPLAY '* TRUNCATED CNT ACCP=' PV-TRUNC-CNT-ACCP.           05962037
           DISPLAY '********************************************'.      05970018
                                                                        05980018
      *9000-EXIT.                                                       05990018
      ******************************************************************06000018
       9100-SQL-ABEND.                                                  06010018
      *  FORMAT A DB2 ERROR MESSAGE, AND ABEND THE PROGRAM.            *06020018
      ******************************************************************06030018
           DISPLAY '** ABEND **'.                                       06040018
           DISPLAY PV-PARAGRAPH-NAME.                                   06050018
           DISPLAY PC-PGM-NAME  ' - SQL ERROR'.                         06060018
           DISPLAY PC-PGM-NAME  ' - ATTEMPTING TO '                     06070018
                     PV-LAST-DB2-ACTION.                                06080018
                                                                        06090018
      *  ROUTINE TO CONVERT THE SQLCA INTO A READABLE FORMAT, USING     06100018
      *  THE CALLED MODULE DSNTIAR.                                     06110018
      *                                                                 06120018
      ***************************************************************** 06130018
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     06140018
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      06150018
      ***************************************************************** 06160018
           COPY DPPD004.                                                06170018
                                                                        06180018
           SET AC-DB2-ERROR               TO TRUE.                      06190018
           CALL 'ILBOABN0' USING ABEND-CODE.                            06200018
      *9100-EXIT.                                                       06210018
      ******************************************************************06220018
       9200-DISPLAY-MQ-ERROR.                                           06230018
      * DISPLAY THE VALUES OF A FAILED MQ API COMMAND, AND ABEND PGM    06240018
      ******************************************************************06250018
                                                                        06260018
           CALL WS-MQCHECK USING PV-SAVE-REASON                         06270018
                                 PV-MQ-REASON-TEXT.                     06280018
                                                                        06290018
           DISPLAY '**** ABEND *************************************'.  06300018
           DISPLAY '** MQSERIES ERROR **'.                              06310018
           DISPLAY '** PROGRAM:    ', PC-PGM-NAME.                      06320018
           DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH-NAME.                06330018
           DISPLAY '** MESSAGE:    ', PV-ERROR-MESSAGE.                 06340018
           DISPLAY '** COMP CODE:  ', PV-SAVE-COMP-CODE.                06350018
           DISPLAY '** RSN CODE:   ', PV-SAVE-REASON.                   06360018
           DISPLAY '** RSN TEXT:   ', PV-MQ-REASON-TEXT.                06370018
           DISPLAY '************************************************'.  06380018
           SET AC-MQ-ERROR                TO TRUE.                      06390018
           CALL 'ILBOABN0' USING ABEND-CODE.                            06400018
                                                                        06410018
      *9200-DISPLAY-MQ-ERROR.                                           06420018
      ***************************************************************** 06430018
       9400-DISCONNECT-QMGR.                                            06440018
      ***************************************************************** 06450018
                                                                        06460018
      *    DISCONNECT FROM THE QUEUE MANAGER                            06470018
                                                                        06480018
           IF PV-CON-REASON IS NOT EQUAL  TO MQRC-ALREADY-CONNECTED     06490018
              CALL WS-MQDISC USING PV-HCONN                             06500018
                                   PV-COMPLETION-CODE                   06510018
                                   PV-REASON                            06520018
              IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                06530018
                 MOVE 'MQDISC ERROR'         TO PV-ERROR-MESSAGE        06540018
                 MOVE '9400-DISCONNECT-QMGR' TO PV-PARAGRAPH-NAME       06550018
                 MOVE PV-REASON              TO PV-SAVE-REASON          06560018
                 MOVE PV-COMPLETION-CODE     TO PV-SAVE-COMP-CODE       06570018
                 PERFORM 9200-DISPLAY-MQ-ERROR                          06580018
              ELSE                                                      06590018
                 DISPLAY 'MQDISC SUCCESSFUL'                            06600018
              END-IF                                                    06610018
           END-IF.                                                      06620018
                                                                        06630018
      *9400-EXIT.                                                       06640018
      ******************************************************************06650018
       9500-GET-MQ-MESSAGE.                                             06660018
      *  PERFORM AN MQGET TO RETRIEVE A MESSAGE FROM THE MQ QUEUE.     *06670018
      *  THE OPTIONS ARE SET SO THAT IT WILL WAIT UNTIL A MESSAGE      *06680018
      *  APPEARS ON THE QUEUE, AND THAT SYNCPOINT IS SET FOR COMMITING *06690018
      *  AND ROLLING BACK MESSAGES. THE MESSAGE IS EXAMINED, THE       *06700018
      *  PROGRAM WILL END AFTER 4 END OF DAY MESSAGES ARE RECEIVED.    *06710018
      ******************************************************************06720018
            INITIALIZE PV-MSGBUFFER.                                    06730018
            MOVE +100                  TO PV-MSGLENGTH.                 06740018
            MOVE MQMI-NONE             TO MQMD-MSGID.                   06750018
            MOVE MQCI-NONE             TO MQMD-CORRELID.                06760018
            MOVE CC-PC-QUEUE-NAME      TO MQOD-OBJECTNAME.              06770018
            COMPUTE MQGMO-OPTIONS       = MQGMO-CONVERT   +             06780018
                                          MQGMO-SYNCPOINT +             06790036
                                          MQGMO-ACCEPT-TRUNCATED-MSG.   06791036
                                                                        06800018
            CALL WS-MQGET USING PV-HCONN                                06810018
                                PV-INT-OPEN-INPUT-HNDL                  06820018
                                MQM-MESSAGE-DESCRIPTOR                  06830018
                                MQM-GET-MESSAGE-OPTIONS                 06840018
                                PV-MSGLENGTH                            06850018
                                PV-MSGBUFFER                            06860018
                                PV-DATA-LENGTH                          06870018
                                PV-COMPLETION-CODE                      06880018
                                PV-REASON.                              06890018
                                                                        06900018
      *     IF GET FAILED THEN DISPLAY ERROR MESSAGE                    06910018
      *     AND BREAK OUT OF LOOP                                       06920018
                                                                        06930018
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                        06940018
              DISPLAY 'PV-COMPLETION-CODE: ' PV-COMPLETION-CODE         06950018
              DISPLAY 'PV-REASON: ' PV-REASON                           06960018
                                                                        06970027
IN7466        IF (PV-REASON = MQRC-FORMAT-ERROR OR                      06980034
IN7466           PV-REASON = MQRC-TRUNCATED-MSG-FAILED OR               06981036
IN7466           PV-REASON = MQRC-TRUNCATED-MSG-ACCEPTED)               06982038
IN7319           PERFORM 7000-WRITE-BAD                                 06990027
IN7319           SET PS-BAD-MSG      TO TRUE                            07000029
IN7466           IF PV-REASON = MQRC-TRUNCATED-MSG-FAILED               07001033
IN7466              ADD 1            TO PV-TRUNC-CNT-FAIL               07002037
IN7466              DISPLAY  ' PV-MQ-ROWS-READ-F: ' PV-MQ-ROWS-READ     07002137
IN7466           END-IF                                                 07003033
IN7466           IF PV-REASON = MQRC-TRUNCATED-MSG-ACCEPTED             07004037
IN7466              ADD 1            TO PV-TRUNC-CNT-ACCP               07005037
IN7466              DISPLAY  ' PV-MQ-ROWS-READ-A: ' PV-MQ-ROWS-READ     07006037
IN7466           END-IF                                                 07007037
IN7319        ELSE                                                      07010027
IN7319           SET PS-NO-MORE-MSG  TO  TRUE                           07020027
IN7319        END-IF                                                    07030027
           ELSE                                                         07040018
               MOVE LOW-VALUES            TO PV-HOLD-MESSAGE            07050018
               MOVE PV-MSGBUFFER          TO PV-HOLD-MESSAGE            07060018
IN7319         SET PS-NO-BAD-MSG          TO TRUE                       07070029
           END-IF.                                                      07080018
                                                                        07090018
                                                                        07100018
      *9500-EXIT.                                                       07110018
      ******************************************************************07120018
       9700-BACK-OUT-MQ.                                                07130018
      ******************************************************************07140018
                                                                        07150018
           CALL WS-MQBACK USING PV-HCONN                                07160018
                                PV-COMPLETION-CODE                      07170018
                                PV-REASON.                              07180018
                                                                        07190018
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   07200018
              MOVE '9700-BACK-OUT-MQ'     TO PV-PARAGRAPH-NAME          07210018
              MOVE 'MQBACK ERROR'         TO PV-ERROR-MESSAGE           07220018
              PERFORM 9200-DISPLAY-MQ-ERROR                             07230018
           END-IF.                                                      07240018
                                                                        07250018
      ******************************************************************07260018
       9800-COMMIT-MQ.                                                  07270018
      ******************************************************************07280018
                                                                        07290018
           CALL WS-MQCMIT USING PV-HCONN                                07300018
                                PV-COMPLETION-CODE                      07310018
                                PV-REASON.                              07320018
                                                                        07330018
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   07340018
              MOVE '9800-COMMIT-MQ'  TO PV-PARAGRAPH-NAME               07350018
              MOVE 'MQCMIT ERROR'    TO PV-ERROR-MESSAGE                07360018
              PERFORM 9700-BACK-OUT-MQ                                  07370018
              PERFORM 9200-DISPLAY-MQ-ERROR                             07380018
           END-IF.                                                      07390018
                                                                        07400018
      ******************************************************************07410018
      * 9999-ABEND - PERFORMS A NON-SQL-ABEND                           07420018
      ******************************************************************07430018
       9999-ABEND.                                                      07440018
           DISPLAY '** ABEND           **'.                             07450018
           DISPLAY '** PROGRAM         ** = ' PC-PGM-NAME.              07460018
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07470018
           CALL 'ILBOABN0' USING ABEND-CODE.                            07480018
