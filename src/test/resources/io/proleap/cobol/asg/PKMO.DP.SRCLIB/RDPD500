      ******************************************************************00010000
      * PD500-SHIFT-CONV-ALPHA-DL - THIS MODULE HAS 2 MAJOR FUNCTIONS:  00020000
      * STRIP OFF LEADING ZEROS AND TRANSLATE ALPHA CHARACTERS. AFTER   00021000
      * DETERMINING LEADING ZEROS ARE PRESENT IN A DRIVERS LICENSE,     00021100
      * THE REMAINING DIGITS ARE SHIFTED BYTE BY BYTE THE MAXIMUM       00021200
      * NUMBER OF DIGITS ALLOTED FOR THE DRIVERS LICENSE NUMBER         00021300
      * HAS BEEN EXCEEDED, OR A SPACE IS ENCOUNTED. ALL REMAINING       00021400
      * BYTES AFTER THE SHIFT ARE INITIALIZED TO SPACES.                00021500
      *    THE SECOND FUNCTION IS TO TRANSLATE ANY/ALL ALPHA            00021600
      * CHARACTERS IN A DRIVERS LICENSE BASED UPON A KEY PROVIDED BY    00021700
      * SCAN. EACH ALPHA CHARACTER IS REPLACED BY TWO NUMBERS. THEN     00021800
      * THE REMAINING DIGITS ARE SHIFTED BYTE BY BYTE UNTIL THE MAX     00021900
      * NUMBER OF DIGITS ALLOTED FOR THE DL HAS BEEN EXCEEDED (OR A     00022000
      * SPACE IS ENCOUNTED).                                            00023000
      ******************************************************************00024000
      * CREATED 3/15/99 BY NANCY MARTIN FOR WO 10632 SWITCH FROM        00025000
      *         REGIONAL SCAN FILE (UPLOADED FROM CMS) TO A NATIONAL    00026000
      *         FILE LOADED DIRECTLY ONTO MAINFRAME.                    00027000
      ******************************************************************00028000
      * UPDATED 5/21/03 BY JOANNE PIETROSKE                             00029000
      *         ADDED CHECK THAT IF INDEX > MAX INDEX, TO BYPASS THE    00030000
      *         REST OF THE CONVERT LOGIC.  THIS PROGRAM ONLY HAD AN    00040000
      *         'EQUAL' CHECK, BUT OTHER PROGRAMS DON'T HAVE THE SAME   00050000
      *         DL MAX LENGTH, SO LOGIC MUST STOP AT THAT POINT.        00060000
      ******************************************************************00070000
      * UPDATED 6/01/23 BY JEAN KURK                                    00071000
      *         RENAMED FROM CRPD500                                    00072000
      ******************************************************************00073000
       PD500-SHIFT-CONV-ALPHA-DL.                                       00080000
           MOVE RDWS500-INPUT-DL TO  RDWS500-ALPHA-DL.                  00090001
                                                                        00091000
           PERFORM                                                      00092000
              VARYING RDWS500-LEFT-INDEX                                00093001
                 FROM 1 BY 1                                            00094000
                UNTIL ((RDWS500-LEFT-INDEX > RDWS500-MAX-DL)            00095001
                   OR (RDWS500-ALPHA-BYTE                               00096001
                         (RDWS500-LEFT-INDEX) NOT = ZERO))              00097001
           END-PERFORM.                                                 00098000
                                                                        00099000
           IF RDWS500-LEFT-INDEX = 1                                    00100001
               CONTINUE                                                 00100100
           ELSE                                                         00100200
               PERFORM                                                  00100300
                  VARYING RDWS500-BYTE-INDEX                            00100401
                     FROM 1 BY 1                                        00100500
                    UNTIL (RDWS500-LEFT-INDEX > RDWS500-MAX-DL)         00100601
                     MOVE RDWS500-ALPHA-BYTE (RDWS500-LEFT-INDEX)       00100701
                       TO RDWS500-ALPHA-BYTE (RDWS500-BYTE-INDEX)       00100801
                   SET RDWS500-LEFT-INDEX UP BY 1                       00100901
               END-PERFORM                                              00101000
               PERFORM                                                  00101100
                 UNTIL (RDWS500-BYTE-INDEX > RDWS500-MAX-DL)            00101201
                  INITIALIZE RDWS500-ALPHA-BYTE (RDWS500-BYTE-INDEX)    00101301
                   SET RDWS500-BYTE-INDEX UP BY 1                       00101401
               END-PERFORM                                              00101500
           END-IF.                                                      00101600
       PD500-ALPHA-LOOP.                                                00101700
           PERFORM                                                      00101800
              VARYING RDWS500-BYTE-INDEX                                00101901
                 FROM 1 BY 1                                            00102000
NEM             UNTIL ((RDWS500-BYTE-INDEX > RDWS500-MAX-DL)            00103001
                   OR (RDWS500-ALPHA-BYTE (RDWS500-BYTE-INDEX)          00104001
                       ALPHABETIC)                                      00105000
                   OR (RDWS500-ALPHA-BYTE (RDWS500-BYTE-INDEX)          00106001
                       = SPACE))                                        00107000
           END-PERFORM.                                                 00108000
                                                                        00109000
           IF RDWS500-BYTE-INDEX = RDWS500-MAX-DL                       00110001
JMP        OR RDWS500-BYTE-INDEX > RDWS500-MAX-DL                       00120001
             OR (RDWS500-ALPHA-BYTE (RDWS500-BYTE-INDEX) = SPACE)       00130001
               GO TO PD500-FINISH                                       00140000
           ELSE                                                         00150000
               PERFORM                                                  00160000
                   VARYING RDWS500-AC-INDEX FROM 1 BY 1                 00170001
                     UNTIL (RDWS500-AC-INDEX = 27)                      00180001
                        OR (RDWS500-AC-LETTER (RDWS500-AC-INDEX) =      00190001
                            RDWS500-ALPHA-BYTE (RDWS500-BYTE-INDEX))    00200001
               END-PERFORM                                              00210000
               SET RDWS500-LEFT-INDEX TO RDWS500-MAX-DL                 00220001
               PERFORM                                                  00230000
                   VARYING RDWS500-LEFT-INDEX                           00240001
                      FROM RDWS500-LEFT-INDEX BY -1                     00250001
                     UNTIL  (RDWS500-LEFT-INDEX = RDWS500-BYTE-INDEX)   00260001
                   MOVE RDWS500-ALPHA-BYTE (RDWS500-LEFT-INDEX)         00270002
                     TO RDWS500-ALPHA-BYTE (RDWS500-LEFT-INDEX + 1)     00280001
               END-PERFORM                                              00290000
               MOVE RDWS500-AC-NUM-1 (RDWS500-AC-INDEX) TO              00300002
                    RDWS500-ALPHA-BYTE (RDWS500-BYTE-INDEX)             00310001
               MOVE RDWS500-AC-NUM-2 (RDWS500-AC-INDEX) TO              00320001
                    RDWS500-ALPHA-BYTE (RDWS500-BYTE-INDEX + 1)         00330001
           END-IF.                                                      00340000
           GO TO PD500-ALPHA-LOOP.                                      00350000
       PD500-FINISH.                                                    00360000
           MOVE RDWS500-ALPHA-DL TO RDWS500-OUTPUT-DL.                  00370001
NEMTST**** DISPLAY '**PD500-CONV  INPUT DL=',                           00380000
NEMTST****         RDWS500-INPUT-DL,                                    00390001
NEMTST****         '** OUTGOING DL=',                                   00400000
NEMTST****         RDWS500-OUTPUT-DL, '**'.                             00410001
       PD500-EXIT.                                                      00420000
           EXIT.                                                        00430000
