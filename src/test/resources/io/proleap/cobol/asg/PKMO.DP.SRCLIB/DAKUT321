      *************************                                         00010000
       IDENTIFICATION DIVISION.                                         00020000
      *************************                                         00030000
       PROGRAM-ID.      DAKUT321.                                       00040000
       AUTHOR.          COGNIZANT.                                      00050000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00060000
       DATE-WRITTEN.    05/01/2009.                                     00070000
       DATE-COMPILED.                                                   00080000
                                                                        00090000
      ***************************************************************** 00100000
      *    ****** NOTE!! - THIS PROGRAM IS CALLED BY BATCH PROGRAMS   * 00110000
      *    ****** AND MUST BE COMPILED AS A BATCH PROGRAM.              00120000
      *                  COBOL II WITH ORACLE                         * 00130000
      ***************************************************************** 00140000
      *                                                               * 00150000
      *               --- GET ORACLE TABLE INFO ---                   * 00160000
      *                                                               * 00170000
      *    THIS PROGRAM RETURNS BRAND PRORATION INFORMATION FOR       * 00180000
      *    THE SPECIFIED DA MARKDOWN ALLOWANCE.                       * 00190000
      *---------------------------------------------------------------* 00210000
      *-   THIS CALLED MODULE NEEDS TO RELEASE THE UNIT OF WORK         00210000
      *-   AFTER RETURNING TO CALLING MODULE (SEE 'COMMIT' LOGIC)       00210000
      *---------------------------------------------------------------* 00210000
      *  THIS PROGRAM IS A COPY OF MLKUT358.  SINCE THIS PROGRAM WAS  *         
      *  CALLING A DA INSTANCE, SO THE PROGRAM SHOULD BE A DA* PROGRAM*         
      *****************************************************************         
      * 8613F    07/17/2015  SUE BARTELT                              *         
      *                      RMS 9 REMEDIATION. CHANGED FROM          *         
      *                      AUTO LOGON TO EXTERNAL SIGN ON           *         
      *****************************************************************         
       EJECT                                                            00220000
                                                                        00230000
       ENVIRONMENT DIVISION.                                            00240000
      *                                                                 00250000
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
8613F      SELECT ORACLE-LOGIN-FILE                                             
8613F          ASSIGN TO ORALOGON.                                              
           SELECT ORA-TBL-UNLD-FILE                                             
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
8613F  FD  ORACLE-LOGIN-FILE                                                    
8613F      RECORDING MODE IS F                                                  
8613F      LABEL RECORDS ARE STANDARD                                           
8613F      BLOCK CONTAINS 0 RECORDS                                             
8613F      DATA RECORD IS ORACLE-LOGIN-REC.                                     
8613F  01  ORACLE-LOGIN-REC.                                                    
8613F      05  FILLER                  PIC X(80).                               
                                                                                
                                                                                
       FD  ORA-TBL-UNLD-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ORA-TBL-UNLD-REC         PIC X(24).                                  
      *                                                                 00270000
       WORKING-STORAGE SECTION.                                         00280000
                                                                        00290000
8613F  01  CC-ORACLE-CONNECTION.                                                
8613F      05  CC-ORACLE-USERID             PIC X(40).                          
8613F          88 CC-AUTO-LOGON                       VALUE '/'.                
8613F      05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  ABEND-CODE                       PIC S9(4) VALUE ZEROS               
                                                      COMP SYNC.                
           88  AC-OTHER-ERROR                         VALUE +4003.              
           88  AC-SQL-ERROR                           VALUE +4013.              
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
8613F      05  PC-AUTO-LOGON                PIC X(01) VALUE '/'.                
                                                                                
                                                                                
       01  PV-PROGRAM-VARIABLES.                                        00300000
           05 PC-PGM-NAME                   PIC X(8)  VALUE 'DAKUT321'.         
           05 PC-JOB-NAME                   PIC X(8)  VALUE SPACES.             
           05 PV-CNVRTED-SQLCODE            PIC Z9999-.                         
           05  PV-ABEND-CODE            PIC S9(04) VALUE +0  COMP.              
               88  PV-ABEND-4001                   VALUE 4001.                  
      *            EMPTY INPUT FILE                                             
               88  PV-ABEND-4013                   VALUE 4013.                  
      *            SQL ERROR                                                    
           05  PV-DA-PRRTN-REC-CNT      PIC S9(06) VALUE +0 COMP.               
8613F      05  PV-ORACLE-USERID-LEN        PIC S9(04)      VALUE ZEROS.         
8613F      05  PV-ORACLE-PASSWORD-LEN      PIC S9(04)      VALUE ZEROS.         
                                                                                
       01  PV-ORA-UNLD-REC-FIELDS.                                              
           05  PV-DR-ALW-NBR            PIC X(16)  VALUE SPACES.                
           05  PV-DA-PRRTN-BRND         PIC S9(09) COMP VALUE ZEROES.           
           05  PV-DA-PRRTN-PCT          PIC S9(04)V99 COMP-3 VALUE +0.          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-BRND-PRRTN-CSR-SW   PIC X(01) VALUE 'N'.               
               88  PS-END-OF-BRND-PRRTN-CSR            VALUE 'Y'.               
               88  PS-NOT-END-OF-BRND-PRRTN-CSR        VALUE 'N'.               
           05  PS-MORE-ORACLE-CC-SW          PIC X(01) VALUE 'Y'.               
               88  PS-MORE-ORACLE-CC                   VALUE 'Y'.               
               88  PS-NO-MORE-ORACLE-CC                VALUE 'N'.               
8613F      05  PS-ORACLE-LOGIN-EOF-SW        PIC  X    VALUE 'N'.               
8613F          88  ORACLE-LOGIN-EOF                    VALUE 'Y'.               
      *----------------------------------------------------------------*00350000
      *  BAD SQL CALL DATA AREAS                                       *00360000
      *----------------------------------------------------------------*00370000
                                                                        00530000
       01  WS-ERROR-FIELDS.                                             00540000
           05 WS-MSG-TEXT                  PIC X(512).                  00550000
           05 WS-MAX-SIZE                  PIC S9(09) COMP              00560000
                                                       VALUE +512.      00570000
           05 WS-MSG-LENGTH                PIC S9(09) COMP.             00580000
           05 WS-ABEND-CODE                PIC S9(04) COMP SYNC         00590000
                                                      VALUE ZEROS.      00600000
      *    05 PV-ORACLE-USERID-LEN         PIC S9(4) COMP.                      
      *    05 PV-ORACLE-PASSWORD-LEN       PIC S9(4) COMP.                      
       EJECT                                                            00610000
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                     00620000
                                                                        00630000
      *                                                                 00640000
      *---------------------------------------------------------------* 00650000
      *    ORACLE LOGON WORKING STORAGE COPYBOOK.                       00660000
      *---------------------------------------------------------------* 00670000
       01  USERNAME                        PIC X(10)      VARYING.      00680000
       01  PASSWD                          PIC X(10)      VARYING.      00690000
      *01  PC-ENT01-TYPE-CDE               PIC X(02)      VALUE '01'.   00700000
      *01  PV-COUNTERS.                                                 00720000
      *    05   PV-NEXT-SEQ               PIC S9(8)V COMP-3             00730000
      *                                               VALUE ZERO.       00740000
                                                                                
       01  OC-BRAND-PRRTN-UP-PARMS.                                             
           05 OC-DR-ALW-NBR        PIC X(16).                                   
           05 OC-LBL-SEQ-NBR       PIC 9(05).                                   
           05 OC-PRRTN-PCT         PIC 9(04).                                   
      *    05 OC-PRRTN-PCT         PIC 9(04).99.                                
                                                                        00960000
      ***************************************************************** 00970000
      *   COPYBOOKS USED FOR STORED PROCEDURES                          00980000
      ***************************************************************** 00990000
                                                                        01000000
                                                                        01010000
           EXEC SQL END DECLARE SECTION END-EXEC.                       01030000
                                                                        01040000
           EXEC SQL INCLUDE SQLCACOB      END-EXEC.                     01050000
                                                                        01060000
       EJECT                                                            01290000
      *                                                                 01300000
       LINKAGE SECTION.                                                 01310000
       PROCEDURE DIVISION.                                              01360000
                                                                        01370000
       0000-MAIN.                                                       01380000
8613F      OPEN INPUT ORACLE-LOGIN-FILE.                                        
           OPEN OUTPUT ORA-TBL-UNLD-FILE.                                       
           MOVE ZEROES TO PV-DA-PRRTN-REC-CNT.                                  
           INITIALIZE PV-ORA-UNLD-REC-FIELDS.                                   
           PERFORM 1000-ORACLE-LOGON.                                           
           PERFORM 2000-DCLR-DA-BRND-PRRTN-CRSR.                                
           IF NOT PS-END-OF-BRND-PRRTN-CSR                                      
              PERFORM 2100-OPEN-DA-BRND-PRRTN-CSR                               
              PERFORM 2200-FETCH-DA-BRND-PRRTN-CSR                              
                UNTIL PS-END-OF-BRND-PRRTN-CSR                                  
              PERFORM 2300-CLOSE-DA-BRND-PRRTN-CSR                              
           END-IF.                                                              
                                                                                
COMMIT     EXEC SQL                                                             
COMMIT         COMMIT WORK RELEASE                                              
COMMIT     END-EXEC.                                                            
             DISPLAY '**********************************'                       
             DISPLAY '*        DAKUT321 COUNTS'                                 
             DISPLAY '*  NO OF RECORDS EXTRACTED :' PV-DA-PRRTN-REC-CNT         
             DISPLAY '**********************************'                       
           CLOSE ORA-TBL-UNLD-FILE.                                             
                                                                                
           STOP RUN.                                                            
      ******************************************************************        
      *  LOG ON TO ORACLE                                                       
      ******************************************************************        
       1000-ORACLE-LOGON.                                                       
                                                                                
8613F      DISPLAY '**------ LOGGING ON TO ORACLE ------**'.                    
8613F                                                                           
8613F      DISPLAY '1000-ORACLE-LOGON'.                                         
8613F                                                                           
8613F      READ ORACLE-LOGIN-FILE INTO CC-ORACLE-CONNECTION                     
8613F         AT END                                                            
8613F            SET ORACLE-LOGIN-EOF      TO TRUE.                             
8613F                                                                           
8613F      IF CC-AUTO-LOGON                                                     
8613F         DISPLAY PC-PGM-NAME ' - AUTO LOGON TO ORACLE'                     
8613F         EXEC SQL                                                          
8613F              CONNECT :PC-AUTO-LOGON                                       
8613F         END-EXEC                                                          
8613F      ELSE                                                                 
8613F         INITIALIZE PV-ORACLE-USERID-LEN                                   
8613F                    PV-ORACLE-PASSWORD-LEN                                 
8613F         INSPECT CC-ORACLE-USERID                                          
8613F                 TALLYING PV-ORACLE-USERID-LEN                             
8613F                 FOR CHARACTERS BEFORE INITIAL SPACE                       
8613F         INSPECT CC-ORACLE-PASSWORD                                        
8613F                 TALLYING PV-ORACLE-PASSWORD-LEN                           
8613F                 FOR CHARACTERS BEFORE INITIAL SPACE                       
8613F         MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN)     TO             
8613F                 USERNAME-ARR                                              
8613F         MOVE PV-ORACLE-USERID-LEN    TO USERNAME-LEN                      
8613F         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
8613F                 PASSWD-ARR                                                
8613F         MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
8613F         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
8613F              PASSWD-ARR                                                   
8613F         MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
8613F         MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN)     TO             
8613F              USERNAME-ARR                                                 
8613F         MOVE PV-ORACLE-USERID-LEN    TO USERNAME-LEN                      
8613F         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
8613F              PASSWD-ARR                                                   
8613F         MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
8613F         DISPLAY PC-PGM-NAME  ' - LOGON TO ORACLE AS USER '                
8613F              USERNAME-ARR                                                 
8613F         EXEC SQL                                                          
8613F              CONNECT :USERNAME IDENTIFIED BY :PASSWD                      
8613F         END-EXEC                                                          
8613F         INITIALIZE CC-ORACLE-PASSWORD                                     
8613F                    PASSWD                                                 
8613F      END-IF.                                                              
8613F                                                                           
8613F      IF (SQLCODE NOT = 0)                                                 
8613F          DISPLAY 'ERROR LOGGING ONTO ORACLE'                              
8613F          PERFORM 9998-SQL-ERROR                                           
8613F      END-IF.                                                              
           EXEC SQL                                                             
            ALTER SESSION                                                       
                SET NLS_DATE_FORMAT = 'YYYY-MM-DD-HH24.MI.SS'                   
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  DECLARE DA BRAND PRORATION CURSOR                                      
      ******************************************************************        
       2000-DCLR-DA-BRND-PRRTN-CRSR.                                            
                                                                                
           EXEC SQL                                                             
                DECLARE DA_BRND_PRRTN_CSR CURSOR FOR                            
                   SELECT                                                       
                        DR_ALW_NBR                                              
                       ,LBL_SEQ_NBR                                             
                       ,PRRTN_PCT                                               
                   FROM                                                         
                        DAT_BRND_PRRTN                                          
                   WHERE EFF_END_TMST IS NULL                                   
                     AND PRRTN_PCT > 0                                          
                   ORDER BY                                                     
                        DR_ALW_NBR                                              
                       ,LBL_SEQ_NBR                                             
           END-EXEC.                                                            
                                                                                
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
                 CONTINUE                                                       
             WHEN SQLCODE = 1403                                                
                 SET PS-END-OF-BRND-PRRTN-CSR TO TRUE                           
             WHEN OTHER                                                         
                 MOVE SQLCODE TO PV-CNVRTED-SQLCODE                             
                                                                                
                 DISPLAY 'UNABLE TO DECLARE DA_BRND_PRRTN_CSR'                  
                 DISPLAY 'SQLCODE: ' PV-CNVRTED-SQLCODE                         
                 PERFORM 9998-SQL-ERROR                                         
                                                                                
           END-EVALUATE.                                                        
      ******************************************************************        
      *  OPEN DA-BRND-PRRTN CURSOR                                              
      ******************************************************************        
       2100-OPEN-DA-BRND-PRRTN-CSR.                                             
                                                                                
           EXEC SQL                                                             
               OPEN DA_BRND_PRRTN_CSR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = 1403                                              
                   SET PS-END-OF-BRND-PRRTN-CSR TO TRUE                         
               WHEN OTHER                                                       
                 MOVE SQLCODE TO PV-CNVRTED-SQLCODE                             
                                                                                
                 DISPLAY 'UNABLE TO OPEN DA_BRND_PRRTN_CSR'                     
                 DISPLAY 'SQLCODE: ' PV-CNVRTED-SQLCODE                         
                 PERFORM 9998-SQL-ERROR                                         
                                                                                
           END-EVALUATE.                                                        
      ******************************************************************        
      *  FETCH DA-BRND-PRRTN CURSOR                                             
      ******************************************************************        
       2200-FETCH-DA-BRND-PRRTN-CSR.                                            
                                                                                
           EXEC SQL                                                             
               FETCH DA_BRND_PRRTN_CSR                                          
               INTO                                                             
                    :OC-DR-ALW-NBR                                              
                   ,:OC-LBL-SEQ-NBR                                             
                   ,:OC-PRRTN-PCT                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                  PERFORM 2500-WRITE-OUTPUT                                     
             WHEN SQLCODE = 1403                                                
                 SET PS-END-OF-BRND-PRRTN-CSR TO TRUE                           
             WHEN OTHER                                                         
                 MOVE SQLCODE TO PV-CNVRTED-SQLCODE                             
                 DISPLAY 'UNABLE TO FETCH FROM DA_BRND_PRRTN_CSR'               
                 DISPLAY 'DA NUMBER: ' OC-DR-ALW-NBR                            
                 DISPLAY 'SQLCODE: ' PV-CNVRTED-SQLCODE                         
                 PERFORM 9998-SQL-ERROR                                         
                                                                                
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  CLOSE DA-BRND-PRRTN CURSOR                                             
      ******************************************************************        
       2300-CLOSE-DA-BRND-PRRTN-CSR.                                            
                                                                                
           EXEC SQL                                                             
               CLOSE DA_BRND_PRRTN_CSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = 0                                                   
               CONTINUE                                                         
             WHEN OTHER                                                         
                 MOVE SQLCODE TO PV-CNVRTED-SQLCODE                             
                                                                                
                 DISPLAY 'UNABLE TO CLOSE DA_BRND_PRRTN_CSR'                    
                 DISPLAY 'SQLCODE: ' PV-CNVRTED-SQLCODE                         
                 PERFORM 9998-SQL-ERROR                                         
                                                                                
           END-EVALUATE.                                                        
      ******************************************************************02036000
      ******************************************************************02036000
       2500-WRITE-OUTPUT.                                                       
                                                                                
            ADD 1 TO PV-DA-PRRTN-REC-CNT.                                       
            INITIALIZE PV-ORA-UNLD-REC-FIELDS.                                  
            MOVE OC-DR-ALW-NBR  TO PV-DR-ALW-NBR.                               
            MOVE OC-LBL-SEQ-NBR TO PV-DA-PRRTN-BRND.                            
            MOVE OC-PRRTN-PCT   TO PV-DA-PRRTN-PCT.                             
            WRITE ORA-TBL-UNLD-REC FROM PV-ORA-UNLD-REC-FIELDS.                 
                                                                                
      ******************************************************************02036000
      *9998-SQL-ERROR.                                                  02037000
      ******************************************************************02038000
      ******************************************************************02036000
      *9998-SQL-ERROR.                                                  02037000
      ******************************************************************02038000
       9998-SQL-ERROR.                                                  02039000
                                                                        02040000
           DISPLAY '** ABEND **       ** = SQLERROR'.                   02050000
           DISPLAY '** PROGRAM        ** = MLKUP359'.                   02060000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   02070000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  02080000
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                02090000
                                                                        02100000
                                                                        02110000
           MOVE SPACES TO WS-MSG-TEXT.                                  02120000
                                                                        02130000
           CALL 'SQLGLM' USING WS-MSG-TEXT,                             02140000
                               WS-MAX-SIZE,                             02150000
                               WS-MSG-LENGTH.                           02160000
           DISPLAY 'MESSAGE TEXT      ** = ' WS-MSG-TEXT.               02170000
                                                                        02180000
           SET PV-ABEND-4013 TO TRUE.                                   02190000
                                                                        02180000
COMMIT     EXEC SQL                                                             
COMMIT         COMMIT WORK RELEASE                                              
COMMIT     END-EXEC.                                                            
                                                                        02180000
