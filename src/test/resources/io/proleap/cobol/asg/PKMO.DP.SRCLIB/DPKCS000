00001 ******************************************************************03/17/92
00002  IDENTIFICATION DIVISION.                                         DPKCS000
00003 ******************************************************************   LV003
00004                                                                   DPKCS000
00005  PROGRAM-ID.                DPKCS000.                             DPKCS000
00006  AUTHOR.                    J. KNOKE - TCC                        DPKCS000
00007  INSTALLATION.              KOHLS DEPARTMENT STORES.              DPKCS000
00008  DATE-WRITTEN.              10-23-89.                             DPKCS000
00009  DATE-COMPILED.                                                   DPKCS000
00010                                                                   DPKCS000
00011 ******************************************************************DPKCS000
00012 *  CICS PROGRAM - DPKCS000 - Z000                                *DPKCS000
00013 *                                                                *DPKCS000
00014 *      DB2 CHECKPOINT RESTART SYSTEM - MAIN MENU                 *DPKCS000
00015 *                                                                *DPKCS000
00016 *  CHANGED 3/92 TO USE CICSAUTH INSTEAD OF RCHKCICS              *DPKCS000
00017 *  BY M. FURLICK.                                                *DPKCS000
00018 *                                                                *DPKCS000
00019 ******************************************************************DPKCS000
00020                                                                   DPKCS000
00021 ******************************************************************DPKCS000
00022  ENVIRONMENT DIVISION.                                            DPKCS000
00023 ******************************************************************DPKCS000
00024                                                                   DPKCS000
00025 ******************************************************************DPKCS000
00026  DATA DIVISION.                                                   DPKCS000
00027 ******************************************************************DPKCS000
00028                                                                   DPKCS000
00029  WORKING-STORAGE SECTION.                                         DPKCS000
00030                                                                   DPKCS000
00031 *                                                                 DPKCS000
00032 *  STANDARD ATTENTION IDENTIFIER LIST                             DPKCS000
00033 *                                                                 DPKCS000
00034                                                                   DPKCS000
00035      COPY DFHAID.                                                 DPKCS000
00036                                                                   DPKCS000
00037 *                                                                 DPKCS000
00038 *  STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST          DPKCS000
00039 *                                                                 DPKCS000
00040                                                                   DPKCS000
00041      COPY DFHBMSCA.                                               DPKCS000
00042                                                                   DPKCS000
00043 *                                                                 DPKCS000
00044 *  DSECT FOR THE DPKM000 MAPSET                                   DPKCS000
00045 *                                                                 DPKCS000
00046                                                                   DPKCS000
00047      COPY DPKM000.                                                DPKCS000
00048                                                                   DPKCS000
00049  01  PC-PROGRAM-CONSTANTS.                                        DPKCS000
00050      05  PC-COMMON-AREA-LENG          PIC S9(4)   VALUE +33       DPKCS000
00051                                       COMP SYNC.                  DPKCS000
00052      05  PC-TWA-LENG                  PIC S9(4)   VALUE +100      DPKCS000
00053                                       COMP SYNC.                  DPKCS000
00054      05  PC-LEAVE-SYSTEM-LENG         PIC S9(4)   VALUE +160      DPKCS000
00055                                       COMP SYNC.                  DPKCS000
00056      05  PC-INQUIRY-PROGRAM           PIC X(8)    VALUE           DPKCS000
00057          'DPKCS005'.                                              DPKCS000
00058      05  PC-RACF-PROGRAM              PIC X(8)    VALUE           DPKCS000
00059          'RCHKCICS'.                                              DPKCS000
00060      05  PC-MAINTENANCE-PROGRAM       PIC X(8)    VALUE           DPKCS000
00061          'DPKCS004'.                                              DPKCS000
00062                                                                   DPKCS000
00063  01  CA-COMMON-AREA.                                              DPKCS000
00064      05  CA-COMMON-DATA.                                          DPKCS000
00065          10  CA-TRANSACTION-STATUS    PIC X.                      DPKCS000
00066              88  CA-INITIALIZE-SCREEN             VALUE 'S'.      DPKCS000
00067              88  CA-PROCESS-SCREEN                VALUE 'R'.      DPKCS000
00068          10  CA-MAIN-MENU             PIC X(8)    VALUE SPACES.   DPKCS000
00069          10  CA-LAST-PROGRAM          PIC X(8)    VALUE SPACES.   DPKCS000
00070          10  FILLER                   PIC X(16)   VALUE SPACES.   DPKCS000
00071                                                                   DPKCS000
00072  01  PV-PROGRAM-VARIABLES.                                        DPKCS000
00073      05  PV-LEAVE-SYSTEM-MESSAGE      PIC X(160)  VALUE SPACES.   DPKCS000
00074      05  PV-USER-ID                   PIC X(8)    VALUE SPACES.   DPKCS000
00075      05  PV-CICS-RESPONSE             PIC S9(9)   VALUE ZERO      DPKCS000
00076                                       COMP SYNC.                  DPKCS000
00077                                                                   DPKCS000
00078  01  SE-SYSTEM-ERROR-MESSAGE.                                     DPKCS000
00079      05  FILLER                       PIC X(43)   VALUE           DPKCS000
00080          'A SYSTEM ERROR HAS OCCURRED - CONTACT DP -'.            DPKCS000
00081      05  FILLER                       PIC X(33)   VALUE           DPKCS000
00082          'PROGRAM DPKCS000       EIBRESP = '.                     DPKCS000
00083      05  SE-CICS-RESPONSE             PIC 9(4)    VALUE ZERO.     DPKCS000
00084      05  FILLER                       PIC X(5)    VALUE SPACES.   DPKCS000
00085      05  FILLER                       PIC X(14)   VALUE           DPKCS000
00086          'ATTEMPING TO '.                                         DPKCS000
00087      05  SE-ATTEMPTED-ACTION          PIC X(61)   VALUE SPACES.   DPKCS000
00088                                                                   DPKCS000
00089  01  ST-SYSTEM-TIME-AREA.                                         DPKCS000
00090      05  ST-ABSTIME                   PIC S9(18)  VALUE ZERO      DPKCS000
00091                                       COMP SYNC.                  DPKCS000
00092      05  ST-TIME-EDIT.                                            DPKCS000
00093          10  ST-TIME-HHMM             PIC X(5)    VALUE SPACES.   DPKCS000
00094          10  FILLER                   PIC X(3)    VALUE SPACES.   DPKCS000
00095                                                                   DPKCS000
00096  01  PM-PROGRAM-MESSAGE-DATA.                                     DPKCS000
00097      05  FILLER                       PIC X(79) VALUE             DPKCS000
00098          '                                                     '. DPKCS000
00099      05  FILLER                       PIC X(79) VALUE             DPKCS000
00100          'UNKNOWN PF KEY PRESSED                               '. DPKCS000
00101      05  FILLER                       PIC X(79) VALUE             DPKCS000
00102          'INVALID SELECTION ENTERED                            '. DPKCS000
00103      05  FILLER                       PIC X(79) VALUE             DPKCS000
00104          'SECURITY VIOLATION                                   '. DPKCS000
00105      05  FILLER                       PIC X(79) VALUE             DPKCS000
00106          'DB2 CHECKPOINT RESTART SYSTEM TERMINATED             '. DPKCS000
00107  01  PM-PROGRAM-MESSAGE-TABLE   REDEFINES                         DPKCS000
00108                                    PM-PROGRAM-MESSAGE-DATA.       DPKCS000
00109      05  PM-PROGRAM-MESSAGE           OCCURS   5   TIMES          DPKCS000
00110                                 INDEXED BY PM-MESSAGE-INDEX       DPKCS000
00111                                       PIC X(79).                  DPKCS000
00112                                                                   DPKCS000
00113 *                                                                 DPKCS000
00114 *    COMMAREA AND WORKING STORAGE VARIABLES USED IN CICSAUTH      DPKCS000
00115 *                                                                 DPKCS000
00116      COPY DPWS008.                                                DPKCS000
00117  EJECT                                                            DPKCS000
00118 ******************************************************************DPKCS000
00119  LINKAGE SECTION.                                                 DPKCS000
00120 ******************************************************************DPKCS000
00121                                                                   DPKCS000
00122  01  DFHCOMMAREA.                                                 DPKCS000
00123      05  DF-COMMON-DATA.                                          DPKCS000
00124          10  DF-TRANSACTION-STATUS    PIC X.                      DPKCS000
00125              88  DF-INITIALIZE-SCREEN             VALUE 'S'.      DPKCS000
00126              88  DF-PROCESS-SCREEN                VALUE 'R'.      DPKCS000
00127          10  DF-MAIN-MENU             PIC X(8).                   DPKCS000
00128          10  DF-LAST-PROGRAM          PIC X(8).                   DPKCS000
00129          10  FILLER                   PIC X(16).                  DPKCS000
00130                                                                   DPKCS000
00131  01  DF-TWA-ADDRESS-AREA.                                         DPKCS000
00132      05  DF-TWA-TRANID       PIC X(4).                            DPKCS000
00133      05  DF-TWA-AUTHOR       PIC X.                               DPKCS000
00134      05  FILLER              PIC X(95).                           DPKCS000
00135                                                                   DPKCS000
00136  EJECT                                                            DPKCS000
00137 ******************************************************************DPKCS000
00138  PROCEDURE DIVISION.                                              DPKCS000
00139 ******************************************************************DPKCS000
00140                                                                   DPKCS000
00141  0000-MAINLINE.                                                   DPKCS000
00142                                                                   DPKCS000
00143      EVALUATE TRUE                                                DPKCS000
00144          WHEN EIBCALEN = 0                                        DPKCS000
00145              PERFORM 1000-INITIALIZE-AREA                         DPKCS000
00146          WHEN DF-INITIALIZE-SCREEN                                DPKCS000
00147              PERFORM 1000-INITIALIZE-AREA                         DPKCS000
00148          WHEN EIBAID = DFHPF3                                     DPKCS000
00149              PERFORM 9999-RETURN-MAIN-MENU                        DPKCS000
00150          WHEN EIBAID = DFHCLEAR                                   DPKCS000
00151              PERFORM 9999-RETURN-MAIN-MENU                        DPKCS000
00152          WHEN OTHER                                               DPKCS000
00153              PERFORM 2000-EDIT-SCREEN-SELECTOR                    DPKCS000
00154      END-EVALUATE.                                                DPKCS000
00155                                                                   DPKCS000
00156      GOBACK.                                                      DPKCS000
00157                                                                   DPKCS000
00158                                                                   DPKCS000
00159 ******************************************************************DPKCS000
00160 *  INITIALIZE SCREEN FIELDS AND THE COMMON AREA FOR PROCESSING.  *DPKCS000
00161 *  SEND THE MAP AND THE INITIAL DATA.                            *DPKCS000
00162 ******************************************************************DPKCS000
00163                                                                   DPKCS000
00164  1000-INITIALIZE-AREA.                                            DPKCS000
00165      MOVE LOW-VALUES TO DP000AI.                                  DPKCS000
00166      PERFORM 6000-UPDATE-SCREEN-HEADER.                           DPKCS000
00167                                                                   DPKCS000
00168      EXEC CICS                                                    DPKCS000
00169          SEND                                                     DPKCS000
00170              MAP('DP000A')                                        DPKCS000
00171              MAPSET('DPKM000')                                    DPKCS000
00172              ERASE                                                DPKCS000
00173              FORMFEED                                             DPKCS000
00174              RESP(PV-CICS-RESPONSE)                               DPKCS000
00175      END-EXEC.                                                    DPKCS000
00176                                                                   DPKCS000
00177      EVALUATE PV-CICS-RESPONSE                                    DPKCS000
00178          WHEN DFHRESP(NORMAL)                                     DPKCS000
00179              CONTINUE                                             DPKCS000
00180          WHEN OTHER                                               DPKCS000
00181              MOVE 'SEND INITIAL MAP' TO SE-ATTEMPTED-ACTION       DPKCS000
00182              MOVE PV-CICS-RESPONSE TO SE-CICS-RESPONSE            DPKCS000
00183              MOVE SE-SYSTEM-ERROR-MESSAGE TO                      DPKCS000
00184                      PV-LEAVE-SYSTEM-MESSAGE                      DPKCS000
00185              PERFORM 9900-LEAVE-SYSTEM                            DPKCS000
00186      END-EVALUATE.                                                DPKCS000
00187                                                                   DPKCS000
00188      MOVE LOW-VALUES TO CA-COMMON-AREA.                           DPKCS000
00189      SET CA-PROCESS-SCREEN TO TRUE.                               DPKCS000
00190      MOVE 'DPKCS000' TO CA-MAIN-MENU.                             DPKCS000
00191      EXEC CICS                                                    DPKCS000
00192          RETURN                                                   DPKCS000
00193              TRANSID('Z000')                                      DPKCS000
00194              COMMAREA(CA-COMMON-AREA)                             DPKCS000
00195              LENGTH(PC-COMMON-AREA-LENG)                          DPKCS000
00196              RESP(PV-CICS-RESPONSE)                               DPKCS000
00197      END-EXEC.                                                    DPKCS000
00198                                                                   DPKCS000
00199      EVALUATE PV-CICS-RESPONSE                                    DPKCS000
00200          WHEN DFHRESP(NORMAL)                                     DPKCS000
00201              CONTINUE                                             DPKCS000
00202          WHEN OTHER                                               DPKCS000
00203              MOVE 'RETURN AFTER INITIAL SEND' TO                  DPKCS000
00204                          SE-ATTEMPTED-ACTION                      DPKCS000
00205              MOVE PV-CICS-RESPONSE TO SE-CICS-RESPONSE            DPKCS000
00206              MOVE SE-SYSTEM-ERROR-MESSAGE TO                      DPKCS000
00207                      PV-LEAVE-SYSTEM-MESSAGE                      DPKCS000
00208              PERFORM 9900-LEAVE-SYSTEM                            DPKCS000
00209      END-EVALUATE.                                                DPKCS000
00210 *1000-EXIT.        EXIT.                                          DPKCS000
00211                                                                   DPKCS000
00212                                                                   DPKCS000
00213 ******************************************************************DPKCS000
00214 *  PROCESS INFORMATION ENTERED ON THE SCREEN, BY RECEIVING THE   *DPKCS000
00215 *  SCREEN FIELDS, EDITING THE SELECTOR FIELD, AND EITHER         *DPKCS000
00216 *  DISPLAYING ERRORS, OR COMPLETE THE REQUESTED FUNCTION.        *DPKCS000
00217 ******************************************************************DPKCS000
00218                                                                   DPKCS000
00219  2000-EDIT-SCREEN-SELECTOR.                                       DPKCS000
00220                                                                   DPKCS000
00221      IF EIBAID NOT = DFHENTER                                     DPKCS000
00222          SET PM-MESSAGE-INDEX TO 2                                DPKCS000
00223          MOVE PM-PROGRAM-MESSAGE (PM-MESSAGE-INDEX) TO AMSGO      DPKCS000
00224          PERFORM 5000-RETURN-TO-MENU                              DPKCS000
00225      END-IF.                                                      DPKCS000
00226                                                                   DPKCS000
00227      MOVE LOW-VALUES TO DP000AI.                                  DPKCS000
00228                                                                   DPKCS000
00229      EXEC CICS                                                    DPKCS000
00230          RECEIVE                                                  DPKCS000
00231              MAP('DP000A')                                        DPKCS000
00232              MAPSET('DPKM000')                                    DPKCS000
00233              RESP(PV-CICS-RESPONSE)                               DPKCS000
00234      END-EXEC.                                                    DPKCS000
00235                                                                   DPKCS000
00236      EVALUATE PV-CICS-RESPONSE                                    DPKCS000
00237          WHEN DFHRESP(NORMAL)                                     DPKCS000
00238              CONTINUE                                             DPKCS000
00239          WHEN OTHER                                               DPKCS000
00240              MOVE 'RECEIVE THE MAP' TO SE-ATTEMPTED-ACTION        DPKCS000
00241              MOVE PV-CICS-RESPONSE TO SE-CICS-RESPONSE            DPKCS000
00242              MOVE SE-SYSTEM-ERROR-MESSAGE TO                      DPKCS000
00243                      PV-LEAVE-SYSTEM-MESSAGE                      DPKCS000
00244              PERFORM 9900-LEAVE-SYSTEM                            DPKCS000
00245      END-EVALUATE.                                                DPKCS000
00246                                                                   DPKCS000
00247      EVALUATE TRUE                                                DPKCS000
00248          WHEN ASELTRI = '1'                                       DPKCS000
00249              PERFORM 4000-XCTL-MAINTENANCE                        DPKCS000
00250          WHEN ASELTRI = '2'                                       DPKCS000
00251              PERFORM 3000-XCTL-INQUIRY                            DPKCS000
00252          WHEN OTHER                                               DPKCS000
00253              SET PM-MESSAGE-INDEX TO 3                            DPKCS000
00254              MOVE PM-PROGRAM-MESSAGE (PM-MESSAGE-INDEX) TO AMSGO  DPKCS000
00255              PERFORM 5000-RETURN-TO-MENU                          DPKCS000
00256      END-EVALUATE.                                                DPKCS000
00257 *2000-EXIT.        EXIT.                                          DPKCS000
00258                                                                   DPKCS000
00259                                                                   DPKCS000
00260 ******************************************************************DPKCS000
00261 *  PROGRAM PASSES CONTROL TO THE INQUIRY PROGRAM.                *DPKCS000
00262 ******************************************************************DPKCS000
00263                                                                   DPKCS000
00264  3000-XCTL-INQUIRY.                                               DPKCS000
00265      MOVE 'DPKCS000' TO CA-LAST-PROGRAM.                          DPKCS000
00266      SET CA-INITIALIZE-SCREEN TO TRUE.                            DPKCS000
00267                                                                   DPKCS000
00268      EXEC CICS XCTL                                               DPKCS000
00269                PROGRAM(PC-INQUIRY-PROGRAM)                        DPKCS000
00270                COMMAREA(CA-COMMON-AREA)                           DPKCS000
00271                LENGTH(PC-COMMON-AREA-LENG)                        DPKCS000
00272                RESP(PV-CICS-RESPONSE)                             DPKCS000
00273      END-EXEC.                                                    DPKCS000
00274                                                                   DPKCS000
00275      EVALUATE PV-CICS-RESPONSE                                    DPKCS000
00276          WHEN DFHRESP(NORMAL)                                     DPKCS000
00277              CONTINUE                                             DPKCS000
00278          WHEN OTHER                                               DPKCS000
00279              STRING 'XCTL TO INQUIRY  ' DELIMITED BY SIZE         DPKCS000
00280                      PC-INQUIRY-PROGRAM DELIMITED BY SIZE         DPKCS000
00281                  INTO SE-ATTEMPTED-ACTION                         DPKCS000
00282              MOVE PV-CICS-RESPONSE TO SE-CICS-RESPONSE            DPKCS000
00283              MOVE SE-SYSTEM-ERROR-MESSAGE TO                      DPKCS000
00284                  PV-LEAVE-SYSTEM-MESSAGE                          DPKCS000
00285          PERFORM 9900-LEAVE-SYSTEM                                DPKCS000
00286      END-EVALUATE.                                                DPKCS000
00287 *3000-EXIT.        EXIT.                                          DPKCS000
00288                                                                   DPKCS000
00289                                                                   DPKCS000
00290 ******************************************************************DPKCS000
00291 *  LINK TO 'RCHKCICS' TO CHECK FOR ACCESS AUTHORITY OF USER,     *DPKCS000
00292 *  BEFORE XCTL TO MAINTENANCE PROGRAM.                           *DPKCS000
00293 ******************************************************************DPKCS000
00294                                                                   DPKCS000
00295  4000-XCTL-MAINTENANCE.                                           DPKCS000
00296                                                                   DPKCS000
00297 *    EXEC CICS ADDRESS                                            DPKCS000
00298 *              TWA(ADDRESS OF DF-TWA-ADDRESS-AREA)                DPKCS000
00299 *    END-EXEC.                                                    DPKCS000
00300 *                                                                 DPKCS000
00301 *    MOVE 'Z004' TO DF-TWA-TRANID.                                DPKCS000
00302 *                                                                 DPKCS000
00303 *    EXEC CICS LINK                                               DPKCS000
00304 *              PROGRAM(PC-RACF-PROGRAM)                           DPKCS000
00305 *              RESP(PV-CICS-RESPONSE)                             DPKCS000
00306 *    END-EXEC.                                                    DPKCS000
00307 *                                                                 DPKCS000
00308 *    EVALUATE PV-CICS-RESPONSE                                    DPKCS000
00309 *        WHEN DFHRESP(NORMAL)                                     DPKCS000
00310 *            CONTINUE                                             DPKCS000
00311 *        WHEN OTHER                                               DPKCS000
00312 *            STRING 'LINK TO RACF ' DELIMITED BY SIZE             DPKCS000
00313 *                    PC-RACF-PROGRAM DELIMITED BY SIZE            DPKCS000
00314 *                INTO SE-ATTEMPTED-ACTION                         DPKCS000
00315 *            MOVE PV-CICS-RESPONSE TO SE-CICS-RESPONSE            DPKCS000
00316 *            MOVE SE-SYSTEM-ERROR-MESSAGE TO                      DPKCS000
00317 *                PV-LEAVE-SYSTEM-MESSAGE                          DPKCS000
00318 *            PERFORM 9900-LEAVE-SYSTEM                            DPKCS000
00319 *    END-EVALUATE.                                                DPKCS000
00320                                                                   DPKCS000
00321 *    EVALUATE TRUE                                                DPKCS000
00322 *        WHEN DF-TWA-AUTHOR = LOW-VALUES                          DPKCS000
00323 *            CONTINUE                                             DPKCS000
00324 *        WHEN OTHER                                               DPKCS000
00325 *            SET PM-MESSAGE-INDEX TO 4                            DPKCS000
00326 *            MOVE PM-PROGRAM-MESSAGE (PM-MESSAGE-INDEX) TO AMSGO  DPKCS000
00327 *            PERFORM 5000-RETURN-TO-MENU                          DPKCS000
00328 *    END-EVALUATE.                                                DPKCS000
00329                                                                   DPKCS000
00330      SET DP008-RSRC-TYPE-TRAN  TO  TRUE.                          DPKCS000
00331      SET DP008-ACCESS-TYPE-READ                                      CL**3
00332                                TO  TRUE.                             CL**2
00333      MOVE 'Z004'               TO  DP008-RESOURCE-NAME.           DPKCS000
00334                                                                   DPKCS000
00335      EXEC CICS                                                    DPKCS000
00336           LINK PROGRAM  (DP008-SECURITY-SUBROUTINE)               DPKCS000
00337                COMMAREA (DP008-SECURITY-COMMAREA)                 DPKCS000
00338                LENGTH   (DP008-SECURITY-COMMAREA-LENGTH)          DPKCS000
00339                RESP     (PV-CICS-RESPONSE)                        DPKCS000
00340      END-EXEC.                                                    DPKCS000
00341                                                                   DPKCS000
00342      IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                   DPKCS000
00343          IF  DP008-CALL-SUCCESSFUL                                   CL**2
00344              IF  DP008-ACCESS-ALLOWED                                CL**2
00345                  CONTINUE                                            CL**2
00346              ELSE                                                    CL**2
00347                  SET PM-MESSAGE-INDEX TO 4                           CL**2
00348                  MOVE PM-PROGRAM-MESSAGE (PM-MESSAGE-INDEX)          CL**2
00349                                       TO AMSGO                       CL**2
00350                  PERFORM 5000-RETURN-TO-MENU                         CL**2
00351              END-IF                                                  CL**2
00352          ELSE                                                        CL**2
00353              STRING '4000-XCLT-MNT BAD RTCD ' DELIMITED BY SIZE      CL**2
00354                      PC-RACF-PROGRAM DELIMITED BY SIZE               CL**2
00355                  INTO SE-ATTEMPTED-ACTION                            CL**2
00356              MOVE DP008-ERROR-CODE TO SE-CICS-RESPONSE               CL**2
00357              MOVE SE-SYSTEM-ERROR-MESSAGE TO                         CL**2
00358                  PV-LEAVE-SYSTEM-MESSAGE                             CL**2
00359              PERFORM 9900-LEAVE-SYSTEM                               CL**2
00360          END-IF                                                      CL**2
00361      ELSE                                                            CL**2
00362          STRING '4000-XCLT-MNT ' DELIMITED BY SIZE                   CL**2
00363                  PC-RACF-PROGRAM DELIMITED BY SIZE                   CL**2
00364              INTO SE-ATTEMPTED-ACTION                                CL**2
00365          MOVE PV-CICS-RESPONSE TO SE-CICS-RESPONSE                   CL**2
00366          MOVE SE-SYSTEM-ERROR-MESSAGE TO                             CL**2
00367              PV-LEAVE-SYSTEM-MESSAGE                                 CL**2
00368          PERFORM 9900-LEAVE-SYSTEM                                   CL**2
00369      END-IF.                                                      DPKCS000
00370                                                                   DPKCS000
00371      MOVE 'DPKCS000' TO CA-LAST-PROGRAM.                          DPKCS000
00372      SET CA-INITIALIZE-SCREEN TO TRUE.                            DPKCS000
00373                                                                   DPKCS000
00374      EXEC CICS XCTL                                               DPKCS000
00375                PROGRAM(PC-MAINTENANCE-PROGRAM)                    DPKCS000
00376                COMMAREA(CA-COMMON-AREA)                           DPKCS000
00377                LENGTH(PC-COMMON-AREA-LENG)                        DPKCS000
00378                RESP(PV-CICS-RESPONSE)                             DPKCS000
00379      END-EXEC.                                                    DPKCS000
00380                                                                   DPKCS000
00381      EVALUATE PV-CICS-RESPONSE                                    DPKCS000
00382          WHEN DFHRESP(NORMAL)                                     DPKCS000
00383              CONTINUE                                             DPKCS000
00384          WHEN OTHER                                               DPKCS000
00385              STRING 'XCTL TO MAINTENANCE' DELIMITED BY SIZE       DPKCS000
00386                      PC-MAINTENANCE-PROGRAM DELIMITED BY SIZE     DPKCS000
00387                  INTO SE-ATTEMPTED-ACTION                         DPKCS000
00388              MOVE PV-CICS-RESPONSE TO SE-CICS-RESPONSE            DPKCS000
00389              MOVE SE-SYSTEM-ERROR-MESSAGE TO                      DPKCS000
00390                  PV-LEAVE-SYSTEM-MESSAGE                          DPKCS000
00391              PERFORM 9900-LEAVE-SYSTEM                            DPKCS000
00392      END-EVALUATE.                                                DPKCS000
00393 *4000-EXIT.        EXIT.                                          DPKCS000
00394                                                                   DPKCS000
00395                                                                   DPKCS000
00396 ******************************************************************DPKCS000
00397 *  SEND MAP AND RETURN TO MENU.                                  *DPKCS000
00398 ******************************************************************DPKCS000
00399                                                                   DPKCS000
00400  5000-RETURN-TO-MENU.                                             DPKCS000
00401                                                                   DPKCS000
00402      MOVE DFHCOMMAREA TO CA-COMMON-AREA.                          DPKCS000
00403                                                                   DPKCS000
00404      PERFORM 6000-UPDATE-SCREEN-HEADER.                           DPKCS000
00405                                                                   DPKCS000
00406      EXEC CICS                                                    DPKCS000
00407          SEND                                                     DPKCS000
00408              MAP('DP000A')                                        DPKCS000
00409              MAPSET('DPKM000')                                    DPKCS000
00410              ERASE                                                DPKCS000
00411              FORMFEED                                             DPKCS000
00412              RESP(PV-CICS-RESPONSE)                               DPKCS000
00413      END-EXEC.                                                    DPKCS000
00414                                                                   DPKCS000
00415      EVALUATE PV-CICS-RESPONSE                                    DPKCS000
00416          WHEN DFHRESP(NORMAL)                                     DPKCS000
00417              CONTINUE                                             DPKCS000
00418          WHEN OTHER                                               DPKCS000
00419              MOVE 'SEND ERROR MENU' TO SE-ATTEMPTED-ACTION        DPKCS000
00420              MOVE PV-CICS-RESPONSE TO SE-CICS-RESPONSE            DPKCS000
00421              MOVE SE-SYSTEM-ERROR-MESSAGE TO                      DPKCS000
00422                      PV-LEAVE-SYSTEM-MESSAGE                      DPKCS000
00423              PERFORM 9900-LEAVE-SYSTEM                            DPKCS000
00424      END-EVALUATE.                                                DPKCS000
00425                                                                   DPKCS000
00426      EXEC CICS                                                    DPKCS000
00427          RETURN                                                   DPKCS000
00428              TRANSID('Z000')                                      DPKCS000
00429              COMMAREA(CA-COMMON-AREA)                             DPKCS000
00430              LENGTH(PC-COMMON-AREA-LENG)                          DPKCS000
00431              RESP(PV-CICS-RESPONSE)                               DPKCS000
00432      END-EXEC.                                                    DPKCS000
00433                                                                   DPKCS000
00434      EVALUATE PV-CICS-RESPONSE                                    DPKCS000
00435          WHEN DFHRESP(NORMAL)                                     DPKCS000
00436              CONTINUE                                             DPKCS000
00437          WHEN OTHER                                               DPKCS000
00438              MOVE 'RETURN AFTER ERROR SEND' TO                    DPKCS000
00439                          SE-ATTEMPTED-ACTION                      DPKCS000
00440              MOVE PV-CICS-RESPONSE TO SE-CICS-RESPONSE            DPKCS000
00441              MOVE SE-SYSTEM-ERROR-MESSAGE TO                      DPKCS000
00442                      PV-LEAVE-SYSTEM-MESSAGE                      DPKCS000
00443              PERFORM 9900-LEAVE-SYSTEM                            DPKCS000
00444      END-EVALUATE.                                                DPKCS000
00445 *5000-EXIT.        EXIT.                                          DPKCS000
00446                                                                   DPKCS000
00447                                                                   DPKCS000
00448 ******************************************************************DPKCS000
00449 *  FILL IN THE SCREEN HEADER WITH THE DATE, TIME, AND TERMINAL   *DPKCS000
00450 *  NUMBER.                                                       *DPKCS000
00451 ******************************************************************DPKCS000
00452                                                                   DPKCS000
00453  6000-UPDATE-SCREEN-HEADER.                                       DPKCS000
00454                                                                   DPKCS000
00455      EXEC CICS                                                    DPKCS000
00456          ASKTIME                                                  DPKCS000
00457              ABSTIME(ST-ABSTIME)                                  DPKCS000
00458      END-EXEC.                                                    DPKCS000
00459                                                                   DPKCS000
00460      EXEC CICS                                                    DPKCS000
00461          FORMATTIME                                               DPKCS000
00462              ABSTIME(ST-ABSTIME)                                  DPKCS000
00463              MMDDYY(ADATEO)                                       DPKCS000
00464              DATESEP                                              DPKCS000
00465              TIME(ST-TIME-EDIT)                                   DPKCS000
00466              TIMESEP                                              DPKCS000
00467      END-EXEC.                                                    DPKCS000
00468                                                                   DPKCS000
00469      MOVE ST-TIME-HHMM TO ATIMEO.                                 DPKCS000
00470      MOVE EIBTRMID TO ATERMO.                                     DPKCS000
00471 *6000-EXIT.        EXIT.                                          DPKCS000
00472                                                                   DPKCS000
00473                                                                   DPKCS000
00474 ******************************************************************DPKCS000
00475 *  SEND UNFORMATTED ERROR MESSAGE THEN EXIT CICS.                *DPKCS000
00476 ******************************************************************DPKCS000
00477                                                                   DPKCS000
00478  9900-LEAVE-SYSTEM.                                               DPKCS000
00479                                                                   DPKCS000
00480      EXEC CICS SEND                                               DPKCS000
00481                FROM(PV-LEAVE-SYSTEM-MESSAGE)                      DPKCS000
00482                LENGTH(PC-LEAVE-SYSTEM-LENG)                       DPKCS000
00483                ERASE                                              DPKCS000
00484      END-EXEC.                                                    DPKCS000
00485                                                                   DPKCS000
00486      EXEC CICS                                                    DPKCS000
00487          RETURN                                                   DPKCS000
00488      END-EXEC.                                                    DPKCS000
00489 *9900-EXIT.        EXIT.                                          DPKCS000
00490 ******************************************************************DPKCS000
00491 *  EXIT PROGRAM AND RETURN TO CICS.                              *DPKCS000
00492 ******************************************************************DPKCS000
00493                                                                   DPKCS000
00494                                                                   DPKCS000
00495  9999-RETURN-MAIN-MENU.                                           DPKCS000
00496                                                                   DPKCS000
00497      SET PM-MESSAGE-INDEX TO 5.                                   DPKCS000
00498      MOVE PM-PROGRAM-MESSAGE (PM-MESSAGE-INDEX) TO                DPKCS000
00499                                  PV-LEAVE-SYSTEM-MESSAGE.         DPKCS000
00500                                                                   DPKCS000
00501      EXEC CICS SEND                                               DPKCS000
00502                FROM(PV-LEAVE-SYSTEM-MESSAGE)                      DPKCS000
00503                LENGTH(PC-LEAVE-SYSTEM-LENG)                       DPKCS000
00504                ERASE                                              DPKCS000
00505      END-EXEC.                                                    DPKCS000
00506      EXEC CICS                                                    DPKCS000
00507          START                                                    DPKCS000
00508          TRANSID('MENU')                                          DPKCS000
00509          TERMID(EIBTRMID)                                         DPKCS000
00510      END-EXEC.                                                    DPKCS000
00511                                                                   DPKCS000
00512      EXEC CICS                                                    DPKCS000
00513          RETURN                                                   DPKCS000
00514      END-EXEC.                                                    DPKCS000
00515 *9999-EXIT.        EXIT.                                          DPKCS000
