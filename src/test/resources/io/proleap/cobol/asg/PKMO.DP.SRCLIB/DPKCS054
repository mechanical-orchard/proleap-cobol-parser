000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    DPKCS054.                                                 
000500 AUTHOR.        CHERI MASTEL - COMPUWARE.                                 
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  05-17-96.                                                 
000800 DATE-COMPILED.                                                           
000900*----------------------------------------------------------------*        
001000*    Z054 - DELETE PHYSICAL UNIT.                                *        
001100*                                                                *        
001200*    THIS PROGRAM ALLOWS THE USER TO DELETE A PHYSICAL UNIT,     *        
001300*    ON THE DB2 TABLE TPHYUNT.                                   *        
001400*                                                                *        
001500*----------------------------------------------------------------*        
      *    12/22/03  TKMA464   MADE CHANGES FOR EXPANDING LOCATION     *        
      *                        FROM 3 TO 5 DIGITS.                     *        
      *----------------------------------------------------------------*        
001600                                                                          
001700                                                                          
001800 ENVIRONMENT DIVISION.                                                    
002100 DATA DIVISION.                                                           
002300                                                                          
002400 WORKING-STORAGE SECTION.                                                 
002500                                                                          
002600 01  DK-DB2-KEYS.                                                         
003100     15  DK-LOC-NBR                     PIC S9(04)  VALUE ZERO            
003200                                                    COMP.                 
003300     15  DK-FCLTY.                                                        
003400         20  DK-FCLTY-N                 PIC  9(03)  VALUE ZERO.           
003500     15  DK-FCLTY-X                     PIC  X(03)  VALUE SPACE.          
003600     15  DK-FCLTY-N4-COMP               PIC S9(04)  VALUE ZERO            
003700                                                    COMP.                 
003800     15  DK-VTAM.                                                         
003900         20  DK-LNE-ADDR-NBR            PIC  X(02)  VALUE SPACE.          
004000         20  DK-PHYUNT-ADDR-NBR         PIC  X(02)  VALUE SPACE.          
004100     15  DK-IP-ADDR-BEG                 PIC  X(15)  VALUE SPACE.          
004200     15  DK-IP-ADDR-END                 PIC  X(15)  VALUE SPACE.          
004300                                                                          
004400 01  PC-PROGRAM-CONSTANTS.                                                
004500     05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'DP054A  '.          
004600     05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'DPKM054 '.          
004700     05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'DPKCS054'.          
004800     05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3                   
004900                                              COMP.                       
005000     05  PC-DOT                   PIC  X(01)  VALUE  '.'.                 
005200     05  PC-TSYMSG-NUMBERS.                                               
005300         10  PC-TSYMSG-00005      PIC  9(05)  VALUE  00005.               
005400         10  PC-TSYMSG-00007      PIC  9(05)  VALUE  00007.               
005500         10  PC-TSYMSG-00008      PIC  9(05)  VALUE  00008.               
005600         10  PC-TSYMSG-00017      PIC  9(05)  VALUE  00017.               
005700         10  PC-TSYMSG-00052      PIC  9(05)  VALUE  00052.               
005800         10  PC-TSYMSG-00059      PIC  9(05)  VALUE  00059.               
005900         10  PC-TSYMSG-00068      PIC  9(05)  VALUE  00068.               
006000         10  PC-TSYMSG-00069      PIC  9(05)  VALUE  00069.               
006100         10  PC-TSYMSG-00070      PIC  9(05)  VALUE  00070.               
006200         10  PC-TSYMSG-00115      PIC  9(05)  VALUE  00115.               
006300         10  PC-TSYMSG-00148      PIC  9(05)  VALUE  00148.               
006400         10  PC-TSYMSG-00223      PIC  9(05)  VALUE  00223.               
006500         10  PC-TSYMSG-00224      PIC  9(05)  VALUE  00224.               
006600         10  PC-TSYMSG-00354      PIC  9(05)  VALUE  00354.               
006700         10  PC-TSYMSG-00388      PIC  9(05)  VALUE  00388.               
006800         10  PC-TSYMSG-01176      PIC  9(05)  VALUE  01176.               
006900         10  PC-TSYMSG-01177      PIC  9(05)  VALUE  01177.               
007000         10  PC-TSYMSG-01282      PIC  9(05)  VALUE  01282.               
007100         10  PC-TSYMSG-01283      PIC  9(05)  VALUE  01283.               
007200         10  PC-TSYMSG-01285      PIC  9(05)  VALUE  01285.               
007300     05  PC-VALID-OCTET-MIN       PIC  X(03)  VALUE  '000'.               
007400     05  PC-VALID-OCTET-MAX       PIC  X(03)  VALUE  '255'.               
007500     05  PC-VALIDATE-PHYU1        PIC  X(01)  VALUE  SPACE.               
007600         88  PC-VALID-HEX-CHAR1               VALUE  '0', '1',            
007700                                                     '2', '3',            
007800                                                     '4', '5',            
007900                                                     '6', '7',            
008000                                                     '8', '9',            
008100                                                     'A', 'B',            
008200                                                     'C', 'D',            
008300                                                     'E', 'F'.            
008400     05  PC-VALIDATE-PHYU2        PIC  X(01)  VALUE  SPACE.               
008500         88  PC-VALID-HEX-CHAR2               VALUE  '0', '1',            
008600                                                     '2', '3',            
008700                                                     '4', '5',            
008800                                                     '6', '7',            
008900                                                     '8', '9',            
009000                                                     'A', 'B',            
009100                                                     'C', 'D',            
009200                                                     'E', 'F'.            
009300     05  PC-LAST-ACTION.                                                  
009400         10  PC-LAST-ACTION-DEL   PIC  X(03)  VALUE  'DEL'.               
009500         10  PC-LAST-ACTION-UPD   PIC  X(03)  VALUE  'UPD'.               
009600         10  PC-LAST-ACTION-INQ   PIC  X(03)  VALUE  'INQ'.               
009700         10  PC-LAST-ACTION-ERR   PIC  X(03)  VALUE  'ERR'.               
009800                                                                          
009900 01  PM-PROGRAM-MESSAGES.                                                 
010000     05  PM-ABEND-INFO-A.                                                 
010100         10  FILLER                PIC X(15)   VALUE                      
010200                                   'DP020-SRC-AID: '.                     
010300         10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.              
010400         10  FILLER                PIC X(19)   VALUE                      
010500                                   '. DP020-FK-ACTION: '.                 
010600         10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.              
010700         10  FILLER                PIC X(22)   VALUE                      
010800                                   '. DP020-FK-DISPN-IND: '.              
010900         10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.              
011000         10  FILLER                PIC X(01)   VALUE '.'.                 
011100         10  FILLER                PIC X(19)   VALUE SPACE.               
011200                                                                          
011300     05  PM-ABEND-KEYFIELDS.                                              
011400         10  FILLER                PIC X(14)   VALUE                      
011500                                   'LNE-ADDR-NBR: '.                      
011600         10  PM-ABEND-KEYFIELD1    PIC X(02)   VALUE  SPACE.              
011700         10  FILLER                PIC X(02)   VALUE  ', '.               
011800         10  FILLER                PIC X(17)   VALUE                      
011900                                   'PHYUNT-ADDR-NBR: '.                   
012000         10  PM-ABEND-KEYFIELD2    PIC X(02)   VALUE  SPACE.              
012100         10  FILLER                PIC X(01)   VALUE  '.'.                
012200                                                                          
012300     05  PM-ABEND-RANGES.                                                 
012400         10  FILLER                PIC X(17)   VALUE                      
012500                                   'BEG-RNG-IP-ADDR: '.                   
012600         10  PM-ABEND-RANGE-B      PIC X(15)   VALUE  SPACE.              
012700         10  FILLER                PIC X(02)   VALUE  ', '.               
012800         10  FILLER                PIC X(17)   VALUE                      
012900                                   'END-RNG-IP-ADDR: '.                   
013000         10  PM-ABEND-RANGE-E      PIC X(15)   VALUE  SPACE.              
013100         10  FILLER                PIC X(01)   VALUE  '.'.                
013200                                                                          
013300     05  PM-SELQ-ABEND-MSG.                                               
013400         10  FILLER                     PIC X(06)   VALUE                 
013500                                                    'TS Q: '.             
013600         10  PM-SELQ-NAME.                                                
013700             15  PM-SELQ-TERM-ID        PIC X(04).                        
013800             15  PM-SELQ-TASK           PIC 9(05).                        
013900             15  PM-SELQ-SEQ            PIC 9(01).                        
014000         10  PM-SELQ-DESC               PIC X(08)   VALUE                 
014100                                                    ', ITEM: '.           
014200         10  PM-SELQ-ITEM               PIC 9(04)   VALUE ZERO.           
014300         10  FILLER                     PIC X(10)   VALUE                 
014400                                                    ', LENGTH: '.         
014500         10  PM-SELQ-LENGTH             PIC 9(04)   VALUE ZERO.           
014600         10  FILLER                     PIC X(01)   VALUE '.'.            
014700                                                                          
014800 01  PS-PROGRAM-SWITCHES.                                                 
014900     05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.                 
015000         88  PS-ERROR                         VALUE  'Y'.                 
015100         88  PS-NO-ERROR                      VALUE  'N'.                 
015200                                                                          
015300 01  PV-PROGRAM-VARIABLES.                                                
015400     05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0                    
015500                                              COMP SYNC.                  
015600     05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0                    
015700                                              COMP SYNC.                  
015800     05  PV-MSG-VAR-CNT           PIC S9(04)  VALUE +0                    
015900                                              COMP SYNC.                  
016000     05  PV-COUNT-OVERLAP-IP-ADDR PIC S9(09)  VALUE +0                    
016100                                              COMP SYNC.                  
016200     05  PV-COUNT-DUPLICATE-VTAM  PIC S9(09)  VALUE +0                    
016300                                              COMP SYNC.                  
016400     05  PV-SELQ-LENGTH           PIC S9(4)   VALUE +0                    
016500                                              COMP SYNC.                  
016600                                                                          
016700*---------------------------------------------------------------**        
016800*  THE LAYOUT FOR THE TEMPORARY STORAGE QUEUE CONTAINING        **        
016900*  SELECTED LIST ITEMS TO BE PROCESSED BY THIS PROGRAM.         **        
017000*---------------------------------------------------------------**        
017100     COPY DPWS252.                                                        
017200                                                                          
017300                                                                          
017400*----------------------------------------------------------------*        
017500*    MAP LAYOUT                                                  *        
017600*----------------------------------------------------------------*        
017700     COPY DPKM054.                                                        
017800                                                                          
017900*----------------------------------------------------------------*        
018000*    ATTRIBUTE SETTINGS COPYBOOK.                                *        
018100*----------------------------------------------------------------*        
018200     COPY DPWS015.                                                        
018300                                                                          
018400*----------------------------------------------------------------*        
018500*    FUNCTION KEYS COPYBOOK                                      *        
018600*----------------------------------------------------------------*        
018700     COPY DPWS016.                                                        
018800                                                                          
      *----------------------------------------------------------------*        
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.           *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
019300                                                                          
019400*****************************************************************         
019500*    ARCHITECTURE COMMAREAS: 4072 MAX LENGTH TOTAL                        
019600*    * GLOBAL COMMAREA:      1265 BYTES, COPY BOOK DPWS020                
019700*    * INTER-APPLICATION:     200 BYTES, TEMPORARY!                       
019800*    * APPLICATION SPECIFIC: 2865 BYTES MAX                               
019900*****************************************************************         
020000                                                                          
020100*****************************************************************         
020200*    STANDARD COMMAREA.                                                   
020300*****************************************************************         
020400     COPY DPWS020.                                                        
020500     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
020600                                                                          
020700*****************************************************************         
020800*    INTER-APPLICATION COMMAREA.                                          
020900*****************************************************************         
021000         10  INTER-APPL-COMM-AREA PIC X(200).                             
021100     COPY DPWS250.                                                        
021200     COPY DPWS251.                                                        
021300                                                                          
021400*****************************************************************         
021500*    SPECIFIC COMMMAREA FOR DPKCS054.                                     
021600*****************************************************************         
021700         10  ASC-SPECIFIC-COMMAREA.                                       
021800*            ****************************************************         
021900*            * SCREEN FIELDS:                                             
022000*            ****************************************************         
022100             15  ASC-SCREEN.                                              
022110                 20  ASC-LOCATION.                                        
022200                     25  ASC-STORE.                                       
022300                         30  ASC-STORE-N     PIC  9(05).                  
022400                     25  ASC-FCLTY.                                       
022500                         30  ASC-FCLTY-N     PIC  9(04).                  
022600                 20  ASC-VTAM.                                            
022700                     25  ASC-LINE.                                        
022800                         30  ASC-LINE1       PIC  X(01).                  
022900                         30  ASC-LINE2       PIC  X(01).                  
023000                     25  ASC-PHYSICAL-UNIT.                               
023100                         30  ASC-PHYSICAL-UNIT1                           
023110                                             PIC  X(01).                  
023200                         30  ASC-PHYSICAL-UNIT2                           
023210                                             PIC  X(01).                  
023300                 20  ASC-IP-ADDR-RANGE.                                   
023400                     25  ASC-IP-ADDR-BEGIN.                               
023500                         30  ASC-IP-ADDR-B-OCTET1                         
023600                                             PIC  X(03).                  
023700                         30  ASC-IP-ADDR-B-DOT1                           
023710                                             PIC  X(01).                  
023800                         30  ASC-IP-ADDR-B-OCTET2                         
023900                                             PIC  X(03).                  
024000                         30  ASC-IP-ADDR-B-DOT2                           
024010                                             PIC  X(01).                  
024100                         30  ASC-IP-ADDR-B-OCTET3                         
024200                                             PIC  X(03).                  
024300                         30  ASC-IP-ADDR-B-DOT3                           
024310                                             PIC  X(01).                  
024400                         30  ASC-IP-ADDR-B-OCTET4                         
024500                                             PIC  X(03).                  
024600                     25  ASC-IP-ADDR-END.                                 
024700                         30  ASC-IP-ADDR-E-OCTET1                         
024800                                             PIC  X(03).                  
024900                         30  ASC-IP-ADDR-E-DOT1                           
024910                                             PIC  X(01).                  
025000                         30  ASC-IP-ADDR-E-OCTET2                         
025100                                             PIC  X(03).                  
025200                         30  ASC-IP-ADDR-E-DOT2                           
025210                                             PIC  X(01).                  
025300                         30  ASC-IP-ADDR-E-OCTET3                         
025400                                             PIC  X(03).                  
025500                         30  ASC-IP-ADDR-E-DOT3                           
025510                                             PIC  X(01).                  
025600                         30  ASC-IP-ADDR-E-OCTET4                         
025700                                             PIC  X(03).                  
025800*            ****************************************************         
025900*            * CURRENT RECORD, BEFORE UPDATES:                            
026000*            ****************************************************         
026100             15  ASC-DKC-CURRENT-ROW-DB2-KEYS.                            
026700                 20  ASC-DKC-LOC-NBR         PIC S9(04)     COMP.         
026800                 20  ASC-DKC-FCLTY.                                       
026900                     25  ASC-DKC-FCLTY-N                                  
027000                                             PIC  9(03).                  
027100                 20  ASC-DKC-FCLTY-X         PIC  X(03).                  
027200                 20  ASC-DKC-FCLTY-N4-COMP                                
027300                                             PIC S9(04)     COMP.         
027400                 20  ASC-DKC-VTAM.                                        
027500                     25  ASC-DKC-LNE-ADDR-NBR                             
027600                                             PIC  X(02).                  
027700                     25  ASC-DKC-PHYUNT-ADDR-NBR                          
027800                                             PIC  X(02).                  
027900                 20  ASC-DKC-IP-ADDR-BEG     PIC  X(15).                  
028000                 20  ASC-DKC-IP-ADDR-END     PIC  X(15).                  
028100                                                                          
028200*            ****************************************************         
028300*            *   TEMPORARY STORAGE QUEUE                                  
028400*            ****************************************************         
028500             15  ASC-TSQ.                                                 
028510                 20  ASC-USE-SELECTION-QUEUE-IND                          
028600                                           PIC  X(01).                    
028700                     88  ASC-USE-SELECTION-QUEUE       VALUE 'Y'.         
028800                 20  ASC-SEL-QUEUE-NAME    PIC  X(08).                    
028900                 20  ASC-NUMBER-OF-TS-ITEMS                               
028910                                           PIC S9(05)  COMP-3.            
029000                 20  ASC-NUMBER-OF-SELECTED-ITEMS                         
029100                                           PIC S9(09)  COMP-3.            
029200                 20  ASC-SEL-SUB           PIC S9(04)  COMP.              
029300                 20  ASC-SEL-ITEM          PIC S9(04)  COMP.              
029400         10  FILLER                    PIC X(2749).                       
029500                                                                          
029600*----------------------------------------------------------------*        
029700*    ABEND PROCESSING COPYBOOK.                                  *        
029800*----------------------------------------------------------------*        
029900     COPY DPWS013.                                                        
030000                                                                          
030900******************************************************************        
031000*    DB2 AREA AND COBOL DECLARATIONS FOR TABLE:                  *        
031100*    TPHYUNT (PHYSICAL UNIT TABLE)                               *        
031200******************************************************************        
031300     EXEC SQL                                                             
031400          INCLUDE TPHYUNT                                                 
031500     END-EXEC.                                                            
031600                                                                          
031700******************************************************************        
031800*    DB2 AREA FOR COMMUNICATIONS:                                *        
031900******************************************************************        
032000     EXEC SQL                                                             
032100          INCLUDE SQLCA                                                   
032200     END-EXEC.                                                            
032300                                                                          
032400                                                                          
032500 LINKAGE SECTION.                                                         
032600                                                                          
032700 01  DFHCOMMAREA.                                                         
032800     05  FILLER                         OCCURS  1 TO 4072 TIMES           
032900                                        DEPENDING ON EIBCALEN.            
033000         10  FILLER                     PIC  X(01).                       
033100                                                                          
033200                                                                          
033300 PROCEDURE DIVISION.                                                      
033400*****************************************************************         
033500** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
033600** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
033700** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
033800**                                                             **         
033900** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
034000** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
034100*****************************************************************         
034200 0000-MAIN-MODULE.                                                        
034300     INITIALIZE DP030-CICS-API-FIELDS.                                    
034400     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
034500     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
034600     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
034700     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
034800     MOVE LENGTH OF DP054AI        TO DP030-MAP-LENGTH (1).               
034900     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
035000     PERFORM 0001-CALL-CICS-ARCH-API.                                     
035100                                                                          
035200     PERFORM 1000-CONTROL-PROCESSING                                      
035300                                                                          
035400     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
035500     PERFORM 0001-CALL-CICS-ARCH-API.                                     
035600                                                                          
035700 0001-CALL-CICS-ARCH-API.                                                 
035800     CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
035900                                    DFHCOMMAREA                           
036000                                    DP030-CICS-API-FIELDS                 
036100                                    DP020-STANDARD-COMMAREA               
036200                                    DP054AI.                              
036300                                                                          
036400     IF  DP030-RC-CALL-SUCCESSFUL                                         
036500         CONTINUE                                                         
036600     ELSE                                                                 
036700         SET DP013-NO-ROLLBACK                                            
036800             DP013-XCTL-DISPLAY-RESTART                                   
036900             DP013-CICS-ABEND      TO TRUE                                
037000         MOVE 'BEFORE 0000-MAIN-MODULE'                                   
037100                                   TO DP013-PARAGRAPH                     
037200         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
037300-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
037400         MOVE DP030-RETURN-CODE                                           
037500                                   TO DP013-MESSAGE-TEXT (3)              
037600         PERFORM DP013-0000-PROCESS-ABEND                                 
037700     END-IF.                                                              
037800                                                                          
037900*----------------------------------------------------------------*        
038000*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
038100*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
038200*----------------------------------------------------------------*        
038300 1000-CONTROL-PROCESSING.                                                 
038400                                                                          
038500     EVALUATE TRUE                                                        
038600         WHEN DP020-NEXT-ACT-INITIAL                                      
038700*            *****************************************************        
038800*            * NEXT ACT: INITIAL                                 *        
038900*            *****************************************************        
039000             PERFORM 1100-FIND-OBJECT-KEYS                                
039100              IF  PS-ERROR                                                
039200                  CONTINUE                                                
039300              ELSE                                                        
039400                  PERFORM 4000-BUILD-INITIAL-PANEL                        
039500                  IF  SQLCODE = +100                                      
039600                      SET DP020-NEXT-ACT-APPL-ERROR TO TRUE               
039700*                     ---- DATA DOES NOT EXIST                            
039800                      MOVE PC-TSYMSG-00148                                
039900                                   TO DP020-MSG-NUMBER                    
040000                      SET DP020-MSG-INFORMATIONAL TO TRUE                 
040100                  END-IF                                                  
040200              END-IF                                                      
040300                                                                          
040400         WHEN DP020-NEXT-ACT-READ-MAP                                     
040500*            *****************************************************        
040600*            * NEXT ACT: READ MAP                                *        
040700*            *****************************************************        
040800             PERFORM 2000-PROCESS-PANEL                                   
040900                                                                          
041000                                                                          
041100         WHEN OTHER                                                       
041200*            *****************************************************        
041300*            * NEXT ACT: OTHER (UNDEFINED)                       *        
041400*            *****************************************************        
041500             SET DP013-LOGIC-ABEND                                        
041600                 DP013-NO-ROLLBACK TO TRUE                                
041700             MOVE '1000-CONTROL-PROCESSING'                               
041800                                   TO DP013-PARAGRAPH                     
041900             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
042000                                   TO DP013-MESSAGE-TEXT(1)               
042100             MOVE DP020-NEXT-APPL-ACTIVITY                                
042200                                   TO DP013-MESSAGE-TEXT(2)               
042300             PERFORM DP013-0000-PROCESS-ABEND                             
042400     END-EVALUATE.                                                        
042500                                                                          
042600                                                                          
042700******************************************************************        
042800** DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE   **        
042900** INTER-APPLICATION FORMAT CODE.                               **        
043000** (FOR THIS PROGRAM, ENTRY WILL ALWAYS BE FROM THE "LIST".)    **        
043100******************************************************************        
043200 1100-FIND-OBJECT-KEYS.                                                   
043300     SET ASC-USE-SELECTION-QUEUE TO TRUE.                                 
043400     MOVE DP251-NUMBER-OF-SELECTED-ITEMS                                  
043500                                 TO ASC-NUMBER-OF-SELECTED-ITEMS.         
043600     MOVE DP251-NUMBER-OF-TS-ITEMS                                        
043700                                 TO ASC-NUMBER-OF-TS-ITEMS.               
043800     MOVE DP251-SEL-QUEUE-NAME   TO ASC-SEL-QUEUE-NAME.                   
043900     PERFORM 1120-PREPARE-LIST-ENTRY.                                     
044000                                                                          
044100                                                                          
044101*----------------------------------------------------------------*        
044102* ENTRY IS FROM A LIST.  GET THE PHYSICAL UNIT FROM THE TEMP     *        
044103* STORAGE QUEUE PASSED FROM THE LIST PROGRAM.                    *        
044104*----------------------------------------------------------------*        
044105 1120-PREPARE-LIST-ENTRY.                                                 
044106     MOVE DP252-MAX-ITEMS          TO ASC-SEL-SUB.                        
044107     MOVE ZERO                     TO ASC-SEL-ITEM.                       
044110     PERFORM 2120-GET-NEXT-FROM-TS-QUEUE.                                 
044200                                                                          
045000                                                                          
045100*----------------------------------------------------------------*        
045200* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
045300* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
045400* EDITS.                                                         *        
045500*----------------------------------------------------------------*        
045600 2000-PROCESS-PANEL.                                                      
045700                                                                          
045800     EVALUATE TRUE                                                        
045900                                                                          
046000         WHEN DP020-SRC-AID = DP016-CLEAR                                 
046100*            *****************************************************        
046200*            * PFKEY ACTION: CLEAR                         <ESC> *        
046300*            *   <CLEAR> ("ESC" KEY)                             *        
046400*            *                                                   *        
046500*            * PRESENT SCREEN AS IT WAS AT THE TIME OF THE LAST  *        
046600*            * <ENTER>.                                          *        
046700*            *****************************************************        
046800             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
046900                                                                          
047000         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
047100*            *****************************************************        
047200*            * PFKEY ACTION: REFRESH                             *        
047300*            *                                                   *        
047400*            * PRESENT SCREEN AS FOLLOWS:                        *        
047500*            * - BRING BACK WHAT WAS LAST SAVED.                 *        
047600*            * - SHOW SCREEN AS ORIGINALLY PRESENTED (BLANK OR   *        
047700*            *   WITH DATA FROM DATABASE).                       *        
047800*            *****************************************************        
047900*            INITIALIZE ASC-SPECIFIC-COMMAREA                             
047910             INITIALIZE ASC-SCREEN                                        
048000             PERFORM 4000-BUILD-INITIAL-PANEL                             
048100                                                                          
048200         WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
048300*        *-------------------------------------------------------*        
048400*        *   GET NEXT SELECTION FROM TS QUEUE                    *        
048500*        *-------------------------------------------------------*        
048600             IF  ASC-USE-SELECTION-QUEUE                                  
048700                 INITIALIZE DCLTPHYUNT                                    
048800*                INITIALIZE ASC-SPECIFIC-COMMAREA                         
048810                 INITIALIZE ASC-SCREEN                                    
048900                 PERFORM 2120-GET-NEXT-FROM-TS-QUEUE                      
049000                 PERFORM 4000-BUILD-INITIAL-PANEL                         
049100             ELSE                                                         
049200*                ---- BOTTOM OF LIST                                      
049300                 MOVE PC-TSYMSG-00070                                     
049400                                   TO DP020-MSG-NUMBER                    
049500                 SET DP020-MSG-INFORMATIONAL                              
049600                                   TO TRUE                                
049700             END-IF                                                       
049800*                                                                         
049900         WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
050000*        *-------------------------------------------------------*        
050100*        *   GET PREV SELECTION FROM TS QUEUE                    *        
050200*        *-------------------------------------------------------*        
050300             IF  ASC-USE-SELECTION-QUEUE                                  
050400                 INITIALIZE DCLTPHYUNT                                    
050500*                INITIALIZE ASC-SPECIFIC-COMMAREA                         
050510                 INITIALIZE ASC-SCREEN                                    
050600                 PERFORM 2130-GET-PREV-FROM-TS-QUEUE                      
050700                 PERFORM 4000-BUILD-INITIAL-PANEL                         
050800             ELSE                                                         
050900*                ---- TOP OF LIST                                         
051000                 MOVE PC-TSYMSG-00069                                     
051100                                   TO DP020-MSG-NUMBER                    
051200                 SET DP020-MSG-INFORMATIONAL                              
051300                                   TO TRUE                                
051400             END-IF                                                       
051500*                                                                         
051600         WHEN OTHER                                                       
051700*            *****************************************************        
051800*            * PFKEY ACTION: OTHER (APPLICATION SPECIFIC)        *        
051900*            *                                                   *        
052000*            * NOTE THAT NO EDITS ARE PERFORMED; NO FIELDS MAY   *        
052100*            * ENTERED ON THIS DELETE SCREEN.                    *        
052200*            *****************************************************        
052300             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
052400             PERFORM 2100-CHECK-FUNCTION-KEY                              
052500             SET DP030-CONTINUE-GO TO TRUE                                
052600     END-EVALUATE.                                                        
052700                                                                          
052800                                                                          
052900******************************************************************        
053000** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
053100** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
053200** FROM THE CICS ARCHITECTURE API.                              **        
053300******************************************************************        
053400 2100-CHECK-FUNCTION-KEY.                                                 
053500     EVALUATE TRUE                                                        
053600                                                                          
053700         WHEN DP020-FK-CONFIRM (DP020-SRC-AID)                          12
053800*            *****************************************************        
053900*            * APPLICATION-SPECIFIC PFKEY: CONFIRM DELETE  <F2>  *        
054000*            * CONFIRMED DELETE REQUEST; PERFORM RECORD DELETION.*        
054100*            *****************************************************        
054200             PERFORM 5120-DELETE-ROW-ON-TABLE                           12
054300*            ---- DELETE SUCCESSFUL                                     12
054400             MOVE PC-TSYMSG-00115        TO DP020-MSG-NUMBER            12
054500             SET DP020-MSG-INFORMATIONAL TO TRUE                        12
054600             PERFORM 5200-UPDATE-SELECTION-QUEUE                        12
054700                                                                          
054800         WHEN DP020-SRC-AID = DP016-ENTER                                 
054900*            *****************************************************        
055000*            * APPLICATION-SPECIFIC PFKEY: <ENTER>               *        
055100*            *****************************************************        
055200             CONTINUE                                                     
055300                                                                          
055400         WHEN OTHER                                                       
055500*            *****************************************************        
055600*            * APPLICATION-SPECIFIC PFKEY: "OTHER" (UNDEFINED)   *        
055700*            *****************************************************        
055800             SET DP013-NO-ROLLBACK                                        
055900                 DP013-XCTL-DISPLAY-RESTART                               
056000                 DP013-CICS-ABEND  TO TRUE                                
056100             MOVE '2100-CHECK-FUNCTION-KEY'                               
056200                                   TO DP013-PARAGRAPH                     
056300             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
056400                                   TO DP013-MESSAGE-TEXT (1)              
056500             MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:'         
056600                                   TO DP013-MESSAGE-TEXT (2)              
056700             MOVE DP020-SRC-AID    TO PM-A-SRC-AID                        
056800             MOVE DP020-FK-ACTION (DP020-SRC-AID)                         
056900                                   TO PM-A-FK-ACTION                      
057000             MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)                      
057100                                   TO PM-A-FK-DISPN                       
057200             MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)              
057300             MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'                
057400                                   TO DP013-MESSAGE-TEXT (4)              
057500             PERFORM DP013-0000-PROCESS-ABEND                             
057600     END-EVALUATE.                                                        
057700                                                                          
057800******************************************************************        
057900** GET NEXT STMT ISRT KEY FROM THE TEMPORARY STORAGE QUEUE OF   **        
058000** SELECTED ITEMS (SELECTED BY THE USER IN THE LIST PROGRAM).   **        
058100******************************************************************        
058200 2120-GET-NEXT-FROM-TS-QUEUE.                                             
058300     ADD +1                        TO ASC-SEL-SUB.                        
058400     IF  ASC-SEL-SUB > DP252-MAX-ITEMS                                    
058500         ADD +1                    TO ASC-SEL-ITEM                        
058600         MOVE +1                   TO ASC-SEL-SUB                         
058700     END-IF.                                                              
058800     IF  ASC-SEL-ITEM <= ASC-NUMBER-OF-TS-ITEMS                           
058900         PERFORM 9100-READ-SEL-QUEUE                                      
059000         IF  DP252-ITEM (ASC-SEL-SUB) > ZERO                              
059100             PERFORM 2140-MOVE-SEL-QUEUE-TO-ASC                           
059200             IF (DP252-ACTION-SW (ASC-SEL-SUB) NOT EQUAL                  
059300                                               PC-LAST-ACTION-DEL         
059400             AND DP252-ACTION-SW (ASC-SEL-SUB) NOT EQUAL                  
059500                                               PC-LAST-ACTION-UPD         
059600             AND DP252-ACTION-SW (ASC-SEL-SUB) NOT EQUAL                  
059700                                               PC-LAST-ACTION-INQ         
059800             AND DP252-ACTION-SW (ASC-SEL-SUB) NOT EQUAL                  
059900                                               PC-LAST-ACTION-ERR)        
060000                 SET DP252-INQ (ASC-SEL-SUB)                              
060100                                   TO TRUE                                
060200             END-IF                                                       
060300             PERFORM 9200-REWRITE-SEL-QUEUE                               
060400         ELSE                                                             
060500             SUBTRACT +1 FROM ASC-SEL-SUB                                 
060600*            ---- BOTTOM OF LIST                                          
060700             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
060800             SET DP020-MSG-INFORMATIONAL                                  
060900                                   TO TRUE                                
061000         END-IF                                                           
061100     ELSE                                                                 
061200         SUBTRACT +1 FROM ASC-SEL-ITEM                                    
061300         MOVE DP252-MAX-ITEMS      TO ASC-SEL-SUB                         
061400*        ---- BOTTOM OF LIST                                              
061500         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
061600         SET DP020-MSG-INFORMATIONAL                                      
061700                                   TO TRUE                                
061800     END-IF.                                                              
061900                                                                          
062000                                                                          
062100******************************************************************        
062200** GET PREV STMT ISRT KEY FROM THE TEMPORARY STORAGE QUEUE OF   **        
062300** SELECTED ITEMS (SELECTED BY THE USER IN THE LIST PROGRAM).   **        
062400******************************************************************        
062500 2130-GET-PREV-FROM-TS-QUEUE.                                             
062600     SUBTRACT +1 FROM ASC-SEL-SUB.                                        
062700     IF  ASC-SEL-SUB < +1                                                 
062800         SUBTRACT +1 FROM ASC-SEL-ITEM                                    
062900         MOVE DP252-MAX-ITEMS      TO ASC-SEL-SUB                         
063000     END-IF.                                                              
063100*                                                                         
063200     IF  ASC-SEL-ITEM > ZERO                                              
063300         PERFORM 9100-READ-SEL-QUEUE                                      
063400         PERFORM 2140-MOVE-SEL-QUEUE-TO-ASC                               
063500     ELSE                                                                 
063600         MOVE +1                   TO ASC-SEL-ITEM                        
063700                                      ASC-SEL-SUB                         
063800*        ---- TOP OF LIST                                                 
063900         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
064000         SET DP020-MSG-INFORMATIONAL                                      
064100                                   TO TRUE                                
064200     END-IF.                                                              
064300                                                                          
064400                                                                          
064500******************************************************************        
064600*  MOVE FIELDS FROM THE TEMPORARY STORAGE QUEUE OF SELECTED ITEMS*        
064700*  TO THE APPLICATION SPECIFIC COMMAREA FOR PROCESSING,          *        
064800*  TO THE DATAKEY AREA FOR DB2 TABLE ACCESS, AND                 *        
064900*  TO THE DATAKEY-CURRENT AREA FOR ADDITIONAL DB2 TABLE ACCESS.  *        
065000******************************************************************        
065100 2140-MOVE-SEL-QUEUE-TO-ASC.                                              
065200     MOVE DP252-LOCATION-NBR (ASC-SEL-SUB)                                
065300                                   TO ASC-STORE-N                         
065500                                      DK-LOC-NBR                          
065700                                      ASC-DKC-LOC-NBR.                    
066100     MOVE DP252-FCLTY-N (ASC-SEL-SUB)                                     
066200                                   TO ASC-FCLTY-N                         
066300                                      DK-FCLTY-N                          
066400                                      DK-FCLTY-N4-COMP                    
066500                                      ASC-DKC-FCLTY-N                     
066600                                      ASC-DKC-FCLTY-N4-COMP.              
066700     MOVE DP252-FCLTY (ASC-SEL-SUB)                                       
066800                                   TO DK-FCLTY-X                          
066900                                      ASC-DKC-FCLTY-X.                    
067000     MOVE DP252-LNE-ADDR-NBR (ASC-SEL-SUB)                                
067100                                   TO ASC-LINE                            
067200                                      DK-LNE-ADDR-NBR                     
067300                                      ASC-DKC-LNE-ADDR-NBR.               
067400     MOVE DP252-PHYSICAL-UNIT (ASC-SEL-SUB)                               
067500                                   TO ASC-PHYSICAL-UNIT                   
067600                                      DK-PHYUNT-ADDR-NBR                  
067700                                      ASC-DKC-PHYUNT-ADDR-NBR.            
067800     MOVE DP252-IP-ADDR-BEG (ASC-SEL-SUB)                                 
067900                                   TO ASC-IP-ADDR-BEGIN                   
068000                                      DK-IP-ADDR-BEG                      
068100                                      ASC-DKC-IP-ADDR-BEG.                
068200     MOVE DP252-IP-ADDR-END (ASC-SEL-SUB)                                 
068300                                   TO ASC-IP-ADDR-END                     
068400                                      DK-IP-ADDR-END                      
068500                                      ASC-DKC-IP-ADDR-END.                
068600                                                                          
068700                                                                          
068800*----------------------------------------------------------------*        
068900* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
069000* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
069100* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
069200*----------------------------------------------------------------*        
069300 2200-MOVE-SCREEN-TO-COMMAREA.                                            
069400*    *************************************************************        
069500*    * LOCATION: STORE                                           *        
069600*    *************************************************************        
069700     IF  ASTORL > ZERO                                                    
069800         MOVE ASTORI               TO ASC-STORE                           
069900     ELSE                                                                 
070000         IF  ASTORF = DP015-ERASE-EOF                                     
070100             MOVE SPACE            TO ASC-STORE                           
070200         END-IF                                                           
070300     END-IF.                                                              
070400                                                                          
070500*    *************************************************************        
070600*    * LOCATION: FACILITY                                        *        
070700*    *************************************************************        
070800     IF  AFCLTL > ZERO                                                    
070900         MOVE AFCLTI               TO ASC-FCLTY                           
071000     ELSE                                                                 
071100         IF  AFCLTF = DP015-ERASE-EOF                                     
071200             MOVE SPACE            TO ASC-FCLTY                           
071300         END-IF                                                           
071400     END-IF.                                                              
071500                                                                          
071600*    *************************************************************        
071700*    * VTAM:     LINE                                            *        
071800*    *************************************************************        
071900     IF  ALINEL > ZERO                                                    
072000         MOVE ALINEI               TO ASC-LINE                            
072100     ELSE                                                                 
072200         IF  ALINEF = DP015-ERASE-EOF                                     
072300             MOVE SPACE            TO ASC-LINE                            
072400         END-IF                                                           
072500     END-IF.                                                              
072600                                                                          
072700*    *************************************************************        
072800*    * VTAM:     UNIT                                            *        
072900*    *************************************************************        
073000     IF  APHYUL > ZERO                                                    
073100         MOVE APHYUI               TO ASC-PHYSICAL-UNIT                   
073200     ELSE                                                                 
073300         IF  APHYUF = DP015-ERASE-EOF                                     
073400             MOVE SPACE            TO ASC-PHYSICAL-UNIT                   
073500         END-IF                                                           
073600     END-IF.                                                              
073700                                                                          
073800*    *************************************************************        
073900*    * IP ADDRESS RANGE: BEGINNING OF RANGE                      *        
074000*    *************************************************************        
074100     IF  AIPB1L > ZERO                                                    
074200         MOVE AIPB1I               TO ASC-IP-ADDR-B-OCTET1                
074300     ELSE                                                                 
074400         IF  AIPB1F = DP015-ERASE-EOF                                     
074500             MOVE SPACE            TO ASC-IP-ADDR-B-OCTET1                
074600         END-IF                                                           
074700     END-IF.                                                              
074800                                                                          
074900     IF  AIPB2L > ZERO                                                    
075000         MOVE AIPB2I               TO ASC-IP-ADDR-B-OCTET2                
075100     ELSE                                                                 
075200         IF  AIPB2F = DP015-ERASE-EOF                                     
075300             MOVE SPACE            TO ASC-IP-ADDR-B-OCTET2                
075400         END-IF                                                           
075500     END-IF.                                                              
075600                                                                          
075700     IF  AIPB3L > ZERO                                                    
075800         MOVE AIPB3I               TO ASC-IP-ADDR-B-OCTET3                
075900     ELSE                                                                 
076000         IF  AIPB3F = DP015-ERASE-EOF                                     
076100             MOVE SPACE            TO ASC-IP-ADDR-B-OCTET3                
076200         END-IF                                                           
076300     END-IF.                                                              
076400                                                                          
076500     IF  AIPB4L > ZERO                                                    
076600         MOVE AIPB4I               TO ASC-IP-ADDR-B-OCTET4                
076700     ELSE                                                                 
076800         IF  AIPB4F = DP015-ERASE-EOF                                     
076900             MOVE SPACE            TO ASC-IP-ADDR-B-OCTET4                
077000         END-IF                                                           
077100     END-IF.                                                              
077200                                                                          
077300*    *************************************************************        
077400*    * IP ADDRESS RANGE: END OF RANGE                            *        
077500*    *************************************************************        
077600     IF  AIPE1L > ZERO                                                    
077700         MOVE AIPE1I               TO ASC-IP-ADDR-E-OCTET1                
077800     ELSE                                                                 
077900         IF  AIPE1F = DP015-ERASE-EOF                                     
078000             MOVE SPACE            TO ASC-IP-ADDR-E-OCTET1                
078100         END-IF                                                           
078200     END-IF.                                                              
078300                                                                          
078400     IF  AIPE2L > ZERO                                                    
078500         MOVE AIPE2I               TO ASC-IP-ADDR-E-OCTET2                
078600     ELSE                                                                 
078700         IF  AIPE2F = DP015-ERASE-EOF                                     
078800             MOVE SPACE            TO ASC-IP-ADDR-E-OCTET2                
078900         END-IF                                                           
079000     END-IF.                                                              
079100                                                                          
079200     IF  AIPE3L > ZERO                                                    
079300         MOVE AIPE3I               TO ASC-IP-ADDR-E-OCTET3                
079400     ELSE                                                                 
079500         IF  AIPE3F = DP015-ERASE-EOF                                     
079600             MOVE SPACE            TO ASC-IP-ADDR-E-OCTET3                
079700         END-IF                                                           
079800     END-IF.                                                              
079900                                                                          
080000     IF  AIPE4L > ZERO                                                    
080100         MOVE AIPE4I               TO ASC-IP-ADDR-E-OCTET4                
080200     ELSE                                                                 
080300         IF  AIPE4F = DP015-ERASE-EOF                                     
080400             MOVE SPACE            TO ASC-IP-ADDR-E-OCTET4                
080500         END-IF                                                           
080600     END-IF.                                                              
080700                                                                          
080800                                                                          
080900*----------------------------------------------------------------*        
081000*    VALIDATE THE QUEUE WAS PASSED AND DISPLAY FOR THE FIRST     *        
081100*    DELETE CONFIRMATION:                                        *        
081200*    PRESENT DATA SELECTED IN LIST PROGRAM, ON THE SCREEN FOR    *        
081300*    THE PERSON TO DELETE IT.                                    *        
081400*    1- GET DATA KEYS PASSED IN TS QUEUE FROM LIST PROGRAM;      *        
081500*       THEIR VALUES ARE PRESENTLY STORED IN THE APPLICATION     *        
081600*       SPECIFIC COMMAREA.                                       *        
081700*    2- GET DATA FROM DB2 TABLE, USING DATA KEYS.                *        
081800*    3- POPULATE SCREEN VARIABLES WITH DATA FROM DB2 TABLE.      *        
081900*    (NOTE: IN THIS APPLICATION, ALL NEEDED DATA FIELDS ARE      *        
082000*           PASSED FROM THE LIST PROGRAM; ACCESS DB2 TABLE AT    *        
082100*           THIS TIME JUST TO VERIFY ALL KEY CONSTRUCTS.)        *        
082200*----------------------------------------------------------------*        
082300 4000-BUILD-INITIAL-PANEL.                                                
082400                                                                          
082500     SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
082600     MOVE -1                       TO ASTORL.                             
082700                                                                          
082800     IF  DP020-MSG-NUMBER NOT EQUAL PC-TSYMSG-00070                     12
082900     AND DP020-MSG-NUMBER NOT EQUAL PC-TSYMSG-00069                     12
083000*        ---- DELETE PENDING - PRESS PF2 TO CONFIRM                     12
083100         MOVE PC-TSYMSG-00354        TO DP020-MSG-NUMBER                12
083200         SET DP020-MSG-INFORMATIONAL TO TRUE                            12
083300     END-IF.                                                            12
083400                                                                          
083600     MOVE ASC-STORE-N              TO DK-LOC-NBR.                         
083900     MOVE ASC-FCLTY-N              TO DK-FCLTY-N                          
084000                                      DK-FCLTY-N4-COMP.                   
084100     MOVE ASC-FCLTY                TO DK-FCLTY-X.                         
084200     MOVE ASC-LINE                 TO DK-LNE-ADDR-NBR.                    
084300     MOVE ASC-PHYSICAL-UNIT        TO DK-PHYUNT-ADDR-NBR.                 
084400     MOVE ASC-IP-ADDR-BEGIN        TO DK-IP-ADDR-BEG.                     
084500     MOVE ASC-IP-ADDR-END          TO DK-IP-ADDR-END.                     
084600                                                                          
084700     PERFORM 4100-GET-OBJECT-TO-DISPLAY.                                  
084800     IF  SQLCODE = ZERO                                                   
085000         MOVE PHYUNT-LNE-ADDR-NBR      TO ASC-LINE                        
085100         MOVE PHYUNT-PHYUNT-ADDR-NBR   TO ASC-PHYSICAL-UNIT               
               MOVE PHYUNT-LOC-NBR           TO ASC-STORE-N                     
085400         MOVE PHYUNT-FCLTY-ID          TO ASC-FCLTY-N                     
085500         MOVE PHYUNT-BEG-RNG-IP-ADDR   TO ASC-IP-ADDR-BEGIN               
085600         MOVE PHYUNT-END-RNG-IP-ADDR   TO ASC-IP-ADDR-END                 
085700         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
085800     END-IF.                                                              
085900                                                                          
086000                                                                          
086100*----------------------------------------------------------------*        
086200*    GET PHYSICAL UNIT ROW FROM TABLE TPHYUNT TO DISPLAY.        *        
086300*----------------------------------------------------------------*        
086400 4100-GET-OBJECT-TO-DISPLAY.                                              
086500     PERFORM                                                              
086600         WITH TEST AFTER                                                  
086700         VARYING PV-RETRY-COUNTER                                         
086800         FROM 1 BY 1                                                      
086900         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
087000                   OR (SQLCODE = ZERO)                                    
087100                   OR (SQLCODE = +100))                                   
087200                                                                          
087300         EXEC SQL                                                         
087400             SELECT LNE_ADDR_NBR,                                         
087500                    PHYUNT_ADDR_NBR,                                      
087600                    LOC_NBR,                                              
087800                    FCLTY_ID,                                             
087900                    BEG_RNG_IP_ADDR,                                      
088000                    END_RNG_IP_ADDR                                       
088100             INTO   :PHYUNT-LNE-ADDR-NBR,                                 
088200                    :PHYUNT-PHYUNT-ADDR-NBR,                              
088300                    :PHYUNT-LOC-NBR,                                      
088500                    :PHYUNT-FCLTY-ID,                                     
088600                    :PHYUNT-BEG-RNG-IP-ADDR,                              
088700                    :PHYUNT-END-RNG-IP-ADDR                               
088800             FROM   TPHYUNT                                               
088900             WHERE (LNE_ADDR_NBR    = :DK-LNE-ADDR-NBR     AND            
089000                    PHYUNT_ADDR_NBR = :DK-PHYUNT-ADDR-NBR  AND            
089200                    LOC_NBR         = :DK-LOC-NBR          AND            
089300                    FCLTY_ID        = :DK-FCLTY-N4-COMP    AND            
089400                    BEG_RNG_IP_ADDR = :DK-IP-ADDR-BEG      AND            
089500                    END_RNG_IP_ADDR = :DK-IP-ADDR-END)                    
089600         END-EXEC                                                         
089700                                                                          
089800         EVALUATE TRUE                                                    
089900             WHEN SQLCODE = ZERO                                          
090000             WHEN SQLCODE = +100                                          
090100             WHEN SQLCODE = -904                                          
090200             WHEN SQLCODE = -913                                          
090300                 CONTINUE                                                 
090400             WHEN SQLWARN0 NOT = SPACES                                   
090500             WHEN SQLCODE < ZERO                                          
090600             WHEN SQLCODE > ZERO                                          
090700                 MOVE '4100-GET-OBJECT-TO-DISPLAY'                        
090800                                   TO DP013-PARAGRAPH                     
090900                 MOVE 'SELECT FAILED FOR:'                                
091000                                   TO DP013-MESSAGE-TEXT (1)              
091100                 MOVE SQLCA        TO DP013-SQLCA                         
091200                 MOVE 'TPHYUNT'    TO DP013-DB2-TABLE-NAME (1)            
091300                 SET DP013-DB2-ABEND                                      
091400                     DP013-XCTL-DISPLAY-RESTART                           
091500                                   TO TRUE                                
091600                 PERFORM DP013-0000-PROCESS-ABEND                         
091700         END-EVALUATE                                                     
091800     END-PERFORM.                                                         
091900                                                                          
092000     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
092100         MOVE '4100-GET-OBJECT-TO-DISPLAY'                                
092200                                   TO DP013-PARAGRAPH                     
092300         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
092400                                   TO DP013-MESSAGE-TEXT (1)              
092500         MOVE 'PHYSICAL UNIT'      TO DP013-MESSAGE-TEXT (2)              
092600         MOVE SQLCA                TO DP013-SQLCA                         
092700         MOVE 'TPHYUNT'            TO DP013-DB2-TABLE-NAME (1)            
092800         SET DP013-DB2-ABEND                                              
092900             DP013-XCTL-DISPLAY-RESTART                                   
093000                                   TO TRUE                                
093100         PERFORM DP013-0000-PROCESS-ABEND                                 
093200     END-IF.                                                              
093300                                                                          
093400                                                                          
093500*----------------------------------------------------------------*        
093600*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
093700*----------------------------------------------------------------*        
093800 4400-MOVE-COMMAREA-TO-SCREEN.                                            
093900     MOVE ASC-STORE                TO ASTORO.                             
094000     MOVE ASC-FCLTY                TO AFCLTO.                             
094100     MOVE ASC-LINE                 TO ALINEO.                             
094200     MOVE ASC-PHYSICAL-UNIT        TO APHYUO.                             
094300     MOVE ASC-IP-ADDR-B-OCTET1     TO AIPB1O.                             
094400     MOVE ASC-IP-ADDR-B-OCTET2     TO AIPB2O.                             
094500     MOVE ASC-IP-ADDR-B-OCTET3     TO AIPB3O.                             
094600     MOVE ASC-IP-ADDR-B-OCTET4     TO AIPB4O.                             
094700     MOVE ASC-IP-ADDR-E-OCTET1     TO AIPE1O.                             
094800     MOVE ASC-IP-ADDR-E-OCTET2     TO AIPE2O.                             
094900     MOVE ASC-IP-ADDR-E-OCTET3     TO AIPE3O.                             
095000     MOVE ASC-IP-ADDR-E-OCTET4     TO AIPE4O.                             
095100                                                                          
095200                                                                          
095300*----------------------------------------------------------------*        
095400* DELETE A ROW ON THE TPHYUNT TABLE.                             *        
095500*----------------------------------------------------------------*        
095600 5120-DELETE-ROW-ON-TABLE.                                                
095700     PERFORM                                                              
095800         WITH TEST AFTER                                                  
095900         VARYING PV-RETRY-COUNTER                                         
096000         FROM 1 BY 1                                                      
096100         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
096200                   OR (SQLCODE = ZERO))                                   
096300         EXEC SQL                                                         
096400             DELETE FROM TPHYUNT                                          
096500             WHERE (LNE_ADDR_NBR    = :ASC-DKC-LNE-ADDR-NBR  AND          
096600                    PHYUNT_ADDR_NBR = :ASC-DKC-PHYUNT-ADDR-NBR            
096700                                                              AND         
096800                    LOC_NBR         = :ASC-DKC-LOC-NBR        AND         
096900                    FCLTY_ID        = :ASC-DKC-FCLTY-N4-COMP  AND         
097000                    BEG_RNG_IP_ADDR = :ASC-DKC-IP-ADDR-BEG    AND         
097100                    END_RNG_IP_ADDR = :ASC-DKC-IP-ADDR-END)               
097200         END-EXEC                                                         
097300                                                                          
097400                                                                          
097500         EVALUATE TRUE                                                    
097600             WHEN SQLCODE = ZERO                                          
097700             WHEN SQLCODE = -904                                          
097800             WHEN SQLCODE = -913                                          
097900                 CONTINUE                                                 
098000             WHEN SQLWARN0 NOT = SPACES                                   
098100             WHEN SQLCODE < ZERO                                          
098200             WHEN SQLCODE > ZERO                                          
098300                 MOVE '5100-UPDATE-ROW-ON-TABLE'                          
098400                                   TO DP013-PARAGRAPH                     
098500                 MOVE 'DELETE OF ROW ON THE PHYSICAL UNIT TABLE'          
098600                                   TO DP013-MESSAGE-TEXT(1)               
098700                 MOVE 'FAILED FOR: '                                      
098800                                   TO DP013-MESSAGE-TEXT(2)               
098900                 MOVE ASC-DKC-LNE-ADDR-NBR                                
099000                                   TO PM-ABEND-KEYFIELD1                  
099100                 MOVE ASC-DKC-PHYUNT-ADDR-NBR                             
099200                                   TO PM-ABEND-KEYFIELD2                  
099300                 MOVE PM-ABEND-KEYFIELDS                                  
099400                                   TO DP013-MESSAGE-TEXT(3)               
099500                 MOVE ASC-DKC-IP-ADDR-BEG                                 
099600                                   TO PM-ABEND-RANGE-B                    
099700                 MOVE ASC-DKC-IP-ADDR-END                                 
099800                                   TO PM-ABEND-RANGE-E                    
099900                 MOVE PM-ABEND-RANGES                                     
100000                                   TO DP013-MESSAGE-TEXT(4)               
100100                 MOVE SQLCA        TO DP013-SQLCA                         
100200                 MOVE 'TPHYUNT'    TO DP013-DB2-TABLE-NAME (1)            
100300                 SET DP013-DB2-ABEND                                      
100400                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
100500                 PERFORM DP013-0000-PROCESS-ABEND                         
100600         END-EVALUATE                                                     
100700     END-PERFORM.                                                         
100800                                                                          
100900     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
101000         MOVE '5100-UPDATE-ROW-ON-TABLE'                                  
101100                                   TO DP013-PARAGRAPH                     
101200         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO DELETE THIS         
101300-             'PHYSICAL UNIT'      TO DP013-MESSAGE-TEXT (1)              
101400         MOVE SQLCA                TO DP013-SQLCA                         
101500         MOVE 'TPHYUNT '           TO DP013-DB2-TABLE-NAME (1)            
101600         SET DP013-DB2-ABEND                                              
101700             DP013-XCTL-DISPLAY-RESTART                                   
101800                                   TO TRUE                                
101900         PERFORM DP013-0000-PROCESS-ABEND                                 
102000     END-IF.                                                              
102100                                                                          
102200                                                                          
102300*----------------------------------------------------------------*        
102400* RECORD IN THE SELECTION TEMP STORAGE QUEUE THE FACT THAT THIS  *        
102500* OBJECT WAS PROCESSED: DELETE.                                  *        
102600*----------------------------------------------------------------*        
102700 5200-UPDATE-SELECTION-QUEUE.                                             
102800     PERFORM 9100-READ-SEL-QUEUE.                                         
102900                                                                          
103000     SET DP252-DEL (ASC-SEL-SUB) TO TRUE.                                 
103100                                                                          
103200     PERFORM 9200-REWRITE-SEL-QUEUE.                                      
103300                                                                          
103400                                                                          
103500*----------------------------------------------------------------*        
103600* READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
103700*----------------------------------------------------------------*        
103800 9100-READ-SEL-QUEUE.                                                     
103900     MOVE LENGTH OF DP252-PHYS-UNIT-SEL-REC TO PV-SELQ-LENGTH.            
104000                                                                          
104100     EXEC CICS                                                            
104200         READQ TS                                                         
104300             QUEUE (ASC-SEL-QUEUE-NAME)                                   
104400             INTO  (DP252-PHYS-UNIT-SEL-REC)                              
104500             ITEM  (ASC-SEL-ITEM)                                         
104600             LENGTH(PV-SELQ-LENGTH)                                       
104700             RESP  (PV-CICS-RESPONSE)                                     
104800     END-EXEC.                                                            
104900                                                                          
105000     IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                           
105100         CONTINUE                                                         
105200     ELSE                                                                 
105300         SET DP013-LOGIC-ABEND                                            
105400             DP013-NO-ROLLBACK     TO TRUE                                
105500         MOVE '9100-READ-SEL-QUEUE'                                       
105600                                   TO DP013-PARAGRAPH                     
105700         MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
105800                                   TO DP013-MESSAGE-TEXT (1)              
105900         MOVE ASC-SEL-QUEUE-NAME     TO PM-SELQ-NAME                      
106100         MOVE ASC-SEL-ITEM           TO PM-SELQ-ITEM                      
106200         MOVE PV-SELQ-LENGTH         TO PM-SELQ-LENGTH                    
106300         MOVE PM-SELQ-ABEND-MSG      TO DP013-MESSAGE-TEXT(2)             
106400         PERFORM DP013-0000-PROCESS-ABEND                                 
106500     END-IF.                                                              
106600                                                                          
106700                                                                          
106800*----------------------------------------------------------------*        
106900* UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
107000*----------------------------------------------------------------*        
107100 9200-REWRITE-SEL-QUEUE.                                                  
107200     EXEC CICS                                                            
107300          WRITEQ TS                                                       
107400              QUEUE  (ASC-SEL-QUEUE-NAME)                                 
107500              FROM   (DP252-PHYS-UNIT-SEL-REC)                            
107600              ITEM   (ASC-SEL-ITEM)                                       
107700              RESP   (PV-CICS-RESPONSE)                                   
107800              REWRITE                                                     
107900     END-EXEC.                                                            
108000                                                                          
108100     IF  PV-CICS-RESPONSE NOT EQUAL DFHRESP(NORMAL)                       
108200         SET DP013-CICS-ABEND                                             
108300             DP013-NO-ROLLBACK     TO TRUE                                
108400         MOVE '9200-REWRITE-SEL-QUEUE'                                    
108500                                   TO DP013-PARAGRAPH                     
108600         MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
108700                                   TO DP013-MESSAGE-TEXT (1)              
108800         PERFORM DP013-0000-PROCESS-ABEND                                 
108900     END-IF.                                                              
109000                                                                          
109100                                                                          
109200*----------------------------------------------------------------*        
109300*    ABEND PROCESSOR MODULE                                      *        
109400*----------------------------------------------------------------*        
109500     COPY DPPD013.                                                        
