      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    CWKUT050.                                                 
       AUTHOR.        DAVID FELLENZ.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11/29/11.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *    EDW DAILY DUMMY CUSTOMER CHOICE EXTRACT                              
      ******************************************************************        
      *    THIS PROGRAM WILL CREATE A DUMMY CUSTOMER CHOICE RECORD              
      *    FOR EACH DUMMY SKU                                                   
      *                                                                         
      * CHANGE LOG:                                                             
      *                                                                         
      * 02/14/2023    JEAN KURK               CHG0281369                        
      *       REMOVE UNUSED COPYBOOK FOR UPC CHECK DIGIT                        
      *                                                                         
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CW-SKU-UPC-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT DATE-CONTROL-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CW-DUMMY-CUST-CHC-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT CW-DUMMY-CUST-CHC-CNT-FILE                                    
               ASSIGN TO OUT02.                                                 
      *                                                                         
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
      *                                                                         
       FD  CW-SKU-UPC-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CW-SKU-UPC-REC               PIC X(344).                             
      *                                                                         
       FD  DATE-CONTROL-FILE                                                    
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  DATE-CONTROL-RECORD          PIC  X(80).                             
      *                                                                         
       FD  CW-DUMMY-CUST-CHC-FILE                                               
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CW-DUMMY-CUST-CHC-REC        PIC X(118).                             
      *                                                                         
       FD  CW-DUMMY-CUST-CHC-CNT-FILE                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CW-DUMMY-CUST-CHC-CNT-REC    PIC X(80).                              
      *                                                                         
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(8)  VALUE 'CWKUT050'.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW                PIC X      VALUE 'N'.           
               88  PS-END-OF-FILE                          VALUE 'Y'.           
               88  PS-NOT-END-OF-FILE                      VALUE 'N'.           
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORD-COUNT            PIC 9(09)  VALUE 0.             
           05  PA-OUTPUT-RECORD-COUNT-SKU       PIC 9(09)  VALUE 0.             
           05  PA-OUTPUT-RECORD-COUNT-VENDOR    PIC 9(09)  VALUE 0.             
           05  PA-OUTPUT-RECORD-COUNT-CC        PIC 9(12)  VALUE 0.             
                                                                                
       01  ABEND-CODE                       PIC S9(04)    VALUE ZERO            
                                                          COMP SYNC.            
       01  PROGRAM-VARIABLES.                                                   
                                                                                
           05  PV-IN-COUNTER1-SKU           PIC 9(9)    VALUE 0.                
           05  PV-OUT-COUNTER1-SKU          PIC 9(9)    VALUE 0.                
           05  PV-IN-COUNTER1-VENDOR        PIC 9(9)    VALUE 0.                
           05  PV-OUT-COUNTER1-VENDOR       PIC 9(9)    VALUE 0.                
                                                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
           05  PV-SKU                       PIC 9(8).                           
           05  PV-SKU-CHAR REDEFINES PV-SKU.                                    
               10  PV-SKU-DEPT              PIC X(3).                           
               10  PV-SKU-SCL               PIC X(2).                           
               10  PV-SKU-000               PIC X(3).                           
           05  PV-CUST-CHOICE-ID            PIC S9(9) COMP-4.                   
                                                                                
      ****************************************************************          
      *  DATE CONTROL RECORD LAYOUT                                  *          
      ****************************************************************          
       01  DATE-CONTROL-RECORD-WS.                                              
           05  DATE-CONTROL-DATE.                                               
               10  DATE-CONTROL-CC          PIC X(02).                          
               10  DATE-CONTROL-YY          PIC X(02).                          
               10  DATE-CONTROL-MM          PIC X(02).                          
               10  DATE-CONTROL-DD          PIC X(02).                          
           05  FILLER                       PIC X(72).                          
                                                                                
       01  WS-CONTROL-DATE-WS.                                                  
           05  WS-CONTROL-DATE.                                                 
               10  WS-CC                    PIC X(02).                          
               10  WS-YY                    PIC X(02).                          
               10  WS-DASH-1                PIC X(01) VALUE '-'.                
               10  WS-MM                    PIC X(02).                          
               10  WS-DASH-2                PIC X(01) VALUE '-'.                
               10  WS-DD                    PIC X(02).                          
                                                                                
      ****                                                                      
      *   DUMMY SKU/DUMMY UPC RECORD LAYOUT.                                    
      ****                                                                      
           COPY CWRD002.                                                        
                                                                                
           COPY CWRD050.                                                        
                                                                                
           COPY SQLCA2.                                                         
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SYSDUMMY                                                 
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
           EJECT                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-FILE                                            
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM 8500-WRITE-DUMMY-CC-CNT-FILE.                                
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  OPEN ALL FILES.                                                        
      *  READ THE FIRST RECORD FROM THE INPUT FILE.                             
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  CW-SKU-UPC-FILE                                          
                       DATE-CONTROL-FILE                                        
                OUTPUT CW-DUMMY-CUST-CHC-FILE                                   
                       CW-DUMMY-CUST-CHC-CNT-FILE.                              
                                                                                
           READ DATE-CONTROL-FILE                                               
               INTO DATE-CONTROL-RECORD-WS                                      
               AT END                                                           
                   DISPLAY '** ABEND - PROGRAM CWKUT050 **'                     
                   DISPLAY '** DATE CONTROL RECORD IS MISSING'                  
                   DISPLAY ' 1000-INITIALIZE'                                   
                   MOVE +4001  TO ABEND-CODE                                    
                   CALL 'ILBOABN0' USING ABEND-CODE                             
           END-READ.                                                            
                                                                                
           DISPLAY 'CWKUT050 DATE CONTROL DATE:  ' DATE-CONTROL-DATE.           
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-SKUUPC-FILE.                                       
                                                                                
      ******************************************************************        
      *  FORMAT AND WRITE THE OUTPUT RECORDS.                                   
      *  READ THE NEXT INPUT RECORD.                                            
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           INITIALIZE CW050-RECORD.                                             
                                                                                
           MOVE CW002-SKU-NBR           TO  CW050-SKU-NBR.                      
           MOVE 0                       TO  CW050-RMS-STYL-ID.                  
           MOVE '999'                   TO  CW050-NRF-CLOR-CDE.                 
           MOVE 'ASSORTED'              TO  CW050-CSTM-CLOR-DESC.               
           MOVE 'DUMMY CUSTOMER CHOICE' TO  CW050-DUMMY-CC-DESC-21.             
           MOVE DATE-CONTROL-CC         TO  WS-CC.                              
           MOVE DATE-CONTROL-YY         TO  WS-YY.                              
           MOVE DATE-CONTROL-MM         TO  WS-MM.                              
           MOVE DATE-CONTROL-DD         TO  WS-DD.                              
           MOVE WS-CONTROL-DATE         TO  CW050-EFF-BEG-DTE.                  
           MOVE WS-CONTROL-DATE         TO  CW050-CREATE-DTE.                   
                                                                                
           PERFORM 5000-CREATE-CUST-CHOICE-ID.                                  
                                                                                
           MOVE PV-CUST-CHOICE-ID       TO  CW050-CUST-CHOICE-ID.               
           MOVE PV-CUST-CHOICE-ID       TO  CW050-DUMMY-CC-DESC-9.              
                                                                                
           PERFORM 8300-WRITE-DUMMY-CUST-CHC-REC.                               
                                                                                
           PERFORM 8000-READ-SKUUPC-FILE.                                       
                                                                                
      ******************************************************************        
      *    CALL TO FIND THE NEXT CUSTOMER CHOICE ID NUMBER                      
      ******************************************************************        
       5000-CREATE-CUST-CHOICE-ID.                                              
                                                                                
           EXEC SQL                                                             
             SELECT NEXT VALUE FOR IMS_CUST_CHC_ID_SEQ                          
               INTO :PV-CUST-CHOICE-ID                                          
               FROM  SYSIBM.SYSDUMMY1                                           
             WITH UR                                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = +0                                                 
              WHEN SQLCODE = +100                                               
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 PERFORM 9998-SQL-ERROR                                         
           END-EVALUATE.                                                        
                                                                                
      ************************************************************              
      *  READ THE SUBCLASS FILE                                                 
      ************************************************************              
       8000-READ-SKUUPC-FILE.                                                   
                                                                                
           READ CW-SKU-UPC-FILE INTO CW002-RECORD                               
               AT END                                                           
                   SET PS-END-OF-FILE TO TRUE                                   
               NOT AT END                                                       
                   ADD 1          TO   PA-INPUT-RECORD-COUNT.                   
                                                                                
      ******************************************************************        
      *  WRITE A DUMMY CUST CHOICE RECORD.                                      
      ******************************************************************        
       8300-WRITE-DUMMY-CUST-CHC-REC.                                           
                                                                                
           WRITE CW-DUMMY-CUST-CHC-REC                                          
               FROM CW050-RECORD.                                               
                                                                                
           ADD 1 TO PA-OUTPUT-RECORD-COUNT-CC.                                  
                                                                                
      ******************************************************************        
      *  WRITE A DUMMY CUST CHOICE COUNT FILE                                   
      ******************************************************************        
       8500-WRITE-DUMMY-CC-CNT-FILE.                                            
                                                                                
           WRITE CW-DUMMY-CUST-CHC-CNT-REC                                      
               FROM PA-OUTPUT-RECORD-COUNT-CC.                                  
                                                                                
      ************************************************************              
      *  DISPLAY RECORD COUNTS AT END OF JOB                                    
      *  CLOSE THE FILES.                                                       
      ************************************************************              
       9000-END-OF-JOB.                                                         
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
                                                                                
           DISPLAY '   SKU RECORDS INPUT         = '                            
                    PA-INPUT-RECORD-COUNT.                                      
                                                                                
           DISPLAY '   DUMMY CC OUTPUT         = '                              
                    PA-OUTPUT-RECORD-COUNT-CC.                                  
                                                                                
           DISPLAY '   '.                                                       
                                                                                
           CLOSE CW-SKU-UPC-FILE                                                
                 DATE-CONTROL-FILE                                              
                 CW-DUMMY-CUST-CHC-FILE                                         
                 CW-DUMMY-CUST-CHC-CNT-FILE.                                    
                                                                                
       9998-SQL-ERROR.                                                          
      ******************************************************************        
      * ABEND ROUTINE FOR BAD SQL CALLS                                *        
      ******************************************************************        
           MOVE SQLCODE                    TO SQLCODE2.                         
           MOVE SQLERRD(3)                 TO SQLERRD3.                         
           DISPLAY '** ABEND **          = SQL ERROR'.                          
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE **        = ' SQLCODE2.                          
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
           DISPLAY '** SQLERRM **        = ' SQLERRM.                           
           DISPLAY '** SQLERRMC **       = ' SQLERRMC.                          
                                                                                
           DISPLAY '** SQLWARN0 **       = ' SQLWARN0.                          
           DISPLAY '** SQLWARN1 **       = ' SQLWARN1.                          
           DISPLAY '** SQLWARN2 **       = ' SQLWARN2.                          
           DISPLAY '** SQLWARN3 **       = ' SQLWARN3.                          
           DISPLAY '** SQLWARN4 **       = ' SQLWARN4.                          
           DISPLAY '** SQLWARN5 **       = ' SQLWARN5.                          
           DISPLAY '** SQLWARN6 **       = ' SQLWARN6.                          
           DISPLAY '** SQLWARN7 **       = ' SQLWARN7.                          
      *                                                                         
           MOVE +4013                      TO ABEND-CODE.                       
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
