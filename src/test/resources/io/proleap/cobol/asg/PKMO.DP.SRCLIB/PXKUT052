       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.           PXKUT052.                                  00020000
       AUTHOR.               D. WELLER                                  00030000
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00040000
       DATE-WRITTEN          02/2009.                                   00050000
       DATE-COMPILED.                                                   00060000
                                                                        00070000
      ******************************************************************00080000
      *   PXKUT052 - DELETE ROWS FROM THE PXT_STR_MDSE_OH_TRKG TABLE    00090000
      *              IN ORACLE IF STR_NBR COMPLETED A FULL INVENTORY    00100000
      *              TODAY OR IF STR_NBR CLOSED TODAY.                  00110007
      *                                                                 00120000
      *   THIS PROGRAM SEQUENTIALLY READS AN INPUT FILE (PXRD052)       00130006
      *   AND USES IT TO DELETE ROWS FROM THE ORACLE TABLE              00140000
      *   PXT_STR_MDSE_OH_TRKG.                                         00150000
      ******************** HISTORY OF CHANGES **************************00160000
      * CHG ID/                                                        *00170000
      * WR/PROJ          DATE        DESCRIPTION OF CHANGES            *00180000
      * ---------------  ----------  --------------------------------  *00190000
      * WR36056          03/01/2010  CREATE THE PROGRAM                 00200003
      * CHG ID/                                                        *00210011
      * WR/PROJ          DATE        DESCRIPTION OF CHANGES            *00211011
      * ---------------  ----------  --------------------------------  *00212011
      * CHG0212016       10/26/2018  MODIFIED PROGRAM TO EXTERNALIZE    00213012
      *                              ORACLE LOGON                       00214011
      *                              CHANGES TAGGED TO CHG016           00215012
      ******************************************************************00220000
                                                                        00230000
       ENVIRONMENT DIVISION.                                            00240000
                                                                        00250000
       CONFIGURATION SECTION.                                           00260000
                                                                        00270000
       SOURCE-COMPUTER. IBM-3090.                                       00280000
       OBJECT-COMPUTER. IBM-3090.                                       00290000
                                                                        00300000
       INPUT-OUTPUT SECTION.                                            00310000
       FILE-CONTROL.                                                    00320000
CHG016     SELECT ORACLE-LOGON-FILE                                     00330012
CHG016         ASSIGN TO ORALOGON.                                      00340012
                                                                        00350000
           SELECT INPUT-FILE                                            00360000
               ASSIGN TO INP02.                                         00370000
                                                                        00380000
       DATA DIVISION.                                                   00390000
                                                                        00400000
       FILE SECTION.                                                    00410000
CHG016 FD  ORACLE-LOGON-FILE                                            00420012
CHG016     RECORDING MODE IS F                                          00430012
CHG016     LABEL RECORDS ARE STANDARD                                   00440012
CHG016     BLOCK CONTAINS 0 RECORDS                                     00450012
CHG016     RECORD CONTAINS 80 CHARACTERS                                00460012
CHG016     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                      00461012
CHG016 01  OL-ORACLE-LOGON-RECORD          PIC X(80).                   00462012
                                                                        00470000
       FD  INPUT-FILE                                                   00480000
           LABEL RECORDS ARE STANDARD                                   00490000
           RECORDING MODE IS F                                          00500000
           BLOCK CONTAINS 0 RECORDS.                                    00510000
       01  INPUT-RECORD                    PIC X(12).                   00520005
                                                                        00530000
       WORKING-STORAGE SECTION.                                         00540000
                                                                        00550000
      ***************************************************************** 00560000
      *  INPUT RECORD LAYOUT                                            00570000
      ***************************************************************** 00580000
      *01  PX052-RECORD.                                                00590001
           COPY PXRD052.                                                00600001
                                                                        00610000
      *----------------------------------------------------------       00620000
      *                      O  R  A  C  L  E                           00630000
      *==========================================================       00640000
      *                                                                 00650000
      * ORACLE DECLARE SECTION                                          00660000
      * ORACLE USED AND RETURNED VARIABLES                              00670000
      *                                                                 00680000
      *----------------------------------------------------------       00690000
           COPY PXWS001.                                                00700000
                                                                        00710000
                                                                        00720000
      ***************************************************************** 00730000
      *  PROGRAM'S ACCUMULATORS                                       * 00740000
      ***************************************************************** 00750000
       01  PA-PROGRAM-ACCUMULATORS.                                     00760000
           05  PA-ROWS-DELETED             PIC 9(10)       VALUE ZEROES.00770001
           05  PA-ROWS-TO-COMMIT           PIC 9(10)       VALUE ZEROES.00790000
           05  PA-RECORDS-READ             PIC 9(10)       VALUE ZEROES.00800000
                                                                        00810000
      ***************************************************************** 00820000
      *  PROGRAM'S CONSTANTS                                          * 00830000
      ***************************************************************** 00840000
       01  PC-PROGRAM-CONSTANTS.                                        00850000
           05  PC-MAX-DEADLOCKS            PIC S9(04)      VALUE +3.    00860000
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE        00870000
               'PXKUT052'.                                              00880000
CHG016     05  PC-AUTO-LOGON               PIC X(1)        VALUE '/'.   00890012
                                                                        00910000
      ***************************************************************** 00920000
      *  PROGRAM'S SWITCHES                                           * 00930000
      ***************************************************************** 00940000
       01  PS-PROGRAM-SWITCHES.                                         00950000
           05  PS-END-OF-INP-FILE-SW       PIC X(01)       VALUE 'N'.   00960000
               88  PS-END-OF-INP-FILE                      VALUE 'Y'.   00970000
               88  PS-NOT-END-OF-INP-FILE                  VALUE 'N'.   00980000
           05  PS-END-OF-CONTROL-CARDS-SW  PIC X(01)       VALUE 'N'.   00990000
               88  PS-END-OF-CONTROL-CARDS                 VALUE 'Y'.   01000000
               88  PS-NOT-END-OF-CONTROL-CARDS             VALUE 'N'.   01010000
           05  PS-ROW-PROCESSED-SW         PIC X(01)       VALUE 'N'.   01020000
               88  PS-ROW-PROCESSED                        VALUE 'Y'.   01030000
               88  PS-ROW-NOT-PROCESSED                    VALUE 'N'.   01040000
           05  PS-ROW-FOUND-SW             PIC X(01)       VALUE 'N'.   01050000
               88  PS-ROW-FOUND                            VALUE 'Y'.   01060000
               88  PS-ROW-NOT-FOUND                        VALUE 'N'.   01070000
           05  PS-REQUEST-STATUS           PIC X(08)       VALUE SPACES.01080000
               88  PS-DELETE-REQUEST                       VALUE        01090001
                   'DELETE  '.                                          01100001
           05  PS-OPERATION-ATTEMPTED      PIC X(40)       VALUE SPACES.01150000
               88  PS-DEADLOCK-MSG                         VALUE        01160000
                   'MAXIMUM DEADLOCK COUNT EXCEEDED         '.          01170000
               88  PS-COMMIT-MSG                           VALUE        01220000
                   'COMMIT FAILED                           '.          01230000
               88  PS-ORA-LOGIN-MSG                        VALUE        01240000
                   'ORACLE LOGIN FAILED                     '.          01250000
               88  PS-GET-RESTART-MSG                      VALUE        01300000
                   'GETTING RESTARTING INFORMATION FAILED   '.          01310000
               88  PS-CHECK-RESTART-MSG                    VALUE        01320000
                   'CHECKING RESTARTING CONTROL FAILED      '.          01330000
CHG016     05  PS-END-OF-LOGON-FILE-SW          PIC  X(01) VALUE 'N'.   01331012
CHG016         88 PS-END-OF-LOGON-FILE                     VALUE 'Y'.   01332012
CHG016     05  PS-ORA-SRC-CTL-DONE-SW           PIC  X(01) VALUE 'N'.   01333012
CHG016         88  PS-ORACLE-SRC-EMPTY                     VALUE 'Y'.   01334012
                                                                        01340000
CHG016******************************************************************01341012
CHG016*      ORACLE CONNECTION VARIABLES                                01342012
CHG016******************************************************************01343012
CHG016 01  CC-ORACLE-CONNECTION.                                        01344012
CHG016     05  CC-ORACLE-USERID             PIC X(40).                  01345012
CHG016         88 CC-AUTO-LOGON                            VALUE '/'.   01346012
CHG016     05  CC-ORACLE-PASSWORD           PIC X(40).                  01347012
CHG016                                                                  01348012
      ***************************************************************** 01350000
      *  PROGRAM'S VARIABLES                                          * 01360000
      ***************************************************************** 01370000
       01  PV-PROGRAM-VARIABLES.                                        01380000
           05  PV-STR-NBR                  PIC  X(05)  VALUE SPACES.    01390001
           05  PV-TABLE-NAME               PIC  X(30)  VALUE SPACES.    01400000
           05  PV-RETRY-COUNT              PIC S9(04)  VALUE ZEROES.    01410000
           05  PV-PARAGRAPH-NAME           PIC X(40)   VALUE SPACES.    01420000
           05  PV-OPERATION-MSG            PIC  X(60)  VALUE SPACES.    01430000
           05  PV-OPERATION-MSG-1          PIC  X(40)  VALUE SPACES.    01440000
           05  PV-OPERATION-MSG-2          PIC  X(40)  VALUE SPACES.    01450000
           05  PV-EVNT-CDE                 PIC X(03)   VALUE SPACES.    01460000
           05  PV-NULL-RETURN              PIC S9(04)  VALUE ZEROES     01470000
                               COMP.                                    01480000
           05  PV-COMMIT-MAX               PIC S9(05)  VALUE ZEROES     01490000
                               COMP.                                    01500000
CHG016     05  PV-ORACLE-USERID-LEN        PIC S9(4)   VALUE ZEROS      01501012
CHG016                                                 COMP SYNC.       01502012
CHG016     05  PV-ORACLE-PASSWORD-LEN      PIC S9(4)   VALUE ZEROS      01503012
CHG016                                                 COMP SYNC.       01504012
                                                                        01510000
                                                                        01520000
      ***************************************************************** 01530000
      *  ABEND CODE AREA                                              * 01540000
      ***************************************************************** 01550000
       01  ABEND-CODE                      PIC S9(04)  VALUE +0  COMP.  01560000
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.     01570000
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     01580000
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.     01590000
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.     01600000
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.     01610000
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.     01620000
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.     01630000
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.     01640000
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.     01650000
           88  ABEND-4013-DB-ERROR                     VALUE +4013.     01660000
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.     01670000
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.     01680000
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.     01690000
                                                                        01700000
       01  WS-MSG-TEXT                     PIC X(512)      VALUE SPACES.01710000
       01  WS-MAX-SIZE                     PIC S9(09)      VALUE +512   01720000
                               COMP.                                    01730000
       01  WS-MSG-LENGTH                   PIC S9(09)      VALUE ZEROES 01740000
                               COMP.                                    01750000
                                                                        01760000
      ******************************************************************01770000
      *    ORACLE DECLARE SECTION                                       01780000
      ******************************************************************01790000
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                     01800000
                                                                        01810000
       01  PV-ORACLE-ID                    PIC  X(01)  VALUE '/'.       01820000
       01  USERNAME                        PIC X(10)      VARYING.      01830000
       01  PASSWD                          PIC X(10)      VARYING.      01840000
                                                                        01850000
      ******************************************************************01860000
      * COBOL DECLARATION FOR TABLE PXT_STR_MDSE_OH_TRKG               *01870002
      ******************************************************************01880000
      *    EXEC SQL                                                     01890004
      *        INCLUDE PXT0000?                                         01900007
      *    END-EXEC.                                                    01910004
                                                                        01920000
           EXEC SQL END DECLARE SECTION END-EXEC.                       01930000
      *                                                                 01940000
      * ORA COMMUNICATION AREA                                          01950000
      *                                                                 01960000
        01       SQLCA.                                                 01970000
                 05  SQLCAID               PIC X(8).                    01980000
                 05  SQLCABC               PIC S9(9) COMPUTATIONAL.     01990000
                 05  SQLCODE               PIC S9(9) COMPUTATIONAL.     02000000
                 05  SQLERRM.                                           02010000
                     49 SQLERRML           PIC S9(4) COMPUTATIONAL.     02020000
                     49 SQLERRMC           PIC X(70).                   02030000
                 05  SQLERRP               PIC X(8).                    02040000
                 05  SQLERRD OCCURS 6 TIMES                             02050000
                                           PIC S9(9) COMPUTATIONAL.     02060000
                 05  SQLWARN.                                           02070000
                     10 SQLWARN0           PIC X(1).                    02080000
                     10 SQLWARN1           PIC X(1).                    02090000
                     10 SQLWARN2           PIC X(1).                    02100000
                     10 SQLWARN3           PIC X(1).                    02110000
                     10 SQLWARN4           PIC X(1).                    02120000
                     10 SQLWARN5           PIC X(1).                    02130000
                     10 SQLWARN6           PIC X(1).                    02140000
                     10 SQLWARN7           PIC X(1).                    02150000
                 05  SQLEXT                PIC X(8).                    02160000
      ******************************************************************02170000
      * ORA COMMUNICATION AREA2                                         02180000
           COPY SQLCA2.                                                 02190000
                                                                        02200000
       PROCEDURE DIVISION.                                              02210000
                                                                        02220000
      ******************************************************************02230000
      *    MAINLINE LOGIC                                              *02240000
      ******************************************************************02250000
                                                                        02260000
       0000-MAINLINE.                                                   02270000
                                                                        02280000
           PERFORM 1000-INITIALIZATION.                                 02290000
                                                                        02300000
           PERFORM 2000-PROCESS-INPUT                                   02310000
               UNTIL PS-END-OF-INP-FILE.                                02320000
                                                                        02330000
           PERFORM 9900-EOJ-LOGIC.                                      02340000
                                                                        02350000
           GOBACK.                                                      02360000
                                                                        02370000
                                                                        02430000
      ******************************************************************02440000
      *    LOGON TO ORACLE, AND DECLARE THE ORACLE CURSORS.            *02450000
      ******************************************************************02460000
       1000-INITIALIZATION.                                             02470000
                                                                        02480000
           OPEN INPUT  ORACLE-LOGON-FILE.                               02490009
CHG016                                                                  02491012
CHG016     DISPLAY 'ORACLE FILE READ'.                                  02492012
CHG016     PERFORM 7500-READ-ORACLE-LOGON.                              02493012
CHG016                                                                  02494012
CHG016     DISPLAY 'ORACLE FILE CLOSE'.                                 02495012
CHG016     CLOSE ORACLE-LOGON-FILE.                                     02496012
CHG016                                                                  02497012
CHG016     IF ((PS-END-OF-LOGON-FILE)                                   02498012
CHG016              OR (CC-ORACLE-CONNECTION = SPACES))                 02499012
CHG016           DISPLAY '** ABEND **'                                  02499112
CHG016           DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '02499212
CHG016               'NOT PROVIDED'                                     02499312
CHG016           SET PS-ORACLE-SRC-EMPTY TO TRUE                        02499412
CHG016           CALL 'ILBOABN0' USING ABEND-CODE                       02499512
CHG016     END-IF.                                                      02499612
CHG016                                                                  02499712
CHG016         IF CC-AUTO-LOGON                                         02499812
CHG016             DISPLAY PC-PROGRAM-NAME  ' - AUTO LOGON TO ORACLE'   02499912
CHG016             EXEC SQL                                             02500012
CHG016               CONNECT  :PC-AUTO-LOGON                            02500112
CHG016             END-EXEC                                             02500212
CHG016     ELSE                                                         02500312
CHG016         INITIALIZE PV-ORACLE-USERID-LEN                          02500412
CHG016                    PV-ORACLE-PASSWORD-LEN                        02500512
CHG016           INSPECT    CC-ORACLE-USERID                            02500612
CHG016             TALLYING   PV-ORACLE-USERID-LEN                      02500712
CHG016             FOR CHARACTERS BEFORE INITIAL SPACE                  02500812
CHG016           INSPECT    CC-ORACLE-PASSWORD                          02500912
CHG016             TALLYING   PV-ORACLE-PASSWORD-LEN                    02501012
CHG016             FOR CHARACTERS BEFORE INITIAL SPACE                  02501112
CHG016         MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO        02501212
CHG016                    USERNAME-ARR                                  02501312
CHG016         MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                02501412
CHG016         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO    02501512
CHG016                      PASSWD-ARR                                  02501612
CHG016         MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                02501712
CHG016           DISPLAY PC-PROGRAM-NAME  ' - LOGON TO ORACLE AS USER ' 02501812
CHG016                      USERNAME-ARR                                02501912
CHG016            EXEC SQL                                              02502012
CHG016                 CONNECT :USERNAME IDENTIFIED BY :PASSWD          02502112
CHG016            END-EXEC                                              02502212
CHG016         INITIALIZE CC-ORACLE-PASSWORD                            02502312
CHG016                    PASSWD                                        02502412
CHG016     END-IF.                                                      02502512
CHG016                                                                  02502612
CHG016         IF SQLCODE NOT = 0                                       02502712
CHG016               DISPLAY '** CONNECTING TO ORACLE BY USERID **'     02502812
CHG016               DISPLAY 'TRIED TO CONNECT TO ORACLE UNDER: '       02502912
CHG016                                  PASSWD                          02503012
CHG016               SET ABEND-4014-ORACLE-LOGON-ERROR TO TRUE          02503112
CHG016               PERFORM Z100-ABEND                                 02503212
CHG016         END-IF                                                   02503312
CHG016                                                                  02503412
           OPEN INPUT  INPUT-FILE.                                      02504000
                                                                        02510000
           PERFORM 6100-READ-INPUT-FILE.                                02540000
           IF PS-END-OF-INP-FILE                                        02550000
               DISPLAY ' *** NOTHING TO PROCESS ***'                    02560000
               DISPLAY ' '                                              02570000
           END-IF.                                                      02580000
                                                                        02590000
      ******************************************************************02600000
      *    2000-PROCESS-INPUT                                          *02610000
      *      THIS IS PERFORMED ONLY VIA THE 0000-MAINLINE              *02620000
      ******************************************************************02670000
       2000-PROCESS-INPUT.                                              02680000
                                                                        02690000
           PERFORM 7000-ORACLE-DELETES.                                 02700001
                                                                        02710000
           PERFORM 6100-READ-INPUT-FILE.                                02720000
                                                                        02730000
      ******************************************************************02740000
      *  READ DELETE IN FILE                                            02750000
      ******************************************************************02760000
       6100-READ-INPUT-FILE.                                            02770000
                                                                        02780000
           READ INPUT-FILE INTO PX052-RECORD                            02790001
               AT END SET PS-END-OF-INP-FILE TO TRUE.                   02800000
                                                                        02810000
           IF PS-NOT-END-OF-INP-FILE                                    02820000
               ADD 1 TO PA-RECORDS-READ                                 02830000
           END-IF.                                                      02840000
                                                                        02850000
      ******************************************************************02860000
      *  DO DELETES IN ORACLE                                           02870000
      ******************************************************************02880000
       7000-ORACLE-DELETES.                                             02890001
                                                                        02900000
           MOVE '7000-ORACLE-DELETES' TO PV-PARAGRAPH-NAME.             02910001
           MOVE PX052-STR-NBR          TO PV-STR-NBR.                   02920002
                                                                        02930002
           EXEC SQL                                                     02940002
               DELETE  FROM                                             02950002
                       PXT_STR_MDSE_OH_TRKG                             02960002
                 WHERE                                                  02980002
                       STR_NBR     = :PV-STR-NBR                        02990002
           END-EXEC.                                                    03020002
                                                                        03030002
           EVALUATE TRUE                                                03040002
               WHEN SQLCODE = 0                                         03050002
                   ADD +1              TO PA-ROWS-DELETED               03051002
                   DISPLAY '=== STORE DELETED ==='                      03052002
                   DISPLAY 'STR-NBR:  ' PV-STR-NBR                      03053002
                   PERFORM 8000-CHECK-COMMIT                            03055002
      * ORACLE RETURNS SQLCODE = 1403 WHEN NO ROW IS FOUND              03056002
               WHEN SQLCODE = 1403                                      03057002
                   DISPLAY '--- STORE NOT FOUND ---'                    03058002
                   DISPLAY 'STR-NBR:  ' PV-STR-NBR                      03058102
                   PERFORM 8000-CHECK-COMMIT                            03059202
               WHEN OTHER                                               03059302
                   DISPLAY 'STR-NBR:  ' PV-STR-NBR                      03059402
                   DISPLAY '** RECORD:         ' PA-RECORDS-READ        03060002
                   PERFORM 9990-SQL-ERROR                               03060102
           END-EVALUATE.                                                03060202
                                                                        03061000
CHG016****************************************************************  03062012
CHG016* LOGON TO ORACLE                                                 03063012
CHG016****************************************************************  03064012
CHG016 7500-READ-ORACLE-LOGON.                                          03065012
CHG016                                                                  03066012
CHG016     READ ORACLE-LOGON-FILE                                       03067012
CHG016         INTO CC-ORACLE-CONNECTION                                03068012
CHG016         AT END                                                   03069012
CHG016             SET PS-END-OF-LOGON-FILE TO TRUE                     03070012
CHG016     END-READ.                                                    03080012
CHG016                                                                  03090012
      ******************************************************************04410000
      *    COMMIT-CHANGES.                                             *04420000
      *      PURPOSE: COMMIT CHANGES TO THUS FAR                       *04430000
      ******************************************************************04440000
       8000-CHECK-COMMIT.                                               04450000
                                                                        04460000
           ADD +1                        TO PA-ROWS-TO-COMMIT.          04470000
           IF PA-ROWS-TO-COMMIT > PV-COMMIT-MAX                         04471002
               PERFORM 8800-COMMIT-CHANGES                              04472002
           END-IF.                                                      04473002
                                                                        04480000
      ******************************************************************04490000
      * UPDATE CHECKPOINT INFORMATION TABLE. TAKE A COMMIT.             04500000
      ******************************************************************04510000
       8800-COMMIT-CHANGES.                                             04520000
                                                                        04530000
           EXEC SQL                                                     04540000
               COMMIT                                                   04550000
           END-EXEC.                                                    04560000
                                                                        04570000
           IF SQLCODE = 0                                               04580000
               MOVE ZEROES                     TO PA-ROWS-TO-COMMIT     04590000
           ELSE                                                         04600000
               MOVE  '8800-COMMIT-CHANGES    ' TO PV-PARAGRAPH-NAME     04610000
               SET PS-COMMIT-MSG               TO TRUE                  04620000
               PERFORM 9990-SQL-ERROR                                   04630000
           END-IF.                                                      04640000
                                                                        04650000
      ******************************************************************04660000
      *    9900-EOJ-LOGIC.                                              04670000
      *      THIS IS PERFORMED AT END OF INPUT FILE.                    04680000
      *      PURPOSE: CLOSE INPUT FILE,                                 04690000
      *      DISPLAY RECORD COUNTS.                                     04700000
      ******************************************************************04710000
       9900-EOJ-LOGIC.                                                  04720000
                                                                        04730000
           IF PA-RECORDS-READ > ZEROES                                  04740000
                                                                        04750000
               PERFORM 8800-COMMIT-CHANGES                              04760000
                                                                        04770000
           END-IF.                                                      04780000
                                                                        04790000
           CLOSE INPUT-FILE.                                            04810000
                                                                        04820000
           DISPLAY ' '.                                                 04830000
           DISPLAY 'RECORDS READ       ' PA-RECORDS-READ.               04840000
           DISPLAY 'ROWS DELETED       ' PA-ROWS-DELETED.               04850002
           DISPLAY '**------ END ' PC-PROGRAM-NAME ' ------**'.         04870000
                                                                        04880000
      ******************************************************************04890000
      * DISPLAYS ERRORS ENCOUNTERED ON SQL REQUEST                      04900000
      ******************************************************************04910000
       9990-SQL-ERROR.                                                  04920000
                                                                        04930000
           MOVE SQLCODE    TO SQLCODE2.                                 04940000
           MOVE SQLERRD(3) TO SQLERRD3.                                 04950000
           DISPLAY '** ABEND **       ** = SQLERROR'.                   04960000
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.           04970000
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.         04980000
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  04990000
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  05000000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   05010000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  05020000
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                05030000
           DISPLAY '** OPERATION      ** = ' PS-OPERATION-ATTEMPTED.    05040000
                                                                        05050000
           MOVE SPACES TO WS-MSG-TEXT.                                  05060000
      *    GET FULL TEXT OF ERROR MESSAGE                               05070000
                                                                        05080000
           CALL 'SQLGLM' USING WS-MSG-TEXT,                             05090000
                               WS-MAX-SIZE,                             05100000
                               WS-MSG-LENGTH.                           05110000
           DISPLAY 'MESSAGE TEXT      ** = ' WS-MSG-TEXT.               05120000
                                                                        05130000
           EXEC SQL                                                     05140000
               ROLLBACK WORK RELEASE                                    05150000
           END-EXEC.                                                    05160000
                                                                        05170000
           SET ABEND-4013-DB-ERROR  TO TRUE.                            05180000
                                                                        05190000
           PERFORM Z100-ABEND.                                          05200000
                                                                        05210000
      *-----------------------------------------------------------      05220000
      * Z100-ABEND                                                      05230000
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE              05240000
      *-----------------------------------------------------------      05250000
       Z100-ABEND.                                                      05260000
                                                                        05270000
           DISPLAY '                       '.                           05280000
           DISPLAY '** ABEND **       ** = SQLERROR'.                   05290000
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.           05300000
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                   05310000
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                05320000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   05330000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  05340000
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                05350000
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.         05360000
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.             05370000
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.          05380000
           DISPLAY '                       '.                           05390000
                                                                        05400000
           CALL 'ILBOABN0' USING ABEND-CODE.                            05410000
                                                                        05420000
      ******************************************************************05430000
      *    END OF SOURCE CODE FOR PXKUT052                             *05440000
      ******************************************************************05450000
