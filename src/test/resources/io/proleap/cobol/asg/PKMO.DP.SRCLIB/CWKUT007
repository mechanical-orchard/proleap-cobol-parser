      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT007.                                                
       AUTHOR.         THOMAS DUFFY.                                            
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/15/03.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  TERRITORY COMMON EXTRACT PROGRAM                              *        
      *  THIS PROGRAM CREATES A CHANGE EXTRACT FILE BY COMPARING       *        
      *  TWO GENERATIONS OF THE RMS COMMON EXTRACT FILE.               *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CW-CURRENT-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CW-PREVIOUS-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CW-CHANGE-FILE                                                
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CW-CURRENT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CURRENT-RECORD                  PIC X(57).                        
                                                                                
       FD  CW-PREVIOUS-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-PREVIOUS-RECORD                 PIC X(57).                        
                                                                                
       FD  CW-CHANGE-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CHANGE-RECORD                   PIC   X(58).                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CURRENT-REC.                                                         
           05  CR-CURR-KEY                    PIC   X(04).                      
           05  CR-CURR-DATA                   PIC   X(53).                      
                                                                                
       01  PREVIOUS-REC.                                                        
           05  PR-PREV-KEY                    PIC   X(04).                      
           05  PR-PREV-DATA                   PIC   X(53).                      
                                                                                
       01  CHANGE-OUTPUT-REC.                                                   
           05  COR-CHANGE-KEY                 PIC   X(04).                      
           05  COR-CHANGE-DATA                PIC   X(53).                      
           05  COR-CHANGE-INDICATOR           PIC   X(01).                      
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT007'.            
           05  PC-OPERCO                 PIC X(02) VALUE '03'.                  
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-UPDATE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL CR-CURR-KEY = HIGH-VALUES                                  
                     AND                                                        
                     PR-PREV-KEY = HIGH-VALUES.                                 
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  CW-CURRENT-FILE                                          
                       CW-PREVIOUS-FILE                                         
                OUTPUT CW-CHANGE-FILE.                                          
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
           INITIALIZE CHANGE-OUTPUT-REC.                                        
           IF CR-CURR-KEY  =  PR-PREV-KEY                                       
               PERFORM 2100-CHECK-EQUALITY                                      
           ELSE                                                                 
               IF CR-CURR-KEY  <  PR-PREV-KEY                                   
                   MOVE 'I'           TO COR-CHANGE-INDICATOR                   
                   MOVE CR-CURR-KEY   TO COR-CHANGE-KEY                         
                   MOVE CR-CURR-DATA  TO COR-CHANGE-DATA                        
                   ADD +1             TO PA-INSERT-RECORDS                      
                   PERFORM 7100-WRITE                                           
                   PERFORM 8000-READ-CURRENT                                    
               ELSE                                                             
                   MOVE 'D'           TO COR-CHANGE-INDICATOR                   
                   MOVE PR-PREV-KEY   TO COR-CHANGE-KEY                         
                   MOVE PR-PREV-DATA  TO COR-CHANGE-DATA                        
                   ADD +1             TO PA-DELETE-RECORDS                      
                   PERFORM 7100-WRITE                                           
                   PERFORM 8100-READ-PREVIOUS                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS RECORD FOR EQUALITY                                             
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2100-CHECK-EQUALITY.                                                     
           IF CR-CURR-DATA  =  PR-PREV-DATA                                     
               ADD +1                 TO PA-NO-CHANGE-RECORDS                   
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           ELSE                                                                 
               MOVE 'U'               TO COR-CHANGE-INDICATOR                   
               MOVE CR-CURR-KEY       TO COR-CHANGE-KEY                         
               MOVE CR-CURR-DATA      TO COR-CHANGE-DATA                        
               ADD +1                 TO PA-UPDATE-RECORDS                      
               PERFORM 7100-WRITE                                               
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT RECORD                                                
      ******************************************************************        
       7100-WRITE.                                                              
           WRITE  CW-CHANGE-RECORD FROM CHANGE-OUTPUT-REC.                      
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
           READ CW-CURRENT-FILE                                                 
               INTO CURRENT-REC                                                 
               AT END                                                           
                   MOVE HIGH-VALUES  TO  CR-CURR-KEY                            
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-CUR-RECORDS                     
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
           READ CW-PREVIOUS-FILE                                                
               INTO PREVIOUS-REC                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  PR-PREV-KEY                            
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-PRE-RECORDS                     
           END-READ.                                                            
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'INPUT PREVIOUS RECS: ' PA-INPUT-PRE-RECORDS.                
           DISPLAY 'INPUT CURRENT RECS : ' PA-INPUT-CUR-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
           DISPLAY 'INSERT RECS WRITTEN: ' PA-INSERT-RECORDS.                   
           DISPLAY 'UPDATE RECS WRITTEN: ' PA-UPDATE-RECORDS.                   
           DISPLAY 'DELETE RECS WRITTEN: ' PA-DELETE-RECORDS.                   
           DISPLAY 'NO CHANGE RECORDS  : ' PA-NO-CHANGE-RECORDS.                
                                                                                
           CLOSE CW-CURRENT-FILE                                                
                 CW-PREVIOUS-FILE                                               
                 CW-CHANGE-FILE.                                                
                                                                                
      ******************************************************************        
      ******************************************************************        
