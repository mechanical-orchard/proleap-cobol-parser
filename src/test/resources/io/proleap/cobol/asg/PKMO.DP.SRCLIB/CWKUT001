      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    CWKUT001.                                                 
       AUTHOR.        CARL RICHTER.                                             
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04/14/03.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *    EDW DUMMY SKU EXTRACT                                                
      ******************************************************************        
      *    THIS PROGRAM WILL CREATE A DUMMY SKU/UPC RECORD AND A                
      *    DUMMY VENDOR STYLE RECORD FO EACH EXISTING SUBLCLASS ON THE          
      *    RMS "SUBCLASS" COMMON EXTRACT FILE.                                  
      *                                                                         
      *    CHANGE LOG :  THOMAS DUFFY  03/22/2004   WR#25199                    
      *                  CHANGED TO USE COPYBOOK CWRD001                        
      *                                                                         
      *    CHANGE LOG :  THOMAS DUFFY  06/01/2005   WR#25199                    
      *                  CHANGED COPYBOOK CWRD001 TO INCLUDE HEIRARCHY          
      *                  INFORMATION - INCREASED SIZE TO 427.                   
      *                  MIKE STOCKARD 01/17/2006    WR#29787                   
      *                  CHANGED PRVT-LBL-IND TO LBL-TYP-CDE                    
      *                                                                         
      *    CHANGE LOG :  MARIBETH ZAMORA 10/01/08   PROJ ID 1526/WR32284        
      *                  CHANGED FOR THE AD ROTATION PROJECT (VENDOR            
      *                  STYLE REWRITE)                                         
      *                                                                         
      *    CHANGE LOG :  LUKE SCHULTE    04/03/09   PROJ ID 2313                
      *                  CHANGED FOR THE QUANTUM PROJECT                        
      *                                                                         
      *    CHANGE LOG :  JEAN KURK       02/14/23   PROJ CHG0281369             
      *                  CHANGE CALL TO UPC CHECK DIGIT ROUTINE DYNAMIC         
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CW-SUBCLASS-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CW-SKU-UPC-FILE                                               
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT CW-VENDOR-STYLE-FILE                                          
               ASSIGN TO OUT02.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CW-SUBCLASS-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CW-SUBCLASS-REC                 PIC X(73).                           
                                                                                
       FD  CW-SKU-UPC-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CW-SKU-UPC-REC               PIC X(343).                             
                                                                                
       FD  CW-VENDOR-STYLE-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CW-VENDOR-STYLE-REC                PIC X(107).                       
                                                                                
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(8)  VALUE 'CWKUT001'.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW        PIC X      VALUE 'N'.                   
               88  PS-END-OF-FILE                  VALUE 'Y'.                   
               88  PS-NOT-END-OF-FILE              VALUE 'N'.                   
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORD-COUNT             PIC 9(09)  VALUE 0.            
           05  PA-OUTPUT-RECORD-COUNT-SKU       PIC 9(09)  VALUE 0.             
           05  PA-OUTPUT-RECORD-COUNT-VENDOR    PIC 9(09)  VALUE 0.             
                                                                                
       01  ABEND-CODE                      PIC S9(04)    VALUE ZERO             
                                                         COMP SYNC.             
       01  PROGRAM-VARIABLES.                                                   
                                                                                
           05  PV-IN-COUNTER1-SKU           PIC 9(9)    VALUE 0.                
           05  PV-OUT-COUNTER1-SKU          PIC 9(9)    VALUE 0.                
           05  PV-IN-COUNTER1-VENDOR        PIC 9(9)    VALUE 0.                
           05  PV-OUT-COUNTER1-VENDOR       PIC 9(9)    VALUE 0.                
                                                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
           05  PV-SKU                       PIC 9(8).                           
           05  PV-SKU-CHAR REDEFINES PV-SKU.                                    
               10  PV-SKU-DEPT              PIC X(3).                           
               10  PV-SKU-SCL               PIC X(2).                           
               10  PV-SKU-000               PIC X(3).                           
                                                                                
                                                                                
      ****                                                                      
      *   UPC CHECK DIGIT PARAMETER LIST                                        
      ****                                                                      
                                                                                
           COPY DPWS040I.                                                       
                                                                                
      ****                                                                      
      *   SUBCLASS RECORD LAYOUT                                                
      ****                                                                      
                                                                                
           COPY HMRD014.                                                        
                                                                                
      ****                                                                      
      *   DUMMY VENDOR RECORD LAYOUT                                            
      ****                                                                      
                                                                                
           COPY CWRD001.                                                        
                                                                                
      ****                                                                      
      *   DUMMY SKU/DUMMY UPC RECORD LAYOUT.                                    
      ****                                                                      
                                                                                
           COPY CWRD002.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-FILE                                            
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  OPEN ALL FILES.                                                        
      *  READ THE FIRST RECORD FROM THE INPUT FILE.                             
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  CW-SUBCLASS-FILE                                         
                OUTPUT CW-SKU-UPC-FILE                                          
                       CW-VENDOR-STYLE-FILE.                                    
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-SUBCLASS-FILE.                                     
                                                                                
      ******************************************************************        
      *  FORMAT AND WRITE THE OUTPUT RECORDS.                                   
      *  READ THE NEXT INPUT RECORD.                                            
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           INITIALIZE CW001-RECORD.                                             
           INITIALIZE CW002-RECORD.                                             
                                                                                
           MOVE HM014-SUB-CL-DEPT-NBR   TO  CW001-DEPT-NBR                      
                                            CW002-DEPT-NBR.                     
           MOVE HM014-SUB-CL-MAJ-CL-NBR TO  CW001-MAJ-CL-NBR                    
                                            CW002-MAJ-CL-NBR.                   
           MOVE HM014-SUB-CL-NBR        TO  CW001-SUB-CL-NBR                    
                                            CW002-SUB-CL-NBR.                   
           MOVE HM014-SUB-CL-DEPT-NBR(2:3) TO PV-SKU-DEPT.                      
           MOVE HM014-SUB-CL-NBR(3:2)   TO  PV-SKU-SCL.                         
           MOVE '000'                   TO  PV-SKU-000.                         
           MOVE PV-SKU                  TO  CW002-SKU-NBR.                      
           MOVE 0                       TO  CW001-ENT-ID                        
                                            CW001-RMS-STYL-ID                   
                                            CW002-RMS-STYL-ID                   
                                            CW002-CORP-RTL-AMT                  
                                            CW002-CORP-COST-AMT                 
                                            CW002-CORP-GP-PRIC-ID.              
           MOVE '00'                    TO  CW002-SKU-STAT-CDE.                 
           MOVE 00001                   TO  CW002-KOHLS-SIZE-CDE.               
           MOVE '999'                   TO  CW002-NRF-CLOR-CDE.                 
           MOVE '0 '                    TO  CW002-SIZE-UM-CDE                   
                                            CW002-WT-UM-CDE                     
           MOVE 'ASSORTED'              TO  CW002-CSTM-CLOR-DESC                
                                            CW002-CUSTFACG-CLOR-DESC.           
           MOVE 'I'                     TO  CW002-UPC-TYP-CDE.                  
           MOVE 'G'                     TO  CW002-UPC-SRC-CDE.                  
           MOVE '9999-09-09'            TO  CW002-CERTD-DTE.                    
           MOVE '1900-01-01'            TO  CW002-EFF-BEG-DTE.                  
           MOVE SPACE                   TO  CW001-LBL-TYP-CDE                   
                                            CW002-EDI-ORD-IND                   
                                            CW002-REGN-PRIC-IND                 
                                            CW002-RPLNMT-IND                    
                                            CW002-KY-ITM-IND                    
                                            CW002-POG-IND                       
                                            CW002-E-BUS-ITM-IND                 
                                            CW002-PREPK-IND                     
                                            CW002-CERTD-IND                     
                                            CW002-TRNSTN-IND                    
                                            CW002-WEB-EXCLV-IND                 
                                            CW002-PO-SHRT-CYC-IND               
                                            CW002-EXTD-SZ-CDE                   
                                            CW002-ORIG-CLR-DTE                  
                                            CW002-MKDN-CDE.                     
                                                                                
           PERFORM 4000-CREATE-INTERNAL-UPC.                                    
           PERFORM 8100-WRITE-VENDOR-STYLE-REC.                                 
           PERFORM 8200-WRITE-SKU-UPC-REC.                                      
                                                                                
           PERFORM 8000-READ-SUBCLASS-FILE.                                     
                                                                                
      ******************************************************************        
      *    CALL THE INTERNAL UPC CREATE ROUTINE PASSING IT THE DEPT,            
      *    CLASS AND SEQ #.                                                     
      ******************************************************************        
       4000-CREATE-INTERNAL-UPC.                                                
           SET  DP040I-GEN-INTERNAL-UPC-OPTION TO TRUE.                    CL**2
           MOVE HM014-SUB-CL-DEPT-NBR    TO DP040I-DEPARTMENT-NUMBER.           
           MOVE HM014-SUB-CL-NBR         TO DP040I-CLASS-NUMBER.                
           MOVE '000'                    TO DP040I-SEQUENCE-NUMBER.             
           CALL DP040I-UPC-CHECK-DIGIT-WO-IUPC                                  
                           USING DP040I-UPC-CHECK-DIGIT-PARMS.                  
                                                                                
           IF DP040I-NO-ERROR-DETECTED                                          
               MOVE DP040I-UPC-CODE   TO CW002-UPC-NBR                          
           ELSE                                                                 
               DISPLAY '**** WARNING ****'                                      
               DISPLAY 'BAD RETURN CODE FROM UPC CHECK DIGIT ROUTINE'           
               DISPLAY 'FOR SKU NUMBER: ' DP040I-SKU-NUMBER                     
               DISPLAY 'UPC CHECK ERR : ' DP040I-ERROR-INDICATOR                
               MOVE +4001 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
      ************************************************************              
      *  READ THE SUBCLASS FILE                                                 
      ************************************************************              
       8000-READ-SUBCLASS-FILE.                                                 
                                                                                
           READ CW-SUBCLASS-FILE INTO HMRD014-SUB-CL-RECORD                     
               AT END                                                           
                   SET PS-END-OF-FILE TO TRUE                                   
               NOT AT END                                                       
                   ADD 1          TO   PA-INPUT-RECORD-COUNT.                   
                                                                                
      ******************************************************************        
      *  WRITE A VENDOR STYLE RECORD                                            
      ******************************************************************        
       8100-WRITE-VENDOR-STYLE-REC.                                             
                                                                                
           WRITE CW-VENDOR-STYLE-REC                                            
               FROM CW001-RECORD.                                               
           ADD 1 TO PA-OUTPUT-RECORD-COUNT-VENDOR.                              
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE AN SKU/UPC RECORD.                                               
      ******************************************************************        
       8200-WRITE-SKU-UPC-REC.                                                  
           WRITE CW-SKU-UPC-REC                                                 
               FROM CW002-RECORD.                                               
                                                                                
           ADD 1 TO PA-OUTPUT-RECORD-COUNT-SKU.                                 
                                                                                
      ************************************************************              
      *  DISPLAY RECORD COUNTS AT END OF JOB                                    
      *  CLOSE THE FILES.                                                       
      ************************************************************              
       9000-END-OF-JOB.                                                         
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
                                                                                
           DISPLAY '   MW SALES RECORDS INPUT         = '                       
                    PA-INPUT-RECORD-COUNT.                                      
                                                                                
           DISPLAY '   VENDOR RECS OUTPUT      = '                              
                    PA-OUTPUT-RECORD-COUNT-VENDOR.                              
                                                                                
           DISPLAY '   SKU RECS OUTPUT = '                                      
                    PA-OUTPUT-RECORD-COUNT-SKU.                                 
           DISPLAY '   '.                                                       
                                                                                
           CLOSE CW-SUBCLASS-FILE                                               
                 CW-SKU-UPC-FILE                                                
                 CW-VENDOR-STYLE-FILE.                                          
                                                                                
