       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT156.                                                 
       AUTHOR.        SUE PARI.                                                 
       DATE-WRITTEN.  06/22/2004.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM SPLITS THE ASN APPLICATION FILE.  ANY ASN THAT   *        
      *  IS FOR DC 090 OR IS FLAGGED AS OCEAN IS A CORPORATE ASN, AND  *        
      *  IS PUT INTO A FILE FOR THE TRK APPLICATION.  ANY OTHER ASN IS *        
      *  PUT INTO A FILE FOR THE SNK APPLICATION.                      *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE1                                                  
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT OUTPUT-FILE2                                                  
               ASSIGN TO OUT02.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS ADVANCE-SHIP-NOTICE-DTL-RECORD.                       
                                                                                
           COPY SNRD001I.                                                       
                                                                                
                                                                                
       FD  OUTPUT-FILE1                                                         
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD1.                                       
                                                                                
       01  OUTPUT-RECORD1                   PIC X(350).                         
                                                                                
                                                                                
       FD  OUTPUT-FILE2                                                         
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD2.                                       
                                                                                
       01  OUTPUT-RECORD2                   PIC X(350).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-ASN-TABLE.                                                        
           05  WS-ASN-TABLE-SEGMENTS OCCURS 200 TIMES.                          
               10  WS-ASN-SEGMENT         PIC X(350).                           
                                                                                
       01  SUB                            PIC 9(3)    VALUE ZERO.               
                                                                                
       01  WS-ID-COUNT                    PIC 9(1)    VALUE ZERO.               
       01  WS-HL-COUNT                    PIC 9(1)    VALUE ZERO.               
                                                                                
       01  CORPORATE-ASN-SW               PIC X(1)    VALUE 'N'.                
           88  CORPORATE-ASN                          VALUE 'Y'.                
                                                                                
       01  END-OF-INPUT-FILE-SW           PIC X(1)    VALUE 'N'.                
           88  END-OF-INPUT-FILE                      VALUE 'Y'.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE.                                              
           OPEN OUTPUT OUTPUT-FILE1.                                            
           OPEN OUTPUT OUTPUT-FILE2.                                            
                                                                                
           INITIALIZE WS-ASN-TABLE.                                             
                                                                                
           PERFORM 4000-READ-INPUT-FILE                                         
              THRU 4000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-INPUT-FILE.                                       
                                                                                
           CLOSE INPUT-FILE.                                                    
           CLOSE OUTPUT-FILE1.                                                  
           CLOSE OUTPUT-FILE2.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-FILE.                                                       
                                                                                
           IF ID-IDENTIFICATION-REC                                             
              MOVE 'N' TO CORPORATE-ASN-SW                                      
              MOVE 1 TO WS-ID-COUNT                                             
              MOVE 0 TO WS-HL-COUNT                                             
              MOVE ZERO TO SUB                                                  
              PERFORM 2000-PROCESS-FIRST-HL                                     
                 THRU 2000-PROCESS-FIRST-HL-EXIT                                
                    UNTIL WS-ID-COUNT > 1 OR                                    
                          WS-HL-COUNT > 1 OR                                    
                          SUB > 199 OR                                          
                          END-OF-INPUT-FILE                                     
              PERFORM 3000-WRITE-FIRST-HL                                       
                 THRU 3000-WRITE-FIRST-HL-EXIT.                                 
                                                                                
           IF END-OF-INPUT-FILE OR ID-IDENTIFICATION-REC                        
              GO TO 1000-PROCESS-FILE-EXIT.                                     
                                                                                
           IF CORPORATE-ASN                                                     
              MOVE SN001I-SHIP-NOTICE-DTL-RECORD TO OUTPUT-RECORD2              
              WRITE OUTPUT-RECORD2                                              
           ELSE                                                                 
              MOVE SN001I-SHIP-NOTICE-DTL-RECORD TO OUTPUT-RECORD1              
              WRITE OUTPUT-RECORD1.                                             
                                                                                
           PERFORM 4000-READ-INPUT-FILE                                         
              THRU 4000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
       2000-PROCESS-FIRST-HL.                                                   
                                                                                
           IF N1-NAME-REC AND                                                   
           SN001I-N104-NME-ID-CDE = '90' OR '090' OR '0090' OR '00090'          
           OR '000090' OR '0000090' OR '00000090' OR '000000090'                
                 SET CORPORATE-ASN TO TRUE.                                     
           IF TD5-CARRIER-RTE-TRNST-REC AND                                     
              SN001I-TD504-TRANS-MTHD-CDE = 'S'                                 
                 SET CORPORATE-ASN TO TRUE.                                     
                                                                                
           ADD 1 TO SUB.                                                        
                                                                                
           MOVE SN001I-SHIP-NOTICE-DTL-RECORD TO                                
              WS-ASN-SEGMENT(SUB).                                              
                                                                                
           PERFORM 4000-READ-INPUT-FILE                                         
              THRU 4000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           IF END-OF-INPUT-FILE                                                 
              GO TO 2000-PROCESS-FIRST-HL-EXIT.                                 
                                                                                
           IF ID-IDENTIFICATION-REC                                             
              ADD 1 TO WS-ID-COUNT.                                             
           IF HL-HIERARCHICAL-REC                                               
              ADD 1 TO WS-HL-COUNT.                                             
                                                                                
       2000-PROCESS-FIRST-HL-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       3000-WRITE-FIRST-HL.                                                     
                                                                                
           PERFORM VARYING SUB FROM 1 BY 1                                      
              UNTIL SUB > 200 OR WS-ASN-SEGMENT(SUB) = SPACES                   
                 IF CORPORATE-ASN                                               
                    MOVE WS-ASN-SEGMENT(SUB) TO OUTPUT-RECORD2                  
                    WRITE OUTPUT-RECORD2                                        
                 ELSE                                                           
                    MOVE WS-ASN-SEGMENT(SUB) TO OUTPUT-RECORD1                  
                    WRITE OUTPUT-RECORD1                                        
                 END-IF                                                         
           END-PERFORM.                                                         
                                                                                
           INITIALIZE WS-ASN-TABLE.                                             
                                                                                
       3000-WRITE-FIRST-HL-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       4000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-INPUT-FILE TO TRUE.                                 
                                                                                
       4000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
