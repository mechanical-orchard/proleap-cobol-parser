      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.      PCKUP801.                                       00050000
       AUTHOR.          SURENDRA BABU PAKALA.                           00060000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00070000
       DATE-WRITTEN.    JANUARY, 2018.                                  00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      * THIS PROGRAM PURGES RECORDS THAT ARE MORE THAN 5 YEARS OLD     *00120000
      * FROM THE PCT_SUM_WRKD AND PCT_SUM_VRFD TABLES                  *00130000
      *                                                                *00140000
      * THIS PROGRAM WILL RUN YEARLY.                                  *00150000
      ******************************************************************00160000
       ENVIRONMENT DIVISION.                                            00170000
      ******************************************************************00180000
                                                                        00190000
       CONFIGURATION SECTION.                                           00200000
                                                                        00210000
       SOURCE-COMPUTER. IBM-3090.                                       00220000
       OBJECT-COMPUTER. IBM-3090.                                       00230000
                                                                        00240000
       INPUT-OUTPUT SECTION.                                            00250000
                                                                        00260000
       FILE-CONTROL.                                                    00270000
                                                                        00280000
           SELECT INPUT-PURGE-DATE-FILE ASSIGN TO IN01.                 00290000
                                                                        00300000
                                                                        00310000
      ******************************************************************00320000
       DATA DIVISION.                                                   00330000
      ******************************************************************00340000
                                                                        00350000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  INPUT-PURGE-DATE-FILE                                        00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           RECORDING MODE IS F                                          00400000
           BLOCK CONTAINS 0 RECORDS                                     00410000
           RECORD CONTAINS 80 CHARACTERS                                00420000
           DATA RECORD IS INPUT-PURGE-DATE-RECORD.                      00430000
                                                                        00440000
       01  INPUT-PURGE-DATE-RECORD         PIC X(80).                   00450000
                                                                        00460000
       WORKING-STORAGE SECTION.                                         00470000
                                                                        00480000
      ******************************************************************00490000
      * PC PROGRAM CONSTANTS                                           *00500000
      ******************************************************************00510000
       01  PC-PROGRAM-CONSTANTS.                                        00520000
           05  PC-PROGRAM-NAME             PIC X(08) VALUE 'PCKUP801'.  00530000
           05  PC-COMMIT-COUNT             PIC 9(09) VALUE 300.         00540000
                                                                        00550000
      ******************************************************************00560000
      * PV PROGRAM VARIABLES                                           *00570000
      ******************************************************************00580000
       01  PV-PROGRAM-VARIABLES.                                        00590000
           05  PV-PARAGRAPH-NAME             PIC X(30) VALUE SPACES.    00600000
           05  PV-DB2-OPERATION              PIC X(30) VALUE SPACES.    00610000
           05  PV-PURGE-DATE                 PIC X(10) VALUE SPACES.    00620000
           05  PV-DISPLAY-COUNT              PIC Z(8)9 VALUE ZEROS.     00630000
                                                                        00640000
      ******************************************************************00650000
      * PS PROGRAM SWITCHES                                            *00660000
      ******************************************************************00670000
       01  PS-PROGRAM-SWITCHES.                                         00680000
           05  PS-END-OF-PURGE-CSR-SW        PIC X(01) VALUE 'N'.       00690000
               88  PS-END-OF-PURGE-CSR                 VALUE 'Y'.       00700000
               88  PS-NOT-END-OF-PURGE-CSR             VALUE 'N'.       00710000
                                                                        00720000
                                                                        00730000
      ******************************************************************00740000
      * PA PROGRAM ACCUMULATORS                                        *00750000
      ******************************************************************00760000
       01  PA-PROGRAM-ACCUMULATORS.                                     00770000
           05  PA-WRKD-FETCH-COUNT           PIC 9(09) VALUE ZEROES.    00780000
           05  PA-WRKD-TOTAL-FETCH-COUNT     PIC 9(09) VALUE ZEROES.    00790000
           05  PA-WRKD-DELETE-COUNT          PIC 9(09) VALUE ZEROES.    00800000
           05  PA-WRKD-TOTAL-DELETE-COUNT    PIC 9(09) VALUE ZEROES.    00810000
           05  PA-WRKD-COMMIT-COUNT          PIC 9(09) VALUE ZEROES.    00820000
           05  PA-VRFD-FETCH-COUNT           PIC 9(09) VALUE ZEROES.    00830000
           05  PA-VRFD-TOTAL-FETCH-COUNT     PIC 9(09) VALUE ZEROES.    00840000
           05  PA-VRFD-DELETE-COUNT          PIC 9(09) VALUE ZEROES.    00850000
           05  PA-VRFD-TOTAL-DELETE-COUNT    PIC 9(09) VALUE ZEROES.    00860000
           05  PA-VRFD-COMMIT-COUNT          PIC 9(09) VALUE ZEROES.    00870000
                                                                        00880000
       01 PT-PC-SUM.                                                    00890000
           05  PT-OPER-UNT-ID                PIC S9(4) COMP             00900000
                                                   OCCURS 100 TIMES.    00910000
           05  PT-SHFT-NBR                   PIC S9(4) COMP             00920000
                                                   OCCURS 100 TIMES.    00930000
           05  PT-BUS-DTE                    PIC X(10)                  00930100
                                                   OCCURS 100 TIMES.    00930200
           05  PT-PRCG-AREA-CDE              PIC X(6)                   00930300
                                                   OCCURS 100 TIMES.    00930400
      ******************************************************************00930500
      * COPY BOOK FOR DRIVER DATE FILE                                  00930600
      ******************************************************************00930700
                                                                        00930800
           COPY PCRD268.                                                00930900
                                                                        00931000
      ******************************************************************00932000
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *00933000
      ******************************************************************00934000
                                                                        00935000
           COPY DPWS004.                                                00936000
                                                                        00937000
      ******************************************************************00938000
      *    SQL COMMUNICATIONS AREA DCLGEN                              *00939000
      ******************************************************************00940000
                                                                        00950000
           EXEC SQL                                                     00960000
               INCLUDE SQLCA                                            00970000
           END-EXEC.                                                    00980000
                                                                        00990000
           COPY SQLCA2.                                                 01000000
                                                                        01010000
      ******************************************************************01020000
      *    PCT_SUM_WRKD  TABLE                                          01030000
      ******************************************************************01040000
                                                                        01050000
           EXEC SQL                                                     01060000
               INCLUDE PCT00005                                         01070000
           END-EXEC.                                                    01080000
                                                                        01090000
                                                                        01100000
      ******************************************************************01110000
      *    PCT_SUM_VRFD TABLE                                           01120000
      ******************************************************************01130000
                                                                        01140000
           EXEC SQL                                                     01150000
               INCLUDE PCT00006                                         01151000
           END-EXEC.                                                    01152000
                                                                        01153000
                                                                        01154000
                                                                        01155000
      ******************************************************************01156000
      *    CURSOR TO FECTCH THE RECORDS WHICH MEET THE PURGE CRITERIA   01157000
      *    FROM PCT_SUM_WRKD TABLE                                      01158000
      ******************************************************************01159000
                                                                        01160000
           EXEC SQL                                                     01170000
            DECLARE WRKD-PURGE-CSR CURSOR WITH ROWSET POSITIONING       01180000
                                                 WITH HOLD FOR          01190000
                SELECT BUS_DTE                                          01200000
                     , OPER_UNT_ID                                      01210000
                     , SHFT_NBR                                         01220000
                     , PRCG_AREA_CDE                                    01230000
                  FROM PCT_SUM_WRKD                                     01240000
                 WHERE BUS_DTE     < :PV-PURGE-DATE                     01250000
                 FOR UPDATE                                             01260000
           END-EXEC.                                                    01270000
                                                                        01280000
      ******************************************************************01290000
      *    CURSOR TO FECTCH THE RECORDS WHICH MEET THE PURGE CRITERIA   01300000
      *    FROM PCT_SUM_VRFD TABLE                                      01310000
      ******************************************************************01320000
                                                                        01330000
           EXEC SQL                                                     01340000
            DECLARE VRFD-PURGE-CSR CURSOR WITH ROWSET POSITIONING       01350000
                                                 WITH HOLD FOR          01360000
                SELECT BUS_DTE                                          01370000
                     , OPER_UNT_ID                                      01380000
                     , SHFT_NBR                                         01390000
                  FROM PCT_SUM_VRFD                                     01400000
                 WHERE BUS_DTE     < :PV-PURGE-DATE                     01410000
                 FOR UPDATE                                             01420000
           END-EXEC.                                                    01430000
                                                                        01440000
                                                                        01450000
      ******************************************************************01460000
      * AC ABEND CODES                                                 *01470000
      ******************************************************************01480000
       01  ABEND-CODE                      PIC S9(04) COMP VALUE +0.    01490000
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001. 01500000
           88  ABEND-4002-READ-ERROR                       VALUE +4002. 01510000
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003. 01520000
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004. 01530000
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005. 01540000
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006. 01550000
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007. 01560000
           88  ABEND-4008-DATA-ERROR                       VALUE +4008. 01570000
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009. 01580000
           88  ABEND-4013-DB2-ERROR                        VALUE +4013. 01590000
           88  ABEND-4019-DATE-ROUTINE-ERROR               VALUE +4019. 01600000
           88  ABEND-4444-FORCED-ABEND                     VALUE +4444. 01610000
                                                                        01620000
                                                                        01630000
      ******************************************************************01640000
       PROCEDURE DIVISION.                                              01650000
      ******************************************************************01660000
       0000-MAINLINE.                                                   01670000
                                                                        01680000
           PERFORM 1000-INITIALIZATION.                                 01690000
                                                                        01700000
           PERFORM 2000-PROCESS-WRKD-PURGE.                             01710000
                                                                        01720000
           PERFORM 4000-PROCESS-VRFD-PURGE.                             01730000
                                                                        01740000
           PERFORM 8000-EOJ-ROUTINE.                                    01750000
                                                                        01760000
           GOBACK.                                                      01770000
                                                                        01780000
      ******************************************************************01790000
      *  - OPEN THE OUTPUT FILE AND GET PURGE DATE                     *01800000
      ******************************************************************01810000
       1000-INITIALIZATION.                                             01820000
                                                                        01830000
           OPEN INPUT  INPUT-PURGE-DATE-FILE.                           01840000
                                                                        01850000
           READ INPUT-PURGE-DATE-FILE    INTO PC268-RECORD.             01860000
           MOVE PC268-PURGE-YR-BEG-DATE  TO PV-PURGE-DATE.              01870000
                                                                        01880000
                                                                        01890000
      ******************************************************************01900000
      * - PCT_SUM_WRKD TABLE PURGE PROCESS                             *01910000
      ******************************************************************01920000
       2000-PROCESS-WRKD-PURGE.                                         01930000
                                                                        01940000
           SET PS-NOT-END-OF-PURGE-CSR TO TRUE.                         01950000
                                                                        01960000
           PERFORM 2100-OPEN-WRKD-PURGE-CSR.                            01970000
                                                                        01980000
           PERFORM 2200-PROCESS-WRKD-PURGE-CSR                          01990000
               UNTIL PS-END-OF-PURGE-CSR.                               02000000
                                                                        02010000
           PERFORM 2300-CLOSE-WRKD-PURGE-CSR.                           02020000
                                                                        02030000
           IF PA-WRKD-DELETE-COUNT > 0                                  02040000
               PERFORM 7000-TAKE-COMMIT                                 02050000
           END-IF.                                                      02060000
                                                                        02070000
      ******************************************************************02080000
      *     2100-OPEN-WRKD-PURGE-CSR                                  * 02090000
      ******************************************************************02100000
       2100-OPEN-WRKD-PURGE-CSR.                                        02110000
           EXEC SQL                                                     02120000
                OPEN WRKD-PURGE-CSR                                     02130000
           END-EXEC.                                                    02140000
                                                                        02150000
           EVALUATE TRUE                                                02160000
               WHEN SQLCODE = ZERO                                      02170000
                    CONTINUE                                            02180000
               WHEN OTHER                                               02190000
                   MOVE '2100-OPEN-WRKD-PURGE-CSR'                      02200000
                                               TO PV-PARAGRAPH-NAME     02210000
                   MOVE 'OPEN WRKD-PURGE-CSR'                           02220000
                                               TO PV-DB2-OPERATION      02230000
                   PERFORM 9000-SQL-ABEND                               02240000
           END-EVALUATE.                                                02250000
                                                                        02260000
                                                                        02270000
      ******************************************************************02280000
      *     2200-PROCESS-WRKD-PURGE-CSR                                *02290000
      ******************************************************************02300000
       2200-PROCESS-WRKD-PURGE-CSR.                                     02310000
                                                                        02320000
           EXEC SQL                                                     02330000
                FETCH NEXT ROWSET                                       02340000
                FROM  WRKD-PURGE-CSR                                    02350000
                 FOR 100 ROWS                                           02360000
                 INTO :PT-BUS-DTE                                       02370000
                    , :PT-OPER-UNT-ID                                   02380000
                    , :PT-SHFT-NBR                                      02390000
                    , :PT-PRCG-AREA-CDE                                 02400000
           END-EXEC.                                                    02410000
                                                                        02420000
           EVALUATE TRUE                                                02430000
               WHEN SQLCODE = ZERO                                      02440000
                   ADD SQLERRD(3)     TO PA-WRKD-TOTAL-FETCH-COUNT      02450000
                   PERFORM 2900-DELETE-EXTRACT-RECORD                   02460000
               WHEN SQLCODE = +100                                      02470000
                   IF SQLERRD(3) > 0                                    02480000
                      ADD SQLERRD(3)  TO PA-WRKD-TOTAL-FETCH-COUNT      02490000
                      PERFORM 2900-DELETE-EXTRACT-RECORD                02500000
                   END-IF                                               02510000
                   SET PS-END-OF-PURGE-CSR TO TRUE                      02520000
               WHEN OTHER                                               02530000
                   MOVE '2200-PROCESS-WRKD-PURGE-CSR'                   02540000
                                               TO PV-PARAGRAPH-NAME     02550000
                   MOVE 'FETCH WRKD-PURGE-CSR'                          02560000
                                               TO PV-DB2-OPERATION      02570000
                   PERFORM 9000-SQL-ABEND                               02580000
           END-EVALUATE.                                                02590000
                                                                        02600000
      ******************************************************************02610000
      *     2300-CLOSE-WRKD-PURGE-CSR                                  *02620000
      ******************************************************************02630000
       2300-CLOSE-WRKD-PURGE-CSR.                                       02640000
                                                                        02650000
           EXEC SQL                                                     02660000
                CLOSE WRKD-PURGE-CSR                                    02670000
           END-EXEC.                                                    02680000
                                                                        02690000
           EVALUATE TRUE                                                02700000
               WHEN SQLCODE = ZERO                                      02710000
                    CONTINUE                                            02720000
               WHEN OTHER                                               02730000
                   MOVE '2300-CLOSE-WRKD-PURGE-CSR'                     02740000
                                               TO PV-PARAGRAPH-NAME     02750000
                   MOVE 'CLOSE WRKD-PURGE-CSR'                          02760000
                                               TO PV-DB2-OPERATION      02770000
                   PERFORM 9000-SQL-ABEND                               02780000
           END-EVALUATE.                                                02790000
                                                                        02800000
      ******************************************************************02810000
      *     2900-DELETE-EXTRACT-RECORD.                                 02820000
      *                                                                 02830000
      ******************************************************************02840000
       2900-DELETE-EXTRACT-RECORD.                                      02850000
                                                                        02860000
           EXEC SQL                                                     02870000
               DELETE FROM PCT_SUM_WRKD                                 02880000
                   WHERE CURRENT OF WRKD-PURGE-CSR                      02890000
           END-EXEC.                                                    02900000
                                                                        02910000
           EVALUATE TRUE                                                02920000
                WHEN SQLCODE = ZEROS                                    02930000
                     ADD SQLERRD(3)   TO PA-WRKD-DELETE-COUNT           02940000
                                         PA-WRKD-TOTAL-DELETE-COUNT     02950000
                     IF PA-WRKD-DELETE-COUNT >= PC-COMMIT-COUNT         02960000
                        PERFORM 7000-TAKE-COMMIT                        02970000
                     END-IF                                             02980000
                WHEN SQLCODE = +100                                     02990000
                     CONTINUE                                           03000000
                WHEN OTHER                                              03010000
                     MOVE '2900-DELETE-EXTRACT-RECORD'                  03020000
                                            TO PV-PARAGRAPH-NAME        03030000
                     MOVE 'DELETE FROM PCT_SUM_WRKD'                    03040000
                                            TO PV-DB2-OPERATION         03050000
                     PERFORM 9000-SQL-ABEND                             03060000
           END-EVALUATE.                                                03070000
                                                                        03080000
      ******************************************************************03090000
      * - PCT_SUM_VRFD TABLE PURGE PROCESS                             *03100000
      ******************************************************************03110000
       4000-PROCESS-VRFD-PURGE.                                         03120000
                                                                        03130000
            SET PS-NOT-END-OF-PURGE-CSR TO TRUE.                        03140000
                                                                        03150000
            PERFORM 4100-OPEN-VRFD-PURGE-CSR.                           03160000
                                                                        03170000
            PERFORM 4200-PROCESS-VRFD-PURGE-CSR                         03180000
              UNTIL PS-END-OF-PURGE-CSR.                                03190000
                                                                        03200000
            PERFORM 4300-CLOSE-VRFD-PURGE-CSR.                          03210000
                                                                        03220000
      ******************************************************************03230000
      *     4100-OPEN-VRFD-PURGE-CSR                                  * 03240000
      ******************************************************************03250000
       4100-OPEN-VRFD-PURGE-CSR.                                        03260000
           EXEC SQL                                                     03270000
                OPEN VRFD-PURGE-CSR                                     03280000
           END-EXEC.                                                    03290000
                                                                        03300000
           EVALUATE TRUE                                                03310000
               WHEN SQLCODE = ZERO                                      03320000
                    CONTINUE                                            03330000
               WHEN OTHER                                               03340000
                   MOVE '4100-OPEN-VRFD-PURGE-CSR'                      03350000
                                               TO PV-PARAGRAPH-NAME     03360000
                   MOVE 'OPEN VRFD-PURGE-CSR'                           03370000
                                               TO PV-DB2-OPERATION      03380000
                   PERFORM 9000-SQL-ABEND                               03390000
           END-EVALUATE.                                                03400000
                                                                        03410000
                                                                        03420000
      ******************************************************************03430000
      *     4200-PROCESS-VRFD-PURGE-CSR                                *03440000
      ******************************************************************03450000
       4200-PROCESS-VRFD-PURGE-CSR.                                     03460000
                                                                        03470000
           EXEC SQL                                                     03480000
                FETCH NEXT ROWSET                                       03490000
                FROM  VRFD-PURGE-CSR                                    03500000
                 FOR 100 ROWS                                           03510000
                 INTO :PT-BUS-DTE                                       03520000
                    , :PT-OPER-UNT-ID                                   03530000
                    , :PT-SHFT-NBR                                      03540000
           END-EXEC.                                                    03550000
                                                                        03560000
           EVALUATE TRUE                                                03570000
               WHEN SQLCODE = ZERO                                      03580000
                   ADD SQLERRD(3)     TO PA-VRFD-TOTAL-FETCH-COUNT      03590000
                   PERFORM 4900-DELETE-EXTRACT-RECORD                   03600000
               WHEN SQLCODE = +100                                      03610000
                   IF SQLERRD(3) > 0                                    03620000
                      ADD SQLERRD(3)  TO PA-VRFD-TOTAL-FETCH-COUNT      03630000
                      PERFORM 4900-DELETE-EXTRACT-RECORD                03631000
                   END-IF                                               03632000
                   SET PS-END-OF-PURGE-CSR TO TRUE                      03633000
               WHEN OTHER                                               03634000
                   MOVE '4200-PROCESS-WRKD-PURGE-CSR'                   03635000
                                               TO PV-PARAGRAPH-NAME     03636000
                   MOVE 'FETCH WRKD-PURGE-CSR'                          03637000
                                               TO PV-DB2-OPERATION      03637100
                   PERFORM 9000-SQL-ABEND                               03637200
           END-EVALUATE.                                                03637300
                                                                        03637400
                                                                        03637500
      ******************************************************************03637600
      *     4300-CLOSE-VRFD-PURGE-CSR                                  *03637700
      ******************************************************************03637800
       4300-CLOSE-VRFD-PURGE-CSR.                                       03637900
                                                                        03638000
           EXEC SQL                                                     03638100
                CLOSE VRFD-PURGE-CSR                                    03638200
           END-EXEC.                                                    03638300
                                                                        03638400
           EVALUATE TRUE                                                03638500
               WHEN SQLCODE = ZERO                                      03638600
                    CONTINUE                                            03638700
               WHEN OTHER                                               03638800
                   MOVE '4300-CLOSE-VRFD-PURGE-CSR'                     03638900
                                               TO PV-PARAGRAPH-NAME     03639000
                   MOVE 'CLOSE VRFD-PURGE-CSR'                          03639100
                                               TO PV-DB2-OPERATION      03639200
                   PERFORM 9000-SQL-ABEND                               03639300
           END-EVALUATE.                                                03639400
                                                                        03639500
      ******************************************************************03639600
      *     4900-DELETE-EXTRACT-RECORD.                                 03639700
      ******************************************************************03639800
       4900-DELETE-EXTRACT-RECORD.                                      03639900
                                                                        03640000
           EXEC SQL                                                     03640100
               DELETE FROM PCT_SUM_VRFD                                 03640200
                   WHERE CURRENT OF VRFD-PURGE-CSR                      03640300
           END-EXEC.                                                    03640400
                                                                        03640500
           EVALUATE TRUE                                                03640600
                WHEN SQLCODE = ZEROS                                    03640700
                     ADD SQLERRD(3)   TO PA-VRFD-DELETE-COUNT           03640800
                                         PA-VRFD-TOTAL-DELETE-COUNT     03640900
                     IF PA-VRFD-DELETE-COUNT >= PC-COMMIT-COUNT         03641000
                        PERFORM 7100-TAKE-COMMIT                        03641100
                     END-IF                                             03641200
                WHEN SQLCODE = +100                                     03641300
                     CONTINUE                                           03641400
                WHEN OTHER                                              03641500
                     MOVE '4900-DELETE-EXTRACT-RECORD'                  03641600
                                            TO PV-PARAGRAPH-NAME        03641700
                     MOVE 'DELETE FROM PCT_SUM_VRFD'                    03641800
                                            TO PV-DB2-OPERATION         03641900
                     PERFORM 9000-SQL-ABEND                             03642000
           END-EVALUATE.                                                03642100
                                                                        03642200
      ******************************************************************03642300
      * -   7000-TAKE-COMMIT - PCT_SUM_WRKD TABLE                      *03642400
      ******************************************************************03642500
       7000-TAKE-COMMIT.                                                03642600
                                                                        03642700
           EXEC SQL                                                     03642800
               COMMIT                                                   03642900
           END-EXEC.                                                    03643000
                                                                        03643100
           EVALUATE TRUE                                                03643200
                WHEN SQLCODE = ZEROS                                    03643300
                     MOVE 0   TO PA-WRKD-DELETE-COUNT                   03643400
                     ADD +1   TO PA-WRKD-COMMIT-COUNT                   03643500
                WHEN OTHER                                              03643600
                     MOVE '7000-TAKE-COMMIT'                            03643700
                                            TO PV-PARAGRAPH-NAME        03643800
                     MOVE 'COMMIT'                                      03643900
                                            TO PV-DB2-OPERATION         03644000
                     PERFORM 9000-SQL-ABEND                             03644100
           END-EVALUATE.                                                03644200
                                                                        03644300
      ******************************************************************03644400
      * -   7100-TAKE-COMMIT  - PCT_SUM_VRFD TABLE                  *   03644500
      ******************************************************************03644600
       7100-TAKE-COMMIT.                                                03644700
                                                                        03644800
           EXEC SQL                                                     03644900
               COMMIT                                                   03645000
           END-EXEC.                                                    03645100
                                                                        03645200
           EVALUATE TRUE                                                03645300
                WHEN SQLCODE = ZEROS                                    03645400
                     MOVE 0   TO PA-VRFD-DELETE-COUNT                   03645500
                     ADD +1   TO PA-VRFD-COMMIT-COUNT                   03645600
                WHEN OTHER                                              03645700
                     MOVE '7100-TAKE-COMMIT'                            03645800
                                            TO PV-PARAGRAPH-NAME        03645900
                     MOVE 'COMMIT'                                      03646000
                                            TO PV-DB2-OPERATION         03646100
                     PERFORM 9000-SQL-ABEND                             03646200
           END-EVALUATE.                                                03646300
                                                                        03646400
      ******************************************************************03646500
      * - CLOSE THE INPUT FILE.                                        *03646600
      * - DISPLAY THE NUMBER OF  RECORDS READ AND  DELETED.            *03646700
      ******************************************************************03646800
       8000-EOJ-ROUTINE.                                                03646900
                                                                        03647000
           CLOSE INPUT-PURGE-DATE-FILE.                                 03648000
                                                                        03649000
           DISPLAY '****** ' PC-PROGRAM-NAME ' ******'.                 03650000
           DISPLAY 'PURGE DATE : ' PV-PURGE-DATE.                       03660000
           DISPLAY '****** PCT_SUM_WRKD TABLE COUNTS ******'.           03670000
           MOVE PA-WRKD-TOTAL-FETCH-COUNT TO PV-DISPLAY-COUNT.          03680000
           DISPLAY 'TOTAL NUMBER OF RECORDS FETCHED : '                 03690000
                                        PV-DISPLAY-COUNT.               03700000
           MOVE PA-WRKD-TOTAL-DELETE-COUNT TO PV-DISPLAY-COUNT.         03710000
           DISPLAY 'TOTAL NUMBER OF RECORDS DELETED : '                 03720000
                                        PV-DISPLAY-COUNT.               03730000
           MOVE PA-WRKD-COMMIT-COUNT      TO PV-DISPLAY-COUNT.          03730100
           DISPLAY 'TOTAL NUMBER OF COMMITS TAKEN : '                   03730200
                                        PV-DISPLAY-COUNT.               03730300
                                                                        03730400
           DISPLAY '****** PCT_SUM_VRFD TABLE COUNTS ******'.           03730500
           MOVE PA-VRFD-TOTAL-FETCH-COUNT TO PV-DISPLAY-COUNT.          03730600
           DISPLAY 'TOTAL NUMBER OF RECORDS FETCHED : '                 03730700
                                        PV-DISPLAY-COUNT.               03730800
           MOVE PA-VRFD-TOTAL-DELETE-COUNT TO PV-DISPLAY-COUNT.         03730900
           DISPLAY 'TOTAL NUMBER OF RECORDS DELETED : '                 03731000
                                        PV-DISPLAY-COUNT.               03731100
           MOVE PA-VRFD-COMMIT-COUNT      TO PV-DISPLAY-COUNT.          03731200
           DISPLAY 'TOTAL NUMBER OF COMMITS TAKEN : '                   03731300
                                        PV-DISPLAY-COUNT.               03731400
                                                                        03731500
      ******************************************************************03731600
      *  PERFORMS ABEND PROCESSING FOR SQL ERRORS.                     *03731700
      *  INITIALLY, THIS PARAGRAPH WILL NOT BE PERFORMED               *03731800
      ******************************************************************03731900
       9000-SQL-ABEND.                                                  03732000
                                                                        03733000
           DISPLAY '9000-SQL-ABEND'.                                    03734000
           DISPLAY '**  ABEND     **'.                                  03735000
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              03736000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            03737000
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION.             03738000
           DISPLAY '**  SQLCODE   **  = ' SQLCODE.                      03739000
                                                                        03740000
      ******************************************************************03750000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *03760000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *03770000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *03780000
      * FORMAT.                                                        *03790000
      ******************************************************************03800000
           COPY DPPD004.                                                03810000
                                                                        03820000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            03830000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03840000
