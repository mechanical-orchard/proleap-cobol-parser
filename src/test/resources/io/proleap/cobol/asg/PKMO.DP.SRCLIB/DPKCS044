00001  IDENTIFICATION DIVISION.                                         10/13/93
00002                                                                   DPKCS044
00003  PROGRAM-ID.    DPKCS044.                                            LV006
00004                                                                   DPKCS044
00005  AUTHOR.        PAT DOYLE.                                        DPKCS044
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          DPKCS044
00007  DATE-WRITTEN.  03/29/93.                                         DPKCS044
00008  DATE-COMPILED.                                                   DPKCS044
00009 ******************************************************************DPKCS044
00010 **    CICS OBJECT LOCKING SUBROUTINE                            **DPKCS044
00011 ******************************************************************DPKCS044
00012 **                                                              **DPKCS044
00013 ** THIS PROGRAM IS A CALLED SUBROUTINE WHICH CAN BE USED BY     **DPKCS044
00014 ** CICS PROGRAMS TO CREATE AND DELETE LOCKS FOR OBJECTS USED    **DPKCS044
00015 ** BY THE APPLICATION.                                          **DPKCS044
00016 **                                                              **DPKCS044
00017 ** 10/01/93   CAL BUSH                                          **DPKCS044
00018 ** CHANGED TO VSAM ACCESS FROM DB2                              **DPKCS044
00019 **                                                              **DPKCS044
00020 ******************************************************************DPKCS044
00021                                                                   DPKCS044
00022                                                                   DPKCS044
00023  ENVIRONMENT DIVISION.                                            DPKCS044
00024                                                                   DPKCS044
00025  DATA DIVISION.                                                   DPKCS044
00026                                                                   DPKCS044
00027  WORKING-STORAGE SECTION.                                         DPKCS044
00028  01  PC-PROGRAM-CONSTANTS.                                        DPKCS044
00029      05  PC-OBJLOK-BASE-DD       PIC  X(08)  VALUE 'DPOBJLOK'.    DPKCS044
00030      05  PC-OBJLOK-AIX-TASK-DD   PIC  X(08)  VALUE 'DPOBJLK1'.    DPKCS044
00031      05  PC-CURRENT-PROGRAM-NAME PIC  X(08)  VALUE 'DPKCS044'.    DPKCS044
00032      05  PC-NAVIGATOR-TRAN-ID    PIC  X(04)  VALUE 'Z008'.           CL**4
00033 *                                                                 DPKCS044
00034  01  PS-PROGRAM-SWITCHES.                                         DPKCS044
00035      05  PS-EDIT-SWITCH         PIC  X(01)  VALUE 'Y'.            DPKCS044
00036          88 PS-EDITS-PASSED                 VALUE 'Y'.            DPKCS044
00037          88 PS-EDITS-FAILED                 VALUE 'N'.            DPKCS044
00038                                                                   DPKCS044
00039  01  PV-PROGRAM-VARIABLES.                                        DPKCS044
00040      05  PV-NAVIGATOR-SYSID     PIC  X(04)  VALUE SPACES.            CL**4
00041      05  PV-CICS-RESP           PIC S9(04)  VALUE +0              DPKCS044
00042                                             COMP SYNC.            DPKCS044
00043      05  PV-CICS-LENGTH         PIC S9(04)  VALUE +0              DPKCS044
00044                                             COMP SYNC.            DPKCS044
00045      05  PV-CICS-KEYLENGTH      PIC S9(04)  VALUE +0                 CL**5
00046                                             COMP SYNC.               CL**5
00047      05  PV-LOCKS-FOR-TASK      PIC S9(03)  VALUE +0              DPKCS044
00048                                             COMP-3.               DPKCS044
00049      05  PV-ABSTIME             PIC S9(15)  VALUE ZERO            DPKCS044
00050                                             COMP-3.               DPKCS044
00051 *---------------------------------------------------------------* DPKCS044
00052 *    GLOBAL COMMAREA                                            * DPKCS044
00053 *---------------------------------------------------------------* DPKCS044
00054      COPY DPWS020.                                                DPKCS044
00055                                                                   DPKCS044
00056 *---------------------------------------------------------------* DPKCS044
00057 *    INCLUDE USED TO COMMUNICATE WITH CALLING PROGRAM           * DPKCS044
00058 *---------------------------------------------------------------* DPKCS044
00059      COPY DPWS044.                                                   CL**6
00060                                                                   DPKCS044
00061  EJECT                                                            DPKCS044
00062 *---------------------------------------------------------------* DPKCS044
00063 *    COPYBOOK FOR THE OBJECT LOCKING FILE                       * DPKCS044
00064 *---------------------------------------------------------------* DPKCS044
00065      COPY DPRD509.                                                   CL**6
00066                                                                   DPKCS044
00067  EJECT                                                            DPKCS044
00068 *---------------------------------------------------------------* DPKCS044
00069 *    COPYBOOK FOR THE CICS ABEND ROUTINE                        * DPKCS044
00070 *---------------------------------------------------------------* DPKCS044
00071      COPY DPWS013.                                                   CL**3
00072                                                                   DPKCS044
00073  EJECT                                                            DPKCS044
00074                                                                   DPKCS044
00075  LINKAGE SECTION.                                                 DPKCS044
00076                                                                   DPKCS044
00077  01  DFHCOMMAREA.                                                 DPKCS044
00078      05  FILLER                   OCCURS 1 TO 4072 TIMES          DPKCS044
00079                                   DEPENDING ON EIBCALEN.          DPKCS044
00080          10  FILLER               PIC X(01).                      DPKCS044
00081                                                                   DPKCS044
00082                                                                   DPKCS044
00083  PROCEDURE DIVISION.                                              DPKCS044
00084                                                                   DPKCS044
00085 ******************************************************************DPKCS044
00086 **   PARAMETERS PASSED TO THIS MODULE ARE EDITED.  IF THEY ARE  **DPKCS044
00087 **   OK, THE LOCK OR UNLOCK REQUEST WILL BE PROCESSED.  THEN    **DPKCS044
00088 **   CONTROL IS PASSED BACK TO THE CALLING PROGRAM.             **DPKCS044
00089 ******************************************************************DPKCS044
00090                                                                   DPKCS044
00091  0000-MAINLINE.                                                   DPKCS044
00092                                                                   DPKCS044
00093      IF EIBCALEN = ZERO                                           DPKCS044
00094          INITIALIZE DP044-OBJECT-LOCK-FIELDS                      DPKCS044
00095      ELSE                                                         DPKCS044
00096          MOVE DFHCOMMAREA TO DP044-OBJECT-LOCK-FIELDS             DPKCS044
00097      END-IF.                                                      DPKCS044
00098                                                                   DPKCS044
00099      SET DP044-RC-CALL-SUCCESSFUL TO TRUE.                        DPKCS044
00100      MOVE ZERO TO DP044-RETURN-UNLOCK-STATUS.                     DPKCS044
00101                                                                   DPKCS044
00102      PERFORM 1000-EDIT-PARAMETERS.                                DPKCS044
00103                                                                   DPKCS044
00104      IF  PS-EDITS-PASSED                                          DPKCS044
00105          EVALUATE TRUE                                            DPKCS044
00106              WHEN DP044-ACTION-LOCK-OBJECT                        DPKCS044
00107                  PERFORM 2000-PROCESS-LOCK-REQUEST                DPKCS044
00108              WHEN DP044-ACTION-UNLOCK-OBJECT                      DPKCS044
00109                  PERFORM 4000-PROCESS-UNLOCK-OBJECT               DPKCS044
00110              WHEN DP044-ACTION-UNLOCK-TASK                        DPKCS044
00111                  PERFORM 6000-PROCESS-UNLOCK-TASK                 DPKCS044
00112          END-EVALUATE                                             DPKCS044
00113      ELSE                                                         DPKCS044
00114          SET DP044-RC-ERR-BAD-PARAMETERS TO TRUE                  DPKCS044
00115      END-IF.                                                      DPKCS044
00116                                                                   DPKCS044
00117      MOVE DP044-OBJECT-LOCK-FIELDS TO DFHCOMMAREA.                DPKCS044
00118      EXEC CICS                                                    DPKCS044
00119          RETURN                                                   DPKCS044
00120      END-EXEC.                                                    DPKCS044
00121                                                                   DPKCS044
00122  EJECT                                                            DPKCS044
00123                                                                   DPKCS044
00124 ******************************************************************DPKCS044
00125 **   EDITS THE PARAMETERS FROM THE CALLING PROGRAM.             **DPKCS044
00126 ******************************************************************DPKCS044
00127                                                                   DPKCS044
00128  1000-EDIT-PARAMETERS.                                            DPKCS044
00129                                                                   DPKCS044
00130      EVALUATE TRUE                                                DPKCS044
00131          WHEN DP044-ACTION-LOCK-OBJECT                            DPKCS044
00132              IF   DP044-OBJECT-TYPE = SPACES                      DPKCS044
00133                OR DP044-TASK-NUMBER = ZEROS                       DPKCS044
00134                OR DP044-NETNAME     = SPACES                      DPKCS044
00135                OR DP044-NETNAME     = ZEROS                       DPKCS044
00136                OR DP044-USERID      = SPACES                      DPKCS044
00137                OR DP044-USERID      = ZEROS                       DPKCS044
00138                OR DP044-MNEMONIC    = SPACES                      DPKCS044
00139                OR DP044-MNEMONIC    = ZEROS                       DPKCS044
00140                  SET PS-EDITS-FAILED TO TRUE                      DPKCS044
00141              END-IF                                               DPKCS044
00142                                                                   DPKCS044
00143          WHEN DP044-ACTION-UNLOCK-OBJECT                          DPKCS044
00144              IF   DP044-OBJECT-TYPE = SPACES                      DPKCS044
00145                OR DP044-TASK-NUMBER = ZEROS                       DPKCS044
00146                  SET PS-EDITS-FAILED TO TRUE                      DPKCS044
00147              END-IF                                               DPKCS044
00148                                                                   DPKCS044
00149          WHEN DP044-ACTION-UNLOCK-TASK                            DPKCS044
00150              IF   DP044-TASK-NUMBER = ZEROS                       DPKCS044
00151                  SET PS-EDITS-FAILED TO TRUE                      DPKCS044
00152              END-IF                                               DPKCS044
00153                                                                   DPKCS044
00154          WHEN OTHER                                               DPKCS044
00155              SET PS-EDITS-FAILED TO TRUE                          DPKCS044
00156                                                                   DPKCS044
00157      END-EVALUATE.                                                DPKCS044
00158                                                                   DPKCS044
00159  EJECT                                                            DPKCS044
00160 ******************************************************************DPKCS044
00161 **   CHECKS TO SEE IF THE OBJECT IS ALREADY LOCKED. IF A ROW FOR**DPKCS044
00162 **   THAT OBJECT TYPE AND KEY EXISTS IN THE OBJECT LOCK TABLE,  **DPKCS044
00163 **   THEN THAT OBJECT IS ALREADY LOCKED, AND A CODE WILL BE     **DPKCS044
00164 **   PASSED TO THE CALLING PROGRAM.  IF THE OBJECT IS NOT       **DPKCS044
00165 **   LOCKED, A LOCK WILL BE CREATED.                            **DPKCS044
00166 ******************************************************************DPKCS044
00167                                                                   DPKCS044
00168  2000-PROCESS-LOCK-REQUEST.                                       DPKCS044
00169      INITIALIZE DP509-OBJECT-LOCKING-RCD.                         DPKCS044
00170      MOVE DP044-OBJECT-TYPE            TO  DP509-TYP-COD.         DPKCS044
00171      MOVE DP044-OBJECT-KEY             TO  DP509-OBJ-ID.          DPKCS044
00172      MOVE DP044-TASK-NUMBER            TO  DP509-TASK-NBR.        DPKCS044
00173      MOVE DP044-NETNAME                TO  DP509-NETWK-NAME.      DPKCS044
00174      MOVE DP044-USERID                 TO  DP509-USER-ID.         DPKCS044
00175      MOVE DP044-MNEMONIC               TO  DP509-OBJ-MNMNC-CDE.   DPKCS044
00176      EXEC CICS                                                    DPKCS044
00177          ASKTIME                                                  DPKCS044
00178              ABSTIME (PV-ABSTIME)                                 DPKCS044
00179      END-EXEC.                                                    DPKCS044
00180                                                                   DPKCS044
00181      EXEC CICS                                                    DPKCS044
00182          FORMATTIME                                               DPKCS044
00183              ABSTIME (PV-ABSTIME)                                 DPKCS044
00184              MMDDYY  (DP509-DATE)                                 DPKCS044
00185              DATESEP                                              DPKCS044
00186              TIME    (DP509-TIME)                                 DPKCS044
00187              TIMESEP                                              DPKCS044
00188      END-EXEC.                                                    DPKCS044
00189                                                                   DPKCS044
00190      PERFORM 2100-CREATE-LOCK.                                    DPKCS044
00191                                                                   DPKCS044
00192      IF  PV-CICS-RESP = DFHRESP (DUPREC)                          DPKCS044
00193              SET DP044-RC-OBJECT-ALREADY-HELD TO TRUE             DPKCS044
00194      END-IF.                                                      DPKCS044
00195                                                                   DPKCS044
00196  EJECT                                                            DPKCS044
00197 ******************************************************************DPKCS044
00198 **   LOCKS THE OBJECT BY INSERTING A RECORD INTO THE OBJECT LOCK**DPKCS044
00199 **   FILE.                                                      **DPKCS044
00200 ******************************************************************DPKCS044
00201  2100-CREATE-LOCK.                                                DPKCS044
00202      EXEC CICS                                                       CL**4
00203          INQUIRE                                                     CL**4
00204              TRANSACTION  (PC-NAVIGATOR-TRAN-ID)                     CL**4
00205              REMOTESYSTEM (PV-NAVIGATOR-SYSID)                       CL**4
00206      END-EXEC.                                                       CL**4
00207                                                                      CL**4
00208      IF PV-NAVIGATOR-SYSID = SPACES                                  CL**4
00209          EXEC CICS                                                   CL**4
00210              WRITE                                                   CL**4
00211                  FROM   (DP509-OBJECT-LOCKING-RCD)                   CL**4
00212                  FILE   (PC-OBJLOK-BASE-DD)                          CL**4
00213                  RIDFLD (DP509-OBJLOK-KEY)                           CL**4
00214                  RESP   (PV-CICS-RESP)                               CL**4
00215          END-EXEC                                                    CL**4
00216      ELSE                                                            CL**4
00217          MOVE LENGTH OF DP509-OBJECT-LOCKING-RCD                     CL**5
00218                                     TO PV-CICS-LENGTH                CL**5
00219          MOVE LENGTH OF DP509-OBJLOK-KEY                             CL**5
00220                                     TO PV-CICS-KEYLENGTH             CL**5
00221          EXEC CICS                                                   CL**4
00222              WRITE                                                   CL**4
00223                  FROM      (DP509-OBJECT-LOCKING-RCD)                CL**5
00224                  FILE      (PC-OBJLOK-BASE-DD)                       CL**5
00225                  LENGTH    (PV-CICS-LENGTH)                          CL**5
00226                  RIDFLD    (DP509-OBJLOK-KEY)                        CL**5
00227                  KEYLENGTH (PV-CICS-KEYLENGTH)                       CL**5
00228                  SYSID     (PV-NAVIGATOR-SYSID)                      CL**5
00229                  RESP      (PV-CICS-RESP)                            CL**5
00230          END-EXEC                                                    CL**4
00231      END-IF.                                                         CL**4
00232                                                                      CL**4
00233 *                                                                 DPKCS044
00234      IF  PV-CICS-RESP = DFHRESP(NORMAL)                           DPKCS044
00235                      OR DFHRESP(DUPREC)                           DPKCS044
00236          CONTINUE                                                 DPKCS044
00237      ELSE                                                         DPKCS044
00238          SET DP013-CICS-ABEND       TO TRUE                       DPKCS044
00239          SET DP013-LINK-RETURN      TO TRUE                       DPKCS044
00240          MOVE '2100-CREATE-LOCK'    TO DP013-PARAGRAPH            DPKCS044
00241          MOVE 'BAD RESPONSE WRITING TO OBJECT LOCK VSAM'          DPKCS044
00242                                         TO DP013-MESSAGE-TEXT (1) DPKCS044
00243          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS044
00244          SET DP044-RC-ERR-VSAM-ERROR TO TRUE                      DPKCS044
00245      END-IF.                                                      DPKCS044
00246  EJECT                                                            DPKCS044
00247 ******************************************************************DPKCS044
00248 **   UNLOCKS THE OBJECT BY DELETING ITS ROW FROM THE OBJECT     **DPKCS044
00249 **   LOCKING TABLE.                                             **DPKCS044
00250 ******************************************************************DPKCS044
00251  4000-PROCESS-UNLOCK-OBJECT.                                      DPKCS044
00252      MOVE DP044-OBJECT-TYPE            TO  DP509-TYP-COD.         DPKCS044
00253      MOVE DP044-OBJECT-KEY             TO  DP509-OBJ-ID.          DPKCS044
00254      PERFORM 4100-DELETE-OBJECT.                                  DPKCS044
00255      IF  PV-CICS-RESP = DFHRESP(NOTFND)                           DPKCS044
00256          SET DP044-RC-WRN-LOCK-NOT-FOUND TO TRUE                  DPKCS044
00257      END-IF.                                                      DPKCS044
00258                                                                   DPKCS044
00259  4100-DELETE-OBJECT.                                              DPKCS044
00260      EXEC CICS                                                       CL**4
00261          INQUIRE                                                     CL**4
00262              TRANSACTION  (PC-NAVIGATOR-TRAN-ID)                     CL**4
00263              REMOTESYSTEM (PV-NAVIGATOR-SYSID)                       CL**4
00264      END-EXEC.                                                       CL**4
00265                                                                      CL**4
00266      IF PV-NAVIGATOR-SYSID = SPACES                                  CL**4
00267          EXEC CICS                                                   CL**4
00268              DELETE                                                  CL**4
00269                  FILE   (PC-OBJLOK-BASE-DD)                          CL**4
00270                  RIDFLD (DP509-OBJLOK-KEY)                           CL**4
00271                  RESP   (PV-CICS-RESP)                               CL**4
00272          END-EXEC                                                    CL**4
00273      ELSE                                                            CL**4
00274          MOVE LENGTH OF DP509-OBJLOK-KEY                             CL**5
00275                                TO PV-CICS-KEYLENGTH                  CL**5
00276          EXEC CICS                                                   CL**4
00277              DELETE                                                  CL**4
00278                  FILE      (PC-OBJLOK-BASE-DD)                       CL**5
00279                  RIDFLD    (DP509-OBJLOK-KEY)                        CL**5
00280                  KEYLENGTH (PV-CICS-KEYLENGTH)                       CL**5
00281                  SYSID     (PV-NAVIGATOR-SYSID)                      CL**5
00282                  RESP      (PV-CICS-RESP)                            CL**5
00283          END-EXEC                                                    CL**4
00284 *                                                                 DPKCS044
00285      IF  PV-CICS-RESP = DFHRESP(NORMAL)                           DPKCS044
00286                      OR DFHRESP(NOTFND)                           DPKCS044
00287          CONTINUE                                                 DPKCS044
00288      ELSE                                                         DPKCS044
00289          SET DP013-CICS-ABEND       TO TRUE                       DPKCS044
00290          SET DP013-LINK-RETURN      TO TRUE                       DPKCS044
00291          MOVE '4100-DELETE-OBJECT'                                DPKCS044
00292                                     TO DP013-PARAGRAPH            DPKCS044
00293          MOVE 'BAD RESPONSE DELETING FROM OBJ LOCK VSAM'          DPKCS044
00294                                         TO DP013-MESSAGE-TEXT (1) DPKCS044
00295          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS044
00296          SET DP044-RC-ERR-VSAM-ERROR TO TRUE                      DPKCS044
00297      END-IF.                                                      DPKCS044
00298 ******************************************************************DPKCS044
00299 **   UNLOCKS ALL THE TASK'S LOCKS BY DELETING ALL ROWS WITH THAT**DPKCS044
00300 **   TASK NUMBER FROM THE OBJECT LOCKING TABLE.                 **DPKCS044
00301 ******************************************************************DPKCS044
00302  6000-PROCESS-UNLOCK-TASK.                                        DPKCS044
00303      MOVE DP044-TASK-NUMBER            TO  DP509-TASK-NBR.        DPKCS044
00304      PERFORM 6100-DELETE-TASK.                                    DPKCS044
00305                                                                   DPKCS044
00306  6100-DELETE-TASK.                                                DPKCS044
00307      EXEC CICS                                                       CL**4
00308          INQUIRE                                                     CL**4
00309              TRANSACTION  (PC-NAVIGATOR-TRAN-ID)                     CL**4
00310              REMOTESYSTEM (PV-NAVIGATOR-SYSID)                       CL**4
00311      END-EXEC.                                                       CL**4
00312                                                                      CL**4
00313      IF PV-NAVIGATOR-SYSID = SPACES                                  CL**4
00314          EXEC CICS                                                   CL**4
00315              DELETE                                                  CL**4
00316                  FILE   (PC-OBJLOK-AIX-TASK-DD)                      CL**4
00317                  RIDFLD (DP509-TASK-NBR)                             CL**4
00318                  RESP   (PV-CICS-RESP)                               CL**4
00319          END-EXEC                                                    CL**4
00320      ELSE                                                            CL**4
00321          MOVE LENGTH OF DP509-OBJLOK-KEY                             CL**5
00322                                TO PV-CICS-KEYLENGTH                  CL**5
00323          EXEC CICS                                                   CL**4
00324              DELETE                                                  CL**4
00325                  FILE      (PC-OBJLOK-AIX-TASK-DD)                   CL**5
00326                  RIDFLD    (DP509-TASK-NBR)                          CL**5
00327                  KEYLENGTH (PV-CICS-KEYLENGTH)                       CL**5
00328                  SYSID     (PV-NAVIGATOR-SYSID)                      CL**5
00329                  RESP      (PV-CICS-RESP)                            CL**5
00330          END-EXEC                                                    CL**4
00331 *                                                                    CL**4
00332 *                                                                 DPKCS044
00333      IF  PV-CICS-RESP = DFHRESP(NORMAL)                           DPKCS044
00334                      OR DFHRESP(NOTFND)                           DPKCS044
00335          CONTINUE                                                 DPKCS044
00336      ELSE                                                         DPKCS044
00337          SET DP013-CICS-ABEND       TO TRUE                       DPKCS044
00338          SET DP013-LINK-RETURN      TO TRUE                       DPKCS044
00339          MOVE '6100-DELETE-TASK'                                  DPKCS044
00340                                     TO DP013-PARAGRAPH            DPKCS044
00341          MOVE 'BAD RESPONSE DELETING FROM OBJ LOCK VSAM'          DPKCS044
00342                                     TO DP013-MESSAGE-TEXT (1)     DPKCS044
00343          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS044
00344          SET DP044-RC-ERR-VSAM-ERROR TO TRUE                      DPKCS044
00345      END-IF.                                                      DPKCS044
00346                                                                   DPKCS044
00347 ***************************************************************   DPKCS044
00348 ** PROCEDURE DIVISION COPYBOOK FOR CALLING THE ABEND PGM.    **   DPKCS044
00349 ***************************************************************   DPKCS044
00350      COPY DPPD013.                                                   CL**4
