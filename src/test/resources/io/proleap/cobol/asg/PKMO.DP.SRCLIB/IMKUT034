      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.             IMKUT034.                                00050000
       AUTHOR.                 BOB MCASEY                               00060000
       INSTALLATION.           KOHLS.                                   00070000
       DATE-WRITTEN            4/28/2023.                               00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      *                                                                 00110000
      *-----------------------------------------------------------------00120000
      *    CREATE ITEM DOMAIN UPC EXTRACT RECORDS                       00130000
      *-----------------------------------------------------------------00140000
      *  THE PURPOSE OF THIS PROGRAM IS TO READ IN THE BMC UNLOAD ITEM  00150000
      *  UPC EXTRACT FILE, INCLUDING ALL ACTIVE INTERNAL, VENDOR, AND   00160000
      *  RENUMBERED UPCS, GET SIZE DESCRIPTION, NRF COLOR DESCRIPTION,  00160100
      *  AND NRF SHORT COLOR DESCRIPTION.  WRITE OUT ITEM UPC DOMAIN    00160200
      *  EXTRACT RECORD.                                                00160300
      *-----------------------------------------------------------------00160400
      *    CHANGE       DATE        DESCRIPTION                         00160500
      *    --------   --------     ---------------------------------    00160600
      *-----------------------------------------------------------------00161200
                                                                        00161300
      ******************************************************************00161400
       ENVIRONMENT DIVISION.                                            00161500
      ******************************************************************00161600
                                                                        00161700
       CONFIGURATION SECTION.                                           00161800
                                                                        00161900
       SOURCE-COMPUTER.        IBM-3090.                                00162000
       OBJECT-COMPUTER.        IBM-3090.                                00163000
                                                                        00164000
       INPUT-OUTPUT SECTION.                                            00165000
                                                                        00166000
       FILE-CONTROL.                                                    00167000
                                                                        00168000
           SELECT UPC-UNLOAD-IN                                         00169000
               ASSIGN TO INP01.                                         00170000
                                                                        00180000
           SELECT TSKXREF-UNLOAD-FILE                                   00190000
               ASSIGN TO INP02.                                         00200000
                                                                        00201000
           SELECT UPC-EXTRACT-OUT                                       00202000
               ASSIGN TO OUT01.                                         00203000
                                                                        00204000
                                                                        00205000
      ******************************************************************00206000
       DATA DIVISION.                                                   00207000
      ******************************************************************00208000
                                                                        00209000
       FILE SECTION.                                                    00210000
                                                                        00220000
       FD  UPC-UNLOAD-IN                                                00230000
           RECORDING MODE IS F                                          00240000
           BLOCK CONTAINS 0 RECORDS                                     00250000
           LABEL RECORDS ARE STANDARD.                                  00260000
       01  UPC-UNLOAD-IN-REC                  PIC X(69).                00270000
                                                                        00280000
       FD  TSKXREF-UNLOAD-FILE                                          00290000
           RECORDING MODE IS F                                          00300000
           LABEL RECORDS ARE STANDARD                                   00310000
           BLOCK CONTAINS 0 RECORDS.                                    00320000
                                                                        00330000
       01  TSKXREF-UNLOAD-RECORD              PIC X(19).                00340000
                                                                        00350000
       FD  UPC-EXTRACT-OUT                                              00360000
           RECORDING MODE IS F                                          00370000
           BLOCK CONTAINS 0 RECORDS                                     00380000
           LABEL RECORDS ARE STANDARD.                                  00390000
       01  UPC-EXTRACT-OUT-REC                PIC X(69).                00400000
                                                                        00410000
      ******************************************************************00420000
       WORKING-STORAGE SECTION.                                         00430000
      ******************************************************************00440000
       01  ABEND-CODE                         PIC S9(04)  VALUE +0      00450000
                                                          COMP SYNC.    00460000
       01  PC-PROGRAM-CONSTANTS.                                        00470000
           05  PC-PGM-NAME             PIC  X(08)     VALUE 'IMKUT034'. 00480000
           05  PC-MAX-RETRIES          PIC S9(04)     VALUE +2.         00490000
                                                                        00500000
       01  PS-PROGRAM-SWITCHES.                                         00510000
           05  PS-TUPC-UNLOAD-DONE-SW         PIC X(01)    VALUE 'N'.   00511000
               88  PS-TUPC-UNLOAD-DONE                     VALUE 'Y'.   00512000
               88  PS-TUPC-UNLOAD-NOT-DONE                 VALUE 'N'.   00513000
           05  PS-TSKXREF-UNLOAD-DONE-SW      PIC X(01)    VALUE 'N'.   00514000
               88  PS-TSKXREF-UNLOAD-DONE                  VALUE 'Y'.   00515000
               88  PS-TSKXREF-UNLOAD-NOT-DONE              VALUE 'N'.   00516000
               88  PS-NO-SQL-ERROR                         VALUE 'N'.   00519000
                                                                        00520000
       01  PV-PROGRAM-VARIABLES.                                        00530000
           05  PV-UPC-RECS-WRITTEN            PIC S9(09) VALUE +0  COMP.00540000
           05  PV-UPC-RECS-READ               PIC S9(09) VALUE +0  COMP.00550000
           05  PV-TSKXREF-UNLOAD-READ         PIC S9(09) VALUE +0  COMP.00560000
           05  PV-TUPC-UNLOAD-DROPPED         PIC S9(09) VALUE +0  COMP.00570000
           05  PV-PARAGRAPH-NAME              PIC  X(30)  VALUE SPACE.  00580000
           05  PV-INTR-UPC-ID-TEN             PIC S9(10) VALUE +0 COMP. 00611000
           05  PV-DISPLAY-INTERNAL            PIC  9(10) VALUE ZERO.    00612000
                                                                        00613000
      *--------------------------------------------------               00614000
      *  COPYBOOK FOR BMC UPC UNLOAD INPUT FILE.                        00615000
      *--------------------------------------------------               00616000
           COPY IMRD054.                                                00617000
                                                                        00618000
      *--------------------------------------------------               00619000
      *  RECORD LAYOUT FOR THE ITEM DOMAIN TSKXREF UNLOAD INPUT FILE    00620000
      *--------------------------------------------------               00630000
           COPY IMRD062.                                                00640000
                                                                        00650000
                                                                        00960000
       PROCEDURE DIVISION.                                              00970000
      *-----------------------------------------------------------------00980000
      *  MAIN LINE OF THE PROGRAM PERFORMS THE                          00990000
      *  INITIALIZATION ROUTINE AND CONTROLS THE PROCESSING OF THE PGM. 01000000
      *-----------------------------------------------------------------01010000
       0000-MAINLINE.                                                   01020000
                                                                        01030000
           PERFORM 0100-HOUSEKEEPING.                                   01040000
                                                                        01050000
           PERFORM 1000-PROCESS-INPUT-FILES                             01060000
               UNTIL PS-TUPC-UNLOAD-DONE                                01070000
                 OR  PS-TSKXREF-UNLOAD-DONE.                            01080000
                                                                        01090000
           PERFORM 9000-EOJ.                                            01100000
           GOBACK.                                                      01110000
                                                                        01120000
      *-----------------------------------------------------------------01130000
      *  OPEN FILES AND PERFORM FIRST READ FROM INPUT FILE              01140000
      *-----------------------------------------------------------------01150000
       0100-HOUSEKEEPING.                                               01160000
                                                                        01170000
           OPEN INPUT UPC-UNLOAD-IN                                     01180000
                      TSKXREF-UNLOAD-FILE                               01190000
                OUTPUT UPC-EXTRACT-OUT.                                 01200000
                                                                        01210000
           PERFORM 6000-READ-UPC-UNLOAD-FILE.                           01220000
                                                                        01230000
           IF PS-TUPC-UNLOAD-DONE                                       01240000
              DISPLAY '*** '                                            01250000
              DISPLAY '*** IMKUT034 - NO INPUT UPC RECS FOUND'          01260000
              DISPLAY '*** '                                            01270000
              MOVE +4003 TO ABEND-CODE                                  01280000
              PERFORM 9999-ABEND                                        01290000
           END-IF.                                                      01300000
                                                                        01310000
           PERFORM 6100-READ-TSKXREF-UNLOAD-DATA.                       01320000
                                                                        01330000
           IF PS-TSKXREF-UNLOAD-DONE                                    01340000
              DISPLAY '*** '                                            01350000
              DISPLAY '*** IMKUT034 - NO INPUT TSKXREF RECS FOUND'      01360000
              DISPLAY '*** '                                            01361000
              MOVE +4004 TO ABEND-CODE                                  01361100
              PERFORM 9999-ABEND                                        01361200
           END-IF.                                                      01361300
                                                                        01361400
      *-----------------------------------------------------------------01361500
      *  PROCESS ALL INPUT RECORDS AND FORMAT ITEM UPC DOMAIN EXTRACT   01361600
      *  RECORDS UNTIL END OF INPUT FILE.                               01361700
      *-----------------------------------------------------------------01361800
       1000-PROCESS-INPUT-FILES.                                        01361900
      *                                                                 01362000
           IF IM054-INTR-UPC-ID = IM062-INTR-UPC-ID                     01363000
*******        PERFORM 1100-CREATE-UPC-OUTPUT-REC                       01364000
               PERFORM 7000-WRITE-UPC-OUTPUT-REC                        01364100
               PERFORM 6000-READ-UPC-UNLOAD-FILE                        01365000
           ELSE                                                         01366000
               IF IM054-INTR-UPC-ID > IM062-INTR-UPC-ID                 01367000
                   PERFORM 6100-READ-TSKXREF-UNLOAD-DATA                01368000
               ELSE                                                     01369000
                   ADD +1 TO PV-TUPC-UNLOAD-DROPPED                     01370000
                   MOVE IM054-UPC-ID       TO PV-INTR-UPC-ID-TEN        01370100
                   MOVE PV-INTR-UPC-ID-TEN TO PV-DISPLAY-INTERNAL       01370200
                   DISPLAY 'EXTRA TUPC UNLOAD RECORD DROPPED FOR UPC ID:01370300
      -                    ' ' PV-DISPLAY-INTERNAL                      01370400
                   PERFORM 6000-READ-UPC-UNLOAD-FILE                    01370500
               END-IF                                                   01370600
           END-IF.                                                      01370700
                                                                        01370800
      *-----------------------------------------------------------------02473000
      * READ THE UPC UNLOAD INPUT FILE                                  02474000
      *-----------------------------------------------------------------02475000
       6000-READ-UPC-UNLOAD-FILE.                                       02476000
                                                                        02477000
           INITIALIZE IM054-ITEM-UPC-UNLD-REC.                          02478000
                                                                        02479000
           READ UPC-UNLOAD-IN INTO IM054-ITEM-UPC-UNLD-REC              02480000
              AT END                                                    02490000
                 SET PS-TUPC-UNLOAD-DONE TO TRUE                        02500000
              NOT AT END                                                02510000
                 ADD +1   TO PV-UPC-RECS-READ.                          02520000
                                                                        02530000
      *-----------------------------------------------------------------02540000
      *  READ AN ITEM DOMAIN TSKXREF UNLOAD FILE RECORD                 02550000
      *-----------------------------------------------------------------02560000
       6100-READ-TSKXREF-UNLOAD-DATA.                                   02570000
                                                                        02580000
           READ TSKXREF-UNLOAD-FILE INTO IM062-ITEM-TSKXREF-UNLD-REC    02590000
             AT END                                                     02600000
                 SET PS-TSKXREF-UNLOAD-DONE TO TRUE                     02610000
             NOT AT END                                                 02620000
                 ADD +1   TO PV-TSKXREF-UNLOAD-READ                     02630000
           END-READ.                                                    02640000
                                                                        02650000
      *-----------------------------------------------------------------02660000
      * WRITE THE UPC ITEM DOMAIN OUTPUT FILE.                          02670000
      *-----------------------------------------------------------------02680000
       7000-WRITE-UPC-OUTPUT-REC.                                       02690000
                                                                        02700000
           WRITE UPC-EXTRACT-OUT-REC FROM   IM054-ITEM-UPC-UNLD-REC.    02710000
                                                                        02720000
           ADD +1 TO PV-UPC-RECS-WRITTEN.                               02730000
                                                                        02740000
                                                                        02750000
      *-----------------------------------------------------------------02760000
      *  PROCESS EXTRA TUPC RECORDS.                                    02770000
      *-----------------------------------------------------------------02780000
       8100-EXTRA-TUPC-RECS.                                            02790000
      *                                                                 02800000
           ADD +1 TO PV-TUPC-UNLOAD-DROPPED.                            02810000
           MOVE IM054-UPC-ID       TO PV-INTR-UPC-ID-TEN.               02820000
           MOVE PV-INTR-UPC-ID-TEN TO PV-DISPLAY-INTERNAL.              02830000
           DISPLAY 'EXTRA TUPC UNLOAD RECORD DROPPED FOR UPC ID: '      02840000
                   PV-DISPLAY-INTERNAL.                                 02850000
           PERFORM 6000-READ-UPC-UNLOAD-FILE.                           02860000
                                                                        02870000
      *-----------------------------------------------------------------02880000
      * 9000-EOJ-ROUTINE - CLOSES THE FILES ASSOCIATED WITH THE JOB.    02890000
      *-----------------------------------------------------------------02900000
       9000-EOJ.                                                        02910000
                                                                        02920000
           IF PS-TUPC-UNLOAD-NOT-DONE                                   02930000
               PERFORM 8100-EXTRA-TUPC-RECS                             02940000
                 UNTIL PS-TUPC-UNLOAD-DONE                              02950000
           END-IF.                                                      02960000
                                                                        02970000
           DISPLAY ' '.                                                 02971000
           DISPLAY '============================================='.     02972000
           DISPLAY ' '.                                                 02973000
           DISPLAY ' ******  IMKUT034 PROCESSING SUMMARY  ******  '.    02974000
           PERFORM 9100-DISPLAY-PROCESSING-INFO.                        02975000
           DISPLAY '============================================='.     02975100
                                                                        02975200
                                                                        02975300
           CLOSE UPC-UNLOAD-IN                                          02975400
                 TSKXREF-UNLOAD-FILE                                    02975500
                 UPC-EXTRACT-OUT.                                       02975600
                                                                        02975700
      *-----------------------------------------------------------------02975800
      * DISPLAY PROCESSED RECORDS COUNTS                                02975900
      *-----------------------------------------------------------------02976000
       9100-DISPLAY-PROCESSING-INFO.                                    02977000
                                                                        02978000
           DISPLAY ' '.                                                 02979000
           DISPLAY '    UPC RECORDS READ         = '                    02980000
                 PV-UPC-RECS-READ.                                      02990000
           DISPLAY '    UPC RECORDS WRITTEN      = '                    03000000
                 PV-UPC-RECS-WRITTEN.                                   03010000
           DISPLAY '    TSKXREF UNLOAD RECS READ = '                    03020000
                 PV-TSKXREF-UNLOAD-READ.                                03030000
           DISPLAY '    TUPC UNLOAD RECS DROPPED = '                    03040000
                 PV-TUPC-UNLOAD-DROPPED.                                03050000
           DISPLAY ' '.                                                 03060000
      *-----------------------------------------------------------------03330000
      * 9999-ABEND - FORCES PROGRAM ABEND WITH RETURN CODE              03340000
      *-----------------------------------------------------------------03350000
       9999-ABEND.                                                      03360000
                                                                        03370000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03380000
                                                                        03390000
