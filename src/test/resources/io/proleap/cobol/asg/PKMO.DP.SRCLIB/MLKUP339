       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    MLKUP339.                                                 
       AUTHOR.        JILL LIN.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10-31-2006.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *     MLKUP339 - MAINTENENT TARGET AND RANGE TABLES                       
      ******************************************************************        
      *     THIS PROGRAM WILL UPDATE AND INSERT NEW ROWS IN            *        
      *  MLT_CUM_MKUP_OVRID, MLT_CUM_MKUP_TGT, AND MLT_CORP_GLBL_DFLT  *        
      *  TABLES.                                                       *        
      *     THE INPUT .CSV FILES ARE CREATED AND FTP FROM THREE SPREAD *        
      *  SHEET FILES. THE FILE FORMAT IS LRECL 100 AND FB FORMAT.      *        
      ******************************************************************        
263218*INCREASED THE LENGTH OF LABEL SEQUENCE NUMER FROM 4 TO 5                 
263218*CHANGED BY : SRINIVASAN SIVARAJ       WR/ID : CHG0263218                 
263218******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT CORP-DFLT-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OVERRIDE-FILE                                                 
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT TARGET-FILE                                                   
               ASSIGN TO INP03.                                                 
                                                                                
           SELECT CC-FILE                                                       
               ASSIGN TO INP04.                                                 
                                                                                
           SELECT INVALID-CORP-DFLT-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT INVALID-OVERRIDE-FILE                                         
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT INVALID-TARGET-FILE                                           
               ASSIGN TO OUT03.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  CORP-DFLT-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CORP-DFLT-RECORD            PIC  X(100).                             
                                                                                
       FD  OVERRIDE-FILE                                                        
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  OVERRIDE-RECORD             PIC  X(100).                             
                                                                                
       FD  TARGET-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  TARGET-RECORD               PIC  X(100).                             
                                                                                
       FD  CC-FILE                                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CC-RECORD                   PIC  X(80).                              
                                                                                
       FD  INVALID-CORP-DFLT-FILE                                               
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INVALID-CORP-DFLT-RECORD    PIC  X(100).                             
                                                                                
       FD  INVALID-OVERRIDE-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INVALID-OVERRIDE-RECORD     PIC  X(100).                             
                                                                                
       FD  INVALID-TARGET-FILE                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INVALID-TARGET-RECORD       PIC  X(100).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-DEADLOCKS        PIC S9(04)    VALUE +3 COMP.             
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP339'.          
           05  PC-COMMA                PIC  X        VALUE ','.                 
           05  PC-SLASH                PIC  X        VALUE '/'.                 
                                                                                
       01  PC-PROGRAM-COUNTER.                                                  
           05  PC-UPDATE-CNT           PIC  9(09).                              
           05  PC-DISPLAY-CNT          PIC  9(09).                              
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                           
           05  PE-MSG-02               PIC X(50) VALUE                          
                'ATTEMPT TO UPDATE ROW ON TABLE FAILED    '.                    
           05  PE-MSG-03               PIC X(50) VALUE                          
                'ATTEMPT TO INSERT ROW ON TABLE FAILED    '.                    
           05  PE-MSG-04               PIC X(50) VALUE                          
                'FAILED ON ACCESSING TABLE                '.                    
           05  PE-MSG-05               PIC X(50) VALUE                          
                'ATTEMPT TO PROCESS IN PAST DATE          '.                    
           05  PE-MSG-06               PIC X(50) VALUE                          
                'ATTEMPT TO GAIN PREVIOUS PROCESS DATE FAILED     '.            
           05  PE-MSG-07               PIC X(50) VALUE                          
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.             
                                                                                
       01  DFLT-RECORD.                                                         
           10 DFLT-DEPT-NBR            PIC  S9(4)V COMP-3.                      
263218     10 DFLT-LBL-SEQ-NBR         PIC  S9(5) COMP.                         
           10 DFLT-ENT-ID              PIC  S9(9) COMP.                         
           10 DFLT-MIN-CUM-MKUP-PCT    PIC  S9(4)V9(7) COMP-3.                  
           10 DFLT-MAX-CUM-MKUP-PCT    PIC  S9(4)V9(7) COMP-3.                  
           10 DFLT-OVRID-CUM-MKUP-PCT  PIC  S9(4)V9(7) COMP-3.                  
      *                                                                         
       01  OVRID-RECORD.                                                        
           10 OVRID-DEPT-NBR           PIC  S9(4)V COMP-3.                      
263218     10 OVRID-LBL-SEQ-NBR        PIC  S9(5) COMP.                         
           10 OVRID-ENT-ID             PIC  S9(9) COMP.                         
           10 OVRID-EFF-BEG-DTE        PIC   X(10).                             
           10 OVRID-CUM-MKUP-PCT       PIC  S9(4)V9(7) COMP-3.                  
           10 OVRID-EFF-END-DTE        PIC   X(10).                             
      *                                                                         
       01  TGT-RECORD.                                                          
           10 TGT-DEPT-NBR             PIC  S9(4)V COMP-3.                      
           10 TGT-EFF-BEG-DTE          PIC   X(10).                             
           10 TGT-MIN-CUM-MKUP-PCT     PIC  S9(4)V9(7) COMP-3.                  
           10 TGT-MAX-CUM-MKUP-PCT     PIC  S9(4)V9(7) COMP-3.                  
           10 TGT-EFF-END-DTE          PIC   X(10).                             
           10 TGT-MIN-GRO-MRGN-AMT     PIC  S9(11)V9(2) COMP-3.                 
           10 TGT-MAX-GRO-MRGN-AMT     PIC  S9(11)V9(2) COMP-3.                 
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-INPUT01-FILE-EOF-SW  PIC  X             VALUE 'N'.            
               88  PS-MORE-INPUT01-RECORDS                VALUE 'N'.            
               88  PS-NO-MORE-INPUT01-RECORDS             VALUE 'Y'.            
           05  PS-INPUT02-FILE-EOF-SW  PIC  X             VALUE 'N'.            
               88  PS-MORE-INPUT02-RECORDS                VALUE 'N'.            
               88  PS-NO-MORE-INPUT02-RECORDS             VALUE 'Y'.            
           05  PS-INPUT03-FILE-EOF-SW  PIC  X             VALUE 'N'.            
               88  PS-MORE-INPUT03-RECORDS                VALUE 'N'.            
               88  PS-NO-MORE-INPUT03-RECORDS             VALUE 'Y'.            
           05  PS-EMPTY-INPUT01-SW     PIC  X             VALUE 'N'.            
               88  PS-EMPTY-INPUT-FILE                    VALUE 'Y'.            
           05  PS-FIRST-COMMIT-SW      PIC  X             VALUE 'N'.            
               88  PS-FIRST-COMMIT                        VALUE 'Y'.            
      *                                                                         
       01  PROGRAM-VARIABLES.                                                   
           05  PV-DEADLOCK-COUNT       PIC S9(04)         VALUE +0 COMP.        
           05  PV-PARAGRAPH-NAME       PIC  X(30) VALUE SPACES.                 
           05  PV-OPERATION-ATTEMPTED  PIC  X(50) VALUE SPACES.                 
           05  PV-SQLCODE-DISPLAY      PIC  ++++9 VALUE ZERO.                   
           05  PV-UNSTR-PTR            PIC S9(04) COMP SYNC VALUE ZERO.         
           05  PV-UNSTR-PTR-SAVE       PIC S9(04) COMP SYNC VALUE ZERO.         
           05  PV-ABEND-CODE           PIC S9(04) COMP SYNC VALUE ZERO.         
       01  PV-TGT-REC                  PIC X(100).                              
       01  PV-OVRID-REC                PIC X(100).                              
       01  PV-DFLT-REC                 PIC X(100).                              
       01  PV-INPUT-DATE.                                                       
           05 PV-DATE-YY               PIC  9(04)          VALUE ZEROES.        
           05 PV-DATE-DASH1            PIC  X              VALUE '-'.           
           05 PV-DATE-MM               PIC  9(02)          VALUE ZEROES.        
           05 PV-DATE-DASH2            PIC  X              VALUE '-'.           
           05 PV-DATE-DD               PIC  9(02)          VALUE ZEROES.        
       01  PV-TODAYS-DATE-FORMATTED.                                            
           05 PV-YEAR                  PIC  9(04)          VALUE ZEROES.        
           05 PV-MONTH                 PIC  99             VALUE ZEROES.        
           05 PV-DAY                   PIC  99             VALUE ZEROES.        
       01  PV-PROCESS-EFF-DATE         PIC  X(10)          VALUE SPACES.        
       01  PV-PREVIOUS-END-DTE         PIC  X(10)          VALUE SPACES.        
      *                                                                         
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECORD-COUNTS.                                                
               10  PA-INPUT01-COUNT            PIC  9(9)  VALUE ZEROES.         
               10  PA-INPUT02-COUNT            PIC  9(9)  VALUE ZEROES.         
               10  PA-INPUT03-COUNT            PIC  9(9)  VALUE ZEROES.         
               10  PA-INS-TARGET-COUNT         PIC  9(9)  VALUE ZEROES.         
               10  PA-INS-OVERRIDE-COUNT       PIC  9(9)  VALUE ZEROES.         
               10  PA-INS-DFLT-COUNT           PIC  9(9)  VALUE ZEROES.         
               10  PA-UPD-TARGET-COUNT         PIC  9(9)  VALUE ZEROES.         
               10  PA-UPD-OVERRIDE-COUNT       PIC  9(9)  VALUE ZEROES.         
               10  PA-UPD-DFLT-COUNT           PIC  9(9)  VALUE ZEROES.         
               10  PA-INVALID1-COUNT           PIC  9(9)  VALUE ZEROES.         
               10  PA-INVALID2-COUNT           PIC  9(9)  VALUE ZEROES.         
               10  PA-INVALID3-COUNT           PIC  9(9)  VALUE ZEROES.         
      *                                                                         
       01  PD-PROGRAM-DISPLAYS.                                                 
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.                    
      *                                                                         
       01  CC-CONTROL-RECORD.                                                   
           05  CC-DATE                  PIC  X(10) VALUE SPACES.                
           05  CC-FILLER                PIC  X(70) VALUE SPACES.                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
      *                                                                         
      *************************************************************             
      * COPYBOOK FOR NUMERIC EDIT ROUTINE                         *             
      *************************************************************             
           COPY DPWS010I.                                                       
      *                                                                         
      ******************************************************************        
      *  USED FOR DB2 ERRORS                                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      *  DCL MLT_CUM_MKUP_OVRID TABLE                                           
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE MLT00012                                                 
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      *  DCL MLT_CUM_MKUP_TGT TABLE                                             
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE MLT00013                                                 
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      *  DCL MLT_CORP_GLBL_DFLT TABLE                                           
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE MLT00014                                                 
           END-EXEC.                                                            
      *                                                                         
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 2000-PROCESS-INPUT01                                         
                UNTIL PS-NO-MORE-INPUT01-RECORDS.                               
           PERFORM 3000-PROCESS-INPUT02                                         
                UNTIL PS-NO-MORE-INPUT02-RECORDS.                               
           PERFORM 4000-PROCESS-INPUT03                                         
                UNTIL PS-NO-MORE-INPUT03-RECORDS.                               
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
      *                                                                         
      ******************************************************************        
      * OPEN INPUT FILE , INITIALIZE TABLES , READ FIRST INPUT RECORD           
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           OPEN INPUT CORP-DFLT-FILE                                            
                      OVERRIDE-FILE                                             
                      TARGET-FILE                                               
                      CC-FILE.                                                  
           OPEN OUTPUT INVALID-CORP-DFLT-FILE                                   
                       INVALID-OVERRIDE-FILE                                    
                       INVALID-TARGET-FILE.                                     
           PERFORM 4400-READ-CC-RECORD.                                         
           IF CC-DATE = SPACES                                                  
               ACCEPT PV-TODAYS-DATE-FORMATTED FROM DATE YYYYMMDD               
               MOVE PV-YEAR  TO PV-DATE-YY                                      
               MOVE PV-MONTH TO PV-DATE-MM                                      
               MOVE PV-DAY   TO PV-DATE-DD                                      
               MOVE PV-INPUT-DATE TO PV-PROCESS-EFF-DATE                        
           ELSE                                                                 
               MOVE CC-DATE TO PV-PROCESS-EFF-DATE                              
           END-IF.                                                              
           EXEC SQL                                                             
               SELECT DATE(:PV-PROCESS-EFF-DATE) - 1 DAY                        
                 INTO :PV-PREVIOUS-END-DTE                                      
                 FROM SYSIBM.SYSDUMMY1                                          
           END-EXEC.                                                            
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-06            TO PV-OPERATION-ATTEMPTED              
               MOVE '* GET PREVIOUS DATE PROCESS *' TO                          
                    PV-PARAGRAPH-NAME                                           
               PERFORM 9998-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM 4100-READ-DFLT-FILE.                                         
           PERFORM 4200-READ-OVERRIDE-FILE.                                     
           PERFORM 4300-READ-TARGET-FILE.                                       
      *                                                                         
      ******************************************************************        
      * PROCESS THE INPUT01 CORP GLOBAL DEFAULT FILE                            
      ******************************************************************        
       2000-PROCESS-INPUT01.                                                    
           PERFORM 4500-UNSTRING-DFLT-REC.                                      
           PERFORM 4800-FIND-DFLT-ROW.                                          
           MOVE DFLT-DEPT-NBR    TO MLT00012-DEPT-NBR.                          
           MOVE DFLT-LBL-SEQ-NBR TO MLT00012-LBL-SEQ-NBR.                       
           MOVE DFLT-ENT-ID      TO MLT00012-ENT-ID.                            
           MOVE DFLT-OVRID-CUM-MKUP-PCT TO OVRID-CUM-MKUP-PCT.                  
           PERFORM 4900-FIND-OVRID-ROW.                                         
           PERFORM 4100-READ-DFLT-FILE.                                         
      *                                                                         
      ******************************************************************        
      * PROCESS THE INPUT02 OVERRIDE FILE                                       
      ******************************************************************        
       3000-PROCESS-INPUT02.                                                    
           PERFORM 4600-UNSTRING-OVRID-REC.                                     
           MOVE OVRID-DEPT-NBR    TO MLT00012-DEPT-NBR.                         
           MOVE OVRID-LBL-SEQ-NBR TO MLT00012-LBL-SEQ-NBR.                      
           MOVE OVRID-ENT-ID      TO MLT00012-ENT-ID.                           
           PERFORM 4900-FIND-OVRID-ROW.                                         
           PERFORM 4200-READ-OVERRIDE-FILE.                                     
      *                                                                         
      ******************************************************************        
      * PROCESS THE INPUT TARGET FILE                                           
      ******************************************************************        
       4000-PROCESS-INPUT03.                                                    
           PERFORM 4700-UNSTRING-TGT-REC.                                       
           MOVE TGT-DEPT-NBR TO MLT00013-DEPT-NBR.                              
           PERFORM 5000-FIND-TGT-ROW.                                           
                                                                                
           PERFORM 4300-READ-TARGET-FILE.                                       
      *****************************************************************         
      * 4100-READ-DFLT-FILE.                                                    
      *****************************************************************         
       4100-READ-DFLT-FILE.                                                     
           READ CORP-DFLT-FILE INTO PV-DFLT-REC                                 
               AT END MOVE 'Y' TO PS-INPUT01-FILE-EOF-SW.                       
           IF PS-MORE-INPUT01-RECORDS                                           
               ADD +1 TO PA-INPUT01-COUNT                                       
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * 4200-READ-OVERRIDE-FILE                                                 
      ******************************************************************        
       4200-READ-OVERRIDE-FILE.                                                 
           READ OVERRIDE-FILE INTO PV-OVRID-REC                                 
               AT END MOVE 'Y' TO PS-INPUT02-FILE-EOF-SW.                       
           IF PS-MORE-INPUT02-RECORDS                                           
               ADD +1 TO PA-INPUT02-COUNT                                       
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * 4300-READ-TARGET-FILE                                                   
      ******************************************************************        
       4300-READ-TARGET-FILE.                                                   
           READ TARGET-FILE INTO PV-TGT-REC                                     
               AT END MOVE 'Y' TO PS-INPUT03-FILE-EOF-SW.                       
           IF PS-MORE-INPUT03-RECORDS                                           
               ADD +1 TO PA-INPUT03-COUNT                                       
           END-IF.                                                              
      *                                                                         
      *****************************************************************         
      * 4400-READ-CC-RECORD.                                                    
      *****************************************************************         
       4400-READ-CC-RECORD.                                                     
           READ CC-FILE INTO CC-CONTROL-RECORD.                                 
      *                                                                         
      ******************************************************************        
      *  THE INPUT RECORDS ARE NOT IN A FIXED FORMAT.  THE FIELDS ARE           
      *  DELIMITED BY COMMAS AND THE NUMERIC FIELDS ARE IN A CHARACTER          
      *  FORMAT.  THIS PARAGRAPH UNSTRINGS THE DATA AND CONVERTS THE            
      *  COST AND RETAIL AMOUNTS INTO A NUMERIC FORMAT.                         
      ******************************************************************        
       4500-UNSTRING-DFLT-REC.                                                  
           MOVE +1    TO PV-UNSTR-PTR.                                          
      *    UNSTRING THE DEFAULT DEPT NBR                                        
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-DFLT-REC                                                 
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 4     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 0     TO DP010I-MAXIMUM-DECIMALS.                               
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO DFLT-DEPT-NBR.                          
                                                                                
      *    UNSTRING THE DEFAULT LBL SEQ NBR                                     
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-DFLT-REC                                                 
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
263218     MOVE 5     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 0     TO DP010I-MAXIMUM-DECIMALS.                               
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO DFLT-LBL-SEQ-NBR.                       
                                                                                
      *    UNSTRING THE DEFAULT ENT ID                                          
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-DFLT-REC                                                 
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 9     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 0     TO DP010I-MAXIMUM-DECIMALS.                               
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO DFLT-ENT-ID.                            
                                                                                
      *    UNSTRING THE DEFAULT MIN CUM MARK UP PERCENT.                        
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-DFLT-REC                                                 
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 4     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 6     TO DP010I-MAXIMUM-DECIMALS.                               
           SET DP010I-NEGATIVE-ALLOWED TO TRUE.                                 
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO DFLT-MIN-CUM-MKUP-PCT.                  
                                                                                
      *    UNSTRING THE DEFAULT MAX CUM MARK UP PERCENT.                        
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-DFLT-REC                                                 
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 4     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 6     TO DP010I-MAXIMUM-DECIMALS.                               
           SET DP010I-NEGATIVE-ALLOWED TO TRUE.                                 
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           IF DP010I-NUMERIC-FIELD LESS THAN DFLT-MIN-CUM-MKUP-PCT              
               MOVE DFLT-MIN-CUM-MKUP-PCT TO DFLT-MAX-CUM-MKUP-PCT              
               MOVE DP010I-NUMERIC-FIELD TO DFLT-MIN-CUM-MKUP-PCT               
           ELSE                                                                 
               MOVE DP010I-NUMERIC-FIELD TO DFLT-MAX-CUM-MKUP-PCT               
           END-IF.                                                              
                                                                                
      *    UNSTRING THE DEFAULT OVERRIDE CUM MARK UP PERCENT.                   
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-DFLT-REC                                                 
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 4     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 6     TO DP010I-MAXIMUM-DECIMALS.                               
           SET DP010I-NEGATIVE-ALLOWED TO TRUE.                                 
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO DFLT-OVRID-CUM-MKUP-PCT.                
                                                                                
      ******************************************************************        
      *  THE INPUT RECORDS ARE NOT IN A FIXED FORMAT.  THE FIELDS ARE           
      *  DELIMITED BY COMMAS AND THE NUMERIC FIELDS ARE IN A CHARACTER          
      *  FORMAT.  THIS PARAGRAPH UNSTRINGS THE DATA AND CONVERTS THE            
      *  COST AND RETAIL AMOUNTS INTO A NUMERIC FORMAT.                         
      ******************************************************************        
       4600-UNSTRING-OVRID-REC.                                                 
      *    UNSTRING THE DEPARTMENT NUMBER.                                      
           MOVE +1    TO PV-UNSTR-PTR.                                          
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-OVRID-REC                                                
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 4     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 0     TO DP010I-MAXIMUM-DECIMALS.                               
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO OVRID-DEPT-NBR.                         
                                                                                
      *    UNSTRING THE EFFECTIVE BEGIN DATE.                                   
           UNSTRING PV-OVRID-REC                                                
               DELIMITED BY PC-SLASH                                            
               INTO         PV-DATE-MM                                          
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           UNSTRING PV-OVRID-REC                                                
               DELIMITED BY PC-SLASH                                            
               INTO         PV-DATE-DD                                          
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           UNSTRING PV-OVRID-REC                                                
               DELIMITED BY PC-COMMA                                            
               INTO         PV-DATE-YY                                          
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE PV-INPUT-DATE TO OVRID-EFF-BEG-DTE.                             
                                                                                
      *    UNSTRING THE EFFECTIVE END DATE.                                     
           UNSTRING PV-OVRID-REC                                                
               DELIMITED BY PC-COMMA                                            
               INTO         OVRID-EFF-END-DTE                                   
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
                                                                                
      *    UNSTRING THE OVERRIDE CUM MARK UP PERCENT.                           
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-OVRID-REC                                                
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 4     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 6     TO DP010I-MAXIMUM-DECIMALS.                               
           SET DP010I-NEGATIVE-ALLOWED TO TRUE.                                 
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO OVRID-CUM-MKUP-PCT.                     
                                                                                
      *    UNSTRING THE OVERRIDE LBL SEQ NBR                                    
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-OVRID-REC                                                
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
263218     MOVE 5     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 0     TO DP010I-MAXIMUM-DECIMALS.                               
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO OVRID-LBL-SEQ-NBR.                      
                                                                                
      *    UNSTRING THE OVERRIDE ENT ID                                         
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-OVRID-REC                                                
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 9     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 0     TO DP010I-MAXIMUM-DECIMALS.                               
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO OVRID-ENT-ID.                           
                                                                                
      ******************************************************************        
      *  THE INPUT RECORDS ARE NOT IN A FIXED FORMAT.  THE FIELDS ARE           
      *  DELIMITED BY COMMAS AND THE NUMERIC FIELDS ARE IN A CHARACTER          
      *  FORMAT.  THIS PARAGRAPH UNSTRINGS THE DATA AND CONVERTS THE            
      *  COST AND RETAIL AMOUNTS INTO A NUMERIC FORMAT.                         
      ******************************************************************        
       4700-UNSTRING-TGT-REC.                                                   
           MOVE +1    TO PV-UNSTR-PTR.                                          
      *    UNSTRING THE TARGET DEPT NBR                                         
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-TGT-REC                                                  
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 4     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 0     TO DP010I-MAXIMUM-DECIMALS.                               
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO TGT-DEPT-NBR.                           
                                                                                
      *    UNSTRING THE TARGET EFFECTIVE BEGIN DATE.                            
           UNSTRING PV-TGT-REC                                                  
               DELIMITED BY PC-SLASH                                            
               INTO         PV-DATE-MM                                          
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           UNSTRING PV-TGT-REC                                                  
               DELIMITED BY PC-SLASH                                            
               INTO         PV-DATE-DD                                          
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           UNSTRING PV-TGT-REC                                                  
               DELIMITED BY PC-COMMA                                            
               INTO         PV-DATE-YY                                          
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE PV-INPUT-DATE TO TGT-EFF-BEG-DTE.                               
                                                                                
      *    UNSTRING THE EFFECTIVE END DATE.                                     
           UNSTRING PV-TGT-REC                                                  
               DELIMITED BY PC-COMMA                                            
               INTO         TGT-EFF-END-DTE                                     
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
                                                                                
      *    UNSTRING THE TARGET MIN CUM MARK UP PERCENT.                         
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-TGT-REC                                                  
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 4     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 6     TO DP010I-MAXIMUM-DECIMALS.                               
           SET DP010I-NEGATIVE-ALLOWED TO TRUE.                                 
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           MOVE DP010I-NUMERIC-FIELD TO TGT-MIN-CUM-MKUP-PCT.                   
                                                                                
      *    UNSTRING THE TARGET MAX CUM MARK UP PERCENT.                         
           MOVE SPACE TO DP010I-UNEDITED-FIELD.                                 
           UNSTRING PV-TGT-REC                                                  
               DELIMITED BY PC-COMMA                                            
               INTO         DP010I-UNEDITED-FIELD                               
               POINTER      PV-UNSTR-PTR                                        
           END-UNSTRING.                                                        
           MOVE 4     TO DP010I-MAXIMUM-DIGITS.                                 
           MOVE 6     TO DP010I-MAXIMUM-DECIMALS.                               
           SET DP010I-NEGATIVE-ALLOWED TO TRUE.                                 
           PERFORM 5550-NUMERIC-CONVERSION.                                     
           IF DP010I-NUMERIC-FIELD LESS THAN TGT-MIN-CUM-MKUP-PCT               
               MOVE TGT-MIN-CUM-MKUP-PCT TO TGT-MAX-CUM-MKUP-PCT                
               MOVE DP010I-NUMERIC-FIELD TO TGT-MIN-CUM-MKUP-PCT                
           ELSE                                                                 
               MOVE DP010I-NUMERIC-FIELD TO TGT-MAX-CUM-MKUP-PCT                
           END-IF.                                                              
                                                                                
           MOVE -15000 TO TGT-MIN-GRO-MRGN-AMT.                                 
           MOVE +15000 TO TGT-MAX-GRO-MRGN-AMT.                                 
                                                                                
      **************************************************************            
      * 4800-FIND-DFLT-ROW.                                                     
      **************************************************************            
       4800-FIND-DFLT-ROW.                                                      
           EXEC SQL                                                             
               SELECT  EFF_BEG_DTE                                              
                 INTO  :MLT00014-EFF-BEG-DTE                                    
                 FROM  MLT_CORP_GLBL_DFLT                                       
                 WHERE EFF_END_DTE IS NULL                                      
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   IF PV-PROCESS-EFF-DATE > MLT00014-EFF-BEG-DTE                
                       MOVE PV-PREVIOUS-END-DTE TO MLT00014-EFF-END-DTE         
                       PERFORM 6150-UPDATE-DFLT-ROW                             
                       MOVE PV-PROCESS-EFF-DATE TO MLT00014-EFF-BEG-DTE         
                       MOVE DFLT-MIN-CUM-MKUP-PCT                               
                           TO MLT00014-MIN-CUM-MKUP-PCT                         
                       MOVE DFLT-MAX-CUM-MKUP-PCT                               
                           TO MLT00014-MAX-CUM-MKUP-PCT                         
                       MOVE -15000                                              
                           TO MLT00014-MIN-GRO-MRGN-AMT                         
                       MOVE +15000                                              
                           TO MLT00014-MAX-GRO-MRGN-AMT                         
                       PERFORM 6100-INSERT-DFLT-ROW                             
                   ELSE                                                         
                       PERFORM 7100-WRITE-INVALID1-REC                          
                   END-IF                                                       
               WHEN +100                                                        
                   MOVE PV-PROCESS-EFF-DATE TO MLT00014-EFF-BEG-DTE             
                   MOVE DFLT-MIN-CUM-MKUP-PCT                                   
                       TO MLT00014-MIN-CUM-MKUP-PCT                             
                   MOVE DFLT-MAX-CUM-MKUP-PCT                                   
                       TO MLT00014-MAX-CUM-MKUP-PCT                             
                   MOVE -15000                                                  
                       TO MLT00014-MIN-GRO-MRGN-AMT                             
                   MOVE +15000                                                  
                       TO MLT00014-MAX-GRO-MRGN-AMT                             
                   PERFORM 6100-INSERT-DFLT-ROW                                 
               WHEN OTHER                                                       
                   MOVE PE-MSG-04              TO PV-OPERATION-ATTEMPTED        
                   MOVE '* 4800-FIND-DFLT-ROW *' TO PV-PARAGRAPH-NAME           
                   PERFORM 9998-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      **************************************************************            
      * 4900-FIND-OVRID-ROW.                                                    
      **************************************************************            
       4900-FIND-OVRID-ROW.                                                     
           EXEC SQL                                                             
               SELECT  EFF_BEG_DTE                                              
                 INTO  :MLT00012-EFF-BEG-DTE                                    
                 FROM  MLT_CUM_MKUP_OVRID                                       
                 WHERE DEPT_NBR    = :MLT00012-DEPT-NBR                         
                   AND LBL_SEQ_NBR = :MLT00012-LBL-SEQ-NBR                      
                   AND ENT_ID      = :MLT00012-ENT-ID                           
                   AND EFF_END_DTE IS NULL                                      
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   IF PV-PROCESS-EFF-DATE > MLT00012-EFF-BEG-DTE                
                       IF OVRID-CUM-MKUP-PCT = ZEROES                           
                           CONTINUE                                             
                       ELSE                                                     
                           MOVE PV-PREVIOUS-END-DTE                             
                               TO MLT00012-EFF-END-DTE                          
                           PERFORM 6250-UPDATE-OVRID-ROW                        
                           MOVE PV-PROCESS-EFF-DATE                             
                               TO MLT00012-EFF-BEG-DTE                          
                           MOVE OVRID-CUM-MKUP-PCT                              
                               TO MLT00012-OVRID-CUM-MKUP-PCT                   
                           PERFORM 6200-INSERT-OVRID-ROW                        
                       END-IF                                                   
                   ELSE                                                         
                       PERFORM 7200-WRITE-INVALID2-REC                          
                   END-IF                                                       
               WHEN +100                                                        
                   IF OVRID-CUM-MKUP-PCT = ZEROES                               
                       CONTINUE                                                 
                   ELSE                                                         
                       MOVE PV-PROCESS-EFF-DATE                                 
                           TO MLT00012-EFF-BEG-DTE                              
                       MOVE OVRID-CUM-MKUP-PCT                                  
                           TO MLT00012-OVRID-CUM-MKUP-PCT                       
                       PERFORM 6200-INSERT-OVRID-ROW                            
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE PE-MSG-04              TO PV-OPERATION-ATTEMPTED        
                   MOVE '* 4900-FIND-OVRID-ROW *' TO PV-PARAGRAPH-NAME          
                   PERFORM 9998-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      **************************************************************            
      * 5000-FIND-TGT-ROW.                                                      
      **************************************************************            
       5000-FIND-TGT-ROW.                                                       
           EXEC SQL                                                             
               SELECT  EFF_BEG_DTE                                              
                 INTO  :MLT00013-EFF-BEG-DTE                                    
                 FROM  MLT_CUM_MKUP_TGT                                         
                 WHERE DEPT_NBR    = :MLT00013-DEPT-NBR                         
                   AND EFF_END_DTE IS NULL                                      
           END-EXEC.                                                            
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   IF PV-PROCESS-EFF-DATE > MLT00013-EFF-BEG-DTE                
                       IF (TGT-MIN-CUM-MKUP-PCT = ZEROES                        
                           OR TGT-MAX-CUM-MKUP-PCT = ZEROES)                    
                           CONTINUE                                             
                       ELSE                                                     
                           MOVE PV-PREVIOUS-END-DTE                             
                               TO MLT00013-EFF-END-DTE                          
                           PERFORM 6350-UPDATE-TGT-ROW                          
                           MOVE PV-PROCESS-EFF-DATE                             
                               TO MLT00013-EFF-BEG-DTE                          
                           MOVE TGT-MIN-CUM-MKUP-PCT                            
                               TO MLT00013-MIN-CUM-MKUP-PCT                     
                           MOVE TGT-MAX-CUM-MKUP-PCT                            
                               TO MLT00013-MAX-CUM-MKUP-PCT                     
                           MOVE TGT-MIN-GRO-MRGN-AMT                            
                               TO MLT00013-MIN-GRO-MRGN-AMT                     
                           MOVE TGT-MAX-GRO-MRGN-AMT                            
                               TO MLT00013-MAX-GRO-MRGN-AMT                     
                           PERFORM 6300-INSERT-TGT-ROW                          
                       END-IF                                                   
                   ELSE                                                         
                       PERFORM 7300-WRITE-INVALID3-REC                          
                   END-IF                                                       
               WHEN +100                                                        
                   IF (TGT-MIN-CUM-MKUP-PCT = ZEROES                            
                       OR TGT-MAX-CUM-MKUP-PCT = ZEROES)                        
                       CONTINUE                                                 
                   ELSE                                                         
                       MOVE PV-PROCESS-EFF-DATE                                 
                           TO MLT00013-EFF-BEG-DTE                              
                       MOVE TGT-MIN-CUM-MKUP-PCT                                
                           TO MLT00013-MIN-CUM-MKUP-PCT                         
                       MOVE TGT-MAX-CUM-MKUP-PCT                                
                           TO MLT00013-MAX-CUM-MKUP-PCT                         
                       MOVE TGT-MIN-GRO-MRGN-AMT                                
                           TO MLT00013-MIN-GRO-MRGN-AMT                         
                       MOVE TGT-MAX-GRO-MRGN-AMT                                
                           TO MLT00013-MAX-GRO-MRGN-AMT                         
                       PERFORM 6300-INSERT-TGT-ROW                              
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE PE-MSG-04              TO PV-OPERATION-ATTEMPTED        
                   MOVE '* 5000-FIND-TGT-ROW *' TO PV-PARAGRAPH-NAME            
                   PERFORM 9998-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      **************************************************************            
      * 5550-NUMERIC-CONVERSION.                                                
      **************************************************************            
       5550-NUMERIC-CONVERSION.                                                 
                                                                                
           CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
               USING DP010I-NUMERIC-EDIT-AREA.                                  
                                                                                
           IF DP010I-NO-ERROR-DETECTED                                          
               NEXT SENTENCE                                                    
           ELSE                                                                 
               IF DP010I-INVALID-CHARACTER                                      
                   MOVE ZEROES TO DP010I-NUMERIC-FIELD                          
               ELSE                                                             
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PARAGRAPH 5550-NUMERIC-CONVERSION **'            
                   DISPLAY '** ERROR IN CALLED NUMERIC EDIT ROUTINE.'           
                   DISPLAY '** ERROR INDICATOR = ' DP010I-ERROR-IND             
                   DISPLAY '** INPUT RECORD = ' PV-OVRID-REC                    
                   MOVE +4008 TO PV-ABEND-CODE                                  
                   PERFORM 9998-ABEND                                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 6100-INSERT-DFLT-ROW.                                                   
      *****************************************************************         
       6100-INSERT-DFLT-ROW.                                                    
           EXEC SQL                                                             
               INSERT INTO MLT_CORP_GLBL_DFLT                                   
                   (   EFF_BEG_DTE                                              
                     , MIN_CUM_MKUP_PCT                                         
                     , MAX_CUM_MKUP_PCT                                         
                     , EFF_END_DTE                                              
                     , MIN_GRO_MRGN_AMT                                         
                     , MAX_GRO_MRGN_AMT)                                        
               VALUES                                                           
                   (   :MLT00014-EFF-BEG-DTE                                    
                     , :MLT00014-MIN-CUM-MKUP-PCT                               
                     , :MLT00014-MAX-CUM-MKUP-PCT                               
                     , NULL                                                     
                     , :MLT00014-MIN-GRO-MRGN-AMT                               
                     , :MLT00014-MAX-GRO-MRGN-AMT)                              
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE ZEROES        TO PV-DEADLOCK-COUNT                      
                   ADD +1             TO PA-INS-DFLT-COUNT                      
               WHEN -911                                                        
                   ADD +1             TO PV-DEADLOCK-COUNT                      
                   DISPLAY 'DEADLOCK -911 IN 6100-INSERT-DFLT-ROW'              
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS                      
                       MOVE '6100-INSERT-DFLT-ROW'                              
                           TO PV-PARAGRAPH-NAME                                 
                       PERFORM 9000-DEADLOCK-ERROR                              
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE '6100-INSERT-DFLT-ROW' TO PV-PARAGRAPH-NAME             
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * THE INPUT01 RECORD FOUND A MATCHING ROW BASED ON EFF_BEG_DTE.           
      * CURRENT TABLE EFF_END_DTE VALUES ARE UPDATED WITH THE PREVIOUS          
      * DATE.                                                                   
      *****************************************************************         
       6150-UPDATE-DFLT-ROW.                                                    
           EXEC SQL                                                             
               UPDATE MLT_CORP_GLBL_DFLT                                        
                   SET EFF_END_DTE  = :MLT00014-EFF-END-DTE                     
               WHERE   EFF_BEG_DTE  = :MLT00014-EFF-BEG-DTE                     
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE ZERO          TO PV-DEADLOCK-COUNT                      
                   ADD 1              TO PA-UPD-DFLT-COUNT                      
               WHEN -911                                                        
                   ADD +1             TO PV-DEADLOCK-COUNT                      
                   DISPLAY 'DEADLOCK -911 IN 6150-UPDATE-DFLT-ROW'              
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS                      
                       MOVE '6150-UPDATE-DFLT-ROW'                              
                                      TO PV-PARAGRAPH-NAME                      
                       PERFORM 9000-DEADLOCK-ERROR                              
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE '6150-UPDATE-DFLT-ROW' TO PV-PARAGRAPH-NAME             
                   MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      *                                                                         
      *****************************************************************         
       6200-INSERT-OVRID-ROW.                                                   
           EXEC SQL                                                             
               INSERT INTO MLT_CUM_MKUP_OVRID                                   
                   (   DEPT_NBR                                                 
                     , LBL_SEQ_NBR                                              
                     , ENT_ID                                                   
                     , EFF_BEG_DTE                                              
                     , OVRID_CUM_MKUP_PCT                                       
                     , EFF_END_DTE)                                             
               VALUES                                                           
                   (   :MLT00012-DEPT-NBR                                       
                     , :MLT00012-LBL-SEQ-NBR                                    
                     , :MLT00012-ENT-ID                                         
                     , :MLT00012-EFF-BEG-DTE                                    
                     , :MLT00012-OVRID-CUM-MKUP-PCT                             
                     , NULL)                                                    
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE ZERO          TO PV-DEADLOCK-COUNT                      
                   ADD 1              TO PA-INS-OVERRIDE-COUNT                  
               WHEN -911                                                        
                   ADD +1             TO PV-DEADLOCK-COUNT                      
                   DISPLAY 'DEADLOCK -911 IN 6200-INSERT-OVRID-ROW'             
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS                      
                       MOVE '6200-INSERT-OVRID-ROW'                             
                           TO PV-PARAGRAPH-NAME                                 
                       PERFORM 9000-DEADLOCK-ERROR                              
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE '6200-INSERT-OVRID-ROW' TO PV-PARAGRAPH-NAME            
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO AND            
      * DEPARTMENT.  CURRENT TABLE VALUES ARE UPDATED WITH THE VALUES           
      * FROM THE INPUT RECORD.                                                  
      *****************************************************************         
       6250-UPDATE-OVRID-ROW.                                                   
           EXEC SQL                                                             
               UPDATE MLT_CUM_MKUP_OVRID                                        
                   SET EFF_END_DTE = :MLT00012-EFF-END-DTE                      
               WHERE DEPT_NBR      = :MLT00012-DEPT-NBR                         
                 AND LBL_SEQ_NBR   = :MLT00012-LBL-SEQ-NBR                      
                 AND ENT_ID        = :MLT00012-ENT-ID                           
                 AND EFF_BEG_DTE   = :MLT00012-EFF-BEG-DTE                      
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE ZERO          TO PV-DEADLOCK-COUNT                      
                   ADD 1              TO PA-UPD-OVERRIDE-COUNT                  
               WHEN -911                                                        
                   ADD +1             TO PV-DEADLOCK-COUNT                      
                   DISPLAY 'DEADLOCK -911 IN 6250-UPDATE-OVRID-ROW'             
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS                      
                       MOVE '6250-UPDATE-OVRID-ROW'                             
                                      TO PV-PARAGRAPH-NAME                      
                       PERFORM 9000-DEADLOCK-ERROR                              
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE '6250-UPDATE-OVRID-ROW' TO PV-PARAGRAPH-NAME            
                   MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      * THERE WAS NO MATCHING ROW ON THE MONTHLY DEPT PLAN TABLE.     *         
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            *         
      *****************************************************************         
       6300-INSERT-TGT-ROW.                                                     
      *                                                                         
           EXEC SQL                                                             
               INSERT INTO MLT_CUM_MKUP_TGT                                     
                   (   DEPT_NBR                                                 
                     , EFF_BEG_DTE                                              
                     , MIN_CUM_MKUP_PCT                                         
                     , MAX_CUM_MKUP_PCT                                         
                     , EFF_END_DTE                                              
                     , MIN_GRO_MRGN_AMT                                         
                     , MAX_GRO_MRGN_AMT)                                        
               VALUES                                                           
                   (   :MLT00013-DEPT-NBR                                       
                     , :MLT00013-EFF-BEG-DTE                                    
                     , :MLT00013-MIN-CUM-MKUP-PCT                               
                     , :MLT00013-MAX-CUM-MKUP-PCT                               
                     , NULL                                                     
                     , :MLT00013-MIN-GRO-MRGN-AMT                               
                     , :MLT00013-MAX-GRO-MRGN-AMT)                              
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE ZERO TO PV-DEADLOCK-COUNT                               
                   ADD 1     TO PA-INS-TARGET-COUNT                             
               WHEN -911                                                        
                   ADD +1    TO PV-DEADLOCK-COUNT                               
                   DISPLAY 'DEADLOCK -911 IN 6300-INSERT-TGT-ROW'               
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS                      
                       MOVE '6300-INSERT-TGT-ROW'                               
                           TO PV-PARAGRAPH-NAME                                 
                       PERFORM 9000-DEADLOCK-ERROR                              
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE '6300-INSERT-TGT-ROW' TO PV-PARAGRAPH-NAME              
                   MOVE PE-MSG-03             TO PV-OPERATION-ATTEMPTED         
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO AND            
      * DEPARTMENT.  CURRENT TABLE VALUES ARE UPDATED WITH THE VALUES           
      * FROM THE INPUT RECORD.                                                  
      *****************************************************************         
       6350-UPDATE-TGT-ROW.                                                     
           EXEC SQL                                                             
               UPDATE MLT_CUM_MKUP_TGT                                          
                   SET EFF_END_DTE = :MLT00013-EFF-END-DTE                      
                WHERE DEPT_NBR     = :MLT00013-DEPT-NBR                         
                  AND EFF_BEG_DTE  = :MLT00013-EFF-BEG-DTE                      
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE ZERO          TO PV-DEADLOCK-COUNT                      
                   ADD 1              TO PA-UPD-TARGET-COUNT                    
               WHEN -911                                                        
                   ADD +1             TO PV-DEADLOCK-COUNT                      
                   DISPLAY 'DEADLOCK -911 IN 6350-UPDATE-TGT-ROW'               
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS                      
                       MOVE '6350-UPDATE-TGT-ROW'                               
                                      TO PV-PARAGRAPH-NAME                      
                       PERFORM 9000-DEADLOCK-ERROR                              
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE '6350-UPDATE-TGT-ROW' TO PV-PARAGRAPH-NAME              
                   MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      *  WHEN THE PROCESS DATE IS LESS THAN EFF BEG DATE FOR DFLT TABLE         
      ******************************************************************        
       7100-WRITE-INVALID1-REC.                                                 
           WRITE INVALID-CORP-DFLT-RECORD FROM CORP-DFLT-RECORD.                
           ADD +1             TO PA-INVALID1-COUNT.                             
                                                                                
       7200-WRITE-INVALID2-REC.                                                 
           WRITE INVALID-OVERRIDE-RECORD FROM OVERRIDE-RECORD.                  
           ADD +1             TO PA-INVALID2-COUNT.                             
                                                                                
       7300-WRITE-INVALID3-REC.                                                 
           WRITE INVALID-TARGET-RECORD FROM TARGET-RECORD.                      
           ADD +1             TO PA-INVALID3-COUNT.                             
                                                                                
      ******************************************************************        
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *        
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY 'READ INPUT01 RECORDS: ', PA-INPUT01-COUNT.                  
           DISPLAY 'READ INPUT02 RECORDS: ', PA-INPUT02-COUNT.                  
           DISPLAY 'READ INPUT03 RECORDS: ', PA-INPUT03-COUNT.                  
           DISPLAY 'INSERT DEFAULT ROWS:  ', PA-INS-DFLT-COUNT.                 
           DISPLAY 'INSERT OVERRIDE ROWS: ', PA-INS-OVERRIDE-COUNT.             
           DISPLAY 'INSERT TARGET ROWS:   ', PA-INS-TARGET-COUNT.               
           DISPLAY 'UPDATE DEFAULT ROWS:  ', PA-UPD-DFLT-COUNT.                 
           DISPLAY 'UPDATE OVERRIDE ROWS: ', PA-UPD-OVERRIDE-COUNT.             
           DISPLAY 'UPDATE TARGET ROWS:   ', PA-UPD-TARGET-COUNT.               
                                                                                
           EVALUATE TRUE                                                        
               WHEN PA-INVALID1-COUNT > ZEROES                                  
               WHEN PA-INVALID2-COUNT > ZEROES                                  
               WHEN PA-INVALID3-COUNT > ZEROES                                  
                   MOVE PE-MSG-05     TO PV-OPERATION-ATTEMPTED                 
                   DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED        
           END-EVALUATE.                                                        
                                                                                
           CLOSE TARGET-FILE                                                    
                 OVERRIDE-FILE                                                  
                 CORP-DFLT-FILE                                                 
                 CC-FILE                                                        
                 INVALID-CORP-DFLT-FILE                                         
                 INVALID-OVERRIDE-FILE                                          
                 INVALID-TARGET-FILE.                                           
                                                                                
      ******************************************************************        
      *  PERFORMED BY 6*00-INSERT AND 6*50-UPDATE                               
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)           
      ******************************************************************        
       9000-DEADLOCK-ERROR.                                                     
      *                                                                         
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND.                                          
      *                                                                         
       9998-ABEND.                                                              
                                                                                
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           MOVE SQLCODE               TO PV-SQLCODE-DISPLAY.                    
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
           DISPLAY '**  SQLCODE   **  = ' PV-SQLCODE-DISPLAY.                   
                                                                                
           EXEC SQL                                                             
                ROLLBACK                                                        
           END-EXEC.                                                            
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
