       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT474.                                         00020022
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  10-01-2000.                                       00050022
       DATE-COMPILED.                                                   00060000
                                                                        00061021
      ******************************************************************00070000
      * MLKUT474 - MTHLY MAJOR CLASS TABLE (TMNMCLC) RECALC EXTRACT     00090022
      *                                                                 00100000
      * THIS PROGRAM WILL CREATE THE EXTRACT FOR THE RECALC PROGRAM     00120000
      * ON TABLE TMNMCLC. IT WILL FIND ALL DEPARTMENT/MAJOR CLASS/CHNL  00130022
      * COMBINATIONS ON THE TMNMCLC DB2 TABLE FOR THE CURRENT AND       00140022
      * PREVIOUS FISCAL MTHS WHICH WILL BE USED TO PROCESS THE TMNMCLC  00150022
      * TABLE RECALC.                                                   00160000
      ******************************************************************00170000
      * WR/PITS#  DATE      DESCRIPTION                                *00171029
      * -------- --------   -----------------------------------------  *00172029
      * W31475  06/01/2009  COGNIZANT                                  *00173029
      *                     MODIFIED CURSOR DECLARATION FOR MULTI ROW  *00174029
      *                     FETCH AS PART OF ML BATCH PERF TUNE PRJ    *00175029
      ******************************************************************00176029
      *                                                                 00180000
       ENVIRONMENT DIVISION.                                            00190000
       CONFIGURATION SECTION.                                           00210000
       SOURCE-COMPUTER.        IBM-3090.                                00230000
       OBJECT-COMPUTER.        IBM-3090.                                00240000
       INPUT-OUTPUT SECTION.                                            00260000
       FILE-CONTROL.                                                    00270000
                                                                        00280000
           SELECT MNMCLC-EXTR-FILE                                      00290022
               ASSIGN TO OUT01.                                         00300000
                                                                        00310000
       DATA DIVISION.                                                   00330000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  MNMCLC-EXTR-FILE                                             00380022
           RECORDING MODE IS F                                          00390000
           LABEL RECORDS ARE STANDARD                                   00400000
           BLOCK CONTAINS 0 RECORDS.                                    00410000
       01  MNMCLC-EXTR-REC             PIC X(10).                       00430022
                                                                        00440000
      ******************************************************************00460000
       WORKING-STORAGE SECTION.                                         00470000
      ******************************************************************00480000
                                                                        00490000
W31475 01  ROW-ID                      PIC S9(04) COMP VALUE 0.         00492028
  |    01  HOST-VARIABLES.                                              00493028
  |        05  HV-MNMCLC-DEPT-NBR      PIC X(03)   VALUE SPACES         00494028
  |                                    OCCURS 100  TIMES.               00495028
  |        05  HV-MNMCLC-MAJ-CL-NBR    PIC X(02)   VALUE SPACES         00496028
  |                                    OCCURS 100  TIMES.               00497028
  |        05  HV-MNMCLC-CHNL-CDE      PIC X(01)   VALUE SPACES         00498028
W31475                                 OCCURS 100  TIMES.               00499028
       01  PS-PROGRAM-SWITCHES.                                         00500000
           05  PS-TMNMCLC-DONE         PIC X       VALUE 'N'.           00510022
               88  PS-TMNMCLC-IS-DONE              VALUE 'Y'.           00520022
               88  PS-TMNMCLC-IS-NOT-DONE          VALUE 'N'.           00530022
                                                                        00540000
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              00550000
           05  PA-TMNMCLC-ROWS-FETCHED PIC S9(13)  VALUE ZERO.          00560022
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.          00570000
                                                                        00580000
       01  CD-COUNTER-DISPLAY.                                          00590000
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00600000
                                                                        00610000
       01 ABEND-AREAS.                                                  00620000
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00630000
                                                   COMP SYNC.           00640000
               88  AC-BAD-DATA                     VALUE +4003.         00650000
               88  AC-DB2-ERROR                    VALUE +4013.         00660000
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00670000
                 '*****       ABEND'.                                   00680000
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00690000
                   '*****   PROGRAM: MLKUT474'.                         00700022
           05  AA-PARAGRAPH-LIT.                                        00710000
               10  FILLER              PIC X(17)   VALUE                00720000
                   '***** PARAGRAPH: '.                                 00730000
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00740000
           05  AA-MESSAGE-LINE.                                         00750000
               10  FILLER              PIC X(06)   VALUE '*****'.       00760000
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00770000
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00780000
                   '*****    DB2 ERROR'.                                00790000
           05  AA-DB2-OPERATION-LIT.                                    00800000
               10  FILLER              PIC X(17)   VALUE                00810000
                   '***** OPERATION: '.                                 00820000
               10  AA-DB2-OPERATION.                                    00830000
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        00840000
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        00850000
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        00860000
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        00870000
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        00880000
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        00890000
                                                                        00900000
       01  EXTR-RECORD.                                                 00920007
           05  EXTR-DEPT-NBR    PIC X(03)    VALUE SPACES.              00930007
           05  EXTR-MAJ-CL-NBR  PIC X(02)    VALUE SPACES.              00940007
           05  EXTR-CHNL-CDE    PIC X(01)    VALUE SPACE.               00950021
           05  FILLER           PIC X(04)    VALUE SPACES.              00960021
       EJECT                                                            00970000
                                                                        00980000
      ******************************************************************00990000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01000000
      ******************************************************************01010000
           COPY DPWS004.                                                01020000
                                                                        01030000
      ******************************************************************01040000
      *  DB2 ERRORS                                                     01050000
      ******************************************************************01060000
           COPY SQLCA2.                                                 01070000
                                                                        01080000
      ******************************************************************01090000
      *  DB2 COMMUNICATION AREA                                         01100000
      ******************************************************************01110000
           EXEC SQL                                                     01120000
                INCLUDE SQLCA                                           01130000
           END-EXEC.                                                    01140000
                                                                        01150000
      ******************************************************************01160000
      *  DB2 TMNMCLC TABLE LAYOUT                                       01170022
      ******************************************************************01180000
           EXEC SQL                                                     01190000
                INCLUDE TMNMCLC                                         01200022
           END-EXEC.                                                    01210000
                                                                        01220000
      ******************************************************************01221021
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO *              01240000
      ******************************************************************01241021
           EXEC SQL                                                     01260000
               INCLUDE TDTATTR                                          01270000
           END-EXEC.                                                    01280000
                                                                        01290000
      ******************************************************************01291021
      * M/L DATE CONTROL INFO                            *              01310000
      ******************************************************************01321021
           EXEC SQL                                                     01330010
               INCLUDE TMLCNTL                                          01340010
           END-EXEC.                                                    01350010
                                                                        01360000
      ******************************************************************01370000
      *  DB2 TMNMCLC CURSOR                                             01380022
      ******************************************************************01390000
           EXEC SQL                                                     01400000
W31475         DECLARE TMNMCLC_CURSOR CURSOR                            01411028
W31475         WITH ROWSET POSITIONING FOR                              01412028
               SELECT DISTINCT DEPT_NBR                                 01420000
                              ,MAJ_CL_NBR                               01430000
                              ,CHNL_CDE                                 01440021
               FROM TMNMCLC                                             01450022
                   ,TMLCNTL                                             01460012
               WHERE (FSCL_MN_END_DTE BETWEEN                           01470022
MTH                                OPN_FSCL_MN_DTE                      01480022
MTH                            AND MXPSTG_FSCL_MN_DTE) OR               01490022
                    ((FSCL_MN_END_DTE BETWEEN                           01500022
                      :DTATTR-PFM-END-DTE AND                           01510008
                      MXPSTG_FSCL_MN_DTE) AND                           01521025
                       (END_INV_RTL_AMT  <> 0  OR                       01530025
                        END_INV_COST_AMT <> 0))                         01540025
               ORDER BY DEPT_NBR, MAJ_CL_NBR, CHNL_CDE                  01550021
           END-EXEC.                                                    01560000
                                                                        01570000
       EJECT                                                            01580000
                                                                        01590000
      ******************************************************************01600000
      ******************************************************************01601021
       PROCEDURE DIVISION.                                              01610000
      ******************************************************************01620000
                                                                        01630000
      ******************************************************************01640000
       A000-MAIN-MODULE.                                                01650000
      ******************************************************************01660000
      *                                                                 01670000
           PERFORM B000-INITIALIZE.                                     01680000
                                                                        01690000
           PERFORM B100-PROCESS-DATA.                                   01700000
                                                                        01710000
           PERFORM B900-EOJ-ROUTINE.                                    01720000
                                                                        01730000
           STOP RUN.                                                    01740000
                                                                        01750000
       EJECT                                                            01760000
      ******************************************************************01780000
       B000-INITIALIZE.                                                 01790000
      ******************************************************************01800000
      *                                                                 01810000
           OPEN OUTPUT MNMCLC-EXTR-FILE.                                01820022
           PERFORM 7100-SELECT-DB2-DATES.                               01830000
                                                                        01840000
       EJECT                                                            01860000
      ******************************************************************01880000
       B100-PROCESS-DATA.                                               01890000
      ******************************************************************01900000
      *                                                                 01910000
           PERFORM Z210-OPEN-CURSOR-TMNMCLC.                            01920022
                                                                        01930000
           PERFORM Z220-FETCH-TMNMCLC.                                  01940022
                                                                        01950000
           PERFORM                                                      01960000
             UNTIL PS-TMNMCLC-IS-DONE                                   01970022
W31475         PERFORM VARYING ROW-ID FROM 1 BY 1                       01972028
  |            UNTIL ROW-ID > SQLERRD (3)                               01973028
  |                 PERFORM C100-WRITE-EXTRACT                          01974028
  |            END-PERFORM                                              01975030
W31475*        PERFORM C100-WRITE-EXTRACT                               01976030
               PERFORM Z220-FETCH-TMNMCLC                               01990022
           END-PERFORM.                                                 02000000
                                                                        02010000
           PERFORM Z230-CLOSE-CURSOR-TMNMCLC.                           02020022
                                                                        02030000
       EJECT                                                            02040000
      ******************************************************************02060000
       C100-WRITE-EXTRACT.                                              02070000
      ******************************************************************02080000
      *                                                                 02090000
W31475*    MOVE MNMCLC-DEPT-NBR        TO EXTR-DEPT-NBR.                02100030
  |   *    MOVE MNMCLC-MAJ-CL-NBR      TO EXTR-MAJ-CL-NBR.              02110030
  |   *    MOVE MNMCLC-CHNL-CDE        TO EXTR-CHNL-CDE.                02120030
  |        MOVE HV-MNMCLC-DEPT-NBR(ROW-ID)                              02122030
  |                                    TO EXTR-DEPT-NBR.                02123028
  |        MOVE HV-MNMCLC-MAJ-CL-NBR(ROW-ID)                            02124028
  |                                    TO EXTR-MAJ-CL-NBR.              02125028
  |        MOVE HV-MNMCLC-CHNL-CDE(ROW-ID)                              02126028
W31475                                 TO EXTR-CHNL-CDE.                02127028
                                                                        02130017
           WRITE MNMCLC-EXTR-REC       FROM EXTR-RECORD.                02140022
                                                                        02150021
           ADD 1                       TO PA-OUTPUT-RECS-WRITTEN.       02160021
                                                                        02170000
       EJECT                                                            02180000
      ******************************************************************02200000
       B900-EOJ-ROUTINE.                                                02210000
      ******************************************************************02220000
      *                                                                 02230000
           DISPLAY ' '.                                                 02240000
           DISPLAY '  PROGRAM MLKUT474 PROCESSING SUMMARY'.             02250022
           DISPLAY '  -----------------------------------'.             02260000
           DISPLAY ' '.                                                 02270000
      *                                                                 02280000
           MOVE PA-TMNMCLC-ROWS-FETCHED TO CD-COUNT-HOLD1.              02290022
           DISPLAY 'TMNMCLC ROWS FETCHED =     ' CD-COUNT-HOLD1.        02300022
      *                                                                 02310000
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               02320000
           DISPLAY 'EXTRACT RECORDS WRITTEN = ' CD-COUNT-HOLD1.         02330000
      *                                                                 02340000
           DISPLAY ' '.                                                 02350000
                                                                        02360000
           CLOSE MNMCLC-EXTR-FILE.                                      02370022
                                                                        02380000
       EJECT                                                            02390000
      ******************************************************************02420000
       Z210-OPEN-CURSOR-TMNMCLC.                                        02430022
      ******************************************************************02440000
      *                                                                 02450000
           EXEC SQL                                                     02460000
               OPEN TMNMCLC_CURSOR                                      02470022
           END-EXEC.                                                    02480000
                                                                        02490000
           EVALUATE TRUE                                                02500000
               WHEN SQLCODE < 0                                         02510000
                   MOVE 'Z210-OPEN-CURSOR-TMNMCLC'                      02520022
                                       TO AA-PARAGRAPH-NAME             02530000
                   MOVE 'TMNMCLC TABLE ERROR'                           02540022
                                       TO AA-MESSAGE                    02550000
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02560000
                   PERFORM Z998-SQL-ERROR                               02570000
               WHEN SQLCODE > 0                                         02580000
               WHEN SQLWARN0 NOT = SPACES                               02590000
                   MOVE 'Z210-OPEN-CURSOR-TMNMCLC'                      02600022
                                       TO AA-PARAGRAPH-NAME             02610000
                   MOVE 'TMNMCLC TABLE WARNING'                         02620022
                                       TO AA-MESSAGE                    02630000
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02640000
                   PERFORM Z998-SQL-ERROR                               02650000
           END-EVALUATE.                                                02660000
                                                                        02670000
       EJECT                                                            02680000
                                                                        02690000
      ******************************************************************02700000
       Z220-FETCH-TMNMCLC.                                              02710022
      ******************************************************************02720000
W31475*                                                                 02730028
  |   *    EXEC SQL                                                     02740028
  |   *        FETCH TMNMCLC_CURSOR                                     02750028
  |   *         INTO :MNMCLC-DEPT-NBR                                   02760028
  |   *             ,:MNMCLC-MAJ-CL-NBR                                 02770028
  |   *             ,:MNMCLC-CHNL-CDE                                   02780028
  |   *    END-EXEC.                                                    02790028
  |        INITIALIZE HOST-VARIABLES.                                   02791028
  |                                                                     02792028
  |        EXEC SQL                                                     02793028
  |            FETCH NEXT ROWSET FROM TMNMCLC_CURSOR FOR 100 ROWS       02794028
  |             INTO  :HV-MNMCLC-DEPT-NBR                               02795028
  |                  ,:HV-MNMCLC-MAJ-CL-NBR                             02796028
  |                  ,:HV-MNMCLC-CHNL-CDE                               02797028
W31475     END-EXEC.                                                    02798028
                                                                        02800000
           EVALUATE TRUE                                                02810000
               WHEN SQLCODE = 0                                         02820000
W31475*            ADD +1              TO PA-TMNMCLC-ROWS-FETCHED       02830030
W31475             ADD +100            TO PA-TMNMCLC-ROWS-FETCHED       02831030
               WHEN SQLCODE = +100                                      02840000
                   MOVE 'Y'            TO PS-TMNMCLC-DONE               02850022
W31475             ADD SQLERRD (3)     TO PA-TMNMCLC-ROWS-FETCHED       02850128
  |                PERFORM VARYING ROW-ID FROM 1 BY 1                   02851028
  |                UNTIL ROW-ID > SQLERRD (3)                           02852028
  |                     PERFORM C100-WRITE-EXTRACT                      02853028
W31475             END-PERFORM                                          02854028
               WHEN SQLCODE < 0                                         02860000
                   MOVE 'Z220-FETCH-TMNMCLC'                            02870022
                                       TO AA-PARAGRAPH-NAME             02880000
                   MOVE 'TMNMCLC TABLE ERROR'                           02890022
                                       TO AA-MESSAGE                    02900000
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              02910000
                   PERFORM Z998-SQL-ERROR                               02920000
               WHEN ((SQLCODE > 0) AND (SQLCODE NOT = +100))            02930000
               WHEN SQLWARN0 NOT = SPACES                               02940000
                   MOVE 'Z220-FETCH-TMNMCLC'                            02950022
                                       TO AA-PARAGRAPH-NAME             02960000
                   MOVE 'TMNMCLC TABLE WARNING'                         02970022
                                       TO AA-MESSAGE                    02980000
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              02990000
                   PERFORM Z998-SQL-ERROR                               03000000
           END-EVALUATE.                                                03010000
                                                                        03020000
       EJECT                                                            03030000
      ******************************************************************03050000
       Z230-CLOSE-CURSOR-TMNMCLC.                                       03060022
      ******************************************************************03070000
      *                                                                 03080000
           EXEC SQL                                                     03090000
               CLOSE TMNMCLC_CURSOR                                     03100022
           END-EXEC.                                                    03110000
                                                                        03120000
           EVALUATE TRUE                                                03130000
               WHEN SQLCODE < 0                                         03140000
                   MOVE 'Z230-CLOSE-CURSOR-TMNMCLC'                     03150022
                                       TO AA-PARAGRAPH-NAME             03160000
                   MOVE 'TMNMCLC TABLE ERROR'                           03170022
                                       TO AA-MESSAGE                    03180000
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03190000
                   PERFORM Z998-SQL-ERROR                               03200000
               WHEN SQLCODE > 0                                         03210000
               WHEN SQLWARN0 NOT = SPACES                               03220000
                   MOVE 'Z230-CLOSE-CURSOR-TMNMCLC'                     03230022
                                       TO AA-PARAGRAPH-NAME             03240000
                   MOVE 'TMNMCLC TABLE WARNING'                         03250022
                                       TO AA-MESSAGE                    03260000
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03270000
                   PERFORM Z998-SQL-ERROR                               03280000
           END-EVALUATE.                                                03290000
                                                                        03300000
       EJECT                                                            03310000
                                                                        03320000
       7100-SELECT-DB2-DATES.                                           03330000
      *                                                                 03340000
           EXEC SQL                                                     03350000
               SELECT  PFM_END_DTE                                      03360000
                      ,OPN_FSCL_MN_DTE                                  03370022
                      ,OPN_FSCL_MN_DTE                                  03380021
                INTO  :DTATTR-PFM-END-DTE                               03390000
                     ,:MLCNTL-OPN-FSCL-MN-DTE                           03400022
                     ,:MLCNTL-OPN-FSCL-MN-DTE                           03410022
                FROM TDTATTR                                            03420000
                    ,TMLCNTL                                            03430013
                WHERE KY_DTE  = OPN_FSCL_MN_DTE                         03440022
           END-EXEC.                                                    03450000
      *                                                                 03460000
           EVALUATE SQLCODE                                             03470000
               WHEN 0                                                   03480000
                   DISPLAY 'DTATTR-PFM-END-DTE : ',                     03490013
                            DTATTR-PFM-END-DTE                          03500000
                   DISPLAY 'MLCNTL-OPN-FSCL-MN-DTE    : ',              03510022
                            MLCNTL-OPN-FSCL-MN-DTE                      03520022
                   DISPLAY 'MLCNTL-OPN-FSCL-MN-DTE    : ',              03530016
                            MLCNTL-OPN-FSCL-MN-DTE                      03540016
               WHEN OTHER                                               03570000
                  MOVE '7100-SELECT-DB2-DATES'                          03580000
                                       TO AA-PARAGRAPH-NAME             03590000
                   PERFORM Z998-SQL-ERROR                               03600000
           END-EVALUATE.                                                03610000
      *                                                                 03630000
      ******************************************************************03640000
       Z998-SQL-ERROR.                                                  03650000
      ******************************************************************03660000
                                                                        03680000
           CLOSE MNMCLC-EXTR-FILE.                                      03690022
                                                                        03700000
           DISPLAY AA-ABEND-LIT.                                        03710000
           DISPLAY AA-DB2-ERROR-LIT.                                    03720000
           DISPLAY AA-PROGRAM-LIT.                                      03730000
           DISPLAY AA-PARAGRAPH-LIT.                                    03740000
           DISPLAY AA-MESSAGE-LINE.                                     03750000
           DISPLAY AA-DB2-OPERATION-LIT.                                03760000
           SET AC-DB2-ERROR TO TRUE.                                    03770000
           MOVE +4013                  TO ABEND-CODE.                   03780000
                                                                        03790000
           COPY DPPD004.                                                03800000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03810000
                                                                        03820000
