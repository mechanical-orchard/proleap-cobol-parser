       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.      TCKUT073.                                       00030000
       AUTHOR.          DAN PAYNTER.                                    00040000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00050000
       DATE-WRITTEN.    05/09/2023.                                     00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *  MODULE TYPE:   COBOL2 BATCH.                                   00100000
      *                                                                 00110000
      * THIS PROGRAM PREPARES THE CONVERTED PLCC TRAILING TRANS FILE    00120000
      * PROCESSED BY TCKUP074 UNDER THE DB2 CHECKPOINT RESTART SYSTEM.  00130000
      *                                                                 00140000
      * THE PROGRAM READS THE TRAILING TRANS UPDATE FILE, REFORMATS     00150000
      * THE RECORDS AND SETS A CHECKPOINT INDICATOR ON THE RECORD BASED 00160000
      * ON HOW CHECKPOINTS WILL BE TAKEN BY THE MAINTENANCE PROGRAM.    00170000
      * AFTER A RECORD HAS BEEN REFORMATTED IT IS PASSED TO THE         00180000
      * TRANSACTION PREPARATION MODULE (DPKUT900) WHICH DOES SOME MORE  00190000
      * PROCESSING ON THE RECORDS AND WRITES THE REFORMATTED RECORDS    00200000
      * OUT TO A SEQUENTIAL FILE.                                       00210000
      *                                                                 00220000
      * THE PROGRAM COMMUNICATES WITH THE TRANSACTION PREPARATION       00230000
      * MODULE USING FUNCTION CODES (OPEN, WRITE, & CLOSE) AND CHECKS   00240000
      * THE RETURN CODE AFTER EACH REQUEST.  IF AN INVALID RETURN CODE  00250000
      * IS RECEIVED, ANY INFORMATION THAT MAY BE HELPFUL IN RESOLVING   00260000
      * THE BAD RETURN CODE IS DISPLAYED ON SYSOUT AND THE PROGRAM      00270000
      * ABENDS.                                                         00280000
      *                                                                 00290000
      *========================== CHANGE LOG ===========================00300000
      * CHANGE       BY           DATE    DESCRIPTION                   00310000
      *=================================================================00320000
      * CHG0288838  DAN PAYNTER 05-22-23  CREATE PROGRAM                00330000
      ******************************************************************00350000
                                                                        00360000
       ENVIRONMENT DIVISION.                                            00370000
       CONFIGURATION SECTION.                                           00380000
                                                                        00390000
       SOURCE-COMPUTER.    IBM-3090.                                    00400000
       OBJECT-COMPUTER.    IBM-3090.                                    00410000
                                                                        00420000
       INPUT-OUTPUT SECTION.                                            00430000
       FILE-CONTROL.                                                    00440000
                                                                        00450000
           SELECT UPDATE-FILE   ASSIGN TO INP01.                        00460000
           SELECT JOB-CARD-FILE ASSIGN TO INP02.                        00461000
                                                                        00470000
       DATA DIVISION.                                                   00480000
       FILE SECTION.                                                    00490000
                                                                        00500000
       FD  UPDATE-FILE                                                  00510000
           RECORDING MODE IS F                                          00520000
           BLOCK CONTAINS 0 RECORDS                                     00530000
           LABEL RECORDS ARE OMITTED.                                   00540000
                                                                        00550000
       01  UPDATE-REC                       PIC  X(250).                00560000
                                                                        00570000
       FD  JOB-CARD-FILE                                                00571000
           RECORDING MODE IS F                                          00572000
           BLOCK CONTAINS 0 RECORDS                                     00573000
           LABEL RECORDS ARE OMITTED.                                   00574000
                                                                        00575000
       01  CONTROL-CARD-REC                 PIC  X(80).                 00576000
                                                                        00577000
      ***************************************************************** 00580000
      *                       WORKING STORAGE                           00590000
      ***************************************************************** 00600000
       WORKING-STORAGE SECTION.                                         00610000
                                                                        00620000
       01  WS-START                         PIC  X(40)                  00630000
           VALUE 'TCKUT073 - WORKING STORAGE BEGINS HERE'.              00640000
                                                                        00650000
                                                                        00660000
      ***************************************************************** 00661000
      * RECORD LAYOUT FOR INPUT JOB CARD  (JOB NAME)                    00662000
      ***************************************************************** 00663000
       01  CC-CONTROL-CARD.                                             00664000
           05  CC-CONTROL-CARD-DISPLAY.                                 00665000
               10  CC-CONTROL-NAME     PIC X(11)    VALUE SPACE.        00666000
                   88  CC-VALID-CONTROL-NAME        VALUE               00667000
                       'JOB-NAME = '.                                   00668000
               10  CC-JOB-NAME         PIC X(6)     VALUE SPACE.        00669000
           05  FILLER                  PIC X(63)    VALUE SPACE.        00669100
                                                                        00669200
      ***************************************************************** 00670000
      *                         SWITCHES                                00680000
      ***************************************************************** 00690000
       01  PROGRAM-SWITCHES.                                            00700000
           05  PS-INPUT-UPDATE-EOF-SW       PIC  X(01) VALUE 'N'.       00710000
               88  PS-INPUT-UPDATE-EOF                 VALUE 'Y'.       00720000
           05  PS-FIRST-TRANS-READ-SW       PIC  X(01) VALUE 'Y'.       00730000
               88  FIRST-TRANS-READ                    VALUE 'Y'.       00740000
           05  PS-END-OF-JOB-FILE-SW        PIC  X(01) VALUE 'N'.       00741000
               88  PS-END-OF-JOB-FILE                  VALUE 'Y'.       00742000
                                                                        00750000
      ******************************************************************00760000
      *                       ACCUMULATORS                              00770000
      ******************************************************************00780000
       01  PROGRAM-ACCUMULATORS.                                        00790000
           05  PA-READ                      PIC  9(11) VALUE ZEROS.     00800000
           05  PA-WRITTEN                   PIC  9(11) VALUE ZEROS.     00810000
                                                                        00820000
      ******************************************************************00830000
      *                           CONSTANTS                             00840000
      ******************************************************************00850000
       01  PROGRAM-CONSTANTS.                                           00860000
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'TCKUT073'.00870000
                                                                        00880000
           05  PC-TRAN-PREP-FUNC-CODES.                                 00890000
               10  PC-TRAN-PREP-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   00900000
               10  PC-TRAN-PREP-FUNC-WRITE  PIC  X(05) VALUE 'WRITE'.   00910000
               10  PC-TRAN-PREP-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   00920000
                                                                        00930000
           05  PC-PLAN-NAME                 PIC  X(08) VALUE 'TCKUP074'.00950000
           05  PC-TRANS-RECORD-LENGTH       PIC S9(04) VALUE +250 COMP. 00960000
                                                                        00970000
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      00971000
                                                        COMP SYNC.      00972000
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    00973000
           88  AC-DB2-ERROR                             VALUE +4013.    00974000
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    00975000
                                                                        00976000
      ******************************************************************00980000
      *                          VARIABLES                              00990000
      ******************************************************************01000000
       01  PROGRAM-VARIABLES.                                           01010000
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.    01020000
           05  PV-OPERATION-MSG             PIC  X(60) VALUE SPACES.    01030000
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01031000
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01032000
           05  PV-RET-CODE                  PIC  9(04) VALUE ZEROS.     01040000
                                                                        01050000
           05  PV-CHECKPOINT-TYPE           PIC  X(01) VALUE SPACE.     01060000
               88  CONTROL-BREAK                       VALUE 'C'.       01070000
               88  LOGICAL-TRANSACTION                 VALUE 'L'.       01080000
               88  TIME-INTERVAL                       VALUE 'T'.       01090000
               88  REQUESTED-BY-PROGRAM                VALUE 'R'.       01100000
                                                                        01110000
           05  PV-HOLD-STORE-NBR            PIC  9(04) VALUE ZERO.      01120000
                                                                        01130000
      ******************************************************************01140000
      *               SYSTEM TIME AND DATE VARIABLES                    01150000
      ******************************************************************01160000
       01  SYS-DATE-TIME.                                               01170000
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    01180000
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    01190000
                                                                        01200000
       01  ST-BEG-TIME.                                                 01210000
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     01220000
           05  FILLER                       PIC  X(01) VALUE ':'.       01230000
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     01240000
                                                                        01250000
       01  ST-END-TIME.                                                 01260000
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     01270000
           05  FILLER                       PIC  X(01) VALUE ':'.       01280000
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     01290000
                                                                        01300000
       01  SD-EDIT-DATE.                                                01310000
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     01320000
           05  FILLER                       PIC  X(01) VALUE '-'.       01330000
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     01340000
           05  FILLER                       PIC  X(01) VALUE '-'.       01350000
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     01360000
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     01370000
                                                                        01380000
      ******************************************************************01390000
      *                        ERROR HANDLING                           01400000
      ******************************************************************01410000
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROS      01420000
                                                       COMP SYNC.       01430000
           88  ABEND-4019-CAL-SUB-ERROR          VALUE +4019.           01440000
                                                                        01450000
                                                                        01460000
      ***************************************************************** 01470000
      *  WS FIELDS FOR TEPITM AND TEPEID ASSOCIATE DISCOUNT UPDATES     01480000
      ***************************************************************** 01490000
           COPY TCRD072.                                                01500000
                                                                        01510000
                                                                        01520000
      ******************************************************************01530000
      *  TRANSACTION PREPARATION MODULE LINKAGE SECTION                 01540000
      ******************************************************************01550000
           COPY DPWS990.                                                01560000
                                                                        01570000
           COPY DPLS000.                                                01571000
                                                                        01572000
      ******************************************************************01580000
      *  SQL COMMUNICATIONS WORK AREA                                   01590000
      ******************************************************************01600000
           COPY SQLCA2.                                                 01610000
                                                                        01620000
                                                                        01630000
                                                                        01640000
       PROCEDURE DIVISION.                                              01650000
      ******************************************************************01660000
      * MAINLINE-PROCESSING                                             01670000
      ******************************************************************01680000
                                                                        01690000
           PERFORM A100-INITIALIZE.                                     01700000
                                                                        01710000
           PERFORM B100-PROCESS-UPDATE-INPUT-FILE                       01720000
             UNTIL PS-INPUT-UPDATE-EOF.                                 01730000
                                                                        01740000
           PERFORM Z990-END-OF-JOB-ROUTINE.                             01750000
                                                                        01760000
           GOBACK.                                                      01770000
                                                                        01780000
                                                                        01790000
      ******************************************************************01800000
      * PREFORMS TASK INITIATION                                        01810000
      ******************************************************************01820000
       A100-INITIALIZE.                                                 01830000
                                                                        01840000
           OPEN INPUT UPDATE-FILE.                                      01850000
                                                                        01860000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01870000
                                                                        01880000
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            01890000
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            01900000
                                                                        01910000
           MOVE SYS-DATE (1:2) TO SD-CENT.                              01920000
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              01930000
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             01940000
           MOVE SYS-DATE (7:2) TO SD-DAY.                               01950000
                                                                        01960000
           PERFORM A110-PROCESS-JOB-CARD.                               01970000
                                                                        01970100
           PERFORM C100-ISSUE-OPEN-REQUEST.                             01971000
                                                                        01980000
           PERFORM C200-READ-UPDATE-INPUT-FILE.                         01990000
                                                                        02000000
           IF PS-INPUT-UPDATE-EOF                                       02010000
              DISPLAY '**** EMPTY UPDATE FILE ****'                     02020000
           END-IF.                                                      02030000
                                                                        02040000
                                                                        02050000
      ******************************************************************02051000
      * A110-PROCESS-JOB-CARD                                           02052000
      ******************************************************************02053000
       A110-PROCESS-JOB-CARD.                                           02054000
                                                                        02055000
           OPEN INPUT JOB-CARD-FILE.                                    02055100
           READ JOB-CARD-FILE INTO CC-CONTROL-CARD                      02056000
               AT END                                                   02057000
                   SET PS-END-OF-JOB-FILE TO TRUE.                      02058000
           CLOSE JOB-CARD-FILE.                                         02059000
                                                                        02059100
           IF PS-END-OF-JOB-FILE                                        02059200
               MOVE 'A110-PROCESS-JOB-CARD '  TO PV-PARAGRAPH-NAME      02059300
               MOVE 'JOB CARD MISSING       ' TO PV-OPERATION-MSG-1     02059400
               SET AC-CONTROL-CARD-ERROR TO TRUE                        02059500
               PERFORM 9999-ABEND                                       02059600
           ELSE                                                         02059700
               DISPLAY 'CC USED = ',CC-CONTROL-CARD-DISPLAY             02059800
           END-IF.                                                      02059900
                                                                        02062000
      ******************************************************************02063000
      * PROCESS THE UPDATE FILE. IF A RECORD IS READ, GATHER WHATEVER   02070000
      * OTHER DATA IS NECESSARY TO TAKE CHECKPOINTS BASED ON THE TYPE OF02080000
      * CHECKPOINTS BEING TAKEN BY TCKUP074 AND ISSUE A WRITE REQUEST   02090000
      * TO THE TRANSACTION PREPARATION PROGRAM.                         02100000
      ******************************************************************02110000
       B100-PROCESS-UPDATE-INPUT-FILE.                                  02120000
                                                                        02130000
           IF FIRST-TRANS-READ                                          02140000
               MOVE TC072-EPTRN-STR-NBR TO PV-HOLD-STORE-NBR            02150000
               MOVE 'N'                 TO PS-FIRST-TRANS-READ-SW       02160000
           END-IF.                                                      02170000
                                                                        02180000
           IF CONTROL-BREAK                                             02190000
               PERFORM C300-CKPT-BY-CNTL-BREAK                          02200000
           ELSE                                                         02210000
               IF LOGICAL-TRANSACTION                                   02220000
                   PERFORM C400-CKPT-BY-LOGICAL-TRANS                   02230000
               ELSE                                                     02240000
                   PERFORM C500-CKPT-BY-TIME-OR-REQUEST                 02250000
               END-IF                                                   02260000
           END-IF.                                                      02270000
                                                                        02280000
           PERFORM C200-READ-UPDATE-INPUT-FILE.                         02290000
                                                                        02300000
                                                                        02310000
      ******************************************************************02320000
      * ISSUE AN 'OPEN' REQUEST TO THE TRANSACTION PREPARATION MODULE   02330000
      ******************************************************************02340000
       C100-ISSUE-OPEN-REQUEST.                                         02350000
                                                                        02360000
           MOVE PC-TRAN-PREP-FUNC-OPEN  TO DP000-FUNC-CODE.             02370000
           MOVE CC-JOB-NAME             TO DP000-JOB-NAME.              02380000
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.             02390000
                                                                        02400000
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         02410000
                                                                        02420000
           MOVE DP000-CKPT-TYPE-CODE    TO PV-CHECKPOINT-TYPE.          02430000
                                                                        02440000
                                                                        02450000
      ******************************************************************02460000
      * READ THE UPDATE INPUT FILE                                      02470000
      ******************************************************************02480000
       C200-READ-UPDATE-INPUT-FILE.                                     02490000
                                                                        02500000
           READ UPDATE-FILE INTO TC072-TRAN-UPDT-RECORD                 02510000
             AT END SET PS-INPUT-UPDATE-EOF TO TRUE.                    02520000
                                                                        02530000
           IF PS-INPUT-UPDATE-EOF                                       02540000
               CONTINUE                                                 02550000
           ELSE                                                         02560000
               ADD +1 TO PA-READ                                        02570000
           END-IF.                                                      02580000
                                                                        02590000
                                                                        02600000
      ******************************************************************02610000
      * THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSACATION  02620000
      * RECORD "ON" IF THERE IS A CHANGE IN THE STORE NUMBER. AFTER     02630000
      * THE TRANSACTION RECORD HAS BEEN FORMATTED, A WRITE REQUEST IS   02640000
      * ISSUED TO THE TRANSACTION PREPARATION MODULE.                   02650000
      *                                                                 02660000
      * NOTE: AT THIS TIME CONTROL/LOGIC BREAK PROCESSING IS IDENTICAL. 02670000
      ******************************************************************02680000
       C300-CKPT-BY-CNTL-BREAK.                                         02690000
                                                                        02700000
           IF  TC072-EPTRN-STR-NBR = PV-HOLD-STORE-NBR                  02710000
               MOVE 'N' TO DP000-CHECKPOINT-IND                         02720000
           ELSE                                                         02730000
               MOVE 'Y' TO DP000-CHECKPOINT-IND                         02740000
               MOVE TC072-EPTRN-STR-NBR TO PV-HOLD-STORE-NBR            02750000
           END-IF.                                                      02760000
                                                                        02770000
           MOVE PC-TRANS-RECORD-LENGTH  TO DP000-TRANS-REC-LENGTH.      02780000
           MOVE TC072-TRAN-UPDT-RECORD  TO DP000-TRANS-REC.             02790000
           MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.             02800000
           MOVE CC-JOB-NAME             TO DP000-JOB-NAME.              02810000
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.             02820000
                                                                        02830000
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         02840000
                                                                        02850000
           ADD +1 TO PA-WRITTEN.                                        02860000
                                                                        02870000
                                                                        02880000
      ******************************************************************02890000
      * THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSACTION   02900000
      * RECORD "ON".  THE UPDATE PROGRAM WILL CHECK THE CHECKPOINT      02910000
      * FREQUENCY TO KNOW WHETHER A CHECKPOINT SHOULD BE TAKEN. AFTER   02920000
      * THE TRANSACTION RECORD HAS BEEN FORMATTED, A WRITE REQUEST IS   02930000
      * ISSUED TO THE TRANSACTION PREPARATION MODULE.                   02940000
      ******************************************************************02950000
       C400-CKPT-BY-LOGICAL-TRANS.                                      02960000
                                                                        02970000
           IF  TC072-EPTRN-STR-NBR = PV-HOLD-STORE-NBR                  02980000
               MOVE 'N' TO DP000-CHECKPOINT-IND                         02990000
           ELSE                                                         03000000
               MOVE 'Y' TO DP000-CHECKPOINT-IND                         03010000
               MOVE TC072-EPTRN-STR-NBR TO PV-HOLD-STORE-NBR            03020000
           END-IF.                                                      03030000
                                                                        03040000
           MOVE PC-TRANS-RECORD-LENGTH  TO DP000-TRANS-REC-LENGTH.      03050000
           MOVE TC072-TRAN-UPDT-RECORD  TO DP000-TRANS-REC.             03060000
           MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.             03070000
           MOVE CC-JOB-NAME             TO DP000-JOB-NAME.              03080000
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.             03090000
                                                                        03100000
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03110000
                                                                        03120000
           ADD +1 TO PA-WRITTEN.                                        03130000
                                                                        03140000
                                                                        03150000
      ******************************************************************03160000
      * THIS ROUTINE BUILDS THE TRANSACTION RECORD AND ISSUES A WRITE   03170000
      * REQUEST TO THE TRANSACTION PREPARATION MODULE IF CHECKPOINTS ARE03180000
      * TAKEN BY TIME INTERVAL (SECONDS) OR REQUESTED BY THE MAINTENANCE03190000
      * PROGRAM.                                                        03200000
      ******************************************************************03210000
       C500-CKPT-BY-TIME-OR-REQUEST.                                    03220000
                                                                        03230000
           MOVE 'N'                     TO DP000-CHECKPOINT-IND.        03240000
                                                                        03250000
           MOVE PC-TRANS-RECORD-LENGTH  TO DP000-TRANS-REC-LENGTH.      03260000
           MOVE TC072-TRAN-UPDT-RECORD  TO DP000-TRANS-REC.             03270000
           MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.             03280000
           MOVE CC-JOB-NAME             TO DP000-JOB-NAME.              03290000
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.             03300000
                                                                        03310000
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03320000
                                                                        03330000
           ADD +1 TO PA-WRITTEN.                                        03340000
                                                                        03350000
                                                                        03360000
      ******************************************************************03370000
      * ISSUE A 'CLOSE' REQUEST TO THE TRANSACTION PREPARATION MODULE   03380000
      ******************************************************************03390000
       C600-ISSUE-CLOSE-REQUEST.                                        03400000
                                                                        03410000
           MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.             03420000
           MOVE CC-JOB-NAME             TO DP000-JOB-NAME.              03430000
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.             03440000
                                                                        03450000
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03460000
                                                                        03470000
                                                                        03480000
      ******************************************************************03490000
      * VERIFY THE RETURN CODE ON THE CALLED MODULE                     03500000
      ******************************************************************03510000
       Z100-CALL-TRANS-PREP-MODULE.                                     03520000
                                                                        03530000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         03540000
                                                                        03550000
           IF  DP000-GOOD-RET-CODE                                      03560000
               CONTINUE                                                 03570000
                                                                        03580000
           ELSE                                                         03590000
               MOVE 'Z100-CALL-TRANS-PREP-MODULE' TO PV-PARAGRAPH-NAME  03600000
               MOVE '** BAD CALL TO TRANS PREP MODULE **'               03610000
                                                  TO PV-OPERATION-MSG   03620000
               MOVE DP000-RET-CODE                TO PV-RET-CODE        03630000
                                                                        03640000
               SET ABEND-4019-CAL-SUB-ERROR TO TRUE                     03650000
                                                                        03660000
               PERFORM Z200-ABEND                                       03670000
           END-IF.                                                      03680000
                                                                        03690000
                                                                        03700000
      ******************************************************************03710000
      * NON-DB2 ABEND                                                   03720000
      ******************************************************************03730000
       Z200-ABEND.                                                      03740000
                                                                        03750000
           PERFORM Z999-CONTROLS.                                       03760000
                                                                        03770000
           DISPLAY '                     '.                             03780000
           DISPLAY '** ABEND           **'.                             03790000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          03800000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        03810000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         03820000
           DISPLAY '** RETURN CODE     ** = ' PV-RET-CODE.              03830000
                                                                        03840000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03850000
                                                                        03860000
                                                                        03870000
      ******************************************************************03880000
      * PERFORMS TASK TERMINATION PROCESSING                            03890000
      ******************************************************************03900000
       Z990-END-OF-JOB-ROUTINE.                                         03910000
                                                                        03920000
           CLOSE UPDATE-FILE.                                           03930000
                                                                        03940000
           PERFORM C600-ISSUE-CLOSE-REQUEST.                            03950000
                                                                        03960000
           PERFORM Z999-CONTROLS.                                       03970000
                                                                        03980000
                                                                        03990000
      ******************************************************************04000000
      * DISPLAY CONTROLS FROM THE PROGRAM                               04010000
      ******************************************************************04020000
       Z999-CONTROLS.                                                   04030000
                                                                        04040000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 04050000
                                                                        04060000
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            04070000
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            04080000
                                                                        04090000
           DISPLAY '                    '.                              04100000
           DISPLAY '**********************'.                            04110000
           DISPLAY '   TCKUT073 CONTROLS  '.                            04120000
           DISPLAY '**********************'.                            04130000
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         04140000
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          04150000
           DISPLAY 'END TIME  = ' ST-END-TIME.                          04160000
           DISPLAY '======================'.                            04170000
           DISPLAY 'RECORDS READ    = ' PA-READ.                        04180000
           DISPLAY '                  '.                                04190000
           DISPLAY 'RECORDS WRITTEN = ' PA-WRITTEN.                     04200000
           DISPLAY '                  '.                                04210000
                                                                        04220000
      ***************************************************************** 04221000
      * 9999-ABEND                                                      04222000
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  04223000
      ***************************************************************** 04224000
       9999-ABEND.                                                      04225000
           DISPLAY '** ABEND           **'.                             04226000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          04227000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        04228000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       04229000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       04229100
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         04229200
                                                                        04230000
      ******************************************************************04240000
      * TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES          04250000
      ******************************************************************04260000
           COPY DPPD000.                                                04270000
                                                                        04280000
                                                                        04290000
