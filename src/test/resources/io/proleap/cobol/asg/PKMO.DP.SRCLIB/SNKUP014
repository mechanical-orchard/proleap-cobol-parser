00001  ID DIVISION.                                                     06/29/93
00002                                                                   SNKUP014
00003  PROGRAM-ID.    SNKUP014.                                            LV003
00004                                                                   SNKUP014
00005  AUTHOR.        ANNE YEKO.                                        SNKUP014
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP014
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP014
00008  DATE-COMPILED.                                                   SNKUP014
00009                                                                   SNKUP014
00010 ******************************************************************SNKUP014
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP014
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP014
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASHANM TABLE.          **SNKUP014
00014 ******************************************************************SNKUP014
00015 * CHANGED __/__/__ BY ____________                              **SNKUP014
00016 *   P_________  ________________________________________________**SNKUP014
00017 *               ________________________________________________**SNKUP014
00018 *                                                               **SNKUP014
00019 ******************************************************************SNKUP014
00020                                                                   SNKUP014
00021      EJECT                                                        SNKUP014
00022                                                                   SNKUP014
00023  ENVIRONMENT DIVISION.                                            SNKUP014
00024                                                                   SNKUP014
00025  INPUT-OUTPUT SECTION.                                            SNKUP014
00026                                                                   SNKUP014
00027  FILE-CONTROL.                                                    SNKUP014
00028                                                                   SNKUP014
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP014
00030             ASSIGN TO INP01.                                      SNKUP014
00031                                                                   SNKUP014
00032  DATA DIVISION.                                                   SNKUP014
00033                                                                   SNKUP014
00034  FILE SECTION.                                                    SNKUP014
00035                                                                   SNKUP014
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP014
00037      RECORDING MODE IS F                                          SNKUP014
00038      LABEL RECORDS ARE STANDARD                                   SNKUP014
00039      RECORD CONTAINS 05 CHARACTERS                                   CL**3
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP014
00041                                                                   SNKUP014
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**3
00043                                                                   SNKUP014
00044      EJECT                                                        SNKUP014
00045  WORKING-STORAGE SECTION.                                         SNKUP014
00046                                                                   SNKUP014
00047  01  FILLER.                                                      SNKUP014
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP014
00049          ' ***** WORKING STORAGE PGM=SNKUP014 '.                  SNKUP014
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP014
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP014
00052          ' LOCATION='.                                            SNKUP014
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP014
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP014
00055          ' *****'.                                                SNKUP014
00056                                                                   SNKUP014
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP014
00058                                              COMP SYNC.           SNKUP014
00059                                                                   SNKUP014
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP014
00061                                                                   SNKUP014
00062      EJECT                                                        SNKUP014
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP014
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP014
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP014
00066                                                                   SNKUP014
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP014
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP014
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP014
00070                                                                   SNKUP014
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP014
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP014
00073                                              COMP-3.              SNKUP014
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP014
00075                                              COMP-3.              SNKUP014
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0             SNKUP014
00077                                              COMP-3.              SNKUP014
00078                                                                   SNKUP014
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP014
00080                                                                   SNKUP014
00081      COPY SNRD002I.                                               SNKUP014
00082      EJECT                                                        SNKUP014
00083 ******************************************************************SNKUP014
00084 *    DB2 DATABASE AREA                                            SNKUP014
00085 ******************************************************************SNKUP014
00086                                                                   SNKUP014
00087 *    ASN HEADER ADDRESS NAME TEXT                                 SNKUP014
00088                                                                   SNKUP014
00089      EXEC SQL                                                     SNKUP014
00090          INCLUDE TASHANM                                          SNKUP014
00091      END-EXEC.                                                    SNKUP014
00092                                                                   SNKUP014
00093      EJECT                                                        SNKUP014
00094 *    SQL COMMUNICATION AREA                                       SNKUP014
00095                                                                   SNKUP014
00096      EXEC SQL                                                     SNKUP014
00097          INCLUDE SQLCA                                            SNKUP014
00098      END-EXEC.                                                    SNKUP014
00099      EJECT                                                        SNKUP014
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP014
00101                                                                   SNKUP014
00102      COPY SQLCA2.                                                 SNKUP014
00103      EJECT                                                        SNKUP014
00104                                                                   SNKUP014
00105 ***************************************************************** SNKUP014
00106  PROCEDURE DIVISION.                                              SNKUP014
00107 ***************************************************************** SNKUP014
00108                                                                   SNKUP014
00109  A000-MAIN-DRIVER.                                                SNKUP014
00110                                                                   SNKUP014
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP014
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP014
00113                                                                   SNKUP014
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP014
00115                                                                   SNKUP014
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP014
00117                                                                   SNKUP014
00118      PERFORM B100-DELETE-ROWS                                     SNKUP014
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP014
00120                                                                   SNKUP014
00121      DISPLAY 'SNKUP014 RECORDS READ:    ' PV-RECS-READ.           SNKUP014
00122      DISPLAY 'SNKUP014 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP014
00123                                                                   SNKUP014
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP014
00125                                                                   SNKUP014
00126      GOBACK.                                                      SNKUP014
00127                                                                   SNKUP014
00128      EJECT                                                        SNKUP014
00129                                                                   SNKUP014
00130 ***************************************************************** SNKUP014
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP014
00132 ***************************************************************** SNKUP014
00133  B100-DELETE-ROWS.                                                SNKUP014
00134                                                                   SNKUP014
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP014
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP014
00137                                  TO ABEND-PARAGRAPH.              SNKUP014
00138                                                                   SNKUP014
00139      PERFORM B300-DELETE-ASHANM.                                  SNKUP014
00140      IF PV-DELETE-COUNTER > +500                                     CL**2
00141          PERFORM B400-COMMIT                                      SNKUP014
00142          MOVE ZERO               TO PV-DELETE-COUNTER             SNKUP014
00143      END-IF.                                                      SNKUP014
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP014
00145                                                                   SNKUP014
00146      EJECT                                                        SNKUP014
00147                                                                   SNKUP014
00148 ***************************************************************** SNKUP014
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP014
00150 ***************************************************************** SNKUP014
00151  B200-READ-INPUT-FILE.                                            SNKUP014
00152                                                                   SNKUP014
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP014
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP014
00155                                  TO ABEND-PARAGRAPH.              SNKUP014
00156                                                                   SNKUP014
00157      READ ASNHDR-DELETE-FILE                                      SNKUP014
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP014
00159          AT END                                                   SNKUP014
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP014
00161                                                                   SNKUP014
00162      IF NOT PS-END-OF-INPUT                                       SNKUP014
00163          ADD 1                   TO PV-RECS-READ                  SNKUP014
00164      END-IF.                                                      SNKUP014
00165                                                                   SNKUP014
00166      EJECT                                                        SNKUP014
00167                                                                   SNKUP014
00168 ***************************************************************** SNKUP014
00169 *    THIS PARAGRAPH DELETES ALL ASHANM ROWS THAT ARE EQUAL TO     SNKUP014
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP014
00171 ***************************************************************** SNKUP014
00172  B300-DELETE-ASHANM.                                              SNKUP014
00173                                                                   SNKUP014
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP014
00175      MOVE 'B300-DELETE-ASHANM        '                            SNKUP014
00176                                  TO ABEND-PARAGRAPH.              SNKUP014
00177                                                                   SNKUP014
00178      EXEC SQL                                                     SNKUP014
00179          DELETE                                                   SNKUP014
00180          FROM   TASHANM                                           SNKUP014
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**3
00182      END-EXEC.                                                    SNKUP014
00183                                                                   SNKUP014
00184      IF SQLCODE = ZERO                                            SNKUP014
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP014
00186          ADD +1                  TO PV-DELETE-COUNTER             SNKUP014
00187      ELSE                                                         SNKUP014
00188          IF SQLCODE = +100                                        SNKUP014
00189              CONTINUE                                             SNKUP014
00190          ELSE                                                     SNKUP014
00191              PERFORM Z998-SQL-ERROR                               SNKUP014
00192          END-IF                                                   SNKUP014
00193      END-IF.                                                      SNKUP014
00194                                                                   SNKUP014
00195      EJECT                                                        SNKUP014
00196                                                                   SNKUP014
00197 ***************************************************************** SNKUP014
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP014
00199 ***************************************************************** SNKUP014
00200  B400-COMMIT.                                                     SNKUP014
00201                                                                   SNKUP014
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP014
00203      MOVE 'B400-COMMIT               '                            SNKUP014
00204                                  TO ABEND-PARAGRAPH.              SNKUP014
00205                                                                   SNKUP014
00206      EXEC SQL                                                     SNKUP014
00207          COMMIT                                                   SNKUP014
00208      END-EXEC.                                                    SNKUP014
00209                                                                   SNKUP014
00210      IF SQLCODE = ZERO                                            SNKUP014
00211          CONTINUE                                                 SNKUP014
00212      ELSE                                                         SNKUP014
00213          PERFORM Z998-SQL-ERROR                                   SNKUP014
00214      END-IF.                                                      SNKUP014
00215                                                                   SNKUP014
00216      EJECT                                                        SNKUP014
00217                                                                   SNKUP014
00218 ***************************************************************** SNKUP014
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP014
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP014
00221 ***************************************************************** SNKUP014
00222  Z998-SQL-ERROR.                                                  SNKUP014
00223                                                                   SNKUP014
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP014
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP014
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP014
00227      DISPLAY '** PROGRAM          ** = SNKUP014'.                 SNKUP014
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP014
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP014
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP014
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP014
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP014
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP014
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP014
