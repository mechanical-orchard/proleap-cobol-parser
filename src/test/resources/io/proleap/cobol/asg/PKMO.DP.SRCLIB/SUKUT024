000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT024.                                                 
000600 AUTHOR.        DENISE HAHN                                               
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   JULY, 1999.                                               
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES AN EXTRACT FILE OF DATA FROM THE LABEL            
001300*  PRINT DATA FILE (TLBPTDA).                                             
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900                                                                          
003000*    SELECT TIMESTAMP-FILE                                                
003100*        ASSIGN TO IN01.                                                  
003200                                                                          
003300     SELECT LBL-PRT-DATA-EXTRACT                                          
003400         ASSIGN TO OUT02.                                                 
003410                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
004100*FD  TIMESTAMP-FILE                                                       
004200*    RECORDING MODE IS F                                                  
004300*    LABEL RECORDS ARE STANDARD                                           
004400*    BLOCK CONTAINS 0 RECORDS.                                            
004500                                                                          
004600*01  TIMESTAMP-RECORD           PIC  X(50).                               
004700                                                                          
004800 FD  LBL-PRT-DATA-EXTRACT                                                 
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005200                                                                          
005300 01  LBL-PRT-DATA-RECORD.                                                 
005400     05   LBP-TRIP-ID           PIC  S9(9) COMP.                          
005410     05   LBP-CRE-USR-ID        PIC   X(8).                               
005420     05   LBP-LBL-PRT-BTCH-ID   PIC  S9(4) COMP.                          
P48069*    05   LBP-CRE-TMST          PIC   X(26).                              
005470                                                                          
005500                                                                          
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
005800******************************************************************        
005900                                                                          
006700*-----------------------------------------------------------------        
006800*  RECORD LAYOUT FOR THE TIMESTAMP FILE.                                  
006810*-----------------------------------------------------------------        
007000     COPY RCRD011.                                                        
007100                                                                          
007200*-----------------------------------------------------------------        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007510*-----------------------------------------------------------------        
007700     COPY DPWS004.                                                        
007800                                                                          
007806*-----------------------------------------------------------------        
007807*  COMMUNICATIONS AREA FOR DB2                                            
007808*-----------------------------------------------------------------        
007809     EXEC SQL                                                             
007810          INCLUDE SQLCA                                                   
007811     END-EXEC.                                                            
007812                                                                          
007813*-----------------------------------------------------------------        
007820*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE LABEL PRINT DATA           
007830*  TABLE                                                                  
007840*-----------------------------------------------------------------        
007850     EXEC SQL                                                             
007860          INCLUDE TLBPTDA                                                 
007870     END-EXEC.                                                            
007880                                                                          
007881*-----------------------------------------------------------------        
007882*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE PARM TABLE                 
007884*-----------------------------------------------------------------        
007890     EXEC SQL                                                             
007900          INCLUDE TKRPRPM                                                 
008000     END-EXEC.                                                            
008100/                                                                         
010320*-----------------------------------------------------------------        
010330*                                                                         
010340*-----------------------------------------------------------------        
011200 01  ABEND-CODE                    PIC S9(04) VALUE +0                    
011300                                              COMP SYNC.                  
011400                                                                          
011500 01  PS-PROGRAM-SWITCHES.                                                 
011600     05  PS-EOF-LABEL-DATA-CSR-SW  PIC  X(01) VALUE 'N'.                  
011700         88  PS-EOF-LABEL-DATA-CSR            VALUE 'Y'.                  
011800         88  PS-MORE-LABEL-DATA-CSR           VALUE 'N'.                  
011900                                                                          
012000                                                                          
012100 01  PC-PROGRAM-CONSTANTS.                                                
012400     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08) VALUE 'SUKUT024'.           
013000     05  PC-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.               
013100                                                                          
013200 01  PV-PROGRAM-VARIABLES.                                                
013400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
013500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
014200     05  PV-DISPLAY-COUNT            PIC S9(09)    VALUE ZERO             
014300                                                   COMP SYNC.             
014400     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
014500                                                   COMP SYNC.             
015900                                                                          
015910*-----------------------------------------------------------------        
015920*  LABEL PRINT DATA TO BE DELETED CURSOR                                  
015930*-----------------------------------------------------------------        
016000     EXEC SQL                                                             
016100          DECLARE LABEL_DATA_CURSOR CURSOR FOR                            
016200           SELECT DISTINCT                                                
016201                  TRIP_ID,                                                
016202                  CRE_USR_ID,                                             
P48069                  LBL_PRT_BTCH_ID                                         
P48069*                 CRE_TMST                                                
017700             FROM TLBPTDA                                                 
018000            WHERE CRE_TMST  < :PC-CURRENT-TIMESTAMP                       
018200     END-EXEC.                                                            
018300                                                                          
018400                                                                          
018500******************************************************************        
018600 PROCEDURE DIVISION.                                                      
018700******************************************************************        
018800                                                                          
018801                                                                          
018810*-----------------------------------------------------------------        
018820* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
018830*      PROGRAM.                                                           
018840*-----------------------------------------------------------------        
018900 0000-PROGRAM-MAINLINE.                                                   
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019600     IF  PS-MORE-LABEL-DATA-CSR                                           
019700         PERFORM 2000-PROCESS-LABEL-DATA                                  
019800           UNTIL PS-EOF-LABEL-DATA-CSR                                    
019900     END-IF.                                                              
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020100                                                                          
020200     STOP RUN.                                                            
020300                                                                          
020310                                                                          
020320*-----------------------------------------------------------------        
020330*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020340*    -- OPEN OUTPUT FILE                                                  
020350*    -- OPEN CURSOR                                                       
020360*    -- FETCH CURSOR                                                      
020370*-----------------------------------------------------------------        
020400 1000-INITIALIZE.                                                         
021100                                                                          
021200*    OPEN INPUT  TIMESTAMP-FILE.                                          
021400     OPEN OUTPUT LBL-PRT-DATA-EXTRACT.                                    
021600                                                                          
021700*    PERFORM 1100-READ-CONTROL-CARD.                                      
021701     PERFORM 1200-SELECT-TKRPRPM.                                         
021710                                                                          
021800     PERFORM 7000-OPEN-LABEL-DATA-CURSOR.                                 
021900     PERFORM 7100-FETCH-LABEL-DATA-CURSOR.                                
022000                                                                          
022010                                                                          
022100*-----------------------------------------------------------------        
022200*                                                                         
022300*-----------------------------------------------------------------        
025400 1100-READ-CONTROL-CARD.                                                  
025401                                                                          
025402*    READ TIMESTAMP-FILE   INTO RC011-CONTROL-RECORD.                     
025403                                                                          
025404*    MOVE RC011-CONTROL-TMST TO PC-CURRENT-TIMESTAMP.                     
025405                                                                          
025406*    DISPLAY 'SUKUT024 CONTROL CARD -- ' RC011-CONTROL-TMST.              
025407                                                                          
025408                                                                          
025409*********************************************************************     
025410* SELECT PARM VALUE FROM TKRPRPM TO DETERMINE DATE TO USE TO CONTROL      
025411* THE DELETES FROM LABEL PRINT DATA TABLE.                                
025412*********************************************************************     
025413 1200-SELECT-TKRPRPM.                                                     
025414                                                                          
025415     INITIALIZE DCLTKRPRPM.                                               
025416                                                                          
025417     EXEC SQL                                                             
025418         SELECT  (CURRENT TIMESTAMP - FUNC_QTY DAYS)                      
025420           INTO  :PC-CURRENT-TIMESTAMP                                    
025422           FROM  TKRPRPM                                                  
025423          WHERE  LOC_ID            = 90                                   
025424            AND  INTFC_SYS_CDE     = 'KHL'                                
025425            AND  SYS_PRC_FUNC_CDE  = 'PTDADL'                             
025426            AND  FUNC_PRC_STAT_CDE = 'AC'                                 
025427     END-EXEC.                                                            
025428                                                                          
025429     EVALUATE TRUE                                                        
025430         WHEN SQLCODE = ZERO                                              
025431              CONTINUE                                                    
025433         WHEN SQLCODE = +100                                              
025434         WHEN SQLWARN0 NOT = SPACE                                        
025435         WHEN SQLCODE < ZERO                                              
025436         WHEN SQLCODE > ZERO                                              
025437             MOVE '1200-SELECT-TKRPRPM '                                  
025438                                 TO PV-PARAGRAPH-NAME                     
025439             MOVE 'SELECT PARM VALUE FOR DELETES  '                       
025440                                 TO PV-DB2-OPERATION-ATTEMPTED            
025441             PERFORM 9100-SQL-ABEND                                       
025446     END-EVALUATE.                                                        
025447                                                                          
025448/                                                                         
025449*-----------------------------------------------------------------        
025450*  LOOP THROUGH THE RESULTS TABLE.                                        
025451*  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT MEET CRITERIA.       
025452*-----------------------------------------------------------------        
025460 2000-PROCESS-LABEL-DATA.                                                 
026000                                                                          
026100     PERFORM 8000-WRITE-LABEL-DATA-RECORD.                                
026101                                                                          
026200     PERFORM 7100-FETCH-LABEL-DATA-CURSOR.                                
026300                                                                          
026310                                                                          
026320*-----------------------------------------------------------------        
026330*  OPEN THE CURSOR.                                                       
026340*-----------------------------------------------------------------        
026400 7000-OPEN-LABEL-DATA-CURSOR.                                             
027300                                                                          
027400     EXEC SQL                                                             
027500          OPEN LABEL_DATA_CURSOR                                          
027600     END-EXEC                                                             
027700                                                                          
027800     EVALUATE TRUE                                                        
027810         WHEN SQLCODE = ZERO                                              
027820              CONTINUE                                                    
027900         WHEN SQLCODE = -904                                              
028000         WHEN SQLCODE = -911                                              
028100              CONTINUE                                                    
028200         WHEN SQLWARN0 NOT = SPACES                                       
028300         WHEN SQLCODE < ZERO                                              
028400         WHEN SQLCODE > ZERO                                              
028500             MOVE '7000-OPEN-LABEL-DATA-CURSOR    '                       
028600                                 TO PV-PARAGRAPH-NAME                     
028700             MOVE 'OPEN THE LABEL-DATA-CSR        '                       
028800                                 TO PV-DB2-OPERATION-ATTEMPTED            
028900             PERFORM 9100-SQL-ABEND                                       
029200     END-EVALUATE.                                                        
029400                                                                          
030200                                                                          
030210*-----------------------------------------------------------------        
030220*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
030230*-----------------------------------------------------------------        
030300 7100-FETCH-LABEL-DATA-CURSOR.                                            
030500                                                                          
030700     EXEC SQL                                                             
030800          FETCH LABEL_DATA_CURSOR                                         
031000           INTO :LBPTDA-TRIP-ID,                                          
031100                :LBPTDA-CRE-USR-ID,                                       
P48069                :LBPTDA-LBL-PRT-BTCH-ID                                   
P48069*               :LBPTDA-CRE-TMST                                          
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
032700             CONTINUE                                                     
032800         WHEN SQLCODE = +100                                              
032900             SET PS-EOF-LABEL-DATA-CSR  TO  TRUE                          
033000                                                                          
033100         WHEN SQLWARN0 NOT = SPACES                                       
033200         WHEN SQLCODE < ZERO                                              
033300         WHEN SQLCODE > ZERO                                              
033400             MOVE '7100-FETCH-LABEL-DATA-CURSOR  '                        
033500                                 TO PV-PARAGRAPH-NAME                     
033600             MOVE 'FETCH THE SHIPMT-UNIT-CURSOR   '                       
033700                                 TO PV-DB2-OPERATION-ATTEMPTED            
033800             PERFORM 9100-SQL-ABEND                                       
033900     END-EVALUATE.                                                        
034000                                                                          
034120                                                                          
034121*-----------------------------------------------------------------        
034122*  WRITE THE SHIPMENT-UNIT-RECORD                                         
034123*-----------------------------------------------------------------        
034130 8000-WRITE-LABEL-DATA-RECORD.                                            
034140                                                                          
034150     INITIALIZE LBL-PRT-DATA-RECORD.                                      
034160     MOVE LBPTDA-TRIP-ID          TO LBP-TRIP-ID.                         
034170     MOVE LBPTDA-CRE-USR-ID       TO LBP-CRE-USR-ID.                      
034180     MOVE LBPTDA-LBL-PRT-BTCH-ID  TO LBP-LBL-PRT-BTCH-ID.                 
P48069*    MOVE LBPTDA-CRE-TMST         TO LBP-CRE-TMST.                        
034200                                                                          
034600     WRITE LBL-PRT-DATA-RECORD.                                           
034800     ADD +1 TO PV-WRITE-COUNTER.                                          
035690                                                                          
035700                                                                          
035710*-----------------------------------------------------------------        
035711*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
035720*-----------------------------------------------------------------        
035800 9000-EOJ-ROUTINE.                                                        
036200     CLOSE LBL-PRT-DATA-EXTRACT.                                          
036310*          TIMESTAMP-FILE.                                                
036400                                                                          
036500     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                           
036600     DISPLAY 'TOTAL LABEL BATCH RECORDS WRITTEN ' PV-DISPLAY-COUNT.       
036700                                                                          
037400     EXEC SQL                                                             
037500          CLOSE LABEL_DATA_CURSOR                                         
037600     END-EXEC.                                                            
037700                                                                          
037800     EVALUATE TRUE                                                        
037810         WHEN SQLCODE = ZERO                                              
037820              CONTINUE                                                    
037900         WHEN SQLCODE = -904                                              
038000         WHEN SQLCODE = -911                                              
038100              CONTINUE                                                    
038200         WHEN SQLWARN0 NOT = SPACES                                       
038300         WHEN SQLCODE < ZERO                                              
038400         WHEN SQLCODE > ZERO                                              
038500              MOVE '9000-EOJ-ROUTINE               '                      
038600                                 TO PV-PARAGRAPH-NAME                     
038700              MOVE 'CLOSE THE LABEL_DATA_CURSOR     '                     
038800                                 TO PV-DB2-OPERATION-ATTEMPTED            
038900              PERFORM 9100-SQL-ABEND                                      
039200     END-EVALUATE.                                                        
039400                                                                          
040200                                                                          
040210*-----------------------------------------------------------------        
040220*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040230*  ERROR OR WARNING CODE OCCURS.                                          
040240*-----------------------------------------------------------------        
040300 9100-SQL-ABEND.                                                          
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
042310/                                                                         
