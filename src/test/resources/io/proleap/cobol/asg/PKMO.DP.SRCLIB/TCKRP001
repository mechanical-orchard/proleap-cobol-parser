      ***************************************************************** 00010004
       IDENTIFICATION DIVISION.                                         00020004
      ***************************************************************** 00030004
                                                                        00040004
       PROGRAM-ID.             TCKRP001.                                00050004
       AUTHOR.                 CHRISTOPHER CLARK.                       00060004
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070004
       DATE-WRITTEN            FEBRUARY 2002.                           00080004
       DATE-COMPILED.                                                   00090004
                                                                        00100004
      ***************************************************************** 00110004
      *                                                               * 00120004
      *  THIS PROGRAM CREATES THE DAILY DEBIT CARD AUDIT REPORT.      * 00130004
      *                                                               * 00140004
      *   INPUT: INP01 - CONTAINS POS DATE, WHICH IS USED AS THE      * 00150004
      *                  PROCESS DATE TO CREATE THE REPORT.           * 00160004
      *          INP02 - CONTAINS DEBIT CARD TRANSACTIONS FOR THE PAST* 00170004
      *                  TWO DAYS SORTED BY STORE NUMBER, DATE, TIME, * 00180004
      *                  REGISTER, AND TRANSACTION NUMBER.            * 00190004
      *  OUTPUT: RPT01 - REPORT SHOWING ALL DEBIT CARDS PROCESS FOR   * 00200004
      *                  THE PROCESSING DAY BY STORE.                 * 00210004
      *                                                               * 00220004
      ***************************************************************** 00230004
      *                                                               * 00240004
      *  THE PROGRAM READS A CONTROL CARD FILE TO GET THE POS DATE.   * 00250004
      *                                                               * 00260004
      *  WRITE EACH TRANSACTIONS TO THE DEBIT REPORT.                 * 00270004
      *                                                               * 00280004
      ***************************************************************** 00290004
      *  PLEASE SEE PROGRAM SPECS FOR A DESCRIPTION OF ALL UPDATES.   * 00300004
C24628***************************************************************** 00310004
C24628*  10/19/2015  CHG0124628 - COGNIZANT                             00320004
C24628*  MODIFIED PROGRAM TO ADD UNPROCESSED DESCRIPTION OR CODE TO   * 00330004
C24628*               TCK08410 DEBIT REPORT                           * 00340004
      ***************************************************************** 00350004
       ENVIRONMENT DIVISION.                                            00360004
      ***************************************************************** 00370004
                                                                        00380004
       CONFIGURATION SECTION.                                           00390004
                                                                        00400004
       SOURCE-COMPUTER.        IBM-3090.                                00410004
                                                                        00420004
       OBJECT-COMPUTER.        IBM-3090.                                00430004
                                                                        00440004
       INPUT-OUTPUT SECTION.                                            00450004
                                                                        00460004
       FILE-CONTROL.                                                    00470004
                                                                        00480004
           SELECT POS-DATE-CARD-FILE                                    00490004
               ASSIGN TO INP01.                                         00500004
                                                                        00510004
           SELECT DBT-CRD-INPUT-FILE                                    00520004
               ASSIGN TO INP02.                                         00530004
                                                                        00540004
           SELECT DBT-CRD-AUDIT-REPORT-FILE                             00550004
               ASSIGN TO RPT01.                                         00560004
                                                                        00570004
      ******************************************************************00580004
       DATA DIVISION.                                                   00590004
      ******************************************************************00600004
                                                                        00610004
       FILE SECTION.                                                    00620004
                                                                        00630004
       FD  POS-DATE-CARD-FILE                                           00640004
           RECORDING MODE IS F                                          00650004
           BLOCK CONTAINS 0 RECORDS                                     00660004
           LABEL RECORDS ARE STANDARD.                                  00670004
                                                                        00680004
       01  POS-DATE-CARD-RECORD            PIC X(80).                   00690004
                                                                        00700004
       FD  DBT-CRD-INPUT-FILE                                           00710004
           RECORDING MODE IS F                                          00720004
           BLOCK CONTAINS 0 RECORDS                                     00730004
           LABEL RECORDS ARE STANDARD.                                  00740004
                                                                        00750004
C24628 01  DBT-CRD-INPUT-RECORD            PIC X(72).                   00760004
                                                                        00770004
       FD  DBT-CRD-AUDIT-REPORT-FILE                                    00780004
           RECORDING MODE IS F                                          00790004
           BLOCK CONTAINS 0  RECORDS.                                   00800004
                                                                        00810004
       01  DBT-CRD-AUDIT-REPORT-RECORD     PIC  X(132).                 00820004
                                                                        00830004
      ******************************************************************00840004
       WORKING-STORAGE SECTION.                                         00850004
      ******************************************************************00860004
                                                                        00870004
      ******************************************************************00880004
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                   *00890004
      ******************************************************************00900004
       01  CC-CONTROL-CARD.                                             00910004
           05  CC-CONTROL-CARD-DISPLAY.                                 00920004
               10  CC-CONTROL-NAME     PIC X(14)    VALUE SPACE.        00930004
                   88  CC-VALID-CONTROL-NAME        VALUE               00940004
                       'P.O.S. DATE = '.                                00950004
               10  CC-POS-DATE-MMDDYY  PIC 9(6)     VALUE ZERO.         00960004
               10  FILLER REDEFINES CC-POS-DATE-MMDDYY.                 00970004
                   15  CC-POS-DATE-MM      PIC X(02).                   00980004
                   15  CC-POS-DATE-DD      PIC X(02).                   00990004
                   15  CC-POS-DATE-YY      PIC X(02).                   01000004
           05  FILLER                  PIC X(60)    VALUE SPACE.        01010004
                                                                        01020004
       01 CC-RFRM-POS-DATE.                                             01030004
          05   CC-RFRM-POS-DATE-CCYYMMDD   PIC 9(08)    VALUE ZERO.     01040004
          05   FILLER REDEFINES CC-RFRM-POS-DATE-CCYYMMDD.              01050004
               10  CC-RFRM-POS-DATE-CC     PIC X(02).                   01060004
               10  CC-RFRM-POS-DATE-YY     PIC X(02).                   01070004
               10  CC-RFRM-POS-DATE-MM     PIC X(02).                   01080004
               10  CC-RFRM-POS-DATE-DD     PIC X(02).                   01090004
      ******************************************************************01100004
      * DATE AND TIME FORMAT FOR RECORDS                               *01110004
      ******************************************************************01120004
       01  PV-DATE-FORMAT.                                              01130004
           05  PV-DATE-CCYYMMDD            PIC X(08)     VALUE SPACE.   01140004
           05  FILLER REDEFINES PV-DATE-CCYYMMDD.                       01150004
               10  PV-DATE-CCYY            PIC X(04).                   01160004
               10  PV-DATE-MM              PIC X(02).                   01170004
               10  PV-DATE-DD              PIC X(02).                   01180004
       01  PV-TIME-FORMAT.                                              01190004
           05  PV-TIME-HHMMSS              PIC X(06)     VALUE SPACE.   01200004
           05  FILLER REDEFINES PV-TIME-HHMMSS.                         01210004
               10  PV-TIME-HH              PIC X(02).                   01220004
               10  PV-TIME-MM              PIC X(02).                   01230004
               10  PV-TIME-SS              PIC X(02).                   01240004
           05  PV-HOLD-CCYY.                                            01250004
               10  PV-HOLD-CC              PIC 9(02)     VALUE ZEROES.  01260004
               10  PV-HOLD-YY              PIC 9(02)     VALUE ZEROES.  01270004
                                                                        01280004
      ******************************************************************01290004
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)            *01300004
      ******************************************************************01310004
           COPY DPWS500.                                                01320004
                                                                        01330004
      ******************************************************************01340004
      *   ABEND CODE - AND NUMBER FOR EACH POSSIBLE TYPE OF ABEND.     *01350004
      ******************************************************************01360004
       01  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO        01370004
                                                      COMP SYNC.        01380004
           88  AC-CONTROL-CARD-ERROR                  VALUE +4003.      01390004
           88  AC-CALENDAR-ROUTINE-ERROR              VALUE +4019.      01400004
                                                                        01410004
       01  SUBSCRIPTS.                                                  01420004
           05  SUB                         PIC S9(04) VALUE ZERO        01430004
                                                       COMP.            01440004
                                                                        01450004
      ******************************************************************01460004
      *   PROGRAM SWITCHES                                             *01470004
      ******************************************************************01480004
       01  PS-PROGRAM-SWITCHES.                                         01490004
           05  PS-END-OF-CARD-FILE-SW        PIC X(01)  VALUE 'N'.      01500004
               88  PS-END-OF-CARD-FILE                  VALUE 'Y'.      01510004
           05  PS-END-OF-INPUT-FILE-SW       PIC X(01)  VALUE 'N'.      01520004
               88  PS-END-OF-INPUT-FILE                 VALUE 'Y'.      01530004
           05  PS-FIRST-RECORD               PIC X(01)  VALUE 'Y'.      01540004
               88  PS-FIRST-RECORD-NO                   VALUE 'N'.      01550004
               88  PS-FIRST-RECORD-YES                  VALUE 'Y'.      01560004
           05  PS-FOUND-DATE-SW              PIC X(01)  VALUE 'Y'.      01570004
               88  PS-NOT-FOUND-DATE                    VALUE 'N'.      01580004
               88  PS-FOUND-DATE                        VALUE 'Y'.      01590004
                                                                        01600004
      ******************************************************************01610004
      *   PROGRAM CONSTANTS                                            *01620004
      ******************************************************************01630004
       01  PC-CONSTANTS.                                                01640004
           05  PC-CENTURY                  PIC X(02)     VALUE '20'.    01650004
           05  PC-LINE-LIMIT               PIC S9(04)    VALUE +60.     01660004
           05  PC-LINE-SMALL-LIMIT         PIC S9(04)    VALUE +55.     01670004
           05  PC-CURRENT-PROGRAM-NAME     PIC X(08)     VALUE          01680004
                                                         'TCKRP001'.    01690004
           05  PC-REPORT-NBR-1             PIC 9(01)     VALUE 1.       01700004
           05  PC-EXCEPT-DAYS-NUMBER       PIC 9(02)     VALUE 30.      01710004
                                                                        01720004
      ******************************************************************01730004
      *   PROGRAM ACCUMULATERS.                                        *01740004
      ******************************************************************01750004
       01  PA-ACCUMULATORS                 COMP-3.                      01760004
           05  PA-PAGE-CNT                 PIC S9(05)    VALUE ZERO.    01770004
           05  PA-LINE-CNT                 PIC S9(03)    VALUE ZERO.    01780004
                                                                        01790004
      ******************************************************************01800004
      *   PROGRAM VARIABLES.                                           *01810004
      ******************************************************************01820004
       01  PV-PROGRAM-VARIABLES.                                        01830004
           05  PV-PRINT-RECORD             PIC X(200)    VALUE SPACE.   01840004
           05  PV-BLANK-RECORD             PIC X(200)    VALUE SPACE.   01850004
           05  PV-LINE-SPACING             PIC S9(04)    VALUE 1        01860004
                                                         COMP SYNC.     01870004
C24628     05  PV-REASON-CDE               PIC  X(03)    VALUE SPACE.   01880004
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01890004
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01900004
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01910004
           05  PV-SAVE-TRAN-DATE           PIC  X(08)    VALUE SPACES.  01920004
           05  PV-DAY1-TRAN-DATE           PIC  X(08)    VALUE SPACES.  01930004
           05  PV-DAY2-TRAN-DATE           PIC  X(08)    VALUE SPACES.  01940004
           05  PV-SAVE-STR-NBR             PIC  S9(4)    COMP           01950004
                                                         VALUE ZERO.    01960004
           05  PV-STORE-GRAND-PROC-TOT     PIC S9(09)V99 VALUE ZERO.    01970004
           05  PV-STORE-GRAND-UNPROC-TOT   PIC S9(09)V99 VALUE ZERO.    01980004
           05  PV-STORE-GRAND-TOTAL-TOT    PIC S9(09)V99 VALUE ZERO.    01990004
       01  PV-STORE-DAY-TOTAL.                                          02000004
           05  PV-STORE-DAY-TOTALS    OCCURS 30 TIMES                   02010004
                                      INDEXED BY PV-IDX.                02020004
               10  PV-DATE                 PIC X(08).                   02030004
               10  PV-STORE-DAY-PROC-TOT   PIC S9(09)V99.               02040004
               10  PV-STORE-DAY-UNPROC-TOT PIC S9(09)V99.               02050004
               10  PV-STORE-DAY-TOTAL-TOT  PIC S9(09)V99.               02060004
                                                                        02070004
       01  PV-CORP-DAY-TOTAL.                                           02080004
           05  PV-CORP-DAY-TOTALS     OCCURS 30 TIMES                   02090004
                                      INDEXED BY PV-CORP-IDX.           02100004
               10  PV-CORP-DATE            PIC X(08).                   02110004
               10  PV-CORP-DAY-PROC-TOT    PIC S9(09)V99.               02120004
               10  PV-CORP-DAY-UNPROC-TOT  PIC S9(09)V99.               02130004
               10  PV-CORP-DAY-TOTAL-TOT   PIC S9(09)V99.               02140004
                                                                        02150004
      ******************************************************************02160004
      *   DATE AND TIME FIELDS.                                        *02170004
      ******************************************************************02180004
       01  PV-DATE-AND-TIME-FIELDS.                                     02190004
           05  PV-POS-CCYY-MM-DD           PIC X(10)     VALUE SPACE.   02200004
           05  FILLER REDEFINES PV-POS-CCYY-MM-DD.                      02210004
               10  PV-POS-CCYY             PIC X(04).                   02220004
               10  FILLER                  PIC X(01).                   02230004
               10  PV-POS-MM               PIC X(02).                   02240004
               10  FILLER                  PIC X(01).                   02250004
               10  PV-POS-DD               PIC X(02).                   02260004
           05  PV-CURR-DATE-CCYYMMDD.                                   02270004
               10  PV-CURR-DATE-CCYY       PIC  9(04).                  02280004
               10  PV-CURR-DATE-MM         PIC  9(02).                  02290004
               10  PV-CURR-DATE-DD         PIC  9(02).                  02300004
           05  PV-CURR-TIME.                                            02310004
               10  PV-CURR-TIME-HH         PIC  9(02)    VALUE ZERO.    02320004
               10  PV-CURR-TIME-MM         PIC  9(02)    VALUE ZERO.    02330004
               10  PV-CURR-TIME-SSHH       PIC  9(04)    VALUE ZERO.    02340004
           05  PV-P-MMDDYY.                                             02350004
               10  PV-P-MM                 PIC 9(02)     VALUE ZEROES.  02360004
               10  PV-P-DD                 PIC 9(02)     VALUE ZEROES.  02370004
               10  PV-P-YY                 PIC 9(02)     VALUE ZEROES.  02380004
           05  PV-TABLE-DATE-CCYYMMDD.                                  02390004
               10  PV-TABLE-DATE-CCYY      PIC  9(04)    VALUE ZEROES.  02400004
               10  PV-TABLE-DATE-MM        PIC  9(02)    VALUE ZEROES.  02410004
               10  PV-TABLE-DATE-DD        PIC  9(02)    VALUE ZEROES.  02420004
           05  PV-CARD-NUMBER.                                          02430004
               10  PV-SPACE-CARD-NUMBER      PIC  X(12)                 02440004
                                                  VALUE 'XXXXXXXXXXXX'. 02450004
               10  PV-CARD-NBR-1             PIC  X(03) VALUE SPACE.    02460004
               10  PV-CARD-NBR-2             PIC  X(01) VALUE SPACE.    02470004
               10  PV-CARD-NBR-3             PIC  X(03) VALUE SPACE.    02480004
      ******************************************************************02490004
      *  TOP OF PAGE HEADING LINE FOR PRINTING A 132 CHARACTER REPORT. *02500004
      ******************************************************************02510004
           COPY DPWS132.                                                02520004
                                                                        02530004
      ******************************************************************02540004
      *  HEADING LINES DAILY DEBIT CARD AUDIT REPORT.                  *02550004
      ******************************************************************02560004
       01  H1-TITLE-HEADING.                                            02570004
           05  FILLER                      PIC  X(44)    VALUE SPACES.  02580004
           05  H1-TITLE                    PIC  X(35)    VALUE          02590004
               'DAILY DEBIT CARD AUDIT REPORT FOR '.                    02600004
           05  H1-PROCESS-DTE.                                          02610004
               10  H1-MM                   PIC  X(02)    VALUE SPACES.  02620004
               10  FILLER                  PIC  X(01)    VALUE '/'.     02630004
               10  H1-DD                   PIC  X(02)    VALUE SPACES.  02640004
               10  FILLER                  PIC  X(01)    VALUE '/'.     02650004
               10  H1-CCYY                 PIC  X(04)    VALUE SPACES.  02660004
           05  FILLER                      PIC  X(43)    VALUE SPACES.  02670004
                                                                        02680004
       01  H2-STORE-HEADING.                                            02690004
           05  FILLER                      PIC  X(01)    VALUE SPACES.  02700004
           05  FILLER                      PIC  X(10)    VALUE          02710004
               'STORE # - '.                                            02720004
           05  H2-STR-NBR                  PIC  X(04)    VALUE SPACES.  02730004
           05  FILLER                      PIC  X(117)   VALUE SPACES.  02740004
                                                                        02750004
       01  H3-COLUMN-HEADING.                                           02760004
           05  FILLER                      PIC  X(66)    VALUE SPACES.  02770004
           05  FILLER                      PIC  X(04)    VALUE          02780004
               'SALE'.                                                  02790004
           05  FILLER                      PIC  X(06)    VALUE SPACES.  02800004
           05  FILLER                      PIC  X(09)    VALUE          02810004
               'PROCESSED'.                                             02820004
           05  FILLER                      PIC  X(07)    VALUE SPACES.  02830004
           05  FILLER                      PIC  X(11)    VALUE          02840004
               'UNPROCESSED'.                                           02850004
C24628     05  FILLER                      PIC  X(02)    VALUE SPACES.  02860004
C24628     05  FILLER                      PIC  X(06)    VALUE          02870004
C24628         'REASON'.                                                02880004
C24628     05  FILLER                      PIC  X(02)    VALUE SPACES.  02890004
           05  FILLER                      PIC  X(05)    VALUE          02900004
               'TOTAL'.                                                 02910004
           05  FILLER                      PIC  X(18)    VALUE SPACES.  02920004
                                                                        02930004
       01  H4-COLUMN-HEADING.                                           02940004
           05  FILLER                      PIC  X(04)    VALUE SPACES.  02950004
           05  FILLER                      PIC  X(04)    VALUE          02960004
               'DATE'.                                                  02970004
           05  FILLER                      PIC  X(08)    VALUE SPACES.  02980004
           05  FILLER                      PIC  X(04)    VALUE          02990004
               'TIME'.                                                  03000004
           05  FILLER                      PIC  X(08)    VALUE SPACES.  03010004
           05  FILLER                      PIC  X(03)    VALUE          03020004
               'REG'.                                                   03030004
           05  FILLER                      PIC  X(07)    VALUE SPACES.  03040004
           05  FILLER                      PIC  X(04)    VALUE          03050004
               'TRAN'.                                                  03060004
           05  FILLER                      PIC  X(07)    VALUE SPACES.  03070004
           05  FILLER                      PIC  X(11)    VALUE          03080004
               'CARD NUMBER'.                                           03090004
           05  FILLER                      PIC  X(06)    VALUE SPACES.  03100004
           05  FILLER                      PIC  X(04)    VALUE          03110004
               'TYPE'.                                                  03120004
           05  FILLER                      PIC  X(08)    VALUE SPACES.  03130004
           05  FILLER                      PIC  X(05)    VALUE          03140004
               'SALES'.                                                 03150004
           05  FILLER                      PIC  X(12)    VALUE SPACES.  03160004
           05  FILLER                      PIC  X(05)    VALUE          03170004
               'SALES'.                                                 03180004
C24628     05  FILLER                      PIC  X(06)    VALUE SPACES.  03190004
C24628     05  FILLER                      PIC  X(04)    VALUE          03200004
C24628         'CODE'.                                                  03210004
C24628     05  FILLER                      PIC  X(03)    VALUE SPACES.  03220004
           05  FILLER                      PIC  X(05)    VALUE          03230004
               'SALES'.                                                 03240004
           05  FILLER                      PIC  X(21)    VALUE SPACES.  03250004
                                                                        03260004
      ******************************************************************03270004
      *  DETAIL LINE.                                                   03280004
      ******************************************************************03290004
       01  D1-DETAIL-LINE.                                              03300004
           05  FILLER                      PIC  X(01)    VALUE SPACES.  03310004
           05  D1-TRN-DATE-MM              PIC  X(02).                  03320004
           05  FILLER                      PIC  X(01)    VALUE '/'.     03330004
           05  D1-TRN-DATE-DD              PIC  X(02).                  03340004
           05  FILLER                      PIC  X(01)    VALUE '/'.     03350004
           05  D1-TRN-DATE-CCYY            PIC  X(04).                  03360004
           05  FILLER                      PIC  X(03)    VALUE SPACES.  03370004
           05  D1-TRN-TIME-HH              PIC  X(02).                  03380004
           05  FILLER                      PIC  X(01)    VALUE ':'.     03390004
           05  D1-TRN-TIME-MM              PIC  X(02).                  03400004
           05  FILLER                      PIC  X(01)    VALUE ':'.     03410004
           05  D1-TRN-TIME-SS              PIC  X(02).                  03420004
           05  FILLER                      PIC  X(06)    VALUE SPACES.  03430004
           05  D1-TRN-REG                  PIC  9(04).                  03440004
           05  FILLER                      PIC  X(06)    VALUE SPACES.  03450004
           05  D1-TRN-TRAN                 PIC  9(04).                  03460004
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03470004
           05  D1-CARD-NBR                 PIC  X(19).                  03480004
           05  FILLER                      PIC  X(03)    VALUE SPACES.  03490004
           05  D1-SALE-TYPE                PIC  X(01).                  03500004
           05  FILLER                      PIC  X(03)    VALUE SPACES.  03510004
           05  D1-PROCESSED                PIC  Z,ZZZ,ZZ9.99-.          03520004
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03530004
           05  D1-UNPROCESSED              PIC  Z,ZZZ,ZZ9.99-.          03540004
C24628     05  FILLER                      PIC  X(04)    VALUE SPACES.  03550004
C24628     05  D1-REASON-CDE               PIC  X(03)    VALUE SPACES.  03560004
C24628     05  FILLER                      PIC  X(28)    VALUE SPACES.  03570004
                                                                        03580004
      ******************************************************************03590004
      *  TOTAL LINE FOR STORE BY DAY.                                   03600004
      ******************************************************************03610004
       01  T1-TOTAL-STORE.                                              03620004
           05  FILLER                      PIC  X(11)    VALUE          03630004
               'TOTAL SALES'.                                           03640004
           05  FILLER                      PIC  X(59)    VALUE SPACES.  03650004
           05  T1-STORE-PROCESSED          PIC  ZZZ,ZZZ,ZZ9.99-.        03660004
           05  FILLER                      PIC  X(02)    VALUE SPACES.  03670004
           05  T1-STORE-UNPROCESSED        PIC  ZZZ,ZZZ,ZZ9.99-.        03680004
           05  FILLER                      PIC  X(03)    VALUE SPACES.  03690004
           05  T1-STORE-TOTAL              PIC  ZZZ,ZZZ,ZZ9.99-.        03700004
           05  FILLER                      PIC  X(13)    VALUE SPACES.  03710004
                                                                        03720004
      ******************************************************************03730004
      *  GRAND TOTAL LINE FOR STORE.                                    03740004
      ******************************************************************03750004
       01  T2-GRAND-TOTAL-STORE.                                        03760004
           05  FILLER                      PIC  X(22)    VALUE          03770004
               'TOTAL SALES FOR STORE '.                                03780004
           05  T2-STR-NBR                  PIC  9(04).                  03790004
           05  FILLER                      PIC  X(03)    VALUE ' - '.   03800004
           05  T2-TRN-DATE-MM              PIC  X(02).                  03810004
           05  FILLER                      PIC  X(01)    VALUE '/'.     03820004
           05  T2-TRN-DATE-DD              PIC  X(02).                  03830004
           05  FILLER                      PIC  X(01)    VALUE '/'.     03840004
           05  T2-TRN-DATE-CCYY            PIC  X(04).                  03850004
           05  FILLER                      PIC  X(31)    VALUE SPACES.  03860004
           05  T2-STORE-PROCESSED          PIC  ZZZ,ZZZ,ZZ9.99-.        03870004
           05  FILLER                      PIC  X(02)    VALUE SPACES.  03880004
           05  T2-STORE-UNPROCESSED        PIC  ZZZ,ZZZ,ZZ9.99-.        03890004
           05  FILLER                      PIC  X(03)    VALUE SPACES.  03900004
           05  T2-STORE-TOTAL              PIC  ZZZ,ZZZ,ZZ9.99-.        03910004
           05  FILLER                      PIC  X(08)    VALUE SPACES.  03920004
                                                                        03930004
      ******************************************************************03940004
      *  TOTAL LINE FOR CORP BY DAY.                                    03950004
      ******************************************************************03960004
       01  T3-TOTAL-CORP.                                               03970004
           05  FILLER                      PIC  X(10)    VALUE          03980004
               'CORPORATE '.                                            03990004
           05  T3-TRN-DATE-MM              PIC  X(02).                  04000004
           05  FILLER                      PIC  X(01)    VALUE '/'.     04010004
           05  T3-TRN-DATE-DD              PIC  X(02).                  04020004
           05  FILLER                      PIC  X(01)    VALUE '/'.     04030004
           05  T3-TRN-DATE-CCYY            PIC  X(04).                  04040004
           05  FILLER                      PIC  X(06)    VALUE          04050004
               ' TOTAL'.                                                04060004
           05  FILLER                      PIC  X(44)    VALUE SPACES.  04070004
           05  T3-CORP-PROCESSED           PIC  ZZZ,ZZZ,ZZ9.99-.        04080004
           05  FILLER                      PIC  X(02)    VALUE SPACES.  04090004
           05  T3-CORP-UNPROCESSED         PIC  ZZZ,ZZZ,ZZ9.99-.        04100004
           05  FILLER                      PIC  X(03)    VALUE SPACES.  04110004
           05  T3-CORP-TOTAL               PIC  ZZZ,ZZZ,ZZ9.99-.        04120004
           05  FILLER                      PIC  X(08)    VALUE SPACES.  04130004
                                                                        04140004
      ******************************************************************04150004
      *  GRAND TOTAL LINE FOR CORP.                                     04160004
      ******************************************************************04170004
       01  T4-GRAND-TOTAL-CORP.                                         04180004
           05  FILLER                      PIC  X(28)    VALUE          04190004
               'GRAND TOTAL CORPORATE SALES '.                          04200004
           05  FILLER                      PIC  X(42)    VALUE SPACES.  04210004
           05  T4-CORP-PROCESSED           PIC  ZZZ,ZZZ,ZZ9.99-.        04220004
           05  FILLER                      PIC  X(02)    VALUE SPACES.  04230004
           05  T4-CORP-UNPROCESSED         PIC  ZZZ,ZZZ,ZZ9.99-.        04240004
           05  FILLER                      PIC  X(03)    VALUE SPACES.  04250004
           05  T4-CORP-TOTAL               PIC  ZZZ,ZZZ,ZZ9.99-.        04260004
           05  FILLER                      PIC  X(08)    VALUE SPACES.  04270004
C24628******************************************************************04280004
C24628*  REASON CODE DESCRIPTION.                                       04290004
C24628******************************************************************04300004
C24628 01  PV-REASON-CDE-DESC              PIC  X(26)    VALUE          04310004
C24628           '*** REASON CODE LEGEND ***'.                          04320004
C24628 01  PV-LEGEND1                      PIC  X(28)    VALUE          04330004
C24628           'MSA - MISSING SA TRANSACTION'.                        04340004
C24628 01  PV-LEGEND2                      PIC  X(30)    VALUE          04350004
C24628         'MSB - MISSING BANK TRANSACTION'.                        04360004
C24628 01  PV-LEGEND3                      PIC  X(27)    VALUE          04370004
C24628         'CRD - CARD NUMBER DIFFERENT'.                           04380004
C24628 01  PV-LEGEND4                      PIC  X(23)    VALUE          04390004
C24628         'AMT - SALES AMT DIFFERS'.                               04400004
C24628 01  PV-LEGEND5                      PIC  X(22)    VALUE          04410004
C24628         'UNA - UNMATCHED ADJMNT'.                                04420004
C24628 01  PV-LEGEND6                      PIC  X(25)    VALUE          04430004
C24628         'BRE - BANK REVERSAL TRANS'.                             04440004
C24628 01  PV-LEGEND7                      PIC  X(26)    VALUE          04450004
C24628         'DBT - DUPLICATE BANK TRANS'.                            04460004
C24628 01  PV-LEGEND8                      PIC  X(24)    VALUE          04470004
C24628         'STD - SALE TYPE DIFFERS'.                               04480004
                                                                        04490004
      ******************************************************************04500004
      *  INPUT FILE LAYOUT                                             *04510004
      ******************************************************************04520004
           COPY TCWS002.                                                04530004
                                                                        04540004
      ***************************************************************** 04550004
      * PROCEDURE DIVISION.                                           * 04560004
      ***************************************************************** 04570004
       PROCEDURE DIVISION.                                              04580004
                                                                        04590004
      ******************************************************************04600004
      * 0000-MAINLINE-PARAGRAPH                                        *04610004
      *      PRODUCE DAILY DEBIT CARD AUDIT REPORT                     *04620004
      ******************************************************************04630004
       0000-MAINLINE-PARAGRAPH.                                         04640004
                                                                        04650004
           PERFORM 1000-INITIALIZE-PROGRAM.                             04660004
                                                                        04670004
           PERFORM 2100-READ-DBT-CRD-INPUT-FILE.                        04680004
                                                                        04690004
           PERFORM 2000-PROCESS-RECORDS                                 04700004
               UNTIL PS-END-OF-INPUT-FILE.                              04710004
                                                                        04720004
           PERFORM 2350-FINAL-TOTALS.                                   04730004
                                                                        04740004
           CLOSE DBT-CRD-AUDIT-REPORT-FILE                              04750004
                 DBT-CRD-INPUT-FILE.                                    04760004
                                                                        04770004
           GOBACK.                                                      04780004
                                                                        04790004
      ******************************************************************04800004
      * 1000-INITIALIZE-PROGRAM                                         04810004
      *  -  OPEN FILES                                                  04820004
      *  -  PROCESS CONTROL CARD                                        04830004
      *  -  FORMAT REPORT HEADINGS                                      04840004
      *  -  SELECT PARTN-NBR FROM TEPPART                               04850004
      *  -  OPEN TENDER CURSOR                                          04860004
      *  -  ATTEMPT FIRST FETCH OF TENDER CURSOR.                       04870004
      ******************************************************************04880004
                                                                        04890004
       1000-INITIALIZE-PROGRAM.                                         04900004
                                                                        04910004
           OPEN INPUT  POS-DATE-CARD-FILE                               04920004
                       DBT-CRD-INPUT-FILE                               04930004
                OUTPUT DBT-CRD-AUDIT-REPORT-FILE.                       04940004
                                                                        04950004
           PERFORM 1100-PROCESS-CONTROL-CARD.                           04960004
                                                                        04970004
           PERFORM 1400-FORMAT-HEADINGS.                                04980004
                                                                        04990004
           PERFORM 2200-WRITE-KOHLS-PAGE-HEADINGS.                      05000004
                                                                        05010004
           INITIALIZE PV-STORE-DAY-TOTAL                                05020004
                      PV-CORP-DAY-TOTAL.                                05030004
                                                                        05040004
           PERFORM 9000-ADD-DATE-2-CORP-TOTAL-TBL.                      05050004
                                                                        05060004
      ******************************************************************05070004
      * 1100-PROCESS-CONTROL-CARD                                       05080004
      *     READ CONTROL CARD TO GET POS DATE IN MMDDYY FORMAT.  CALL   05090004
      *     DATE ROUTINE TO CONVERT DATE TO A CCYY-MM-DD PROCESS DATE.  05100004
      ******************************************************************05110004
                                                                        05120004
       1100-PROCESS-CONTROL-CARD.                                       05130004
                                                                        05140004
           PERFORM 1110-READ-POS-DATE-CARD-FILE.                        05150004
                                                                        05160004
           CLOSE POS-DATE-CARD-FILE.                                    05170004
                                                                        05180004
           IF PS-END-OF-CARD-FILE                                       05190004
               MOVE '1100-PROCESS-CONTROL-CARD'  TO PV-PARAGRAPH-NAME   05200004
               MOVE 'CONTROL CARD IS MISSING'    TO PV-OPERATION-MSG-1  05210004
               SET AC-CONTROL-CARD-ERROR TO TRUE                        05220004
               PERFORM 9999-ABEND                                       05230004
           END-IF.                                                      05240004
                                                                        05250004
           DISPLAY PC-CURRENT-PROGRAM-NAME                              05260004
                   ' - CONTROL CARD = (' CC-CONTROL-CARD-DISPLAY  ')'.  05270004
                                                                        05280004
           PERFORM 1115-CONVERT-INPUT-DATE.                             05290004
                                                                        05300004
      ******************************************************************05310004
      * 1110-READ-POS-DATE-CARD-FILE                                    05320004
      *     READ DATE CONTROL CARD.                                     05330004
      ******************************************************************05340004
       1110-READ-POS-DATE-CARD-FILE.                                    05350004
                                                                        05360004
           READ POS-DATE-CARD-FILE INTO CC-CONTROL-CARD                 05370004
              AT END                                                    05380004
                 SET PS-END-OF-CARD-FILE TO TRUE                        05390004
           END-READ.                                                    05400004
                                                                        05410004
           MOVE PC-CENTURY      TO  CC-RFRM-POS-DATE-CC.                05420004
           MOVE CC-POS-DATE-MM  TO  CC-RFRM-POS-DATE-MM.                05430004
           MOVE CC-POS-DATE-DD  TO  CC-RFRM-POS-DATE-DD.                05440004
           MOVE CC-POS-DATE-YY  TO  CC-RFRM-POS-DATE-YY.                05450004
                                                                        05460004
      ******************************************************************05470004
      * 1115-CONVERT-INPUT-DATE                                         05480004
      *     CALL KOHLS CALENDAR ROUTINE:                                05490004
      *         PASS: CONTROL CARD POS DATE (MMDDYY FORMAT).            05500004
      *       RETURN: ACTUAL CALENDAR DB2 ISO DATE (CCYY-MM-DD FORMAT). 05510004
      ******************************************************************05520004
       1115-CONVERT-INPUT-DATE.                                         05530004
                                                                        05540004
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              05550004
                                                                        05560004
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   05570004
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   05580004
           SET  DPG52-LK-DTE-GREG            TO TRUE.                   05590004
           MOVE CC-POS-DATE-MMDDYY           TO DPG52-LK-DATE-INPUT.    05600004
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    05610004
                                                   DPG54 DPG55 DPG56.   05620004
           EVALUATE TRUE                                                05630004
               WHEN DPG54-SEVERE-ERROR                                  05640004
               WHEN DPG54-DATE-INVALID                                  05650004
                   MOVE '1115-CONVERT-INPUT-DATE'                       05660004
                                                 TO PV-PARAGRAPH-NAME   05670004
                   MOVE 'ERROR CONVERTING INPUT CARD DATE'              05680004
                                                 TO PV-OPERATION-MSG-1  05690004
                   SET AC-CALENDAR-ROUTINE-ERROR TO TRUE                05700004
                   MOVE DPG54-ERROR-MESSAGE      TO PV-OPERATION-MSG-2  05710004
                   PERFORM 9999-ABEND                                   05720004
           END-EVALUATE.                                                05730004
                                                                        05740004
           MOVE DPG55-DB2-ISO-DATE-FMT       TO PV-POS-CCYY-MM-DD.      05750004
                                                                        05760004
      ******************************************************************05770004
      * 1400-FORMAT-HEADINGS                                            05780004
      *     GET CURRENT DATE AND TIME FROM SYSTEM.                      05790004
      *     FORMAT REPORT FIELDS IN THE TOP PAGE HEADING LINE.          05800004
      *     FORMAT HEADING LINE 2 WITH CONVERTED POS DATE.              05810004
      ******************************************************************05820004
       1400-FORMAT-HEADINGS.                                            05830004
                                                                        05840004
           MOVE FUNCTION CURRENT-DATE (1:8) TO PV-CURR-DATE-CCYYMMDD.   05850004
           ACCEPT PV-CURR-TIME FROM TIME.                               05860004
                                                                        05870004
           MOVE PV-CURR-DATE-MM             TO DP132-RUN-MONTH.         05880004
           MOVE PV-CURR-DATE-DD             TO DP132-RUN-DAY.           05890004
           MOVE PV-CURR-DATE-CCYY           TO DP132-RUN-YEAR.          05900004
                                                                        05910004
           MOVE PV-CURR-TIME-HH             TO DP132-RUN-HOUR.          05920004
           MOVE PV-CURR-TIME-MM             TO DP132-RUN-MINUTE.        05930004
           MOVE PC-CURRENT-PROGRAM-NAME     TO DP132-PROGRAM-NAME.      05940004
           MOVE PC-REPORT-NBR-1             TO DP132-REPORT-NUMBER.     05950004
                                                                        05960004
           MOVE PV-POS-CCYY                 TO H1-CCYY.                 05970004
           MOVE PV-POS-MM                   TO H1-MM.                   05980004
           MOVE PV-POS-DD                   TO H1-DD.                   05990004
                                                                        06000004
      ******************************************************************06010004
      * 2000-PROCESS-RECORDS                                           *06020004
      ******************************************************************06030004
       2000-PROCESS-RECORDS.                                            06040004
                                                                        06050004
           IF PS-FIRST-RECORD-YES                                       06060004
              MOVE TC002-STR-NBR      TO PV-SAVE-STR-NBR                06070004
              MOVE PV-SAVE-STR-NBR    TO H2-STR-NBR                     06080004
                                         T2-STR-NBR                     06090004
              MOVE TC002-SLS-DTE      TO PV-SAVE-TRAN-DATE              06100004
              PERFORM 2300-WRITE-PAGE-HEADINGS                          06110004
              SET  PS-FIRST-RECORD-NO TO TRUE                           06120004
           END-IF.                                                      06130004
                                                                        06140004
           IF TC002-STR-NBR > PV-SAVE-STR-NBR                           06150004
              MOVE PV-BLANK-RECORD TO PV-PRINT-RECORD                   06160004
              MOVE 1               TO PV-LINE-SPACING                   06170004
              PERFORM 3000-WRITE-RECORD                                 06180004
              SET PV-IDX TO +1                                          06190004
              PERFORM 2500-WRITE-STR-DAY-TOTAL                          06200004
                  UNTIL PV-IDX > PC-EXCEPT-DAYS-NUMBER                  06210004
                    OR  PV-DATE(PV-IDX) = SPACES                        06220004
              MOVE PV-BLANK-RECORD TO PV-PRINT-RECORD                   06230004
              MOVE 1               TO PV-LINE-SPACING                   06240004
              PERFORM 3000-WRITE-RECORD                                 06250004
              SET PV-IDX TO +1                                          06260004
              INITIALIZE PV-STORE-DAY-TOTAL                             06270004
              PERFORM 2700-NEXT-STORE                                   06280004
           END-IF.                                                      06290004
                                                                        06300004
           PERFORM 2400-WRITE-DETAIL.                                   06310004
           PERFORM 2425-PROCESS-TOTALS.                                 06320004
                                                                        06330004
           PERFORM 2100-READ-DBT-CRD-INPUT-FILE.                        06340004
                                                                        06350004
      ******************************************************************06360004
      *2100-READ-DBT-CRD-INPUT-FILE                                    *06370004
      ******************************************************************06380004
       2100-READ-DBT-CRD-INPUT-FILE.                                    06390004
           READ DBT-CRD-INPUT-FILE                                      06400004
               INTO TC002-DATA                                          06410004
               AT END                                                   06420004
                   SET PS-END-OF-INPUT-FILE TO TRUE                     06430004
           END-READ.                                                    06440004
                                                                        06450004
      ******************************************************************06460004
      * 2200-WRITE-KOHLS-PAGE-HEADINGS                                 *06470004
      ******************************************************************06480004
       2200-WRITE-KOHLS-PAGE-HEADINGS.                                  06490004
                                                                        06500004
           MOVE +4 TO PA-LINE-CNT.                                      06510004
           ADD  +1 TO PA-PAGE-CNT.                                      06520004
                                                                        06530004
           MOVE PA-PAGE-CNT TO DP132-PAGE-NUMBER.                       06540004
                                                                        06550004
           WRITE DBT-CRD-AUDIT-REPORT-RECORD                            06560004
            FROM DP132-STANDRD-HEADER-132-COLS                          06570004
            AFTER ADVANCING PAGE                                        06580004
           END-WRITE.                                                   06590004
                                                                        06600004
           WRITE DBT-CRD-AUDIT-REPORT-RECORD                            06610004
            FROM H1-TITLE-HEADING                                       06620004
            AFTER ADVANCING 1                                           06630004
           END-WRITE.                                                   06640004
                                                                        06650004
      ******************************************************************06660004
      * 2300-WRITE-PAGE-HEADINGS.                                      *06670004
      ******************************************************************06680004
       2300-WRITE-PAGE-HEADINGS.                                        06690004
                                                                        06700004
           MOVE +3 TO PA-LINE-CNT.                                      06710004
                                                                        06720004
           MOVE PA-PAGE-CNT TO DP132-PAGE-NUMBER.                       06730004
                                                                        06740004
           WRITE DBT-CRD-AUDIT-REPORT-RECORD                            06750004
            FROM H2-STORE-HEADING                                       06760004
            AFTER ADVANCING 2                                           06770004
           END-WRITE.                                                   06780004
                                                                        06790004
           WRITE DBT-CRD-AUDIT-REPORT-RECORD                            06800004
            FROM H3-COLUMN-HEADING                                      06810004
            AFTER ADVANCING 2                                           06820004
           END-WRITE.                                                   06830004
                                                                        06840004
           WRITE DBT-CRD-AUDIT-REPORT-RECORD                            06850004
            FROM H4-COLUMN-HEADING                                      06860004
            AFTER ADVANCING 1                                           06870004
           END-WRITE.                                                   06880004
                                                                        06890004
      ******************************************************************06900004
      * 2350-FINAL-TOTALS.                                             *06910004
      ******************************************************************06920004
       2350-FINAL-TOTALS.                                               06930004
           MOVE PV-BLANK-RECORD TO PV-PRINT-RECORD.                     06940004
           MOVE 1               TO PV-LINE-SPACING.                     06950004
           PERFORM 3000-WRITE-RECORD.                                   06960004
           SET PV-IDX TO +1.                                            06970004
           PERFORM 2500-WRITE-STR-DAY-TOTAL                             06980004
                  UNTIL PV-IDX > PC-EXCEPT-DAYS-NUMBER                  06990004
                    OR  PV-DATE(PV-IDX) = SPACES.                       07000004
           MOVE PV-BLANK-RECORD TO PV-PRINT-RECORD.                     07010004
           MOVE 1               TO PV-LINE-SPACING.                     07020004
           PERFORM 3000-WRITE-RECORD.                                   07030004
           SET PV-IDX TO +1.                                            07040004
           INITIALIZE PV-STORE-DAY-TOTAL.                               07050004
           SET PV-CORP-IDX TO +1.                                       07060004
           PERFORM 2600-WRITE-CORP-TOTALS                               07070004
               UNTIL PV-CORP-IDX > PC-EXCEPT-DAYS-NUMBER                07080004
                 OR  PV-CORP-DATE(PV-CORP-IDX) = SPACES.                07090004
                                                                        07100004
           MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            07110004
           MOVE 1                        TO PV-LINE-SPACING.            07120004
           PERFORM 3000-WRITE-RECORD.                                   07130004
                                                                        07140004
           MOVE PV-STORE-GRAND-PROC-TOT   TO T4-CORP-PROCESSED.         07150004
           MOVE PV-STORE-GRAND-UNPROC-TOT TO T4-CORP-UNPROCESSED.       07160004
           MOVE PV-STORE-GRAND-TOTAL-TOT  TO T4-CORP-TOTAL.             07170004
                                                                        07180004
                                                                        07190004
           MOVE T4-GRAND-TOTAL-CORP  TO PV-PRINT-RECORD.                07200004
C24628     MOVE 1                    TO PV-LINE-SPACING.                07210004
C24628     PERFORM 3000-WRITE-RECORD.                                   07220004
C24628     MOVE 2                    TO PV-LINE-SPACING.                07230004
C24628     PERFORM 3000-WRITE-RECORD.                                   07240004
                                                                        07250004
C24628     MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            07260004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07270004
C24628     MOVE PV-REASON-CDE-DESC       TO PV-PRINT-RECORD.            07280004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07290004
C24628     PERFORM 3000-WRITE-RECORD.                                   07300004
                                                                        07310004
C24628     MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            07320004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07330004
C24628     MOVE PV-LEGEND1               TO PV-PRINT-RECORD.            07340004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07350004
C24628     PERFORM 3000-WRITE-RECORD.                                   07360004
                                                                        07370004
C24628     MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            07380004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07390004
C24628     MOVE PV-LEGEND2               TO PV-PRINT-RECORD.            07400004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07410004
C24628     PERFORM 3000-WRITE-RECORD.                                   07420004
                                                                        07430004
C24628     MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            07440004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07450004
C24628     MOVE PV-LEGEND3               TO PV-PRINT-RECORD.            07460004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07470004
C24628     PERFORM 3000-WRITE-RECORD.                                   07480004
                                                                        07490004
C24628     MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            07500004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07510004
C24628     MOVE PV-LEGEND4               TO PV-PRINT-RECORD.            07520004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07530004
C24628     PERFORM 3000-WRITE-RECORD.                                   07540004
                                                                        07550004
C24628     MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            07560004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07570004
C24628     MOVE PV-LEGEND5               TO PV-PRINT-RECORD.            07580004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07590004
C24628     PERFORM 3000-WRITE-RECORD.                                   07600004
                                                                        07610004
C24628     MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            07620004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07630004
C24628     MOVE PV-LEGEND6               TO PV-PRINT-RECORD.            07640004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07650004
C24628     PERFORM 3000-WRITE-RECORD.                                   07660004
                                                                        07670004
C24628     MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            07680004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07690004
C24628     MOVE PV-LEGEND7               TO PV-PRINT-RECORD.            07700004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07710004
C24628     PERFORM 3000-WRITE-RECORD.                                   07720004
                                                                        07730004
C24628     MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            07740004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07750004
C24628     MOVE PV-LEGEND8               TO PV-PRINT-RECORD.            07760004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07770004
C24628     PERFORM 3000-WRITE-RECORD.                                   07780004
                                                                        07790004
C24628     MOVE 1                        TO PV-LINE-SPACING.            07800004
C24628     PERFORM 3000-WRITE-RECORD.                                   07810004
                                                                        07820004
      ******************************************************************07830004
      * 2400-WRITE-DETAIL.                                             *07840004
      ******************************************************************07850004
       2400-WRITE-DETAIL.                                               07860004
           MOVE TC002-SLS-DTE            TO PV-DATE-CCYYMMDD            07870004
           MOVE TC002-STRT-TIME          TO PV-TIME-HHMMSS              07880004
           MOVE PV-DATE-MM               TO D1-TRN-DATE-MM              07890004
           MOVE PV-DATE-DD               TO D1-TRN-DATE-DD              07900004
           MOVE PV-DATE-CCYY             TO D1-TRN-DATE-CCYY            07910004
           MOVE PV-TIME-HH               TO D1-TRN-TIME-HH              07920004
           MOVE PV-TIME-MM               TO D1-TRN-TIME-MM              07930004
           MOVE PV-TIME-SS               TO D1-TRN-TIME-SS              07940004
           MOVE TC002-RGST-ID            TO D1-TRN-REG                  07950004
           MOVE TC002-TRN-NBR            TO D1-TRN-TRAN                 07960004
           MOVE TC002-CARD-NBR           TO PV-CARD-NUMBER              07970004
           IF PV-CARD-NBR-3 NUMERIC                                     07980004
              MOVE 'XXXXXXXXXXXX'        TO PV-SPACE-CARD-NUMBER        07990004
              MOVE 'XXX'                 TO PV-CARD-NBR-1               08000004
           ELSE                                                         08010004
              MOVE 'XXXXXXXXXXXX'        TO PV-SPACE-CARD-NUMBER        08020004
           END-IF                                                       08030004
           MOVE PV-CARD-NUMBER           TO D1-CARD-NBR                 08040004
           MOVE TC002-SALE-TYPE          TO D1-SALE-TYPE                08050004
           IF TC002-MATCH                                               08060004
               IF TC002-SALE-TYPE = 'S' OR                              08070004
                  TC002-SALE-TYPE = 'L' OR                              08080004
                  TC002-SALE-TYPE = 'R' OR                              08090004
                  TC002-SALE-TYPE = 'X' OR                              08100004
                  TC002-SALE-TYPE = 'C' OR                              08110004
                  TC002-SALE-TYPE = 'V'                                 08120004
                    MOVE TC002-TNDR-AMT     TO D1-PROCESSED             08130004
                    MOVE ZERO               TO D1-UNPROCESSED           08140004
               END-IF                                                   08150004
           ELSE                                                         08160004
               IF TC002-NOT-MATCHED                                     08170004
                 IF TC002-SALE-TYPE = 'S' OR                            08180004
                    TC002-SALE-TYPE = 'L' OR                            08190004
                    TC002-SALE-TYPE = 'R' OR                            08200004
                    TC002-SALE-TYPE = 'X' OR                            08210004
                    TC002-SALE-TYPE = 'C' OR                            08220004
                    TC002-SALE-TYPE = 'V'                               08230004
                      MOVE TC002-TNDR-AMT  TO D1-UNPROCESSED            08240004
                      MOVE ZERO            TO D1-PROCESSED              08250004
                 END-IF                                                 08260004
               END-IF                                                   08270004
           END-IF.                                                      08280004
C24628     MOVE TC002-REASON-CDE         TO PV-REASON-CDE.              08290004
C24628     MOVE PV-REASON-CDE            TO D1-REASON-CDE.              08300004
                                                                        08310004
           MOVE D1-DETAIL-LINE           TO PV-PRINT-RECORD.            08320004
           MOVE 1                        TO PV-LINE-SPACING.            08330004
           PERFORM 3000-WRITE-RECORD.                                   08340004
                                                                        08350004
      ******************************************************************08360004
      * 2425-PROCESS-TOTALS.                                           *08370004
      ******************************************************************08380004
       2425-PROCESS-TOTALS.                                             08390004
                                                                        08400004
           SET PS-NOT-FOUND-DATE TO TRUE.                               08410004
           SET PV-IDX TO +1.                                            08420004
           SEARCH PV-STORE-DAY-TOTALS                                   08430004
             AT END                                                     08440004
               IF PS-NOT-FOUND-DATE                                     08450004
                  PERFORM 2450-ADD-DATE-TO-TABLE                        08460004
               END-IF                                                   08470004
             WHEN PV-DATE (PV-IDX) = TC002-SLS-DTE                      08480004
               PERFORM 2475-ADD-DETAIL-TO-TOTAL                         08490004
               SET PS-FOUND-DATE TO TRUE                                08500004
           END-SEARCH.                                                  08510004
                                                                        08520004
           SET PS-NOT-FOUND-DATE   TO TRUE.                             08530004
           MOVE TC002-STR-NBR      TO PV-SAVE-STR-NBR.                  08540004
           MOVE TC002-SLS-DTE      TO PV-SAVE-TRAN-DATE.                08550004
                                                                        08560004
      ******************************************************************08570004
      * ADD DATE TO TABLE AND ADD THE SALES AMT TO THE TOTAL           *08580004
      ******************************************************************08590004
       2450-ADD-DATE-TO-TABLE.                                          08600004
                                                                        08610004
           SET PV-IDX TO +1.                                            08620004
           SEARCH  PV-STORE-DAY-TOTALS                                  08630004
             AT END                                                     08640004
                 CONTINUE                                               08650004
             WHEN PV-DATE (PV-IDX) = SPACES OR LOW-VALUES               08660004
                 MOVE TC002-SLS-DTE TO PV-DATE(PV-IDX)                  08670004
                PERFORM 2475-ADD-DETAIL-TO-TOTAL                        08680004
           END-SEARCH.                                                  08690004
                                                                        08700004
      ******************************************************************08710004
      * ADD THE DETAIL TO THE CORRECT DAY'S TOTAL                      *08720004
      ******************************************************************08730004
       2475-ADD-DETAIL-TO-TOTAL.                                        08740004
           IF TC002-MATCH                                               08750004
             IF TC002-SALE-TYPE = 'S' OR                                08760004
                TC002-SALE-TYPE = 'L' OR                                08770004
                TC002-SALE-TYPE = 'R' OR                                08780004
                TC002-SALE-TYPE = 'X' OR                                08790004
                TC002-SALE-TYPE = 'V' OR                                08800004
                TC002-SALE-TYPE = 'C'                                   08810004
                ADD  TC002-TNDR-AMT TO                                  08820004
                             PV-STORE-DAY-PROC-TOT (PV-IDX)             08830004
             END-IF                                                     08840004
           ELSE                                                         08850004
              IF TC002-NOT-MATCHED                                      08860004
                 IF TC002-SALE-TYPE = 'S' OR                            08870004
                     TC002-SALE-TYPE = 'L' OR                           08880004
                     TC002-SALE-TYPE = 'R' OR                           08890004
                     TC002-SALE-TYPE = 'X' OR                           08900004
                     TC002-SALE-TYPE = 'V' OR                           08910004
                     TC002-SALE-TYPE = 'C'                              08920004
                       ADD  TC002-TNDR-AMT TO                           08930004
                               PV-STORE-DAY-UNPROC-TOT (PV-IDX)         08940004
                 END-IF                                                 08950004
              END-IF                                                    08960004
           END-IF.                                                      08970004
                                                                        08980004
      ******************************************************************08990004
      * 2500-WRITE-STR-DAY-TOTAL.                                      *09000004
      ******************************************************************09010004
       2500-WRITE-STR-DAY-TOTAL.                                        09020004
           MOVE PV-DATE (PV-IDX)         TO PV-DATE-CCYYMMDD.           09030004
           MOVE PV-DATE-CCYY             TO T2-TRN-DATE-CCYY.           09040004
           MOVE PV-DATE-MM               TO T2-TRN-DATE-MM.             09050004
           MOVE PV-DATE-DD               TO T2-TRN-DATE-DD.             09060004
           MOVE PV-SAVE-STR-NBR          TO T2-STR-NBR.                 09070004
                                                                        09080004
           ADD  PV-STORE-DAY-PROC-TOT (PV-IDX)                          09090004
                           TO PV-STORE-DAY-TOTAL-TOT (PV-IDX)           09100004
                              PV-STORE-GRAND-PROC-TOT                   09110004
                              PV-STORE-GRAND-TOTAL-TOT.                 09120004
           ADD  PV-STORE-DAY-UNPROC-TOT (PV-IDX)                        09130004
                           TO PV-STORE-DAY-TOTAL-TOT (PV-IDX)           09140004
                              PV-STORE-GRAND-UNPROC-TOT                 09150004
                              PV-STORE-GRAND-TOTAL-TOT.                 09160004
                                                                        09170004
           PERFORM 2550-ADD-CORP-TOTAL.                                 09180004
                                                                        09190004
           MOVE PV-STORE-DAY-PROC-TOT (PV-IDX)  TO T2-STORE-PROCESSED.  09200004
           MOVE PV-STORE-DAY-UNPROC-TOT (PV-IDX)                        09210004
                                    TO T2-STORE-UNPROCESSED.            09220004
           MOVE PV-STORE-DAY-TOTAL-TOT (PV-IDX) TO T2-STORE-TOTAL.      09230004
                                                                        09240004
           MOVE T2-GRAND-TOTAL-STORE      TO PV-PRINT-RECORD.           09250004
           MOVE 1                         TO PV-LINE-SPACING.           09260004
           PERFORM 3000-WRITE-RECORD.                                   09270004
                                                                        09280004
           SET PV-IDX UP BY +1.                                         09290004
                                                                        09300004
      ******************************************************************09310004
      * 2550-ADD-CORP-TOTAL                                            *09320004
      ******************************************************************09330004
       2550-ADD-CORP-TOTAL.                                             09340004
                                                                        09350004
           SET PS-NOT-FOUND-DATE TO TRUE.                               09360004
           SET PV-CORP-IDX TO +1.                                       09370004
           SEARCH PV-CORP-DAY-TOTALS                                    09380004
             AT END                                                     09390004
               IF PS-NOT-FOUND-DATE                                     09400004
                  PERFORM 2575-ADD-DATE-TO-TABLE                        09410004
               END-IF                                                   09420004
             WHEN PV-CORP-DATE (PV-CORP-IDX) = PV-DATE (PV-IDX)         09430004
               ADD  PV-STORE-DAY-PROC-TOT (PV-IDX)                      09440004
                          TO PV-CORP-DAY-PROC-TOT (PV-CORP-IDX)         09450004
                             PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX)        09460004
               ADD  PV-STORE-DAY-UNPROC-TOT (PV-IDX)                    09470004
                          TO PV-CORP-DAY-UNPROC-TOT (PV-CORP-IDX)       09480004
                             PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX)        09490004
               SET PS-FOUND-DATE TO TRUE                                09500004
           END-SEARCH.                                                  09510004
                                                                        09520004
      ******************************************************************09530004
      * ADD DATE TO CORP TABLE AND ADD THE SALES AMT TO THE CORP TOTALS*09540004
      ******************************************************************09550004
       2575-ADD-DATE-TO-TABLE.                                          09560004
                                                                        09570004
           SET PV-CORP-IDX TO +1.                                       09580004
           SEARCH  PV-CORP-DAY-TOTALS                                   09590004
             AT END                                                     09600004
               CONTINUE                                                 09610004
             WHEN PV-CORP-DATE (PV-CORP-IDX) = SPACES OR LOW-VALUES     09620004
               MOVE PV-DATE (PV-IDX) TO PV-CORP-DATE (PV-CORP-IDX)      09630004
               ADD  PV-STORE-DAY-PROC-TOT (PV-IDX)                      09640004
                             TO PV-CORP-DAY-PROC-TOT (PV-CORP-IDX)      09650004
                                PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX)     09660004
               ADD  PV-STORE-DAY-UNPROC-TOT (PV-IDX)                    09670004
                             TO PV-CORP-DAY-UNPROC-TOT (PV-CORP-IDX)    09680004
                                PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX)     09690004
           END-SEARCH.                                                  09700004
                                                                        09710004
      ******************************************************************09720004
      * 2600-WRITE-CORP-TOTALS                                         *09730004
      ******************************************************************09740004
       2600-WRITE-CORP-TOTALS.                                          09750004
                                                                        09760004
           IF PV-CORP-DAY-PROC-TOT (PV-CORP-IDX) = 0 AND                09770004
              PV-CORP-DAY-UNPROC-TOT (PV-CORP-IDX) = 0 AND              09780004
              PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX) = 0                   09790004
                 CONTINUE                                               09800004
           ELSE                                                         09810004
              MOVE PV-CORP-DATE (PV-CORP-IDX) TO PV-DATE-CCYYMMDD       09820004
              MOVE PV-DATE-CCYY             TO T3-TRN-DATE-CCYY         09830004
              MOVE PV-DATE-MM               TO T3-TRN-DATE-MM           09840004
              MOVE PV-DATE-DD               TO T3-TRN-DATE-DD           09850004
              MOVE PV-CORP-DAY-PROC-TOT (PV-CORP-IDX)                   09860004
                                            TO T3-CORP-PROCESSED        09870004
              MOVE PV-CORP-DAY-UNPROC-TOT (PV-CORP-IDX)                 09880004
                                            TO T3-CORP-UNPROCESSED      09890004
              MOVE PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX)                  09900004
                                            TO T3-CORP-TOTAL            09910004
              MOVE T3-TOTAL-CORP            TO PV-PRINT-RECORD          09920004
              MOVE 1                        TO PV-LINE-SPACING          09930004
              PERFORM 3000-WRITE-RECORD                                 09940004
           END-IF.                                                      09950004
                                                                        09960004
           SET PV-CORP-IDX UP BY +1.                                    09970004
                                                                        09980004
      ******************************************************************09990004
      * 2700-NEXT-STORE.                                               *10000004
      ******************************************************************10010004
       2700-NEXT-STORE.                                                 10020004
                                                                        10030004
           MOVE TC002-STR-NBR    TO H2-STR-NBR                          10040004
                                    T2-STR-NBR.                         10050004
                                                                        10060004
           IF PA-LINE-CNT > PC-LINE-SMALL-LIMIT                         10070004
              PERFORM 2200-WRITE-KOHLS-PAGE-HEADINGS                    10080004
              PERFORM 2300-WRITE-PAGE-HEADINGS                          10090004
           ELSE                                                         10100004
              MOVE H2-STORE-HEADING TO PV-PRINT-RECORD                  10110004
              MOVE 2                TO PV-LINE-SPACING                  10120004
              PERFORM 3000-WRITE-RECORD                                 10130004
              MOVE PV-BLANK-RECORD  TO PV-PRINT-RECORD                  10140004
              MOVE 1                TO PV-LINE-SPACING                  10150004
              PERFORM 3000-WRITE-RECORD                                 10160004
              ADD +3 TO PA-LINE-CNT                                     10170004
           END-IF.                                                      10180004
                                                                        10190004
      ******************************************************************10200004
      * 3000-WRITE-RECORD                                               10210004
      ******************************************************************10220004
       3000-WRITE-RECORD.                                               10230004
                                                                        10240004
           ADD PV-LINE-SPACING   TO PA-LINE-CNT.                        10250004
                                                                        10260004
           IF PA-LINE-CNT > PC-LINE-LIMIT                               10270004
               PERFORM 2200-WRITE-KOHLS-PAGE-HEADINGS                   10280004
               PERFORM 2300-WRITE-PAGE-HEADINGS                         10290004
           END-IF.                                                      10300004
                                                                        10310004
           WRITE DBT-CRD-AUDIT-REPORT-RECORD                            10320004
            FROM PV-PRINT-RECORD                                        10330004
            AFTER ADVANCING PV-LINE-SPACING                             10340004
           END-WRITE.                                                   10350004
                                                                        10360004
           MOVE SPACES           TO PV-PRINT-RECORD.                    10370004
      ******************************************************************10380004
      * 9000-ADD-DATE-2-CORP-TOTAL-TBL                                 *10390004
      *     LOAD THE LAST 30 BUSINESS DAYS TO THE PV-CORP-DAY-TOTAL    *10400004
      *     TABLE                                                      *10410004
      *     EXCEPTION DAYS NUMBER FROM INPUT CONTROL CARD WILL         *10420004
      *     DETERMINE HOW MANYS DAYS TO MOVE TO THE TABLE              *10430004
      *            ************  MAX IS 30  MAX OCCURS IN TABLE ***    *10440004
      ******************************************************************10450004
       9000-ADD-DATE-2-CORP-TOTAL-TBL.                                  10460004
                                                                        10470004
           SET PV-CORP-IDX        TO +1.                                10480004
           MOVE PC-EXCEPT-DAYS-NUMBER TO SUB.                           10490004
           SUBTRACT +1               FROM SUB.                          10500004
           PERFORM 9100-DATES-TO-ADD                                    10510004
                UNTIL SUB = 0.                                          10520004
           MOVE CC-POS-DATE-MMDDYY TO  PV-P-MMDDYY.                     10530004
           MOVE PV-P-MM            TO  PV-TABLE-DATE-MM.                10540004
           MOVE PV-P-DD            TO  PV-TABLE-DATE-DD.                10550004
           MOVE PV-P-YY            TO  PV-HOLD-YY.                      10560004
           MOVE '20'               TO  PV-HOLD-CC.                      10570004
           MOVE PV-HOLD-CCYY       TO  PV-TABLE-DATE-CCYY.              10580004
           MOVE PV-TABLE-DATE-CCYYMMDD TO PV-CORP-DATE (PV-CORP-IDX).   10590004
                                                                        10600004
      ******************************************************************10610004
      * 9100-DATES-TO-ADD.                                             *10620004
      *     LOAD DATES TO PV-CORP-DATE IN THE PV-CORP-DAY-TOTALS TABLE *10630004
      *     LOAD DATES IN ASCENDING SEQUENCE BY SETTING SUB TO THE     *10640004
      *     CONTROL CARD NUMBER AND SUBTRACT 1 UNTIL SUB = 0           *10650004
      ******************************************************************10660004
       9100-DATES-TO-ADD.                                               10670004
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              10680004
                                                                        10690004
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   10700004
           SET  DPG51-DECREMENT-DATE   TO TRUE.                         10710004
           MOVE SUB                    TO DPG51-INCR-DECR-DAYS-9.       10720004
           SET  DPG52-LK-DTE-GREG      TO TRUE.                         10730004
           MOVE CC-POS-DATE-MMDDYY     TO DPG52-LK-DATE-INPUT.          10740004
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    10750004
                                                   DPG54 DPG55 DPG56.   10760004
           EVALUATE TRUE                                                10770004
              WHEN DPG54-SEVERE-ERROR                                   10780004
              WHEN DPG54-DATE-INVALID                                   10790004
                  MOVE '9100-DATES-TO-ADD'                              10800004
                                                TO PV-PARAGRAPH-NAME    10810004
                  MOVE 'ERROR CONVERTING PROCESSING DATE'               10820004
                                                TO PV-OPERATION-MSG-1   10830004
                  SET AC-CALENDAR-ROUTINE-ERROR TO TRUE                 10840004
                  MOVE DPG54-ERROR-MESSAGE      TO PV-OPERATION-MSG-2   10850004
                  PERFORM 9999-ABEND                                    10860004
           END-EVALUATE.                                                10870004
                                                                        10880004
           MOVE DPG55-DB2-ISO-DATE     TO PV-CORP-DATE(PV-CORP-IDX).    10890004
           SET PV-CORP-IDX  UP BY +1.                                   10900004
           SUBTRACT +1  FROM SUB.                                       10910004
                                                                        10920004
      ******************************************************************10930004
      * 9999-ABEND                                                      10940004
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  10950004
      ******************************************************************10960004
       9999-ABEND.                                                      10970004
           DISPLAY '** ABEND           **'.                             10980004
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  10990004
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        11000004
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       11010004
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       11020004
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         11030004
