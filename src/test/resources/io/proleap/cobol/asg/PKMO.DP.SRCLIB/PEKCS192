00001 *-----------------------*                                                 
00002  IDENTIFICATION DIVISION.                                                 
00003 *-----------------------*                                                 
00004  PROGRAM-ID.    PEKCS192.                                                 
00005  AUTHOR.        AMY REILAND.                                              
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
00007  DATE-WRITTEN.  07-31-93.                                                 
00008  DATE-COMPILED.                                                           
00009 *----------------------------------------------------------------*        
00010 *    Q192 - CANCEL IN-STORE DOCUMENT                             *        
00011 *                                                                *        
00012 *    THIS PROGRAM ALLOWS THE USER TO CANCEL ONE OR MORE IN-STORE *        
00013 *    DOCUMENTS.  A SINGLE DOCUMENT CAN COME FROM THE CRITERIA    *        
00014 *    SPEC, OR A LIST OF DOCUMENTS FROM THE ENTRY LIST.  ALL      *        
00015 *    DOCUMENTS CANCELLED WILL BE CHANGED TO HAVE A STATUS OF     *        
00016 *    'CA'.  ALL FIELDS ON THE SCREEN ARE PROTECTED.              *        
00017 *                                                                *        
00018 *    ONLY THOSE DOCUMENTS IN 'AV', 'GE', OR 'DQ' STATUS CAN BE   *        
00019 *    CANCELLED.                                                  *        
00020 *                                                                *        
00021 *    A STATUS CODE OF 'GE' AND TYPE/REASON CODE OF '175' CANNOT  *        
00022 *    BE CANCELLED.                                               *        
00023 *                                                                *        
00024 *----------------------------------------------------------------*        
00027 * 12/03/96 - TCM -  WR NO. 3904                                  *        
00020 *    - REPLACE ALL "++INCLUDE" REFERENCES WITH "COPY"            *        
00020 *      REFERENCES, PER STANDARDS                                 *        
00020 *    - DO NOT ALLOW REASON CODE "65" (RETURN TO VENDOR) PERM     *        
00020 *      IN-STORE DOCUMENTS TO BE PROCESSED BY THIS PROGRAM        *        
00030 *----------------------------------------------------------------*        
      * 02/09/01 - JFK -  WR NO. 21963                                 *        
      * ADDED RF CONTINUED CLEARANCE SHORT DESCRIPTION.                *        
      * ADDED CODE TO ALLOW AN RF CONTINUED CLEARANCE DOCUMENT TO BE   *        
      * CANCELLED WHEN IN A 'COUNTS ENTERED' STATUS.                   *        
      *----------------------------------------------------------------*        
      * 02/11/05 - TODD C. MICHALEK       - WR NO WR27559              *        
      *    - CHANGED ALL STORE NUMBER LENGTH REFERENCES FROM 3 TO 4    *        
      *      DIGITS IN LENGTH AND ADDED A 4 DIGIT NUMERIC STORE        *        
      *      NUMBER FIELD(S)                                           *        
      *    - REPLACED TSTR000 STORE TABLE CALL(S) WITH STORE TABLE     *        
      *      CALL(S) TO XST_LOC                                        *        
      *    - CHANGED THE FILLER REDEFINES DP020-VARIABLE-COMMAREA      *        
      *      FILLER LENGTH AND THE APPROPRIATE MNEMONIC LENGTH         *        
      *    - ADDED THE NEW REASON CODE ("87") FOR                      *        
      *      "COSMETICS TESTERS ONLY" IN-STORE DOCUMENTS               *        
      *----------------------------------------------------------------*        
      * 10/26/05 - TCM -  WR NO. 28048                                 *        
      *    - ADDED THE NEW REASON CODE ("89") FOR                      *        
      *      "MARK OUT-OF-STOCK - SALVAGE" IN-STORE DOCUMENTS          *        
      *----------------------------------------------------------------*        
CICS  *  CHANGED 10/17/2022    BY BARB SCHULTZ    WR: CHG0276658                
CICS  *                                                                         
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL             
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.              
CICS  *-----------------------------------------------------------------        
00025                                                                           
00026                                                                           
00027                                                                           
00028  ENVIRONMENT DIVISION.                                                    
00029                                                                           
00030                                                                           
00031                                                                           
00032  DATA DIVISION.                                                           
00033                                                                           
00034                                                                           
00035  WORKING-STORAGE SECTION.                                                 
00036                                                                           
00037  01  DK-DB2-KEYS.                                                         
00038      05  DK-CURRENT-DATE          PIC  X(10)  VALUE SPACE.                
00039      05  DK-DOCUMENT-NUMBER       PIC S9(07)  VALUE ZERO                  
00040                                               COMP-3.                     
00042      05  DK-STORE-NUMBER          PIC S9(04)  VALUE ZEROS                 
                                                    COMP.                       
00043      05  DK-USERID                PIC  X(08)  VALUE SPACE.                
00044 *                                                                         
00045  01  PC-PROGRAM-CONSTANTS.                                                
00046      05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'PE192A  '.          
00047      05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'PEKM192 '.          
00048      05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'PEKCS192'.          
00049      05  PC-INTER-APPL-CODES.                                             
00050          10  PC-INTER-APPL-CRIT-SPEC                                      
00051                                   PIC  X(01)  VALUE '1'.                  
00052          10  PC-INTER-APPL-LIST   PIC  X(01)  VALUE '2'.                  
00053      05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3                   
00054                                               COMP.                       
00055      05  PC-1                     PIC  X(01)  VALUE  '1'.                 
00056      05  PC-TSYMSG-NUMBERS.                                               
00057          10  PC-TSYMSG-00004      PIC  9(05)  VALUE  00004.               
00058          10  PC-TSYMSG-00026      PIC  9(05)  VALUE  00026.               
00059          10  PC-TSYMSG-00027      PIC  9(05)  VALUE  00027.               
00060          10  PC-TSYMSG-00070      PIC  9(05)  VALUE  00070.               
00061          10  PC-TSYMSG-00148      PIC  9(05)  VALUE  00148.               
00062          10  PC-TSYMSG-00283      PIC  9(05)  VALUE  00283.               
00063          10  PC-TSYMSG-00507      PIC  9(05)  VALUE  00507.               
00064          10  PC-TSYMSG-00522      PIC  9(05)  VALUE  00522.               
00065          10  PC-TSYMSG-00975      PIC  9(05)  VALUE  00975.               
00066 *                                                                         
00067  01  PS-PROGRAM-SWITCHES.                                                 
00068      05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.                 
00069          88  PS-ERROR                         VALUE  'Y'.                 
00070          88  PS-NO-ERROR                      VALUE  'N'.                 
00071      05  PS-LIST-END-SW           PIC  X(01)  VALUE  'N'.                 
00072          88  PS-LIST-END                      VALUE  'Y'.                 
00073          88  PS-NOT-LIST-END                  VALUE  'N'.                 
00074 *                                                                         
00075  01  PV-PROGRAM-VARIABLES.                                                
00076      05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0                    
00077                                               COMP SYNC.                  
00078      05  PV-HOLD-CREATE-DATE.                                             
00079          10  PV-HOLD-CREATE-CC    PIC  9(02)  VALUE ZEROS.                
00080          10  PV-HOLD-CREATE-YY    PIC  9(02)  VALUE ZEROS.                
00081          10  FILLER               PIC  X(01)  VALUE SPACES.               
00082          10  PV-HOLD-CREATE-MM    PIC  9(02)  VALUE ZEROS.                
00083          10  FILLER               PIC  X(01)  VALUE SPACES.               
00084          10  PV-HOLD-CREATE-DD    PIC  9(02)  VALUE ZEROS.                
00085      05  PV-HOLD-CREATE-DATE-RFMT.                                        
00086          10  PV-HOLD-CREATE-RFMT-MM                                       
00087                                   PIC  9(02)  VALUE ZEROS.                
00088          10  FILLER               PIC  X(01)  VALUE '/'.                  
00089          10  PV-HOLD-CREATE-RFMT-DD                                       
00090                                   PIC  9(02)  VALUE ZEROS.                
00091          10  FILLER               PIC  X(01)  VALUE '/'.                  
00092          10  PV-HOLD-CREATE-RFMT-YY                                       
00093                                   PIC  9(02)  VALUE ZEROS.                
00094      05  PV-HOLD-TYPE-REASON.                                             
00095          10  PV-HOLD-TYPE-CODE    PIC  X(01)  VALUE SPACES.               
00096          10  PV-HOLD-REASON-CODE.                                         
00097              15  PV-HOLD-REASON-CODE-N                                    
00098                                   PIC  9(02)  VALUE ZEROS.                
00099      05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0                    
00100                                               COMP SYNC.                  
00101 *                                                                         
00102  EJECT                                                                    
00103 *---------------------------------------------------------------**        
00104 *  THE LAYOUT FOR THE TEMPORARY STORAGE QUEUE CONTAINING        **        
00105 *  SELECTED LIST ITEMS TO BE PROCESSED BY THIS PROGRAM.         **        
00106 *---------------------------------------------------------------**        
00107                                                                           
00108      COPY PEWS067.                                                        
00109 *                                                                         
00110  EJECT                                                                    
00111 *----------------------------------------------------------------*        
00112 *    MAP LAYOUT                                                  *        
00113 *----------------------------------------------------------------*        
00114                                                                           
00115      COPY PEKM192.                                                        
00116 *                                                                         
00117  EJECT                                                                    
00118 *----------------------------------------------------------------*        
00119 *    ATTRIBUTE SETTINGS COPYBOOK.                                *        
00120 *----------------------------------------------------------------*        
00121                                                                           
00122      COPY DPWS015.                                                        
00123 *                                                                         
00124  EJECT                                                                    
00125 *----------------------------------------------------------------*        
00126 *    PERM CONSTANTS/WORKING STORAGE AREA COPYBOOK                *        
00127 *----------------------------------------------------------------*        
00128                                                                           
00129      COPY PEWS049.                                                        
00130 *                                                                         
00131  EJECT                                                                    
00132 *----------------------------------------------------------------*        
00133 *    FORMATTED DATE COPYBOOK.                                    *        
00134 *----------------------------------------------------------------*        
00135                                                                           
00136      COPY DPWS017.                                                        
00137 *                                                                         
00138  EJECT                                                                    
00139 *----------------------------------------------------------------*        
00140 *    FUNCTION KEYS COPYBOOK                                      *        
00141 *----------------------------------------------------------------*        
00142                                                                           
00143      COPY DPWS016.                                                        
00144 *                                                                         
00145  EJECT                                                                    
00146 *---------------------------------------------------------------**        
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          **        
00149 *---------------------------------------------------------------**        
00150                                                                           
00151      COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
00152 *                                                                         
00153  EJECT                                                                    
00154 *---------------------------------------------------------------**        
00155 *    STANDARD COMMAREA.                                                   
00156 *---------------------------------------------------------------**        
00157                                                                           
00158      COPY DPWS020.                                                        
00159      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
00160                                                                           
00161 *----------------------------------------------------------------*        
00162 *    INTER-APPLICATION COMMAREA.                                          
00163 *----------------------------------------------------------------*        
00164                                                                           
00165          10  INTER-APPL-COMMAREA  PIC X(200).                             
00166 *                                                                         
00167  EJECT                                                                    
00168 *----------------------------------------------------------------*        
00169 *    INTER-APPLICATION COMMAREA PASSED FROM THE CRITERIA SPEC             
00170 *----------------------------------------------------------------*        
00171      COPY PEWS064.                                                        
00172 *                                                                         
00173  EJECT                                                                    
00174 *----------------------------------------------------------------*        
00175 *    INTER-APPLICATION COMMAREA PASSED FROM THE ENTRY LIST                
00176 *----------------------------------------------------------------*        
00177      COPY PEWS066.                                                        
00178 *                                                                         
00179  EJECT                                                                    
00180 *----------------------------------------------------------------*        
00181 *    SPECIFIC COMMMAREA FOR PEKCS192.                                     
00182 *----------------------------------------------------------------*        
00183          10  ASC-SPECIFIC-COMMAREA.                                       
00184              15  ASC-INT-APPL-FORMAT-IND   PIC  X(01).                    
00185              15  ASC-STORE-NUMBER          PIC  X(04).                    
                   15  FILLER REDEFINES ASC-STORE-NUMBER.                       
                       20  ASC-STORE-NUMBER-N    PIC  9(04).                    
00186              15  ASC-STORE-NAME            PIC  X(25).                    
00187              15  ASC-DOCUMENT-NUMBER.                                     
00188                  20  ASC-DOCUMENT-NUMBER-N PIC  9(07).                    
00189              15  ASC-STATUS-CODE           PIC  X(02).                    
00190              15  ASC-STATUS-DESC           PIC  X(10).                    
00191              15  ASC-REASON-CODE           PIC  X(03).                    
00192              15  ASC-TYPE-DESC             PIC  X(08).                    
00193              15  ASC-REASON-DESC           PIC  X(12).                    
00194              15  ASC-DEPT-AREA             PIC  X(40).                    
00195              15  ASC-ISSUED-DATE           PIC  X(08).                    
00196              15  ASC-ISSUED-ID             PIC  X(08).                    
00197              15  ASC-DOC-VALID-IND         PIC  X(01).                    
00198                  88  ASC-DOC-VALID                     VALUE ' '.         
00199                  88  ASC-DOC-INVALID                   VALUE 'N'.         
00200                  88  ASC-DOC-INVALID-RSN               VALUE 'R'.         
00201              15  ASC-USE-SELECTION-QUEUE-IND                              
00202                                            PIC  X(01).                    
00203                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
00204              15  ASC-SEL-QUEUE-NAME        PIC  X(08).                    
00205              15  ASC-NUMBER-OF-TS-ITEMS    PIC S9(05)  COMP-3.            
00206              15  ASC-NUMBER-OF-SELECTED-ITEMS                             
00207                                            PIC S9(09)  COMP-3.            
00208              15  ASC-SEL-SUB               PIC S9(04)  COMP.              
00209              15  ASC-SEL-ITEM              PIC S9(04)  COMP.              
00210          10  FILLER                        PIC  X(2715).                  
00211      EJECT                                                                
00212 *----------------------------------------------------------------*        
00213 *    ABEND PROCESSING COPYBOOK.                                  *        
00214 *----------------------------------------------------------------*        
00215                                                                           
00216      COPY DPWS013.                                                        
00217 *                                                                         
00218  EJECT                                                                    
00219                                                                           
00220 *    DB2 AREA FOR TSTRPPC (IN-STORE HEADER TABLE)                         
00221                                                                           
00222      EXEC SQL                                                             
00223           INCLUDE TSTRPPC                                                 
00224      END-EXEC.                                                            
                                                                                
00469 *    SELLING AND CUSTOMER SERVICE LOCATION TABLE                          
                                                                                
00471      EXEC SQL                                                             
00472           INCLUDE XST00001                                                
00473      END-EXEC.                                                            
00232                                                                           
00233 *    DB2 AREA FOR COMMUNICATIONS                                          
00234                                                                           
00235      EXEC SQL                                                             
00236           INCLUDE SQLCA                                                   
00237      END-EXEC.                                                            
00238                                                                           
00239  EJECT                                                                    
00240  LINKAGE SECTION.                                                         
00241                                                                           
00242  01  DFHCOMMAREA.                                                         
00243      05  FILLER                         OCCURS  1 TO 4072 TIMES           
00244                                         DEPENDING ON EIBCALEN.            
00245          10  FILLER                     PIC X.                            
00246                                                                           
00247                                                                           
00248  PROCEDURE DIVISION.                                                      
00249 *****************************************************************         
00250 ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
00251 ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
00252 ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
00253 **                                                             **         
00254 ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
00255 ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
00256 *****************************************************************         
00257  0000-MAIN-MODULE.                                                        
00258      INITIALIZE DP030-CICS-API-FIELDS.                                    
00259      MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
00260      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
00261      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
00262      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
00263      MOVE LENGTH OF PE192AI        TO DP030-MAP-LENGTH (1).               
00264      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
00265      PERFORM 0001-CALL-CICS-ARCH-API.                                     
00266 *                                                                         
00267      PERFORM 1000-CONTROL-PROCESSING                                      
00268 *                                                                         
00269      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
00270      PERFORM 0001-CALL-CICS-ARCH-API.                                     
00271                                                                           
00272  0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
00274                                     DFHCOMMAREA                           
00275                                     DP030-CICS-API-FIELDS                 
00276                                     DP020-STANDARD-COMMAREA               
00277                                     PE192AI.                              
00278 *                                                                         
00279      IF  DP030-RC-CALL-SUCCESSFUL                                         
00280          CONTINUE                                                         
00281      ELSE                                                                 
00282          SET DP013-NO-ROLLBACK                                            
00283              DP013-XCTL-DISPLAY-RESTART                                   
00284              DP013-CICS-ABEND      TO TRUE                                
00285          MOVE 'BEFORE 0000-MAIN-MODULE'                                   
00286                                    TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00289          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
00290          PERFORM DP013-0000-PROCESS-ABEND                                 
00291      END-IF.                                                              
00292 *                                                                         
00293  EJECT                                                                    
00294 *----------------------------------------------------------------*        
00295 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
00296 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
00297 *----------------------------------------------------------------*        
00298                                                                           
00299  1000-CONTROL-PROCESSING.                                                 
00300                                                                           
00301      PERFORM DP017-0000-GET-CURR-DATE-TIME.                               
00302      SET DP017-EDITED-YMD-DATE-DASH-1                                     
00303          DP017-EDITED-YMD-DATE-DASH-2 TO TRUE.                            
00304      MOVE DP017-EDITED-DATE-YYYYMMDD                                      
00305                                    TO DK-CURRENT-DATE.                    
00307      MOVE DP020-USERID             TO DK-USERID.                          
00308                                                                           
00309      EVALUATE TRUE                                                        
00310          WHEN DP020-NEXT-ACT-INITIAL                                      
00311               PERFORM 1100-FIND-OBJECT-KEYS                               
00312               IF  PS-ERROR                                                
00313                   CONTINUE                                                
00314               ELSE                                                        
00315                   PERFORM 4000-BUILD-INITIAL-PANEL                        
00316               END-IF                                                      
00317                                                                           
00318          WHEN DP020-NEXT-ACT-READ-MAP                                     
00319              PERFORM 2000-PROCESS-PANEL                                   
00320                                                                           
00321          WHEN DP020-NEXT-ACT-RETURN                                       
00322              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
00323                                                                           
00324          WHEN OTHER                                                       
00325              SET DP013-LOGIC-ABEND TO TRUE                                
00326              SET DP013-NO-ROLLBACK TO TRUE                                
00327              MOVE '1000-CONTROL-PROCESSING'                               
00328                                    TO DP013-PARAGRAPH                     
00329              MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
00330                                    TO DP013-MESSAGE-TEXT(1)               
00331              MOVE DP020-NEXT-APPL-ACTIVITY                                
00332                                    TO DP013-MESSAGE-TEXT(2)               
00333              PERFORM DP013-0000-PROCESS-ABEND                             
00334      END-EVALUATE.                                                        
00335 *                                                                         
00336  EJECT                                                                    
00337 *----------------------------------------------------------------*        
00338 *  DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE   **        
00339 *  INTER-APPLICATION FORMAT CODE.                               **        
00340 *----------------------------------------------------------------*        
00341  1100-FIND-OBJECT-KEYS.                                                   
00342      INITIALIZE ASC-SPECIFIC-COMMAREA.                                    
00343      MOVE DP020-INT-APPL-FORMAT-IND                                       
00344                                    TO ASC-INT-APPL-FORMAT-IND.            
00345      EVALUATE DP020-INT-APPL-FORMAT-IND                                   
00346          WHEN PC-INTER-APPL-CRIT-SPEC                                     
00347              IF  PE064-STORE-NUMBER = SPACES                              
00348                  SET PS-ERROR                                             
00349                      DP020-MSG-FATAL                                      
00350                      DP020-NEXT-ACT-APPL-ERROR                            
00351                                    TO TRUE                                
00352 *                --- STORE NUMBER REQUIRED ---                            
00353                  MOVE PC-TSYMSG-00507                                     
00354                                    TO DP020-MSG-NUMBER                    
00355              ELSE                                                         
00356                  MOVE PE064-STORE-NUMBER                                  
00357                                    TO ASC-STORE-NUMBER-N                  
00358                                       DK-STORE-NUMBER                     
                                            XST00001-LOC-NBR                    
00359                  MOVE PE064-STORE-NAME                                    
00360                                    TO ASC-STORE-NAME                      
00361                  IF  PE064-DOCUMENT-NUMBER = SPACES                       
00362                      SET PS-ERROR                                         
00363                          DP020-MSG-FATAL                                  
00364                          DP020-NEXT-ACT-APPL-ERROR                        
00365                                    TO TRUE                                
00366 *                --- DOCUMENT NUMBER REQUIRED ---                         
00367                      MOVE PC-TSYMSG-00026                                 
00368                                    TO DP020-MSG-NUMBER                    
00369                  ELSE                                                     
00370                      MOVE PE064-DOCUMENT-NUMBER                           
00371                                    TO ASC-DOCUMENT-NUMBER                 
00372                      MOVE ASC-DOCUMENT-NUMBER-N                           
00373                                    TO DK-DOCUMENT-NUMBER                  
00374                  END-IF                                                   
00375              END-IF                                                       
00376                                                                           
00377              IF PS-NO-ERROR                                               
00378                  PERFORM 3310-VALIDATE-DOCUMENT                           
00379                  IF SQLCODE = ZERO                                        
00384                      MOVE STRPPC-RSN-CDE                                  
00385                                    TO PE049-WORKING-INSTR-RSN-CDE         
00380                      IF ((STRPPC-STATUS-CDE =                             
00381                           PE049-IS-AVAILABLE-STATUS-CDE OR                
00382                           PE049-IS-GENERATED-STATUS-CDE OR                
00383                           PE049-IS-DELINQUENT-STATUS-CDE) AND             
                                NOT PE049-IS-RTN-TO-VNDR-RSN)   OR              
                              ((STRPPC-STATUS-CDE =                             
                                PE049-IS-CNTS-ENTER-STATUS-CDE)                 
                                AND PE049-IS-RF-ZERO-OUT-RSN)                   
00386                          MOVE STRPPC-TYP-CDE                              
00387                                    TO PE049-WORKING-TYPE-CDE              
00388                          IF PE049-IS-INVENTORY-MISSED-RSN AND             
00389                             PE049-MARKDOWN                                
00390                              SET PS-ERROR                                 
00391                                  DP020-MSG-FATAL                          
00392                                  DP020-NEXT-ACT-APPL-ERROR                
00393                                    TO TRUE                                
00394 *          --- INVENTORY DOCUMENTS CANNOT BE ADDED OR CANCELLED --        
00395                              MOVE PC-TSYMSG-00975                         
00396                                    TO DP020-MSG-NUMBER                    
00397                          END-IF                                           
00398                      ELSE                                                 
00399                          SET PS-ERROR                                     
00400                              DP020-MSG-FATAL                              
00401                              DP020-NEXT-ACT-APPL-ERROR                    
00402                                    TO TRUE                                
00403 *          --- DOCUMENT IN INVALID STATUS FOR THIS FUNCTION ---           
00404                          MOVE PC-TSYMSG-00027                             
00405                                    TO DP020-MSG-NUMBER                    
00406                      END-IF                                               
00407                  ELSE                                                     
00408                      SET PS-ERROR                                         
00409                          DP020-MSG-FATAL                                  
00410                          DP020-NEXT-ACT-APPL-ERROR                        
00411                                    TO TRUE                                
00412 *          --- DOCUMENT NUMBER NOT ASSIGNED TO THAT LOCATION --           
00413                      MOVE PC-TSYMSG-00283                                 
00414                                    TO DP020-MSG-NUMBER                    
00415                  END-IF                                                   
00416                                                                           
00417              END-IF                                                       
00418                                                                           
00419          WHEN PC-INTER-APPL-LIST                                          
00420              SET ASC-USE-SELECTION-QUEUE TO TRUE                          
00421              MOVE PE066-NUMBER-OF-SELECTED-ITEMS                          
00422                                    TO ASC-NUMBER-OF-SELECTED-ITEMS        
00423              MOVE PE066-NUMBER-OF-TS-ITEMS                                
00424                                    TO ASC-NUMBER-OF-TS-ITEMS              
00425              MOVE PE066-SEL-QUEUE-NAME                                    
00426                                    TO ASC-SEL-QUEUE-NAME                  
00427              PERFORM 1120-PREPARE-LIST-ENTRY                              
00428                                                                           
00429          WHEN OTHER                                                       
00430              MOVE PC-TSYMSG-00148  TO DP020-MSG-NUMBER                    
00431              SET PS-ERROR                                                 
00432                  DP020-MSG-FATAL                                          
00433                  DP020-NEXT-ACT-APPL-ERROR                                
00434                                    TO TRUE                                
00435      END-EVALUATE.                                                        
00436 *                                                                         
00437  EJECT                                                                    
00438 *----------------------------------------------------------------*        
00439 * ENTRY IS FROM A LIST.  GET THE DOCUMENT NUMBER FROM THE TEMP   *        
00440 * STORAGE QUEUE PASSED FROM THE LIST.                            *        
00441 *----------------------------------------------------------------*        
00442  1120-PREPARE-LIST-ENTRY.                                                 
00443      MOVE PE067-MAX-ITEMS          TO ASC-SEL-SUB.                        
00444      MOVE ZERO                     TO ASC-SEL-ITEM.                       
00445      PERFORM 2110-GET-NEXT-DOCUMENT.                                      
00446 *                                                                         
00447  EJECT                                                                    
00448 *----------------------------------------------------------------*        
00449 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
00450 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
00451 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
00452 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
00453 * FUNCTION KEYS CAN GET THIS FAR.                                *        
00454 *----------------------------------------------------------------*        
00455                                                                           
00456  2000-PROCESS-PANEL.                                                      
00457                                                                           
00458      EVALUATE TRUE                                                        
00459                                                                           
00460          WHEN DP020-SRC-AID = DP016-CLEAR                                 
00461              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
00462                                                                           
00463          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
00464              PERFORM 4000-BUILD-INITIAL-PANEL                             
00465              IF ASC-DOC-VALID                                             
00466                  CONTINUE                                                 
00467              ELSE                                                         
00468                  IF ASC-DOC-INVALID                                       
00469 *          --- DOCUMENT IN INVALID STATUS FOR THIS FUNCTION ---           
00470                      MOVE PC-TSYMSG-00027                                 
00471                                    TO DP020-MSG-NUMBER                    
00472                      SET DP020-MSG-FATAL                                  
00473                                    TO TRUE                                
00474                  ELSE                                                     
00475                      IF ASC-DOC-INVALID-RSN                               
00476 *          --- INVENTORY DOCUMENTS CANNOT BE ADDED OR CANCELLED --        
00477                          MOVE PC-TSYMSG-00975                             
00478                                    TO DP020-MSG-NUMBER                    
00479                          SET DP020-MSG-FATAL                              
00480                                    TO TRUE                                
00481                      END-IF                                               
00482                  END-IF                                                   
00483              END-IF                                                       
00484                                                                           
00485          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
00486              IF  ASC-USE-SELECTION-QUEUE                                  
00487                  PERFORM 2110-GET-NEXT-DOCUMENT                           
00488                  IF PS-LIST-END                                           
00489                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
00490                      SET PS-NOT-LIST-END TO TRUE                          
00491                  ELSE                                                     
00492                      PERFORM 4000-BUILD-INITIAL-PANEL                     
00493                  END-IF                                                   
00494              ELSE                                                         
00495 *                --- BOTTOM OF LIST ---                                   
00496                  MOVE PC-TSYMSG-00070                                     
00497                                    TO DP020-MSG-NUMBER                    
00498                  SET DP020-MSG-INFORMATIONAL                              
00499                                    TO TRUE                                
00500              END-IF                                                       
00501                                                                           
00502                                                                           
00503          WHEN OTHER                                                       
00504 *            PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
00505              PERFORM 2100-CHECK-FUNCTION-KEY                              
00506      END-EVALUATE.                                                        
00507 *                                                                         
00508  EJECT                                                                    
00509 *----------------------------------------------------------------*        
00510 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED       *        
00511 * FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED   *        
00512 * FROM THE CICS ARCHITECTURE API.                                *        
00513 *----------------------------------------------------------------*        
00514                                                                           
00515  2100-CHECK-FUNCTION-KEY.                                                 
00516      EVALUATE TRUE                                                        
00517          WHEN DP020-SRC-AID = DP016-ENTER                                 
00518              IF ASC-DOC-VALID                                             
00519                  CONTINUE                                                 
00520              ELSE                                                         
00521                  IF ASC-DOC-INVALID                                       
00522 *          --- DOCUMENT IN INVALID STATUS FOR THIS FUNCTION ---           
00523                      MOVE PC-TSYMSG-00027                                 
00524                                    TO DP020-MSG-NUMBER                    
00525                      SET DP020-MSG-FATAL                                  
00526                                    TO TRUE                                
00527                  ELSE                                                     
00528                      IF ASC-DOC-INVALID-RSN                               
00529 *          --- INVENTORY DOCUMENTS CANNOT BE ADDED OR CANCELLED --        
00530                          MOVE PC-TSYMSG-00975                             
00531                                    TO DP020-MSG-NUMBER                    
00532                          SET DP020-MSG-FATAL                              
00533                                    TO TRUE                                
00534                      END-IF                                               
00535                  END-IF                                                   
00536              END-IF                                                       
00537                                                                           
00538          WHEN DP020-FK-CONFIRM (DP020-SRC-AID)                            
00539              IF ASC-DOC-VALID                                             
00540                  PERFORM 5000-UPDATE-DOC-TO-CANCELLED                     
00541 *            --- CANCEL SUCCESSFUL ---                                    
00542                  MOVE PC-TSYMSG-00522  TO DP020-MSG-NUMBER                
00543                  SET DP020-MSG-INFORMATIONAL                              
00544                                    TO TRUE                                
00545              ELSE                                                         
00546                  IF ASC-DOC-INVALID                                       
00547 *          --- DOCUMENT IN INVALID STATUS FOR THIS FUNCTION ---           
00548                      MOVE PC-TSYMSG-00027                                 
00549                                    TO DP020-MSG-NUMBER                    
00550                      SET DP020-MSG-FATAL                                  
00551                                    TO TRUE                                
00552                  ELSE                                                     
00553                      IF ASC-DOC-INVALID-RSN                               
00554 *          --- INVENTORY DOCUMENTS CANNOT BE ADDED OR CANCELLED --        
00555                          MOVE PC-TSYMSG-00975                             
00556                                    TO DP020-MSG-NUMBER                    
00557                          SET DP020-MSG-FATAL                              
00558                                    TO TRUE                                
00559                      END-IF                                               
00560                  END-IF                                                   
00561              END-IF                                                       
00562                                                                           
00563          WHEN OTHER                                                       
00564              SET DP013-NO-ROLLBACK                                        
00565                  DP013-XCTL-DISPLAY-RESTART                               
00566                  DP013-CICS-ABEND  TO TRUE                                
00567              MOVE '2000-PROCESS-PANEL'                                    
00568                                    TO DP013-PARAGRAPH                     
00569              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
00570                                    TO DP013-MESSAGE-TEXT (1)              
00571              PERFORM DP013-0000-PROCESS-ABEND                             
00572      END-EVALUATE.                                                        
00573 *                                                                         
00574  EJECT                                                                    
00575 *----------------------------------------------------------------*        
00576 * GET THE NEXT DOCUMENT FROM THE TEMP STORAGE QUEUE TO BE        *        
00577 * PROCESSED.  UPDATE THE DOCUMENT WITH AN ACTION OF INQUIRY.     *        
00578 *----------------------------------------------------------------*        
00579                                                                           
00580  2110-GET-NEXT-DOCUMENT.                                                  
00581      ADD +1                        TO ASC-SEL-SUB.                        
00582      IF  ASC-SEL-SUB > PE067-MAX-ITEMS                                    
00583          ADD +1                    TO ASC-SEL-ITEM                        
00584          MOVE +1                   TO ASC-SEL-SUB                         
00585      END-IF.                                                              
00586      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
00587          PERFORM 9100-READ-SEL-QUEUE                                      
00588          IF  PE067-ITEM (ASC-SEL-SUB) > ZERO                              
00589              MOVE PE067-STORE-NUMBER (ASC-SEL-SUB)                        
00590                                    TO ASC-STORE-NUMBER                    
00591                                       DK-STORE-NUMBER                     
                   MOVE ASC-STORE-NUMBER-N                                      
                                         TO XST00001-LOC-NBR                    
00592              MOVE PE067-DOCUMENT-NUMBER (ASC-SEL-SUB)                     
00593                                    TO ASC-DOCUMENT-NUMBER-N               
00594              MOVE ASC-DOCUMENT-NUMBER-N                                   
00595                                    TO DK-DOCUMENT-NUMBER                  
00596              PERFORM 3310-VALIDATE-DOCUMENT                               
00597              IF SQLCODE = ZERO                                            
00605                  MOVE STRPPC-RSN-CDE                                      
00606                                    TO PE049-WORKING-INSTR-RSN-CDE         
00598                  IF ((STRPPC-STATUS-CDE =                                 
00599                       PE049-IS-AVAILABLE-STATUS-CDE OR                    
00600                       PE049-IS-GENERATED-STATUS-CDE OR                    
00601                       PE049-IS-DELINQUENT-STATUS-CDE) AND                 
                            NOT PE049-IS-RTN-TO-VNDR-RSN)   OR                  
                          ((STRPPC-STATUS-CDE =                                 
                            PE049-IS-CNTS-ENTER-STATUS-CDE)                     
                            AND PE049-IS-RF-ZERO-OUT-RSN)                       
00602                      SET PE067-INQ (ASC-SEL-SUB)                          
00603                          ASC-DOC-VALID                                    
00604                                    TO TRUE                                
00607                      MOVE STRPPC-TYP-CDE                                  
00608                                    TO PE049-WORKING-TYPE-CDE              
00609                      IF PE049-IS-INVENTORY-MISSED-RSN AND                 
00610                         PE049-MARKDOWN                                    
00611                          SET PE067-ERR (ASC-SEL-SUB)                      
00612                              DP020-MSG-FATAL                              
00613                              ASC-DOC-INVALID-RSN                          
00614                                    TO TRUE                                
00615 *          --- INVENTORY DOCUMENTS CANNOT BE ADDED OR CANCELLED --        
00616                          MOVE PC-TSYMSG-00975                             
00617                                    TO DP020-MSG-NUMBER                    
00618                      END-IF                                               
00619                  ELSE                                                     
00620                      SET PE067-ERR (ASC-SEL-SUB)                          
00621                          DP020-MSG-FATAL                                  
                               DP020-NEXT-ACT-APPL-ERROR                        
00622                          ASC-DOC-INVALID                                  
00623                                    TO TRUE                                
00624 *          --- DOCUMENT IN INVALID STATUS FOR THIS FUNCTION ---           
00625                      MOVE PC-TSYMSG-00027                                 
00626                                    TO DP020-MSG-NUMBER                    
00627                  END-IF                                                   
00628                  PERFORM 9200-REWRITE-LIST-QUEUE                          
00629                  PERFORM 3000-GET-STORE-NAME                              
00630                  IF SQLCODE = 0                                           
00631                      MOVE XST00001-LOC-NM                                 
00632                                    TO ASC-STORE-NAME                      
00633                  ELSE                                                     
00634                      MOVE SPACES   TO ASC-STORE-NAME                      
00635                  END-IF                                                   
00636              ELSE                                                         
00637                  MOVE '2110-GET-NEXT-DOCUMENT'                            
00638                                    TO DP013-PARAGRAPH                     
00639                  MOVE 'SELECT THE NEXT DOC FROM THE LIST TSQ'             
00640                                    TO DP013-MESSAGE-TEXT (1)              
00641                  SET DP013-LOGIC-ABEND                                    
00642                      DP013-XCTL-DISPLAY-RESTART TO TRUE                   
00643                  PERFORM DP013-0000-PROCESS-ABEND                         
00644              END-IF                                                       
00645          ELSE                                                             
00646              SUBTRACT +1 FROM ASC-SEL-SUB                                 
00647 *            --- BOTTOM OF LIST ---                                       
00648              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
00649              SET DP020-MSG-INFORMATIONAL                                  
00650                  PS-LIST-END       TO TRUE                                
00651          END-IF                                                           
00652      ELSE                                                                 
00653          SUBTRACT +1 FROM ASC-SEL-ITEM                                    
00654          MOVE PE067-MAX-ITEMS      TO ASC-SEL-SUB                         
00655          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
00656          SET DP020-MSG-INFORMATIONAL                                      
00657              PS-LIST-END           TO TRUE                                
00658      END-IF.                                                              
00659 *                                                                         
00660  EJECT                                                                    
00661 *----------------------------------------------------------------*        
00662 * GET THE STORE NAME FOR THE ITEMS PASSED FROM THE LIST.         *        
00663 *----------------------------------------------------------------*        
00664 *                                                                         
00665  3000-GET-STORE-NAME.                                                     
00666      PERFORM                                                              
00667          WITH TEST AFTER                                                  
00668          VARYING PV-RETRY-COUNTER                                         
00669          FROM 1 BY 1                                                      
00670          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
00671                    OR (SQLCODE = ZERO)                                    
00672                    OR (SQLCODE = +100))                                   
00673 *                                                                         
00599          EXEC SQL                                                         
                   SELECT LOC_NM                                                
                   INTO :XST00001-LOC-NM                                        
                   FROM XST_LOC                                                 
                   WHERE LOC_NBR = :XST00001-LOC-NBR                            
00607          END-EXEC                                                         
00683                                                                           
00684          EVALUATE TRUE                                                    
00685              WHEN SQLCODE = ZERO                                          
00686              WHEN SQLCODE = +100                                          
00687              WHEN SQLCODE = -904                                          
00688              WHEN SQLCODE = -913                                          
00689                  CONTINUE                                                 
00690              WHEN SQLWARN0 NOT = SPACES                                   
00691              WHEN SQLCODE < ZERO                                          
00692              WHEN SQLCODE > ZERO                                          
00693                  MOVE '3300-GET-STORE-NAME'                               
00694                                    TO DP013-PARAGRAPH                     
00695                  MOVE 'SELECT STORE NAME FOR A SPECIFIC STORE:'           
00696                                    TO DP013-MESSAGE-TEXT (1)              
00697                  MOVE DK-STORE-NUMBER                                     
00698                                    TO DP013-MESSAGE-TEXT (2)              
00699                  MOVE SQLCA        TO DP013-SQLCA                         
00700                  MOVE 'TSTR000'    TO DP013-DB2-TABLE-NAME (1)            
00701                  SET DP013-DB2-ABEND                                      
00702                      DP013-XCTL-DISPLAY-RESTART TO TRUE                   
00703                  PERFORM DP013-0000-PROCESS-ABEND                         
00704          END-EVALUATE                                                     
00705      END-PERFORM.                                                         
00706 *                                                                         
00707      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
00708          MOVE '3000-GET-STORE-NAME'                                       
00709                                    TO DP013-PARAGRAPH                     
00710          MOVE 'MAX RETRIES EXCEEDED FOR STORE: '                          
00711                                    TO DP013-MESSAGE-TEXT (1)              
00712          MOVE DK-STORE-NUMBER      TO DP013-MESSAGE-TEXT (2)              
00713          MOVE SQLCA                TO DP013-SQLCA                         
00714          MOVE 'TSTR000'            TO DP013-DB2-TABLE-NAME (1)            
00715          SET DP013-DB2-ABEND                                              
00716              DP013-XCTL-DISPLAY-RESTART                                   
00717                                    TO TRUE                                
00718          PERFORM DP013-0000-PROCESS-ABEND                                 
00719      END-IF.                                                              
00720 *                                                                         
00721  EJECT                                                                    
00722 *----------------------------------------------------------------*        
00723 * VALIDATE THE STORE/DOCUMENT NUMBER COMBINATION FROM THE        *        
00724 * CRITERIA SPEC IS FOUND ON THE HEADER TABLE.  ALSO VERIFY THAT  *        
00725 * THE DOCUMENT IS IN A STATUS WHICH CAN BE CANCELLED.            *        
00726 *----------------------------------------------------------------*        
00727 *                                                                         
00728  3310-VALIDATE-DOCUMENT.                                                  
00729      PERFORM                                                              
00730          WITH TEST AFTER                                                  
00731          VARYING PV-RETRY-COUNTER                                         
00732          FROM 1 BY 1                                                      
00733          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
00734                    OR (SQLCODE = ZERO)                                    
00735                    OR (SQLCODE = +100))                                   
00736 *                                                                         
00737          EXEC SQL                                                         
00738              SELECT                                                       
00739                  STATUS_CDE,                                              
00740                  RSN_CDE,                                                 
00741                  TYP_CDE                                                  
00742              INTO                                                         
00743                  :STRPPC-STATUS-CDE,                                      
00744                  :STRPPC-RSN-CDE,                                         
00745                  :STRPPC-TYP-CDE                                          
00746              FROM TSTRPPC                                                 
00748              WHERE (DEST_LOC_NBR = :DK-STORE-NUMBER                       
00749                AND  DOC_NBR      = :DK-DOCUMENT-NUMBER                    
00750                AND  DOC_TYP_CDE  = :PE049-STORE-DOC-TYPE-CDE)             
00751          END-EXEC                                                         
00752                                                                           
00753          EVALUATE TRUE                                                    
00754              WHEN SQLCODE = ZERO                                          
00755              WHEN SQLCODE = +100                                          
00756              WHEN SQLCODE = -904                                          
00757              WHEN SQLCODE = -913                                          
00758                  CONTINUE                                                 
00759              WHEN SQLWARN0 NOT = SPACES                                   
00760              WHEN SQLCODE < ZERO                                          
00761              WHEN SQLCODE > ZERO                                          
00762                  MOVE '3310-VALIDATE-DOCUMENT'                            
00763                                    TO DP013-PARAGRAPH                     
00764                  MOVE 'SELECT STATUS CODE FOR STORE/DOCUMENT:'            
00765                                    TO DP013-MESSAGE-TEXT (1)              
00766                  MOVE DK-STORE-NUMBER                                     
00767                                    TO DP013-MESSAGE-TEXT (2)              
00768                  MOVE DK-DOCUMENT-NUMBER                                  
00769                                    TO DP013-MESSAGE-TEXT (2)              
00770                  MOVE SQLCA        TO DP013-SQLCA                         
00771                  MOVE 'TSTRPPC'    TO DP013-DB2-TABLE-NAME (1)            
00772                  SET DP013-DB2-ABEND                                      
00773                      DP013-XCTL-DISPLAY-RESTART TO TRUE                   
00774                  PERFORM DP013-0000-PROCESS-ABEND                         
00775          END-EVALUATE                                                     
00776      END-PERFORM.                                                         
00777 *                                                                         
00778      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
00779          MOVE '3310-VALIDATE-DOCUMENT'                                    
00780                                    TO DP013-PARAGRAPH                     
00781          MOVE 'MAX RETRIES EXCEEDED FOR STORE/DOC: '                      
00782                                    TO DP013-MESSAGE-TEXT (1)              
00783          MOVE DK-STORE-NUMBER      TO DP013-MESSAGE-TEXT (2)              
00784          MOVE DK-DOCUMENT-NUMBER   TO DP013-MESSAGE-TEXT (3)              
00785          MOVE SQLCA                TO DP013-SQLCA                         
00786          MOVE 'TSTRPPC'            TO DP013-DB2-TABLE-NAME (1)            
00787          SET DP013-DB2-ABEND                                              
00788              DP013-XCTL-DISPLAY-RESTART                                   
00789                                    TO TRUE                                
00790          PERFORM DP013-0000-PROCESS-ABEND                                 
00791      END-IF.                                                              
00792 *                                                                         
00793  EJECT                                                                    
00794 *----------------------------------------------------------------*        
00795 *    VALIDATE THE QUEUE WAS PASSED AND DISPLAY FOR THE FIRST     *        
00796 *    ENTRY.                                                      *        
00797 *----------------------------------------------------------------*        
00798                                                                           
00799  4000-BUILD-INITIAL-PANEL.                                                
00800      SET DP030-SET-CURSOR-APPL-1   TO TRUE                                
00801      MOVE ASC-STORE-NUMBER-N       TO DK-STORE-NUMBER.                    
00802      MOVE ASC-DOCUMENT-NUMBER-N    TO DK-DOCUMENT-NUMBER.                 
00803      PERFORM 4100-GET-DOCUMENT.                                           
00804      MOVE STRPPC-STATUS-CDE        TO ASC-STATUS-CODE.                    
00805      MOVE STRPPC-TYP-CDE           TO PV-HOLD-TYPE-CODE.                  
00806      MOVE STRPPC-RSN-CDE           TO PV-HOLD-REASON-CODE-N.              
00807 *                                                                         
00808      MOVE PV-HOLD-TYPE-CODE        TO PE049-WORKING-TYPE-CDE.             
00809      EVALUATE TRUE                                                        
00810          WHEN PE049-MARKDOWN                                              
00811              MOVE PE049-SHORT-MKDN-DESC1                                  
00812                                    TO ASC-TYPE-DESC                       
00813          WHEN PE049-MARKUP                                                
00814              MOVE PE049-SHORT-MKUP-DESC1                                  
00815                                    TO ASC-TYPE-DESC                       
00816          WHEN PE049-MARKDOWN-CANCEL                                       
00817              MOVE PE049-SHORT-MKDN-CANCEL-DESC                            
00818                                    TO ASC-TYPE-DESC                       
00819          WHEN PE049-MARKUP-CANCEL                                         
00820              MOVE PE049-SHORT-MKUP-CANCEL-DESC                            
00821                                    TO ASC-TYPE-DESC                       
00822          WHEN OTHER                                                       
00823              MOVE SPACES           TO ASC-TYPE-DESC                       
00824      END-EVALUATE.                                                        
00825 *                                                                         
00826      MOVE PV-HOLD-REASON-CODE-N    TO                                     
00827                                    PE049-WORKING-INSTR-RSN-CDE.           
00828      EVALUATE TRUE                                                        
00829          WHEN PE049-IS-ZERO-OUT-RSN                                       
00830              MOVE PE049-IS-SHORT-ZERO-OUT-DESC                            
00831                                    TO ASC-REASON-DESC                     
               WHEN PE049-IS-RF-ZERO-OUT-RSN                                    
                   MOVE PE049-IS-SHORT-RF-ZERO-OUT                              
                                         TO ASC-REASON-DESC                     
00832          WHEN PE049-IS-CORRECTION-RSN                                     
00833              MOVE PE049-IS-SHORT-CORRECTION-DESC                          
00834                                    TO ASC-REASON-DESC                     
00835          WHEN PE049-IS-BUYER-GEN-MISSED-RSN                               
00836              MOVE PE049-IS-SHORT-BYR-MISSED-DESC                          
00837                                    TO ASC-REASON-DESC                     
00838          WHEN PE049-IS-CUSTOMER-RET-RSN                                   
00839              MOVE PE049-IS-SHORT-CUST-RET-DESC                            
00840                                    TO ASC-REASON-DESC                     
00841          WHEN PE049-IS-KNOWN-THEFT-RSN                                    
00842              MOVE PE049-IS-SHORT-THEFT-DESC                               
00843                                    TO ASC-REASON-DESC                     
00844          WHEN PE049-IS-INSTR-DAMAGE-RSN                                   
00845              MOVE PE049-IS-SHORT-INSTR-DMG-DESC                           
00846                                    TO ASC-REASON-DESC                     
               WHEN PE049-IS-COSMETICS-TESTER-RSN                               
                   MOVE PE049-IS-SHORT-CSMTCS-TST-DESC                          
                                         TO ASC-REASON-DESC                     
               WHEN PE049-IS-MOS-SALVAGE-RSN                                    
                   MOVE PE049-IS-SHORT-MOS-SALVAGE                              
                                         TO ASC-REASON-DESC                     
00847          WHEN PE049-IS-FINANCIAL-ADJ-RSN                                  
00848              MOVE PE049-IS-SHORT-FIN-ADJ-DESC                             
00849                                    TO ASC-REASON-DESC                     
00850          WHEN OTHER                                                       
00851              MOVE SPACES           TO ASC-REASON-DESC                     
00852      END-EVALUATE.                                                        
00853      IF (PV-HOLD-TYPE-CODE = ZERO                                         
00854       OR PV-HOLD-REASON-CODE = ZERO)                                      
00855          MOVE SPACES               TO ASC-REASON-CODE                     
00856      ELSE                                                                 
00857          MOVE PV-HOLD-TYPE-REASON  TO ASC-REASON-CODE                     
00858      END-IF.                                                              
00859 *                                                                         
00860      MOVE STRPPC-DEPT-AREA-DEST    TO ASC-DEPT-AREA.                      
00861      MOVE STRPPC-CRE-DTE           TO PV-HOLD-CREATE-DATE.                
00862      MOVE PV-HOLD-CREATE-YY        TO PV-HOLD-CREATE-RFMT-YY.             
00863      MOVE PV-HOLD-CREATE-MM        TO PV-HOLD-CREATE-RFMT-MM.             
00864      MOVE PV-HOLD-CREATE-DD        TO PV-HOLD-CREATE-RFMT-DD.             
00865      MOVE PV-HOLD-CREATE-DATE-RFMT                                        
00866                                    TO ASC-ISSUED-DATE.                    
00867      MOVE STRPPC-CRE-ID            TO ASC-ISSUED-ID.                      
00868 *                                                                         
00869                                                                           
00870      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
00871                                                                           
00872      IF PS-NO-ERROR                                                       
00873          IF ASC-DOC-INVALID OR                                            
00874             ASC-DOC-INVALID-RSN                                           
00875              CONTINUE                                                     
00876          ELSE                                                             
00877              MOVE PC-TSYMSG-00004  TO DP020-MSG-NUMBER                    
00878              SET DP020-MSG-INFORMATIONAL TO TRUE                          
00879          END-IF                                                           
00880      END-IF.                                                              
00881 *                                                                         
00882  EJECT                                                                    
00883 *----------------------------------------------------------------*        
00884 * SELECT A ROW FROM THE DB2 IN-STORE HEADER TABLE.               *        
00885 * STORE/DOCUMENT SHOULD BE VALID WHEN IT GETS TO THIS CALL.  THE *        
00886 * ENTRY LIST SHOULD HAVE ALREADY VALIDATED THAT THE STORE/DOC    *        
00887 * EXISTS, AND THE STORE/DOC SENT FROM THE CRITERIA SPEC SHOULD   *        
00888 * HAVE BEEN VERIFIED IN 3110-VALIDATE-DOCUMENT.                  *        
00889 *----------------------------------------------------------------*        
00890  4100-GET-DOCUMENT.                                                       
00891      PERFORM                                                              
00892          WITH TEST AFTER                                                  
00893          VARYING PV-RETRY-COUNTER                                         
00894          FROM 1 BY 1                                                      
00895          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
00896                    OR (SQLCODE = ZERO)                                    
00897                    OR (SQLCODE = +100))                                   
00898 *                                                                         
00899          EXEC SQL                                                         
00900              SELECT STATUS_CDE,                                           
00901                     TYP_CDE,                                              
00902                     RSN_CDE,                                              
00903                     DEPT_AREA_DEST,                                       
00904                     CRE_DTE,                                              
00905                     CRE_ID                                                
00906              INTO   :STRPPC-STATUS-CDE,                                   
00907                     :STRPPC-TYP-CDE,                                      
00908                     :STRPPC-RSN-CDE,                                      
00909                     :STRPPC-DEPT-AREA-DEST,                               
00910                     :STRPPC-CRE-DTE,                                      
00911                     :STRPPC-CRE-ID                                        
00912              FROM   TSTRPPC                                               
00914              WHERE (DEST_LOC_NBR  = :DK-STORE-NUMBER                      
00915                AND  DOC_NBR       = :DK-DOCUMENT-NUMBER                   
00916                AND  DOC_TYP_CDE   = :PE049-STORE-DOC-TYPE-CDE)            
00917          END-EXEC                                                         
00918                                                                           
00919          EVALUATE TRUE                                                    
00920              WHEN SQLCODE = ZERO                                          
00921              WHEN SQLCODE = -904                                          
00922              WHEN SQLCODE = -913                                          
00923                  CONTINUE                                                 
00924              WHEN SQLWARN0 NOT = SPACES                                   
00925              WHEN SQLCODE < ZERO                                          
00926              WHEN SQLCODE > ZERO                                          
00927                  MOVE '4100-GET-DOCUMENT'                                 
00928                                    TO DP013-PARAGRAPH                     
00929                  MOVE 'SELECT STORE/DOCUMENT INFORMATION'                 
00930                                    TO DP013-MESSAGE-TEXT (1)              
00931                  MOVE SQLCA        TO DP013-SQLCA                         
00932                  MOVE 'TSTRPPC'    TO DP013-DB2-TABLE-NAME (1)            
00933                  SET DP013-DB2-ABEND                                      
00934                      DP013-XCTL-DISPLAY-RESTART                           
00935                                    TO TRUE                                
00936                  PERFORM DP013-0000-PROCESS-ABEND                         
00937          END-EVALUATE                                                     
00938      END-PERFORM.                                                         
00939 *                                                                         
00940      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
00941          MOVE '4100-GET-DOCUMENT'  TO DP013-PARAGRAPH                     
00942          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
00943                                    TO DP013-MESSAGE-TEXT (1)              
00944          MOVE 'STORE/DOCUMENT INFORMATION'                                
00945                                    TO DP013-MESSAGE-TEXT (2)              
00946          MOVE SQLCA                TO DP013-SQLCA                         
00947          MOVE 'TSTRPPC'            TO DP013-DB2-TABLE-NAME (1)            
00948          SET DP013-DB2-ABEND                                              
00949              DP013-XCTL-DISPLAY-RESTART                                   
00950                                    TO TRUE                                
00951          PERFORM DP013-0000-PROCESS-ABEND                                 
00952      END-IF.                                                              
00953 *                                                                         
00954  EJECT                                                                    
00955 *----------------------------------------------------------------*        
00956 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
00957 *----------------------------------------------------------------*        
00958                                                                           
00959  4400-MOVE-COMMAREA-TO-SCREEN.                                            
00960      MOVE ASC-STORE-NUMBER         TO ASTRNO.                             
00961      MOVE ASC-STORE-NAME           TO ASTRNMO.                            
00962      MOVE ASC-DOCUMENT-NUMBER      TO ADOCNO.                             
00963      MOVE ASC-STATUS-CODE          TO ASTATO.                             
00964      MOVE ASC-STATUS-DESC          TO ASTATDO.                            
00965      MOVE ASC-REASON-CODE          TO ARSNO.                              
00966      MOVE ASC-TYPE-DESC            TO ATYPDO.                             
00967      MOVE ASC-REASON-DESC          TO ARSNDO.                             
00968      MOVE ASC-DEPT-AREA            TO ADPTARO.                            
00969      MOVE ASC-ISSUED-DATE          TO AISSDTEO.                           
00970      MOVE ASC-ISSUED-ID            TO AISSIDO.                            
00971                                                                           
00972 *                                                                         
00973  EJECT                                                                    
00974 *----------------------------------------------------------------*        
00975 * APPLY THE CHANGES TO THE MESSAGE TO THE DB2 TABLE.             *        
00976 *----------------------------------------------------------------*        
00977  5000-UPDATE-DOC-TO-CANCELLED.                                            
00978      MOVE ASC-STORE-NUMBER-N       TO DK-STORE-NUMBER.                    
00979      MOVE ASC-DOCUMENT-NUMBER-N    TO DK-DOCUMENT-NUMBER.                 
00980      MOVE ASC-STATUS-CODE          TO STRPPC-PREV-STATUS-CDE.             
00981      MOVE PE049-IS-CANCELLED-STATUS-CDE                                   
00982                                    TO STRPPC-STATUS-CDE.                  
00983      PERFORM 5100-UPDATE-INSTR-DOC.                                       
00984      IF  ASC-USE-SELECTION-QUEUE                                          
00985          PERFORM 5200-UPDATE-SELECTION-QUEUE                              
00986      END-IF.                                                              
00987 *                                                                         
00988  EJECT                                                                    
00989 ******************************************************************        
00990 ** UPDATE THE ROW ON THE DB2 TABLE.                             **        
00991 ******************************************************************        
00992  5100-UPDATE-INSTR-DOC.                                                   
00993      PERFORM                                                              
00994          WITH TEST AFTER                                                  
00995          VARYING PV-RETRY-COUNTER                                         
00996          FROM 1 BY 1                                                      
00997          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
00998                    OR (SQLCODE = ZERO))                                   
00999          EXEC SQL                                                         
01000              UPDATE TSTRPPC                                               
01001                  SET STATUS_CDE      = :STRPPC-STATUS-CDE,                
01002                      PREV_STATUS_CDE = :STRPPC-PREV-STATUS-CDE,           
01003                      CAN_DTE         = :DK-CURRENT-DATE,                  
01004                      CAN_ID          = :DK-USERID                         
01006              WHERE (DEST_LOC_NBR     = :DK-STORE-NUMBER                   
01007                AND  DOC_NBR          = :DK-DOCUMENT-NUMBER                
01008                AND  DOC_TYP_CDE      = :PE049-STORE-DOC-TYPE-CDE)         
01009          END-EXEC                                                         
01010                                                                           
01011          EVALUATE TRUE                                                    
01012              WHEN SQLCODE = ZERO                                          
01013              WHEN SQLCODE = -904                                          
01014              WHEN SQLCODE = -913                                          
01015                  CONTINUE                                                 
01016              WHEN SQLWARN0 NOT = SPACES                                   
01017              WHEN SQLCODE < ZERO                                          
01018              WHEN SQLCODE > ZERO                                          
01019                  MOVE '5100-UPDATE-INSTR-DOC'                             
01020                                    TO DP013-PARAGRAPH                     
01021                  MOVE 'UPDATE A ROW ON THE INSTR DOC HEADER TBL'          
01022                                    TO DP013-MESSAGE-TEXT(1)               
01023                  MOVE SQLCA        TO DP013-SQLCA                         
01024                  MOVE 'TSTRPPC'    TO DP013-DB2-TABLE-NAME (1)            
01025                  SET DP013-DB2-ABEND                                      
01026                      DP013-XCTL-DISPLAY-RESTART TO TRUE                   
01027                  PERFORM DP013-0000-PROCESS-ABEND                         
01028          END-EVALUATE                                                     
01029      END-PERFORM.                                                         
01030 *                                                                         
01031      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
01032          MOVE '5100-UPDATE-INSTR-DOC'                                     
01033                                    TO DP013-PARAGRAPH                     
01034          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO UPDATE A STO        
01035 -             'RE/DOC'             TO DP013-MESSAGE-TEXT (1)              
01036          MOVE SQLCA                TO DP013-SQLCA                         
01037          MOVE 'TSTRPPC'            TO DP013-DB2-TABLE-NAME (1)            
01038          SET DP013-DB2-ABEND                                              
01039              DP013-XCTL-DISPLAY-RESTART                                   
01040                                    TO TRUE                                
01041          PERFORM DP013-0000-PROCESS-ABEND                                 
01042      END-IF.                                                              
01043 *                                                                         
01044  EJECT                                                                    
01045 *----------------------------------------------------------------*        
01046 * RECORD IN THE SELECTION TEMP STORAGE QUEUE THE FACT THAT THIS  *        
01047 * OBJECT WAS UDPATED.                                            *        
01048 *----------------------------------------------------------------*        
01049  5200-UPDATE-SELECTION-QUEUE.                                             
01050      PERFORM 9100-READ-SEL-QUEUE.                                         
01051      SET PE067-CAN (ASC-SEL-SUB) TO TRUE.                                 
01052      PERFORM 9200-REWRITE-LIST-QUEUE.                                     
01053 *                                                                         
01054  EJECT                                                                    
01055 *----------------------------------------------------------------*        
01056 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
01057 *----------------------------------------------------------------*        
01058                                                                           
01059  9100-READ-SEL-QUEUE.                                                     
01060      EXEC CICS                                                            
01061          READQ TS                                                         
01062              QUEUE (ASC-SEL-QUEUE-NAME)                                   
01063              INTO  (PE067-DOCUMENT-SEL-REC)                               
01064              ITEM  (ASC-SEL-ITEM)                                         
01065              RESP  (PV-CICS-RESPONSE)                                     
01066      END-EXEC.                                                            
01067                                                                           
01068      IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                           
01069          CONTINUE                                                         
01070      ELSE                                                                 
01071          SET DP013-LOGIC-ABEND                                            
01072              DP013-NO-ROLLBACK     TO TRUE                                
01073          MOVE '9100-READ-SEL-QUEUE'                                       
01074                                    TO DP013-PARAGRAPH                     
01075          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
01076                                    TO DP013-MESSAGE-TEXT (1)              
01077          PERFORM DP013-0000-PROCESS-ABEND                                 
01078      END-IF.                                                              
01079 *                                                                         
01080  EJECT                                                                    
01081 *----------------------------------------------------------------*        
01082 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
01083 *----------------------------------------------------------------*        
01084  9200-REWRITE-LIST-QUEUE.                                                 
01085      EXEC CICS                                                            
01086           WRITEQ TS                                                       
01087               QUEUE  (ASC-SEL-QUEUE-NAME)                                 
01088               FROM   (PE067-DOCUMENT-SEL-REC)                             
01089               ITEM   (ASC-SEL-ITEM)                                       
01090               RESP   (PV-CICS-RESPONSE)                                   
01091               REWRITE                                                     
01092      END-EXEC.                                                            
01093                                                                           
01094      IF  PV-CICS-RESPONSE NOT EQUAL DFHRESP(NORMAL)                       
01095          SET DP013-CICS-ABEND                                             
01096              DP013-NO-ROLLBACK     TO TRUE                                
01097          MOVE '9200-REWRITE-LIST-QUEUE'                                   
01098                                    TO DP013-PARAGRAPH                     
01099          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
01100                                    TO DP013-MESSAGE-TEXT (1)              
01101          PERFORM DP013-0000-PROCESS-ABEND                                 
01102      END-IF.                                                              
01103                                                                           
01104 *                                                                         
01105  EJECT                                                                    
01106 *----------------------------------------------------------------*        
01107 *    DATE PROCESSOR MODULE                                       *        
01108 *----------------------------------------------------------------*        
01109                                                                           
01110      COPY DPPD017.                                                        
01111 *                                                                         
01112  EJECT                                                                    
01113 *----------------------------------------------------------------*        
01114 *    ABEND PROCESSOR MODULE                                      *        
01115 *----------------------------------------------------------------*        
01117      COPY DPPD013.                                                        
