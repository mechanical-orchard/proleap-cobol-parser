      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
                                                                                
       PROGRAM-ID.    PEKUT014.                                                 
       AUTHOR.        AMY REILAND                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10/17/93.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  IN-STORE PRICE CHANGE HISTORY DOCUMENT DELETION EXTRACT                
      *                                                                         
      *  THIS PROGRAM WILL CREATE THE FILE WHICH WILL BE PASSED TO THE          
      *  DOCUMENT DELETION PROGRAM TO DELETE THE IN-STORE PRICE CHANGE          
      *  HISTORY DOCUMENTS.                                                     
      *                                                                         
      *  TABLES WHICH ARE ACCESSED ARE:-                                        
      *      TSTRPCH IN-STORE HISTORY PERMANENT PRICE CHANGE HEADER             
      *  RECORDS WRITTEN ARE;-                                                  
      *      PERD071 IN-STORE DOCUMENT HISTORY DELETE RECORD                    
      *****************************************************************         
      * 04/11/2002 - TODD C.MICHALEK - WR NO. 23591                             
      *  CHANGE MADE:                                                           
      *    -  CHANGE VARIABLE PC-TWO-YEARS VALUE 730                            
      *       TO PC-EIGHTEEN-MONTHS VALUE 548                                   
      *    -  CHANGE VARIABLE PV-DATE-TWO-YEARS-AGO                             
      *       TO PV-EIGHTEEN-MONTHS-AGO                                         
      *****************************************************************         
      * 03/25/05 - TODD C. MICHALEK -  WR NO. 27559                             
      *    - CHANGED ALL STORE NUMBER LENGTH REFERENCES FROM 3 TO 4             
      *      DIGITS IN LENGTH                                                   
      *    - REMOVED THE OPERCO_NBR FIELD FROM ALL IN-STORE PERM TABLE          
      *      CALLS                                                              
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
      *                                                                         
      *                                                                         
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT DOC-HIST-DEL-FILE                                             
               ASSIGN TO OUT01.                                                 
                                                                                
      *                                                                         
      *                                                                         
      *                                                                         
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
      *                                                                         
      *                                                                         
       FILE SECTION.                                                            
                                                                                
       FD  DOC-HIST-DEL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 20 CHARACTERS                                        
           DATA RECORD IS DOC-HIST-DEL-RECORD.                                  
                                                                                
       01  DOC-HIST-DEL-RECORD                PIC X(20).                        
                                                                                
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *****************************************************************         
      *    DOCUMENT DELETE FILE LAYOUT                                          
      *****************************************************************         
                                                                                
           COPY PERD071.                                                        
      *****************************************************************         
      *    PERM PRICE CHANGE CONSTANTS                                          
      *****************************************************************         
                                                                                
           COPY PEWS049.                                                        
      *****************************************************************         
      *    DATE ROUTINE COPY MEMBER                                             
      *****************************************************************         
                                                                                
           COPY DPWS500.                                                        
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
      *                                                                         
       01  ABEND-CODE                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           88 ABEND-4008-DATA-ERROR                        VALUE +4008.         
           88 ABEND-4013-DB2-ERROR                         VALUE +4013.         
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CALENDAR-PGM                PIC  X(08)   VALUE                
                                                           'DPKUT500'.          
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)   VALUE                
                                                           'PEKUT014'.          
           05  PC-EIGHTEEN-MONTHS             PIC  9(3)    VALUE 548.           
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-INSTR-DOCS-SW           PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-INSTR-HIST-DOCS               VALUE 'Y'.           
               88  PS-NOT-END-OF-INSTR-HIST-DOCS           VALUE 'N'.           
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CURRENT-DATE.                                                 
               10  PV-CURRENT-CC              PIC  9(02)   VALUE ZEROES.        
               10  PV-CURRENT-YY              PIC  9(02)   VALUE ZEROES.        
               10  PV-CURRENT-DASH-1          PIC  X(01)   VALUE SPACES.        
               10  PV-CURRENT-MM              PIC  9(02)   VALUE ZEROES.        
               10  PV-CURRENT-DASH-2          PIC  X(01)   VALUE SPACES.        
               10  PV-CURRENT-DD              PIC  9(02)   VALUE ZEROES.        
           05  PV-EIGHTEEN-MONTHS-AGO         PIC  X(10)   VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-YY               PIC  9(02)   VALUE ZEROES.        
               10  PV-TODAYS-MM               PIC  9(02)   VALUE ZEROES.        
               10  PV-TODAYS-DD               PIC  9(02)   VALUE ZEROES.        
           05  PV-TODAYS-DATE-GREG.                                             
               10  PV-TODAYS-MM-GREG          PIC  9(02)   VALUE ZEROES.        
               10  PV-TODAYS-DD-GREG          PIC  9(02)   VALUE ZEROES.        
               10  PV-TODAYS-YY-GREG          PIC  9(02)   VALUE ZEROES.        
      *                                                                         
       01  RC-ROWS-READ-CNTRS.                                                  
           05  RC-STRPCH-ROW-READ-CNTR        PIC S9(08)   VALUE +0             
                                                           COMP-3.              
       01  RW-RECORDS-WRITTEN-CNTRS.                                            
           05  RW-PERD071-RECS-WRITTEN-CNTR   PIC S9(08)   VALUE +0             
                                                           COMP-3.              
       01  DC-DISPLAY-CNTRS.                                                    
           05  DC-STRPCH-ROW-READ-CNTR        PIC Z(07)9   VALUE ZEROS.         
           05  DC-PERD071-RECS-WRITTEN-CNTR   PIC Z(07)9   VALUE ZEROS.         
                                                                                
      *                                                                         
      *****************************************************************         
      *    DB2 IN-STORE HISTORY PERMANENT PRICE CHANGE HEADER TABLE             
      *****************************************************************         
                                                                                
                                                                                
           EXEC SQL                                                             
               INCLUDE TSTRPCH                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
           EXEC SQL                                                             
             DECLARE HIST-DOC-CURSOR CURSOR FOR                                 
                 SELECT DEST_LOC_NBR                                            
                      , DOC_NBR                                                 
                   FROM TSTRPCH                                                 
                  WHERE (DOC_TYP_CDE = :PE049-STORE-DOC-TYPE-CDE                
                    AND (PREV_STATUS_CDE                                        
                                     = :PE049-IS-COMPLETED-STATUS-CDE           
                     AND EFF_DTE    <= :PV-EIGHTEEN-MONTHS-AGO)                 
                    OR  (PREV_STATUS_CDE                                        
                                     = :PE049-IS-CANCELLED-STATUS-CDE           
                     AND CAN_DTE    <= :PV-EIGHTEEN-MONTHS-AGO))                
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      *    DB2 COMMUNICATION AREA.                                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
       EJECT                                                                    
      *                                                                         
      *                                                                         
      *                                                                         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      ******************************************************************        
      *A100-MAIN-MODULE                                                *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      *  ==> PERFORMED FROM:                                           *        
      *                     HIGHEST LEVEL IN PROGRAM                   *        
      ******************************************************************        
       A100-MAIN-MODULE.                                                        
                                                                                
           PERFORM B100-PRE-PROCESSING.                                         
                                                                                
           PERFORM                                                              
               UNTIL PS-END-OF-INSTR-HIST-DOCS                                  
                                                                                
               PERFORM C200-WRITE-DELETE-REC                                    
               PERFORM C100-READ-HIST-PC                                        
                                                                                
           END-PERFORM.                                                         
                                                                                
           PERFORM F100-DISPLAY-CONTROL-INFO.                                   
                                                                                
           PERFORM B200-EOJ-PROCESSING.                                         
                                                                                
                                                                                
           STOP RUN.                                                            
      *                                                                         
      ******************************************************************        
      *B100-PRE-PROCESSING                                             *        
      *  ==> PROCESSES:                                                *        
      *    - OPENS FILE, OPENS CURSOR FOR IN-STORE HISTORY DOCUMENTS,  *        
      *      CONTROLS READING OF FIRST DOCUMENT, PERFORMS DATE         *        
      *      CALCULATION PARA.                                         *        
      *  ==> PERFORMED FROM: A100-MAIN-MODULE                          *        
      ******************************************************************        
       B100-PRE-PROCESSING.                                                     
                                                                                
           OPEN OUTPUT DOC-HIST-DEL-FILE.                                       
                                                                                
           PERFORM D100-GET-CALCULATE-DATES.                                    
      *                                                                         
           EXEC SQL                                                             
                OPEN HIST-DOC-CURSOR                                            
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    CONTINUE                                                    
               WHEN SQLWARN NOT EQUAL SPACES                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   MOVE 'B100-PRE-PROCESSING  '                                 
                                      TO PV-PARAGRAPH-NAME                      
                   MOVE 'OPEN CURSOR FOR TSTRPCH TABLE '                        
                                      TO PV-DB2-OPERATION-ATTEMPTED             
                   PERFORM Z900-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
           PERFORM C100-READ-HIST-PC.                                           
      *                                                                         
      ******************************************************************        
      *B200-EOJ-PROCESSING                                             *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS CLOSE OF FILES AND CURSOR HIST-DOC-CURSOR        *        
      *  ==> PERFORMED FROM: A100-MAIN-MODULE                          *        
      ******************************************************************        
       B200-EOJ-PROCESSING.                                                     
                                                                                
           EXEC SQL                                                             
                CLOSE HIST-DOC-CURSOR                                           
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    CONTINUE                                                    
               WHEN SQLWARN NOT EQUAL SPACES                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   MOVE 'B200-EOJ-PROCESSING'                                   
                                      TO PV-PARAGRAPH-NAME                      
                   MOVE 'CLOSE CURSOR FOR TSTRPCH TABLE'                        
                                      TO PV-DB2-OPERATION-ATTEMPTED             
                   PERFORM Z900-DB2-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
           CLOSE DOC-HIST-DEL-FILE.                                             
      *                                                                         
      ******************************************************************        
      *C100-READ-HIST-PC                                               *        
      *  ==> PROCESSES:                                                *        
      *    - READS THE INSTORE PRICE CHANGE HISTORY DOCUMENT TABLE     *        
      *  ==> PERFORMED FROM: A100-MAIN-MODULE                          *        
      *                      B100-PRE-PROCESSING                       *        
      ******************************************************************        
       C100-READ-HIST-PC.                                                       
                                                                                
           EXEC SQL                                                             
               FETCH HIST-DOC-CURSOR                                            
                INTO :STRPCH-DEST-LOC-NBR                                       
                   , :STRPCH-DOC-NBR                                            
           END-EXEC.                                                            
                                                                                
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   ADD 1              TO RC-STRPCH-ROW-READ-CNTR                
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-INSTR-HIST-DOCS TO TRUE                        
               WHEN SQLWARN NOT EQUAL SPACES                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   MOVE 'C100-READ-HIST-PC'                                     
                                      TO PV-PARAGRAPH-NAME                      
                   MOVE 'FETCH ROW FROM TSTRPCH TABLE'                          
                                      TO PV-DB2-OPERATION-ATTEMPTED             
                   PERFORM Z900-DB2-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      *C200-WRITE-DELETE-REC.                                          *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS WRITE OF DOCUMENT DELETE RECORD                  *        
      *  ==> PERFORMED FROM: A100-MAIN-MODULE                          *        
      ******************************************************************        
       C200-WRITE-DELETE-REC.                                                   
                                                                                
           INITIALIZE                 PE071-INSTR-DOC-HST-DELETE-REC.           
           MOVE STRPCH-DEST-LOC-NBR   TO PE071-INSTR-DEST-LOC-NBR.              
           MOVE STRPCH-DOC-NBR        TO PE071-INSTR-DOC-NBR.                   
                                                                                
           WRITE DOC-HIST-DEL-RECORD                                            
               FROM PE071-INSTR-DOC-HST-DELETE-REC.                             
                                                                                
           ADD 1 TO RW-PERD071-RECS-WRITTEN-CNTR.                               
      *                                                                         
      ******************************************************************        
      *D100-GET-CALCULATE-DATES                                        *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS SET UP OF DATES USED BY THE PROGRAM              *        
      *  ==> PERFORMED FROM: B100-PRE-PROCESSING                       *        
      ******************************************************************        
       D100-GET-CALCULATE-DATES.                                                
                                                                                
           ACCEPT PV-TODAYS-DATE                                                
               FROM DATE.                                                       
           MOVE PV-TODAYS-DATE        TO DPG51-SYSTEM-DATE.                     
                                                                                
           MOVE PV-TODAYS-YY          TO PV-TODAYS-YY-GREG.                     
           MOVE PV-TODAYS-MM          TO PV-TODAYS-MM-GREG.                     
           MOVE PV-TODAYS-DD          TO PV-TODAYS-DD-GREG.                     
                                                                                
           SET DPG51-ACTUAL-CALENDAR-ONLY                                       
               DPG51-DO-NOT-INCR-DECR-DATE                                      
               DPG52-LK-DTE-GREG      TO TRUE.                                  
           MOVE PV-TODAYS-DATE-GREG   TO DPG52-LK-DATE-INPUT.                   
           PERFORM E100-CALL-DATE-CALC-MODULE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT                                          
                                      TO PV-CURRENT-DATE.                       
           SET DPG51-ACTUAL-CALENDAR-ONLY                                       
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT   TO TRUE.                                  
           MOVE PV-CURRENT-DATE       TO DPG52-LK-DATE-INPUT.                   
           MOVE PC-EIGHTEEN-MONTHS    TO DPG51-INCR-DECR-DAYS-9.                
           PERFORM E100-CALL-DATE-CALC-MODULE.                                  
           MOVE DPG55-DB2-ISO-DATE-FMT                                          
                                      TO PV-EIGHTEEN-MONTHS-AGO.                
      *                                                                         
      ******************************************************************        
      *E100-CALL-DATE-CALC-MODULE                                      *        
      *  ==> PROCESSES:                                                *        
      *    - GETS DATE TWO YEARS AGO                                   *        
      *  ==> PERFORMED FROM: D100-GET-CALCULATE-DATES                  *        
      ******************************************************************        
                                                                                
       E100-CALL-DATE-CALC-MODULE.                                              
           CALL PC-CALENDAR-PGM USING DPG51 DPG52                               
                                      DPG53 DPG54                               
                                      DPG55 DPG56.                              
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
               WHEN DPG54-DATE-INVALID                                          
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PROGRAM ** = ' PC-CURRENT-PROGRAM-NAME           
                   DISPLAY '** BAD CALL TO THE CALENDAR MODULE DPKUT500'        
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   SET ABEND-4008-DATA-ERROR  TO TRUE                           
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      *F100-DISPLAY-CONTROL-INFO                                       *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      *  ==> PERFORMED FROM: A100-MAIN-MODULE                          *        
      *                      Z900-DB2-ABEND                            *        
      ******************************************************************        
                                                                                
       F100-DISPLAY-CONTROL-INFO.                                               
                                                                                
           MOVE RC-STRPCH-ROW-READ-CNTR                                         
                                      TO DC-STRPCH-ROW-READ-CNTR.               
                                                                                
           MOVE RW-PERD071-RECS-WRITTEN-CNTR                                    
                                      TO DC-PERD071-RECS-WRITTEN-CNTR.          
                                                                                
           DISPLAY '************** CONTROL DATA START **************'.          
           DISPLAY '************** CONTROL DATA START **************'.          
           DISPLAY 'DC-PERD071-RECS-WRITTEN-CNTR'.                              
           DISPLAY '===>' DISPLAY DC-PERD071-RECS-WRITTEN-CNTR.                 
           DISPLAY 'DC-STRPCH-ROW-READ-CNTR'.                                   
           DISPLAY '===>' DISPLAY DC-STRPCH-ROW-READ-CNTR.                      
           DISPLAY 'PV-CURRENT-DATE: '  PV-CURRENT-DATE.                        
           DISPLAY 'PV-EIGHTEEN-MONTHS-AGO: ' PV-EIGHTEEN-MONTHS-AGO.           
           DISPLAY '************** CONTROL DATA END **************'.            
           DISPLAY '************** CONTROL DATA END **************'.            
      *                                                                         
      ******************************************************************        
      *Z900-DB2-ABEND.                                                 *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      *  ==> PERFORMED FROM:                                           *        
      *                    BY ANY PARA THAT MAKES AN EXEC SQL CALL     *        
      *                    AND HAS AN UNEXPECTED RETURN CODE           *        
      ******************************************************************        
                                                                                
       Z900-DB2-ABEND.                                                          
                                                                                
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY '** PROGRAM **   = ' PC-CURRENT-PROGRAM-NAME.                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
           PERFORM F100-DISPLAY-CONTROL-INFO.                                   
                                                                                
      * ROUTINE TO DISPLAY ABEND IN READABLE FORMAT                             
           COPY DPPD004.                                                        
                                                                                
                                                                                
           SET ABEND-4013-DB2-ERROR TO TRUE.                                    
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
