       PROCEDURE DIVISION USING IS003-NETCOOL-ALERT.                            
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-SETUP.                                                  
           PERFORM 2000-PROCESS-ALERT.                                          
           PERFORM 3000-CLEANUP.                                                
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
       1000-SETUP.                                                              
                                                                                
           PERFORM 1001-EDIT-PARMS.                                             
           PERFORM 1002-GET-JOB-INFO.                                           
           PERFORM 1003-MQSERIES-CONNECT.                                       
                                                                                
                                                                                
      ************************************************************              
      * EDIT INPUT PARMS PASSED TO PROGRAM.                                     
      ************************************************************              
       1001-EDIT-PARMS.                                                         
                                                                                
           SET  IS003-OK TO TRUE.                                               
           MOVE SPACES   TO IS003-MESSAGE.                                      
                                                                                
           IF NOT IS003-VALID-VERSION                                           
               MOVE 'IS003-VERSION IS NOT SET TO A VALID VERSION NUMBER'        
                 TO IS003-MESSAGE                                               
               PERFORM 9000-ERROR.                                              
                                                                                
           IF IS003-SYSTEM-ID <= SPACES                                         
               MOVE 'IS003-SYSTEM-ID IS REQUIRED BUT WAS NOT FILLED.'           
                 TO IS003-MESSAGE                                               
               PERFORM 9000-ERROR.                                              
                                                                                
           EVALUATE IS003-TYPE                                                  
             WHEN 0001                                                          
               IF NOT IS003-PROBLEM-SEVERITY                                    
                   MOVE 'IS003-SEVERITY MUST BE 1 THRU 5 FOR A PROBLEM'         
                     TO IS003-MESSAGE                                           
                   PERFORM 9000-ERROR                                           
               END-IF                                                           
             WHEN 0002                                                          
               IF NOT IS003-ALL-CLEAR                                           
                   MOVE 'IS003-SEVERITY MUST BE ZERO FOR A RESOLUTION'          
                     TO IS003-MESSAGE                                           
                   PERFORM 9000-ERROR                                           
               END-IF                                                           
             WHEN OTHER                                                         
               MOVE 'IS003-TYPE MUST BE 1 OR 2.'                                
                 TO IS003-MESSAGE                                               
               PERFORM 9000-ERROR                                               
           END-EVALUATE.                                                        
                                                                                
           IF IS003-AUTOMATION-LABEL <= SPACES                                  
               MOVE 'IS003-AUTOMATION-LABEL IS A REQUIRED FIELD'                
                 TO IS003-MESSAGE                                               
               PERFORM 9000-ERROR.                                              
                                                                                
           IF IS003-SUMMARY <= SPACES                                           
               MOVE 'IS003-SUMMARY IS A REQUIRED FIELD'                         
                 TO IS003-MESSAGE                                               
               PERFORM 9000-ERROR.                                              
                                                                                
           MOVE ZERO TO PV-COLON-CNT.                                           
           INSPECT IS003-SUMMARY                                                
               TALLYING PV-COLON-CNT FOR ALL ':'.                               
           IF PV-COLON-CNT > ZERO                                               
               INSPECT IS003-SUMMARY REPLACING ALL ':' BY ' '                   
               MOVE 'IS003-SUMMARY HAD COLONS IN IT.  THEY ARE INVALID &        
      -             ' HAVE BEEN CHANGED TO SPACES.'                             
                 TO IS003-MESSAGE                                               
               SET IS003-INFO-MSG TO TRUE.                                      
                                                                                
                                                                                
       1003-MQSERIES-CONNECT.                                                   
                                                                                
           IF PV-TEST-ENVIRONMENT                                               
               MOVE 'QKST'   TO PV-QM-NAME                                      
           ELSE                                                                 
               MOVE 'QKSP'   TO PV-QM-NAME.                                     
                                                                                
           CALL WS-MQCONN USING PV-QM-NAME                                      
                                PV-HCONN                                        
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
                                                                                
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE                      
      *    AND EXIT                                                             
                                                                                
           MOVE PV-REASON TO PV-CON-REASON.                                     
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
               STRING 'CONNECT (' PV-QM-NAME(1:4) ')'                           
                 DELIMITED BY SIZE INTO PV-ACTION                               
               PERFORM 9001-MQSERIES-ERROR.                                     
                                                                                
                                                                                
       2000-PROCESS-ALERT.                                                      
                                                                                
      * INITIALIZE SO THAT ALL FILLER FIELDS ARE ":".                           
           MOVE ALL ':' TO ALERT-LINE.                                          
           INITIALIZE      ALERT-LINE.                                          
                                                                                
           STRING 'ISKUT003V'                                                   
                  IS003-VERSION  DELIMITED BY SIZE                              
                                     INTO ALERT-VERSION.                        
           MOVE 'MQSERIES'             TO ALERT-METHOD.                         
           MOVE IS003-SYSTEM-ID        TO ALERT-SYSTEM-ID.                      
           MOVE IS003-TYPE             TO ALERT-TYPE.                           
           MOVE IS003-SEVERITY         TO ALERT-SEVERITY.                       
           MOVE IS003-AUTOMATION-LABEL TO ALERT-AUTOMATION-LABEL.               
           MOVE IS003-SUMMARY          TO ALERT-SUMMARY.                        
           IF PV-JOB-PROC > SPACES                                              
               STRING PV-JOB-NAME                                               
                  '.' PV-JOB-PROC                                               
                  '.' PV-JOB-STEP DELIMITED BY SPACE                            
                                     INTO ALERT-LABEL                           
           ELSE                                                                 
               STRING PV-JOB-NAME                                               
                  '.' PV-JOB-STEP DELIMITED BY SPACE                            
                                     INTO ALERT-LABEL.                          
                                                                                
           PERFORM 2100-MQSERIES-PUT1.                                          
                                                                                
                                                                                
       2100-MQSERIES-PUT1.                                                      
                                                                                
           IF PV-TEST-ENVIRONMENT                                               
               MOVE 'MQXI01T'         TO MQOD-OBJECTQMGRNAME                    
           ELSE                                                                 
               MOVE 'MQXI01P'         TO MQOD-OBJECTQMGRNAME.                   
           MOVE 'KM.NETCOOL.ALERT.Q'  TO MQOD-OBJECTNAME.                       
           MOVE MQMI-NONE             TO MQMD-MSGID.                            
           MOVE MQCI-NONE             TO MQMD-CORRELID.                         
           MOVE MQFMT-STRING          TO MQMD-FORMAT.                           
           IF PV-TEST-ENVIRONMENT                                               
               MOVE  3000             TO MQMD-EXPIRY                            
           ELSE                                                                 
               MOVE 18000             TO MQMD-EXPIRY.                           
           MOVE 332                   TO PV-MSGLENGTH.                          
           MOVE ALERT-LINE            TO PV-MSGBUFFER.                          
           MOVE MQPER-PERSISTENT      TO MQMD-PERSISTENCE.                      
           COMPUTE MQPMO-OPTIONS       = MQPMO-SYNCPOINT.                       
                                                                                
           CALL WS-MQPUT1 USING PV-HCONN                                        
                                MQM-OBJECT-DESCRIPTOR                           
                                MQM-MESSAGE-DESCRIPTOR                          
                                MQM-PUT-MESSAGE-OPTIONS                         
                                PV-MSGLENGTH                                    
                                PV-MSGBUFFER                                    
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
      *                                                                         
      *    IF PUT FAILED THEN DISPLAY ERROR MESSAGE                             
      *                                                                         
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
               STRING 'PUT1 TO ' MQOD-OBJECTQMGRNAME                            
                 DELIMITED BY SIZE INTO PV-ACTION                               
               PERFORM 9001-MQSERIES-ERROR.                                     
                                                                                
                                                                                
       3000-CLEANUP.                                                            
                                                                                
      *                                                                         
      *    DISCONNECT FROM THE QUEUE MANAGER                                    
      *                                                                         
           IF PV-CON-REASON NOT = MQRC-ALREADY-CONNECTED                        
               CALL WS-MQDISC USING PV-HCONN                                    
                                    PV-COMPLETION-CODE                          
                                    PV-REASON                                   
               IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                       
                   MOVE 'DISCONNECT' TO PV-ACTION                               
                   PERFORM 9001-MQSERIES-ERROR.                                 
                                                                                
                                                                                
       9000-ERROR.                                                              
                                                                                
           SET IS003-ERROR TO TRUE.                                             
           GOBACK.                                                              
