       ID  DIVISION.                                                    01/25/95
00002  PROGRAM-ID.         INKUT169.                                    INKUT169
00003  AUTHOR.             SCOTT SWESSEL.                                       
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                     INKUT169
00005  DATE-WRITTEN.       09/98.                                       INKUT169
00006  DATE-COMPILED.                                                   INKUT169
00007                                                                   INKUT169
00008 ******************************************************************INKUT169
00009 *                                                                *INKUT169
00010 *    READ THE SORTED VALID INVENTORY DATA FROM INK300, DETERMINE *INKUT169
00011 *    THE TIMESTAMP TO BE ASSIGNED TO AN INVENTORIED ITEM, AND    *INKUT169
00011 *    WRITE A TIMESTAMP SEUENTIAL FILE.                           *INKUT169
00012 *                                                                *INKUT169
      ******************** HISTORY OF CHANGES **************************        
      *                                                                *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * 22000    02-28-2001  CHANGE TO CREATE CONTROL CARDS FOR IDCAMS *        
      *                      STEP THAT WILL COPY THE ITEM TIMESTAMP    *        
      *                      RECORDS TO THE APPROPRIATE DATED FILE.    *        
      *                      DATE CONTROL CARD NOW ACCEPT AN OPTIONAL  *        
      *                      PARAMETER AFTER THE DATE FOR DATASET NAME *        
      *                      PREFIX FOR THE ABOVE CONTROL CARDS.       *        
      *                      CHANGE TO ABEND WHERE IT SHOULD ABEND.    *        
      *                      PREVIOUSLY, IT JUST SILENTLY WRITES       *        
      *                      MESSAGES TO SYSOUT.  - V. LEE YANG        *        
      *                                                                *        
00013 ******************************************************************INKUT169
W21732* 11/18/2002        CHANGED BY J.S.NAVIS RAJA,IGSI  WR# W21732   *        
W21732* CHANGED CURRENT DEPT/CLASS/SEQ NUMBER TO NON-INTELLIGENT SKU.  *        
W21732******************************************************************        
W26600*                                                                *        
W26600*  W26600  06/  /2004                                            *        
W26600*                      M JOHNSON                                 *        
W26600*  STORE NUMBER EXPANSION                                        *        
W26600*                                                                *        
P28545* W28545 03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
P28545*                               (TINCNTL/TINVPAR MODIFICATIONS)           
W26600******************************************************************        
W33304* CHANGED 08/20/2008    BY THERESE RAMSEYER WR#  W33304                   
W33304*                       HILO - HOLD INVENTORY LEDGER OPEN                 
W33304*                     --> RECOMPILE TO PICK UP CHANGES IN                 
W33304*                         TINCNTL & TINVPAR                               
W33304*                     --> ADD 'NON DELETED LOCATION LOGIC' FILTER         
W33304*                         "A.LOC_INV_STAT_CDE = 'IN'" IN THE              
W33304*                         WHERE CLAUSE OF 2300-GET-ACTL-INV-DTE           
W33304*                     --> ADD FILTER                                      
W33304*                         "A.ACTL_FIN_BK_DTE = '9999-09-09'"  TO          
W33304*                         WHERE CLAUSE OF 2300-GET-ACTL-INV-DTE           
W33304*                     --> CODED A PERFORM INSTEAD OF A GO TO IN           
W33304*                         PARA 2300-                                      
W33304*                     --> CORRECTED PROGRAM NAME IN THE ABEND             
W33304*                         ROUTINE & 9050-SQL-WARNING                      
W33304*                     --> DISPLAY IN009-STORE-NUMBER IN 2300-             
W33304*                         DURING AN ABEND                                 
W33304*                     --> CHANGED IN MAIN LINE :                          
W33304*                         IF PV-STR-SKIP > 0 , TO DISPLAY                 
W33304*                         MESSAGE BUT REMOVED 4003 USER ABEND             
W33304*                         CODE SO THE PROGRAM KEEPS PROCESSING            
W33304*                         AND DOES NOT ABEND WHEN A NON ACTV              
W33304*                         STORE IS ENCOUNTERED.                           
W33304*                     --> REMOVED 'MAX' FROM SELECT IN PARA               
W33304*                         2300-GET-ACTL-INV-DATE BECAUSE THIS             
W33304*                         WOULD ONLY BE RETRIEVING A SINGLE ROW           
W33304*                         ANYWAY.                                         
W33304*                     --> EXPANDED TIMED-INVENTORY-OUTPUT-REC             
W33304*                         FROM 90 TO 100 BYTES TO ACCOMMODATE             
W33304*                         THE EXPANSION OF INRD019                        
W33304*****************************************************************         
SR3403* CHANGED 03/04/2012    BY ANUJ GUPTA       WR#  WR3403                   
SR3403*                       LARGE RETAIL PROJECT                              
SR3403*****************************************************************         
00014                                                                   INKUT169
00015  ENVIRONMENT DIVISION.                                            INKUT169
00016  CONFIGURATION SECTION.                                           INKUT169
00017  INPUT-OUTPUT SECTION.                                            INKUT169
00018  FILE-CONTROL.                                                    INKUT169
00019                                                                   INKUT169
00020      SELECT VALID-INVENTORY-INPUT                                 INKUT169
00021          ASSIGN TO INP01.                                         INKUT169
00022                                                                   INKUT169
           SELECT GAP-CONTROL-CARD-FILE                                         
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT PRC-CONTROL-CARD-FILE                                         
               ASSIGN TO INP03.                                                 
                                                                                
00023      SELECT TIMED-INVENTORY-OUTPUT                                INKUT169
00024          ASSIGN TO OUT01.                                         INKUT169
00025                                                                   INKUT169
22000      SELECT MERGED-CONTROL-CARDS-FILE                             INKUT169
22000          ASSIGN TO OUT02.                                         INKUT169
22000                                                                   INKUT169
22000      SELECT COPIED-CONTROL-CARDS-FILE                             INKUT169
22000          ASSIGN TO OUT03.                                         INKUT169
22000                                                                   INKUT169
00026  DATA DIVISION.                                                   INKUT169
00027  FILE SECTION.                                                    INKUT169
00028                                                                   INKUT169
00029  FD  VALID-INVENTORY-INPUT                                        INKUT169
00030      LABEL RECORDS ARE STANDARD                                   INKUT169
00031      RECORDING MODE IS F                                          INKUT169
00032      BLOCK CONTAINS 0 RECORDS.                                    INKUT169
SR3403 01  VALID-INVENTORY-INPUT-REC     PIC X(100).                    INKUT169
00034                                                                   INKUT169
00034  FD  GAP-CONTROL-CARD-FILE                                        INKUT169
00035      LABEL RECORDS ARE STANDARD                                   INKUT169
00036      RECORDING MODE IS F                                          INKUT169
00037      BLOCK CONTAINS 0 RECORDS.                                    INKUT169
00038  01  GAP-CONTROL-CARD-REC         PIC X(80).                      INKUT169
00039                                                                   INKUT169
00034  FD  PRC-CONTROL-CARD-FILE                                                
00035      LABEL RECORDS ARE STANDARD                                   INKUT169
00036      RECORDING MODE IS F                                          INKUT169
00037      BLOCK CONTAINS 0 RECORDS.                                    INKUT169
00038  01  PRC-CONTROL-CARD-REC         PIC X(80).                              
00039                                                                   INKUT169
00035  FD  TIMED-INVENTORY-OUTPUT                                       INKUT169
00036      LABEL RECORDS ARE STANDARD                                   INKUT169
00037      RECORDING MODE IS F                                          INKUT169
00038      BLOCK CONTAINS 0 RECORDS.                                    INKUT169
W33304 01  TIMED-INVENTORY-OUTPUT-REC    PIC X(100).                            
00040                                                                   INKUT169
22000  FD  MERGED-CONTROL-CARDS-FILE                                    INKUT169
22000      LABEL RECORDS ARE STANDARD                                   INKUT169
22000      RECORDING MODE IS F                                          INKUT169
22000      BLOCK CONTAINS 0 RECORDS.                                    INKUT169
22000  01  MERGED-CONTROL-CARDS-RECORD   PIC X(080).                            
22000                                                                   INKUT169
22000  FD  COPIED-CONTROL-CARDS-FILE                                    INKUT169
22000      LABEL RECORDS ARE STANDARD                                   INKUT169
22000      RECORDING MODE IS F                                          INKUT169
22000      BLOCK CONTAINS 0 RECORDS.                                    INKUT169
22000  01  COPIED-CONTROL-CARDS-RECORD   PIC X(080).                            
22000                                                                   INKUT169
00041  WORKING-STORAGE SECTION.                                         INKUT169
00042                                                                   INKUT169
00054  01  CC-GAP-CNTL-REC.                                                     
00055      05  CC-GAP                  PIC X(03).                       INKUT169
00057      05  FILLER                  PIC X(01).                       INKUT169
00058      05  CC-GAP-TIME.                                                     
00059          10  CC-GAP-HOUR         PIC X(02).                       INKUT169
00060          10  FILLER              PIC X(01).                       INKUT169
00061          10  CC-GAP-MIN          PIC X(02).                       INKUT169
           05  FILLER                  PIC X(01).                               
           05  CC-GAP-QTY              PIC 9(03).                               
00073      05  FILLER                  PIC X(71).                       INKUT169
00074                                                                   INKUT169
00054  01  CC-PRC-CNTL-REC.                                                     
22000      05  CC-PRC-YEAR             PIC X(04).                       INKUT169
22000      05  FILLER REDEFINES CC-PRC-YEAR.                                    
22000          10  CC-PRC-CC           PIC X(02).                               
22000          10  CC-PRC-YY           PIC X(02).                               
00057      05  FILLER                  PIC X(01).                       INKUT169
00058      05  CC-PRC-MONTH            PIC X(02).                               
00058      05  FILLER                  PIC X(01).                               
00058      05  CC-PRC-DAY              PIC X(02).                               
22000      05  FILLER                  PIC X(02).                       INKUT169
22000      05  OPT-PRC-DSN-PREF        PIC X(30).                       INKUT169
22000      05  FILLER                  PIC X(38).                       INKUT169
00074                                                                   INKUT169
22000  01  CNTL-CARDS-RECORD-AREA.                                              
22000      05  CNTL-CARDS-REPRO-REC        PIC X(10)       VALUE                
22000          '  REPRO - '.                                                    
22000      05  CNTL-CARDS-SOURCE-COMMENT.                                       
22000          10  FILLER                  PIC X(37)       VALUE                
22000              '  /*  THIS IDCAMS CARD IS CREATED IN '.                     
22000          10  CNTL-CARDS-SOURCE-PGM   PIC X(08)       VALUE                
22000              'INKUT169'.                                                  
22000          10  FILLER                  PIC X(04)       VALUE ' ON '.        
22000          10  CNTL-CARDS-SOURCE-DATE.                                      
22000              15  CNTL-CARDS-SRCE-MM  PIC X(02)       VALUE SPACES.        
22000              15  FILLER              PIC X(01)       VALUE '/'.           
22000              15  CNTL-CARDS-SRCE-DD  PIC X(02)       VALUE SPACES.        
22000              15  FILLER              PIC X(01)       VALUE '/'.           
22000              15  CNTL-CARDS-SRCE-YY  PIC X(02)       VALUE SPACES.        
22000          10  FILLER                  PIC X(01)       VALUE '-'.           
22000          10  CNTL-CARDS-SOURCE-TIME  PIC X(08)       VALUE SPACES.        
22000          10  FILLER                  PIC X(04)       VALUE '  */'.        
22000      05  CNTL-CARDS-MERGE-COMMENT-1  PIC X(53)       VALUE                
22000          '  /*  COPIES FROM HARD-CODED FILE TO SPECIFIED DD  */'.         
22000      05  CNTL-CARDS-MERGE-OUTPUT     PIC X(16)       VALUE                
22000          '  OUTFILE(OUT01)'.                                              
22000      05  CNTL-CARDS-COPY-COMMENT-1   PIC X(53)       VALUE                
22000          '  /*  COPIES FROM SPECIFIED DD TO HARD-CODED FILE  */'.         
22000      05  CNTL-CARDS-COPY-INPUT       PIC X(18)       VALUE                
22000          '  INFILE(INP01) - '.                                            
22000      05  CNTL-CARDS-DSN-BUILD        PIC X(80)       VALUE SPACES.        
PROD       05  CNTL-CARDS-DSN-PREF         PIC X(30)       VALUE                
PROD           'PKMO.IN.ITEM.TIMESTMP.DT'.                                      
TEST  *    05  CNTL-CARDS-DSN-PREF         PIC X(33)       VALUE                
TEST  *        'TKMA.IN.ITEM.TIMESTMP.DT'.                                      
22000      05  CNTL-CARDS-DSN-DATE.                                             
22000          10  CNTL-CARDS-DSN-DATE-YY  PIC X(02)       VALUE SPACES.        
22000          10  CNTL-CARDS-DSN-DATE-MM  PIC X(02)       VALUE SPACES.        
22000          10  CNTL-CARDS-DSN-DATE-DD  PIC X(02)       VALUE SPACES.        
22000                                                                           
00043  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.INKUT169
00044      88  ABEND-4003                          VALUE +4003.         INKUT169
00045      88  ABEND-4013                          VALUE +4013.         INKUT169
00046                                                                   INKUT169
W33304 01  PC-PROGRAM-CONSTANTS.                                        INKUT169
W33304     05  PC-DEFAULT-NINES        PIC  X(10) VALUE '9999-09-09'.   INKUT169
W33304     05  PC-INITIAL-STATUS       PIC  X(02) VALUE 'IN'.           INKUT169
W33304                                                                          
00047  01  PV-PROGRAM-VARIABLES.                                        INKUT169
22000      05  PV-STR-SKIP             PIC S9(07)   COMP-3 VALUE 0.     INKUT169
22000      05  PV-STR-IN               PIC S9(07)   COMP-3 VALUE 0.     INKUT169
00048      05  PV-REC-IN               PIC S9(07)   COMP-3 VALUE 0.     INKUT169
00049      05  PV-REC-OUT              PIC S9(07)   COMP-3 VALUE 0.     INKUT169
00051      05  PV-END-OF-INPUT-SW      PIC  X(01)          VALUE 'N'.   INKUT169
00052          88  PV-END-OF-INPUT                         VALUE 'Y'.   INKUT169
00051      05  PV-END-OF-GAP-SW        PIC  X(01)          VALUE 'N'.   INKUT169
00052          88  PV-END-OF-GAP                           VALUE 'Y'.   INKUT169
00051      05  PV-END-OF-PROC-SW       PIC  X(01)          VALUE 'N'.   INKUT169
00052          88  PV-END-OF-PROC-DTE                      VALUE 'Y'.   INKUT169
22000      05  PV-TODAYS-DATE.                                                  
22000          10  PV-TODAYS-DATE-YY   PIC 9(02)    VALUE ZEROES.               
22000          10  PV-TODAYS-DATE-MM   PIC 9(02)    VALUE ZEROES.               
22000          10  PV-TODAYS-DATE-DD   PIC 9(02)    VALUE ZEROES.               
           05  PV-TEMP-DATE            PIC 9(04)    VALUE ZEROES.               
           05  PV-HOLD-DATE            PIC 9(04)    VALUE ZEROES.               
W26600     05  PV-HOLD-STORE           PIC X(04)    VALUE SPACES.               
           05  PV-HOLD-AREA            PIC 9(06)    VALUE ZEROES.               
W21732     05  PV-HOLD-DEPT-CLASS.                                              
W21732         10  PV-HOLD-DEPT        PIC 9(04)    VALUE ZEROES.               
W21732         10  PV-HOLD-MAJ-CLASS   PIC 9(04)    VALUE ZEROES.               
W21732         10  PV-HOLD-SUB-CLASS   PIC 9(04)    VALUE ZEROES.               
           05  PV-HOLD-SKU             PIC 9(08)    VALUE ZEROES.               
00066      05  PV-HOLD-PRICE           PIC 9(05)V99 VALUE ZEROES.               
00066      05  PV-HOLD-SCAN-DATE       PIC X(04)    VALUE SPACES.               
           05  PV-TEMP-TIMESTAMP       PIC 9(06)    VALUE ZEROES.               
W26600     05  PV-HOLD-TIMESTAMP       PIC 9(06)    VALUE ZEROES.               
W26600     05  FILLER REDEFINES PV-HOLD-TIMESTAMP.                              
W26600         07  PV-HOLD-HOUR        PIC 9(02).                               
W26600         07  PV-HOLD-MIN         PIC 9(02).                               
W26600         07  PV-HOLD-SEC         PIC 9(02).                               
W26600     05  PV-CURR-TIMESTAMP       PIC 9(06)    VALUE ZEROES.               
W26600     05  FILLER REDEFINES PV-CURR-TIMESTAMP.                              
W26600         07  PV-CURR-HOUR        PIC 9(02).                               
W26600         07  PV-CURR-MIN         PIC 9(02).                               
W26600         07  PV-CURR-SEC         PIC 9(02).                               
           05  PV-LAPSE-TIME.                                                   
               07  PV-LAPSE-HOUR       PIC 9(02)    VALUE ZEROES.               
               07  PV-LAPSE-MIN        PIC 9(02)    VALUE ZEROES.               
           05  PV-GAP-TIME.                                                     
               07  PV-GAP-HOUR         PIC 9(02)    VALUE ZEROES.               
               07  PV-GAP-MIN          PIC 9(02)    VALUE ZEROES.               
           05  PV-PRC-DATE.                                                     
               07  PV-PRC-MONTH        PIC 9(02)    VALUE ZEROES.               
               07  PV-PRC-DAY          PIC 9(02)    VALUE ZEROES.               
           05  PV-ACTL-DATE.                                                    
               07  PV-ACTL-MONTH       PIC 9(02)    VALUE ZEROES.               
               07  PV-ACTL-DAY         PIC 9(02)    VALUE ZEROES.               
           05  PV-ACTL-ENTIRE-DATE.                                             
               07  PV-ACTL-ENTIRE-YEAR  PIC 9(02)    VALUE ZEROES.              
               07  PV-ACTL-ENTIRE-MONTH PIC 9(02)    VALUE ZEROES.              
               07  PV-ACTL-ENTIRE-DAY   PIC 9(02)    VALUE ZEROES.              
           05  PV-ACTL-INV             PIC X(10)    VALUE SPACES.               
           05  PV-ACTL-INV-DTE REDEFINES PV-ACTL-INV.                           
               10  FILLER                 PIC  X(02).                           
               10  PV-ACTL-INV-DTE-YY     PIC  X(02).                           
               10  FILLER                 PIC  X.                               
               10  PV-ACTL-INV-DTE-MM     PIC  X(02).                           
               10  FILLER                 PIC  X.                               
               10  PV-ACTL-INV-DTE-DD     PIC  X(02).                           
           05  PV-PREV-TAB-QUANT       PIC 9(09)    VALUE ZEROES.               
           05  PV-CURR-TAB-QUANT       PIC 9(09)    VALUE ZEROES.               
           05  PV-HOLD-QUANT           PIC 9(09)    VALUE ZEROES.               
           05  PV-GAP-FLAG             PIC X(01)    VALUE 'Y'.                  
00064                                                                           
00065      05  PV-ITEM-REC.                                                     
W26600         10  PV-STORE-NUM        PIC  S9(04)         VALUE ZEROS          
W26600                                                     COMP.                
W26600         10  PV-SKU-NUM          PIC  X(08)          VALUE SPACE.         
W26600         10  PV-ACTL-INV-DATE    PIC  9(06)          VALUE ZEROS.         
W26600         10  PV-AREA-NUM         PIC  9(06)          VALUE ZEROS.         
W26600         10  PV-SCAN-DATE        PIC  9(04)          VALUE ZEROS          
W26600                                                     COMP-3.              
W26600         10  PV-TIME-STAMP       PIC  9(06)          VALUE ZEROS          
W26600                                                     COMP-3.              
W26600         10  PV-TOTAL-QUANT      PIC  9(06)          VALUE ZEROS          
W26600                                                     COMP-3.              
W26600         10  PV-UNIT-PRICE       PIC  9(05)V99       VALUE ZEROS  INKUT169
W26600                                                     COMP-3.              
W26600         10  FILLER              PIC  X(30)          VALUE SPACE. INKUT169
W26600         10  PV-DEPT-NUM         PIC  X(04)          VALUE SPACE.         
W26600         10  PV-CLASS-NUM        PIC  X(04)          VALUE SPACE.         
W33304         10  FILLER              PIC  X(25)          VALUE SPACE. INKUT169
00077                                                                   INKUT169
00130                                                                   INKUT169
00135 /                                                                 INKUT169
00137                                                                   INKUT169
00008 *----------------------------------------------------------------*INKUT169
00009 *                                                                *INKUT169
00010 *    COPY BOOK FOR INPUT FILE PKMT.IN.SRT.VALID.RECORDS.         *INKUT169
00012 *                                                                *INKUT169
00013 *----------------------------------------------------------------*INKUT169
00014                                                                   INKUT169
00138      COPY INWS009.                                                INKUT169
00139                                                                   INKUT169
00008 *----------------------------------------------------------------*INKUT169
00009 *                                                                *INKUT169
00010 *    COPY BOOK FOR OUTPUT FILE PKMT.IN.TIME.STAMP.EXTRACT.       *        
00012 *                                                                *INKUT169
00013 *----------------------------------------------------------------*INKUT169
00014                                                                   INKUT169
00136      COPY INRD019.                                                INKUT169
      *---------------------------------------------------------------*         
      *  DCLGEN FOR THE DB2 INVENTORY PARAMETERS TABLE                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
P28545*---------------------------------------------------------------*         
P28545*  DCLGEN FOR THE DB2 INVENTORY CONTROL MASTER TABLE                      
P28545*---------------------------------------------------------------*         
P28545     EXEC SQL                                                             
P28545         INCLUDE TINCNTL                                                  
P28545     END-EXEC.                                                            
P28545                                                                          
      *----------------------------------------------------------------*        
      *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING          *        
      *  SQL CODES FOR DISPLAY                                         *        
      *----------------------------------------------------------------*        
           COPY SQLCA2.                                                         
                                                                                
      *----------------------------------------------------------------*        
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA               *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                                
00140 /                                                                 INKUT169
00141  LINKAGE SECTION.                                                 INKUT169
00142                                                                   INKUT169
00143  PROCEDURE DIVISION.                                              INKUT169
00144                                                                   INKUT169
00145  1000-MAIN-MODULE.                                                INKUT169
00146                                                                   INKUT169
00147      PERFORM 1100-HOUSEKEEPING.                                   INKUT169
00148                                                                   INKUT169
00149      PERFORM 2000-PROCESS-INPUT                                           
22000          UNTIL PV-END-OF-INPUT.                                           
22000      IF PV-STR-SKIP > 0                                                   
22000          DISPLAY '**********************************'             INKUT169
22000          DISPLAY '*                                *'             INKUT169
W33304         DISPLAY '* INKUT169 STORE SKIPPED MESSAGE *'             INKUT169
22000          DISPLAY '*        --------------          *'             INKUT169
22000          DISPLAY '*                                *'             INKUT169
22000          DISPLAY '*  ' PV-STR-SKIP                                INKUT169
22000                            ' OF '                                 INKUT169
22000                               PV-STR-IN                           INKUT169
22000                                       ' STORE(S)   *'             INKUT169
22000          DISPLAY '*  SKIPPED BECAUSE OF CONTROL    *'             INKUT169
22000          DISPLAY '*  DATE SEE PREVIOUS MESSAGE(S). *'             INKUT169
22000          DISPLAY '**********************************'             INKUT169
22000      END-IF.                                                              
22000      IF PV-REC-IN > 0                                                     
               IF PV-GAP-FLAG = 'Y'                                             
                   PERFORM 2055-DETERMINE-CUTOFF                                
               END-IF                                                           
               PERFORM 2200-WRITE-RECORD-OUT                                    
22000          PERFORM 2500-CREATE-WRITE-CNTL-CARDS                             
22000      END-IF.                                                              
22000                                                                   INKUT169
00152      PERFORM 3000-EOJ.                                            INKUT169
00153                                                                   INKUT169
00154      GOBACK.                                                      INKUT169
00155 /                                                                 INKUT169
00156 *----------------------------------------------------------------*INKUT169
00157 *                                                                *INKUT169
00158 *    OPEN THE FILES, GET THE FIRST RECORD AND SAVE SCAN DATE,    *INKUT169
00158 *    TIMESTAMP, AND SKU.                                         *INKUT169
00159 *                                                                *INKUT169
00160 *----------------------------------------------------------------*INKUT169
00161                                                                   INKUT169
00162  1100-HOUSEKEEPING.                                               INKUT169
00163                                                                   INKUT169
00164      OPEN  INPUT VALID-INVENTORY-INPUT                            INKUT169
                       GAP-CONTROL-CARD-FILE                                    
                       PRC-CONTROL-CARD-FILE                                    
22000           OUTPUT TIMED-INVENTORY-OUTPUT                           INKUT169
22000                  MERGED-CONTROL-CARDS-FILE                        INKUT169
22000                  COPIED-CONTROL-CARDS-FILE.                       INKUT169
00166                                                                   INKUT169
00167      READ GAP-CONTROL-CARD-FILE INTO CC-GAP-CNTL-REC                      
                AT END                                                          
00168           DISPLAY '**********************************'            INKUT169
00169           DISPLAY '*                                *'            INKUT169
00170           DISPLAY '*        INKUT169 ABEND          *'            INKUT169
00171           DISPLAY '*        --------------          *'            INKUT169
00172           DISPLAY '*                                *'            INKUT169
00173           DISPLAY '*  GAP DATE CONTROL CARD MISSING *'            INKUT169
00174           DISPLAY '*                                *'            INKUT169
00175           DISPLAY '**********************************'            INKUT169
22000           MOVE +4003 TO ABEND-CODE                                        
00176           CALL 'ILBOABN0'  USING ABEND-CODE                       INKUT169
00177      END-READ.                                                    INKUT169
00178                                                                   INKUT169
00179      DISPLAY '*'.                                                 INKUT169
22000      DISPLAY '* CONTROL INFORMATION:'.                            INKUT169
22000      DISPLAY '* ' CC-GAP-CNTL-REC.                                        
22000      DISPLAY '* ....5...10...15...20...25...30...35...40...45'.   INKUT169
00183      DISPLAY '*'.                                                 INKUT169
           MOVE CC-GAP-HOUR TO PV-GAP-HOUR.                                     
           MOVE CC-GAP-MIN  TO PV-GAP-MIN.                                      
00184                                                                   INKUT169
00167      READ PRC-CONTROL-CARD-FILE INTO CC-PRC-CNTL-REC AT END               
00168           DISPLAY '**********************************'            INKUT169
00169           DISPLAY '*                                *'            INKUT169
00170           DISPLAY '*        INKUT169 ABEND          *'            INKUT169
00171           DISPLAY '*        --------------          *'            INKUT169
00172           DISPLAY '*                                *'            INKUT169
00173           DISPLAY '*  PRC DATE CONTROL CARD MISSING *'            INKUT169
00174           DISPLAY '*                                *'            INKUT169
00175           DISPLAY '**********************************'            INKUT169
22000           MOVE +4003 TO ABEND-CODE                                        
00176           CALL 'ILBOABN0'  USING ABEND-CODE                       INKUT169
00177      END-READ.                                                    INKUT169
00178                                                                   INKUT169
00179      DISPLAY '*'.                                                 INKUT169
22000      DISPLAY '* CONTROL INFORMATION:'.                            INKUT169
22000      DISPLAY '* ' CC-PRC-CNTL-REC.                                        
22000      DISPLAY '* ....5...10...15...20...25...30...35...40...45'.   INKUT169
00183      DISPLAY '*'.                                                 INKUT169
           MOVE CC-PRC-MONTH TO PV-PRC-MONTH.                                   
           MOVE CC-PRC-DAY   TO PV-PRC-DAY.                                     
00184                                                                   INKUT169
00211      PERFORM 2100-READ-INPUT UNTIL                                INKUT169
                  (PV-ACTL-DATE = PV-PRC-DATE) OR                               
                  (PV-END-OF-INPUT)                                             
22000      IF PV-END-OF-INPUT AND PV-REC-IN > 0                                 
22000          DISPLAY '**************************************'         INKUT169
22000          DISPLAY '*                                    *'         INKUT169
22000          DISPLAY '*          INKUT169 ABEND            *'         INKUT169
22000          DISPLAY '*          --------------            *'         INKUT169
22000          DISPLAY '*                                    *'         INKUT169
22000          DISPLAY '*  NO STORES FOUND WITH ACTL INV     *'         INKUT169
22000          DISPLAY '*  DATE EQUAL TO CONTROL (TODAYS)    *'         INKUT169
22000          DISPLAY '*  DATE OF '                                    INKUT169
22000                     PV-PRC-DATE '                      *'         INKUT169
22000          DISPLAY '**************************************'         INKUT169
22000          MOVE +4003 TO ABEND-CODE                                         
22000          CALL 'ILBOABN0'  USING ABEND-CODE                        INKUT169
           END-IF.                                                              
00212      MOVE IN009-SCAN-DTE       TO  PV-HOLD-DATE                           
                                         PV-TEMP-DATE.                          
           MOVE IN009-SCAN-TIMESTAMP TO  PV-TEMP-TIMESTAMP.                     
           MOVE PV-TEMP-TIMESTAMP    TO  PV-HOLD-TIMESTAMP.                     
W21732     MOVE IN009-DEPT-CLASS     TO  PV-HOLD-DEPT-CLASS.                    
00212      MOVE IN009-SKU            TO  PV-HOLD-SKU.                           
                                                                                
22000      ACCEPT PV-TODAYS-DATE         FROM DATE.                             
22000      MOVE PV-TODAYS-DATE-MM    TO CNTL-CARDS-SRCE-MM.                     
22000      MOVE PV-TODAYS-DATE-DD    TO CNTL-CARDS-SRCE-DD.                     
22000      MOVE PV-TODAYS-DATE-YY    TO CNTL-CARDS-SRCE-YY.                     
22000      ACCEPT CNTL-CARDS-SOURCE-TIME FROM TIME.                             
22000      WRITE MERGED-CONTROL-CARDS-RECORD                                    
22000          FROM CNTL-CARDS-SOURCE-COMMENT.                                  
22000      WRITE COPIED-CONTROL-CARDS-RECORD                                    
22000          FROM CNTL-CARDS-SOURCE-COMMENT.                                  
                                                                        INKUT169
00215 *----------------------------------------------------------------*INKUT169
00216 *                                                                *INKUT169
00217 *    DETERMINE LAPSE TIME BETWEEN ITEM SCAN TIMES.  IF LAPSE     *INKUT169
00217 *    TIME IS GREATER THAN THE GAP CONTROL CARD SAVE TABULATED    *INKUT169
00217 *    QUANTITY, PRICE, LAST TIMESTAMP.  IF ANOTHER GAP IS REACHED *INKUT169
00217 *    COMPARE TABULATED QUANTITIES AND SAVE THE QUANTITY, PRICE,  *INKUT169
00217 *    AND TIMESTAMP FROM THE GREATER OF THE TWO QUANTITIES.       *INKUT169
00217 *    WRITE THE ITEM TIMESTAMP IF THE ITEM NUMBER HAS CHANGED.    *INKUT169
00218 *                                                                *INKUT169
00219 *----------------------------------------------------------------*INKUT169
00221  2000-PROCESS-INPUT.                                              INKUT169
00222                                                                   INKUT169
           IF PV-ACTL-DATE = PV-PRC-DATE                                        
W21732      IF IN009-DEPT-CLASS = PV-HOLD-DEPT-CLASS AND                        
W21732         IN009-SKU = PV-HOLD-SKU                                          
              IF PV-HOLD-DATE = PV-PRC-DATE AND                                 
                 PV-TEMP-DATE = PV-PRC-DATE                                     
                 PERFORM 2050-CALC-TIME-GAP                                     
              ELSE                                                              
                 IF PV-HOLD-DATE = PV-TEMP-DATE                                 
                    PERFORM 2050-CALC-TIME-GAP                                  
                 ELSE                                                           
                    MOVE 9999 TO PV-LAPSE-TIME                                  
                 END-IF                                                         
              END-IF                                                            
00229         IF PV-LAPSE-TIME > PV-GAP-TIME                                    
00229            IF PV-GAP-FLAG = 'Y'                                           
                    PERFORM 2055-DETERMINE-CUTOFF                               
                    MOVE ZEROES            TO PV-CURR-TAB-QUANT                 
                 ELSE                                                           
                    MOVE PV-CURR-TAB-QUANT TO PV-PREV-TAB-QUANT                 
                    MOVE PV-HOLD-TIMESTAMP TO PV-TIME-STAMP                     
                    MOVE PV-HOLD-PRICE     TO PV-UNIT-PRICE                     
                    MOVE PV-HOLD-AREA      TO PV-AREA-NUM                       
                    MOVE ZEROES            TO PV-CURR-TAB-QUANT                 
                    MOVE 'Y'               TO PV-GAP-FLAG                       
                 END-IF                                                         
              END-IF                                                            
              MOVE IN009-QUANTITY-COMP TO PV-HOLD-QUANT                         
              COMPUTE PV-TOTAL-QUANT = PV-TOTAL-QUANT +                         
                                       PV-HOLD-QUANT                            
              COMPUTE PV-CURR-TAB-QUANT = PV-CURR-TAB-QUANT +                   
                                          PV-HOLD-QUANT                         
W21732        MOVE IN009-DEPT-CLASS     TO PV-HOLD-DEPT-CLASS                   
              MOVE IN009-SKU            TO PV-HOLD-SKU                          
              MOVE IN009-PRICE          TO PV-HOLD-PRICE                        
              MOVE IN009-SCAN-TIMESTAMP TO PV-TEMP-TIMESTAMP                    
              MOVE PV-TEMP-TIMESTAMP    TO PV-HOLD-TIMESTAMP                    
              MOVE IN009-STORE-NUMBER   TO PV-STORE-NUM                         
              MOVE IN009-DEPT           TO PV-DEPT-NUM                          
W21732        MOVE IN009-SUB-CLASS      TO PV-CLASS-NUM                         
W21732        MOVE IN009-SKU            TO PV-SKU-NUM                           
              MOVE IN009-AREA-NUMBER    TO PV-HOLD-AREA                         
              MOVE IN009-SCAN-DTE       TO PV-HOLD-SCAN-DATE                    
           ELSE                                                                 
00229         IF PV-GAP-FLAG = 'Y'                                              
                 PERFORM 2055-DETERMINE-CUTOFF                                  
              END-IF                                                            
              PERFORM 2200-WRITE-RECORD-OUT                                     
W21732        MOVE IN009-DEPT-CLASS     TO PV-HOLD-DEPT-CLASS                   
              MOVE IN009-SKU            TO PV-HOLD-SKU                          
              MOVE IN009-SCAN-TIMESTAMP TO PV-TEMP-TIMESTAMP                    
                                           PV-TIME-STAMP                        
              MOVE PV-TEMP-TIMESTAMP    TO PV-HOLD-TIMESTAMP                    
              MOVE IN009-QUANTITY-COMP  TO PV-TOTAL-QUANT                       
                                           PV-CURR-TAB-QUANT                    
                                           PV-HOLD-QUANT                        
              MOVE IN009-STORE-NUMBER   TO PV-STORE-NUM                         
              MOVE IN009-DEPT           TO PV-DEPT-NUM                          
W21732        MOVE IN009-SUB-CLASS      TO PV-CLASS-NUM                         
W21732        MOVE IN009-SKU            TO PV-SKU-NUM                           
              MOVE IN009-AREA-NUMBER    TO PV-AREA-NUM                          
                                           PV-HOLD-AREA                         
              MOVE IN009-SCAN-DTE       TO PV-SCAN-DATE                         
                                           PV-HOLD-SCAN-DATE                    
              MOVE PV-HOLD-QUANT       TO PV-TOTAL-QUANT                        
              MOVE IN009-PRICE         TO PV-UNIT-PRICE                         
                                          PV-HOLD-PRICE                         
            END-IF                                                              
           END-IF.                                                              
00251                                                                   INKUT169
00277                                                                   INKUT169
00278      PERFORM 2100-READ-INPUT.                                     INKUT169
00279 /                                                                 INKUT169
00156 *----------------------------------------------------------------*INKUT169
00157 *                                                                *INKUT169
00158 *    CALCULATE THE TIME GAP BETWEEN TIMESTAMPS.                  *INKUT169
00159 *                                                                *INKUT169
00160 *----------------------------------------------------------------*INKUT169
00161                                                                   INKUT169
00162  2050-CALC-TIME-GAP.                                              INKUT169
00163                                                                   INKUT169
           MOVE IN009-SCAN-TIMESTAMP TO PV-TEMP-TIMESTAMP.                      
           MOVE PV-TEMP-TIMESTAMP    TO PV-CURR-TIMESTAMP.                      
           IF PV-CURR-MIN < PV-HOLD-MIN                                         
              COMPUTE PV-LAPSE-MIN = PV-CURR-MIN - PV-HOLD-MIN + 60             
              COMPUTE PV-LAPSE-HOUR = PV-CURR-HOUR - PV-HOLD-HOUR - 1           
           ELSE                                                                 
              COMPUTE PV-LAPSE-MIN = PV-CURR-MIN - PV-HOLD-MIN                  
              COMPUTE PV-LAPSE-HOUR = PV-CURR-HOUR - PV-HOLD-HOUR               
           END-IF.                                                              
00279 /                                                                 INKUT169
00156 *----------------------------------------------------------------*INKUT169
00157 *                                                                *INKUT169
00158 *    CALCULATE THE CUTOFF TIMESTAMP.                             *INKUT169
00159 *                                                                *INKUT169
00160 *----------------------------------------------------------------*INKUT169
00161                                                                   INKUT169
00162  2055-DETERMINE-CUTOFF.                                           INKUT169
00163                                                                   INKUT169
           IF (PV-PREV-TAB-QUANT > CC-GAP-QTY AND                               
               PV-CURR-TAB-QUANT > CC-GAP-QTY)                                  
               MOVE PV-CURR-TAB-QUANT TO PV-PREV-TAB-QUANT                      
               MOVE PV-HOLD-TIMESTAMP TO PV-TIME-STAMP                          
               MOVE PV-HOLD-PRICE     TO PV-UNIT-PRICE                          
               MOVE PV-HOLD-AREA      TO PV-AREA-NUM                            
               MOVE PV-HOLD-SCAN-DATE TO PV-SCAN-DATE                           
           ELSE                                                                 
              IF (PV-PREV-TAB-QUANT <= CC-GAP-QTY AND                           
                  PV-CURR-TAB-QUANT > CC-GAP-QTY)                               
                  MOVE PV-CURR-TAB-QUANT TO PV-PREV-TAB-QUANT                   
                  MOVE PV-HOLD-TIMESTAMP TO PV-TIME-STAMP                       
                  MOVE PV-HOLD-PRICE     TO PV-UNIT-PRICE                       
                  MOVE PV-HOLD-AREA      TO PV-AREA-NUM                         
                  MOVE PV-HOLD-SCAN-DATE TO PV-SCAN-DATE                        
              ELSE                                                              
                 IF (PV-PREV-TAB-QUANT > CC-GAP-QTY AND                         
                     PV-CURR-TAB-QUANT <= CC-GAP-QTY)                           
                     NEXT SENTENCE                                              
                 ELSE                                                           
                    IF (PV-PREV-TAB-QUANT <= CC-GAP-QTY AND                     
                        PV-CURR-TAB-QUANT <= CC-GAP-QTY)                        
                        IF (PV-PREV-TAB-QUANT = PV-CURR-TAB-QUANT)              
                           MOVE PV-CURR-TAB-QUANT TO PV-PREV-TAB-QUANT          
                           MOVE PV-HOLD-TIMESTAMP TO PV-TIME-STAMP              
                           MOVE PV-HOLD-PRICE     TO PV-UNIT-PRICE              
                           MOVE PV-HOLD-AREA      TO PV-AREA-NUM                
                           MOVE PV-HOLD-SCAN-DATE TO PV-SCAN-DATE               
                        END-IF                                                  
                        IF (PV-PREV-TAB-QUANT < PV-CURR-TAB-QUANT)              
                           MOVE PV-CURR-TAB-QUANT TO PV-PREV-TAB-QUANT          
                           MOVE PV-HOLD-TIMESTAMP TO PV-TIME-STAMP              
                           MOVE PV-HOLD-PRICE     TO PV-UNIT-PRICE              
                           MOVE PV-HOLD-AREA      TO PV-AREA-NUM                
                           MOVE PV-HOLD-SCAN-DATE TO PV-SCAN-DATE               
                        END-IF                                                  
                        IF (PV-PREV-TAB-QUANT > PV-CURR-TAB-QUANT)              
                           NEXT SENTENCE                                        
                        END-IF.                                                 
  279 /                                                                         
00280 *----------------------------------------------------------------*INKUT169
00281 *                                                                *INKUT169
00282 *    READ THE INPUT FILE.                                        *INKUT169
00283 *                                                                *INKUT169
00284 *----------------------------------------------------------------*INKUT169
00285                                                                   INKUT169
00286  2100-READ-INPUT.                                                 INKUT169
00287                                                                   INKUT169
00288      READ VALID-INVENTORY-INPUT INTO IN009-EDITED-INVENTORY-RECORDINKUT169
00289           AT END                                                  INKUT169
00290           SET PV-END-OF-INPUT TO TRUE                             INKUT169
00296      END-READ.                                                    INKUT169
00297                                                                           
00298      IF  NOT PV-END-OF-INPUT                                      INKUT169
22000          ADD 1 TO PV-REC-IN                                       INKUT169
               IF PV-HOLD-STORE NOT = IN009-STORE-NUMBER                        
22000             ADD 1 TO PV-STR-IN                                            
                  PERFORM 2300-GET-ACTL-INV-DATE                                
                  MOVE PV-ACTL-INV-DTE-YY TO PV-ACTL-ENTIRE-YEAR                
                  MOVE PV-ACTL-INV-DTE-MM TO PV-ACTL-MONTH                      
                                             PV-ACTL-ENTIRE-MONTH               
                  MOVE PV-ACTL-INV-DTE-DD TO PV-ACTL-DAY                        
                                             PV-ACTL-ENTIRE-DAY                 
                  IF PV-ACTL-DATE = PV-PRC-DATE                                 
                     MOVE PV-ACTL-ENTIRE-DATE TO PV-ACTL-INV-DATE               
                  ELSE                                                          
                     IF PV-HOLD-STORE NOT = IN009-STORE-NUMBER                  
22000                    IF PV-STR-SKIP = 0                                     
22000                        DISPLAY '********************************'         
22000                                '********************************'         
22000                    END-IF                                                 
22000                    DISPLAY '* STORE '                                     
22000                            IN009-STORE-NUMBER                             
22000                            ' SKIPPED!'                                    
22000                            ' INV DATE (' PV-ACTL-DATE ')'                 
22000                            ' DOES NOT MATCH CNTL'                         
22000                            ' PROCESS DATE (' PV-PRC-DATE ').'             
22000                    ADD 1 TO PV-STR-SKIP                                   
                     END-IF                                                     
                  END-IF                                                        
                  MOVE IN009-STORE-NUMBER TO PV-HOLD-STORE                      
               END-IF                                                           
               MOVE PV-HOLD-DATE    TO  PV-TEMP-DATE                            
00299          MOVE IN009-SCAN-DTE  TO  PV-HOLD-DATE                            
00302      END-IF.                                                      INKUT169
00336 /                                                                 INKUT169
00485 *----------------------------------------------------------------*INKUT169
00486 *                                                                *INKUT169
00487 *    WRITE THE OUTPUT RECORD.                                    *INKUT169
00488 *                                                                *INKUT169
00489 *----------------------------------------------------------------*INKUT169
00490                                                                   INKUT169
00491  2200-WRITE-RECORD-OUT.                                                   
00492                                                                   INKUT169
W33304     INITIALIZE IN019-ITEM-REC.                                           
           MOVE PV-ITEM-REC TO IN019-ITEM-REC.                                  
           MOVE PV-SCAN-DATE TO IN019-SCAN-DTE.                                 
           MOVE ZEROES       TO IN019-FIRST-SLS-TIMESTAMP                       
                                IN019-UNITS-SLD-AFT-TIMESTAMP                   
                                IN019-SALES-ADJ-QTY                             
                                IN019-NEG-INV-ADJUST                            
                                IN019-SALES-ADJ-EXT-RTL-AMT                     
                                IN019-INVLD-STKRMW                              
                                IN019-DKEY-NBR.                                 
W33304     INITIALIZE           IN019-INV-ID.                                   
W33304     MOVE SPACE        TO IN019-FINCL-INV-STAT-CDE.                       
           IF PV-GAP-FLAG = 'Y'                                                 
              MOVE PV-TIME-STAMP TO IN019-SCAN-TIMESTAMP                        
              MOVE PV-UNIT-PRICE TO IN019-UNIT-PRICE                            
           ELSE                                                                 
              MOVE PV-HOLD-TIMESTAMP TO PV-TIME-STAMP                           
              MOVE PV-TIME-STAMP     TO IN019-SCAN-TIMESTAMP                    
              MOVE PV-HOLD-PRICE     TO IN019-UNIT-PRICE                        
              MOVE PV-HOLD-AREA      TO IN019-SCAN-AREA                         
           END-IF.                                                              
           MOVE 'N' TO PV-GAP-FLAG.                                             
           MOVE PV-TOTAL-QUANT TO IN019-TTL-PHY-COUNT.                          
00503                                                                   INKUT169
00489 *----------------------------------------------------------------*INKUT169
00489 * TIMESTAMPS ASSOCIATED WITH SHEET INVENTORY WILL NOT BE --------*INKUT169
00489 * GENERATED                                              --------*INKUT169
00489 *----------------------------------------------------------------*INKUT169
           IF IN019-SCAN-DTE = ZERO                                             
             AND IN019-SCAN-TIMESTAMP = ZERO                                    
               CONTINUE                                                         
           ELSE                                                                 
               WRITE TIMED-INVENTORY-OUTPUT-REC                             INKU
                   FROM IN019-ITEM-REC                                          
               ADD 1           TO  PV-REC-OUT                               INKU
           END-IF.                                                              
00336 /                                                                 INKUT169
00485 *----------------------------------------------------------------*INKUT169
00486 *                                                                *INKUT169
00487 *    CHECK INVENTORY DATE.                                       *INKUT169
00488 *                                                                *INKUT169
00489 *----------------------------------------------------------------*INKUT169
00490                                                                   INKUT169
       2300-GET-ACTL-INV-DATE.                                                  
           INITIALIZE DCLTINVPAR.                                               
W26600                                                                          
W26600     MOVE IN009-STORE-NUMBER     TO INVPAR-LOC-NBR                        
                                                                                
           EXEC SQL                                                             
W33304         SELECT ACTL_INV_DTE                                              
               INTO  :PV-ACTL-INV                                               
P28545         FROM  TINVPAR A,                                                 
P28545               TINCNTL B                                                  
P28545         WHERE B.ACTV_IND = 'Y'                                           
P28545           AND A.INV_ID = B.INV_ID                                        
W33304           AND A.LOC_INV_STAT_CDE = :PC-INITIAL-STATUS                    
W33304           AND A.ACTL_FIN_BK_DTE = :PC-DEFAULT-NINES                      
P28545           AND LOC_NBR = :INVPAR-LOC-NBR                                  
P28545         WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
               WHEN SQLCODE = +100                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    CONTINUE                                                    
               WHEN SQLCODE < ZERO                                              
                    DISPLAY '** ABEND - INKUT169 **'                            
                    DISPLAY '** READ FOR TINVPAR'                               
W33304              DISPLAY 'IN009-STORE-NUMBER:' IN009-STORE-NUMBER            
W33304              PERFORM 9000-SQL-ERROR                                      
           END-EVALUATE.                                                        
                                                                                
22000                                                                           
22000 ******************************************************************INKUT169
22000 *                                                                *INKUT169
22000 *    CREATE AND WRITE OUT IDCAMS CONTROL CARDS                   *INKUT169
22000 *                                                                *INKUT169
22000 ******************************************************************INKUT169
22000  2500-CREATE-WRITE-CNTL-CARDS.                                            
22000      WRITE MERGED-CONTROL-CARDS-RECORD                                    
22000          FROM CNTL-CARDS-MERGE-COMMENT-1.                                 
22000      WRITE MERGED-CONTROL-CARDS-RECORD                                    
22000          FROM CNTL-CARDS-REPRO-REC.                                       
22000      MOVE CC-PRC-YY    TO CNTL-CARDS-DSN-DATE-YY                          
22000      MOVE CC-PRC-MONTH TO CNTL-CARDS-DSN-DATE-MM                          
22000      MOVE CC-PRC-DAY   TO CNTL-CARDS-DSN-DATE-DD                          
22000      MOVE SPACES TO CNTL-CARDS-DSN-BUILD.                                 
22000      IF OPT-PRC-DSN-PREF = SPACES                                         
22000          STRING '  INDATASET('                                            
22000              DELIMITED BY SIZE                                            
22000                 CNTL-CARDS-DSN-PREF                                       
22000              DELIMITED BY SPACE                                           
22000                 CNTL-CARDS-DSN-DATE                                       
22000                 ') -'                                                     
22000              DELIMITED BY SIZE                                            
22000              INTO CNTL-CARDS-DSN-BUILD                                    
22000      ELSE                                                                 
22000          STRING '  INDATASET('                                            
22000              DELIMITED BY SIZE                                            
22000                 OPT-PRC-DSN-PREF                                          
22000              DELIMITED BY SPACE                                           
22000                 CNTL-CARDS-DSN-DATE                                       
22000                 ') -'                                                     
22000              DELIMITED BY SIZE                                            
22000              INTO CNTL-CARDS-DSN-BUILD                                    
22000      END-IF.                                                              
22000      WRITE MERGED-CONTROL-CARDS-RECORD                                    
22000          FROM CNTL-CARDS-DSN-BUILD.                                       
22000      WRITE MERGED-CONTROL-CARDS-RECORD                                    
22000          FROM CNTL-CARDS-MERGE-OUTPUT.                                    
22000                                                                           
22000      WRITE COPIED-CONTROL-CARDS-RECORD                                    
22000          FROM CNTL-CARDS-COPY-COMMENT-1.                                  
22000      WRITE COPIED-CONTROL-CARDS-RECORD                                    
22000          FROM CNTL-CARDS-REPRO-REC.                                       
22000      WRITE COPIED-CONTROL-CARDS-RECORD                                    
22000          FROM CNTL-CARDS-COPY-INPUT.                                      
22000      MOVE SPACES TO CNTL-CARDS-DSN-BUILD.                                 
22000      IF OPT-PRC-DSN-PREF = SPACES                                         
22000          STRING '  OUTDATASET('                                           
22000              DELIMITED BY SIZE                                            
22000                 CNTL-CARDS-DSN-PREF                                       
22000              DELIMITED BY SPACE                                           
22000                 CNTL-CARDS-DSN-DATE                                       
22000                 ') '                                                      
22000              DELIMITED BY SIZE                                            
22000              INTO CNTL-CARDS-DSN-BUILD                                    
22000      ELSE                                                                 
22000          STRING '  OUTDATASET('                                           
22000              DELIMITED BY SIZE                                            
22000                 OPT-PRC-DSN-PREF                                          
22000              DELIMITED BY SPACE                                           
22000                 CNTL-CARDS-DSN-DATE                                       
22000                 ') '                                                      
22000              DELIMITED BY SIZE                                            
22000              INTO CNTL-CARDS-DSN-BUILD                                    
22000      END-IF.                                                              
22000      WRITE COPIED-CONTROL-CARDS-RECORD                                    
22000          FROM CNTL-CARDS-DSN-BUILD.                                       
22000                                                                           
22000                                                                           
      *                                                                         
00485 *----------------------------------------------------------------*INKUT169
00486 *                                                                *INKUT169
00487 *    WRITE OUT COUNTS AND CLOSE THE FILES                        *INKUT169
00488 *                                                                *INKUT169
00489 *----------------------------------------------------------------*INKUT169
00490                                                                   INKUT169
00508  3000-EOJ.                                                        INKUT169
00509                                                                   INKUT169
00510      DISPLAY '***************************'.                       INKUT169
00511      DISPLAY '*     INKUT169 COUNTS     *'.                       INKUT169
00512      DISPLAY '* '                                                 INKUT169
00513              PV-REC-IN                                            INKUT169
00514              ' RECORDS READ    *'.                                INKUT169
00515      DISPLAY '* '                                                 INKUT169
00516              PV-REC-OUT                                           INKUT169
00517              ' RECORDS WRITTEN *'.                                INKUT169
00518      DISPLAY '*                         *'.                       INKUT169
00519      DISPLAY '***************************'.                       INKUT169
00520                                                                   INKUT169
00521      CLOSE VALID-INVENTORY-INPUT                                  INKUT169
                 GAP-CONTROL-CARD-FILE                                          
                 PRC-CONTROL-CARD-FILE                                          
22000            TIMED-INVENTORY-OUTPUT                                 INKUT169
22000            MERGED-CONTROL-CARDS-FILE                              INKUT169
22000            COPIED-CONTROL-CARDS-FILE.                             INKUT169
00523                                                                   INKUT169
      ******************************************************************        
      * 9000-SQL-ERROR - PERFORMS THE ABEND FOR THE PROGRAM IN THE     *        
      *  EVENT THAT AN SQL ERROR CODE OCCURS.                          *        
      ******************************************************************        
       9000-SQL-ERROR.                                                          
           MOVE SQLCODE TO SQLCODE2.                                            
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
W33304     DISPLAY '** PROGRAM          ** = INKUT169'.                         
           DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
           DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
           DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
           DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
      *                                                                         
      ******************************************************************        
      * 9050-SQL-WARNING - PERFORMS THE ABEND FOR THE PROGRAM IN THE   *        
      *  EVENT THAT AN SQL WARNING CODE OCCURS.                        *        
      ******************************************************************        
       9050-SQL-WARNING.                                                        
           MOVE SQLCODE TO SQLCODE2.                                            
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '** ABEND **       ** = SQLWARNING'.                         
W33304     DISPLAY '** PROGRAM        ** = INKUT169'.                           
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
           DISPLAY '** SQLWARN0       ** = ' SQLWARN0.                          
           DISPLAY '** SQLWARN1       ** = ' SQLWARN1.                          
           DISPLAY '** SQLWARN2       ** = ' SQLWARN2.                          
           DISPLAY '** SQLWARN3       ** = ' SQLWARN3.                          
           DISPLAY '** SQLWARN4       ** = ' SQLWARN4.                          
           DISPLAY '** SQLWARN5       ** = ' SQLWARN5.                          
           DISPLAY '** SQLWARN6       ** = ' SQLWARN6.                          
           DISPLAY '** SQLWARN7       ** = ' SQLWARN7.                          
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
