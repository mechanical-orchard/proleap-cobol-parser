       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT441.                                         00020000
       AUTHOR.        SCOTT JACKSON.                                    00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  07-14-1999.                                       00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      * MLKUT441 - DESCRIPTION.............                             00080000
      *                                                                 00090000
      * THIS PROGRAM WILL RE-ALLOCATE DEPT 099/CLASS TRANSACTIONS FOR   00100000
      * THE CURRENT MONTH TO THE TOP FORTY SELLING DEPARTMENTS.         00110000
      *                                                                 00120000
      * INP01 - CONTAINS THE CURRENT SYSTEM DATE                        00130000
      * INP02 - CONTAINS PROCESSING DATE / PROCESSING FLAG              00140000
      * INP03 - CONTAINS TOP-FORTY SELLING DEPARTMENTS                  00150000
      * INP04 - CONTAINS ADJUSTMENTS TO SALES RECORDS                   00160000
      * INP05 - CONTAINS ADJUSTMENTS TO PRICE CHANGE RECORDS            00170000
      *                                                                 00180000
      * OUTPUT FORMAT IS IN MLRD020 FORMAT                              00190000
      *                                                                 00200000
      * IF A PRCHG IS PROCESSED FOR RE-ALLOCATION (SPREAD OVER TOP 40)  00210000
      * AND BOTH OLD AND NEW RETAIL ARE CALCULATED TO BE EQUAL TO 0,    00220000
      * THEN THIS RECORD WILL BE WRITTEN TO THE ERROR FILE AND WILL NOT 00230000
      * PROCESSED ANY FURTHER. THIS OCCURS WHEN BOTH OLD/NEW PRICE HAVE 00240000
      * A VALUE OF < $.10 . WHEN THE RECORD IS PROCESSED FOR REALLOCATON00250000
      * THESE 'VALUES' ARE DIVIDED BY 40 AND REMAINDER IS CALCULATED.   00260000
      * THE RESULT IS 9 RECORDS WITH OLD/NEW PRICE OF 0 AND THE LAST REC00270000
      * WILL CONTAIN A VALUE (REMAINDER).THESE 0-RECS ERROR IN MLKUP021 00280000
      * THEREFORE, WHEN OLD/NEW PRICE = 0 , WRITE THIS TO THE ERROR FILE00290000
      * AND DO NOT WRITE RECORD TO THE 'REALLOCATION' GDG               00300000
      *                                                                 00310000
      *CHANGE HISTORY:                                                  00320000
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00330000
      * ------- ---------- ----------- -------------------------------- 00340000
      * 10/26/99  AML  CONTROL CARD IS NO LONGER BEING USED TO CONTROL  00350000
      *                PROCESSING.                                      00360000
      *                                                                 00370000
      * 02/04/00  SMJ  RE-ALLOCATION OF DOLLARS WILL NOW BE SPREAD OUT  00380000
      *                AMONG TOP 40 SELLING DEPT/SCL (VS. TOP TEN). MANY00390000
      *                PARA REFERENCE 'TOP-TEN' (IT'S ACTUALLY TOP 40)  00400000
W21731*                                                                 00410000
W21731* 12/12/01  RLB  NON-INTELLIGENT SKU                              00420000
W21731*                MLRD020 AND MLRD129 HAVE CHANGED.  THE DEPT,     00430000
W21731*                CLASS, AND SEQ DEFINED WITHIN THE SKU HAVE BEEN  00440000
W21731*                DELETED, AND THE SKU IS DEFINED AS A CHAR 8.     00450000
W21731*                DEPT AND CLASS HAVE BEEN MOVED INTO THE AVAILABLE00460000
W21731*                FILLER.  (THE CLASS IS REALLY THE SUBCLASS)      00470000
W21731*                NOW THE PROGRAM CAN CONTINUE                     00480000
W21731*                TO POPULATE THE THE DEPT AND CLASS AS IS, HOWEVER00490000
W21731*                NEEDED TO ADD CODE TO MAP THE SKU FIELD.         00500000
W21731*                                                                 00510000
R21731* 07/31/02  RLB  NON-INTELLIGENT SKU                              00520000
R21731*                MAP MAJOR CLASS.  COPYBOOK MLRD020 WAS           00530000
R21731*                CHANGED TO INCLUDE MAJOR CLASS                   00540000
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION     00550000
PM3403* PMO3403 04-20-2012 S. JACKSON  LARGE RETAIL MLRD020 RECOMPILE   00560000
      ******************************************************************00570000
      *                                                                 00580000
      *                                                                 00590000
       ENVIRONMENT DIVISION.                                            00600000
      *                                                                 00610000
       CONFIGURATION SECTION.                                           00620000
      *                                                                 00630000
       SOURCE-COMPUTER.        IBM-3090.                                00640000
       OBJECT-COMPUTER.        IBM-3090.                                00650000
      *                                                                 00660000
       INPUT-OUTPUT SECTION.                                            00670000
       FILE-CONTROL.                                                    00680000
                                                                        00690000
           SELECT CONTROL-DATE-FILE                                     00700000
               ASSIGN TO INP01.                                         00710000
                                                                        00720000
           SELECT CONTROL-CARD-FILE                                     00730000
               ASSIGN TO INP02.                                         00740000
                                                                        00750000
           SELECT TOPTEN-SALES-FILE                                     00760000
               ASSIGN TO INP03.                                         00770000
                                                                        00780000
           SELECT SALES-REVERSAL-FILE                                   00790000
               ASSIGN TO INP04.                                         00800000
                                                                        00810000
           SELECT PRICE-CHANGE-REVERSAL-FILE                            00820000
               ASSIGN TO INP05.                                         00830000
                                                                        00840000
           SELECT REVERSAL-OUTPUT-FILE                                  00850000
               ASSIGN TO OUT01.                                         00860000
                                                                        00870000
           SELECT ERROR-OUTPUT-FILE                                     00880000
               ASSIGN TO OUT02.                                         00890000
                                                                        00900000
      ******************************************************************00910000
       DATA DIVISION.                                                   00920000
      ******************************************************************00930000
                                                                        00940000
       FILE SECTION.                                                    00950000
                                                                        00960000
       FD  CONTROL-DATE-FILE                                            00970000
           RECORDING MODE IS F                                          00980000
           LABEL RECORDS ARE STANDARD                                   00990000
           BLOCK CONTAINS 0 RECORDS.                                    01000000
      *                                                                 01010000
       01  CONTROL-DATE-RECORD         PIC X(80).                       01020000
                                                                        01030000
       FD  CONTROL-CARD-FILE                                            01040000
           RECORDING MODE IS F                                          01050000
           LABEL RECORDS ARE STANDARD                                   01060000
           BLOCK CONTAINS 0 RECORDS.                                    01070000
      *                                                                 01080000
       01  CONTROL-CARD-RECORD         PIC X(80).                       01090000
                                                                        01100000
       FD  TOPTEN-SALES-FILE                                            01110000
           RECORDING MODE IS F                                          01120000
           LABEL RECORDS ARE STANDARD                                   01130000
           BLOCK CONTAINS 0 RECORDS.                                    01140000
      *                                                                 01150000
       01  TOPTEN-SALES-RECORD         PIC X(80).                       01160000
                                                                        01170000
       FD  SALES-REVERSAL-FILE                                          01180000
           RECORDING MODE IS F                                          01190000
           LABEL RECORDS ARE STANDARD                                   01200000
           BLOCK CONTAINS 0 RECORDS.                                    01210000
      *                                                                 01220000
W26603 01  SALES-REVERSAL-RECORD       PIC X(175).                      01230000
                                                                        01240000
       FD  PRICE-CHANGE-REVERSAL-FILE                                   01250000
           RECORDING MODE IS F                                          01260000
           LABEL RECORDS ARE STANDARD                                   01270000
           BLOCK CONTAINS 0 RECORDS.                                    01280000
      *                                                                 01290000
W26603 01  PRICE-CHANGE-REVERSAL-RECORD PIC X(175).                     01300000
                                                                        01310000
       FD  REVERSAL-OUTPUT-FILE                                         01320000
           RECORDING MODE IS F                                          01330000
           LABEL RECORDS ARE STANDARD                                   01340000
           BLOCK CONTAINS 0 RECORDS.                                    01350000
      *                                                                 01360000
W26603 01  REVERSAL-OUTPUT-RECORD      PIC X(175).                      01370000
                                                                        01380000
       FD  ERROR-OUTPUT-FILE                                            01390000
           RECORDING MODE IS F                                          01400000
           LABEL RECORDS ARE STANDARD                                   01410000
           BLOCK CONTAINS 0 RECORDS.                                    01420000
      *                                                                 01430000
W26603 01  ERROR-OUTPUT-RECORD         PIC X(175).                      01440000
                                                                        01450000
      ******************************************************************01460000
       WORKING-STORAGE SECTION.                                         01470000
      ******************************************************************01480000
      *                                                                 01490000
       01  ABEND-CODE                          PIC S9(04)  VALUE +0     01500000
                                                           COMP SYNC.   01510000
      * TMLCNTL OPEN MONTH                                              01520000
       01  CNTL-TABLE-DATE.                                             01530000
           05  CT-OPEN-MONTH.                                           01540000
               10 CT-OPEN-CC         PIC X(02)          VALUE SPACES.   01550000
               10 CT-OPEN-YY         PIC X(02)          VALUE SPACES.   01560000
               10 FILLER             PIC X(01)          VALUE '-'.      01570000
               10 CT-OPEN-MM         PIC X(02)          VALUE SPACES.   01580000
               10 FILLER             PIC X(01)          VALUE '-'.      01590000
               10 CT-OPEN-DD         PIC X(02)          VALUE SPACES.   01600000
      *                                                                 01610000
       01  CC1-SCHDULE-DATE.                                            01620000
           05  CC1-SYSTEM-DTE        PIC X(10)          VALUE SPACES.   01630000
           05  FILLER                PIC X(70)          VALUE SPACES.   01640000
      *                                                                 01650000
       01  CC2-PROCESS-REC.                                             01660000
           05  CC2-FSCL-END-DTE      PIC X(10)          VALUE SPACES.   01670000
           05  CC2-CURRENT-DTE.                                         01680000
               10 CC2-CURRENT-CC     PIC X(02)          VALUE SPACES.   01690000
               10 CC2-CURRENT-YY     PIC X(02)          VALUE SPACES.   01700000
               10 FILLER             PIC X(01)          VALUE '-'.      01710000
               10 CC2-CURRENT-MM     PIC X(02)          VALUE SPACES.   01720000
               10 FILLER             PIC X(01)          VALUE '-'.      01730000
               10 CC2-CURRENT-DD     PIC X(02)          VALUE SPACES.   01740000
           05  CC2-NO-PROCESS-IND    PIC X(01)          VALUE 'N'.      01750000
               88  CC2-PROCESS-DEPTSCL                  VALUE 'Y'.      01760000
           05  FILLER                PIC X(59)          VALUE SPACES.   01770000
      *                                                                 01780000
       01  CC3-REVERSAL-LIST.                                           01790000
           05  CC3-DEPT-NBR          PIC X(03)          VALUE SPACES.   01800000
           05  FILLER                PIC X(01)          VALUE SPACES.   01810000
           05  CC3-SCL-NBR           PIC X(02)          VALUE SPACES.   01820000
           05  FILLER                PIC X(74)          VALUE SPACES.   01830000
      *                                                                 01840000
       01  PC-PROGRAM-CONSTANTS.                                        01850000
           05  PC-JOB-NAME           PIC  X(06)        VALUE 'MLK490'.  01860000
           05  PC-PROGRAM-NAME       PIC  X(08)        VALUE 'MLKUT441'.01870000
      *    05  PC-REVERSAL-BATCH     PIC  X(03)        VALUE '795'.     01880000
SMJ        05  PC-REVERSAL-DEPT-COUNT PIC S9(02)       VALUE +40.       01890000
           05  PC-NET-ZERO           PIC  9(07)V99     VALUE  0.00.     01900000
W21731     05  PC-SEQ-NBR            PIC  X(03)        VALUE '000'.     01910000
      *                                                                 01920000
       01  PV-PROGRAM-VARIABLES.                                        01930000
           05  PV-SUB                    PIC 9(02)      VALUE ZERO.     01940000
W26603     05  PV-REVERSE-SLS-AMT        PIC S9(11)V99  VALUE ZEROES.   01950000
W26603     05  PV-REVERSE-OLD-AMT        PIC S9(11)V99  VALUE ZEROES.   01960000
W26603     05  PV-REVERSE-NEW-AMT        PIC S9(11)V99  VALUE ZEROES.   01970000
           05  PV-REVERSE-SLS-REMAINDER  PIC SV999      VALUE ZEROES.   01980000
           05  PV-REVERSE-OLD-REMAINDER  PIC SV999      VALUE ZEROES.   01990000
           05  PV-REVERSE-NEW-REMAINDER  PIC SV999      VALUE ZEROES.   02000000
R21731     05  PV-MAJOR-CLASS.                                          02010000
R21731         10  PV-MAJOR-CLASS-CHAR1  PIC X(01)      VALUE SPACES.   02020000
R21731         10  PV-MAJOR-CLASS-CHAR2  PIC X(01)      VALUE SPACES.   02030000
      *                                                                 02040000
       01  PS-SWITCHES.                                                 02050000
           05  PS-END-OF-CC1-FILE    PIC X(01)          VALUE 'N'.      02060000
               88  PS-EMPTY-CC1-FILE                    VALUE 'Y'.      02070000
           05  PS-END-OF-CC2-FILE    PIC X(01)          VALUE 'N'.      02080000
               88  PS-EMPTY-CC2-FILE                    VALUE 'Y'.      02090000
           05  PS-END-OF-CC3-FILE    PIC X(01)          VALUE 'N'.      02100000
               88  PS-TABLE-LOADED                      VALUE 'Y'.      02110000
           05  PS-VALID-RECORD-SW    PIC X(01)          VALUE 'N'.      02120000
               88  PS-VALID-RECORD                      VALUE 'Y'.      02130000
           05  PS-FORMATS-REQD-SW    PIC X(01)          VALUE 'N'.      02140000
               88  PS-CONTINUE-PROCESSING               VALUE 'Y'.      02150000
           05  PS-REVERSE-TRANS      PIC X(01)          VALUE 'N'.      02160000
               88  PS-PROCESSING-COMPLETE               VALUE 'Y'.      02170000
           05  PS-REVERSE-SALES-SW   PIC X(01)          VALUE 'N'.      02180000
               88  PS-SALE-COMPLETE                     VALUE 'Y'.      02190000
           05  PS-REVERSE-PCHG-SW    PIC X(01)          VALUE 'N'.      02200000
               88  PS-PCHG-COMPLETE                     VALUE 'Y'.      02210000
      *                                                                 02220000
       01  DEPT-TABLE.                                                  02230000
SMJ        05  DT-CONTROL-ENTRIES OCCURS 40 TIMES.                      02240000
               10  DT-DEPT-NBR             PIC  X(03)      VALUE SPACES.02250000
               10  DT-SCL-NBR              PIC  X(02)      VALUE SPACES.02260000
      *                                                                 02270000
      *FORMAT IS SAME AS MLRD020 SALE TRANSACTION FMT (READ INPUT INTO) 02280000
       01  WS-SALE-COMPOSITE-LAYOUT.                                    02290000
W26603     05  SLS-STORE-NBR                    PIC  X(05).             02300000
W21731     05  SLS-SKU                          PIC  X(08).             02310000
           05  SLS-TRANS-CDE                    PIC  X(02).             02320000
           05  SLS-SYST-ORIG-CDE                PIC  X(02).             02330000
W21731     05  SLS-DEPT-NBR                     PIC  X(03).             02340000
W21731     05  SLS-CLASS-NBR                    PIC  X(02).             02350000
R21731     05  SLS-MAJOR-CLASS                  PIC  X(02).             02360000
W26603     05  SLS-AVAILABLE-FILLER             PIC  X(01).             02370000
W26603     05  SLS-TRANSACTION-DATA             PIC  X(150).            02380000
           05  SLS-SALES-RECORD     REDEFINES                           02390000
               SLS-TRANSACTION-DATA.                                    02400000
               10  SLS-SALE-DTE                 PIC  9(06).             02410000
               10  SLS-SALE-TYP-CDE             PIC  X(01).             02420000
               10  SLS-SALE-RSN-CDE             PIC  X(02).             02430000
               10  SLS-SALE-INIT-BY-CDE         PIC  X(01).             02440000
W26603         10  SLS-SALE-EXTD-RTL-AMT        PIC S9(11)V99.          02450000
               10  SLS-SALE-KOHLS-MM            PIC  X(02).             02460000
               10  SLS-SALE-TYPE-2              PIC  X(01).             02470000
               10  SLS-SALE-CORR-CDE            PIC  X(01).             02480000
W22603         10  SLS-SALE-FILLER              PIC  X(123).            02490000
      *                                                                 02500000
      ****************************************************              02510000
      *  SALES TRANSACTIONS FILE LAYOUT OUTPUT           *              02520000
      ****************************************************              02530000
           COPY MLRD020.                                                02540000
      *                                                                 02550000
      ****************************************************              02560000
      *  PCHG TRANSACTIONS FILE LAYOUT INPUT            *               02570000
      ****************************************************              02580000
           COPY MLRD129.                                                02590000
      *                                                                 02600000
      ******************************************************************02610000
      ***  MERCHANDISE LEDGER CONTROL DATES                             02620000
      ******************************************************************02630000
           EXEC SQL                                                     02640000
               INCLUDE TMLCNTL                                          02650000
           END-EXEC.                                                    02660000
      ******************************************************************02670000
      ***  DB2 COMMUNICATION AREA                                       02680000
      ******************************************************************02690000
      *                                                                 02700000
           EXEC SQL                                                     02710000
               INCLUDE SQLCA                                            02720000
           END-EXEC.                                                    02730000
                                                                        02740000
      *-------------------*                                             02750000
       PROCEDURE DIVISION.                                              02760000
      *-------------------*                                             02770000
                                                                        02780000
       0000-MAIN-MODULE.                                                02790000
                                                                        02800000
           PERFORM 1000-INITIALIZATION.                                 02810000
                                                                        02820000
           PERFORM 2000-PROCESS-REVERSALS                               02830000
               UNTIL PS-PROCESSING-COMPLETE.                            02840000
                                                                        02850000
           STOP RUN.                                                    02860000
      *                                                                 02870000
      *                                                                 02880000
      ******************************************************************02890000
      * OPEN INPUT AND OUTPUT DATASETS.                                 02900000
      *                                                                 02910000
      * EACH CONTROL CARD WILL BE READ AND VALIDATED BEFORE CONTINUING  02920000
      *                                                                 02930000
      * IF CONTROL CARDS VALID & TOP-TEN LOADED THEN READ A RECORD      02940000
      ******************************************************************02950000
       1000-INITIALIZATION.                                             02960000
      *                                                                 02970000
           OPEN  INPUT CONTROL-DATE-FILE                                02980000
                       CONTROL-CARD-FILE                                02990000
                       TOPTEN-SALES-FILE                                03000000
                       SALES-REVERSAL-FILE                              03010000
                       PRICE-CHANGE-REVERSAL-FILE                       03020000
                OUTPUT REVERSAL-OUTPUT-FILE                             03030000
                       ERROR-OUTPUT-FILE.                               03040000
                                                                        03050000
           MOVE 'N' TO PS-END-OF-CC1-FILE                               03060000
                       PS-END-OF-CC2-FILE                               03070000
                       PS-END-OF-CC3-FILE                               03080000
                       PS-FORMATS-REQD-SW                               03090000
                       PS-REVERSE-TRANS.                                03100000
      *                                                                 03110000
           DISPLAY 'PROGRAM : ', PC-PROGRAM-NAME.                       03120000
      *                                                                 03130000
           INITIALIZE DEPT-TABLE                                        03140000
                      PV-PROGRAM-VARIABLES.                             03150000
                                                                        03160000
           PERFORM 4000-PROCESS-CONTROL-CARDS.                          03170000
           PERFORM 7000-GET-CONTROL-DATES.                              03180000
      *                                                                 03190000
      * READ THE FIRST RECORD FROM EACH FILE                            03200000
           IF PS-CONTINUE-PROCESSING                                    03210000
               PERFORM 5000-READ-SALES-FILE                             03220000
               PERFORM 5100-READ-PCHG-FILE                              03230000
           END-IF.                                                      03240000
      *                                                                 03250000
      *                                                                 03260000
      ******************************************************************03270000
      ***   MAIN PROCESSING FOR DEPT 099-10 TRANSACTION REVERSALS.      03280000
      ***            TRANSACTION CODES (ML020-TRANS-CDE)                03290000
      *** PERFORM....                                                   03300000
      *** 2100-PROCESS-SALES                                            03310000
      ***    TRANS-CDE '20' PROCESS ALL SALES REVERSALS/CORRECTIONS     03320000
      ***                   UNTIL ALL RECORDS HAVE BEEN RE-ALLOCATED    03330000
      *** 2200-PROCESS-PCHG                                             03340000
      ***    TRANS-CDE '40','50' PROCESS PRICE CHANGE RECORDS           03350000
      ***                   UNTIL ALL RECORDS HAVE BEEN RE-ALLOCATED    03360000
      ***                                                               03370000
      ******************************************************************03380000
       2000-PROCESS-REVERSALS.                                          03390000
      *                                                                 03400000
           PERFORM 2100-PROCESS-SALE-REVERSALS                          03410000
               UNTIL PS-SALE-COMPLETE.                                  03420000
      *                                                                 03430000
           PERFORM 2200-PROCESS-PCHG-REVERSALS                          03440000
               UNTIL PS-PCHG-COMPLETE.                                  03450000
      *                                                                 03460000
           IF PS-SALE-COMPLETE AND                                      03470000
              PS-PCHG-COMPLETE                                          03480000
                  SET PS-PROCESSING-COMPLETE TO TRUE                    03490000
           END-IF.                                                      03500000
      *                                                                 03510000
      *                                                                 03520000
      ******************************************************************03530000
      * REVERSALS OF SALES RECORDS                                      03540000
      ****************************                                      03550000
      *** IF EXTENDED RETAIL FROM SALES FILE = 0, DO NOT ATTEMPT TO     03560000
      *** CREATE REVERSAL TRANSACTIONS FOR TOP TEN DEPARTMENTS. INSTEAD 03570000
      *** READ THROUGH SALES FILE UNTIL A NON-0 ROW IS RETRIEVED.       03580000
      ******************************************************************03590000
       2100-PROCESS-SALE-REVERSALS.                                     03600000
      * IF SLS ARE 0, THEN REVERSAL NEW RECORD WILL NOT BE CREATED      03610000
      *                                                                 03620000
           INITIALIZE ML020-COMPOSITE-RECORD                            03630000
                      ML020-SALES-RECORD.                               03640000
      *                                                                 03650000
           MOVE WS-SALE-COMPOSITE-LAYOUT TO                             03660000
                ML020-COMPOSITE-RECORD.                                 03670000
      *                                                                 03680000
           PERFORM 2300-CALC-SLS-ADJUSTMENT.                            03690000
      *                                                                 03700000
      * PERFORM THIS LOOP IF THE RECORD IS NOT IN ERROR                 03710000
           IF PS-VALID-RECORD                                           03720000
               MOVE +1 TO PV-SUB                                        03730000
               MOVE CT-OPEN-YY     TO ML020-SALE-DTE-YY                 03740000
               MOVE CT-OPEN-MM     TO ML020-SALE-DTE-MM                 03750000
               MOVE CT-OPEN-DD     TO ML020-SALE-DTE-DD                 03760000
      * PERFORM FOLLOWING LOGIC ONE TIME FOR EACH OF TOP 40  DEPTS      03770000
SMJ            PERFORM 40 TIMES                                         03780000
                   PERFORM 3100-APPLY-SALES-ADJUSTMENT                  03790000
                   PERFORM 6000-WRITE-OUTPUT-RECORD                     03800000
                   ADD 1      TO PV-SUB                                 03810000
               END-PERFORM                                              03820000
           END-IF.                                                      03830000
                                                                        03840000
           PERFORM 5000-READ-SALES-FILE.                                03850000
      *                                                                 03860000
      *                                                                 03870000
      ******************************************************************03880000
      * REVERSALS OF PRICE CHANGE RECORDS                               03890000
      ******************************************************************03900000
       2200-PROCESS-PCHG-REVERSALS.                                     03910000
      *                                                                 03920000
           INITIALIZE ML020-COMPOSITE-RECORD                            03930000
                      ML020-PRICE-CHANGE-RECORD.                        03940000
      *                                                                 03950000
           MOVE ML129-PC-RECORD TO ML020-COMPOSITE-RECORD.              03960000
      *                                                                 03970000
           PERFORM 2400-CALC-PCHG-ADJUSTMENT.                           03980000
      *                                                                 03990000
           IF PS-VALID-RECORD                                           04000000
               MOVE +1 TO PV-SUB                                        04010000
               MOVE CT-OPEN-MM     TO ML020-PC-DTE-MM                   04020000
               MOVE CT-OPEN-DD     TO ML020-PC-DTE-DD                   04030000
      * PERFORM FOLLOWING LOGIC ONE TIME FOR EACH OF TOP 40  DEPTS      04040000
SMJ            PERFORM 40 TIMES                                         04050000
                   PERFORM 3200-APPLY-PRCHG-ADJUSTMENT                  04060000
      *            PERFORM 6000-WRITE-OUTPUT-RECORD                     04070000
                   ADD 1      TO PV-SUB                                 04080000
               END-PERFORM                                              04090000
           END-IF.                                                      04100000
                                                                        04110000
           MOVE 'N' TO PS-VALID-RECORD-SW.                              04120000
           PERFORM 5100-READ-PCHG-FILE.                                 04130000
      *                                                                 04140000
      ******************************************************************04150000
      * CALCULATE THE REVERSAL ADJUSTMENT AMOUNT - SALES '20' RECORDS   04160000
      ******************************************************************04170000
       2300-CALC-SLS-ADJUSTMENT.                                        04180000
      *                                                                 04190000
           IF SLS-SALE-EXTD-RTL-AMT NOT EQUAL 0                         04200000
               DIVIDE SLS-SALE-EXTD-RTL-AMT                             04210000
                   BY PC-REVERSAL-DEPT-COUNT                            04220000
                       GIVING PV-REVERSE-SLS-AMT                        04230000
                           REMAINDER PV-REVERSE-SLS-REMAINDER           04240000
               SET PS-VALID-RECORD TO TRUE                              04250000
           ELSE                                                         04260000
               DISPLAY '** SALES RECORD AMOUNT $ 0.00  '                04270000
               DISPLAY ML020-SALES-RECORD                               04280000
               PERFORM 6100-WRITE-ERROR-RECORD                          04290000
           END-IF.                                                      04300000
      *                                                                 04310000
      *                                                                 04320000
      *                                                                 04330000
      ******************************************************************04340000
      * CALCULATE THE REVERSAL ADJUSTMENT FOR PRICE CHG '40','50' RECS  04350000
      *******                                                           04360000
      * IF OLD-RETAIL = 0 AND NEW-RETAIL = 0 (WRITE ERROR RECORD)       04370000
      * IF OLD-RETAIL = 0 AND NEW-RETAIL NOT 0 (MOVE ZERO/CALC NEW)     04380000
      * IF NEW-RETAIL = 0 AND OLD-RETAIL NOT 0 (MOVE ZERO/CALC NEW)     04390000
      ******************************************************************04400000
       2400-CALC-PCHG-ADJUSTMENT.                                       04410000
      *                                                                 04420000
      * OLD-RTL <> 0                                                    04430000
           IF  ML129-PC-EXTD-OLD-PR-AMT NOT EQUAL +0                    04440000
               DIVIDE ML129-PC-EXTD-OLD-PR-AMT                          04450000
                   BY PC-REVERSAL-DEPT-COUNT                            04460000
                       GIVING PV-REVERSE-OLD-AMT                        04470000
                           REMAINDER PV-REVERSE-OLD-REMAINDER           04480000
      *                                                                 04490000
      * NEW RTL <> 0                                                    04500000
               IF  ML129-PC-EXTD-NEW-PR-AMT NOT EQUAL +0                04510000
                   DIVIDE ML129-PC-EXTD-NEW-PR-AMT                      04520000
                       BY PC-REVERSAL-DEPT-COUNT                        04530000
                           GIVING PV-REVERSE-NEW-AMT                    04540000
                                REMAINDER PV-REVERSE-NEW-REMAINDER      04550000
      *                                                                 04560000
                   SET PS-VALID-RECORD TO TRUE                          04570000
      * OLD RTL <> 0 / NEW RTL = 0                                      04580000
               ELSE                                                     04590000
                   MOVE +0 TO PV-REVERSE-NEW-AMT                        04600000
                              PV-REVERSE-NEW-REMAINDER                  04610000
                   SET PS-VALID-RECORD TO TRUE                          04620000
               END-IF                                                   04630000
      *                                                                 04640000
      * OLD RTL = 0                                                     04650000
           ELSE                                                         04660000
               MOVE +0 TO PV-REVERSE-OLD-AMT                            04670000
                          PV-REVERSE-OLD-REMAINDER                      04680000
      * NEW RTL <> 0                                                    04690000
               IF  ML129-PC-EXTD-NEW-PR-AMT NOT EQUAL +0                04700000
                   DIVIDE ML129-PC-EXTD-NEW-PR-AMT                      04710000
                       BY PC-REVERSAL-DEPT-COUNT                        04720000
                           GIVING PV-REVERSE-NEW-AMT                    04730000
                                REMAINDER PV-REVERSE-NEW-REMAINDER      04740000
      *                                                                 04750000
                   SET PS-VALID-RECORD TO TRUE                          04760000
      *                                                                 04770000
      * OLD RTL = 0 / NEW RTL = 0                                       04780000
               ELSE                                                     04790000
                   DISPLAY '** OLD/NEW PRCHG AMT = $ 0.00  '            04800000
                   DISPLAY 'ML129-PC-RECORD: ' , ML129-PC-RECORD        04810000
                   DISPLAY 'ML020-PC-RECORD: ' , ML020-COMPOSITE-RECORD 04820000
                   PERFORM 6100-WRITE-ERROR-RECORD                      04830000
               END-IF                                                   04840000
      *                                                                 04850000
           END-IF.                                                      04860000
      *                                                                 04870000
      *                                                                 04880000
      ***************************************************************** 04890000
      * POPULATE INTERNAL TABLE WITH TOP 40  DEPT THAT RECEIVE REVERSAL 04900000
      ***************************************************************** 04910000
       3000-LOAD-DEPT-TABLE.                                            04920000
                                                                        04930000
           ADD 1                        TO PV-SUB.                      04940000
                                                                        04950000
           MOVE CC3-DEPT-NBR            TO DT-DEPT-NBR(PV-SUB).         04960000
           MOVE CC3-SCL-NBR             TO DT-SCL-NBR(PV-SUB).          04970000
      *                                                                 04980000
           DISPLAY '-->  ', PV-SUB, ') ', DT-DEPT-NBR(PV-SUB), '-',     04990000
                               , DT-SCL-NBR(PV-SUB).                    05000000
                                                                        05010000
           PERFORM 4400-PROCESS-TOPTEN-LIST.                            05020000
      *                                                                 05030000
      *                                                                 05040000
      ***************************************************************** 05050000
      * POPULATE INTERNAL TABLE WITH TOP TEN DEPT THAT RECEIVE REVERSAL 05060000
      ***************************************************************** 05070000
       3100-APPLY-SALES-ADJUSTMENT.                                     05080000
                                                                        05090000
W21731     STRING DT-DEPT-NBR(PV-SUB) DELIMITED BY SIZE                 05100000
W21731            DT-SCL-NBR(PV-SUB)  DELIMITED BY SIZE                 05110000
W21731            PC-SEQ-NBR          DELIMITED BY SIZE                 05120000
W21731       INTO ML020-SKU                                             05130000
W21731     END-STRING.                                                  05140000
W21731                                                                  05150000
           IF PV-SUB < PC-REVERSAL-DEPT-COUNT                           05160000
               MOVE DT-DEPT-NBR(PV-SUB)     TO ML020-DEPT-NBR           05170000
               MOVE DT-SCL-NBR(PV-SUB)      TO ML020-CLASS-NBR          05180000
               MOVE PV-REVERSE-SLS-AMT      TO ML020-SALE-EXTD-RTL-AMT  05190000
      *                                                                 05200000
      *        DISPLAY '--> ', ML020-DEPT-NBR, ML020-CLASS-NBR, '  $',  05210000
      *                        ML020-SALE-EXTD-RTL-AMT                  05220000
           ELSE                                                         05230000
               MOVE DT-DEPT-NBR(PV-SUB)     TO ML020-DEPT-NBR           05240000
               MOVE DT-SCL-NBR(PV-SUB)      TO ML020-CLASS-NBR          05250000
               COMPUTE ML020-SALE-EXTD-RTL-AMT ROUNDED =                05260000
                   (PV-REVERSE-SLS-AMT + PV-REVERSE-SLS-REMAINDER)      05270000
      *                                                                 05280000
      *        DISPLAY '--> ', ML020-DEPT-NBR, ML020-CLASS-NBR, '  $',  05290000
      *                        ML020-SALE-EXTD-RTL-AMT                  05300000
           END-IF.                                                      05310000
R21731                                                                  05320000
R21731     MOVE ML020-CLASS-NBR             TO PV-MAJOR-CLASS.          05330000
R21731     MOVE '0'                         TO PV-MAJOR-CLASS-CHAR2.    05340000
R21731     MOVE PV-MAJOR-CLASS              TO ML020-MAJOR-CLASS.       05350000
                                                                        05360000
      *                                                                 05370000
      *                                                                 05380000
      ***************************************************************** 05390000
      * POPULATE INTERNAL TABLE WITH TOP 40  DEPT THAT RECEIVE REVERSAL 05400000
      ***************************************************************** 05410000
       3200-APPLY-PRCHG-ADJUSTMENT.                                     05420000
                                                                        05430000
W21731     STRING DT-DEPT-NBR(PV-SUB) DELIMITED BY SIZE                 05440000
W21731            DT-SCL-NBR(PV-SUB)  DELIMITED BY SIZE                 05450000
W21731            PC-SEQ-NBR          DELIMITED BY SIZE                 05460000
W21731       INTO ML020-SKU                                             05470000
W21731     END-STRING.                                                  05480000
W21731                                                                  05490000
           IF PV-SUB < PC-REVERSAL-DEPT-COUNT                           05500000
               MOVE DT-DEPT-NBR(PV-SUB)     TO ML020-DEPT-NBR           05510000
               MOVE DT-SCL-NBR(PV-SUB)      TO ML020-CLASS-NBR          05520000
               MOVE PV-REVERSE-OLD-AMT      TO ML020-PC-OLD-PR-AMT      05530000
               MOVE PV-REVERSE-NEW-AMT      TO ML020-PC-NEW-PR-AMT      05540000
      *                                                                 05550000
      *        DISPLAY '--> ', ML020-DEPT-NBR, ML020-CLASS-NBR, 'OLD ', 05560000
      *                        ML020-PC-OLD-PR-AMT, ' NEW ',            05570000
      *                        ML020-PC-NEW-PR-AMT                      05580000
           ELSE                                                         05590000
               MOVE DT-DEPT-NBR(PV-SUB)     TO ML020-DEPT-NBR           05600000
               MOVE DT-SCL-NBR(PV-SUB)      TO ML020-CLASS-NBR          05610000
               COMPUTE ML020-PC-OLD-PR-AMT ROUNDED =                    05620000
                   (PV-REVERSE-OLD-AMT + PV-REVERSE-OLD-REMAINDER)      05630000
               COMPUTE ML020-PC-NEW-PR-AMT ROUNDED =                    05640000
                   (PV-REVERSE-NEW-AMT + PV-REVERSE-NEW-REMAINDER)      05650000
      *                                                                 05660000
      *        DISPLAY '--> ', ML020-DEPT-NBR, ML020-CLASS-NBR, 'OLD ', 05670000
      *                        ML020-PC-OLD-PR-AMT, ' NEW ',            05680000
      *                        ML020-PC-NEW-PR-AMT                      05690000
           END-IF.                                                      05700000
                                                                        05710000
R21731                                                                  05720000
R21731     MOVE ML020-CLASS-NBR             TO PV-MAJOR-CLASS.          05730000
R21731     MOVE '0'                         TO PV-MAJOR-CLASS-CHAR2.    05740000
R21731     MOVE PV-MAJOR-CLASS              TO ML020-MAJOR-CLASS.       05750000
                                                                        05760000
           IF (ML020-PC-OLD-PR-AMT = 0    AND                           05770000
               ML020-PC-NEW-PR-AMT = 0)                                 05780000
               PERFORM 6100-WRITE-ERROR-RECORD                          05790000
           ELSE                                                         05800000
               PERFORM 6000-WRITE-OUTPUT-RECORD                         05810000
           END-IF.                                                      05820000
      *                                                                 05830000
      *                                                                 05840000
      ******************************************************************05850000
      * VALUES IN CONTROL CARDS DETERMINES PROCESSING OPTIONS           05860000
      * IF REVERSALS ARE TO BE PROCESSED - LOAD INTERNAL TABLE          05870000
      ******************************************************************05880000
       4000-PROCESS-CONTROL-CARDS.                                      05890000
      *                                                                 05900000
           PERFORM 4100-PROCESS-DATE-FILE.                              05910000
           PERFORM 4200-PROCESS-CNTL-FILE.                              05920000
           PERFORM 4300-PROCESS-OPTIONS.                                05930000
      *                                                                 05940000
      * IF UPDATE CRITERIA MET, CONTINUE BY LOADING INTERNAL TABLE      05950000
      *    OTHERWISE SET SWITCH TO INDICATE NO REVERSALS WILL BE RUN.   05960000
                                                                        05970000
           DISPLAY '*** TOP 40 LIST ***'.                               05980000
           PERFORM 4400-PROCESS-TOPTEN-LIST.                            05990000
           PERFORM 3000-LOAD-DEPT-TABLE                                 06000000
               UNTIL PS-TABLE-LOADED.                                   06010000
                                                                        06020000
      *AML                                                              06030000
      *AML IF PS-CONTINUE-PROCESSING                                    06040000
      *AML     DISPLAY '*** TOPTEN LIST ***'                            06050000
      *AML     PERFORM 4400-PROCESS-TOPTEN-LIST                         06060000
      *AML     PERFORM 3000-LOAD-DEPT-TABLE                             06070000
      *AML         UNTIL PS-TABLE-LOADED                                06080000
      *AML ELSE                                                         06090000
      *AML     SET PS-PROCESSING-COMPLETE TO TRUE                       06100000
      *AML END-IF.                                                      06110000
      *                                                                 06120000
      *                                                                 06130000
      *                                                                 06140000
      ******************************************************************06150000
      * READ IN CONTROL CARD CONTAINING CURRENT SYSTEM DATE             06160000
      ******************************************************************06170000
       4100-PROCESS-DATE-FILE.                                          06180000
                                                                        06190000
           READ CONTROL-DATE-FILE   INTO CC1-SYSTEM-DTE                 06200000
             AT END                                                     06210000
               SET PS-EMPTY-CC1-FILE TO TRUE                            06220000
           END-READ.                                                    06230000
                                                                        06240000
           IF  PS-EMPTY-CC1-FILE                                        06250000
               DISPLAY '** PROGRAM ABEND **'                            06260000
               DISPLAY '** SYSTEM CONTROL CARD INVALID ',               06270000
               MOVE +4003              TO ABEND-CODE                    06280000
               PERFORM 9999-ABEND                                       06290000
           END-IF                                                       06300000
      *                                                                 06310000
           DISPLAY '  SYSTEM DATE : ', CC1-SYSTEM-DTE.                  06320000
      *                                                                 06330000
      *                                                                 06340000
      ******************************************************************06350000
      * ENSURE POS CONTROL RECORD (SACC001) EXISTS-DISPLAY 'CURRENT' DTE06360000
      ******************************************************************06370000
       4200-PROCESS-CNTL-FILE.                                          06380000
                                                                        06390000
           READ CONTROL-CARD-FILE INTO CC2-PROCESS-REC                  06400000
             AT END                                                     06410000
               SET PS-EMPTY-CC2-FILE TO TRUE                            06420000
           END-READ.                                                    06430000
                                                                        06440000
           IF  PS-EMPTY-CC2-FILE                                        06450000
               DISPLAY '** PROGRAM ABEND **'                            06460000
               DISPLAY '** CONTROL CARD INVALID ',                      06470000
               MOVE +4003              TO ABEND-CODE                    06480000
               PERFORM 9999-ABEND                                       06490000
           END-IF                                                       06500000
      *                                                                 06510000
           DISPLAY 'POS CNTL DATE : ', CC2-CURRENT-DTE, ' ',            06520000
                                       CC2-NO-PROCESS-IND.              06530000
      *                                                                 06540000
      *                                                                 06550000
      *                                                                 06560000
      ******************************************************************06570000
      * DETERMINE IF REVERSALS SHOULD BE PROCESSED BASED ON INPUTS.     06580000
      * PROCESS ONLY IF DATES ARE EQUAL AND INIDICATOR IS SET TO 'Y' .  06590000
      *                                                                 06600000
      * CONTROL CARD IS NO LONGER BEING USED TO CONTROL PROCESSING.     06610000
      ******************************************************************06620000
       4300-PROCESS-OPTIONS.                                            06630000
                                                                        06640000
           SET PS-CONTINUE-PROCESSING TO TRUE.                          06650000
                                                                        06660000
      *AML                                                              06670000
      *AML IF CC1-SYSTEM-DTE EQUAL  CC2-CURRENT-DTE                     06680000
      *AML     IF CC2-PROCESS-DEPTSCL                                   06690000
      *AML         SET PS-CONTINUE-PROCESSING TO TRUE                   06700000
      *AML     ELSE                                                     06710000
      *AML         DISPLAY '*** CONTROL FILE INDICATOR MUST EQUAL ',    06720000
      *AML         , '''Y'' -- NO REVERSALS WILL BE PROCESSED ***'      06730000
      *AML     END-IF                                                   06740000
      *AML ELSE                                                         06750000
      *AML     DISPLAY '*** CONTROL DATES MUST BE EQUAL - NO REVERSALS '06760000
      *AML           , 'WILL BE PROCESSED FOR THIS RUN ***'             06770000
      *AML END-IF.                                                      06780000
                                                                        06790000
      *                                                                 06800000
      ******************************************************************06810000
      * READ IN CONTROL CARD CONTAINING DEPT/SCL TO APPLY REVERSALS     06820000
      ******************************************************************06830000
       4400-PROCESS-TOPTEN-LIST.                                        06840000
                                                                        06850000
           READ TOPTEN-SALES-FILE   INTO CC3-REVERSAL-LIST              06860000
             AT END                                                     06870000
               SET PS-TABLE-LOADED TO TRUE                              06880000
               DISPLAY '*** END OF LIST ***'                            06890000
           END-READ.                                                    06900000
      *                                                                 06910000
      *                                                                 06920000
      ******************************************************************06930000
      * READ A RECORD FROM SALES REVERSAL FILE                          06940000
      ******************************************************************06950000
       5000-READ-SALES-FILE.                                            06960000
      *                                                                 06970000
           READ SALES-REVERSAL-FILE                                     06980000
               INTO WS-SALE-COMPOSITE-LAYOUT                            06990000
           AT END SET PS-SALE-COMPLETE TO TRUE.                         07000000
      *                                                                 07010000
      *                                                                 07020000
      *                                                                 07030000
      ******************************************************************07040000
      * READ A RECORD FROM THE PRICE CHANGE REVERSAL FILE               07050000
      ******************************************************************07060000
       5100-READ-PCHG-FILE.                                             07070000
      *                                                                 07080000
           READ PRICE-CHANGE-REVERSAL-FILE                              07090000
               INTO ML129-PC-RECORD                                     07100000
           AT END SET PS-PCHG-COMPLETE TO TRUE.                         07110000
      *                                                                 07120000
      *                                                                 07130000
      ******************************************************************07140000
      * WRITE A RECORD TO THE OUTPUT FILE                               07150000
      ******************************************************************07160000
       6000-WRITE-OUTPUT-RECORD.                                        07170000
      *                                                                 07180000
           WRITE REVERSAL-OUTPUT-RECORD FROM ML020-COMPOSITE-RECORD.    07190000
      *                                                                 07200000
      *                                                                 07210000
      ******************************************************************07220000
      * WRITE AN ERROR RECORD                                           07230000
      ******************************************************************07240000
       6100-WRITE-ERROR-RECORD.                                         07250000
      *                                                                 07260000
           WRITE ERROR-OUTPUT-RECORD FROM ML020-COMPOSITE-RECORD.       07270000
      *                                                                 07280000
      *                                                                 07290000
      ******************************************************************07300000
       7000-GET-CONTROL-DATES.                                          07310000
      ******************************************************************07320000
      *                                                                 07330000
           EXEC SQL                                                     07340000
               SELECT OPN_FSCL_MN_DTE                                   07350000
               INTO   :MLCNTL-OPN-FSCL-MN-DTE                           07360000
               FROM   TMLCNTL                                           07370000
           END-EXEC                                                     07380000
                                                                        07390000
           EVALUATE SQLCODE                                             07400000
               WHEN ZERO                                                07410000
                   MOVE MLCNTL-OPN-FSCL-MN-DTE TO CNTL-TABLE-DATE       07420000
               WHEN OTHER                                               07430000
                   DISPLAY '** PROGRAM ABEND **'                        07440000
                   DISPLAY '** COULD NOT ACCESS CONTROL TABLE ***'      07450000
                   MOVE +4003              TO ABEND-CODE                07460000
                   PERFORM 9999-ABEND                                   07470000
           END-EVALUATE.                                                07480000
      ******************************************************************07490000
      *                                                                 07500000
      ******************************************************************07510000
      *                                                                 07520000
      *                     ABEND ROUTINE                               07530000
      *                                                                 07540000
      ******************************************************************07550000
       9999-ABEND.                                                      07560000
                                                                        07570000
           CALL 'ILBOABN0'             USING ABEND-CODE.                07580000
                                                                        07590000
                                                                        07600000
