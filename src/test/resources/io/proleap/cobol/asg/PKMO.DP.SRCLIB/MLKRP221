000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    MLKRP221.                                                 
000300 AUTHOR.        TESS BESTE.                                               
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  12-20-99.                                                 
000600 DATE-COMPILED.                                                           
000700******************************************************************        
000800*                                                                         
000900*   MLKRP221 - CREATE DEPARTMENT ELIMINATION REPORT BY BUYER              
001000*                                                                         
      * THIS PROGRAM READS THE BUYER REPORT EXTRACT FILE FROM MLKUT426          
      * AND CREATES THE DEPARTMENT ELIMINATION REPORT BY BUYER.                 
      *                                                                         
      * THE INPUT FILE IS IN REC TYPE, BUYER, DEPARTMENT, SUBCLASS              
      * SEQUENCE AND IS SUMMED TO THE SUBCLASS LEVEL. THE FIRST RECORD          
      * ON THE FILE IS A HEADER RECORD. IT CONTAINS THE FISCAL MONTH-           
      * -END DATE THAT IS USED IN THE REPORT HEADING AND IN THE DB2             
      * CALLS TO GET MBS INFORMATION. THE REMAINING RECORDS ARE DETAIL          
      * RECORDS. EACH DETAIL RECORD CONTAINS THE TOTAL RETAIL OF THE            
      * PURCHASE ADJUSTMENTS CREATED BY MLKUT426 FOR THAT SUBCLASS.             
      *                                                                         
      * THE REPORT CONTAINS ONE DETAIL LINE FOR EACH SUBCLASS.                  
      * TOTALS ARE PRODUCED AT THE MAJOR CLASS, DEPARTMENT, AND BUYER           
      * LEVEL.                                                                  
007000*                                                                         
001100******************************************************************        
001200                                                                          
001300 ENVIRONMENT DIVISION.                                                    
001500 CONFIGURATION SECTION.                                                   
001700 SOURCE-COMPUTER.        IBM-3090.                                        
001800 OBJECT-COMPUTER.        IBM-3090.                                        
002000 INPUT-OUTPUT SECTION.                                                    
002100 FILE-CONTROL.                                                            
002200                                                                          
002300     SELECT BYR-RPT-EXTRCT-FILE                                           
002400         ASSIGN TO INP01.                                                 
002500                                                                          
002600     SELECT DEPT-ELIM-BYR-RPT                                             
002700         ASSIGN TO RPT01.                                                 
002800                                                                          
003000 DATA DIVISION.                                                           
003300 FILE SECTION.                                                            
003400                                                                          
003500 FD  BYR-RPT-EXTRCT-FILE                                                  
003600     LABEL RECORDS ARE STANDARD                                           
003700     RECORDING MODE IS F                                                  
003800     BLOCK CONTAINS 0 RECORDS.                                            
003900 01  BYR-RPT-EXTRCT-REC          PIC X(80).                               
004000                                                                          
004100 FD  DEPT-ELIM-BYR-RPT                                                    
004200     LABEL RECORDS ARE OMITTED                                            
004300     RECORDING MODE IS F                                                  
004400     BLOCK CONTAINS 0 RECORDS.                                            
004500 01  DEPT-ELIM-BYR-RPT-REC       PIC X(132).                              
004600                                                                          
004900 WORKING-STORAGE SECTION.                                                 
005100                                                                          
019000******************************************************************        
019100* DEPARTMENT ELIMINATION BUYER REPORT EXTRACT RECORD LAYOUT               
019200******************************************************************        
018700                                                                          
018600     COPY MLRD119.                                                        
018700                                                                          
019000******************************************************************        
019100* STANDARD REPORT HEADING                                                 
019200******************************************************************        
019300                                                                          
019400     COPY DPWS132O.                                                       
019500                                                                          
019500                                                                          
006600 01  PC-PROGRAM-CONSTANTS.                                                
009700     05  PC-PGM-NAME             PIC X(08)     VALUE 'MLKRP221'.          
009720     05  PC-LINE-CNT-MAX         PIC S9(03)    VALUE +60.                 
009721     05  PC-OPERCO-NBR           PIC X(02)     VALUE '03'.                
                                                                                
006610     05  PC-LV-CDE-MCL           PIC X(03)     VALUE 'MCL'.               
006611     05  PC-LV-CDE-DPT           PIC X(03)     VALUE 'DPT'.               
006620     05  PC-RSPLVL-CDE-BYR       PIC X(03)     VALUE 'BYR'.               
007100                                                                          
005200 01  PS-PROGRAM-SWITCHES.                                                 
005300     05  PS-END-OF-RPT-EXTRCT-FILE-SW PIC X VALUE 'Y'.                    
005400         88  PS-NOT-END-OF-RPT-EXTRCT-FILE  VALUE 'Y'.                    
005500         88  PS-END-OF-RPT-EXTRCT-FILE      VALUE 'N'.                    
005900                                                                          
007200 01  PV-PROGRAM-VARIABLES.                                                
007210     05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
013500     05  PV-PARAGRAPH-NAME            PIC X(30).                          
013600     05  PV-ABEND-CODE                PIC S9(04) COMP SYNC.               
013750     05  PV-DISPLAY-CNT               PIC ZZZ,ZZZ,ZZ9.                    
           05  PV-DB2-MAJ-CL-NBR            PIC X(02).                          
           05  PV-MAJ-CL-NBR.                                                   
               10  PV-MAJ-CL-NBR-POS1       PIC X.                              
               10  FILLER                   PIC X      VALUE '0'.               
           05  PV-PREV-BYR-NBR              PIC X(03)  VALUE SPACE.             
           05  PV-PREV-DEPT-NBR             PIC X(03)  VALUE SPACE.             
           05  PV-PREV-MAJ-CL-NBR           PIC X(02)  VALUE SPACE.             
                                                                                
008000     05  PV-TIME-HHMMSS.                                                  
008100         10  PV-TIME-HHMMSS-HH   PIC XX.                                  
008200         10  PV-TIME-HHMMSS-MM   PIC XX.                                  
008300         10  PV-TIME-HHMMSS-SS   PIC XX.                                  
008400                                                                          
007401                                                                          
014618*    **  COUNTS.                                                          
007300     05  PV-LINE-CNT             PIC S9(3) COMP-3  VALUE +0.              
007400     05  PV-PAGE-CNT             PIC S9(7) COMP-3  VALUE +0.              
014624     05  PV-BYR-RPT-EXTRCT-RECS-READ                                      
014625                                 PIC S9(09) COMP-3 VALUE +0.              
014618*    **  DATES.                                                           
007510     05  PV-FSCL-MN-END-DTE         PIC X(10).                            
                                                                                
007600     05  PV-CURR-DATE-YYMMDD.                                             
007700         10  PV-CURR-DATE-YYMMDD-YY PIC XX.                               
007800         10  PV-CURR-DATE-YYMMDD-MM PIC XX.                               
007900         10  PV-CURR-DATE-YYMMDD-DD PIC XX.                               
009700                                                                          
009901*    **  TOTALS.                                                          
009902     05  PV-END-INV-RTL-MCL-TOT   PIC S9(09)V99 COMP-3 VALUE +0.          
009920     05  PV-END-INV-RTL-DEPT-TOT  PIC S9(09)V99 COMP-3 VALUE +0.          
009940     05  PV-END-INV-RTL-BYR-TOT   PIC S9(09)V99 COMP-3 VALUE +0.          
011200                                                                          
018400                                                                          
019900 01  RH1-RPT-HEADING-1.                                                   
020100     05  FILLER                  PIC X(55)   VALUE SPACES.                
020200     05  FILLER                  PIC X(38)                                
020300         VALUE 'DEPARTMENT ELIMINATION REPORT BY BUYER'.                  
020400     05  FILLER                  PIC X(55)   VALUE SPACES.                
020500                                                                          
020501 01  RH2-RPT-HEADING-2.                                           02870021
020502     05  FILLER                  PIC X(45)   VALUE SPACE.         02880021
020503     05  FILLER                  PIC X(24)   VALUE                02890021
020504         'FOR FISCAL MONTH ENDING'.                               02900021
020505     05  FILLER                  PIC X(01)   VALUE SPACE.         02910021
020506     05  RH2-FROM-DATE.                                           02920021
020507         10  RH2-FROM-MM         PIC X(02)   VALUE ZEROS.         02930021
020508         10  FILLER              PIC X(01)   VALUE '/'.           02940021
020509         10  RH2-FROM-DD         PIC X(02)   VALUE ZEROS.         02950021
020510         10  FILLER              PIC X(01)   VALUE '/'.           02960021
020511         10  RH2-FROM-CCYY       PIC X(04)   VALUE ZEROS.         02970021
020512     05  FILLER                  PIC X(17)                        02980021
020513                                 VALUE ' - BATCH NUMBERS '.       02990021
020514     05  RH2-DR-BATCH-NBR        PIC X(03).                       03000021
020517     05  FILLER                  PIC X(03)   VALUE ' - '.         03010021
020520     05  RH2-CR-BATCH-NBR        PIC X(03).                       03020021
021700                                                                          
022100 01  RH3-RPT-HEADING-3.                                                   
022300     05  FILLER                  PIC X(06)   VALUE SPACE.                 
022200     05  FILLER                  PIC X(05)   VALUE 'BUYER'.               
022300     05  FILLER                  PIC X(24)   VALUE SPACE.                 
022400     05  FILLER                  PIC X(10)   VALUE 'DEPARTMENT'.          
022500     05  FILLER                  PIC X(15)   VALUE SPACE.                 
022600     05  FILLER                  PIC X(05)   VALUE 'CLASS'.               
022700     05  FILLER                  PIC X(21)   VALUE SPACE.                 
022800     05  FILLER                  PIC X(08)   VALUE 'SUBCLASS'.            
022900     05  FILLER                  PIC X(27)   VALUE SPACE.                 
023000     05  FILLER                  PIC X(06)   VALUE 'RETAIL'.              
023400                                                                          
023800 01  DL-DETAIL-LINE.                                                      
           05  DL-BYR-NBR              PIC X(03)   VALUE SPACE.                 
           05  DL-BYR-HYPHEN           PIC X       VALUE SPACE.                 
           05  DL-BYR-NAME             PIC X(26)   VALUE SPACE.                 
024000     05  FILLER                  PIC X       VALUE SPACE.                 
024120     05  DL-DEPT-NBR             PIC X(03)   VALUE SPACE.                 
024130     05  DL-DEPT-HYPHEN          PIC X(01)   VALUE SPACE.                 
024140     05  DL-DEPT-DESC            PIC X(21)   VALUE SPACE.                 
024200     05  FILLER                  PIC X       VALUE SPACE.                 
024320     05  DL-MAJ-CL-NBR           PIC X(02)   VALUE SPACE.                 
024330     05  DL-MAJ-CL-HYPHEN        PIC X       VALUE SPACE.                 
024350     05  DL-MAJ-CL-DESC          PIC X(22)   VALUE SPACE.                 
024400     05  FILLER                  PIC X       VALUE SPACE.                 
024410     05  DL-SUB-CL.                                                       
024420         10  DL-SUB-CL-NBR       PIC X(02)   VALUE SPACE.                 
024430         10  DL-SUB-CL-HYPHEN    PIC X       VALUE SPACE.                 
024450         10  DL-SUB-CL-DESC      PIC X(22)   VALUE SPACE.                 
024600     05  FILLER                  PIC X(02)   VALUE SPACE.                 
024700     05  DL-RTL-AMT              PIC ZZZ,ZZZ,ZZZ,ZZ9.99-.                 
025000                                                                          
025400 01  ER-END-OF-REPORT-LINE.                                               
025500     05  FILLER                  PIC X(21)   VALUE                        
025600                                    '*** END OF REPORT ***'.              
025700                                                                          
025900                                                                          
025910******************************************************************        
025920*  DB2 ERROR MESSAGES FORMAT AREA.                                        
025930******************************************************************        
025940     COPY DPWS004.                                                        
025950                                                                          
026000******************************************************************        
026100*  DB2 COMMUNICATION AREA                                                 
026200******************************************************************        
026300     EXEC SQL                                                             
026400          INCLUDE SQLCA                                                   
026500     END-EXEC.                                                            
026600                                                                          
027200******************************************************************        
027300*  DB2 MBS RESPONSIBILTY AREA TABLE                                       
027400******************************************************************        
027500     EXEC SQL                                                             
027600          INCLUDE TRSPBAR                                                 
027700     END-EXEC.                                                            
027800                                                                          
027810******************************************************************        
027820*  DB2 MERCHANDISE AREA TABLE                                             
027830******************************************************************        
027840     EXEC SQL                                                             
027850          INCLUDE TMDSEAR                                                 
027860     END-EXEC.                                                            
027870                                                                          
057200 PROCEDURE DIVISION.                                                      
057400                                                                          
057500 0000-MAIN-ROUTINE.                                                       
057600                                                                          
057700     PERFORM 1000-INITIALIZATION.                                         
057800                                                                          
057910     PERFORM 6000-READ-BYR-RPT-EXTRCT-FILE.                               
057920                                                                          
057930     IF PS-NOT-END-OF-RPT-EXTRCT-FILE                                     
057940         MOVE ML119-SUB-CL-NBR(1:1) TO PV-MAJ-CL-NBR-POS1                 
057950         PERFORM 2175-PREP-FOR-NEW-MAJ-CL                                 
057960         PERFORM 2275-PREP-FOR-NEW-DEPT                                   
057970         PERFORM 2375-PREP-FOR-NEW-BYR                                    
057980                                                                          
057990         PERFORM 2000-PROCESS-RPT-EXTRCT-FILE                             
057991           UNTIL PS-END-OF-RPT-EXTRCT-FILE                                
           ELSE                                                                 
059798         PERFORM 7000-WRITE-RPT-HEADINGS                                  
057992     END-IF.                                                              
058000                                                                          
058100     PERFORM 2150-FINISH-PREV-MAJ-CL.                                     
058200     PERFORM 2250-FINISH-PREV-DEPT.                                       
058300     PERFORM 2350-FINISH-PREV-BYR.                                        
058400                                                                          
090700     WRITE DEPT-ELIM-BYR-RPT-REC FROM ER-END-OF-REPORT-LINE               
090800         AFTER ADVANCING 3 LINES.                                         
                                                                                
014624     MOVE PV-BYR-RPT-EXTRCT-RECS-READ TO PV-DISPLAY-CNT.                  
           DISPLAY 'NUMBER OF REPORT EXTRACT RECORDS READ: '                    
014624              PV-BYR-RPT-EXTRCT-RECS-READ.                                
                                                                                
058500     CLOSE BYR-RPT-EXTRCT-FILE                                            
058600           DEPT-ELIM-BYR-RPT.                                             
058700                                                                          
058800     STOP RUN.                                                            
058900                                                                          
059000                                                                          
059100 1000-INITIALIZATION.                                                     
059200                                                                          
059300     OPEN INPUT  BYR-RPT-EXTRCT-FILE.                                     
059400     OPEN OUTPUT DEPT-ELIM-BYR-RPT.                                       
059500                                                                          
059600     INITIALIZE PV-PROGRAM-VARIABLES.                                     
059700                                                                          
059701*    READ THE HEADER RECORD FROM THE EXTRCT FILE.                         
059710     PERFORM 6000-READ-BYR-RPT-EXTRCT-FILE.                               
059720                                                                          
059722     IF PS-END-OF-RPT-EXTRCT-FILE                                         
059723         MOVE +4008 TO PV-ABEND-CODE                                      
059724         DISPLAY '** ABEND **'                                            
059725         DISPLAY '** PROGRAM   = ' PC-PGM-NAME                            
059726         DISPLAY '** PARAGRAPH =  1000-INITIALIZATION'                    
059727         DISPLAY '** BYR-RPT-EXTRCT-FILE IS EMPTY.'                       
059728         PERFORM 9999-ABEND                                               
059730     END-IF.                                                              
059731                                                                          
059741     IF NOT ML119-REC-TYP-HDR                                             
059750         MOVE +4008 TO PV-ABEND-CODE                                      
059760         DISPLAY '** ABEND **'                                            
059770         DISPLAY '** PROGRAM   = ' PC-PGM-NAME                            
059780         DISPLAY '** PARAGRAPH =  1000-INITIALIZATION'                    
059790         DISPLAY '** 1ST RECRD ON BYR-RPT-EXTRCT-FILE NOT HEADER.'        
059791         PERFORM 9999-ABEND                                               
059792     END-IF.                                                              
059793                                                                          
059794     DISPLAY 'REPORT EXTRCT FILE HEADER RECORD: '                         
059795              ML119-DEPT-ELIM-BY-BYR-EXTRACT.                             
059796     MOVE ML119-FSCL-MN-END-DTE TO PV-FSCL-MN-END-DTE.                    
059797     PERFORM 1100-BUILD-RPT-HEADINGS.                                     
060200                                                                          
060300                                                                          
060310 1100-BUILD-RPT-HEADINGS.                                                 
060320                                                                          
060330     ACCEPT PV-CURR-DATE-YYMMDD FROM DATE.                                
060340     MOVE PV-CURR-DATE-YYMMDD-YY      TO DP132O-RUN-YEAR.                 
060350     MOVE PV-CURR-DATE-YYMMDD-MM      TO DP132O-RUN-MONTH.                
060360     MOVE PV-CURR-DATE-YYMMDD-DD      TO DP132O-RUN-DAY.                  
060370                                                                          
060380     ACCEPT PV-TIME-HHMMSS FROM TIME.                                     
060390     MOVE PV-TIME-HHMMSS-HH           TO DP132O-RUN-HOUR.                 
060391     MOVE PV-TIME-HHMMSS-MM           TO DP132O-RUN-MINUTE.               
060392     MOVE 'MLKRP221'                  TO DP132O-PROGRAM-NAME.             
060393     MOVE 1                           TO DP132O-REPORT-NUMBER.            
060394                                                                          
060395     MOVE ML119-FSCL-MN-END-DTE (6:2) TO RH2-FROM-MM.                     
060396     MOVE ML119-FSCL-MN-END-DTE (9:2) TO RH2-FROM-DD.                     
060397     MOVE ML119-FSCL-MN-END-DTE (1:4) TO RH2-FROM-CCYY.                   
060398                                                                          
060399     MOVE ML119-DR-BATCH-NBR          TO RH2-DR-BATCH-NBR.                
060400     MOVE ML119-CR-BATCH-NBR          TO RH2-CR-BATCH-NBR.                
060401                                                                          
062100                                                                          
066000 2000-PROCESS-RPT-EXTRCT-FILE.                                            
066200                                                                          
066300     MOVE ML119-SUB-CL-NBR(1:1) TO PV-MAJ-CL-NBR-POS1.                    
066400                                                                          
066500     IF ML119-BYR-NBR NOT = PV-PREV-BYR-NBR                               
071101         PERFORM 2150-FINISH-PREV-MAJ-CL                                  
070200         PERFORM 2250-FINISH-PREV-DEPT                                    
070800         PERFORM 2350-FINISH-PREV-BYR                                     
071200         PERFORM 2175-PREP-FOR-NEW-MAJ-CL                                 
070300         PERFORM 2275-PREP-FOR-NEW-DEPT                                   
070900         PERFORM 2375-PREP-FOR-NEW-BYR                                    
066900     ELSE                                                                 
067000         IF ML119-DEPT-NBR NOT = PV-PREV-DEPT-NBR                         
071101             PERFORM 2150-FINISH-PREV-MAJ-CL                              
070200             PERFORM 2250-FINISH-PREV-DEPT                                
071200             PERFORM 2175-PREP-FOR-NEW-MAJ-CL                             
070300             PERFORM 2275-PREP-FOR-NEW-DEPT                               
067300         ELSE                                                             
067400             IF PV-MAJ-CL-NBR NOT = PV-PREV-MAJ-CL-NBR                    
071101                 PERFORM 2150-FINISH-PREV-MAJ-CL                          
071200                 PERFORM 2175-PREP-FOR-NEW-MAJ-CL                         
067600             END-IF                                                       
067700         END-IF                                                           
067800     END-IF.                                                              
067900                                                                          
068000     PERFORM 7100-WRITE-SUB-CL-LINE.                                      
069000                                                                          
069010     ADD ML119-END-INV-RTL-AMT TO PV-END-INV-RTL-MCL-TOT.                 
069030                                                                          
069100     PERFORM 6000-READ-BYR-RPT-EXTRCT-FILE.                               
069200                                                                          
069300                                                                          
071101 2150-FINISH-PREV-MAJ-CL.                                                 
071102                                                                          
071103     PERFORM 7200-WRITE-MAJ-CL-TOTAL.                                     
071110     ADD PV-END-INV-RTL-MCL-TOT  TO PV-END-INV-RTL-DEPT-TOT.              
071130                                                                          
071140                                                                          
071200 2175-PREP-FOR-NEW-MAJ-CL.                                                
071300                                                                          
071320     MOVE PV-MAJ-CL-NBR      TO DL-MAJ-CL-NBR.                            
071321     MOVE '-'                TO DL-MAJ-CL-HYPHEN.                         
                                                                                
           MOVE PV-MAJ-CL-NBR      TO PV-DB2-MAJ-CL-NBR.                        
071610     PERFORM 8000-GET-MAJ-CL-DESC.                                        
071700     MOVE MDSEAR-DESC        TO DL-MAJ-CL-DESC.                           
071701                                                                          
071702     MOVE ZERO TO  PV-END-INV-RTL-MCL-TOT.                                
071705                                                                          
071710     MOVE PV-MAJ-CL-NBR      TO PV-PREV-MAJ-CL-NBR.                       
071800                                                                          
071810                                                                          
071811 2250-FINISH-PREV-DEPT.                                                   
071812                                                                          
071813     PERFORM 7300-WRITE-DEPT-TOTAL.                                       
071820     ADD PV-END-INV-RTL-DEPT-TOT  TO PV-END-INV-RTL-BYR-TOT.              
071840                                                                          
071850                                                                          
071900 2275-PREP-FOR-NEW-DEPT.                                                  
072000                                                                          
072001     MOVE ML119-DEPT-NBR     TO DL-DEPT-NBR.                              
072002     MOVE '-'                TO DL-DEPT-HYPHEN.                           
072004     MOVE ML119-DEPT-DESC    TO DL-DEPT-DESC.                             
072005                                                                          
072010     MOVE ZERO               TO PV-END-INV-RTL-DEPT-TOT.                  
072030                                                                          
072100     MOVE ML119-DEPT-NBR     TO PV-PREV-DEPT-NBR.                         
072300                                                                          
072400                                                                          
072401 2350-FINISH-PREV-BYR.                                                    
072402                                                                          
072410     PERFORM 7400-WRITE-BYR-TOTAL.                                        
072420                                                                          
072430                                                                          
072500 2375-PREP-FOR-NEW-BYR.                                                   
072600                                                                          
072810     PERFORM 8100-GET-BYR-NAME.                                           
072820                                                                          
073000     PERFORM 7000-WRITE-RPT-HEADINGS.                                     
                                                                                
           MOVE ML119-BYR-NBR      TO DL-BYR-NBR.                               
           MOVE '-' TO DL-BYR-HYPHEN.                                           
           MOVE SPACES             TO DL-BYR-NAME.                              
           STRING RSPBAR-WORKER-FIR-NAME DELIMITED BY '  '                      
                  ' '                                                           
                  RSPBAR-WORKER-LAST-NAME DELIMITED BY '  '                     
             INTO DL-BYR-NAME.                                                  
                                                                                
073010     MOVE ZERO               TO PV-END-INV-RTL-BYR-TOT.                   
                                                                                
073030     MOVE ML119-BYR-NBR      TO PV-PREV-BYR-NBR.                          
073100                                                                          
073200                                                                          
073400 6000-READ-BYR-RPT-EXTRCT-FILE.                                           
073600                                                                          
073700     READ BYR-RPT-EXTRCT-FILE INTO ML119-DEPT-ELIM-BY-BYR-EXTRACT         
073900         AT END SET PS-END-OF-RPT-EXTRCT-FILE TO TRUE                     
074200     END-READ.                                                            
074300                                                                          
074400     IF PS-NOT-END-OF-RPT-EXTRCT-FILE                                     
074500         ADD 1 TO PV-BYR-RPT-EXTRCT-RECS-READ                             
074600     END-IF.                                                              
074700                                                                          
074900                                                                          
075100 7000-WRITE-RPT-HEADINGS.                                                 
075300                                                                          
075400     ADD 1                       TO PV-PAGE-CNT.                          
075500     MOVE PV-PAGE-CNT            TO DP132O-PAGE-NUMBER.                   
075600                                                                          
075700     WRITE DEPT-ELIM-BYR-RPT-REC                                          
075700      FROM DP132O-STANDRD-HEADER-132-COLS                                 
075800         AFTER ADVANCING PAGE.                                            
075900                                                                          
076000     WRITE DEPT-ELIM-BYR-RPT-REC FROM RH1-RPT-HEADING-1                   
076100         AFTER ADVANCING 1 LINE.                                          
076200                                                                          
076300     WRITE DEPT-ELIM-BYR-RPT-REC FROM RH2-RPT-HEADING-2                   
076400         AFTER ADVANCING 1 LINE.                                          
076500                                                                          
076600     WRITE DEPT-ELIM-BYR-RPT-REC FROM RH3-RPT-HEADING-3                   
076700         AFTER ADVANCING 2 LINES.                                         
076800                                                                          
076810     MOVE SPACE TO DEPT-ELIM-BYR-RPT-REC.                                 
076820     WRITE DEPT-ELIM-BYR-RPT-REC                                          
076830         AFTER ADVANCING 2 LINES.                                         
076840                                                                          
076900     MOVE +7                     TO PV-LINE-CNT.                          
077200                                                                          
077300                                                                          
077400 7100-WRITE-SUB-CL-LINE.                                                  
077600                                                                          
077700     IF PV-LINE-CNT > PC-LINE-CNT-MAX                                     
077800         PERFORM 7000-WRITE-RPT-HEADINGS.                                 
079000                                                                          
079100     MOVE ML119-SUB-CL-NBR       TO DL-SUB-CL-NBR.                        
           MOVE '-'                    TO DL-SUB-CL-HYPHEN.                     
079200     MOVE ML119-SUB-CL-DESC      TO DL-SUB-CL-DESC.                       
079400                                                                          
079500     MOVE ML119-END-INV-RTL-AMT  TO DL-RTL-AMT.                           
079700                                                                          
079800     WRITE DEPT-ELIM-BYR-RPT-REC FROM DL-DETAIL-LINE                      
079900         AFTER ADVANCING 1 LINE.                                          
080000     ADD 1                       TO PV-LINE-CNT.                          
080100     MOVE SPACE TO DL-DETAIL-LINE.                                        
080110                                                                          
080600                                                                          
080800 7200-WRITE-MAJ-CL-TOTAL.                                                 
081000                                                                          
081100     IF PV-LINE-CNT > PC-LINE-CNT-MAX                                     
081200         PERFORM 7000-WRITE-RPT-HEADINGS.                                 
082200                                                                          
082300     MOVE '*** CLASS TOTALS ***     '                                     
082400                                  TO DL-SUB-CL.                           
082600     MOVE PV-END-INV-RTL-MCL-TOT  TO DL-RTL-AMT.                          
082800                                                                          
082900     WRITE DEPT-ELIM-BYR-RPT-REC FROM DL-DETAIL-LINE                      
083000         AFTER ADVANCING 2 LINE.                                          
083100     ADD 2 TO PV-LINE-CNT.                                                
                                                                                
082900     MOVE SPACE TO DEPT-ELIM-BYR-RPT-REC.                                 
082900     WRITE DEPT-ELIM-BYR-RPT-REC                                          
083000         AFTER ADVANCING 1 LINE.                                          
087000     ADD 1                        TO PV-LINE-CNT.                         
084000                                                                          
084500                                                                          
084700 7300-WRITE-DEPT-TOTAL.                                                   
084900                                                                          
085000     IF PV-LINE-CNT > PC-LINE-CNT-MAX                                     
085100         PERFORM 7000-WRITE-RPT-HEADINGS.                                 
085200                                                                          
086200     MOVE '*** DEPARTMENT TOTALS ***'                                     
086300                                   TO DL-SUB-CL.                          
086500     MOVE PV-END-INV-RTL-DEPT-TOT  TO DL-RTL-AMT.                         
086700                                                                          
086800     WRITE DEPT-ELIM-BYR-RPT-REC FROM DL-DETAIL-LINE                      
086900         AFTER ADVANCING 1 LINE.                                          
087000     ADD 1                        TO PV-LINE-CNT.                         
087100                                                                          
082900     MOVE SPACE TO DEPT-ELIM-BYR-RPT-REC.                                 
082900     WRITE DEPT-ELIM-BYR-RPT-REC                                          
083000         AFTER ADVANCING 1 LINE.                                          
087000     ADD 1                        TO PV-LINE-CNT.                         
088300                                                                          
088300                                                                          
088600 7400-WRITE-BYR-TOTAL.                                                    
088800                                                                          
088900     IF PV-LINE-CNT > PC-LINE-CNT-MAX                                     
089000         PERFORM 7000-WRITE-RPT-HEADINGS.                                 
090000                                                                          
090100     MOVE '*** BUYER TOTALS ***     '                                     
090200                                  TO DL-SUB-CL.                           
090400     MOVE PV-END-INV-RTL-BYR-TOT  TO DL-RTL-AMT.                          
090600                                                                          
090700     WRITE DEPT-ELIM-BYR-RPT-REC FROM DL-DETAIL-LINE                      
090800         AFTER ADVANCING 1 LINE.                                          
090900     ADD 1                       TO PV-LINE-CNT.                          
091000                                                                          
103300                                                                          
107001 8000-GET-MAJ-CL-DESC.                                                    
107002                                                                          
107003     EXEC SQL                                                             
107004         SELECT MDSEAR_DESC                                               
107005           INTO :MDSEAR-DESC                                              
107006           FROM TMDSEAR                                                   
107007          WHERE OPERCO_NBR       = :PC-OPERCO-NBR                         
107008            AND DEPT_NBR         = :ML119-DEPT-NBR                        
107009            AND MAJ_CL_NBR       = :PV-DB2-MAJ-CL-NBR                     
107010            AND MDSELV_CDE       = :PC-LV-CDE-MCL                         
107011            AND :PV-FSCL-MN-END-DTE BETWEEN                               
107012                 STRT_DTE AND END_DTE                                     
107013     END-EXEC.                                                            
107014                                                                          
107015     EVALUATE TRUE                                                        
107016         WHEN SQLCODE = 0                                                 
107017             CONTINUE                                                     
107018         WHEN SQLCODE = +100                                              
080800             MOVE '** MAJ CL DESC NOT FND' TO MDSEAR-DESC                 
107020         WHEN OTHER                                                       
107021             MOVE '8000-GET-MAJ-CL-DESC'                                  
107022                                        TO PV-PARAGRAPH-NAME              
107023             MOVE 'SELECT FROM TMDSEAR' TO PV-DB2-OPER-ATTEMPTED          
107024             PERFORM 9998-SQL-ABEND                                       
107025     END-EVALUATE.                                                        
107026                                                                          
107027                                                                          
107029 8100-GET-BYR-NAME.                                                       
107031                                                                          
107032     EXEC SQL                                                             
107033         SELECT WORKER_FIR_NAME,                                          
107034                WORKER_LAST_NAME                                          
107035           INTO :RSPBAR-WORKER-FIR-NAME,                                  
107036                :RSPBAR-WORKER-LAST-NAME                                  
107037           FROM TMDSEAR, TRSPBAR                                          
107038          WHERE  TMDSEAR.OPERCO_NBR  = :PC-OPERCO-NBR                     
107039            AND  DEPT_NBR            = :ML119-DEPT-NBR                    
107040            AND  MDSELV_CDE          = :PC-LV-CDE-DPT                     
107041            AND (STRT_DTE           <= :PV-FSCL-MN-END-DTE                
107042            AND  END_DTE            >= :PV-FSCL-MN-END-DTE)               
107043            AND  TMDSEAR.MDSEAR_DKEY = TRSPBAR.MDSEAR_DKEY                
107044            AND  RSPLVL_CDE          = :PC-RSPLVL-CDE-BYR                 
107045            AND (WORKER_STRT_DTE    <= :PV-FSCL-MN-END-DTE                
107046            AND  WORKER_END_DTE     >= :PV-FSCL-MN-END-DTE)               
107047     END-EXEC.                                                            
107048                                                                          
107049     EVALUATE TRUE                                                        
107050         WHEN SQLCODE = 0                                                 
107051             CONTINUE                                                     
107053         WHEN SQLCODE = +100                                              
107055             MOVE '*** NO'         TO RSPBAR-WORKER-FIR-NAME      AME     
107057             MOVE 'BUYER NAME ***' TO RSPBAR-WORKER-LAST-NAME             
107059         WHEN OTHER                                                       
107060             MOVE '8100-GET-BYR-NAME'   TO PV-PARAGRAPH-NAME              
107061             MOVE 'JOIN TMDSEAR, TRSPBAR'                                 
107062                                        TO PV-DB2-OPER-ATTEMPTED          
107063             PERFORM 9998-SQL-ABEND                                       
107066     END-EVALUATE.                                                        
107067                                                                          
107068                                                                          
107069 9998-SQL-ABEND.                                                          
107070                                                                          
107071     MOVE +4013 TO PV-ABEND-CODE.                                         
107072     DISPLAY '** ABEND *   = SQLERROR'.                                   
107073     DISPLAY '** PROGRAM   = ' PC-PGM-NAME.                               
107074     DISPLAY '** PARAGRAPH = ' PV-PARAGRAPH-NAME.                         
107075     DISPLAY '** OPERATION = ' PV-DB2-OPER-ATTEMPTED.                     
107076                                                                          
107077     COPY DPPD004.                                                        
107080     PERFORM 9999-ABEND.                                                  
107100                                                                          
110200                                                                          
110201 9999-ABEND.                                                              
110210                                                                          
110300     CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
