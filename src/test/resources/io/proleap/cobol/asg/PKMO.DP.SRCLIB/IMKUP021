       IDENTIFICATION DIVISION.                                         00010021
                                                                        00020021
       PROGRAM-ID.    IMKUP021.                                         00030021
       AUTHOR.        DAVE METZGER.                                     00040021
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050021
       DATE-WRITTEN.  09/11/2001.                                       00060021
       DATE-COMPILED.                                                   00070021
                                                                        00080021
      ******************************************************************00090021
      * STORED PROCEDURE FOR TMDARVS INSERT TRIGGER FOR STYLE RECLASS   00100021
      * (ROW INSERTED INTO TMDARVS WITH ACTV_IND = 'Y')                 00110021
      *                                                                 00120021
      *  ***   RECLASS STYLE BUSINESS EVENT   ***                       00130021
      *                                                                 00140021
      * THIS STORED PROCEDURE PROGRAM WILL BE USED TO FORMAT A          00150021
      * ITEM MAINTENANCE STYLE RECLASS TRANSACTION RECORD AND           00160021
      * INSERT IT IN THE ITEM EVENT MESSAGE BRIDGE DB2 TABLE.           00170021
      ******************************************************************00180026
      * 02/27/2002 TESS BESTE     WR23439 - RENUMBER REDESIGN           00221026
      *   REMOVED IM008-STYLE-RECL-RMS-STYL-ID-N FROM THE STYLE         00222026
      *   RECLASS MESSAGE.  A STYLE RECLASS NO LONGER CREATES A NEW     00222226
      *   STYLE, SO THIS FIELD IS NO LONGER NEEDED.                     00223026
      *                                                                 00224027
      * 01/03/2003  P. KEBER      WR24405 - PRICING & STATUS CHGS       00225027
      *   REMOVED REFERENCE TO IM008-ITEM-KEY-STR-NBR.                  00226027
      *                                                                 00227029
      * 12/22/2005  P. KEBER      WR29786 - PRICE TIER AND LIFESTYLE    00228029
      *   CHANGE TO FILL IM008-STYLE-RECL-LBL-SUP ON THE 010 MESSAGE.   00229029
      ******************************************************************00230021
                                                                        00240021
       ENVIRONMENT DIVISION.                                            00250021
                                                                        00260021
       CONFIGURATION SECTION.                                           00270021
                                                                        00280021
       SOURCE-COMPUTER.        IBM-3090.                                00290021
       OBJECT-COMPUTER.        IBM-3090.                                00300021
                                                                        00310021
       INPUT-OUTPUT SECTION.                                            00320021
                                                                        00330021
       FILE-CONTROL.                                                    00340021
                                                                        00350021
       DATA DIVISION.                                                   00360021
                                                                        00370021
       FILE SECTION.                                                    00380021
                                                                        00390021
       WORKING-STORAGE SECTION.                                         00400021
                                                                        00410021
      ***************************************************************** 00420021
      *  LEGACY ITEM MAINTENANCE INFO COPYBOOK                          00430021
      *  FOR ALL BUSINESS EVENTS                                        00440021
      ***************************************************************** 00450021
           COPY IMRD008.                                                00460021
                                                                        00470021
      ******************************************************************00480021
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        00490021
      ******************************************************************00500021
           COPY DPWS004.                                                00510021
                                                                        00520021
       01  PC-PROGRAM-CONSTANTS.                                        00530021
           05  PC-ORIGIN-CNTRY          PIC  X(03)     VALUE 'US '.     00540021
           05  PC-NO                    PIC  X(01)     VALUE 'N'.       00560021
           05  PC-YES                   PIC  X(01)     VALUE 'Y'.       00570021
           05  PC-PGM-NAME              PIC  X(08)     VALUE 'IMKUP021'.00580021
           05  PC-MAX-RETRIES           PIC S9(04)     VALUE +2         00590021
                                                           COMP SYNC.   00600021
                                                                        00610021
       01  PS-PROGRAM-SWITCHES.                                         00620021
           05  PS-SQL-ERROR-SW          PIC X          VALUE 'N'.       00630021
               88  PS-SQL-ERROR                        VALUE 'Y'.       00640021
               88  PS-NO-SQL-ERROR                     VALUE 'N'.       00650021
                                                                        00660021
       01  PV-PROGRAM-VARIABLES.                                        00670021
           05  PV-PARAGRAPH-NAME        PIC  X(30)     VALUE SPACE.     00680021
           05  PV-DB2-OPERATION         PIC  X(30)     VALUE SPACE.     00690021
           05  PV-RETRY-COUNT           PIC S9(04)     VALUE ZERO       00700021
                                                       COMP SYNC.       00710021
           05  PV-OLD-SUB-CL-MDSEAR-ID  PIC  S9(06)V   COMP-3           00720021
                                                       VALUE ZERO.      00730021
           05  PV-OLD-DEPT-NBR          PIC  X(03)     VALUE SPACE.     00740021
           05  PV-OLD-MAJ-CL-NBR        PIC  X(02)     VALUE SPACE.     00750021
           05  PV-OLD-SUB-CL-NBR        PIC  X(03)     VALUE SPACE.     00760021
                                                                        00770021
      ************************************************************      00780021
      *  ITEM EVENT MESSAGE BRIDGE TABLE                                00790021
      ************************************************************      00800021
           EXEC SQL                                                     00810021
               INCLUDE IMT00001                                         00820021
           END-EXEC.                                                    00830021
                                                                        00840021
      ******************************************************************00850021
      *  VENDOR STYLE TABLE                                             00860021
      ******************************************************************00870021
           EXEC SQL                                                     00880021
               INCLUDE TVNDSTL                                          00890021
           END-EXEC.                                                    00900021
                                                                        00910021
      ************************************************************      00920021
      *  VENDOR STYLE UPC TABLE                                         00930021
      ************************************************************      00940021
           EXEC SQL                                                     00950021
               INCLUDE TUPC                                             00960021
           END-EXEC.                                                    00970021
                                                                        00980021
      ************************************************************      00990021
      *  EXTERNAL ENTITY TABLE                                          01000021
      ************************************************************      01010021
           EXEC SQL                                                     01020021
               INCLUDE TEXTENT                                          01030021
           END-EXEC.                                                    01040021
                                                                        01050021
      ******************************************************************01060021
      *  MERCHANDISE AREA TABLE                                         01070021
      ******************************************************************01080021
           EXEC SQL                                                     01090021
               INCLUDE TMDSEAR                                          01100021
           END-EXEC.                                                    01110021
                                                                        01120021
      ******************************************************************01130021
      *  MERCHANDISE AREA VENDOR STYLE                                  01140021
      ******************************************************************01150021
           EXEC SQL                                                     01160021
               INCLUDE TMDARVS                                          01170021
           END-EXEC.                                                    01180021
      *                                                                 01190021
      ******************************************************************01200021
      *    DB2 COMMUNICATION AREA                                       01210021
      ******************************************************************01220021
           EXEC SQL                                                     01230021
               INCLUDE SQLCA                                            01240021
           END-EXEC.                                                    01250021
                                                                        01260021
      ******************************************************************01270021
      * LINKAGE SECTION FOR VALUES PASSED FROM THE DB2 TRIGGER ON       01280021
      * AN INSERT INTO TDMARVS FOR VENDOR STYLE RECLASS (VS MOVE)       01290021
      ******************************************************************01300021
       LINKAGE SECTION.                                                 01310021
       01  LS-NEW-VS-ID                 PIC  S9(09)      COMP.          01320021
       01  LS-NEW-RMS-STYL-ID           PIC  S9(08)V     COMP-3.        01330021
       01  LS-NEW-SUB-CL-MDSEAR-ID      PIC  S9(06)V     COMP-3.        01340021
                                                                        01350021
      *-----------------------------------------------------------------01360021
      * PROCEDURE DIVISION - (INPUTS PASSED IN FROM TRIGGER)            01370021
      *-----------------------------------------------------------------01380021
       PROCEDURE DIVISION USING  LS-NEW-VS-ID                           01390021
                               , LS-NEW-RMS-STYL-ID                     01400021
                               , LS-NEW-SUB-CL-MDSEAR-ID.               01410021
                                                                        01420021
      *-----------------------------------------------------------------01430021
      * BUILD DATA FOR THE STYLE RECLASS EVENT                          01440028
      *-----------------------------------------------------------------01450021
       1000-MAIN-MODULE.                                                01460021
                                                                        01470021
           PERFORM 3000-PROCESS-STYLE-RECLASS.                          01560021
                                                                        01570021
           GOBACK.                                                      01580021
                                                                        01590021
      *-----------------------------------------------------------------01610021
      * PROCESS STYLE RECLASS                                           01620021
      *-----------------------------------------------------------------01630021
       3000-PROCESS-STYLE-RECLASS.                                      01640021
                                                                        01650021
           PERFORM 8000-GET-OLD-STYLE-INFO.                             01660026
                                                                        01670021
           IF PS-NO-SQL-ERROR                                           01680021
               MOVE LS-NEW-SUB-CL-MDSEAR-ID TO MDSEAR-DKEY              01690021
               PERFORM 8100-GET-NEW-MDSEAR                              01710021
           END-IF.                                                      01720021
                                                                        01730021
           IF PS-NO-SQL-ERROR                                           01740021
               PERFORM 3100-BUILD-MESSAGE-KEY                           01750021
               PERFORM 3200-BUILD-STYLE-RECLASS-DATA                    01760021
               PERFORM 3300-MOVE-DATA-TO-EVENT-TBL                      01770021
               PERFORM 8900-INSERT-IMT-EVNT-MSG                         01780021
           END-IF.                                                      01790021
                                                                        01800021
      *-----------------------------------------------------------------01810021
      * BUILD THE BUSINESS EVENT KEY DATA FOR IM008                     01820021
      *-----------------------------------------------------------------01830021
       3100-BUILD-MESSAGE-KEY.                                          01840021
                                                                        01870021
           INITIALIZE IMRD008-RECORD.                                   01880021
                                                                        01890021
           SET IM008-BUS-EVENT-STYLE-RECLASS TO TRUE.                   01900021
           MOVE SPACES                  TO  IM008-CRE-TMST.             01910021
                                                                        01920021
           MOVE LS-NEW-VS-ID            TO IM008-ITEM-KEY-VS-ID.        01930021
           MOVE VNDSTL-ENT-ID           TO IM008-ITEM-KEY-ENT-ID.       01940021
           MOVE EXTENT-DUN-NBR          TO IM008-ITEM-KEY-DUN-NBR.      01950021
           MOVE VNDSTL-VS-NBR           TO IM008-ITEM-KEY-VS-NBR.       01960021
           MOVE PV-OLD-SUB-CL-MDSEAR-ID TO IM008-ITEM-KEY-SUBCL-MDSE-ID.01970021
           MOVE PV-OLD-DEPT-NBR         TO IM008-ITEM-KEY-DEPT.         01980021
           MOVE PV-OLD-MAJ-CL-NBR       TO IM008-ITEM-KEY-CLASS.        01990021
           MOVE PV-OLD-SUB-CL-NBR       TO IM008-ITEM-KEY-SUBCL.        02000021
           MOVE LS-NEW-RMS-STYL-ID      TO IM008-ITEM-KEY-RMS-STYL-ID.  02010026
           MOVE ZEROS                   TO IM008-ITEM-KEY-SKU           02020021
                                           IM008-ITEM-KEY-INTR-UPC-ID   02030021
                                           IM008-ITEM-KEY-UPC-ID        02040021
                                           IM008-ITEM-KEY-UPC-NBR.      02050027
           MOVE PC-ORIGIN-CNTRY         TO IM008-ITEM-KEY-ORIGIN-CNTRY. 02070021
                                                                        02260021
      *-----------------------------------------------------------------02270021
      * BUILD THE UPC ADD ATTRIBUTES FOR IM008 LAYOUT                   02280021
      *-----------------------------------------------------------------02290021
       3200-BUILD-STYLE-RECLASS-DATA.                                   02300021
                                                                        02310021
023500     INITIALIZE IM008-STYLE-RECLASS-DATA.                         02320022
023600     MOVE LS-NEW-SUB-CL-MDSEAR-ID                                 02341024
023601                                 TO IM008-STYLE-RECL-SCL-MDSE-NEW.02342024
023602                                                                  02350022
      ***  MOVE DEPT NBR AND SET CHANGE INDICATOR                       02360022
023610     MOVE MDSEAR-DEPT-NBR        TO IM008-STYLE-RECL-DEPT-NEW.    02370022
023620     IF IM008-STYLE-RECL-DEPT-NEW = IM008-ITEM-KEY-DEPT           02380022
023621        SET IM008-STYLE-RECL-DEPT-NOC   TO TRUE                   02390022
023622     ELSE                                                         02400022
023621        SET IM008-STYLE-RECL-DEPT-CHG   TO TRUE                   02410022
023630     END-IF.                                                      02420022
023640                                                                  02430022
      ***  MOVE MAJOR CLASS NBR AND SET CHANGE INDICATOR                02440022
023700     MOVE MDSEAR-MAJ-CL-NBR      TO IM008-STYLE-RECL-CLASS-NEW.   02450022
023710     IF IM008-STYLE-RECL-CLASS-NEW = IM008-ITEM-KEY-CLASS         02460022
C23720        SET IM008-STYLE-RECL-CLASS-NOC  TO TRUE                   02470022
023730     ELSE                                                         02480022
C23720        SET IM008-STYLE-RECL-CLASS-CHG  TO TRUE                   02490022
023750     END-IF.                                                      02500022
023760                                                                  02510022
      ***  MOVE SUB-CLASS NBR AND SET CHANGE INDICATOR                  02520022
023800     MOVE MDSEAR-SUB-CL-NBR      TO IM008-STYLE-RECL-SUBCL-NEW.   02530022
023810     IF IM008-STYLE-RECL-SUBCL-NEW = IM008-ITEM-KEY-SUBCL         02540022
023840        SET IM008-STYLE-RECL-SUBCL-NOC TO TRUE                    02550022
023830     ELSE                                                         02560022
023840        SET IM008-STYLE-RECL-SUBCL-CHG TO TRUE                    02561022
023850     END-IF.                                                      02562022
                                                                        02690021
023800     MOVE MDARVS-LBL-SEQ-NBR     TO IM008-STYLE-RECL-LBL-SUP.     02691029
                                                                        02692029
      *-----------------------------------------------------------      02700021
      * MOVE THE STYLE RECLASS MSG TO THE ITEM EVENT                    02710021
      * MSG BRIDGE TABLE                                                02720021
      *-----------------------------------------------------------      02730021
       3300-MOVE-DATA-TO-EVENT-TBL.                                     02740021
                                                                        02750021
           INITIALIZE  DCLIMT00001.                                     02760021
           MOVE LS-NEW-RMS-STYL-ID      TO IMT00001-RMS-STYL-ID.        02770021
           MOVE IM008-BUSINESS-EVENT-ID TO IMT00001-BUS-EVNT-TYP-CDE.   02780021
           MOVE IMRD008-RECORD          TO IMT00001-MSG-TXT-TEXT.       02790021
                                                                        02800021
      ***  DETERMINE LENGTH OF MESSAGE FOR STYLE RECLASS                02810021
      ***  ENTIRE LENGTH OF RECORD MINUS THE STYLE RECL FILLER          02820021
           COMPUTE IMT00001-MSG-TXT-LEN =                               02830021
              LENGTH OF IMRD008-RECORD -                                02840021
              LENGTH OF IM008-STYLE-RECL-FILLER.                        02850021
                                                                        02860021
      *-----------------------------------------------------------------02890021
      * GET THE VENDOR STYLE INFO AND THE OLD MERCH HIERARCHY INFO.     02900026
      *-----------------------------------------------------------------02910021
       8000-GET-OLD-STYLE-INFO.                                         02920026
                                                                        02950021
           INITIALIZE PV-RETRY-COUNT.                                   02960021
                                                                        02970021
           PERFORM WITH TEST AFTER                                      02980021
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       02990021
                 UNTIL SQLCODE = +0                                     03000021
                    OR PS-SQL-ERROR                                     03010021
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  03020021
               EXEC SQL                                                 03030021
                   SELECT DISTINCT A.SUB_CL_MDSEAR_ID                   03040021
                         ,A.LBL_SEQ_NBR                                 03051029
                         ,B.ENT_ID                                      03052029
                         ,B.VS_NBR                                      03060021
                         ,D.DUN_NBR                                     03070021
                         ,E.DEPT_NBR                                    03080021
                         ,E.MAJ_CL_NBR                                  03090021
                         ,E.SUB_CL_NBR                                  03100021
                     INTO :PV-OLD-SUB-CL-MDSEAR-ID                      03110021
                         ,:MDARVS-LBL-SEQ-NBR                           03121029
                         ,:VNDSTL-ENT-ID                                03122029
                         ,:VNDSTL-VS-NBR                                03130021
                         ,:EXTENT-DUN-NBR                               03140021
                         ,:PV-OLD-DEPT-NBR                              03150021
                         ,:PV-OLD-MAJ-CL-NBR                            03160021
                         ,:PV-OLD-SUB-CL-NBR                            03170021
                     FROM TMDARVS A                                     03180021
                         ,TVNDSTL B                                     03190021
                         ,TUPC    C                                     03200021
                         ,TEXTENT D                                     03210021
                         ,TMDSEAR E                                     03220021
                    WHERE  A.VS_ID            = :LS-NEW-VS-ID           03230021
                      AND  A.ACTV_IND         = 'N'                     03240022
                      AND  B.VS_ID            = A.VS_ID                 03250021
                      AND  C.VS_ID            = B.VS_ID                 03260021
                      AND  C.SUB_CL_MDSEAR_ID = A.SUB_CL_MDSEAR_ID      03270021
                      AND  C.UPC_TYP_CDE      = 'I'                     03280022
                      AND  C.EFF_END_DTE      IS NULL                   03290021
                      AND  D.ENT_ID           = B.ENT_ID                03300021
                      AND  E.MDSEAR_DKEY      = A.SUB_CL_MDSEAR_ID      03310021
                    WITH UR                                             03320021
               END-EXEC                                                 03330021
                                                                        03340021
               EVALUATE TRUE                                            03350021
                   WHEN SQLCODE = ZERO                                  03360021
                   WHEN SQLCODE = -904                                  03370021
                   WHEN SQLCODE = -913                                  03380021
                       CONTINUE                                         03390021
                   WHEN OTHER                                           03400021
                       MOVE '8000-GET-OLD-STYLE-INFO'                   03410026
                                                   TO PV-PARAGRAPH-NAME 03420021
                       MOVE 'SELECT FROM TVNDSTL'  TO PV-DB2-OPERATION  03430021
                       PERFORM 9998-SQL-ERROR                           03440021
               END-EVALUATE                                             03450021
           END-PERFORM.                                                 03460021
                                                                        03470021
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          03480021
               MOVE '8000-GET-OLD-STYLE-INFO' TO PV-PARAGRAPH-NAME      03490026
               MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-OPERATION      03500021
               PERFORM 9998-SQL-ERROR                                   03510021
           END-IF.                                                      03520021
                                                                        03530021
      *-----------------------------------------------------------------03540021
      * GET MERCHANDISE AREA INFO FOR NEW SUB CLASS MDSEAR ID           03550021
      *-----------------------------------------------------------------03560021
       8100-GET-NEW-MDSEAR.                                             03570021
                                                                        03600021
           PERFORM WITH TEST AFTER                                      03610021
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       03620021
                 UNTIL SQLCODE = +0                                     03630021
                    OR PS-SQL-ERROR                                     03640021
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  03650021
               EXEC SQL                                                 03660021
                   SELECT  DEPT_NBR                                     03670021
                         , MAJ_CL_NBR                                   03680021
                         , SUB_CL_NBR                                   03690021
                     INTO :MDSEAR-DEPT-NBR                              03700021
                         ,:MDSEAR-MAJ-CL-NBR                            03710021
                         ,:MDSEAR-SUB-CL-NBR                            03720021
                     FROM TMDSEAR                                       03730021
                    WHERE MDSEAR_DKEY = :MDSEAR-DKEY                    03740021
               END-EXEC                                                 03750021
                                                                        03760021
               EVALUATE TRUE                                            03770021
                   WHEN SQLCODE = ZERO                                  03780021
                   WHEN SQLCODE = -904                                  03790021
                   WHEN SQLCODE = -913                                  03800021
                       CONTINUE                                         03810021
                   WHEN OTHER                                           03820021
                       MOVE '8100-GET-NEW-MDSEAR'  TO PV-PARAGRAPH-NAME 03830021
                       MOVE 'SELECT FROM TMDSEAR'  TO PV-DB2-OPERATION  03840021
                       PERFORM 9998-SQL-ERROR                           03850021
               END-EVALUATE                                             03860021
           END-PERFORM.                                                 03870021
                                                                        03880021
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          03890021
               MOVE '8100-GET-NEW-MDSEAR'      TO PV-PARAGRAPH-NAME     03900021
               MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-OPERATION      03910021
               PERFORM 9998-SQL-ERROR                                   03920021
           END-IF.                                                      03930021
                                                                        03940021
      *-----------------------------------------------------------      03950021
      *  INSERT A BUSINESS EVENT RECORD TO THE ITEM EVENT MESSAGE       03960021
      *  BRIDGE DB2 TABLE.                                              03970021
      *-----------------------------------------------------------      03980021
       8900-INSERT-IMT-EVNT-MSG.                                        03990021
                                                                        04000021
           PERFORM WITH TEST AFTER                                      04010021
                 UNTIL SQLCODE = +0                                     04020021
                    OR PS-SQL-ERROR                                     04030021
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  04040021
           EXEC SQL                                                     04050021
                INSERT INTO IMT_EVNT_MSG_BRG                            04060021
                       (RMS_STYL_ID                                     04070021
                       ,CRE_TMST                                        04080021
                       ,BUS_EVNT_TYP_CDE                                04090021
                       ,MSG_TXT )                                       04100021
                VALUES                                                  04110021
                      (:IMT00001-RMS-STYL-ID                            04120021
                      ,CURRENT TIMESTAMP                                04130021
                      ,:IMT00001-BUS-EVNT-TYP-CDE                       04140021
                      ,:IMT00001-MSG-TXT)                               04150021
           END-EXEC                                                     04160021
           EVALUATE TRUE                                                04170021
               WHEN SQLCODE = 0                                         04180021
                    CONTINUE                                            04190021
               WHEN SQLCODE = -911                                      04200021
               WHEN SQLCODE = -913                                      04210021
                    ADD +1 TO PV-RETRY-COUNT                            04220021
               WHEN SQLWARN0 NOT = SPACES                               04230021
               WHEN SQLCODE < ZERO                                      04240021
               WHEN SQLCODE > ZERO                                      04250021
                    MOVE '8900-INSERT-IMT-EVNT-MSG'                     04260021
                                   TO PV-PARAGRAPH-NAME                 04270021
                    MOVE 'INSERT INTO IMT_EVNT_MSG_BRG'                 04280021
                                   TO PV-DB2-OPERATION                  04290021
                    PERFORM 9998-SQL-ERROR                              04300021
               END-EVALUATE                                             04310021
           END-PERFORM.                                                 04320021
                                                                        04330021
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          04340021
               MOVE '8900-INSERT-IMT-EVNT-MSG'                          04350021
                                      TO PV-PARAGRAPH-NAME              04360021
               MOVE 'INSERT INTO IMT_EVNT_MSG_BRG'                      04370021
                                      TO PV-DB2-OPERATION               04380021
               PERFORM 9998-SQL-ERROR                                   04390021
           END-IF.                                                      04400021
                                                                        04410021
      *-----------------------------------------------------------------04420021
      * DB2 SQL ERROR PROCESSING                                        04430021
      *-----------------------------------------------------------------04440021
       9998-SQL-ERROR.                                                  04450021
                                                                        04460021
           SET PS-SQL-ERROR TO TRUE.                                    04470021
                                                                        04480021
           DISPLAY '** '                                                04490021
                   PC-PGM-NAME                                          04500021
                   ' ABEND IN PARAGRAPH '                               04510021
                   PV-PARAGRAPH-NAME.                                   04520021
           DISPLAY '** '                                                04530021
                   PC-PGM-NAME                                          04540021
                   ' OPERATION = ' PV-DB2-OPERATION.                    04550021
                                                                        04560021
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT     04570021
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.               04580021
           COPY DPPD004.                                                04590021
      *                                                                 04600021
           EXEC SQL                                                     04610021
               ROLLBACK                                                 04620021
           END-EXEC.                                                    04630021
