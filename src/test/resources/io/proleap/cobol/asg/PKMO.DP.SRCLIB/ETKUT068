       IDENTIFICATION DIVISION.                                         ETKUT004
                                                                        ETKUT004
       PROGRAM-ID.              ETKUT068.                               ETKUT004
       AUTHOR.                  LINDA VOGT - COMPUWARE.                 ETKUT004
       DATE-WRITTEN.            09/24/98.                               ETKUT004
       DATE-COMPILED.                                                   ETKUT004
                                                                        ETKUT004
      ******************************************************************ETKUT004
      * THIS PROGRAM EXTRACTS DATA FROM A UNIX FILE CONTAINING THE     *        
      * APPLICATION INTEGRATOR TRADING PARTNER DATA.  THE DATA         *        
      * EXTRACTED WILL BE USED IN UPDATING THE ISA LAST USED           *        
      * CONTROL NUMBERS ON THE MAINFRAME IN THE TRACKING DATABASE.     *        
      ******************************************************************ETKUT004
JF0502*                                                                         
JF0502*   DATE 05/02/2002                                                       
JF0502*     ADDED LOGIC FOR 816 PROCESSING.                                     
JF0502*   MADE BY: JON FIEDLER             WR/IR #: 22778                       
      ******************************************************************ETKUT004
       ENVIRONMENT DIVISION.                                            ETKUT004
                                                                        ETKUT004
       CONFIGURATION SECTION.                                           ETKUT004
                                                                        ETKUT004
       INPUT-OUTPUT SECTION.                                            ETKUT004
                                                                        ETKUT004
       FILE-CONTROL.                                                    ETKUT004
                                                                        ETKUT004
           SELECT UNIX-INPUT-FILE                                       ETKUT004
              ASSIGN TO INP01.                                          ETKUT004
                                                                        ETKUT004
           SELECT TRADING-PARTNER-EXT-FILE                              ETKUT004
              ASSIGN TO OUT01.                                          ETKUT004
                                                                        ETKUT004
                                                                                
       DATA DIVISION.                                                   ETKUT004
                                                                        ETKUT004
       FILE SECTION.                                                    ETKUT004
                                                                        ETKUT004
       FD  UNIX-INPUT-FILE                                              ETKUT004
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 240 CHARACTERS                               ETKUT004
           DATA RECORD IS UNIX-INPUT-RECORD.                            ETKUT004
                                                                        ETKUT004
       01  UNIX-INPUT-RECORD.                                           ETKUT004
           05  FILLER                       PIC X.                              
           05  UNIX-REC-ID-DATA.                                                
               10  FILLER                   PIC X(03).                  ETKUT004
               10  UNIX-REC-DATA            PIC X(236).                         
                                                                                
       FD  TRADING-PARTNER-EXT-FILE                                     ETKUT004
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                   ETKUT004
           RECORD CONTAINS 120 CHARACTERS                               ETKUT004
           DATA RECORD IS TRADING-PARTNER-EXT-RECORD.                   ETKUT004
                                                                        ETKUT004
       01  TRADING-PARTNER-EXT-RECORD       PIC X(120).                 ETKUT004
                                                                        ETKUT004
       WORKING-STORAGE SECTION.                                         ETKUT004
                                                                        ETKUT004
       01  WS-SWITCHES-AND-SUBSCRIPTS.                                  ETKUT004
           05  END-OF-FILE-SW                PIC X(01)   VALUE 'N'.     ETKUT004
               88  END-UNIX-FILE                         VALUE 'Y'.     ETKUT004
           05  WRITE-EXT-SW                  PIC X       VALUE 'N'.             
               88  NO-WRITE-EXT                          VALUE 'N'.             
               88  WRITE-EXT                             VALUE 'Y'.             
           05  TP-SUB             COMP-3     PIC S9(3)   VALUE +0.              
                                                                        ETKUT004
       01  WS-WORK-FIELDS.                                              ETKUT004
           05  WS-REC-ID                    PIC X(05)    VALUE SPACES.  ETKUT004
               88  TP-REC                                VALUE 'TP   '.         
           05  WS-NAME                      PIC X(45)    VALUE SPACES.          
           05  WS-PREV-NAME                 PIC X(45)    VALUE SPACES.          
           05  WS-FIELD-ID                  PIC X(17)    VALUE SPACES.          
           05  WS-SPACE                     PIC X        VALUE SPACE.           
           05  WS-FIELD-VALUE               PIC X(15)    VALUE SPACES.          
           05  WS-FLD-VAL-CNT      COMP-3   PIC S9(5)    VALUE ZEROES.          
                                                                                
       01  WS-TP-TABLE.                                                         
           05  WS-TP-DATA OCCURS 4 TIMES    PIC X(35).                          
                                                                                
       01  WS-TRADING-PART-EXT.                                                 
           05  WS-TPE-NAME                  PIC X(45)  VALUE SPACES.            
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  WS-TPE-IC-CNTL-NO            PIC X(09)  VALUE SPACES.            
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  WS-TPE-FG-CNTL-NO            PIC X(09)  VALUE SPACES.            
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  WS-TPE-MSG-XREF              PIC X(14)  VALUE SPACES.            
           05  FILLER REDEFINES WS-TPE-MSG-XREF.                                
               10  FILLER                   pic X(11).                          
               10  WS-TPE-SET-CDE           pic X(03).                          
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  WS-TPE-SEND-ID               PIC X(15)  VALUE SPACES.            
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  WS-TPE-SEND-QUAL             PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(16)  VALUE SPACES.            
                                                                        ETKUT004
       01  PC-PROGRAM-CONSTANTS.                                                
JF0502     05  PC-SET-CDE-816               PIC X(03)  VALUE '816'.             
           05  PC-SET-CDE-820               PIC X(03)  VALUE '820'.             
           05  PC-SET-CDE-840               PIC X(03)  VALUE '840'.             
           05  PC-SET-CDE-850               PIC X(03)  VALUE '850'.             
SP0509     05  PC-SET-CDE-860               PIC X(03)  VALUE '860'.             
           05  PC-SET-CDE-852               PIC X(03)  VALUE '852'.             
           05  PC-SET-CDE-864               PIC X(03)  VALUE '864'.             
                                                                        ETKUT004
       PROCEDURE DIVISION.                                              ETKUT004
                                                                        ETKUT004
           OPEN INPUT UNIX-INPUT-FILE                                           
                OUTPUT TRADING-PARTNER-EXT-FILE.                                
                                                                                
           PERFORM 2000-PROCESS-UNIX-FILE THRU 2000-EXIT                        
              UNTIL END-UNIX-FILE.                                              
                                                                                
           CLOSE UNIX-INPUT-FILE                                                
                 TRADING-PARTNER-EXT-FILE.                                      
                                                                                
           STOP RUN.                                                    ETKUT004
                                                                        ETKUT004
                                                                                
       2000-PROCESS-UNIX-FILE.                                          ETKUT004
                                                                        ETKUT004
           PERFORM 8000-READ-UNIX-FILE THRU 8000-EXIT.                          
           IF END-UNIX-FILE                                                     
              PERFORM 8100-WRITE-EXTRACT THRU 8100-EXIT                         
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
           INITIALIZE WS-REC-ID                                                 
                      WS-NAME.                                                  
                                                                                
           UNSTRING UNIX-REC-ID-DATA                                            
              DELIMITED BY '|'                                          ETKUT004
              INTO WS-REC-ID                                            ETKUT004
                   WS-NAME.                                                     
           IF TP-REC                                                            
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 8100-WRITE-EXTRACT THRU 8100-EXIT                         
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-PREV-NAME = SPACES                                             
              MOVE WS-NAME TO WS-PREV-NAME.                             ETKUT004
           IF WS-NAME = WS-PREV-NAME                                            
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 8100-WRITE-EXTRACT THRU 8100-EXIT                         
              MOVE WS-NAME TO WS-PREV-NAME                                      
           END-IF.                                                              
                                                                                
           INITIALIZE WS-TP-TABLE.                                              
           UNSTRING UNIX-REC-DATA                                               
             DELIMITED BY '|'                                                   
             INTO WS-TP-DATA(1)                                                 
                  WS-TP-DATA(2)                                                 
                  WS-TP-DATA(3)                                         ETKUT004
                  WS-TP-DATA(4).                                        ETKUT004
                                                                                
           PERFORM 2100-EXTRACT-FIELDS THRU 2100-EXIT                           
             VARYING TP-SUB FROM 2 BY 1                                         
             UNTIL TP-SUB > 4.                                                  
                                                                                
       2000-EXIT.                                                       ETKUT004
           EXIT.                                                                
                                                                                
                                                                        ETKUT004
       2100-EXTRACT-FIELDS.                                             ETKUT004
                                                                        ETKUT004
           INITIALIZE WS-FIELD-ID                                               
                      WS-FIELD-VALUE                                            
                      WS-FLD-VAL-CNT.                                           
           UNSTRING WS-TP-DATA(TP-SUB)                                  ETKUT004
             DELIMITED BY '"'                                           ETKUT004
             INTO WS-FIELD-ID                                                   
                  WS-SPACE                                                      
                  WS-FIELD-VALUE COUNT IN WS-FLD-VAL-CNT.                       
                                                                                
            IF WS-FLD-VAL-CNT = 0                                               
               GO TO 2100-EXIT.                                                 
                                                                        ETKUT004
            IF WS-FIELD-ID = 'X12ICLstSentCtlNo'                        ETKUT004
              MOVE WS-FIELD-VALUE TO WS-TPE-IC-CNTL-NO                          
              SET WRITE-EXT TO TRUE                                             
            ELSE                                                                
              IF WS-FIELD-ID = 'X12SndID'                                       
                 MOVE WS-FIELD-VALUE TO WS-TPE-SEND-ID                          
                 SET WRITE-EXT TO TRUE                                          
              ELSE                                                              
                 IF WS-FIELD-ID = 'X12SndQual'                                  
                   MOVE WS-FIELD-VALUE TO WS-TPE-SEND-QUAL                      
                   SET WRITE-EXT TO TRUE                                        
                 ELSE                                                           
                   IF WS-FIELD-ID = 'MsgXRef'                                   
                      MOVE WS-FIELD-VALUE TO WS-TPE-MSG-XREF                    
                      SET WRITE-EXT TO TRUE                                     
                   ELSE                                                         
                     IF WS-FIELD-ID = 'X12FGLstSentCtlNo'                       
                        MOVE WS-FIELD-VALUE TO WS-TPE-FG-CNTL-NO                
                        SET WRITE-EXT TO TRUE                                   
                     END-IF                                                     
                   END-IF                                                       
                 END-IF                                                         
              END-IF                                                            
            END-IF.                                                             
                                                                                
      ****************************************************************          
      *                                                                         
      *  ONLY OUTBOUND CONTROL NUMBERS ARE LOADED.  THE 840 DOCUMENT            
      *  IS USED AS A DUMMY FOR TRADING PARTNER ID'S WHICH DO NOT               
      *  HAVE AN OUTBOUND DOCUMENT LOADED ON UNIX.  WE NEED TO LOAD             
      *  THE DUMMY TO ACCOUNT FOR OUTBOUND FA CONTROL NUMBERS.                  
      *                                                                         
      ****************************************************************          
                                                                                
            IF WS-TPE-SET-CDE NOT EQUAL PC-SET-CDE-850 AND                      
SP0509         WS-TPE-SET-CDE NOT EQUAL PC-SET-CDE-860 AND                      
               WS-TPE-SET-CDE NOT EQUAL PC-SET-CDE-852 AND                      
               WS-TPE-SET-CDE NOT EQUAL PC-SET-CDE-840 AND                      
               WS-TPE-SET-CDE NOT EQUAL PC-SET-CDE-820 AND                      
JF0502         WS-TPE-SET-CDE NOT EQUAL PC-SET-CDE-816 AND                      
               WS-TPE-SET-CDE NOT EQUAL PC-SET-CDE-864                          
                SET NO-WRITE-EXT TO TRUE                                        
            END-IF.                                                             
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                        ETKUT004
                                                                                
       8000-READ-UNIX-FILE.                                             ETKUT004
                                                                        ETKUT004
           READ UNIX-INPUT-FILE                                         ETKUT004
              AT END SET END-UNIX-FILE TO TRUE.                         ETKUT004
                                                                                
       8000-EXIT.                                                       ETKUT004
           EXIT.                                                                
                                                                                
                                                                                
       8100-WRITE-EXTRACT.                                                      
                                                                                
           IF NO-WRITE-EXT                                                      
              GO TO 8100-EXIT.                                                  
                                                                                
           MOVE WS-PREV-NAME TO WS-TPE-NAME.                            ETKUT004
           WRITE TRADING-PARTNER-EXT-RECORD                                     
              FROM WS-TRADING-PART-EXT.                                         
           INITIALIZE WS-TRADING-PART-EXT.                                      
           SET NO-WRITE-EXT TO TRUE.                                            
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
