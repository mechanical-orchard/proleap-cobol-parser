      *-----------------------*                                         12/16/94
00002  IDENTIFICATION DIVISION.                                         INKCS011
00003 *-----------------------*                                            LV001
00004  PROGRAM-ID.    INKCS011.                                         INKCS011
00005  AUTHOR.        M. FURLICK - STRATAGEM                            INKCS011
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          INKCS011
00007  DATE-WRITTEN.  09/93.                                            INKCS011
00008  DATE-COMPILED.                                                   INKCS011
00009                                                                   INKCS011
00010 *---------------------------------------------------------------* INKCS011
00011 *    I011 - PI PHYSICAL INVENTORY ADJUSTMENT LIST               * INKCS011
00012 *                                                               * INKCS011
00013 *    THIS PROGRAM GENERATES A LIST OF THE SKUS FOR THE REQUESTED* INKCS011
00014 *    STORE/AREA.  QUANTITY, PRICE, AND EXTENDED PRICE ARE THE   * INKCS011
00015 *    FIELDS THAT ARE DISPLAYED.  THE USER CAN SELECT ONE OR     * INKCS011
00016 *    MORE SKUS FOR PROCESSING IN INKCS012.                      * INKCS011
00017 *                                                               *         
      *  03/23/99 - PAUL KEBER - STRATAGEM  WR#: 10657                *         
      *    ADDED A TOTALS LINE AT THE END OF THE ITEM LIST.           *         
      *---------------------------------------------------------------* INKCS011
W21015*  08/01/00 - S JACKSON  -  WR#21015                            *         
W21015*    ADD INFO TO LISTED ITEM INFORMING USER AS TO ORIGINATING   *         
W21015*    TABLE FROM WHERE DATA WAS SELECTED FROM. RECORDS FETCHED...* INKCS011
W21015*    FROM TSUMINV - 'ADJUSTMENT' IS DISPLAY ON ITEM LIST SCREEN * INKCS011
W21015*    FROM TADJADD - 'ADJ ADD'    IS DISPLAY ON ITEM LIST SCREEN * INKCS011
00018 *---------------------------------------------------------------* INKCS011
W21732*  10/24/02 - CHANGED BY IGSI    WR#21732                       *         
W21732*    CHANGED THE PROGRAM TO DISPLAY  NON-INTELLIGENT SKU INSTEAD*         
W21732*    OF DEPT/CLASS/SEQ NUMBER.                                  *         
W21732*---------------------------------------------------------------* INKCS011
W25248*  09/24/03 - S. JACKSON -   WR#25248                           *         
W25248*    ALLOW FOR UPC TO BE INCORPORATED INTO ADJUSTMENT PROCESS.  *         
W25248*    ADD ASC-DMS FIELD TO ASC-ITEM-ARRAY TO CONTAIN SKU ATTR.   *         
W25248*    CHANGE CURSOR PROCESSING TO SUM SIMILAR RECORD COUNTS THAT *         
W25248*    CONTAIN THE SAME KEY FIELDS (SKU/RETAIL) REGARDLES OF UPC. *         
P25248*    ADD PRINT FUNC (F18) TO GENERATE RPT AT STORE/AREA LEVEL   *         
W25248*    ADD LIST FUNC (F17) TO VIEW UPC ENTRIES BASED ON SKU LEVEL *         
W25248*---------------------------------------------------------------* INKCS011
R25248*  12/04/03 - R.JANSSEN  -   WR#25248                           *         
R25248*      ISSUE ON THE SUMMARY SCREEN WHERE THE LAST SKU NUMBER IS *         
R25248*    NOT DISPLAYING ON THE SCREEN.                              *         
R25248*---------------------------------------------------------------* INKCS011
W26682*  02/02/04 - S.JACKSON  -   WR#26682                           *         
W26682*      IF AREA DOES NOT EXIST FOR STORE LIST, INITIALIZE DCLGEN *         
W26682*    FIELDS AND DISPLAY 'AREA NOT FOUND' MESSAGE                *         
W26682*---------------------------------------------------------------* INKCS011
R26682*  06/25/04 - R. JANSSEN -   WR#26682                           *         
R26682*      MISSING DMS INFORMATION ON THE SUMMARY SCREEN. WHY???              
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
R26682*---------------------------------------------------------------* INKCS011
W28545*  04/18/06 - B. GRAHAM  WR#28545                               * INKCS011
W28545*      ADD INWS400, INPD400 COMON PRICING LOGIC-----------------* INKCS011
W30692* W30692  02-01-2006 ROD JANSSEN DEFINE THE DEFAULT PRINTER FOR           
W30692*                                THE USER.                                
W28545*  09/12/06 - B. GRAHAM  WR#28545                               * INKCS011
W28545*      CHG PASS DATE FROM IN400-PV-EFF-BEG-DTE-IN TO            * INKCS011
W28545*                         IN400-PV-EFF-DTE-IN                   * INKCS011
W28545*---------------------------------------------------------------* INKCS011
263126* CHANGED 10/25/2021    BY JIM HUNTER      WR: CHG0263126                 
263126* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263126* MAKING THE CALL DYNAMIC VS STATIC.                                      
263126*----------------------------------------------------------------*        
1122RM* CHANGED 11/03/2022    BY BOB MCASEY      WR: CHG0277188                 
1122RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1122RM* THE CICS ARCHITECTURE API.                                              
1122RM*---------------------------------------------------------------*         
1122JK* CHANGED 01/11/2024    BY JEAN KURK       WR: CHG0297810                 
1122JK* REMOVED CODE FOR LOCAL-05                                               
1122JK*---------------------------------------------------------------*         
00019                                                                   INKCS011
00020  ENVIRONMENT DIVISION.                                            INKCS011
00022  DATA DIVISION.                                                   INKCS011
00024  WORKING-STORAGE SECTION.                                         INKCS011
P25248                                                                  INKCS046
P25248 01  TD-TRANSIENT-DATA-AREA.                                              
P25248     05  TD-QUEUE-NAME           PIC X(4)     VALUE 'IRTD'.               
P25248     05  TD-QUEUE-NAME-LENGTH    PIC S9(4)    VALUE +4                    
P25248                                 COMP SYNC.                               
P25248     05  TD-JCL-LINE             PIC X(80)    VALUE SPACES.               
P25248     05  TD-JCL-LINE-LENGTH      PIC S9(4)    VALUE +80                   
P25248                                 COMP SYNC.                               
P25248                                                                          
P25248 01  JL-JCL-WORK-AREA.                                            INKCS046
P25248     05  JL-JCL-INK950-LINE-1.                                            
P25248         10  FILLER                     PIC X(02)    VALUE '//'.          
P25248         10  JL-JCL-INK950-JOBNAME      PIC X(08)    VALUE SPACES.        
P25248         10  FILLER REDEFINES JL-JCL-INK950-JOBNAME.                      
P25248             15  FILLER                 PIC X(07).                        
P25248             15  JL-JCL-INK950-JOB-UNQ  PIC X(01).                        
P25248         10  FILLER                     PIC X(11)    VALUE                
P25248             ' JOB KOHLS,'.                                               
P25248         10  FILLER                     PIC X(01)    VALUE QUOTE.         
P25248         10  FILLER                     PIC X(18)    VALUE                
P25248             'INV ADJUST DTL RPT'.                                        
P25248         10  FILLER                     PIC X(01)    VALUE QUOTE.         
P25248         10  FILLER                     PIC X(01)    VALUE ','.           
P25248     05  JL-JCL-INK950-LINE-2.                                            
P25248         10  FILLER                     PIC X(21)    VALUE                
P25248             '//             CLASS='.                                     
P25248         10  JL-JCL-INK950-JOBCLASS     PIC X(01)    VALUE 'Q'.           
P25248         10  FILLER                     PIC X(10)    VALUE                
P25248             ',MSGCLASS='.                                                
P25248         10  JL-JCL-INK950-MSGCLASS     PIC X(01)    VALUE 'P'.           
P25248         10  JL-JCL-INK950-NOTIFY-OPT   PIC X(16)    VALUE SPACES.        
P25248     05  JL-JCL-INK950-LINE-3.                                            
P25248         10  FILLER                     PIC X(13)    VALUE                
P25248             '/*JOBPARM K=0'.                                             
P25248     05  JL-JCL-INK950-CMT-SHORT.                                         
P25248         10  FILLER                     PIC X(03)    VALUE '//*'.         
P25248     05  JL-JCL-INK950-CMT-LONG.                                          
P25248         10  FILLER                     PIC X(40)    VALUE                
P25248             '//**************************************'.          ********
P25248         10  FILLER                     PIC X(32)    VALUE                
P25248             '********************************'.                  ********
P25248     05  JL-JCL-INK950-CMT-USER-INFO.                                     
P25248         10  FILLER                     PIC X(26)    VALUE                
P25248             '//* INK950 - SUBMITTED BY '.                                
P25248         10  JL-JCL-INK950-CMT-USRID    PIC X(08)    VALUE SPACES.        
P25248         10  FILLER                     PIC X(06)    VALUE                
P25248             ' FROM '.                                                    
P25248         10  JL-JCL-INK950-CMT-REGION   PIC X(08)    VALUE SPACES.        
P25248         10  FILLER                     PIC X(04)    VALUE                
P25248             ' ON '.                                                      
P25248         10  JL-JCL-INK950-CMT-SUBDATE  PIC X(10)    VALUE SPACES.        
P25248         10  FILLER                     PIC X(02)    VALUE SPACES.        
P25248         10  JL-JCL-INK950-CMT-SUBTIME  PIC X(08)    VALUE SPACES.        
P25248     05  JL-JCL-INK950-CMT-1.                                             
P25248         10  FILLER                     PIC X(40)    VALUE                
P25248             '//* THIS JOB PRODUCES A DETAILED REPORT '.          ********
P25248         10  FILLER                     PIC X(32)    VALUE                
P25248             'OF SKU / UPC ADJUSTMENTS        '.                  ********
P25248     05  JL-JCL-INK950-CMT-2.                                             
P25248         10  FILLER                     PIC X(40)    VALUE                
P25248             '//* REPORT AND PRINT THEM AT THE USER DE'.          ********
P25248         10  FILLER                     PIC X(32)    VALUE                
P25248             'FAULT PRINT DESTINATION OR      '.                  ********
P25248     05  JL-JCL-INK950-CMT-3.                                             
P25248         10  FILLER                     PIC X(40)    VALUE                
P25248             '//* LOCAL, IF THERE IS NO DEFAULT PRINTE'.          ********
P25248         10  FILLER                     PIC X(32)    VALUE                
P25248             'R DEFINED TO THE USER.          '.                  ********
P25248     05  JL-JCL-INK950-CMT-4.                                             
P25248         10  FILLER                     PIC X(22)    VALUE                
P25248             '//* OPTION REQUESTED: '.                            ********
P25248         10  JL-JCL-INK950-CMT-OPTION   PIC X(35)    VALUE SPACES.        
P25248     05  JL-JCL-INK950-LINE-10.                                           
P25248         10  FILLER                     PIC X(39)    VALUE                
P25248             '//JOBLIB   DD DSN=PKMO.LOADLIB,DISP=SHR'.                   
P25248     05  JL-JCL-INK950-LINE-11.                                           
P25248         10  FILLER                     PIC X(03)    VALUE                
P25248             '//*'.                                                       
P25248     05  JL-JCL-INK950-LINE-12.                                           
P25248         10  FILLER                     PIC X(32)    VALUE                
P25248             '//PS020    EXEC INK9501,PRTDEST='.                          
P25248         10  JL-JCL-INK950-PRTDEST      PIC X(08)    VALUE                
P25248             'LOCAL'.                                                     
P25248     05  JL-JCL-INK950-LINE-13.                                           
P25248         10  FILLER                     PIC X(20)    VALUE                
P25248             '//S010.SYSUT1   DD *'.                                      
P25248     05  JL-JCL-INK950-LINE-14.                                           
W28545         10  JL-JCL-INK950-REQ-OPTION   PIC X(56).                        
P25248     05  JL-JCL-INK950-LINE-EOD.                                          
P25248         10  FILLER                     PIC X(02)    VALUE '/*'.          
P25248     05  JL-JCL-INK950-LINE-EOF.                                          
P25248         10  FILLER                     PIC X(05)    VALUE                
P25248             '/*EOF'.                                                     
P25248     05  JL-JCL-TEST-LINE-10.                                             
P25248         10  FILLER                     PIC X(35)    VALUE                
P25248             '//PROCS  JCLLIB ORDER=(TKMA.PROCLIB'.                       
P25248         10  FILLER                     PIC X(20)    VALUE                
P25248             ',QKMO.PROCLIB.STAGE)'.                                      
P25248     05  JL-JCL-TEST-LINE-11A.                                            
P25248         10  FILLER                     PIC X(39)    VALUE                
P25248             '//JOBLIB   DD DSN=TKMA.LOADLIB,DISP=SHR'.                   
P25248     05  JL-JCL-TEST-LINE-11B.                                            
P25248         10  FILLER                     PIC X(39)    VALUE                
P25248             '//         DD DSN=QKMO.LOADLIB,DISP=SHR'.                   
P25248     05  JL-JCL-TEST-LINE-11C.                                            
P25248         10  FILLER                     PIC X(39)    VALUE                
P25248             '//         DD DSN=PKMO.LOADLIB,DISP=SHR'.                   
P25248     05  JL-JCL-TEST-NOTIFY.                                              
P25248         10  FILLER                     PIC X(08)    VALUE                
P25248             ',NOTIFY='.                                                  
P25248         10  JL-JCL-TEST-USER           PIC X(08)    VALUE SPACES.        
P25248     05  JL-JCL-INK950-CC-INFO.                                           
P25248         10  FILLER                     PIC X(03)    VALUE                
P25248             'SKU'.                                                       
P25248         10  FILLER                     PIC X(01)    VALUE SPACES.        
W26600         10  JL-JCL-INK950-STORE        PIC X(04)    VALUE SPACES.        
P26600*        10  JL-JCL-INK950-STR-NBR.                                       
P26600*            15  FILLER                 PIC X(01)    VALUE '0'.           
P26600*            15  JL-JCL-INK950-STORE    PIC X(03)    VALUE SPACES.        
P25248         10  FILLER                     PIC X(01)    VALUE SPACES.        
P25248         10  JL-JCL-INK950-AREA         PIC X(07)    VALUE SPACES.        
P25248         10  FILLER                     PIC X(01)    VALUE SPACES.        
P25248         10  JL-JCL-INK950-SKU          PIC X(08)    VALUE SPACES.        
P25248         10  FILLER                     PIC X(01)    VALUE SPACES.        
P28545         10  JL-JCL-INK950-RETAIL       PIC X(09)    VALUE SPACES.        
W26600         10  FILLER                     PIC X(01)    VALUE SPACES.INKCS011
W26600         10  JL-JCL-INK950-USERID       PIC X(07)    VALUE SPACES.        
W26600         10  FILLER                     PIC X(01)    VALUE SPACES.        
W26600         10  JL-JCL-INK950-PRT-IND      PIC X(01)    VALUE SPACES.        
W28545         10  FILLER                     PIC X(01)    VALUE SPACES.        
W28545         10  JL-JCL-DB2-ACTL-INV-DTE    PIC X(10)    VALUE SPACES.        
                                                                                
W25248 01  PA-PROGRAM-ACCUMULATOR.                                      INKCS011
W25248     05  PA-ACCUM-SKU          PIC  S9(8)V     COMP-3 VALUE +0.   INKCS011
W25248     05  PA-ACCUM-UPC          PIC  S9(15)V    COMP-3 VALUE +0.   INKCS011
W25248     05  PA-ACCUM-QTY          PIC  S9(8)V     COMP-3 VALUE +0.   INKCS011
W25248     05  PA-ACCUM-RTL          PIC  S9(5)V99   COMP-3 VALUE +0.   INKCS011
W25248     05  PA-ACCUM-EXTD-RTL     PIC  S9(8)V99   COMP-3 VALUE +0.   INKCS011
                                                                        INKCS011
00026  01  PC-PROGRAM-CONSTANTS.                                        INKCS011
00027      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         INKCS011
00028                                                     'IN011A  '.   INKCS011
00029      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         INKCS011
00030                                                     'INKM011 '.   INKCS011
00031      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         INKCS011
00032                                                     'INKCS011'.   INKCS011
00033      05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.      INKCS011
00034      05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.      INKCS011
00035      05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.    INKCS011
00036      05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13     INKCS011
00037                                                     COMP SYNC.    INKCS011
00038      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2      INKCS011
00039                                                     COMP SYNC.    INKCS011
00040      05  PC-MAX-ITEMS                   PIC S9(09)  VALUE         INKCS011
00041                                                     +1000000      INKCS011
00042                                                     COMP-3.       INKCS011
00043      05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5      INKCS011
00044                                                     COMP SYNC.    INKCS011
00045      05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3       INKCS011
00046                                                     COMP-3.       INKCS011
00047      05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.  INKCS011
00048      05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.  INKCS011
00049      05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.  INKCS011
00050      05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.  INKCS011
00051      05  PC-Y                           PIC  X(01)  VALUE 'Y'.    INKCS011
00052      05  PC-CORPORATE                   PIC  X(02)  VALUE 'HQ'.   INKCS011
00053      05  PC-COMPLETE                    PIC  X(08)  VALUE         INKCS011
00054                                                    'COMPLETE'.    INKCS011
W21015*    05  PC-ADJ-MSG                     PIC  X(10)  VALUE         INKCS011
W21015*                                                  'ADJUSTMENT'.  INKCS011
W21015     05  PC-ADJ-MESSAGE.                                          INKCS011
W21015         10  PC-ADJ-MSG1                PIC  X(10)  VALUE                 
W21015                                                   'ADJUSTMENT'.          
W21015         10  PC-ADJ-MSG2                PIC  X(10)  VALUE                 
W21015                                                   'ADJ ADD   '.          
W21015         10  PC-NO-ADJ-MSG              PIC  X(10)  VALUE         INKCS011
W21015                                                   '          '.          
00055      05  PC-TOTAL                       PIC  X(03)  VALUE 'TTL'.  INKCS011
00057      05  PC-TSYMSG-NUMBERS.                                       INKCS011
00058          10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.  INKCS011
00059          10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.  INKCS011
00060          10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.  INKCS011
00061          10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.  INKCS011
00062          10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.  INKCS011
00063          10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.  INKCS011
00064          10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.  INKCS011
00065          10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.  INKCS011
00066          10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.  INKCS011
00067          10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.  INKCS011
00068          10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.  INKCS011
P25248         10  PC-TSYMSG-00098            PIC  9(05)  VALUE 00098.  INKCS011
00069          10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.  INKCS011
00070          10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.  INKCS011
00071          10  PC-TSYMSG-00551            PIC  9(05)  VALUE 00551.  INKCS011
W30692         10  PC-TSYMSG-01714            PIC  9(05)  VALUE 01714.  INKCS011
00072          10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.  INKCS011
00073 *                                                                 INKCS011
00074  01  PS-PROGRAM-SWITCHES.                                         INKCS011
00075      05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.    INKCS011
00076          88  PS-NO-ERROR                            VALUE 'Y'.    INKCS011
00077          88  PS-ERROR                               VALUE 'N'.    INKCS011
R25248     05  PS-PROCESSED-SW                PIC  X(01)  VALUE 'N'.    INKCS011
R25248         88  RECORD-PROCESSED                       VALUE 'Y'.    INKCS011
00078      05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.    INKCS011
00079          88  PS-COMMAREA-VALID                      VALUE 'Y'.    INKCS011
00080          88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.    INKCS011
00081      05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.    INKCS011
00082          88  PS-DONE                                VALUE 'Y'.    INKCS011
00083          88  PS-NOT-DONE                            VALUE 'N'.    INKCS011
00084      05  PS-STORE-REC-SW                PIC  X(01)  VALUE 'N'.    INKCS011
00085          88  PS-STORE-REC                           VALUE 'Y'.    INKCS011
00086          88  PS-NO-STORE-REC                        VALUE 'N'.    INKCS011
W25248     05  PS-DUPLICATE-RECORD-SW         PIC  X(01)  VALUE 'N'.    INKCS011
W25248         88  PS-END-OF-DUP-CHECK                    VALUE 'Y'.    INKCS011
W25248         88  PS-DUPLICATE-KEY                       VALUE 'N'.    INKCS011
P25248     05  PS-INTERNAL-READER-ENQ-DEQ-IND PIC X(1)    VALUE 'D'.            
P25248         88  PS-INTERNAL-READER-ENQUEUED            VALUE 'E'.            
P25248         88  PS-INTERNAL-READER-DEQUEUED            VALUE 'D'.            
00087 *                                                                 INKCS011
00088  01  PV-PROGRAM-VARIABLES.                                        INKCS011
W26600     05  PV-LOC-NBR           PIC X(04)  VALUE SPACES.            INKCS006
W26600     05  PV-LOC-NBR-NUM REDEFINES                                 INKCS006
W26600         PV-LOC-NBR           PIC 9(04).                          INKCS006
00089      05  PV-SUB                         PIC S9(04)  VALUE +0      INKCS011
00090                                                     COMP SYNC.    INKCS011
00091      05  PV-REMAINDER                   PIC S9(04)  VALUE +0      INKCS011
00092                                                     COMP SYNC.    INKCS011
00093      05  PV-READ-COUNT                  PIC S9(09)  VALUE +0      INKCS011
00094                                                     COMP-3.       INKCS011
00095      05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0      INKCS011
00096                                                     COMP-3.       INKCS011
00097      05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0      INKCS011
00098                                                     COMP-3.       INKCS011
00099      05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0      INKCS011
00100                                                     COMP-3.       INKCS011
00101      05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0      INKCS011
00102                                                     COMP-3.       INKCS011
00103      05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0      INKCS011
00104                                                     COMP-3.       INKCS011
00105      05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0      INKCS011
00106                                                     COMP-3.       INKCS011
00107      05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0      INKCS011
00108                                                     COMP SYNC.    INKCS011
00109      05  PV-CICS-RESP                   PIC S9(04)  VALUE +0      INKCS011
00110                                                     COMP SYNC.    INKCS011
00111      05  PV-ITEM                        PIC S9(04)  VALUE +0      INKCS011
00112                                                     COMP SYNC.    INKCS011
00113      05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0      INKCS011
00114                                                     COMP SYNC.    INKCS011
00115      05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0      INKCS011
00116                                                     COMP SYNC.    INKCS011
00117      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0      INKCS011
00118                                                     COMP SYNC.    INKCS011
00119      05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0      INKCS011
00120                                                     COMP SYNC.    INKCS011
00121      05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0      INKCS011
00122                                                     COMP SYNC.    INKCS011
00123      05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS011
00124                                                     COMP SYNC.    INKCS011
00125      05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS011
00126                                                     COMP SYNC.    INKCS011
00127      05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0      INKCS011
00128                                                     COMP SYNC.    INKCS011
00129      05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0      INKCS011
00130                                                     COMP SYNC.    INKCS011
00131      05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0      INKCS011
00132                                                     COMP SYNC.    INKCS011
00133      05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0      INKCS011
00134                                                     COMP SYNC.    INKCS011
R26682     05  PV-INV-QTY-TTL                 PIC S9(08)  VALUE +0      INKCS011
R26682                                                    COMP-3.       INKCS011
P0133 *    05  PV-PR-EXT-AMT-TTL              PIC S9(09)V99 VALUE +0    INKCS011
W21015     05  PV-PR-EXT-AMT-TTL              PIC S9(12)V99 VALUE +0    INKCS011
P0134                                                     COMP-3.       INKCS011
00135      05  PV-START-ITEM-REQUEST          PIC  X(05).               INKCS011
00136      05  PV-ROW-TYPE                    PIC  X(03).               INKCS011
00137          88  PV-TSUMINV-ROW                         VALUE 'SUM'.  INKCS011
00138          88  PV-TADJADD                             VALUE 'ADJ'.  INKCS011
W21015         88  PV-TADJDTL                             VALUE 'DTL'.  INKCS011
W21015     05  PV-TADJDTL-ROW.                                          INKCS011
W21732         10  PV-SKU                     PIC 9(08)   VALUE ZEROES. INKCS011
W21015         10  PV-TADJDTL-PR-AMT          PIC S9(07)V99  COMP-3     INKCS011
W21015                                                    VALUE +0.     INKCS011
W25248     05  PV-PREVIOUS-KEY.                                         INKCS011
W25248         10  PV-PREV-SKU-NBR            PIC  S9(8)V     COMP-3.   INKCS011
W25248         10  PV-PREV-UPC-NBR            PIC  S9(15)V    COMP-3.   INKCS011
W25248         10  PV-PREV-UNIT-RETAIL        PIC  S9(5)V9(2) COMP-3.   INKCS011
W25248         10  PV-PREV-SOURCE-TABLE       PIC  X(3)   VALUE SPACES. INKCS011
W25248                                                                  INKCS011
W25248     05  PV-CURRENT-KEY.                                          INKCS011
W25248         10  PV-CURR-SKU-NBR            PIC  S9(8)V     COMP-3.   INKCS011
W25248         10  PV-CURR-UPC-NBR            PIC  S9(15)V    COMP-3.   INKCS011
W25248         10  PV-CURR-UNIT-RETAIL        PIC  S9(5)V9(2) COMP-3.   INKCS011
W25248         10  PV-CURR-SOURCE-TABLE       PIC  X(3)   VALUE SPACES. INKCS011
W25248                                                                  INKCS011
W25248     05  PV-KEY-RECORD.                                           INKCS011
W25248         10 PV-KEY-SKU           PIC  S9(8)V     COMP-3 VALUE +0. INKCS011
W25248         10 PV-KEY-UPC           PIC  S9(15)V    COMP-3 VALUE +0. INKCS011
W25248         10 PV-KEY-QTY           PIC  S9(5)V     COMP-3 VALUE +0. INKCS011
W25248         10 PV-KEY-RTL           PIC  S9(5)V9(2) COMP-3 VALUE +0. INKCS011
W25248         10 PV-KEY-EXTD-RTL      PIC  S9(8)V9(2) COMP-3 VALUE +0. INKCS011
W25248                                                                  INKCS011
W25248     05  PV-HOLD-DMS.                                             INKCS011
W25248         10  PV-DMS-DEPT                PIC X(03).                INKCS011
W25248         10  FILLER                     PIC X(01)   VALUE '-'.    INKCS011
W25248         10  PV-DMS-MCL                 PIC X(03).                INKCS011
W25248         10  FILLER                     PIC X(01)   VALUE '-'.    INKCS011
W25248         10  PV-DMS-SCL                 PIC X(03).                INKCS011
W25248                                                                  INKCS011
W25248     05  PV-HOLD-SKU                    PIC 9(08)   VALUE ZEROES. INKCS011
W25248     05  PV-HOLD-DEPT                   PIC 9(03)   VALUE  0.     INKCS011
W25248     05  PV-HOLD-MCL                    PIC 9(03)   VALUE  0.     INKCS011
W25248     05  PV-HOLD-SCL                    PIC 9(03)   VALUE  0.     INKCS011
W25248                                                                  INKCS011
W25248     05  PV-HOLD-TEMP-MCL.                                        INKCS011
W25248         10 FILLER                      PIC 9(02)   VALUE ZEROES. INKCS011
W25248         10 PV-HOLD-MCL-HLQ             PIC 9(01)   VALUE ZEROES. INKCS011
W25248         10 FILLER                      PIC 9(01)   VALUE ZEROES. INKCS011
W25248     05  PV-MAJOR-CLASS                 PIC 9(04)   VALUE ZEROES. INKCS011
P25248     05  PV-CICS-ID.                                                      
P25248         10  PV-FIRST-4              PIC X(4)       VALUE SPACES.         
P25248         10  PV-CICS-REGION-TYPE     PIC X          VALUE SPACE.          
P25248             88  PV-CICS-REGION-PRODUCTION          VALUE 'P'.            
P25248             88  PV-CICS-REGION-ACCEPTANCE          VALUE 'A'.            
P25248             88  PV-CICS-REGION-QUALITY             VALUE 'Q'.            
P25248             88  PV-CICS-REGION-LEARNING            VALUE 'L'.            
P25248             88  PV-CICS-REGION-TEST                VALUE 'T'.            
P25248             88  PV-CICS-REGION-SYSTEST             VALUE 'S'.            
P25248         10  PV-CICS-REGION-CODE     PIC X          VALUE SPACES.         
P25248         10  PV-LAST-2               PIC X(2)       VALUE SPACES.         
W30692                                                                          
W30692     05  PV-EDIT-LOC-NBR           PIC 9(04)  VALUE ZEROS.        INKCS011
W30692     05  PV-EDIT-LOC-NBR-RED REDEFINES                            INKCS011
W30692         PV-EDIT-LOC-NBR.                                         INKCS011
W30692         10  PV-EDIT-LOC-NBR-P1    PIC 9(01).                     INKCS011
W30692         10  PV-EDIT-LOC-NBR-P2    PIC 9(03).                     INKCS011
W30692     05  PV-USER-USER-CODE         PIC X(08)  VALUE SPACES.       INKCS011
W28545     05  PV-PR-AMT                 PIC S9(07)V99  COMP-3.         INKCS011
W30692                                                                          
00140 *----------------------------------------------------------------*INKCS011
00141 *   THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.    *INKCS011
00142 *   THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE            *INKCS011
00143 *   APPLICATION-SPECIFIC COMMAREA.                               *INKCS011
00144 *----------------------------------------------------------------*INKCS011
00145  01  LQW-LIST-QUEUE-WORK-AREA.                                    INKCS011
00146      05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.  INKCS011
00147      05  LQW-ITEM-ENTRIES               OCCURS 13 TIMES           INKCS011
00148                                         INDEXED BY LQW-IDX.       INKCS011
00149          10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.       INKCS011
00150          10  LQW-TEMP-SELECTED-SW       PIC  X(01).               INKCS011
00151          10  LQW-SELECTED-SW            PIC  X(01).               INKCS011
00152              88  LQW-SELECT                         VALUE 'S'.    INKCS011
00153              88  LQW-DESELECT                       VALUE ' '.    INKCS011
00154          10  LQW-ACTION                 PIC  X(03).               INKCS011
W25248         10  LQW-DMS                    PIC  X(11).               INKCS011
W25248         10  LQW-KEY.                                             INKCS011
W25248             15  LQW-UPC                PIC  9(07)  VALUE ZEROES. INKCS011
W25248             15  LQW-SKU                PIC  9(08)  VALUE ZEROES. INKCS011
W30692         10  LQW-INV-QTY                PIC S9(06)        COMP-3. INKCS011
W25248         10  LQW-PR-AMT                 PIC S9(07)V99     COMP-3. INKCS011
W25248         10  LQW-PR-EXT-AMT             PIC S9(09)V99     COMP-3. INKCS011
00161          10  LQW-COLOR                  PIC X.                    INKCS011
00162                                                                   INKCS011
00162                                                                   INKCS011
00163      EJECT                                                        INKCS011
00164 *---------------------------------------------------------------* INKCS011
00165 *  RECORD LAYOUT FOR THE MESSAGE SELECTION TEMP STORAGE QUEUE.  * INKCS011
W25248*  FORMAT IS READ INTO THE SKU/UPC ADJUSTMENT SCREEN (I012)     * INKCS011
00166 *---------------------------------------------------------------* INKCS011
00167                                                                   INKCS011
           COPY INWS012.                                                INKCS011
00169                                                                   INKCS011
00164 *---------------------------------------------------------------* INKCS011
00165 *  RECORD LAYOUT FOR THE MESSAGE SELECTION TEMP STORAGE QUEUE.  * INKCS011
W25248*  FORMAT IS READ INTO THE SKU/UPC LIST ADJUSTMENT SCREEN (I013)* INKCS011
00166 *---------------------------------------------------------------* INKCS011
00167                                                                   INKCS011
00168      COPY INWS027.                                                INKCS011
00169                                                                   INKCS011
00170      EJECT                                                        INKCS011
00171 *---------------------------------------------------------------* INKCS011
00172 *    MAP LAYOUT                                                 * INKCS011
00173 *---------------------------------------------------------------* INKCS011
00174                                                                   INKCS011
W26600     COPY INKM011.                                                INKCS011
00176 *                                                                 INKCS011
00177  01  MR-OCCURS-LAYOUT                   REDEFINES  IN011AO.       INKCS011
W26600     05  FILLER                         PIC X(93).                INKCS011
00179      05  MR-SCROLL-AREA                 OCCURS 14 TIMES.          INKCS011
00180          10  MR-SELECTL                 PIC S9(04) COMP.          INKCS011
00181          10  MR-SELECTA                 PIC  X(01).               INKCS011
00182          10  MR-SELECTC                 PIC  X(01).               INKCS011
00183          10  MR-SELECTH                 PIC  X(01).               INKCS011
00184          10  MR-SELECTI                 PIC  X(01).               INKCS011
00185                                                                   INKCS011
00186          10  MR-ACTIONL                 PIC S9(04) COMP.          INKCS011
00187          10  MR-ACTIONA                 PIC  X(01).               INKCS011
00188          10  MR-ACTIONC                 PIC  X(01).               INKCS011
00189          10  MR-ACTIONH                 PIC  X(01).               INKCS011
00190          10  MR-ACTION                  PIC  X(03).               INKCS011
00191                                                                   INKCS011
W25248         10  MR-DMSL                    PIC S9(04) COMP.          INKCS011
W25248         10  MR-DMSA                    PIC  X(01).               INKCS011
W25248         10  MR-DMSC                    PIC  X(01).               INKCS011
W25248         10  MR-DMSH                    PIC  X(01).               INKCS011
W25248         10  MR-DMS                     PIC  X(11).               INKCS011
W25248                                                                  INKCS011
W21732         10  MR-SKUL                    PIC S9(04) COMP.          INKCS011
W21732         10  MR-SKUA                    PIC  X(01).               INKCS011
W21732         10  MR-SKUC                    PIC  X(01).               INKCS011
W21732         10  MR-SKUH                    PIC  X(01).               INKCS011
W21732         10  MR-SKU                     PIC  X(08).               INKCS011
00209                                                                   INKCS011
00210          10  MR-INV-QTYL                PIC S9(04) COMP.          INKCS011
00211          10  MR-INV-QTYA                PIC  X(01).               INKCS011
00212          10  MR-INV-QTYC                PIC  X(01).               INKCS011
00213          10  MR-INV-QTYH                PIC  X(01).               INKCS011
00214          10  MR-INV-QTY-X.                                        INKCS011
W25248*            15  MR-INV-QTY             PIC  -(6)9.               INKCS011
00215              15  MR-INV-QTY             PIC  -(5)9.               INKCS011
00216                                                                   INKCS011
00217          10  MR-PR-AMTL                 PIC S9(04) COMP.          INKCS011
00218          10  MR-PR-AMTA                 PIC  X(01).               INKCS011
00219          10  MR-PR-AMTC                 PIC  X(01).               INKCS011
00220          10  MR-PR-AMTH                 PIC  X(01).               INKCS011
00221          10  MR-PR-AMT-X.                                         INKCS011
00222              15  MR-PR-AMT              PIC  Z(7).99.             INKCS011
00223                                                                   INKCS011
00224          10  MR-PR-EXT-AMTL             PIC S9(04) COMP.          INKCS011
00225          10  MR-PR-EXT-AMTA             PIC  X(01).               INKCS011
00226          10  MR-PR-EXT-AMTC             PIC  X(01).               INKCS011
00227          10  MR-PR-EXT-AMTH             PIC  X(01).               INKCS011
00228          10  MR-PR-EXT-AMT-X.                                     INKCS011
W25248*            15  MR-PR-EXT-AMT          PIC  -(10).99.            INKCS011
W25248             15  MR-PR-EXT-AMT          PIC  -(09).99.            INKCS011
00230                                                                   INKCS011
00231          10  MR-ADJ-MSGL                PIC S9(04) COMP.          INKCS011
00232          10  MR-ADJ-MSGA                PIC  X(01).               INKCS011
00233          10  MR-ADJ-MSGC                PIC  X(01).               INKCS011
00234          10  MR-ADJ-MSGH                PIC  X(01).               INKCS011
00235          10  MR-ADJ-MSG                 PIC  X(10).               INKCS011
00236                                                                   INKCS011
00237 /                                                                 INKCS011
00238 *---------------------------------------------------------------* INKCS011
00239 *    ATTRIBUTE SETTINGS COPYBOOK.                               * INKCS011
00240 *---------------------------------------------------------------* INKCS011
00241                                                                   INKCS011
00242      COPY DPWS015.                                                INKCS011
00243                                                                   INKCS011
W30692     COPY DPWS026.                                                        
W30692                                                                  INKCS055
00244 *---------------------------------------------------------------* INKCS011
00245 *    FUNCTION KEYS COPYBOOK.                                    * INKCS011
00246 *---------------------------------------------------------------* INKCS011
00247                                                                   INKCS011
00248      COPY DPWS016.                                                INKCS011
00249                                                                   INKCS011
00250 *---------------------------------------------------------------* INKCS011
00251 *    NUMERIC EDIT LAYOUT.                                       * INKCS011
00252 *---------------------------------------------------------------* INKCS011
00253                                                                   INKCS011
00254      COPY DPWS010I.                                               INKCS011
00255 /                                                                 INKCS011
00256 *---------------------------------------------------------------* INKCS011
00257 *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   * INKCS011
00258 *---------------------------------------------------------------* INKCS011
00259                                                                   INKCS011
00260      COPY DPWS030.                                                INKCS011
1122RM     COPY DPWS930.                                                INKCS011
00261 /                                                                 INKCS011
00262 *---------------------------------------------------------------* INKCS011
00263 *    STANDARD COMMAREA.                                         * INKCS011
00264 *---------------------------------------------------------------* INKCS011
00265                                                                   INKCS011
00266      COPY DPWS020.                                                INKCS011
00267      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                INKCS011
00268                                                                   INKCS011
00269          10  INTER-APPL-COMM-AREA       PIC X(200).               INKCS011
W26600     COPY INWS001.                                                INKCS011
00271 *---------------------------------------------------------------* INKCS011
00272 *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   * INKCS011
00273 *---------------------------------------------------------------* INKCS011
00274          10  ASC-SPECIFIC-COMMAREA.                               INKCS011
00275              15  ASC-LIST-KEYS.                                   INKCS011
W26600                 20  ASC-KEY-STORE-NBR               PIC  X(04).  INKCS011
00277                  20  ASC-KEY-AREA-NBR                PIC S9(08)   INKCS011
00278                                                      COMP.        INKCS011
00279              15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                INKCS011
00280                  20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).  INKCS011
00281                      88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.   INKCS011
00282                      88  ASC-BLOCK-MODIFIED          VALUE 'Y'.   INKCS011
00283                                                                   INKCS011
00284                  20  ASC-ITEM-ARRAY.                              INKCS011
00285                      25  ASC-ITEM-ENTRIES OCCURS 13 TIMES.        INKCS011
00286                          30  ASC-LIST-ITEM-NUMBER                 INKCS011
00287                                                      PIC S9(09)   INKCS011
00288                                                      COMP-3.      INKCS011
00289                          30  ASC-TEMP-SELECTED-SW    PIC  X(01).  INKCS011
00290                              88  ASC-TEMP-NO-SELECT  VALUE ' '.   INKCS011
00291                              88  ASC-TEMP-SELECT     VALUE 'S'.   INKCS011
00292                              88  ASC-TEMP-BEGIN      VALUE 'B'.   INKCS011
00293                              88  ASC-TEMP-END        VALUE 'E'.   INKCS011
00294                          30  ASC-SELECTED-SW         PIC  X(01).  INKCS011
00295                              88  ASC-NO-SELECT       VALUE ' '.   INKCS011
00296                              88  ASC-SELECT          VALUE 'S'.   INKCS011
00297                              88  ASC-BEGIN           VALUE 'B'.   INKCS011
00298                              88  ASC-END             VALUE 'E'.   INKCS011
00299                          30  ASC-ACTION              PIC  X(03).  INKCS011
W25248                         30  ASC-DMS                 PIC  X(11).  INKCS011
W25248                         30  ASC-KEY-NUMBER.                      INKCS011
W25248                             35  ASC-KEY-UPC         PIC 9(07).   INKCS011
W25248                             35  ASC-SKU             PIC 9(08).   INKCS011
W25248                         30  ASC-INV-QTY             PIC S9(06)   INKCS011
                                                                COMP-3. INKCS011
W25248                         30  ASC-PR-AMT              PIC S9(07)V99INKCS011
00306                                                           COMP-3. INKCS011
W25248                         30  ASC-PR-EXT-AMT          PIC S9(09)V99INKCS011
00308                                                           COMP-3. INKCS011
00309                          30  ASC-COLOR               PIC X.       INKCS011
00310                                                                   INKCS011
00311              15  ASC-BLK-SUB                         PIC S9(04)   INKCS011
00312                                                           COMP    INKCS011
00313                                                           SYNC.   INKCS011
00314              15  ASC-ITEM-SUB                        PIC S9(04)   INKCS011
00315                                                           COMP    INKCS011
00316                                                           SYNC.   INKCS011
00317              15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)   INKCS011
00318                                                           COMP-3. INKCS011
00319              15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)   INKCS011
00320                                                           COMP-3. INKCS011
00321              15  ASC-ITEMS-DISPLAYED                 PIC S9(03)   INKCS011
00322                                                           COMP-3. INKCS011
00323              15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)   INKCS011
00324                                                           COMP-3. INKCS011
00325              15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)   INKCS011
00326                                                           COMP-3. INKCS011
00327              15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)   INKCS011
00328                                                           COMP    INKCS011
00329                                                           SYNC.   INKCS011
00330              15  ASC-START-OF-SELECTED-RANGE          PIC S9(09)  INKCS011
00331                                                           COMP-3. INKCS011
00332              15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)   INKCS011
00333                                                           COMP-3. INKCS011
00334              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).  INKCS011
00335                  88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.   INKCS011
00336                  88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.   INKCS011
00337              15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).  INKCS011
00338                  88  ASC-UPDATE-TSQ                  VALUE 'Y'.   INKCS011
00339                  88  ASC-NO-TSQ-UPDATE               VALUE ' '.   INKCS011
00340              15  ASC-LIST-QUEUE-NAME.                             INKCS011
00341                  20  ASC-LIST-TERM-ID                PIC  X(04).  INKCS011
00342                  20  ASC-LIST-TASK-NUMBER            PIC S9(05)   INKCS011
00343                                                           COMP-3. INKCS011
00344                  20  ASC-LIST-SEQ                    PIC  9(01).  INKCS011
00345              15  ASC-SEL-QUEUE-NAME.                              INKCS011
00346                  20  ASC-SEL-TERM-ID                 PIC  X(04).  INKCS011
00347                  20  ASC-SEL-TASK-NUMBER             PIC S9(05)   INKCS011
00348                                                           COMP-3. INKCS011
00349                  20  ASC-SEL-SEQ                     PIC  9(01).  INKCS011
00350                                                                   INKCS011
00351              15  ASC-CURSOR-SW                       PIC  X(01).  INKCS011
00352                  88  ASC-NO-CURSOR                   VALUE 'Z'.   INKCS011
00353                  88  ASC-SYSTEM-ID-CURSOR            VALUE 'S'.   INKCS011
00354                  88  ASC-MESSAGE-NUMBER-CURSOR       VALUE 'N'.   INKCS011
00355                  88  ASC-MESSAGE-TEXT-CURSOR         VALUE 'T'.   INKCS011
00356              15  ASC-START-ITEM-REQUEST.                          INKCS011
00357                  20  ASC-START-ITEM-REQUEST-N        PIC  9(05).  INKCS011
W25248             15  ASC-RETURN-FROM-MNEMONIC            PIC X(8).    INKCS011
00358              15  ASC-HOLD-INTER-APPL                 PIC X(200).  INKCS011
W26600****      10 FILLER                                  PIC X(1233). INKCS011
00359 /                                                                 INKCS011
00360 *---------------------------------------------------------------* INKCS011
00361 *    ABEND PROCESSING COPYBOOK.                                 * INKCS011
00362 *---------------------------------------------------------------* INKCS011
00363                                                                   INKCS011
00364      COPY DPWS013.                                                INKCS011
00365                                                                   INKCS011
00366 /                                                                 INKCS011
00367 *---------------------------------------------------------------* INKCS011
00368 *    DB2 AREA FOR SUMMARIZED INVENTORY DATA TABLE               * INKCS011
00369 *---------------------------------------------------------------* INKCS011
00370      EXEC SQL                                                     INKCS011
00371           INCLUDE TSUMINV                                         INKCS011
00372      END-EXEC.                                                    INKCS011
00373 *---------------------------------------------------------------* INKCS011
00374 *    DB2 AREA FOR ADJUSTMENT ADD TABLE                          * INKCS011
00375 *---------------------------------------------------------------* INKCS011
00376      EXEC SQL                                                     INKCS011
00377           INCLUDE TADJADD                                         INKCS011
00378      END-EXEC.                                                    INKCS011
W21015*---------------------------------------------------------------* INKCS011
W21015*    DB2 AREA FOR ADJUSTMENT DETAIL TABLE                       * INKCS011
W21015*---------------------------------------------------------------* INKCS011
W21015     EXEC SQL                                                     INKCS011
W21015          INCLUDE TADJDTL                                         INKCS011
W21015     END-EXEC.                                                    INKCS011
W25248*---------------------------------------------------------------* INKCS011
W28545*    DB2 AREAS AND COPYBOOKS FOR COMMON PRICING LOOK UP         * INKCS011
W25248*---------------------------------------------------------------* INKCS011
W28545                                                                  INKCS011
W28545     COPY INWS400.                                                INKCS011
W28545                                                                  INKCS011
W25248*---------------------------------------------------------------* INKCS011
W25248*    DB2 AREA FOR USER PRINTER DEFAULT ID                      *  INKCS011
W25248*---------------------------------------------------------------* INKCS011
W25248     EXEC SQL                                                     INKCS011
W25248          INCLUDE TUSER                                           INKCS011
W25248     END-EXEC.                                                    INKCS011
00379 *---------------------------------------------------------------* INKCS011
00380 *    DB2 AREA FOR COMMUNICATIONS                                * INKCS011
00381 *---------------------------------------------------------------* INKCS011
00382      EXEC SQL                                                     INKCS011
00383           INCLUDE SQLCA                                           INKCS011
00384      END-EXEC.                                                    INKCS011
00385                                                                   INKCS011
00386 *---------------------------------------------------------------* INKCS011
00387 *    TSUMINV UNION WITH TADJADD CURSOR DECLARATION.             * INKCS011
00388 *---------------------------------------------------------------* INKCS011
00389                                                                   INKCS011
00390      EXEC SQL                                                     INKCS011
00391           DECLARE SUMMARY-CSR CURSOR                              INKCS011
00392           FOR SELECT                                              INKCS011
W21732                SKU_NBR                                           INKCS011
W25248              , UPC_NBR                                           INKCS011
00396               , INV_UNIT_PR_AMT                                   INKCS011
00397               , INV_QTY                                           INKCS011
00398               , EXTD_AMT                                          INKCS011
00399               , 'SUM'                                             INKCS011
00400           FROM  TSUMINV                                           INKCS011
W26600          WHERE LOC_NBR    =   :SUMINV-LOC-NBR                    INKCS011
W21732            AND AREA_NBR     =   :SUMINV-AREA-NBR                 INKCS011
00407         UNION                                                     INKCS011
00409           SELECT                                                  INKCS011
00412                 SKU_NBR                                           INKCS011
W25248              , UPC_NBR                                           INKCS011
00413               , ADJ_UNIT_PR_AMT                                   INKCS011
00414               , ADJ_QTY                                           INKCS011
00415               , ADJ_EXTD_AMT                                      INKCS011
00416               , 'ADJ'                                             INKCS011
00417           FROM  TADJADD                                           INKCS011
W26600          WHERE LOC_NBR    =   :ADJADD-LOC-NBR                    INKCS011
W21732            AND AREA_NBR     =   :ADJADD-AREA-NBR                 INKCS011
00423           ORDER BY 1, 3                                           INKCS011
00427      END-EXEC.                                                    INKCS011
00428                                                                   INKCS011
00429 /                                                                 INKCS011
00430  LINKAGE SECTION.                                                 INKCS011
00431                                                                   INKCS011
00432  01  DFHCOMMAREA.                                                 INKCS011
00433      05  FILLER                         OCCURS  1 TO 4072 TIMES   INKCS011
00434                                         DEPENDING ON EIBCALEN.    INKCS011
00435          10  FILLER                     PIC X.                    INKCS011
00436                                                                   INKCS011
00437  PROCEDURE DIVISION.                                              INKCS011
00438 ******************************************************************INKCS011
00439 **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **INKCS011
00440 **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **INKCS011
00441 **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **INKCS011
00442 ******************************************************************INKCS011
00443                                                                   INKCS011
00444  0000-MAIN-MODULE.                                                INKCS011
00445                                                                   INKCS011
00446      INITIALIZE DP030-CICS-API-FIELDS.                            INKCS011
00447      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       INKCS011
00448      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          INKCS011
00449      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         INKCS011
00450      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       INKCS011
00451                                                                   INKCS011
00452      MOVE LENGTH OF IN011AI        TO DP030-MAP-LENGTH (1).       INKCS011
00453      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     INKCS011
00454      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS011
00455                                                                   INKCS011
00456      PERFORM 1000-CONTROL-PROCESSING                              INKCS011
00457                                                                   INKCS011
00458      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     INKCS011
00459      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS011
00460                                                                   INKCS011
00461 *----------------------------------------------------------------*INKCS011
00462 *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *INKCS011
00463 *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *INKCS011
00464 *----------------------------------------------------------------*INKCS011
00465                                                                   INKCS011
00466  0001-CALL-CICS-ARCH-API.                                         INKCS011
00467                                                                   INKCS011
00468***   CALL 'DPKCS030' USING DFHEIBLK                               INKCS011
1122RM     CALL DP930-CICS-ARCH-API                                             
1122RM                     USING DFHEIBLK                                       
00469                            DFHCOMMAREA                            INKCS011
00470                            DP030-CICS-API-FIELDS                  INKCS011
00471                            DP020-STANDARD-COMMAREA                INKCS011
00472                            IN011AI.                               INKCS011
00473                                                                   INKCS011
00474      IF  DP030-RC-CALL-SUCCESSFUL                                 INKCS011
00475          CONTINUE                                                 INKCS011
00476      ELSE                                                         INKCS011
00477          SET DP013-NO-ROLLBACK                                    INKCS011
00478              DP013-XCTL-DISPLAY-RESTART                           INKCS011
00479              DP013-CICS-ABEND      TO TRUE                        INKCS011
00480          MOVE '0001-CALL-CICS-ARCH-API'                           INKCS011
00481                                    TO DP013-PARAGRAPH             INKCS011
00482***       MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEXINKCS011
00483***            'T LINE'             TO DP013-MESSAGE-TEXT (2)      INKCS011
1122RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1122RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00484          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      INKCS011
00485          PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
00486      END-IF.                                                      INKCS011
00487                                                                   INKCS011
00488 *----------------------------------------------------------------*INKCS011
00489 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *INKCS011
00490 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *INKCS011
00491 *                                                                *INKCS011
00492 * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *INKCS011
00493 *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *INKCS011
00494 *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *INKCS011
00495 *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *INKCS011
00496 *----------------------------------------------------------------*INKCS011
00497                                                                   INKCS011
00498  1000-CONTROL-PROCESSING.                                         INKCS011
00499                                                                   INKCS011
00500      EVALUATE TRUE                                                INKCS011
00501          WHEN DP020-NEXT-ACT-INITIAL                              INKCS011
00502               INITIALIZE ASC-SPECIFIC-COMMAREA                    INKCS011
00503               MOVE EIBTRMID         TO  ASC-LIST-TERM-ID          INKCS011
00504                                         ASC-SEL-TERM-ID           INKCS011
00505               MOVE DP020-SRC-TASK-NUMBER                          INKCS011
00506                                     TO  ASC-LIST-TASK-NUMBER      INKCS011
00507                                         ASC-SEL-TASK-NUMBER       INKCS011
00508               MOVE PC-LIST-QUEUE-SEQ                              INKCS011
00509                                     TO  ASC-LIST-SEQ              INKCS011
00510               MOVE PC-SEL-QUEUE-SEQ TO  ASC-SEL-SEQ               INKCS011
00511                                                                   INKCS011
00512               MOVE INTER-APPL-COMM-AREA                           INKCS011
00513                                     TO  ASC-HOLD-INTER-APPL       INKCS011
00514               PERFORM 4000-BUILD-INITIAL-PANEL                    INKCS011
00515                                                                   INKCS011
00516          WHEN DP020-NEXT-ACT-READ-MAP                             INKCS011
00517               MOVE ASC-ITEM-AT-TOP-OF-PANEL                       INKCS011
00518                                    TO PV-TOP-ITEM                 INKCS011
00519               PERFORM 2000-PROCESS-PANEL                          INKCS011
00520                                                                   INKCS011
00521          WHEN DP020-NEXT-ACT-RETURN                               INKCS011
00522               MOVE ASC-ITEM-AT-TOP-OF-PANEL                       INKCS011
00523                                    TO PV-TOP-ITEM                 INKCS011
00524               MOVE ZERO            TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS011
00525               MOVE ASC-HOLD-INTER-APPL                            INKCS011
00526                                    TO INTER-APPL-COMM-AREA        INKCS011
00527***                                                                INKCS011
W25248*             DETERMINE WHICH SCREEN APPL IS RETURNING FROM       INKCS011
W25248*             USE ASC-*** VARIABLE (INAPHADJ-I012/INLADJST-I013)  INKCS011
W25248              EVALUATE ASC-RETURN-FROM-MNEMONIC                   INKCS011
W25248                  WHEN 'INAPHADJ'                                 INKCS011
W25248                      PERFORM 6000-REFRESH-PANEL-FROM-TEMP        INKCS011
W25248                  WHEN 'INLADJST'                                 INKCS011
W25248                      PERFORM 7500-REFRESH-PANEL-FROM-TEMP        INKCS011
W25248              END-EVALUATE                                        INKCS011
W25248*             DETERMINE WHICH SCREEN APPL IS RETURNING FROM       INKCS011
00528               PERFORM 3000-EDIT-DATA-IN-COMMAREA                  INKCS011
00529               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                INKCS011
00530                                                                   INKCS011
00531          WHEN OTHER                                               INKCS011
00532               SET DP013-LOGIC-ABEND TO TRUE                       INKCS011
00533               SET DP013-NO-ROLLBACK TO TRUE                       INKCS011
00534               MOVE '1000-CONTROL-PROCESSING'                      INKCS011
00535                                    TO DP013-PARAGRAPH             INKCS011
00536               MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'   INKCS011
00537                                    TO DP013-MESSAGE-TEXT(1)       INKCS011
00538               PERFORM DP013-0000-PROCESS-ABEND                    INKCS011
00539      END-EVALUATE.                                                INKCS011
00540 /                                                                 INKCS011
00541 *----------------------------------------------------------------*INKCS011
00542 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *INKCS011
00543 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *INKCS011
00544 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *INKCS011
00545 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *INKCS011
00546 * FUNCTION KEYS CAN GET THIS FAR.                                *INKCS011
00547 *----------------------------------------------------------------*INKCS011
00548                                                                   INKCS011
00549  2000-PROCESS-PANEL.                                              INKCS011
00550                                                                   INKCS011
00551      IF  IN001-STORE-NBR NOT NUMERIC                              INKCS011
00552          MOVE ASC-HOLD-INTER-APPL TO INTER-APPL-COMM-AREA         INKCS011
00553      END-IF.                                                      INKCS011
00554                                                                   INKCS011
00555      EVALUATE TRUE                                                INKCS011
00556          WHEN DP020-SRC-AID = DP016-CLEAR                         INKCS011
00557              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS011
00558              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS011
00559              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS011
00560                                                                   INKCS011
00561          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    INKCS011
00562              PERFORM 2050-DELETE-LIST-QUEUE                       INKCS011
00563              PERFORM 4000-BUILD-INITIAL-PANEL                     INKCS011
00564                                                                   INKCS011
00565          WHEN DP020-FK-DESELECT(DP020-SRC-AID)                    INKCS011
00566              PERFORM 2140-DESELECT-ALL-ITEMS                      INKCS011
00567              PERFORM 3200-RESET-ATTRIBUTES                        INKCS011
00568              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS011
00569                                                                   INKCS011
00570          WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                  INKCS011
00571              PERFORM 2150-SELECT-ALL-ITEMS                        INKCS011
00572              PERFORM 3200-RESET-ATTRIBUTES                        INKCS011
00573              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS011
00574                                                                   INKCS011
00575          WHEN OTHER                                               INKCS011
00576              PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 INKCS011
00577              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS011
00578              IF  PS-NO-ERROR                                      INKCS011
00579                  SET DP030-CONTINUE-GO                            INKCS011
00580                                  TO TRUE                          INKCS011
00581                  PERFORM 2100-CHECK-FUNCTION-KEY                  INKCS011
00582              ELSE                                                 INKCS011
00583                  PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             INKCS011
00584                  SET DP030-OVERRIDE-GO                            INKCS011
00585                                  TO TRUE                          INKCS011
00586              END-IF                                               INKCS011
00587      END-EVALUATE.                                                INKCS011
00588  EJECT                                                            INKCS011
00589 *----------------------------------------------------------------*INKCS011
00590 *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *INKCS011
00591 *----------------------------------------------------------------*INKCS011
00592  2050-DELETE-LIST-QUEUE.                                          INKCS011
00593                                                                   INKCS011
00594      EXEC CICS                                                    INKCS011
00595          DELETEQ TS                                               INKCS011
00596              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS011
00597              RESP  (PV-CICS-RESP)                                 INKCS011
00598      END-EXEC.                                                    INKCS011
00599                                                                   INKCS011
00600      IF (PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS011
00601      OR  PV-CICS-RESP = DFHRESP(QIDERR))                          INKCS011
00602          CONTINUE                                                 INKCS011
00603      ELSE                                                         INKCS011
00604          SET DP013-CICS-ABEND      TO TRUE                        INKCS011
00605          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS011
00606                                                                   INKCS011
00607          MOVE '2050-DELETE-LIST-QUEUE'                            INKCS011
00608                                    TO DP013-PARAGRAPH             INKCS011
00609          MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'    INKCS011
00610                                    TO DP013-MESSAGE-TEXT (1)      INKCS011
00611          PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
00612      END-IF.                                                      INKCS011
00613                                                                   INKCS011
00614      MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.  INKCS011
00615 /                                                                 INKCS011
00616 *----------------------------------------------------------------*INKCS011
00617 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*INKCS011
00618 * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *INKCS011
00619 * CICS ARCHITECTURE API.                                         *INKCS011
00620 *----------------------------------------------------------------*INKCS011
00621                                                                   INKCS011
00622  2100-CHECK-FUNCTION-KEY.                                         INKCS011
00623                                                                   INKCS011
00624      EVALUATE TRUE                                                INKCS011
00625          WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                  INKCS011
00626              IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO              INKCS011
00627                  MOVE ASC-NUMBER-OF-SELECTED-ITEMS                INKCS011
00628                                 TO IN012-SELECTED-ITEMS           INKCS011
00629                                    IN001-NUMBER-OF-SELECTED-ITEMS INKCS011
00630                  PERFORM 5000-PREPARE-SEL-QUEUE                   INKCS011
00631              ELSE                                                 INKCS011
00632                  SET DP030-OVERRIDE-GO                            INKCS011
00633                                    TO TRUE                        INKCS011
00634                  MOVE PC-TSYMSG-00057                             INKCS011
00635                                    TO DP020-MSG-NUMBER            INKCS011
00636                  SET DP020-MSG-WARNING                            INKCS011
00637                                    TO TRUE                        INKCS011
00638              END-IF                                               INKCS011
00639                                                                   INKCS011
00640          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  INKCS011
00641              MOVE +1               TO PV-TOP-ITEM                 INKCS011
00642              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS011
00643                                                                   INKCS011
00644          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   INKCS011
00645              PERFORM 2110-BROWSE-BACKWARD                         INKCS011
00646              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS011
00647                                                                   INKCS011
00648          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   INKCS011
00649              PERFORM 2120-BROWSE-FORWARD                          INKCS011
00650              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS011
00651                                                                   INKCS011
00652          WHEN DP020-SRC-AID = DP016-ENTER                         INKCS011
00653              PERFORM 2130-CHECK-FOR-JUMP-BROWSE                   INKCS011
00654              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS011
00655                                                                   INKCS011
W25248*        LIST DETAIL (UPC ENTRIES FOR SKU/RETAIL COMBO)                   
W25248*        WHEN DP020-FK-LOCAL-FUNC-05 (DP020-SRC-AID)                      
W25248*            IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO              INKCS011
W25248*                MOVE ASC-NUMBER-OF-SELECTED-ITEMS                INKCS011
W25248*                               TO IN027-SELECTED-ITEMS           INKCS011
W25248*                                  IN001-NUMBER-OF-SELECTED-ITEMS INKCS011
W25248*                PERFORM 7000-PREPARE-SEL-QUEUE                   INKCS011
W25248*            ELSE                                                 INKCS011
W25248*                SET DP030-OVERRIDE-GO TO TRUE                    INKCS011
W25248*                MOVE PC-TSYMSG-00057  TO DP020-MSG-NUMBER        INKCS011
W25248*                SET DP020-MSG-WARNING TO TRUE                    INKCS011
W25248*            END-IF                                               INKCS011
W25248                                                                  INKCS011
W25248*        PRINT SKU LISTING (INKMP193)                                     
P25248         WHEN DP020-FK-LOCAL-FUNC-06 (DP020-SRC-AID)                      
W30692             PERFORM DP026-0000-GET-LOGON-INFO                            
W30692             IF DP026-CICS-RESPONSE = DFHRESP(NORMAL)                     
W30692                 IF DP026-DFLT-PRTR-ID EQUAL SPACES                       
W30692                   IF DP026-LOCATION-TYPE NOT EQUAL 'HQ'                  
W30692                     PERFORM 8001-USE-STORE-DEFAULT-PRTR                  
W30692                     IF JL-JCL-INK950-PRTDEST EQUAL SPACES                
W30692                         SET DP030-OVERRIDE-GO                            
W30692                             DP020-MSG-FATAL TO TRUE                      
W30692                         MOVE PC-TSYMSG-01714                             
W30692                                           TO DP020-MSG-NUMBER            
W30692                     ELSE                                                 
W30692                         PERFORM 8000-FORMAT-AND-SUBMIT                   
W30692                         PERFORM 2130-CHECK-FOR-JUMP-BROWSE       INKCS046
W30692                         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN     INKCS046
W30692                         MOVE PC-TSYMSG-00098                             
W30692                                              TO DP020-MSG-NUMBER         
W30692                         SET DP020-MSG-INFORMATIONAL TO TRUE              
W30692                     END-IF                                               
W30692                   ELSE                                                   
W30692                       MOVE 'LOCAL'  TO JL-JCL-INK950-PRTDEST             
W30692                       MOVE 'Y'      TO JL-JCL-INK950-PRT-IND             
W30692                       PERFORM 8000-FORMAT-AND-SUBMIT                     
W30692                       PERFORM 2130-CHECK-FOR-JUMP-BROWSE         INKCS046
W30692                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN       INKCS046
W30692                       MOVE PC-TSYMSG-00098   TO DP020-MSG-NUMBER         
W30692                       SET DP020-MSG-INFORMATIONAL TO TRUE                
W30692                   END-IF                                                 
W30692                 ELSE                                                     
W30692                     MOVE DP026-DFLT-PRTR-ID                              
W30692                                        TO JL-JCL-INK950-PRTDEST          
W30692                     MOVE 'Y'           TO JL-JCL-INK950-PRT-IND          
W30692                     PERFORM 8000-FORMAT-AND-SUBMIT                       
W30692                     PERFORM 2130-CHECK-FOR-JUMP-BROWSE           INKCS046
W30692                     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN         INKCS046
W30692                     MOVE PC-TSYMSG-00098   TO DP020-MSG-NUMBER           
W30692                     SET DP020-MSG-INFORMATIONAL TO TRUE                  
W30692                 END-IF                                                   
W30692             ELSE                                                         
W30692                 SET DP013-NO-ROLLBACK                            INKCS055
W30692                     DP013-XCTL-DISPLAY-RESTART                   INKCS055
W30692                     DP013-CICS-ABEND  TO TRUE                    INKCS055
W30692                 MOVE '2100-CHECK-FUNCTION-KEY'                   INKCS055
W30692                                       TO DP013-PARAGRAPH         INKCS055
W30692                 MOVE 'DP026-0000-GET-LOGON-INFO ERROR'           INKCS055
W30692                                       TO DP013-MESSAGE-TEXT (1)  INKCS055
W30692                 PERFORM DP013-0000-PROCESS-ABEND                         
W30692             END-IF                                                       
W30692*            PERFORM 8000-FORMAT-AND-SUBMIT                               
W30692*            PERFORM 2130-CHECK-FOR-JUMP-BROWSE                   INKCS046
W30692*            PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS046
W30692*            MOVE PC-TSYMSG-00098 TO DP020-MSG-NUMBER                     
W30692*            SET DP020-MSG-INFORMATIONAL TO TRUE                          
P25248                                                                  INKCS011
00656          WHEN OTHER                                               INKCS011
00657              SET DP013-NO-ROLLBACK                                INKCS011
00658                  DP013-XCTL-DISPLAY-RESTART                       INKCS011
00659                  DP013-CICS-ABEND  TO TRUE                        INKCS011
00660              MOVE '2100-CHECK-FUNCTION-KEY'                       INKCS011
00661                                    TO DP013-PARAGRAPH             INKCS011
00662              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      INKCS011
00663                                    TO DP013-MESSAGE-TEXT (1)      INKCS011
00664              PERFORM DP013-0000-PROCESS-ABEND                     INKCS011
00665      END-EVALUATE.                                                INKCS011
00666                                                                   INKCS011
00667 *----------------------------------------------------------------*INKCS011
00668 *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *INKCS011
00669 *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *INKCS011
00670 *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *INKCS011
00671 *    MESSAGE.                                                    *INKCS011
00672 *----------------------------------------------------------------*INKCS011
00673                                                                   INKCS011
00674  2110-BROWSE-BACKWARD.                                            INKCS011
00675                                                                   INKCS011
00676      SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL    INKCS011
00677          GIVING PV-TOP-ITEM.                                      INKCS011
00678                                                                   INKCS011
00679      IF  ((PV-TOP-ITEM < +1)                                      INKCS011
00680        OR (PV-TOP-ITEM = +1))                                     INKCS011
00681          MOVE +1                   TO PV-TOP-ITEM                 INKCS011
00682 *        -- TOP OF LIST --                                        INKCS011
00683          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS011
00684          SET DP020-MSG-INFORMATIONAL TO TRUE                      INKCS011
00685      END-IF.                                                      INKCS011
00686                                                                   INKCS011
00687 /                                                                 INKCS011
00688 *----------------------------------------------------------------*INKCS011
00689 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *INKCS011
00690 *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *INKCS011
00691 *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *INKCS011
00692 *----------------------------------------------------------------*INKCS011
00693                                                                   INKCS011
00694  2120-BROWSE-FORWARD.                                             INKCS011
00695                                                                   INKCS011
00696      ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL              INKCS011
00697          GIVING PV-TOP-ITEM.                                      INKCS011
00698      IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                   INKCS011
00699          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS011
00700                                    TO PV-TOP-ITEM                 INKCS011
00701 *        -- BOTTOM OF LIST --                                     INKCS011
00702          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            INKCS011
00703          SET DP020-MSG-INFORMATIONAL                              INKCS011
00704                                    TO TRUE                        INKCS011
00705      ELSE                                                         INKCS011
00706          COMPUTE PV-BOTTOM-ITEM =                                 INKCS011
00707              PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                 INKCS011
00708          IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ            INKCS011
00709 *            -- BOTTOM OF LIST --                                 INKCS011
00710              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            INKCS011
00711              SET DP020-MSG-INFORMATIONAL                          INKCS011
00712                                    TO TRUE                        INKCS011
00713          END-IF                                                   INKCS011
00714      END-IF.                                                      INKCS011
00715 /                                                                 INKCS011
00716 *----------------------------------------------------------------*INKCS011
00717 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *INKCS011
00718 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *INKCS011
00719 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *INKCS011
00720 *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *INKCS011
00721 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *INKCS011
00722 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *INKCS011
00723 *----------------------------------------------------------------*INKCS011
00724                                                                   INKCS011
00725  2130-CHECK-FOR-JUMP-BROWSE.                                      INKCS011
00726                                                                   INKCS011
00727      SET PS-NO-ERROR               TO TRUE.                       INKCS011
00728      IF  ASC-START-ITEM-REQUEST = SPACE                           INKCS011
00729          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS011
00730                                    TO PV-TOP-ITEM                 INKCS011
00731      ELSE                                                         INKCS011
00732          MOVE ASC-START-ITEM-REQUEST                              INKCS011
00733                                    TO DP010I-UNEDITED-FIELD       INKCS011
00734                                       PV-START-ITEM-REQUEST       INKCS011
00735          MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS       INKCS011
00736          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS     INKCS011
00737          SET DP010I-NEGATIVE-NOT-ALLOWED                          INKCS011
00738                                    TO TRUE                        INKCS011
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
00740                                                                   INKCS011
00741          IF  NOT DP010I-ERROR-DETECTED                            INKCS011
00742              MOVE DP010I-NUMERIC-FIELD                            INKCS011
00743                                    TO  ASC-START-ITEM-REQUEST-N   INKCS011
00744              IF  ASC-START-ITEM-REQUEST-N > ZERO                  INKCS011
00745                  IF  ASC-START-ITEM-REQUEST-N >                   INKCS011
00746                                           ASC-NUMBER-OF-ITEMS-READINKCS011
00747                      MOVE SPACE TO ASC-START-ITEM-REQUEST         INKCS011
00748                      COMPUTE PV-TOP-ITEM =                        INKCS011
00749                              ASC-NUMBER-OF-ITEMS-READ             INKCS011
00750                              - PC-ITEMS-PER-PANEL + 1             INKCS011
00751                      IF  PV-TOP-ITEM < +1                         INKCS011
00752                          MOVE +1   TO PV-TOP-ITEM                 INKCS011
00753                          MOVE PC-TSYMSG-00070                     INKCS011
00754                                    TO DP020-MSG-NUMBER            INKCS011
00755                          SET DP020-MSG-INFORMATIONAL              INKCS011
00756                                    TO TRUE                        INKCS011
00757                      END-IF                                       INKCS011
00758                  ELSE                                             INKCS011
00759                      MOVE ASC-START-ITEM-REQUEST-N                INKCS011
00760                                    TO PV-TOP-ITEM                 INKCS011
00761                      MOVE SPACE    TO ASC-START-ITEM-REQUEST      INKCS011
00762                      COMPUTE PV-BOTTOM-ITEM =                     INKCS011
00763                          PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1     INKCS011
00764                      IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READINKCS011
00765 *                        -- BOTTOM OF LIST --                     INKCS011
00766                          MOVE PC-TSYMSG-00070                     INKCS011
00767                                    TO DP020-MSG-NUMBER            INKCS011
00768                          SET DP020-MSG-INFORMATIONAL              INKCS011
00769                                    TO TRUE                        INKCS011
00770                      END-IF                                       INKCS011
00771                  END-IF                                           INKCS011
00772              ELSE                                                 INKCS011
00773 *                --- VALUE OUT OF RANGE ----                      INKCS011
00774                  MOVE PC-TSYMSG-00101                             INKCS011
00775                                    TO DP020-MSG-NUMBER            INKCS011
00776                  MOVE -1           TO ASTRTITL                    INKCS011
00777                  SET DP030-SET-CURSOR-APPL-1                      INKCS011
00778                                    TO TRUE                        INKCS011
00779                  MOVE DP015-REVERSE                               INKCS011
00780                                    TO ASTRTITH                    INKCS011
00781                  MOVE DP015-RED    TO ASTRTITC                    INKCS011
00782                  MOVE DP015-UNP-NUM-BRT-OFF                       INKCS011
00783                                    TO ASTRTITA                    INKCS011
00784                  SET DP020-MSG-FATAL                              INKCS011
00785                                    TO TRUE                        INKCS011
00786                  SET PS-ERROR      TO TRUE                        INKCS011
00787              END-IF                                               INKCS011
00788                                                                   INKCS011
00789          ELSE                                                     INKCS011
00790              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER            INKCS011
00791              MOVE -1               TO ASTRTITL                    INKCS011
00792              SET DP030-SET-CURSOR-APPL-1 TO TRUE                  INKCS011
00793              MOVE DP015-REVERSE    TO ASTRTITH                    INKCS011
00794              MOVE DP015-RED        TO ASTRTITC                    INKCS011
00795              MOVE DP015-UNP-NUM-BRT-OFF                           INKCS011
00796                                    TO ASTRTITA                    INKCS011
00797              SET DP020-MSG-FATAL   TO TRUE                        INKCS011
00798              SET PS-ERROR          TO TRUE                        INKCS011
00799          END-IF                                                   INKCS011
00800      END-IF.                                                      INKCS011
00801 /                                                                 INKCS011
00802 *----------------------------------------------------------------*INKCS011
00803 *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *INKCS011
00804 *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *INKCS011
00805 *----------------------------------------------------------------*INKCS011
00806                                                                   INKCS011
00807  2140-DESELECT-ALL-ITEMS.                                         INKCS011
00808                                                                   INKCS011
00809      MOVE +1                       TO ASC-BLK-SUB.                INKCS011
00810                                                                   INKCS011
00811      PERFORM 2141-CLEAR-SELECTED-INDS                             INKCS011
00812          VARYING ASC-ITEM-SUB                                     INKCS011
00813          FROM 1 BY 1                                              INKCS011
00814          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS011
00815                                                                   INKCS011
00816      ADD +1                        TO ASC-BLK-SUB.                INKCS011
00817                                                                   INKCS011
00818      PERFORM 2141-CLEAR-SELECTED-INDS                             INKCS011
00819          VARYING ASC-ITEM-SUB                                     INKCS011
00820          FROM 1 BY 1                                              INKCS011
00821          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS011
00822                                                                   INKCS011
00823      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS011
00824                                    TO PV-LIST-ITEM-NUMBER.        INKCS011
00825                                                                   INKCS011
00826      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS011
00827          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS011
00828                                                                   INKCS011
00829      IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                     INKCS011
00830         (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)      INKCS011
00831          MOVE +1                   TO ASC-BLK-SUB                 INKCS011
00832                                                                   INKCS011
00833          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS011
00834                                                                   INKCS011
00835          MOVE +1                   TO PV-BLOCK-NUMBER             INKCS011
00836                                                                   INKCS011
00837          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS011
00838              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS011
00839                                                                   INKCS011
00840          PERFORM                                                  INKCS011
00841              UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY       INKCS011
00842                                                                   INKCS011
00843              PERFORM 4410-READ-TEMP-STORAGE                       INKCS011
00844                                                                   INKCS011
00845              PERFORM 2141-CLEAR-SELECTED-INDS                     INKCS011
00846                  VARYING ASC-ITEM-SUB FROM 1 BY 1                 INKCS011
00847                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS011
00848                                                                   INKCS011
00849              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS011
00850                                                                   INKCS011
00851              ADD +1                TO PV-BLOCK-NUMBER             INKCS011
00852                                                                   INKCS011
00853          END-PERFORM                                              INKCS011
00854                                                                   INKCS011
00855          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS011
00856              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS011
00857                                                                   INKCS011
00858          PERFORM                                                  INKCS011
00859              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS011
00860                    ASC-HIGHEST-BLOCK-WRITTEN                      INKCS011
00861                                                                   INKCS011
00862              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS011
00863                                    TO PV-BLOCK-NUMBER             INKCS011
00864                                                                   INKCS011
00865              PERFORM 4410-READ-TEMP-STORAGE                       INKCS011
00866                                                                   INKCS011
00867              PERFORM 2141-CLEAR-SELECTED-INDS                     INKCS011
00868                  VARYING ASC-ITEM-SUB                             INKCS011
00869                  FROM 1 BY 1                                      INKCS011
00870                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS011
00871                                                                   INKCS011
00872              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS011
00873                                                                   INKCS011
00874              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS011
00875                                                                   INKCS011
00876          END-PERFORM                                              INKCS011
00877                                                                   INKCS011
00878          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS011
00879                                    TO PV-BLOCK-NUMBER             INKCS011
00880                                                                   INKCS011
00881          PERFORM 4410-READ-TEMP-STORAGE                           INKCS011
00882      END-IF.                                                      INKCS011
00883                                                                   INKCS011
00884      MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS. INKCS011
00885      MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.INKCS011
00886      MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.  INKCS011
00887                                                                   INKCS011
00888      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        INKCS011
00889                                                                   INKCS011
00890 /                                                                 INKCS011
00891 *----------------------------------------------------------------*INKCS011
00892 *    MARK EACH ITEM AS BEING NOT SELECTED.                       *INKCS011
00893 *----------------------------------------------------------------*INKCS011
00894                                                                   INKCS011
00895  2141-CLEAR-SELECTED-INDS.                                        INKCS011
00896                                                                   INKCS011
00897      IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)           INKCS011
00898              NOT EQUAL ZERO                                       INKCS011
00899          IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS011
00900              SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS011
00901                  ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS011
00902                  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                  INKCS011
00903                                    TO TRUE                        INKCS011
00904          END-IF                                                   INKCS011
00905      END-IF.                                                      INKCS011
00906                                                                   INKCS011
00907 /                                                                 INKCS011
00908 *----------------------------------------------------------------*INKCS011
00909 *  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *INKCS011
00910 *  FOR ALL ITEMS.                                                *INKCS011
00911 *----------------------------------------------------------------*INKCS011
00912                                                                   INKCS011
00913  2150-SELECT-ALL-ITEMS.                                           INKCS011
00914                                                                   INKCS011
00915      MOVE +1                       TO ASC-BLK-SUB.                INKCS011
00916                                                                   INKCS011
00917      PERFORM 2151-SET-SELECTED-INDS                               INKCS011
00918          VARYING ASC-ITEM-SUB                                     INKCS011
00919          FROM 1 BY 1                                              INKCS011
00920          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS011
00921                                                                   INKCS011
00922      ADD +1                        TO ASC-BLK-SUB.                INKCS011
00923                                                                   INKCS011
00924      PERFORM 2151-SET-SELECTED-INDS                               INKCS011
00925          VARYING ASC-ITEM-SUB                                     INKCS011
00926          FROM 1 BY 1                                              INKCS011
00927          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS011
00928                                                                   INKCS011
00929      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS011
00930                                    TO PV-LIST-ITEM-NUMBER.        INKCS011
00931                                                                   INKCS011
00932      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS011
00933          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS011
00934                                                                   INKCS011
00935      IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                     INKCS011
00936         (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)    INKCS011
00937          MOVE +1                   TO ASC-BLK-SUB                 INKCS011
00938                                                                   INKCS011
00939          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS011
00940                                                                   INKCS011
00941          MOVE +1                   TO PV-BLOCK-NUMBER             INKCS011
00942                                                                   INKCS011
00943          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS011
00944              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS011
00945                                                                   INKCS011
00946          PERFORM                                                  INKCS011
00947              UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY       INKCS011
00948                                                                   INKCS011
00949              PERFORM 4410-READ-TEMP-STORAGE                       INKCS011
00950                                                                   INKCS011
00951              PERFORM 2151-SET-SELECTED-INDS                       INKCS011
00952                  VARYING ASC-ITEM-SUB                             INKCS011
00953                  FROM 1 BY 1                                      INKCS011
00954                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS011
00955                                                                   INKCS011
00956              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS011
00957                                                                   INKCS011
00958              ADD +1                TO PV-BLOCK-NUMBER             INKCS011
00959                                                                   INKCS011
00960          END-PERFORM                                              INKCS011
00961                                                                   INKCS011
00962          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS011
00963              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS011
00964                                                                   INKCS011
00965          PERFORM                                                  INKCS011
00966              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS011
00967                    ASC-HIGHEST-BLOCK-WRITTEN                      INKCS011
00968                                                                   INKCS011
00969              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS011
00970                                    TO PV-BLOCK-NUMBER             INKCS011
00971                                                                   INKCS011
00972              PERFORM 4410-READ-TEMP-STORAGE                       INKCS011
00973                                                                   INKCS011
00974              PERFORM 2151-SET-SELECTED-INDS                       INKCS011
00975                  VARYING ASC-ITEM-SUB                             INKCS011
00976                  FROM 1 BY 1                                      INKCS011
00977                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS011
00978                                                                   INKCS011
00979              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS011
00980                                                                   INKCS011
00981              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS011
00982                                                                   INKCS011
00983          END-PERFORM                                              INKCS011
00984                                                                   INKCS011
00985          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS011
00986                                    TO PV-BLOCK-NUMBER             INKCS011
00987                                                                   INKCS011
00988          PERFORM 4410-READ-TEMP-STORAGE                           INKCS011
00989      END-IF.                                                      INKCS011
00990                                                                   INKCS011
00991                                                                   INKCS011
00992      MOVE ASC-NUMBER-OF-ITEMS-READ TO                             INKCS011
00993                        ASC-NUMBER-OF-SELECTED-ITEMS.              INKCS011
P0993      IF ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                       INKCS011
P0993          SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            INKCS011
P0994      END-IF.                                                      INKCS011
00994                                                                   INKCS011
00995      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        INKCS011
00996                                                                   INKCS011
00997 /                                                                 INKCS011
00998 *----------------------------------------------------------------*INKCS011
00999 *    MARK EACH ITEM AS BEING SELECTED.                           *INKCS011
01000 *----------------------------------------------------------------*INKCS011
01001                                                                   INKCS011
01002  2151-SET-SELECTED-INDS.                                          INKCS011
01003                                                                   INKCS011
01004      IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)           INKCS011
01005              NOT EQUAL ZERO                                       INKCS011
01006          IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)             INKCS011
01007              SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)             INKCS011
01008                  ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)        INKCS011
01009                  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                  INKCS011
01010                                TO  TRUE                           INKCS011
01011          END-IF                                                   INKCS011
01012      END-IF.                                                      INKCS011
01013                                                                   INKCS011
01014 *----------------------------------------------------------------*INKCS011
01015 *  MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE     *INKCS011
01016 *  SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM.  *INKCS011
01017 *----------------------------------------------------------------*INKCS011
01018                                                                   INKCS011
01019  2200-MOVE-SCREEN-TO-COMMAREA.                                    INKCS011
01020                                                                   INKCS011
01021      MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.             INKCS011
01022      MOVE +1                       TO PV-SUB.                     INKCS011
01023                                                                   INKCS011
01024      IF  ASTRTITL > ZERO                                          INKCS011
01025          MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST          INKCS011
01026      ELSE                                                         INKCS011
01027          IF  ASTRTITF EQUAL DP015-ERASE-EOF                       INKCS011
01028              INITIALIZE ASC-START-ITEM-REQUEST                    INKCS011
01029          END-IF                                                   INKCS011
01030      END-IF.                                                      INKCS011
01031                                                                   INKCS011
01032      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS011
01033                                                                   INKCS011
01034      PERFORM 2210-MOVE-LINES-TO-COMMAREA                          INKCS011
01035          VARYING PV-SUB                                           INKCS011
01036          FROM 1 BY 1                                              INKCS011
01037          UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                      INKCS011
01038                                                                   INKCS011
01039 ******************************************************************INKCS011
01040 ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**INKCS011
01041 ******************************************************************INKCS011
01042                                                                   INKCS011
01043  2210-MOVE-LINES-TO-COMMAREA.                                     INKCS011
01044                                                                   INKCS011
01045      IF  MR-SELECTL (PV-SUB) > ZERO                               INKCS011
01046          SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             INKCS011
01047          MOVE MR-SELECTI (PV-SUB)                                 INKCS011
01048                                    TO ASC-TEMP-SELECTED-SW        INKCS011
01049                                  (ASC-BLK-SUB, ASC-ITEM-SUB)      INKCS011
01050      ELSE                                                         INKCS011
01051          IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                INKCS011
01052                  SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE     INKCS011
01053                  MOVE SPACE        TO ASC-TEMP-SELECTED-SW        INKCS011
01054                                  (ASC-BLK-SUB, ASC-ITEM-SUB)      INKCS011
01055          END-IF                                                   INKCS011
01056      END-IF.                                                      INKCS011
01057                                                                   INKCS011
01058      ADD +1                        TO ASC-ITEM-SUB.               INKCS011
01059      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS011
01060          ADD +1                    TO ASC-BLK-SUB                 INKCS011
01061          MOVE +1                   TO ASC-ITEM-SUB                INKCS011
01062      END-IF.                                                      INKCS011
01063                                                                   INKCS011
01064 *----------------------------------------------------------------*INKCS011
01065 *  EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE       *INKCS011
01066 *  COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY).  *INKCS011
01067 *----------------------------------------------------------------*INKCS011
01068                                                                   INKCS011
01069  3000-EDIT-DATA-IN-COMMAREA.                                      INKCS011
01070                                                                   INKCS011
01071      SET PS-NO-ERROR               TO TRUE.                       INKCS011
01072      PERFORM 3200-RESET-ATTRIBUTES.                               INKCS011
01073      MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                     INKCS011
W25248*                                                                 INKCS011
W25248     MOVE +0 TO ASC-START-OF-SELECTED-RANGE                       INKCS011
W25248                ASC-END-OF-SELECTED-RANGE.                        INKCS011
01074 **                                                                INKCS011
01075 * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.              INKCS011
01076 * EDITTING IS DONE FROM THE BOTTOM UP.                            INKCS011
01077 **                                                                INKCS011
01078      COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                       INKCS011
01079          ASC-ITEMS-DISPLAYED - 1.                                 INKCS011
01080      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS011
01081      PERFORM 3300-EDIT-SELECTIONS                                 INKCS011
01082          VARYING PV-SUB                                           INKCS011
01083          FROM PV-SUB BY -1                                        INKCS011
01084          UNTIL PV-SUB < +1.                                       INKCS011
01085                                                                   INKCS011
01086      IF  PS-NO-ERROR                                              INKCS011
01087          PERFORM 3400-CHECK-RANGE-SEQUENCE                        INKCS011
01088      END-IF.                                                      INKCS011
01089                                                                   INKCS011
01090      IF  ((PS-NO-ERROR)                                           INKCS011
01091        AND (ASC-START-OF-SELECTED-RANGE > ZERO)                   INKCS011
01092        AND (ASC-END-OF-SELECTED-RANGE > ZERO))                    INKCS011
01093              PERFORM 3500-MARK-RANGE-AS-SELECTED                  INKCS011
01094      END-IF.                                                      INKCS011
01095 /                                                                 INKCS011
01096 *----------------------------------------------------------------*INKCS011
01097 *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *INKCS011
01098 *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*INKCS011
01099 *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *INKCS011
01100 *                                                                *INKCS011
01101 *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *INKCS011
01102 *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *INKCS011
01103 *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *INKCS011
01104 *    US IN THE 2ND BLOCK OF THE ARRAY.                           *INKCS011
01105 *----------------------------------------------------------------*INKCS011
01106                                                                   INKCS011
01107  3100-SET-SUBSCRIPTS.                                             INKCS011
01108                                                                   INKCS011
01109      COMPUTE ASC-ITEM-SUB =                                       INKCS011
01110          (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.        INKCS011
01111                                                                   INKCS011
01112      IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                         INKCS011
01113          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS011
01114                                                                   INKCS011
01115          MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB               INKCS011
01116      ELSE                                                         INKCS011
01117          MOVE +1                     TO ASC-BLK-SUB               INKCS011
01118      END-IF.                                                      INKCS011
01119                                                                   INKCS011
01120 *----------------------------------------------------------------*INKCS011
01121 *  RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.      *INKCS011
01122 *----------------------------------------------------------------*INKCS011
01123                                                                   INKCS011
01124  3200-RESET-ATTRIBUTES.                                           INKCS011
01125                                                                   INKCS011
01126      MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                   INKCS011
01127      MOVE DP015-GREEN              TO ASTRTITC.                   INKCS011
01128      MOVE DP015-UNDERLINE          TO ASTRTITH.                   INKCS011
01129                                                                   INKCS011
01130      PERFORM                                                      INKCS011
01131          VARYING PV-SUB                                           INKCS011
01132          FROM 1 BY 1                                              INKCS011
01133          UNTIL PV-SUB > PC-ITEMS-PER-PANEL                        INKCS011
01134          MOVE DP015-UNP-ALP-NOR-OFF                               INKCS011
01135                                    TO MR-SELECTA (PV-SUB)         INKCS011
01136          MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)         INKCS011
01137          MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)         INKCS011
01138      END-PERFORM.                                                 INKCS011
01139                                                                   INKCS011
01140 *----------------------------------------------------------------*INKCS011
01141 *  EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS     *INKCS011
01142 *  WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'    *INKCS011
01143 *  IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR     *INKCS011
01144 *  WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.              *INKCS011
01145 *----------------------------------------------------------------*INKCS011
01146                                                                   INKCS011
01147  3300-EDIT-SELECTIONS.                                            INKCS011
01148                                                                   INKCS011
01149      EVALUATE TRUE                                                INKCS011
01150          WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS011
01151              = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS011
01152                  CONTINUE                                         INKCS011
01153                                                                   INKCS011
01154          WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS011
01155              IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS011
01156                  ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS   INKCS011
01157                  MOVE ASC-TEMP-SELECTED-SW                        INKCS011
01158                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS011
01159                                 TO ASC-SELECTED-SW                INKCS011
01160                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS011
01161              END-IF                                               INKCS011
01162                                                                   INKCS011
01163          WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS011
01164                  = SPACE                                          INKCS011
01165              EVALUATE TRUE                                        INKCS011
01166                  WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS011
01167                      SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMSINKCS011
01168                      MOVE SPACE TO ASC-SELECTED-SW                INKCS011
01169                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS011
01170                  WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS011
01171                      MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE    INKCS011
01172                      MOVE SPACE TO ASC-SELECTED-SW                INKCS011
01173                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS011
01174                  WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)          INKCS011
01175                      MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE      INKCS011
01176                      MOVE SPACE TO ASC-SELECTED-SW                INKCS011
01177                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS011
01178              END-EVALUATE                                         INKCS011
01179                                                                   INKCS011
01180          WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)           INKCS011
01181              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS011
01182                  SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS    INKCS011
01183              END-IF                                               INKCS011
01184              PERFORM 3310-EDIT-RANGE                              INKCS011
01185                                                                   INKCS011
01186          WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)             INKCS011
01187              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS011
01188                  SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS    INKCS011
01189              END-IF                                               INKCS011
01190              PERFORM 3310-EDIT-RANGE                              INKCS011
01191                                                                   INKCS011
01192          WHEN OTHER                                               INKCS011
01193              MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER           INKCS011
01194              SET DP020-MSG-FATAL    TO TRUE                       INKCS011
01195              MOVE -1                TO MR-SELECTL (PV-SUB)        INKCS011
01196              SET DP030-SET-CURSOR-APPL-1                          INKCS011
01197                                     TO TRUE                       INKCS011
01198              MOVE DP015-RED         TO MR-SELECTC (PV-SUB)        INKCS011
01199              MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)        INKCS011
01200              SET PS-ERROR TO TRUE                                 INKCS011
01201              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS011
01202                  SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS     INKCS011
01203                  MOVE SPACE         TO ASC-SELECTED-SW            INKCS011
01204                                         (ASC-BLK-SUB ASC-ITEM-SUB)INKCS011
01205              END-IF                                               INKCS011
01206                                                                   INKCS011
01207      END-EVALUATE.                                                INKCS011
01208                                                                   INKCS011
01209      SUBTRACT +1             FROM PV-ITEM-IN-USE.                 INKCS011
01210      SUBTRACT +1 FROM ASC-ITEM-SUB.                               INKCS011
01211      IF  ASC-ITEM-SUB < 1                                         INKCS011
01212          SUBTRACT +1 FROM ASC-BLK-SUB                             INKCS011
01213          MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                INKCS011
01214      END-IF.                                                      INKCS011
01215                                                                   INKCS011
01216 *----------------------------------------------------------------*INKCS011
01217 *  IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A       *INKCS011
01218 *  RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1     *INKCS011
01219 *  ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) *INKCS011
01220 *  THE ENDING.                                                   *INKCS011
01221 *----------------------------------------------------------------*INKCS011
01222                                                                   INKCS011
01223  3310-EDIT-RANGE.                                                 INKCS011
01224                                                                   INKCS011
01225      IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                INKCS011
01226          IF  ASC-START-OF-SELECTED-RANGE = ZERO                   INKCS011
01227              MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)INKCS011
01228                                    TO ASC-SELECTED-SW             INKCS011
01229                                         (ASC-BLK-SUB ASC-ITEM-SUB)INKCS011
01230              MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE INKCS011
01231          ELSE                                                     INKCS011
01232              IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE     INKCS011
01233                  CONTINUE                                         INKCS011
01234              ELSE                                                 INKCS011
01235 *                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --        INKCS011
01236                  MOVE PC-TSYMSG-00054                             INKCS011
01237                                    TO DP020-MSG-NUMBER            INKCS011
01238                  SET DP020-MSG-FATAL                              INKCS011
01239                                    TO TRUE                        INKCS011
01240                  MOVE -1           TO MR-SELECTL (PV-SUB)         INKCS011
01241                  SET DP030-SET-CURSOR-APPL-1                      INKCS011
01242                                    TO TRUE                        INKCS011
01243                  MOVE DP015-RED    TO MR-SELECTC (PV-SUB)         INKCS011
01244                  MOVE DP015-UNDERLINE                             INKCS011
01245                                    TO MR-SELECTH (PV-SUB)         INKCS011
01246                  SET PS-ERROR      TO TRUE                        INKCS011
01247              END-IF                                               INKCS011
01248          END-IF                                                   INKCS011
01249      ELSE                                                         INKCS011
01250          IF  ASC-END-OF-SELECTED-RANGE = ZERO                     INKCS011
01251              MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
01252                                    TO ASC-SELECTED-SW             INKCS011
01253                                      (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS011
01254              MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE   INKCS011
01255          ELSE                                                     INKCS011
01256              IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE       INKCS011
01257                  CONTINUE                                         INKCS011
01258              ELSE                                                 INKCS011
01259 *                -- ONLY ONE END-OF-RANGE ALLOWED --              INKCS011
01260                  MOVE PC-TSYMSG-00055                             INKCS011
01261                                    TO DP020-MSG-NUMBER            INKCS011
01262                  SET DP020-MSG-FATAL                              INKCS011
01263                                    TO TRUE                        INKCS011
01264                  MOVE -1           TO MR-SELECTL (PV-SUB)         INKCS011
01265                  SET DP030-SET-CURSOR-APPL-1                      INKCS011
01266                                    TO TRUE                        INKCS011
01267                  SET PS-ERROR      TO TRUE                        INKCS011
01268              END-IF                                               INKCS011
01269          END-IF                                                   INKCS011
01270      END-IF.                                                      INKCS011
01271                                                                   INKCS011
01272 *----------------------------------------------------------------*INKCS011
01273 *  IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,     *INKCS011
01274 *  MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.   *INKCS011
01275 *----------------------------------------------------------------*INKCS011
01276                                                                   INKCS011
01277  3400-CHECK-RANGE-SEQUENCE.                                       INKCS011
01278                                                                   INKCS011
01279      IF  ASC-START-OF-SELECTED-RANGE > ZERO                       INKCS011
01280        AND  ASC-END-OF-SELECTED-RANGE > ZERO                      INKCS011
01281        AND  ASC-START-OF-SELECTED-RANGE >                         INKCS011
01282                                          ASC-END-OF-SELECTED-RANGEINKCS011
01283 *        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                INKCS011
01284          MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                 INKCS011
01285          SET DP020-MSG-FATAL TO TRUE                              INKCS011
01286          SET PS-ERROR TO TRUE                                     INKCS011
01287          PERFORM 3410-POSITION-AT-ERROR                           INKCS011
01288      END-IF.                                                      INKCS011
01289                                                                   INKCS011
01290 *----------------------------------------------------------------*INKCS011
01291 *  BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE     *INKCS011
01292 *  PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR  *INKCS011
01293 *  SHOULD BE POSITIONED ON A RANGE ERROR.                        *INKCS011
01294 *----------------------------------------------------------------*INKCS011
01295                                                                   INKCS011
01296  3410-POSITION-AT-ERROR.                                          INKCS011
01297                                                                   INKCS011
01298      SET DP030-SET-CURSOR-APPL-1                                  INKCS011
01299                                    TO TRUE.                       INKCS011
01300      COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                 INKCS011
01301                     - PV-TOP-ITEM + 1.                            INKCS011
01302      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS011
01303          CONTINUE                                                 INKCS011
01304      ELSE                                                         INKCS011
01305          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS011
01306          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS011
01307          MOVE DP015-RED            TO MR-SELECTC (PV-SUB)         INKCS011
01308      END-IF.                                                      INKCS011
01309 *                                                                 INKCS011
01310      COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                   INKCS011
01311                     - PV-TOP-ITEM + 1.                            INKCS011
01312      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS011
01313          CONTINUE                                                 INKCS011
01314      ELSE                                                         INKCS011
01315          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS011
01316          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS011
01317          MOVE DP015-RED            TO MR-SELECTC (PV-SUB)         INKCS011
01318      END-IF.                                                      INKCS011
01319 /                                                                 INKCS011
01320 *----------------------------------------------------------------*INKCS011
01321 *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *INKCS011
01322 *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *INKCS011
01323 *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *INKCS011
01324 *----------------------------------------------------------------*INKCS011
01325                                                                   INKCS011
01326  3500-MARK-RANGE-AS-SELECTED.                                     INKCS011
01327                                                                   INKCS011
01328      MOVE +1                       TO  ASC-BLK-SUB.               INKCS011
01329      PERFORM 3510-MARK-SELECTED-INDS                              INKCS011
01330          VARYING ASC-ITEM-SUB                                     INKCS011
01331          FROM 1 BY 1                                              INKCS011
01332          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS011
01333                                                                   INKCS011
01334      ADD +1                        TO ASC-BLK-SUB.                INKCS011
01335      PERFORM 3510-MARK-SELECTED-INDS                              INKCS011
01336          VARYING ASC-ITEM-SUB                                     INKCS011
01337          FROM 1 BY 1                                              INKCS011
01338          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS011
01339                                                                   INKCS011
01340      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS011
01341                                    TO  PV-LIST-ITEM-NUMBER.       INKCS011
01342      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS011
01343          1 + ((PV-LIST-ITEM-NUMBER - 1) /                         INKCS011
01344                PC-ITEMS-PER-PANEL).                               INKCS011
01345                                                                   INKCS011
01346      MOVE ASC-START-OF-SELECTED-RANGE                             INKCS011
01347                                    TO PV-START-OF-SELECTED-RANGE. INKCS011
01348      COMPUTE PV-FIRST-BLOCK-IN-RANGE =                            INKCS011
01349          1 + ((PV-START-OF-SELECTED-RANGE - 1) /                  INKCS011
01350                PC-ITEMS-PER-PANEL).                               INKCS011
01351                                                                   INKCS011
01352      MOVE ASC-END-OF-SELECTED-RANGE                               INKCS011
01353                                    TO  PV-END-OF-SELECTED-RANGE.  INKCS011
01354      COMPUTE PV-LAST-BLOCK-IN-RANGE =                             INKCS011
01355          1 + ((PV-END-OF-SELECTED-RANGE - 1) /                    INKCS011
01356                PC-ITEMS-PER-PANEL).                               INKCS011
01357      COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                             INKCS011
01358          PV-FIRST-BLOCK-IN-ARRAY + 1.                             INKCS011
01359                                                                   INKCS011
01360      IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR    INKCS011
01361         (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)        INKCS011
01362          MOVE +1                   TO ASC-BLK-SUB                 INKCS011
01363          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS011
01364          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS011
01365              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS011
01366                                                                   INKCS011
01367          PERFORM                                                  INKCS011
01368              UNTIL PV-FIRST-BLOCK-IN-RANGE >                      INKCS011
01369                    PV-PREV-BLOCK-IN-ARRAY                         INKCS011
01370              MOVE PV-FIRST-BLOCK-IN-RANGE                         INKCS011
01371                                    TO PV-BLOCK-NUMBER             INKCS011
01372              PERFORM 4410-READ-TEMP-STORAGE                       INKCS011
01373              PERFORM 3510-MARK-SELECTED-INDS                      INKCS011
01374                  VARYING ASC-ITEM-SUB                             INKCS011
01375                  FROM 1 BY 1                                      INKCS011
01376                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS011
01377              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS011
01378              ADD +1                TO PV-FIRST-BLOCK-IN-RANGE     INKCS011
01379          END-PERFORM                                              INKCS011
01380                                                                   INKCS011
01381          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS011
01382              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS011
01383          PERFORM                                                  INKCS011
01384              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS011
01385                    PV-LAST-BLOCK-IN-RANGE                         INKCS011
01386              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS011
01387                                    TO PV-BLOCK-NUMBER             INKCS011
01388              PERFORM 4410-READ-TEMP-STORAGE                       INKCS011
01389              PERFORM 3510-MARK-SELECTED-INDS                      INKCS011
01390                  VARYING ASC-ITEM-SUB                             INKCS011
01391                  FROM 1 BY 1                                      INKCS011
01392                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS011
01393              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS011
01394              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS011
01395          END-PERFORM                                              INKCS011
01396                                                                   INKCS011
01397          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS011
01398                                    TO PV-BLOCK-NUMBER             INKCS011
01399          PERFORM 4410-READ-TEMP-STORAGE                           INKCS011
01400      END-IF.                                                      INKCS011
01401                                                                   INKCS011
01402      MOVE +0                       TO ASC-START-OF-SELECTED-RANGE INKCS011
01403                                       ASC-END-OF-SELECTED-RANGE.  INKCS011
01404 /                                                                 INKCS011
01405 *----------------------------------------------------------------*INKCS011
01406 * SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *INKCS011
01407 *----------------------------------------------------------------*INKCS011
01408                                                                   INKCS011
01409  3510-MARK-SELECTED-INDS.                                         INKCS011
01410                                                                   INKCS011
01411      IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                         INKCS011
01412                                 ASC-ITEM-SUB) >                   INKCS011
01413          ASC-START-OF-SELECTED-RANGE)                             INKCS011
01414      OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                         INKCS011
01415                                 ASC-ITEM-SUB) =                   INKCS011
01416          ASC-START-OF-SELECTED-RANGE)                             INKCS011
01417                                                                   INKCS011
01418          IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                     INKCS011
01419                                     ASC-ITEM-SUB) <               INKCS011
01420              ASC-END-OF-SELECTED-RANGE)                           INKCS011
01421          OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                     INKCS011
01422                                     ASC-ITEM-SUB) =               INKCS011
01423              ASC-END-OF-SELECTED-RANGE)                           INKCS011
01424                                                                   INKCS011
01425              IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS011
01426                  SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)   INKCS011
01427                      ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)   INKCS011
01428                                TO TRUE                            INKCS011
01429                  SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)              INKCS011
01430                                TO TRUE                            INKCS011
01431                  ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS    INKCS011
01432              END-IF                                               INKCS011
01433          END-IF                                                   INKCS011
01434      END-IF.                                                      INKCS011
01435 /                                                                 INKCS011
01436 *----------------------------------------------------------------*INKCS011
01437 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *INKCS011
01438 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *INKCS011
01439 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *INKCS011
01440 *    DISPLAY THE PANEL TO USER.                                  *INKCS011
01441 *----------------------------------------------------------------*INKCS011
01442                                                                   INKCS011
01443  4000-BUILD-INITIAL-PANEL.                                        INKCS011
01444                                                                   INKCS011
01445      INITIALIZE                  ASC-BLOCK-ENTRIES (1)            INKCS011
01446                                  ASC-BLOCK-ENTRIES (2).           INKCS011
01447      MOVE +1                 TO  ASC-BLK-SUB.                     INKCS011
01448      SET ASC-ITEMS-LEFT-ON-DB                                     INKCS011
01449                              TO TRUE.                             INKCS011
01450                                                                   INKCS011
01451      MOVE +0                 TO ASC-ITEM-SUB                      INKCS011
01452                                 ASC-NUMBER-OF-ITEMS-READ          INKCS011
01453                                 ASC-NUMBER-OF-SELECTED-ITEMS      INKCS011
01454                                 ASC-HIGHEST-BLOCK-WRITTEN         INKCS011
01455                                 ASC-START-OF-SELECTED-RANGE       INKCS011
01456                                 ASC-END-OF-SELECTED-RANGE         INKCS011
01457                                 ASC-ITEM-AT-TOP-OF-PANEL.         INKCS011
01458      PERFORM 4100-PREPARE-CURSOR.                                 INKCS011
01459                                                                   INKCS011
01460      PERFORM 4010-OPEN-CURSOR.                                    INKCS011
01461                                                                   INKCS011
W25248     MOVE 'N' TO PS-DUPLICATE-RECORD-SW.                          INKCS011
01461                                                                   INKCS011
01462      PERFORM 4300-READ-ITEMS-FROM-DB.                             INKCS011
01463                                                                   INKCS011
01464      PERFORM 4020-CLOSE-CURSOR.                                   INKCS011
01465                                                                   INKCS011
01466      MOVE +1                   TO  PV-TOP-ITEM.                   INKCS011
01467      MOVE SPACE                TO  ASC-START-ITEM-REQUEST.        INKCS011
01468      IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                          INKCS011
01469          SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                    INKCS011
01470          SET DP020-MSG-FATAL   TO  TRUE                           INKCS011
01471          MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER               INKCS011
01472      ELSE                                                         INKCS011
01473          MOVE -1               TO MR-SELECTL (1)                  INKCS011
01474          SET DP030-SET-CURSOR-APPL-1                              INKCS011
01475                                TO TRUE                            INKCS011
01476          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     INKCS011
01477      END-IF.                                                      INKCS011
01478 /                                                                 INKCS011
01479 *----------------------------------------------------------------*INKCS011
01480 * OPEN THE CURSOR.                                               *INKCS011
01481 *----------------------------------------------------------------*INKCS011
01482                                                                   INKCS011
01483  4010-OPEN-CURSOR.                                                INKCS011
01484                                                                   INKCS011
W26600         MOVE ASC-KEY-STORE-NBR      TO PV-LOC-NBR.               INKCS011
W26600         MOVE PV-LOC-NBR-NUM         TO SUMINV-LOC-NBR            INKCS011
W26600                                        ADJADD-LOC-NBR.           INKCS011
W21732         MOVE ASC-KEY-AREA-NBR       TO SUMINV-AREA-NBR           INKCS011
W21732                                        ADJADD-AREA-NBR.          INKCS011
01484                                                                   INKCS011
01485          EXEC SQL                                                 INKCS011
01486               OPEN SUMMARY-CSR                                    INKCS011
01487          END-EXEC.                                                INKCS011
01488                                                                   INKCS011
01489          EVALUATE TRUE                                            INKCS011
01490              WHEN SQLCODE = ZERO                                  INKCS011
01491              WHEN SQLCODE = +100                                  INKCS011
01492                  CONTINUE                                         INKCS011
01493              WHEN SQLCODE = -904                                  INKCS011
01494              WHEN SQLCODE = -913                                  INKCS011
01495                   PERFORM 9900-SOFT-ABORT                         INKCS011
01496              WHEN SQLWARN0 NOT = SPACES                           INKCS011
01497              WHEN SQLCODE < ZERO                                  INKCS011
01498              WHEN SQLCODE > ZERO                                  INKCS011
01499                  MOVE '4010-OPEN-CURSOR'                          INKCS011
01500                                    TO DP013-PARAGRAPH             INKCS011
01501                  MOVE 'OPEN SUMMARY INVENTORY CURSOR - '          INKCS011
01502                                    TO DP013-MESSAGE-TEXT (1)      INKCS011
01503                  MOVE SQLCA        TO DP013-SQLCA                 INKCS011
01504                  MOVE 'TSUMINV '   TO DP013-DB2-TABLE-NAME (1)    INKCS011
01505                  SET DP013-DB2-ABEND                              INKCS011
01506                      DP013-XCTL-DISPLAY-RESTART TO TRUE           INKCS011
01507                  PERFORM DP013-0000-PROCESS-ABEND                 INKCS011
01508          END-EVALUATE.                                            INKCS011
01509 /                                                                 INKCS011
01510 *----------------------------------------------------------------*INKCS011
01511 * CLOSE THE TMESSAGE CURSOR.                                     *INKCS011
01512 *   IN THIS PARTICULAR APPLICATION ONE OF 3 DIFFERENT CURSORS    *INKCS011
01513 *   WILL BE CLOSED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *INKCS011
01514 *   USER.                                                        *INKCS011
01515 *----------------------------------------------------------------*INKCS011
01516                                                                   INKCS011
01517  4020-CLOSE-CURSOR.                                               INKCS011
01518                                                                   INKCS011
01519      EXEC SQL                                                     INKCS011
01520           CLOSE SUMMARY-CSR                                       INKCS011
01521      END-EXEC.                                                    INKCS011
01522                                                                   INKCS011
01523      EVALUATE TRUE                                                INKCS011
01524          WHEN SQLCODE = ZERO                                      INKCS011
01525              CONTINUE                                             INKCS011
01526          WHEN SQLWARN0 NOT = SPACES                               INKCS011
01527          WHEN SQLCODE < ZERO                                      INKCS011
01528          WHEN SQLCODE > ZERO                                      INKCS011
01529              MOVE '4020-CLOSE-CURSOR'                             INKCS011
01530                                TO DP013-PARAGRAPH                 INKCS011
01531              MOVE 'CLOSE SUMMARY INVENTORY CURSOR'                INKCS011
01532                                TO DP013-MESSAGE-TEXT (1)          INKCS011
01533              MOVE SQLCA        TO DP013-SQLCA                     INKCS011
01534              MOVE 'TSUMINV '   TO DP013-DB2-TABLE-NAME (1)        INKCS011
01535              SET DP013-DB2-ABEND                                  INKCS011
01536                  DP013-XCTL-DISPLAY-RESTART TO TRUE               INKCS011
01537              PERFORM DP013-0000-PROCESS-ABEND                     INKCS011
01538      END-EVALUATE.                                                INKCS011
01539 /                                                                 INKCS011
01540 *----------------------------------------------------------------*INKCS011
01541 *    SET UP THE HOST VARIABLES.                                  *INKCS011
01542 *----------------------------------------------------------------*INKCS011
01543                                                                   INKCS011
01544  4100-PREPARE-CURSOR.                                             INKCS011
01545                                                                   INKCS011
01546      MOVE IN001-STORE-NBR    TO  ASC-KEY-STORE-NBR.               INKCS011
01547      MOVE IN001-AREA-NBR     TO  ASC-KEY-AREA-NBR.                INKCS011
01548                                                                   INKCS011
01549 /                                                                 INKCS011
01550 *----------------------------------------------------------------*INKCS011
01551 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *INKCS011
01552 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *INKCS011
01553 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *INKCS011
01554 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *INKCS011
01555 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *INKCS011
01556 *                                                                *INKCS011
01557 *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *INKCS011
01558 *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *INKCS011
01559 *    UPON INITIALIZATION.                                        *INKCS011
01556 *                                                                *INKCS011
W25248*    WITH UPC FUNCTIONALITY ADDED TO DB2 TABLES, MULTIPLE ROWS   *INKCS011
W25248*    MAY EXIST AT A SKU/RETAIL LEVEL. THESE RECORDS WILL NOW BE  *INKCS011
W25248*    SUMMED TO ONE DISPLAY ROW EVEN IF MULTIPLE ROWS WERE FETCHED*INKCS011
W25248*                                                                 INKCS011
W25248*    DATABASE PROCESSING FLOW....                                 INKCS011
W25248*    1) FETCH INITIAL ROW SAVING OFF ROW TO CURRENT VARIABLE      INKCS011
W25248*    2) CHECK IF THE CURRENT ROW MATCHES THE PREVIOUS KEY FETCHED INKCS011
W25248*      A) IF CURRENT =  PREVIOUS, ACCUMULATE QTY AND EXTD VALUES  INKCS011
W25248*      B) IF CURRENT <> PREVIOUS, WRITE ACCUMULATED VALUES TO ASC-INKCS011
W25248*    3) FETCH SKU ATTRIBUTES TO DISPLAY ON SCREEN AS WELL         INKCS011
W25248*    4) WRITE ASC- SCREEN VALUES TO TEMP STORAGE                  INKCS011
01560 *----------------------------------------------------------------*INKCS011
01561                                                                   INKCS011
01562  4300-READ-ITEMS-FROM-DB.                                         INKCS011
01563                                                                   INKCS011
W25248     INITIALIZE PV-CURRENT-KEY.                                   INKCS011
W25248     INITIALIZE PV-PREVIOUS-KEY.                                  INKCS011
W26682                                                                  INKCS011
W26682     INITIALIZE DCLTSUMINV.                                       INKCS011
W26682                                                                  INKCS011
W25248*    INITIAL FETCH                                                INKCS011
R25248     MOVE 'N'                    TO PS-PROCESSED-SW.                      
W25248     PERFORM 4310-FETCH.                                          INKCS011
W26682*                                                                 INKCS011
W26682     IF ASC-NO-ITEMS-LEFT-ON-DB                                   INKCS011
W26682         CONTINUE                                                 INKCS011
W26682     ELSE                                                         INKCS011
W26682*                                                                 INKCS011
W25248     MOVE SUMINV-SKU-NBR         TO PV-CURR-SKU-NBR               INKCS011
W25248                                    PV-PREV-SKU-NBR               INKCS011
W25248     MOVE SUMINV-UPC-NBR         TO PV-CURR-UPC-NBR               INKCS011
W25248                                    PV-PREV-UPC-NBR               INKCS011
W25248     MOVE SUMINV-INV-UNIT-PR-AMT TO PV-CURR-UNIT-RETAIL           INKCS011
W25248                                    PV-PREV-UNIT-RETAIL           INKCS011
W25248*                                                                 INKCS011
01564      PERFORM                                                      INKCS011
01565        VARYING PV-READ-COUNT                                      INKCS011
01566        FROM 1 BY 1                                                INKCS011
01567        UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                      INKCS011
01568           OR (ASC-NO-ITEMS-LEFT-ON-DB))                           INKCS011
01569                                                                   INKCS011
W25248         PERFORM 4320-PROCESS-DUP-KEYS                            INKCS011
W25248             UNTIL                                                        
W25248             ((PV-CURR-SKU-NBR     NOT EQUAL PV-PREV-SKU-NBR)             
W25248           OR (PV-CURR-UNIT-RETAIL NOT EQUAL PV-PREV-UNIT-RETAIL))INKCS011
W25248           OR (ASC-NO-ITEMS-LEFT-ON-DB)                           INKCS011
W25248                                                                  INKCS011
W25248         MOVE PA-ACCUM-SKU      TO PV-KEY-SKU                             
W25248         MOVE PA-ACCUM-UPC      TO PV-KEY-UPC                             
W25248         MOVE PA-ACCUM-QTY      TO PV-KEY-QTY                             
W25248         MOVE PA-ACCUM-RTL      TO PV-KEY-RTL                     INKCS011
W25248         MOVE PA-ACCUM-EXTD-RTL TO PV-KEY-EXTD-RTL                INKCS011
W25248*                                                                 INKCS011
W25248         PERFORM 4325-LOAD-ASC-AREA                               INKCS011
W25248                                                                  INKCS011
W25248         INITIALIZE PV-KEY-RECORD                                 INKCS011
W25248                    PA-PROGRAM-ACCUMULATOR                        INKCS011
W25248                                                                  INKCS011
W25248         MOVE PV-CURR-SKU-NBR      TO PV-PREV-SKU-NBR                     
W25248         MOVE PV-CURR-UPC-NBR      TO PV-PREV-UPC-NBR                     
W25248         MOVE PV-CURR-UNIT-RETAIL  TO PV-PREV-UNIT-RETAIL         INKCS011
W25248         MOVE PV-CURR-SOURCE-TABLE TO PV-ROW-TYPE                 INKCS011
W25248                                      PV-PREV-SOURCE-TABLE        INKCS011
01627      END-PERFORM                                                  INKCS011
01628                                                                   INKCS011
W26682     END-IF.                                                      INKCS011
W26682                                                                  INKCS011
01629      IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                          INKCS011
01630          MOVE +1               TO ASC-BLK-SUB                     INKCS011
01631          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS011
01632          ADD +1                TO ASC-BLK-SUB                     INKCS011
01633          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS011
01634                                                                   INKCS011
01635          MOVE 1                TO ASC-BLK-SUB                     INKCS011
01636                                   PV-BLOCK-NUMBER                 INKCS011
01637          PERFORM 4410-READ-TEMP-STORAGE                           INKCS011
01638          IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1             INKCS011
01639              MOVE 2            TO ASC-BLK-SUB                     INKCS011
01640                                   PV-BLOCK-NUMBER                 INKCS011
01641              PERFORM 4410-READ-TEMP-STORAGE                       INKCS011
01642          END-IF                                                   INKCS011
01643      END-IF.                                                      INKCS011
01644                                                                   INKCS011
01646 *----------------------------------------------------------------*INKCS011
01647 *    FETCH THE CURSOR.                                           *INKCS011
01648 *----------------------------------------------------------------*INKCS011
01649                                                                   INKCS011
01650  4310-FETCH.                                                      INKCS011
01651                                                                   INKCS011
01652      EXEC SQL                                                     INKCS011
01653           FETCH                                                   INKCS011
01654                 SUMMARY-CSR                                       INKCS011
01655           INTO                                                    INKCS011
W21732                :SUMINV-SKU-NBR                                   INKCS011
W25248              , :SUMINV-UPC-NBR                                   INKCS011
01659               , :SUMINV-INV-UNIT-PR-AMT                           INKCS011
01660               , :SUMINV-INV-QTY                                   INKCS011
01661               , :SUMINV-EXTD-AMT                                  INKCS011
01662               , :PV-ROW-TYPE                                      INKCS011
01663      END-EXEC.                                                    INKCS011
01664                                                                   INKCS011
01665      EVALUATE TRUE                                                INKCS011
01666          WHEN SQLCODE = ZERO                                      INKCS011
W25248              MOVE SUMINV-SKU-NBR         TO PV-CURR-SKU-NBR      INKCS011
W25248              MOVE SUMINV-UPC-NBR         TO PV-CURR-UPC-NBR      INKCS011
W25248              MOVE SUMINV-INV-UNIT-PR-AMT TO PV-CURR-UNIT-RETAIL  INKCS011
W25248              MOVE PV-ROW-TYPE            TO PV-CURR-SOURCE-TABLE         
01667          WHEN SQLCODE = +100                                      INKCS011
W26682              INITIALIZE DCLTSUMINV                               INKCS011
W25248              SET ASC-NO-ITEMS-LEFT-ON-DB TO TRUE                 INKCS011
01669          WHEN SQLCODE = -904                                      INKCS011
01670          WHEN SQLCODE = -913                                      INKCS011
01671               PERFORM 9900-SOFT-ABORT                             INKCS011
01672          WHEN SQLWARN0 NOT = SPACE                                INKCS011
01673          WHEN SQLCODE NOT = ZERO                                  INKCS011
01674               MOVE '4310-FETCH-CURSOR'                            INKCS011
01675                                TO DP013-PARAGRAPH                 INKCS011
01676               MOVE 'FETCH SUMMARY INVENTORY CURSOR'               INKCS011
01677                                TO DP013-MESSAGE-TEXT (1)          INKCS011
01678               MOVE SQLCA       TO DP013-SQLCA                     INKCS011
01679               MOVE 'TSUMINV '  TO DP013-DB2-TABLE-NAME (1)        INKCS011
01680               SET DP013-DB2-ABEND                                 INKCS011
01681                                TO TRUE                            INKCS011
01682               PERFORM DP013-0000-PROCESS-ABEND                    INKCS011
01683      END-EVALUATE.                                                INKCS011
01684                                                                   INKCS011
           IF  SQLCODE EQUAL +100                                       INKCS011
               SET ASC-NO-ITEMS-LEFT-ON-DB                              INKCS011
                                     TO TRUE                            INKCS011
           END-IF.                                                      INKCS011
01689 /                                                                 INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248* CHECK IF THIS RECORD IS THE SAME KEY AS WAS PREVIOUSLY FETCHED. INKCS011
W25248* DUPLICATE RECORDS WILL HAVE THE SAME STORE/AREA/SKU/RETAIL,     INKCS011
W25248* BUT WILL HAVE DIFFERENT UPC NUMBERS AND DKEY_NBR.               INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 4320-PROCESS-DUP-KEYS.                                           INKCS011
W25248                                                                  INKCS011
W25248     MOVE PV-PREV-SKU-NBR     TO PA-ACCUM-SKU.                            
W25248     MOVE PV-PREV-UPC-NBR     TO PA-ACCUM-UPC.                            
W25248     MOVE PV-PREV-UNIT-RETAIL TO PA-ACCUM-RTL.                            
W25248                                                                          
W25248     COMPUTE PA-ACCUM-QTY      = PA-ACCUM-QTY                             
W25248                               + SUMINV-INV-QTY.                          
W25248     COMPUTE PA-ACCUM-EXTD-RTL = PA-ACCUM-EXTD-RTL                INKCS011
W25248                               + SUMINV-EXTD-AMT.                 INKCS011
W25248                                                                  INKCS011
W25248     IF PV-TADJADD                                                INKCS011
W25248         MOVE 'ADJ' TO PV-PREV-SOURCE-TABLE                       INKCS011
W25248     END-IF.                                                      INKCS011
W25248                                                                  INKCS011
R25248     MOVE 'Y'                    TO PS-PROCESSED-SW.                      
W25248     PERFORM 4310-FETCH.                                          INKCS011
W25248                                                                  INKCS011
01646 *----------------------------------------------------------------*INKCS011
01647 *    PREP SCREEN. THIS LOGIC WAS CONTAINED IN THE 4300-READ PARA. INKCS011
01647 *    MOVE TO SEPARATE MODULE TO ACCOUNT FOR DUP RECORDS HANDLING. INKCS011
01648 *----------------------------------------------------------------*INKCS011
01649                                                                   INKCS011
W25248 4325-LOAD-ASC-AREA.                                              INKCS011
01645 /                                                                 INKCS011
01574          IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                    INKCS011
01575              ADD +1                TO  ASC-ITEM-SUB               INKCS011
01576          ELSE                                                     INKCS011
01577              ADD +1                TO  ASC-BLK-SUB                INKCS011
01578              MOVE +1               TO  ASC-ITEM-SUB               INKCS011
01579                                                                           
01580              IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY             INKCS011
01581                  MOVE +1           TO  ASC-BLK-SUB                INKCS011
01582                  SET ASC-BLOCK-MODIFIED (1)                       INKCS011
01583                                    TO  TRUE                       INKCS011
01584                  PERFORM 5100-WRITE-TEMP-STORAGE                  INKCS011
01585                  MOVE ASC-BLOCK-ENTRIES                           INKCS011
01586                            (PC-MAX-BLOCKS-IN-ARRAY)               INKCS011
01587                             TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)             
01588                  ADD +1            TO  ASC-BLK-SUB                INKCS011
01589                  INITIALIZE ASC-BLOCK-ENTRIES(ASC-BLK-SUB)        INKCS011
01590                  SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)          INKCS011
01591                                    TO  TRUE                       INKCS011
01592              END-IF                                               INKCS011
01593          END-IF                                                   INKCS011
01573                                                                   INKCS011
01572          IF  ASC-ITEMS-LEFT-ON-DB                                 INKCS011
R25248             MOVE 'N'              TO PS-PROCESSED-SW                     
01595              ADD +1                TO ASC-NUMBER-OF-ITEMS-READ    INKCS011
01596              MOVE ASC-NUMBER-OF-ITEMS-READ                        INKCS011
01597                                    TO ASC-LIST-ITEM-NUMBER        INKCS011
01598                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS011
W25248             MOVE PA-ACCUM-SKU     TO  PV-SKU                     INKCS011
W21732             MOVE PV-SKU           TO  ASC-SKU                    INKCS011
W21732                                       (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
W25248             MOVE PA-ACCUM-RTL                                    INKCS011
01609                                    TO  ASC-PR-AMT                 INKCS011
01610                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
W25248             MOVE PA-ACCUM-QTY     TO  ASC-INV-QTY                INKCS011
01613                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
W25248             ADD PA-ACCUM-QTY      TO  PV-INV-QTY-TTL             INKCS011
W25248             MOVE PA-ACCUM-EXTD-RTL TO  ASC-PR-EXT-AMT            INKCS011
01616                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
W25248             ADD PA-ACCUM-EXTD-RTL TO  PV-PR-EXT-AMT-TTL          INKCS011
W25248*                                                                 INKCS011
W25248*            RETRIEVE ASSOCIATED SKU ATTRIBUTES (TSUMINV ROWS)    INKCS011
01617              IF  PV-PREV-SOURCE-TABLE = 'SUM' OR                  INKCS011
R25248                 PV-PREV-SOURCE-TABLE = '   '                     INKCS011
01619                  MOVE DP015-BLUE                                  INKCS011
01620                      TO ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS011
W25248*                                                                 INKCS011
W25248                 PERFORM 4600-TSUMINV-SKU-ATTR                    INKCS011
W25248                     MOVE SUMINV-DEPT-NBR   TO PV-HOLD-DEPT       INKCS011
W25248                     MOVE SUMINV-MAJ-CL-NBR TO PV-HOLD-MCL        INKCS011
W25248                     MOVE SUMINV-SUB-CL-NBR TO PV-HOLD-SCL        INKCS011
W25248                     MOVE PV-HOLD-DEPT      TO PV-DMS-DEPT        INKCS011
W25248                     MOVE PV-HOLD-MCL       TO PV-DMS-MCL         INKCS011
W25248                     MOVE PV-HOLD-SCL       TO PV-DMS-SCL         INKCS011
W25248                     MOVE PV-HOLD-DMS       TO ASC-DMS            INKCS011
W25248                                        (ASC-BLK-SUB ASC-ITEM-SUB)INKCS011
01621              ELSE                                                 INKCS011
W25248*                RETRIEVE ASSOCIATED SKU ATTRIBUTES (TADJADD ROWS)INKCS011
01622                  MOVE DP015-TURQUOISE                             INKCS011
01624                       TO ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS011
W25248                 MOVE ASC-SKU (ASC-BLK-SUB ASC-ITEM-SUB)                  
W28545                                            TO PV-HOLD-SKU                
W28545                 PERFORM 4700-INPD400-SKU-ATTR                            
W28545                 MOVE IN400-ATT-DEPT-OUT TO PV-HOLD-DEPT          INKCS011
W28545                 MOVE IN400-ATT-MCL-OUT  TO PV-HOLD-MCL           INKCS011
W28545                 MOVE IN400-ATT-SCL-OUT  TO PV-HOLD-SCL           INKCS011
W28545                 MOVE PV-HOLD-DEPT       TO PV-DMS-DEPT           INKCS011
W28545                 MOVE PV-HOLD-MCL        TO PV-DMS-MCL            INKCS011
W28545                 MOVE PV-HOLD-SCL        TO PV-DMS-SCL            INKCS011
W28545                 MOVE PV-HOLD-DMS        TO ASC-DMS               INKCS011
W28545                                     (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS011
01625              END-IF                                               INKCS011
01594                                                                   INKCS011
P1572          ELSE                                                     INKCS011
R25248         IF RECORD-PROCESSED                                              
R25248             PERFORM 4526-ADD-ROW                                         
R25248         ELSE                                                             
P1595              IF ASC-NUMBER-OF-ITEMS-READ > ZERO                   INKCS011
P1595                  ADD +1            TO ASC-NUMBER-OF-ITEMS-READ    INKCS011
P1596                  MOVE ASC-NUMBER-OF-ITEMS-READ                    INKCS011
P1597                                    TO ASC-LIST-ITEM-NUMBER        INKCS011
P1598                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS011
R25248                 MOVE 'AREA TOTAL:'  TO ASC-DMS                   INKCS011
R25248                                        (ASC-BLK-SUB ASC-ITEM-SUB)INKCS011
W25248                 MOVE ZEROES       TO  ASC-SKU                    INKCS011
W21732                                       (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
P1609                  MOVE ZERO         TO  ASC-PR-AMT                 INKCS011
P1610                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
P1612                  MOVE PV-INV-QTY-TTL                              INKCS011
P1612                                    TO  ASC-INV-QTY                INKCS011
P1613                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
P1615                  MOVE PV-PR-EXT-AMT-TTL                           INKCS011
P1615                                    TO  ASC-PR-EXT-AMT             INKCS011
P1616                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
P1619****              MOVE DP015-BLUE   TO  ASC-COLOR                  INKCS011
W25248                 MOVE DP015-WHITE  TO  ASC-COLOR                  INKCS011
P1620                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
P1626              END-IF                                               INKCS011
01626          END-IF.                                                  INKCS011
W25248                                                                  INKCS011
R25248******************************************************************INKCS011
  |   * 4526-ADD-ROW.                                                   INKCS011
  |   *     END OF AREA_NBR LOGIC TO MAKE SURE WE HAVE LOADED THE LAST  INKCS011
  |   * SKU/UPC FOR THE AREA...                                         INKCS011
  |   ******************************************************************INKCS011
  |    4526-ADD-ROW.                                                    INKCS011
  |                                                                     INKCS011
  |        ADD +1                TO ASC-NUMBER-OF-ITEMS-READ.                   
  |        MOVE ASC-NUMBER-OF-ITEMS-READ                                        
  |                              TO ASC-LIST-ITEM-NUMBER                        
  |                                 (ASC-BLK-SUB ASC-ITEM-SUB).                 
  |        MOVE PA-ACCUM-SKU     TO  PV-SKU.                                    
  |        MOVE PV-SKU           TO  ASC-SKU                                    
  |                                  (ASC-BLK-SUB ASC-ITEM-SUB).                
  |        MOVE PA-ACCUM-RTL                                                    
  |                              TO  ASC-PR-AMT                                 
  |                                  (ASC-BLK-SUB ASC-ITEM-SUB).                
  |        MOVE PA-ACCUM-QTY     TO  ASC-INV-QTY                                
  |                                  (ASC-BLK-SUB ASC-ITEM-SUB).                
  |        ADD PA-ACCUM-QTY      TO  PV-INV-QTY-TTL.                            
  |        MOVE PA-ACCUM-EXTD-RTL TO  ASC-PR-EXT-AMT                            
  |                                  (ASC-BLK-SUB ASC-ITEM-SUB).                
  |        ADD PA-ACCUM-EXTD-RTL TO  PV-PR-EXT-AMT-TTL.                         
  |                                                                             
  |        IF  PV-PREV-SOURCE-TABLE = 'SUM' OR                                  
  |            PV-PREV-SOURCE-TABLE = '   '                                     
  |            MOVE DP015-BLUE                                                  
  |                TO ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB)                      
  |                                                                             
  |            PERFORM 4600-TSUMINV-SKU-ATTR                                    
  |                MOVE SUMINV-DEPT-NBR   TO PV-HOLD-DEPT                       
  |                MOVE SUMINV-MAJ-CL-NBR TO PV-HOLD-MCL                        
  |                MOVE SUMINV-SUB-CL-NBR TO PV-HOLD-SCL                        
  |                MOVE PV-HOLD-DEPT      TO PV-DMS-DEPT                        
  |                MOVE PV-HOLD-MCL       TO PV-DMS-MCL                         
  |                MOVE PV-HOLD-SCL       TO PV-DMS-SCL                         
  |                MOVE PV-HOLD-DMS       TO ASC-DMS                            
  |                                   (ASC-BLK-SUB ASC-ITEM-SUB)                
  |        ELSE                                                                 
  |            MOVE DP015-TURQUOISE                                             
  |                 TO ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB)                     
  |            MOVE ASC-SKU (ASC-BLK-SUB ASC-ITEM-SUB)                          
W28545                                         TO PV-HOLD-SKU                   
W28545         PERFORM 4700-INPD400-SKU-ATTR                                    
W28545         MOVE IN400-ATT-DEPT-OUT TO PV-HOLD-DEPT                  INKCS011
W28545         MOVE IN400-ATT-MCL-OUT  TO PV-HOLD-MCL                   INKCS011
W28545         MOVE IN400-ATT-SCL-OUT  TO PV-HOLD-SCL                   INKCS011
W28545         MOVE PV-HOLD-DEPT       TO PV-DMS-DEPT                   INKCS011
W28545         MOVE PV-HOLD-MCL        TO PV-DMS-MCL                    INKCS011
W28545         MOVE PV-HOLD-SCL        TO PV-DMS-SCL                    INKCS011
W28545         MOVE PV-HOLD-DMS        TO ASC-DMS                       INKCS011
W28545                                     (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS011
 25248     END-IF.                                                              
  |                                                                             
  |        IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        S011    
  |            ADD +1                TO  ASC-ITEM-SUB                   S011    
  |        ELSE                                                         S011    
  |            ADD +1                TO  ASC-BLK-SUB                    S011    
  |            MOVE +1               TO  ASC-ITEM-SUB                   S011    
  |                                                                             
  |            IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 S011    
  |                MOVE +1           TO  ASC-BLK-SUB                    S011    
  |                SET ASC-BLOCK-MODIFIED (1)                           S011    
  |                                  TO  TRUE                           S011    
  |                PERFORM 5100-WRITE-TEMP-STORAGE                      S011    
  |                MOVE ASC-BLOCK-ENTRIES                               S011    
  |                          (PC-MAX-BLOCKS-IN-ARRAY)                   S011    
  |                           TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)                 
  |                ADD +1            TO  ASC-BLK-SUB                    S011    
  |                INITIALIZE ASC-BLOCK-ENTRIES(ASC-BLK-SUB)            S011    
  |                SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              S011    
  |                                  TO  TRUE                           S011    
  |            END-IF                                                   S011    
  |        END-IF.                                                      S011    
  |                                                                     S011    
  |        MOVE 'N' TO PS-PROCESSED-SW.                                         
  |                                                                     S011    
  |        IF ASC-NUMBER-OF-ITEMS-READ > ZERO                           S011    
  |            ADD +1            TO ASC-NUMBER-OF-ITEMS-READ            S011    
  |            MOVE ASC-NUMBER-OF-ITEMS-READ                            S011    
  |                              TO ASC-LIST-ITEM-NUMBER                S011    
  |                                     (ASC-BLK-SUB ASC-ITEM-SUB)      S011    
  |            MOVE 'AREA TOTAL:'  TO ASC-DMS                           INKCS011
  |                                       (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS011
  |            MOVE ZEROES       TO  ASC-SKU                            S011    
  |                                      (ASC-BLK-SUB ASC-ITEM-SUB)     S011    
  |            MOVE ZERO         TO  ASC-PR-AMT                         S011    
  |                                  (ASC-BLK-SUB ASC-ITEM-SUB)         S011    
  |            MOVE PV-INV-QTY-TTL  TO ASC-INV-QTY                      S011    
  |                                    (ASC-BLK-SUB ASC-ITEM-SUB)       S011    
  |            MOVE PV-PR-EXT-AMT-TTL                                   S011    
  |                              TO  ASC-PR-EXT-AMT                     S011    
  |                                  (ASC-BLK-SUB ASC-ITEM-SUB)         S011    
  | *****      MOVE DP015-BLUE   TO  ASC-COLOR                          S011    
  |            MOVE DP015-WHITE  TO  ASC-COLOR                          S011    
  |                                  (ASC-BLK-SUB ASC-ITEM-SUB)         S011    
  |        END-IF.                                                      S011    
R25248                                                                  INKCS011
01690 *----------------------------------------------------------------*INKCS011
01691 * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *INKCS011
01692 *----------------------------------------------------------------*INKCS011
01693                                                                   INKCS011
01694  4400-MOVE-COMMAREA-TO-SCREEN.                                    INKCS011
01695                                                                   INKCS011
01696      IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))             INKCS011
01697          AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))          INKCS011
01698          OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))          INKCS011
01699              MOVE +1    TO ASC-BLK-SUB                            INKCS011
01700              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS011
01701              ADD +1     TO ASC-BLK-SUB                            INKCS011
01702              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS011
01703              DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL             INKCS011
01704                  GIVING PV-BLOCK-NUMBER                           INKCS011
01705                  REMAINDER PV-REMAINDER                           INKCS011
01706              IF  PV-REMAINDER > ZERO                              INKCS011
01707                  ADD +1 TO PV-BLOCK-NUMBER                        INKCS011
01708              END-IF                                               INKCS011
01709              MOVE +1    TO ASC-BLK-SUB                            INKCS011
01710              PERFORM 4410-READ-TEMP-STORAGE                       INKCS011
01711              ADD +1     TO ASC-BLK-SUB                            INKCS011
01712                            PV-BLOCK-NUMBER                        INKCS011
01713              PERFORM 4410-READ-TEMP-STORAGE                       INKCS011
01714      END-IF.                                                      INKCS011
01715                                                                   INKCS011
01716      MOVE +1            TO ASC-BLK-SUB.                           INKCS011
01717      COMPUTE ASC-ITEM-SUB =                                       INKCS011
01718          ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))              INKCS011
01719          + PC-ITEMS-PER-PANEL).                                   INKCS011
01720                                                                   INKCS011
01721      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS011
01722          ADD +1         TO ASC-BLK-SUB                            INKCS011
01723          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS011
01724      END-IF.                                                      INKCS011
01725                                                                   INKCS011
01726      COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                           INKCS011
01727          ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                INKCS011
01728      PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                        INKCS011
01729      PERFORM 4420-FORMAT-LIST-LINES                               INKCS011
01730          VARYING PV-SUB                                           INKCS011
01731          FROM PC-ITEMS-PER-PANEL BY -1                            INKCS011
01732          UNTIL PV-SUB < +1.                                       INKCS011
01733                                                                   INKCS011
01734      COMPUTE ASC-ITEMS-DISPLAYED =                                INKCS011
01735          ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).          INKCS011
01736      MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL.   INKCS011
01737      IF  PS-NO-ERROR                                              INKCS011
01738          MOVE PV-TOP-ITEM          TO ASTRTITO                    INKCS011
01739      END-IF.                                                      INKCS011
01740                                                                   INKCS011
01741      MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                    INKCS011
01742      MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                    INKCS011
01743      MOVE ASC-NUMBER-OF-SELECTED-ITEMS                            INKCS011
01744                                    TO ASELITO.                    INKCS011
01745      MOVE SPACE                    TO ASC-START-ITEM-REQUEST.     INKCS011
W26600     MOVE IN001-STORE-NBR          TO ASTRNBRO.                   INKCS011
01746      MOVE IN001-STORE-NAME         TO ASTRNAMO.                   INKCS011
01747      MOVE IN001-AREA-NBR           TO AAREAO.                     INKCS011
01748                                                                   INKCS011
01749      IF  DP020-MSG-NUMBER-X = SPACE                               INKCS011
01750          IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ  INKCS011
01751 *            --- MORE ITEMS TO DISPLAY ----                       INKCS011
01752              MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER            INKCS011
01753              SET DP020-MSG-INFORMATIONAL TO TRUE                  INKCS011
01754          END-IF                                                   INKCS011
01755      END-IF.                                                      INKCS011
01756                                                                   INKCS011
01757 *----------------------------------------------------------------*INKCS011
01758 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *INKCS011
01759 *----------------------------------------------------------------*INKCS011
01760                                                                   INKCS011
01761  4410-READ-TEMP-STORAGE.                                          INKCS011
01762                                                                   INKCS011
01763      EXEC CICS                                                    INKCS011
01764          READQ TS                                                 INKCS011
01765              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS011
01766              INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS011
01767              ITEM  (PV-BLOCK-NUMBER)                              INKCS011
01768              RESP  (PV-CICS-RESP)                                 INKCS011
01769      END-EXEC.                                                    INKCS011
01770                                                                   INKCS011
01771      IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                       INKCS011
01772        OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                     INKCS011
01773          CONTINUE                                                 INKCS011
01774      ELSE                                                         INKCS011
01775          SET DP013-CICS-ABEND     TO TRUE                         INKCS011
01776          SET DP013-NO-ROLLBACK    TO TRUE                         INKCS011
01777          MOVE '4410-READ-TEMP-STORAGE'                            INKCS011
01778                                   TO DP013-PARAGRAPH              INKCS011
01779          MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'      INKCS011
01780                                   TO DP013-MESSAGE-TEXT(1)        INKCS011
01781          PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
01782      END-IF.                                                      INKCS011
01783                                                                   INKCS011
01784      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS011
01785                                                                   INKCS011
01786      IF  PV-CICS-RESP = DFHRESP (ITEMERR)                         INKCS011
01787          INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)               INKCS011
01788      END-IF.                                                      INKCS011
01789 /                                                                 INKCS011
01790 *----------------------------------------------------------------*INKCS011
01791 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *INKCS011
01792 *----------------------------------------------------------------*INKCS011
01793                                                                   INKCS011
01794  4420-FORMAT-LIST-LINES.                                          INKCS011
01795                                                                   INKCS011
01796      IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ      INKCS011
01797          SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                INKCS011
01798          MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA      (PV-SUB)      INKCS011
01799          MOVE SPACE              TO MR-SELECTI      (PV-SUB)      INKCS011
01800                                     MR-ACTION       (PV-SUB)      INKCS011
W25248                                    MR-DMS          (PV-SUB)      INKCS011
W21732                                    MR-SKU          (PV-SUB)      INKCS011
01804                                     MR-INV-QTY-X    (PV-SUB)      INKCS011
01805                                     MR-PR-AMT-X     (PV-SUB)      INKCS011
01806                                     MR-PR-EXT-AMT-X (PV-SUB)      INKCS011
01807                                     MR-ADJ-MSG      (PV-SUB)      INKCS011
01808      ELSE                                                         INKCS011
01809          IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS011
01810                                                    > SPACE        INKCS011
01811              MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
01812                                  TO MR-SELECTI (PV-SUB)           INKCS011
01813          ELSE                                                     INKCS011
01814              MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS011
01815                                  TO MR-SELECTI (PV-SUB)           INKCS011
01816          END-IF                                                   INKCS011
01817                                                                   INKCS011
01818          IF  DP030-SET-CURSOR-TRAILER                             INKCS011
01819              IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)INKCS011
01820                  > SPACE)                                         INKCS011
01821                OR (PV-SUB = 1))                                   INKCS011
01822                 SET DP030-SET-CURSOR-APPL-1 TO TRUE               INKCS011
01823                 MOVE -1          TO MR-SELECTL (PV-SUB)           INKCS011
01824              END-IF                                               INKCS011
01825          END-IF                                                   INKCS011
01826                                                                   INKCS011
01827          IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL           INKCS011
01828              MOVE ASC-ACTION         (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS011
01829                                  TO  MR-ACTION        (PV-SUB)    INKCS011
W25248             MOVE ASC-DMS            (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS011
W25248                                 TO  MR-DMS           (PV-SUB)    INKCS011
W21732             MOVE ASC-SKU            (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS011
W21732                                 TO  MR-SKU           (PV-SUB)    INKCS011
01837              MOVE ASC-INV-QTY        (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS011
01838                                  TO  MR-INV-QTY       (PV-SUB)    INKCS011
01839              MOVE ASC-PR-AMT         (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS011
01840                                  TO  MR-PR-AMT        (PV-SUB)    INKCS011
01841              MOVE ASC-PR-EXT-AMT     (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS011
01842                                  TO  MR-PR-EXT-AMT    (PV-SUB)    INKCS011
01843              MOVE ASC-COLOR          (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS011
W21732                                 TO  MR-SKUC          (PV-SUB)    INKCS011
W25248                                     MR-DMSC          (PV-SUB)    INKCS011
01847                                      MR-INV-QTYC      (PV-SUB)    INKCS011
01848                                      MR-PR-AMTC       (PV-SUB)    INKCS011
01849                                      MR-PR-EXT-AMTC   (PV-SUB)    INKCS011
01850                                                                   INKCS011
W21015******************************************************************INKCS011
W21015** THE LIST SCREEN INDICATES IF THE INITIAL ADJUSTMENT HAS COME **INKCS011
W21015** FROM THE MASS ADD SCREEN - BY LISTING A REASON 'ADJ ADD'.    **INKCS011
W21015** THESE RECORDS RESIDE ON TADJADD.  ADJUSTMENTS TO A SCANNING  **        
W21015** SERVICES DATA WILL HAVE NO REASON - THESE COME FROM TSUMINV. **        
W21015******************************************************************INKCS011
W21015** TADJADD ROW                                                    INKCS011
                   IF  ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB) EQUAL       INKCS011
                       DP015-TURQUOISE                                  INKCS011
W21015                     MOVE PC-ADJ-MSG2 TO MR-ADJ-MSG (PV-SUB)              
------             ELSE                                                 INKCS011
WHITE **                  TOTAL ROW IS WHITE                                    
W25248                 IF ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB) EQUAL            
W25248                    DP015-WHITE                                           
W25248                     MOVE SPACES    TO MR-ADJ-MSG (PV-SUB)                
W25248***DE-SELECT TOTAL LINE                                                   
W25248                     MOVE DP015-ASK-DRK-OFF  TO                           
W25248                                    MR-SELECTA (PV-SUB)                   
W25248                     MOVE SPACE  TO MR-SELECTI (PV-SUB)                   
W25248                     MOVE ZEROES TO ASC-LIST-ITEM-NUMBER                  
W25248                                   (ASC-BLK-SUB ASC-ITEM-SUB)             
W25248                     MOVE SPACES TO ASC-TEMP-SELECTED-SW                  
W25248                                   (ASC-BLK-SUB ASC-ITEM-SUB)             
W25248** TADJDTL ROW                                                            
------                 ELSE                                                     
W21015                   PERFORM 4500-SELECT-TADJDTL-ROW                INKCS011
W25248                   EVALUATE TRUE                                  INKCS011
W25248                     WHEN SQLCODE = ZERO                                  
W25248                     WHEN SQLCODE = -811                                  
W25248                          MOVE PC-ADJ-MSG1  TO MR-ADJ-MSG (PV-SUB)        
W28545                          MOVE ADJDTL-SKU-NBR TO PV-HOLD-SKU              
W28545                          PERFORM 4700-INPD400-SKU-ATTR                   
W28545*                                                                 INKCS011
W28545                          MOVE IN400-ATT-DEPT-OUT                 INKCS011
W28545                                                TO PV-HOLD-DEPT   INKCS011
W28545                          MOVE IN400-ATT-MCL-OUT                  INKCS011
W28545                                                TO PV-HOLD-MCL    INKCS011
W28545                          MOVE IN400-ATT-SCL-OUT                  INKCS011
W28545                                                TO PV-HOLD-SCL    INKCS011
W28545*                                                                 INKCS011
W28545                          MOVE PV-HOLD-DEPT     TO PV-DMS-DEPT    INKCS011
W28545                          MOVE PV-HOLD-MCL      TO PV-DMS-MCL     INKCS011
W28545                          MOVE PV-HOLD-SCL      TO PV-DMS-SCL     INKCS011
W25248*                                                                 INKCS011
W28248                          MOVE PV-HOLD-DMS     TO MR-DMS (PV-SUB) INKCS011
W28248*                                      (ASC-BLK-SUB ASC-ITEM-SUB) INKCS011
W21015                     WHEN SQLCODE = +100                                  
W21015** TSUMINV ROW                                                    INKCS011
W21015                         MOVE PC-NO-ADJ-MSG TO MR-ADJ-MSG (PV-SUB)        
W21015                     WHEN SQLCODE = -904                                  
W21015                     WHEN SQLCODE = -913                                  
W21015                         PERFORM 9900-SOFT-ABORT                          
W21015                     WHEN SQLWARN0 NOT = SPACE                            
W21015                     WHEN SQLCODE NOT = ZERO                              
W21015                         MOVE '4500-SELECT-TADJDTL-ROW'                   
                                               TO DP013-PARAGRAPH               
W21015                         MOVE 'SELECT TADJDTL ROW'                        
                                               TO DP013-MESSAGE-TEXT (1)        
W21015                         MOVE SQLCA TO DP013-SQLCA                        
W21015                         MOVE 'TSUMINV '                                  
                                             TO DP013-DB2-TABLE-NAME (1)        
W21015                         SET DP013-DB2-ABEND       TO TRUE                
W21015                         PERFORM DP013-0000-PROCESS-ABEND                 
W21015                   END-EVALUATE                                           
01858              END-IF                                               INKCS011
01859          END-IF                                                   INKCS011
01859      END-IF.                                                      INKCS011
01860                                                                   INKCS011
01861      SUBTRACT +1 FROM ASC-ITEM-SUB                                INKCS011
01862      IF  ASC-ITEM-SUB < + 1                                       INKCS011
01863          MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                  INKCS011
01864          MOVE +1                 TO ASC-BLK-SUB                   INKCS011
01865      END-IF.                                                      INKCS011
01866                                                                   INKCS011
01867 ******************************************************************INKCS011
01868 ** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **INKCS011
01869 ** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **INKCS011
01870 ** ISSUE A WARNING.                                             **INKCS011
01871 ******************************************************************INKCS011
01872                                                                   INKCS011
01873  4430-CHECK-FOR-BEGIN-AND-END.                                    INKCS011
01874                                                                   INKCS011
01875      IF  ASC-START-OF-SELECTED-RANGE > ZERO                       INKCS011
01876          IF  ASC-END-OF-SELECTED-RANGE = ZERO                     INKCS011
01877 *            -- NO END-OF-RANGE SPECFIED ---                      INKCS011
01878              MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER            INKCS011
01879              SET DP020-MSG-WARNING TO TRUE                        INKCS011
01880              PERFORM 4431-POSITION-AT-WARNING                     INKCS011
01881          END-IF                                                   INKCS011
01882      ELSE                                                         INKCS011
01883          IF  ASC-END-OF-SELECTED-RANGE > ZERO                     INKCS011
01884 *            -- NO BEGINNING-OF-RANGE SPECFIED ---                INKCS011
01885              MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER            INKCS011
01886              SET DP020-MSG-WARNING TO TRUE                        INKCS011
01887              PERFORM 4431-POSITION-AT-WARNING                     INKCS011
01888          END-IF                                                   INKCS011
01889      END-IF.                                                      INKCS011
01890 ******************************************************************INKCS011
01891 ** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **INKCS011
01892 ** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **INKCS011
01893 ** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **INKCS011
01894 ** POSITION THE CURSOR THERE.                                   **INKCS011
01895 ******************************************************************INKCS011
01896                                                                   INKCS011
01897  4431-POSITION-AT-WARNING.                                        INKCS011
01898                                                                   INKCS011
01899      COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                 INKCS011
01900                     - PV-TOP-ITEM + 1.                            INKCS011
01901      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS011
01902          CONTINUE                                                 INKCS011
01903      ELSE                                                         INKCS011
01904          SET DP030-SET-CURSOR-APPL-1                              INKCS011
01905                                    TO TRUE                        INKCS011
01906          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS011
01907          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS011
01908          MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)         INKCS011
01909      END-IF.                                                      INKCS011
01910                                                                   INKCS011
01911      COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                   INKCS011
01912                     - PV-TOP-ITEM + 1.                            INKCS011
01913      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS011
01914          CONTINUE                                                 INKCS011
01915      ELSE                                                         INKCS011
01916          SET DP030-SET-CURSOR-APPL-1                              INKCS011
01917                                    TO TRUE                        INKCS011
01918          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS011
01919          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS011
01920          MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)         INKCS011
01921      END-IF.                                                      INKCS011
01922 /                                                                 INKCS011
W21015*----------------------------------------------------------------*INKCS011
W21015* DETERMINE IF THE ORIGINAL LOADED INVENTORY DATA (TO TSUMINV)   *INKCS011
W21015* HAS BEEN ADJUSTED FROM THE ORIGINAL QUANTITY AND/OR PRICE      *INKCS011
W21015* THIS OCCURS IF DB2 TABLE TADJDTL CONTAINS ADJUSTMENT ROWS FOR  *INKCS011
W21015* STORE / AREA / SKU / PRICE.                                    *INKCS011
W21015*----------------------------------------------------------------*INKCS011
W21015 4500-SELECT-TADJDTL-ROW.                                         INKCS011
W21015*                                                                 INKCS011
W21732     MOVE ASC-SKU (ASC-BLK-SUB ASC-ITEM-SUB) TO PV-SKU.           INKCS011
W21732     MOVE PV-SKU                             TO ADJDTL-SKU-NBR.   INKCS011
W26600     MOVE ASC-KEY-STORE-NBR   TO PV-LOC-NBR.                      INKCS011
W26600     MOVE PV-LOC-NBR-NUM      TO ADJDTL-LOC-NBR.                  INKCS011
W21732     MOVE ASC-KEY-AREA-NBR                   TO ADJDTL-AREA-NBR.  INKCS011
285452     MOVE ASC-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)                   INKCS011
285452                              TO PV-PR-AMT.                       INKCS011
W21015                                                                  INKCS011
W21015     EXEC SQL                                                     INKCS011
W28545          SELECT A.SKU_NBR                                        INKCS011
W21732          INTO  :ADJDTL-SKU-NBR                                   INKCS011
W28545          FROM TADJDTL A,                                         INKCS011
W28545               TSUMINV B                                          INKCS011
W28545          WHERE A.LOC_NBR         =  :ADJDTL-LOC-NBR              INKCS011
W28545            AND A.LOC_NBR         = B.LOC_NBR                     INKCS011
W28545            AND A.AREA_NBR        =  :ADJDTL-AREA-NBR             INKCS011
W28545            AND A.AREA_NBR        = B.AREA_NBR                    INKCS011
W28545            AND A.SKU_NBR         =  :ADJDTL-SKU-NBR              INKCS011
W28545            AND A.SKU_NBR         = B.SKU_NBR                     INKCS011
W28545            AND A.DKEY_NBR        = B.DKEY_NBR                    INKCS011
W28545            AND B.INV_UNIT_PR_AMT =  :PV-PR-AMT                   INKCS011
W21015     END-EXEC.                                                    INKCS011
W21015*                                                                 INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248* RETRIEVE ATTRIBUTES ASSOCIATED WITH THE SKU NUMBER (TSUMINV)    INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248 4600-TSUMINV-SKU-ATTR.                                           INKCS011
W25248                                                                  INKCS011
W26600     MOVE ASC-KEY-STORE-NBR  TO PV-LOC-NBR.                               
W26600     MOVE PV-LOC-NBR-NUM     TO SUMINV-LOC-NBR.                           
R26682     MOVE ASC-KEY-AREA-NBR   TO SUMINV-AREA-NBR .                         
R26682                                                                          
W25248     EXEC SQL                                                     INKCS011
W25248          SELECT DEPT_NBR                                         INKCS011
W25248               , MAJ_CL_NBR                                       INKCS011
W25248               , SUB_CL_NBR                                       INKCS011
W25248          INTO  :SUMINV-DEPT-NBR                                  INKCS011
W25248               ,:SUMINV-MAJ-CL-NBR                                INKCS011
W25248               ,:SUMINV-SUB-CL-NBR                                INKCS011
W25248          FROM TSUMINV                                            INKCS011
W26600          WHERE LOC_NBR       = :SUMINV-LOC-NBR                   INKCS011
W25248            AND AREA_NBR        = :SUMINV-AREA-NBR                INKCS011
W25248            AND SKU_NBR         = :PV-KEY-SKU                     INKCS011
W25248            AND UPC_NBR         = :PV-KEY-UPC                     INKCS011
W25248            AND INV_UNIT_PR_AMT = :PV-KEY-RTL                     INKCS011
W25248     END-EXEC.                                                    INKCS011
W25248*                                                                 INKCS011
W25248     EVALUATE TRUE                                                INKCS011
W25248         WHEN SQLCODE = ZERO                                              
W25248             CONTINUE                                                     
W25248         WHEN SQLCODE = +100                                              
W25248             MOVE ZEROES    TO  SUMINV-DEPT-NBR                   INKCS011
W25248                                SUMINV-MAJ-CL-NBR                 INKCS011
W25248                                SUMINV-SUB-CL-NBR                 INKCS011
W25248         WHEN SQLCODE = -904                                              
W25248         WHEN SQLCODE = -913                                              
W25248             PERFORM 9900-SOFT-ABORT                                      
W25248             WHEN SQLWARN0 NOT = SPACE                                    
W25248             WHEN SQLCODE NOT = ZERO                                      
W25248             MOVE '4600-TSUMINV-SKU-ATTR'                                 
W25248                   TO DP013-PARAGRAPH                                     
W25248             MOVE 'SELECT TSUMINV ROW'                                    
W25248                   TO DP013-MESSAGE-TEXT (1)                              
W25248             MOVE SQLCA TO DP013-SQLCA                                    
W25248             MOVE 'TSUMINV '                                              
W25248                   TO DP013-DB2-TABLE-NAME (1)                            
W25248             SET DP013-DB2-ABEND       TO TRUE                            
W25248             PERFORM DP013-0000-PROCESS-ABEND                             
W25248         END-EVALUATE.                                                    
W25248*----------------------------------------------------------------*INKCS011
W25248* RETRIEVE ATTRIBUTES ASSOCIATED WITH THE SKU NUMBER (TADJADD)    INKCS011
W25248*----------------------------------------------------------------*INKCS011
W28248 4700-INPD400-SKU-ATTR.                                           INKCS011
W28248                                                                  INKCS011
W28545     INITIALIZE IN400-PV-INPUT-AREA.                                      
W28545                                                                          
F28545     MOVE IN001-DB2-ACTL-INV-DTE  TO IN400-PV-EFF-DTE-IN.                 
W28545     MOVE 'SKU'                   TO IN400-PV-LOOKUP-TYPE-IN.             
W28545     MOVE PV-HOLD-SKU             TO IN400-PV-SKU-IN-NUM .                
W28545     MOVE PV-LOC-NBR              TO IN400-PV-LOC-IN-NUM.                 
W28545                                                                          
W28545     PERFORM IN400-LOOKUP-RETAIL.                                         
W28545                                                                          
W28545     IF IN400-PV-FOUND                                                    
W28545         CONTINUE                                                         
W28545     ELSE                                                                 
W28545         IF IN400-PV-NOT-FOUND                                            
W28545             MOVE ZEROS           TO IN400-ATT-DEPT-OUT                   
W28545             MOVE ZEROS           TO IN400-ATT-MCL-OUT                    
W28545             MOVE ZEROS           TO IN400-ATT-SCL-OUT                    
W28545     ELSE                                                                 
W28545             MOVE '4700-INPD400-LOOK-UP       '                           
W28545                                   TO DP013-PARAGRAPH                     
W28545             MOVE 'CALL INPD400 '                                         
W28545                                   TO DP013-MESSAGE-TEXT (1)              
W28545             MOVE IN400-PV-RETURN-RSN                                     
W28545                                   TO DP013-MESSAGE-TEXT (2)              
W28545             MOVE IN400-PV-PARAGRAPH                                      
W28545                                   TO DP013-MESSAGE-TEXT (3)              
W28545             MOVE IN400-PV-PRC-SQLCDE                                     
W28545                                   TO DP013-SQLCA                         
W28545             MOVE '???????'    TO DP013-DB2-TABLE-NAME (1)                
W28545             SET DP013-DB2-ABEND                                          
W28545                  DP013-XCTL-DISPLAY-RESTART                              
W28545                                   TO TRUE                                
W28545             PERFORM DP013-0000-PROCESS-ABEND                             
W28545                                                                          
W28545     END-IF.                                                              
      /                                                                         
01923 *----------------------------------------------------------------*INKCS011
01924 * PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *INKCS011
01925 * ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *INKCS011
01926 * FOR THE SELECTED ITEMS QUEUE.                                  *INKCS011
01927 *                                                                *INKCS011
01928 * FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *INKCS011
01929 * QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *INKCS011
01930 * THE ARRAY IS FULL.                                             *INKCS011
01931 *----------------------------------------------------------------*INKCS011
01932                                                                   INKCS011
01933  5000-PREPARE-SEL-QUEUE.                                          INKCS011
01934                                                                   INKCS011
01935      MOVE +1                   TO  ASC-BLK-SUB.                   INKCS011
01936      PERFORM 5100-WRITE-TEMP-STORAGE.                             INKCS011
01937      ADD +1                    TO  ASC-BLK-SUB.                   INKCS011
01938      PERFORM 5100-WRITE-TEMP-STORAGE.                             INKCS011
01939                                                                   INKCS011
01940      MOVE ASC-SEL-QUEUE-NAME   TO  IN001-SEL-QUEUE-NAME.          INKCS011
01941      PERFORM 6010-DELETE-SEL-QUEUE.                               INKCS011
01942                                                                   INKCS011
01943      SET ASC-UPDATE-TSQ        TO  TRUE.                          INKCS011
01944                                                                   INKCS011
01945      SET IN012-IDX             TO  1.                             INKCS011
01946      MOVE ZERO                 TO IN001-NUMBER-OF-TS-ITEMS.       INKCS011
01947      INITIALIZE IN012-SELECTED-ITEM-ARRAY.                        INKCS011
01948                                                                   INKCS011
01949      PERFORM 5200-FILL-SEL-QUEUE-RECORD                           INKCS011
01950              VARYING PV-ITEM                                      INKCS011
01951              FROM 1 BY 1                                          INKCS011
01952              UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.     INKCS011
01953                                                                   INKCS011
01954      IF  IN012-IDX > 1                                            INKCS011
01955          PERFORM 5205-WRITE-SEL-QUEUE                             INKCS011
01956          ADD +1                TO IN001-NUMBER-OF-TS-ITEMS        INKCS011
01957      END-IF.                                                      INKCS011
01958                                                                   INKCS011
W25248     MOVE 'INAPHADJ' TO ASC-RETURN-FROM-MNEMONIC.                 INKCS011
01958  EJECT                                                            INKCS011
01959 *----------------------------------------------------------------*INKCS011
01960 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *INKCS011
01961 *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *INKCS011
01962 *----------------------------------------------------------------*INKCS011
01963                                                                   INKCS011
01964  5100-WRITE-TEMP-STORAGE.                                         INKCS011
01965                                                                   INKCS011
01966      MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                     INKCS011
01967                                    TO PV-LIST-ITEM-NUMBER.        INKCS011
01968      COMPUTE PV-BLOCK-NUMBER =                                    INKCS011
01969          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS011
01970                                                                   INKCS011
01971      IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)             INKCS011
01972      OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)             INKCS011
01973          IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      INKCS011
01974              PERFORM 5105-REWRITE-ASC-LISTQ                       INKCS011
01975          END-IF                                                   INKCS011
01976      ELSE                                                         INKCS011
01977          COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       INKCS011
01978          ASC-HIGHEST-BLOCK-WRITTEN + 1                            INKCS011
01979          PERFORM 5110-WRITE-ASC-LISTQ                             INKCS011
01980          ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN   INKCS011
01981      END-IF.                                                      INKCS011
01982                                                                   INKCS011
01983      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS011
01984                                                                   INKCS011
01985 ******************************************************************INKCS011
01986 ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **INKCS011
01987 ** SPECIFIC COMMAREA.                                           **INKCS011
01988 ******************************************************************INKCS011
01989                                                                   INKCS011
01990  5105-REWRITE-ASC-LISTQ.                                          INKCS011
01991                                                                   INKCS011
01992      EXEC CICS                                                    INKCS011
01993          WRITEQ TS                                                INKCS011
01994              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS011
01995              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS011
01996              ITEM  (PV-BLOCK-NUMBER)                              INKCS011
01997              REWRITE                                              INKCS011
01998              RESP  (PV-CICS-RESP)                                 INKCS011
01999      END-EXEC.                                                    INKCS011
02000                                                                   INKCS011
02001      IF  PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS011
02002          CONTINUE                                                 INKCS011
02003      ELSE                                                         INKCS011
02004          SET DP013-CICS-ABEND            TO TRUE                  INKCS011
02005          SET DP013-NO-ROLLBACK           TO TRUE                  INKCS011
02006          MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH       INKCS011
02007          MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'     INKCS011
02008                                          TO DP013-MESSAGE-TEXT(1) INKCS011
02009          PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
02010      END-IF.                                                      INKCS011
02011                                                                   INKCS011
02012 ******************************************************************INKCS011
02013 ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **INKCS011
02014 ** SPECIFIC COMMAREA.                                           **INKCS011
02015 ******************************************************************INKCS011
02016                                                                   INKCS011
02017  5110-WRITE-ASC-LISTQ.                                            INKCS011
02018                                                                   INKCS011
02019      EXEC CICS                                                    INKCS011
02020          WRITEQ TS                                                INKCS011
02021              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS011
02022              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS011
02023              ITEM  (PV-BLOCK-NUMBER)                              INKCS011
02024              RESP  (PV-CICS-RESP)                                 INKCS011
02025      END-EXEC.                                                    INKCS011
02026      IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                      INKCS011
02027          SET DP013-CICS-ABEND      TO TRUE                        INKCS011
02028          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS011
02029          MOVE '5110-WRITE-ASC-LISTQ'                              INKCS011
02030                                    TO DP013-PARAGRAPH             INKCS011
02031          MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS011
02032                                    TO DP013-MESSAGE-TEXT(1)       INKCS011
02033          PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
02034      END-IF.                                                      INKCS011
02035 /                                                                 INKCS011
02036 *----------------------------------------------------------------*INKCS011
02037 * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *INKCS011
02038 * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *INKCS011
02039 *----------------------------------------------------------------*INKCS011
02040                                                                   INKCS011
02041  5200-FILL-SEL-QUEUE-RECORD.                                      INKCS011
02042                                                                   INKCS011
02043      PERFORM 6112-READ-LIST-QUEUE.                                INKCS011
02044                                                                   INKCS011
02045      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       INKCS011
02046          PERFORM                                                  INKCS011
02047          VARYING LQW-IDX                                          INKCS011
02048          FROM 1 BY 1                                              INKCS011
02049          UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL            INKCS011
02050            OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO          INKCS011
02051                                                                   INKCS011
02052              IF  LQW-SELECT (LQW-IDX)                             INKCS011
02053                  MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)              INKCS011
02054                                    TO IN012-ITEM      (IN012-IDX) INKCS011
W21732                 MOVE LQW-SKU  (LQW-IDX)                          INKCS011
W21732                                   TO IN012-SKU-NBR   (IN012-IDX) INKCS011
02061                  MOVE SPACE        TO IN012-ACTION-SW (IN012-IDX) INKCS011
02062                  MOVE LQW-INV-QTY (LQW-IDX)                       INKCS011
02063                                    TO IN012-QTY       (IN012-IDX) INKCS011
02064                  MOVE LQW-PR-AMT (LQW-IDX)                        INKCS011
02065                                    TO IN012-PRICE     (IN012-IDX) INKCS011
02066                  IF  LQW-COLOR (LQW-IDX) EQUAL DP015-BLUE         INKCS011
02067                      SET IN012-FROM-TSUMINV (IN012-IDX) TO TRUE   INKCS011
02068                  ELSE                                             INKCS011
02069                      SET IN012-FROM-TADJADD (IN012-IDX) TO TRUE   INKCS011
02070                  END-IF                                           INKCS011
02071                                                                   INKCS011
02072                  SET IN012-IDX UP BY 1                            INKCS011
02073                  IF  IN012-IDX > IN012-MAX-ITEMS                  INKCS011
02074                      PERFORM 5205-WRITE-SEL-QUEUE                 INKCS011
02075                      ADD +1        TO IN001-NUMBER-OF-TS-ITEMS    INKCS011
02076                      INITIALIZE IN012-SELECTED-ITEM-ARRAY         INKCS011
02077                      SET IN012-IDX TO  1                          INKCS011
02078                  END-IF                                           INKCS011
02079              END-IF                                               INKCS011
02080          END-PERFORM                                              INKCS011
02081      END-IF.                                                      INKCS011
02082                                                                   INKCS011
02083 *----------------------------------------------------------------*INKCS011
02084 *    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *INKCS011
02085 *----------------------------------------------------------------*INKCS011
02086                                                                   INKCS011
02087  5205-WRITE-SEL-QUEUE.                                            INKCS011
02088                                                                   INKCS011
02089      MOVE ASC-NUMBER-OF-SELECTED-ITEMS                            INKCS011
02090                                    TO IN012-SELECTED-ITEMS.       INKCS011
02091                                                                   INKCS011
02092      EXEC CICS                                                    INKCS011
02093           WRITEQ TS                                               INKCS011
02094               QUEUE (ASC-SEL-QUEUE-NAME)                          INKCS011
02095               FROM  (IN012-SELECTED-SKUS)                         INKCS011
02096               ITEM  (PV-SEL-QUEUE-ITEM)                           INKCS011
02097               RESP  (PV-CICS-RESP)                                INKCS011
02098      END-EXEC.                                                    INKCS011
02099                                                                   INKCS011
02100      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS011
02101          CONTINUE                                                 INKCS011
02102      ELSE                                                         INKCS011
02103          SET DP013-CICS-ABEND      TO TRUE                        INKCS011
02104          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS011
02105          MOVE '5205-WRITE-SEL-QUEUE'                              INKCS011
02106                                    TO DP013-PARAGRAPH             INKCS011
02107          MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'          INKCS011
02108                                    TO DP013-MESSAGE-TEXT(1)       INKCS011
02109          PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
02110      END-IF.                                                      INKCS011
02111 /                                                                 INKCS011
02112 *----------------------------------------------------------------*INKCS011
02113 *  WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,  *INKCS011
02114 *  SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT  *INKCS011
02115 *  THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO     *INKCS011
02116 *  THE USER.                                                     *INKCS011
02117 *----------------------------------------------------------------*INKCS011
02118                                                                   INKCS011
02119  6000-REFRESH-PANEL-FROM-TEMP.                                    INKCS011
02120                                                                   INKCS011
02121      IF  ASC-UPDATE-TSQ                                           INKCS011
**********   IF  ASC-UPDATE-TSQ                                         INKCS011
02122          SET ASC-NO-TSQ-UPDATE TO TRUE                            INKCS011
02123          MOVE ZERO                 TO PV-SAVE-ITEM                INKCS011
02124                                       PV-SEL-QUEUE-ITEM           INKCS011
02125          MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        INKCS011
02126                                    TO PV-HOLD-SELECTED-ITEMS      INKCS011
02127          PERFORM 6100-UPDATE-TSQ                                  INKCS011
02128              UNTIL PV-ITEMS-PROCESSED >                           INKCS011
02129                                      ASC-NUMBER-OF-SELECTED-ITEMS INKCS011
02130          PERFORM 6010-DELETE-SEL-QUEUE                            INKCS011
02131          INITIALIZE ASC-BLOCK-ENTRIES (1)                         INKCS011
02132                     ASC-BLOCK-ENTRIES (2)                         INKCS011
02133                                                                   INKCS011
02134          DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                 INKCS011
02135                 GIVING PV-BLOCK-NUMBER                            INKCS011
02136                 REMAINDER PV-REMAINDER                            INKCS011
02137          IF  PV-REMAINDER > ZERO                                  INKCS011
02138              ADD +1     TO PV-BLOCK-NUMBER                        INKCS011
02139          END-IF                                                   INKCS011
02140                                                                   INKCS011
02141          MOVE +1        TO ASC-BLK-SUB                            INKCS011
02142          PERFORM 4410-READ-TEMP-STORAGE                           INKCS011
02143          ADD +1         TO ASC-BLK-SUB                            INKCS011
02144                            PV-BLOCK-NUMBER                        INKCS011
02145          PERFORM 4410-READ-TEMP-STORAGE                           INKCS011
02146      END-IF.                                                      INKCS011
02147                                                                   INKCS011
02148 *----------------------------------------------------------------*INKCS011
02149 *    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *INKCS011
02150 *----------------------------------------------------------------*INKCS011
02151                                                                   INKCS011
02152  6010-DELETE-SEL-QUEUE.                                           INKCS011
02153                                                                   INKCS011
02154      EXEC CICS                                                    INKCS011
02155          DELETEQ TS                                               INKCS011
02156              QUEUE (ASC-SEL-QUEUE-NAME)                           INKCS011
02157              RESP  (PV-CICS-RESP)                                 INKCS011
02158      END-EXEC.                                                    INKCS011
02159                                                                   INKCS011
02160      IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                      INKCS011
02161         (PV-CICS-RESP = DFHRESP(QIDERR)))                         INKCS011
02162          CONTINUE                                                 INKCS011
02163      ELSE                                                         INKCS011
02164          SET DP013-CICS-ABEND TO TRUE                             INKCS011
02165          SET DP013-NO-ROLLBACK TO TRUE                            INKCS011
02166          MOVE '6010-DELETE-SEL-QUEUE'                             INKCS011
02167                         TO DP013-PARAGRAPH                        INKCS011
02168          MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'         INKCS011
02169                         TO DP013-MESSAGE-TEXT (1)                 INKCS011
02170          PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
02171      END-IF.                                                      INKCS011
02172                                                                   INKCS011
02173 *----------------------------------------------------------------*INKCS011
02174 *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *INKCS011
02175 *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *INKCS011
02176 *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *INKCS011
02177 *    THE TEMP QUEUE.                                             *INKCS011
02178 *----------------------------------------------------------------*INKCS011
02179                                                                   INKCS011
02180  6100-UPDATE-TSQ.                                                 INKCS011
02181                                                                   INKCS011
02182      ADD 1                     TO  PV-SEL-QUEUE-ITEM.             INKCS011
02183      PERFORM 6105-READ-SEL-QUEUE.                                 INKCS011
02184      PERFORM                                                      INKCS011
02185          VARYING IN012-IDX                                        INKCS011
02186          FROM 1 BY 1                                              INKCS011
02187          UNTIL ((PV-ITEMS-PROCESSED >                             INKCS011
02188                               PV-HOLD-SELECTED-ITEMS)             INKCS011
02189          OR (IN012-IDX GREATER THAN IN012-MAX-ITEMS))             INKCS011
02190          IF  IN012-ACTION-SW (IN012-IDX) > SPACE                  INKCS011
02191              PERFORM 6110-PROCESS-ITEMS                           INKCS011
02192          END-IF                                                   INKCS011
02193          ADD +1                TO PV-ITEMS-PROCESSED              INKCS011
02194      END-PERFORM.                                                 INKCS011
02195      PERFORM 6111-WRITE-LIST-QUEUE.                               INKCS011
02196 *----------------------------------------------------------------*INKCS011
02197 *    READ THE LIST QUEUE RECORD.                                 *INKCS011
02198 *----------------------------------------------------------------*INKCS011
02199                                                                   INKCS011
02200  6105-READ-SEL-QUEUE.                                             INKCS011
02201                                                                   INKCS011
02202      EXEC CICS                                                    INKCS011
02203           READQ TS                                                INKCS011
02204               QUEUE (ASC-SEL-QUEUE-NAME)                          INKCS011
02205               INTO  (IN012-SELECTED-SKUS)                         INKCS011
02206               ITEM  (PV-SEL-QUEUE-ITEM)                           INKCS011
02207               RESP  (PV-CICS-RESP)                                INKCS011
02208      END-EXEC.                                                    INKCS011
02209                                                                   INKCS011
02210      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS011
02211          CONTINUE                                                 INKCS011
02212      ELSE                                                         INKCS011
02213          SET DP013-CICS-ABEND      TO  TRUE                       INKCS011
02214          SET DP013-NO-ROLLBACK     TO  TRUE                       INKCS011
02215          MOVE '6105-READ-SEL-QUEUE'                               INKCS011
02216                                    TO  DP013-PARAGRAPH            INKCS011
02217          MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'           INKCS011
02218                                    TO  DP013-MESSAGE-TEXT(1)      INKCS011
02219          PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
02220      END-IF.                                                      INKCS011
02221 /                                                                 INKCS011
02222 *----------------------------------------------------------------*INKCS011
02223 *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*INKCS011
02224 *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*INKCS011
02225 *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *INKCS011
02226 *----------------------------------------------------------------*INKCS011
02227                                                                   INKCS011
02228  6110-PROCESS-ITEMS.                                              INKCS011
02229                                                                   INKCS011
02230      COMPUTE PV-ITEM = 1 + ((IN012-ITEM (IN012-IDX) - 1)          INKCS011
02231                      / PC-ITEMS-PER-PANEL).                       INKCS011
02232                                                                   INKCS011
02233      IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                           INKCS011
02234          IF  PV-SAVE-ITEM NOT EQUAL ZERO                          INKCS011
02235              PERFORM 6111-WRITE-LIST-QUEUE                        INKCS011
02236          END-IF                                                   INKCS011
02237          MOVE PV-ITEM          TO PV-SAVE-ITEM                    INKCS011
02238                                                                   INKCS011
02239          PERFORM 6112-READ-LIST-QUEUE                             INKCS011
02240      END-IF.                                                      INKCS011
02241                                                                   INKCS011
02242      COMPUTE PV-SUB = (IN012-ITEM (IN012-IDX)                     INKCS011
02243                     - LQW-LIST-ITEM-NUMBER (1)) + 1.              INKCS011
02244      SET LQW-DESELECT (PV-SUB) TO    TRUE                         INKCS011
02245      MOVE LQW-SELECTED-SW (PV-SUB)                                INKCS011
02246                                TO LQW-TEMP-SELECTED-SW (PV-SUB).  INKCS011
02247      EVALUATE TRUE                                                INKCS011
02248          WHEN IN012-INQ (IN012-IDX)                               INKCS011
02249               MOVE PC-INQ      TO LQW-ACTION (PV-SUB)             INKCS011
02250                                                                   INKCS011
02251          WHEN IN012-UPD (IN012-IDX)                               INKCS011
02252               MOVE PC-UPD      TO LQW-ACTION (PV-SUB)             INKCS011
02253                                                                   INKCS011
02254          WHEN IN012-DEL (IN012-IDX)                               INKCS011
02255               MOVE PC-DEL      TO LQW-ACTION (PV-SUB)             INKCS011
02256                                                                   INKCS011
02257          WHEN IN012-ERR (IN012-IDX)                               INKCS011
02258               MOVE PC-ERR      TO LQW-ACTION (PV-SUB)             INKCS011
02259      END-EVALUATE.                                                INKCS011
02260                                                                   INKCS011
02261      SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                INKCS011
02262                                                                   INKCS011
02263 *----------------------------------------------------------------*INKCS011
02264 *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *INKCS011
02265 *----------------------------------------------------------------*INKCS011
02266                                                                   INKCS011
02267  6111-WRITE-LIST-QUEUE.                                           INKCS011
02268                                                                   INKCS011
02269      EXEC CICS                                                    INKCS011
02270          WRITEQ TS                                                INKCS011
02271              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS011
02272              FROM  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS011
02273              ITEM  (PV-SAVE-ITEM)                                 INKCS011
02274              REWRITE                                              INKCS011
02275              RESP  (PV-CICS-RESP)                                 INKCS011
02276      END-EXEC.                                                    INKCS011
02277                                                                   INKCS011
02278      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS011
02279          CONTINUE                                                 INKCS011
02280      ELSE                                                         INKCS011
02281          SET DP013-CICS-ABEND  TO  TRUE                           INKCS011
02282          SET DP013-NO-ROLLBACK TO  TRUE                           INKCS011
02283          MOVE '6111-WRITE-LIST-QUEUE'                             INKCS011
02284                                TO  DP013-PARAGRAPH                INKCS011
02285          MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                  INKCS011
02286                                TO  DP013-MESSAGE-TEXT(1)          INKCS011
02287          PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
02288      END-IF.                                                      INKCS011
02289 /                                                                 INKCS011
02290 *----------------------------------------------------------------*INKCS011
02291 *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*INKCS011
02292 *----------------------------------------------------------------*INKCS011
02293                                                                   INKCS011
02294  6112-READ-LIST-QUEUE.                                            INKCS011
02295                                                                   INKCS011
02296      EXEC CICS                                                    INKCS011
02297          READQ TS                                                 INKCS011
02298              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS011
02299              INTO  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS011
02300              ITEM  (PV-ITEM)                                      INKCS011
02301              RESP  (PV-CICS-RESP)                                 INKCS011
02302      END-EXEC.                                                    INKCS011
02303                                                                   INKCS011
02304      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS011
02305          CONTINUE                                                 INKCS011
02306      ELSE                                                         INKCS011
02307          SET DP013-CICS-ABEND        TO TRUE                      INKCS011
02308          SET DP013-NO-ROLLBACK       TO TRUE                      INKCS011
02309          MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH           INKCS011
02310          MOVE 'ATTEMPTING TO READQ LIST QUEUE'                    INKCS011
02311                                      TO DP013-MESSAGE-TEXT(1)     INKCS011
02312          PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
02313      END-IF.                                                      INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *INKCS011
W25248* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *INKCS011
W25248* FOR THE SELECTED ITEMS QUEUE.                                  *INKCS011
W25248*                                                                *INKCS011
W25248* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *INKCS011
W25248* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *INKCS011
W25248* THE ARRAY IS FULL.                                             *INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7000-PREPARE-SEL-QUEUE.                                          INKCS011
W25248                                                                  INKCS011
W25248     MOVE +1                   TO  ASC-BLK-SUB.                   INKCS011
W25248     PERFORM 7100-WRITE-TEMP-STORAGE.                             INKCS011
W25248     ADD +1                    TO  ASC-BLK-SUB.                   INKCS011
W25248     PERFORM 7100-WRITE-TEMP-STORAGE.                             INKCS011
W25248                                                                  INKCS011
W25248     MOVE ASC-SEL-QUEUE-NAME   TO  IN001-SEL-QUEUE-NAME.          INKCS011
W25248     PERFORM 7510-DELETE-SEL-QUEUE.                               INKCS011
W25248                                                                  INKCS011
W25248     SET ASC-UPDATE-TSQ        TO  TRUE.                          INKCS011
W25248                                                                  INKCS011
W25248     SET IN027-IDX             TO  1.                             INKCS011
W25248     MOVE ZERO                 TO IN001-NUMBER-OF-TS-ITEMS.       INKCS011
W25248     INITIALIZE IN027-SELECTED-ITEM-ARRAY.                        INKCS011
W25248                                                                  INKCS011
W25248     PERFORM 7200-FILL-SEL-QUEUE-RECORD                           INKCS011
W25248             VARYING PV-ITEM                                      INKCS011
W25248             FROM 1 BY 1                                          INKCS011
W25248             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.     INKCS011
W25248                                                                  INKCS011
W25248     IF  IN027-IDX > 1                                            INKCS011
W25248         PERFORM 7205-WRITE-SEL-QUEUE                             INKCS011
W25248         ADD +1                TO IN001-NUMBER-OF-TS-ITEMS        INKCS011
W25248     END-IF.                                                      INKCS011
W25248                                                                  INKCS011
W25248     MOVE 'INLADJST' TO ASC-RETURN-FROM-MNEMONIC.                 INKCS011
W25248 EJECT                                                            INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *INKCS011
W25248*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7100-WRITE-TEMP-STORAGE.                                         INKCS011
W25248                                                                  INKCS011
W25248     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                     INKCS011
W25248                                   TO PV-LIST-ITEM-NUMBER.        INKCS011
W25248     COMPUTE PV-BLOCK-NUMBER =                                    INKCS011
W25248         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS011
W25248                                                                  INKCS011
W25248     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)             INKCS011
W25248     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)             INKCS011
W25248         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      INKCS011
W25248             PERFORM 7105-REWRITE-ASC-LISTQ                       INKCS011
W25248         END-IF                                                   INKCS011
W25248     ELSE                                                         INKCS011
W25248         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       INKCS011
W25248         ASC-HIGHEST-BLOCK-WRITTEN + 1                            INKCS011
W25248         PERFORM 7110-WRITE-ASC-LISTQ                             INKCS011
W25248         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN   INKCS011
W25248     END-IF.                                                      INKCS011
W25248                                                                  INKCS011
W25248     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS011
W25248                                                                  INKCS011
W25248******************************************************************INKCS011
W25248** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **INKCS011
W25248** SPECIFIC COMMAREA.                                           **INKCS011
W25248******************************************************************INKCS011
W25248                                                                  INKCS011
W25248 7105-REWRITE-ASC-LISTQ.                                          INKCS011
W25248                                                                  INKCS011
W25248     EXEC CICS                                                    INKCS011
W25248         WRITEQ TS                                                INKCS011
W25248             QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS011
W25248             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS011
W25248             ITEM  (PV-BLOCK-NUMBER)                              INKCS011
W25248             REWRITE                                              INKCS011
W25248             RESP  (PV-CICS-RESP)                                 INKCS011
W25248     END-EXEC.                                                    INKCS011
W25248                                                                  INKCS011
W25248     IF  PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS011
W25248         CONTINUE                                                 INKCS011
W25248     ELSE                                                         INKCS011
W25248         SET DP013-CICS-ABEND            TO TRUE                  INKCS011
W25248         SET DP013-NO-ROLLBACK           TO TRUE                  INKCS011
W25248         MOVE '7105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH       INKCS011
W25248         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'     INKCS011
W25248                                         TO DP013-MESSAGE-TEXT(1) INKCS011
W25248         PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
W25248     END-IF.                                                      INKCS011
W25248                                                                  INKCS011
W25248******************************************************************INKCS011
W25248** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **INKCS011
W25248** SPECIFIC COMMAREA.                                           **INKCS011
W25248******************************************************************INKCS011
W25248                                                                  INKCS011
W25248 7110-WRITE-ASC-LISTQ.                                            INKCS011
W25248                                                                  INKCS011
W25248     EXEC CICS                                                    INKCS011
W25248         WRITEQ TS                                                INKCS011
W25248             QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS011
W25248             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS011
W25248             ITEM  (PV-BLOCK-NUMBER)                              INKCS011
W25248             RESP  (PV-CICS-RESP)                                 INKCS011
W25248     END-EXEC.                                                    INKCS011
W25248     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                      INKCS011
W25248         SET DP013-CICS-ABEND      TO TRUE                        INKCS011
W25248         SET DP013-NO-ROLLBACK     TO TRUE                        INKCS011
W25248         MOVE '7110-WRITE-ASC-LISTQ'                              INKCS011
W25248                                   TO DP013-PARAGRAPH             INKCS011
W25248         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS011
W25248                                   TO DP013-MESSAGE-TEXT(1)       INKCS011
W25248         PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
W25248     END-IF.                                                      INKCS011
W25248/                                                                 INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *INKCS011
W25248* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7200-FILL-SEL-QUEUE-RECORD.                                      INKCS011
W25248                                                                  INKCS011
W25248     PERFORM 7612-READ-LIST-QUEUE.                                INKCS011
W25248                                                                  INKCS011
W25248     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       INKCS011
W25248         PERFORM                                                  INKCS011
W25248         VARYING LQW-IDX                                          INKCS011
W25248         FROM 1 BY 1                                              INKCS011
W25248         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL            INKCS011
W25248           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO          INKCS011
W25248                                                                  INKCS011
W25248             IF  LQW-SELECT (LQW-IDX)                             INKCS011
W25248                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)              INKCS011
W25248                                   TO IN027-ITEM      (IN027-IDX) INKCS011
W25248                 MOVE LQW-SKU  (LQW-IDX)                          INKCS011
W25248                                   TO IN027-SKU       (IN027-IDX) INKCS011
W25248                 MOVE SPACE        TO IN027-ACTION-SW (IN027-IDX) INKCS011
W25248                 MOVE LQW-PR-AMT (LQW-IDX)                        INKCS011
W25248                                   TO IN027-PRICE     (IN027-IDX) INKCS011
W25248                                                                  INKCS011
W25248                 SET IN027-IDX UP BY 1                            INKCS011
W25248                 IF  IN027-IDX > IN027-MAX-ITEMS                  INKCS011
W25248                     PERFORM 7205-WRITE-SEL-QUEUE                 INKCS011
W25248                     ADD +1        TO IN001-NUMBER-OF-TS-ITEMS    INKCS011
W25248                     INITIALIZE IN027-SELECTED-ITEM-ARRAY         INKCS011
W25248                     SET IN027-IDX TO  1                          INKCS011
W25248                 END-IF                                           INKCS011
W25248             END-IF                                               INKCS011
W25248         END-PERFORM                                              INKCS011
W25248     END-IF.                                                      INKCS011
W25248                                                                  INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7205-WRITE-SEL-QUEUE.                                            INKCS011
W25248                                                                  INKCS011
W25248     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                            INKCS011
W25248                                   TO IN027-SELECTED-ITEMS.       INKCS011
W25248                                                                  INKCS011
W25248     EXEC CICS                                                    INKCS011
W25248          WRITEQ TS                                               INKCS011
W25248              QUEUE (ASC-SEL-QUEUE-NAME)                          INKCS011
W25248              FROM  (IN027-SELECTED-SKUS)                         INKCS011
W25248              ITEM  (PV-SEL-QUEUE-ITEM)                           INKCS011
W25248              RESP  (PV-CICS-RESP)                                INKCS011
W25248     END-EXEC.                                                    INKCS011
W25248                                                                  INKCS011
W25248     IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS011
W25248         CONTINUE                                                 INKCS011
W25248     ELSE                                                         INKCS011
W25248         SET DP013-CICS-ABEND      TO TRUE                        INKCS011
W25248         SET DP013-NO-ROLLBACK     TO TRUE                        INKCS011
W25248         MOVE '7205-WRITE-SEL-QUEUE'                              INKCS011
W25248                                   TO DP013-PARAGRAPH             INKCS011
W25248         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'          INKCS011
W25248                                   TO DP013-MESSAGE-TEXT(1)       INKCS011
W25248         PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
W25248     END-IF.                                                      INKCS011
W25248/                                                                 INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248*  WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,  *INKCS011
W25248*  SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT  *INKCS011
W25248*  THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO     *INKCS011
W25248*  THE USER. THIS IS RETURNING FROM THE UPC LIST SCREEN (I013)   *INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7500-REFRESH-PANEL-FROM-TEMP.                                    INKCS011
W25248                                                                  INKCS011
W25248     IF  ASC-UPDATE-TSQ                                           INKCS011
W25248         SET ASC-NO-TSQ-UPDATE TO TRUE                            INKCS011
W25248         MOVE ZERO                 TO PV-SAVE-ITEM                INKCS011
W25248                                      PV-SEL-QUEUE-ITEM           INKCS011
W25248         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        INKCS011
W25248                                   TO PV-HOLD-SELECTED-ITEMS      INKCS011
W25248         PERFORM 7600-UPDATE-TSQ                                  INKCS011
W25248             UNTIL PV-ITEMS-PROCESSED >                           INKCS011
W25248                                     ASC-NUMBER-OF-SELECTED-ITEMS INKCS011
W25248         PERFORM 7510-DELETE-SEL-QUEUE                            INKCS011
W25248         INITIALIZE ASC-BLOCK-ENTRIES (1)                         INKCS011
W25248                    ASC-BLOCK-ENTRIES (2)                         INKCS011
W25248                                                                  INKCS011
W25248         DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                 INKCS011
W25248                GIVING PV-BLOCK-NUMBER                            INKCS011
W25248                REMAINDER PV-REMAINDER                            INKCS011
W25248         IF  PV-REMAINDER > ZERO                                  INKCS011
W25248             ADD +1     TO PV-BLOCK-NUMBER                        INKCS011
W25248         END-IF                                                   INKCS011
W25248                                                                  INKCS011
W25248         MOVE +1        TO ASC-BLK-SUB                            INKCS011
W25248         PERFORM 4410-READ-TEMP-STORAGE                           INKCS011
W25248         ADD +1         TO ASC-BLK-SUB                            INKCS011
W25248                           PV-BLOCK-NUMBER                        INKCS011
W25248         PERFORM 4410-READ-TEMP-STORAGE                           INKCS011
W25248     END-IF.                                                      INKCS011
W25248                                                                  INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7510-DELETE-SEL-QUEUE.                                           INKCS011
W25248                                                                  INKCS011
W25248     EXEC CICS                                                    INKCS011
W25248         DELETEQ TS                                               INKCS011
W25248             QUEUE (ASC-SEL-QUEUE-NAME)                           INKCS011
W25248             RESP  (PV-CICS-RESP)                                 INKCS011
W25248     END-EXEC.                                                    INKCS011
W25248                                                                  INKCS011
W25248     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                      INKCS011
W25248        (PV-CICS-RESP = DFHRESP(QIDERR)))                         INKCS011
W25248         CONTINUE                                                 INKCS011
W25248     ELSE                                                         INKCS011
W25248         SET DP013-CICS-ABEND TO TRUE                             INKCS011
W25248         SET DP013-NO-ROLLBACK TO TRUE                            INKCS011
W25248         MOVE '7510-DELETE-SEL-QUEUE'                             INKCS011
W25248                        TO DP013-PARAGRAPH                        INKCS011
W25248         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'         INKCS011
W25248                        TO DP013-MESSAGE-TEXT (1)                 INKCS011
W25248         PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
W25248     END-IF.                                                      INKCS011
W25248                                                                  INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *INKCS011
W25248*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *INKCS011
W25248*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *INKCS011
W25248*    THE TEMP QUEUE.                                             *INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7600-UPDATE-TSQ.                                                 INKCS011
W25248                                                                  INKCS011
W25248     ADD 1                     TO  PV-SEL-QUEUE-ITEM.             INKCS011
W25248     PERFORM 7605-READ-SEL-QUEUE.                                 INKCS011
W25248     PERFORM                                                      INKCS011
W25248         VARYING IN027-IDX                                        INKCS011
W25248         FROM 1 BY 1                                              INKCS011
W25248         UNTIL ((PV-ITEMS-PROCESSED >                             INKCS011
W25248                              PV-HOLD-SELECTED-ITEMS)             INKCS011
W25248         OR (IN027-IDX GREATER THAN IN027-MAX-ITEMS))             INKCS011
W25248         IF  IN027-ACTION-SW (IN027-IDX) > SPACE                  INKCS011
W25248             PERFORM 7610-PROCESS-ITEMS                           INKCS011
W25248         END-IF                                                   INKCS011
W25248         ADD +1                TO PV-ITEMS-PROCESSED              INKCS011
W25248     END-PERFORM.                                                 INKCS011
W25248     PERFORM 7611-WRITE-LIST-QUEUE.                               INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248*    READ THE LIST QUEUE RECORD.                                 *INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7605-READ-SEL-QUEUE.                                             INKCS011
W25248                                                                  INKCS011
W25248     EXEC CICS                                                    INKCS011
W25248          READQ TS                                                INKCS011
W25248              QUEUE (ASC-SEL-QUEUE-NAME)                          INKCS011
W25248              INTO  (IN027-SELECTED-SKUS)                         INKCS011
W25248              ITEM  (PV-SEL-QUEUE-ITEM)                           INKCS011
W25248              RESP  (PV-CICS-RESP)                                INKCS011
W25248     END-EXEC.                                                    INKCS011
W25248                                                                  INKCS011
W25248     IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS011
W25248         CONTINUE                                                 INKCS011
W25248     ELSE                                                         INKCS011
W25248         SET DP013-CICS-ABEND      TO  TRUE                       INKCS011
W25248         SET DP013-NO-ROLLBACK     TO  TRUE                       INKCS011
W25248         MOVE '7605-READ-SEL-QUEUE'                               INKCS011
W25248                                   TO  DP013-PARAGRAPH            INKCS011
W25248         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'           INKCS011
W25248                                   TO  DP013-MESSAGE-TEXT(1)      INKCS011
W25248         PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
W25248     END-IF.                                                      INKCS011
W25248/                                                                 INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*INKCS011
W25248*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*INKCS011
W25248*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7610-PROCESS-ITEMS.                                              INKCS011
W25248                                                                  INKCS011
W25248     COMPUTE PV-ITEM = 1 + ((IN027-ITEM (IN027-IDX) - 1)          INKCS011
W25248                     / PC-ITEMS-PER-PANEL).                       INKCS011
W25248                                                                  INKCS011
W25248     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                           INKCS011
W25248         IF  PV-SAVE-ITEM NOT EQUAL ZERO                          INKCS011
W25248             PERFORM 7611-WRITE-LIST-QUEUE                        INKCS011
W25248         END-IF                                                   INKCS011
W25248         MOVE PV-ITEM          TO PV-SAVE-ITEM                    INKCS011
W25248                                                                  INKCS011
W25248         PERFORM 7612-READ-LIST-QUEUE                             INKCS011
W25248     END-IF.                                                      INKCS011
W25248                                                                  INKCS011
W25248     COMPUTE PV-SUB = (IN027-ITEM (IN027-IDX)                     INKCS011
W25248                    - LQW-LIST-ITEM-NUMBER (1)) + 1.              INKCS011
W25248     SET LQW-DESELECT (PV-SUB) TO    TRUE                         INKCS011
W25248     MOVE LQW-SELECTED-SW (PV-SUB)                                INKCS011
W25248                               TO LQW-TEMP-SELECTED-SW (PV-SUB).  INKCS011
W25248     EVALUATE TRUE                                                INKCS011
W25248         WHEN IN027-INQ (IN027-IDX)                               INKCS011
W25248              MOVE PC-INQ      TO LQW-ACTION (PV-SUB)             INKCS011
W25248                                                                  INKCS011
W25248         WHEN IN027-UPD (IN027-IDX)                               INKCS011
W25248              MOVE PC-UPD      TO LQW-ACTION (PV-SUB)             INKCS011
W25248                                                                  INKCS011
W25248         WHEN IN027-DEL (IN027-IDX)                               INKCS011
W25248              MOVE PC-DEL      TO LQW-ACTION (PV-SUB)             INKCS011
W25248                                                                  INKCS011
W25248         WHEN IN027-ERR (IN027-IDX)                               INKCS011
W25248              MOVE PC-ERR      TO LQW-ACTION (PV-SUB)             INKCS011
W25248     END-EVALUATE.                                                INKCS011
W25248                                                                  INKCS011
W25248     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                INKCS011
W25248                                                                  INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248*    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7611-WRITE-LIST-QUEUE.                                           INKCS011
W25248                                                                  INKCS011
W25248     EXEC CICS                                                    INKCS011
W25248         WRITEQ TS                                                INKCS011
W25248             QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS011
W25248             FROM  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS011
W25248             ITEM  (PV-SAVE-ITEM)                                 INKCS011
W25248             REWRITE                                              INKCS011
W25248             RESP  (PV-CICS-RESP)                                 INKCS011
W25248     END-EXEC.                                                    INKCS011
W25248                                                                  INKCS011
W25248     IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS011
W25248         CONTINUE                                                 INKCS011
W25248     ELSE                                                         INKCS011
W25248         SET DP013-CICS-ABEND  TO  TRUE                           INKCS011
W25248         SET DP013-NO-ROLLBACK TO  TRUE                           INKCS011
W25248         MOVE '7611-WRITE-LIST-QUEUE'                             INKCS011
W25248                               TO  DP013-PARAGRAPH                INKCS011
W25248         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                  INKCS011
W25248                               TO  DP013-MESSAGE-TEXT(1)          INKCS011
W25248         PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
W25248     END-IF.                                                      INKCS011
W25248/                                                                 INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*INKCS011
W25248*----------------------------------------------------------------*INKCS011
W25248                                                                  INKCS011
W25248 7612-READ-LIST-QUEUE.                                            INKCS011
W25248                                                                  INKCS011
W25248     EXEC CICS                                                    INKCS011
W25248         READQ TS                                                 INKCS011
W25248             QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS011
W25248             INTO  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS011
W25248             ITEM  (PV-ITEM)                                      INKCS011
W25248             RESP  (PV-CICS-RESP)                                 INKCS011
W25248     END-EXEC.                                                    INKCS011
W25248                                                                  INKCS011
W25248     IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS011
W25248         CONTINUE                                                 INKCS011
W25248     ELSE                                                         INKCS011
W25248         SET DP013-CICS-ABEND        TO TRUE                      INKCS011
W25248         SET DP013-NO-ROLLBACK       TO TRUE                      INKCS011
W25248         MOVE '7612-READ-LIST-QUEUE' TO DP013-PARAGRAPH           INKCS011
W25248         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                    INKCS011
W25248                                     TO DP013-MESSAGE-TEXT(1)     INKCS011
W25248         PERFORM DP013-0000-PROCESS-ABEND                         INKCS011
W25248     END-IF.                                                      INKCS011
W30692                                                                          
W30692/******************************************************************       
W30692* 8001-USE-STORE-DEFAULT-PRTR.                                            
W30692*******************************************************************       
W30692 8001-USE-STORE-DEFAULT-PRTR.                                             
W30692                                                                          
W30692     MOVE DP026-LOCATION-NBR TO PV-EDIT-LOC-NBR.                          
W30692     IF PV-EDIT-LOC-NBR-P1 IS EQUAL TO ZERO                               
W30692         STRING 'TKST' DELIMITED BY SIZE                                  
W30692                PV-EDIT-LOC-NBR-P2 DELIMITED BY SIZE                      
W30692                            INTO PV-USER-USER-CODE                        
W30692     ELSE                                                                 
W30692         STRING 'TKS' DELIMITED BY SIZE                                   
W30692                PV-EDIT-LOC-NBR DELIMITED BY SIZE                         
W30692                            INTO PV-USER-USER-CODE                        
W30692     END-IF.                                                              
W30692                                                                          
W30692     MOVE PV-USER-USER-CODE TO USER-USER-CODE.                            
W30692                                                                          
W30692     EXEC SQL                                                             
W30692         SELECT DFLT_PRTR_ID                                              
W30692         INTO  :USER-DFLT-PRTR-ID                                         
W30692         FROM  TUSER                                                      
W30692         WHERE USER_CODE = :USER-USER-CODE                                
W30692         WITH UR                                                          
W30692     END-EXEC.                                                            
W30692                                                                          
W30692     EVALUATE TRUE                                                        
W30692         WHEN SQLCODE = ZERO                                              
W30692             IF USER-DFLT-PRTR-ID GREATER SPACES                          
W30692                 MOVE USER-DFLT-PRTR-ID                                   
W30692                               TO JL-JCL-INK950-PRTDEST                   
W30692                 MOVE 'Y'      TO JL-JCL-INK950-PRT-IND                   
W30692             ELSE                                                         
W30692                 MOVE SPACES   TO JL-JCL-INK950-PRTDEST                   
W30692                 MOVE 'N'      TO JL-JCL-INK950-PRT-IND                   
W30692             END-IF                                                       
W30692         WHEN SQLCODE = +100                                              
W30692             MOVE SPACES       TO JL-JCL-INK950-PRTDEST                   
W30692             MOVE 'N'          TO JL-JCL-INK950-PRT-IND                   
W30692         WHEN OTHER                                                       
W30692             MOVE SPACES       TO JL-JCL-INK950-PRTDEST                   
W30692             MOVE 'N'          TO JL-JCL-INK950-PRT-IND                   
W30692             MOVE '8001-USE-STORE-DEFAULT-PRTR'                           
W30692                               TO DP013-PARAGRAPH                         
W30692             MOVE 'SELECT DFLT-PRT-ID'                                    
W30692                               TO DP013-MESSAGE-TEXT (1)                  
W30692             MOVE SQLCA        TO DP013-SQLCA                             
W30692             MOVE 'TUSER  '    TO DP013-DB2-TABLE-NAME (1)                
W30692             SET DP013-DB2-ABEND                                          
W30692                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
W30692             PERFORM DP013-0000-PROCESS-ABEND                             
W30692     END-EVALUATE.                                                        
W30692                                                                          
P25248*******************************************************************       
P25248*** FORMAT AND SUBMIT BATCH JOB.                                **        
P25248*******************************************************************       
P25248 8000-FORMAT-AND-SUBMIT.                                                  
P25248                                                                          
P25248     PERFORM 9500-ENQUEUE-INTERNAL-READER.                                
P25248                                                                          
P25248     EXEC CICS                                                            
P25248       ASSIGN                                                             
P25248         APPLID (PV-CICS-ID)                                              
P25248     END-EXEC.                                                            
P25248                                                                          
P25248     IF PV-CICS-REGION-PRODUCTION                                         
P25248         MOVE 'INK950'     TO JL-JCL-INK950-JOBNAME                       
P25248         MOVE 'Q'          TO JL-JCL-INK950-JOBCLASS                      
P25248         MOVE 'P'          TO JL-JCL-INK950-MSGCLASS                      
P25248         MOVE SPACES       TO JL-JCL-INK950-NOTIFY-OPT                    
P25248     ELSE                                                                 
P25248         MOVE DP020-USERID TO JL-JCL-INK950-JOBNAME                       
P25248                              JL-JCL-TEST-USER                            
P25248         MOVE 'P'          TO JL-JCL-INK950-JOB-UNQ                       
P25248         MOVE 'A'          TO JL-JCL-INK950-JOBCLASS                      
P25248         MOVE 'X'          TO JL-JCL-INK950-MSGCLASS                      
P25248         MOVE JL-JCL-TEST-NOTIFY TO JL-JCL-INK950-NOTIFY-OPT              
P25248     END-IF.                                                              
P25248     MOVE JL-JCL-INK950-LINE-1 TO TD-JCL-LINE.                            
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-LINE-2 TO TD-JCL-LINE.                            
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-LINE-3 TO TD-JCL-LINE.                            
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-LONG      TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE DP020-USERID TO JL-JCL-INK950-CMT-USRID                         
P25248     MOVE PV-CICS-ID   TO JL-JCL-INK950-CMT-REGION.                       
P25248     MOVE JL-JCL-INK950-CMT-USER-INFO TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-1         TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-2         TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-3         TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248**   CONTROL CARD INFORMATION (JL-JCL-INK950-LINE-14)                     
P25248     MOVE IN001-STORE-NBR         TO  JL-JCL-INK950-STORE.                
P25248     MOVE IN001-AREA-NBR          TO  JL-JCL-INK950-AREA.                 
P25248     MOVE ZEROES                  TO  JL-JCL-INK950-SKU                   
P25248                                      JL-JCL-INK950-RETAIL.               
W28545     MOVE IN001-DB2-ACTL-INV-DTE  TO                                      
W28545                                     JL-JCL-DB2-ACTL-INV-DTE.             
W26600                                                                          
W26600     EXEC CICS ASSIGN                                                     
W26600         USERID(JL-JCL-INK950-USERID)                                     
W26600     END-EXEC.                                                            
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CC-INFO       TO JL-JCL-INK950-CMT-OPTION.        
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-4         TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-LONG      TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     IF PV-CICS-REGION-PRODUCTION                                         
P25248         MOVE JL-JCL-INK950-LINE-10       TO TD-JCL-LINE                  
P25248         PERFORM 8100-WRITE-TD-LINE                                       
P25248     ELSE                                                                 
P25248         MOVE JL-JCL-TEST-LINE-10         TO TD-JCL-LINE                  
P25248         PERFORM 8100-WRITE-TD-LINE                                       
P25248         MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE                  
P25248         PERFORM 8100-WRITE-TD-LINE                                       
P25248                                                                          
P25248         MOVE JL-JCL-TEST-LINE-11A        TO TD-JCL-LINE                  
P25248         PERFORM 8100-WRITE-TD-LINE                                       
P25248         MOVE JL-JCL-TEST-LINE-11B        TO TD-JCL-LINE                  
P25248         PERFORM 8100-WRITE-TD-LINE                                       
P25248         MOVE JL-JCL-TEST-LINE-11C        TO TD-JCL-LINE                  
P25248         PERFORM 8100-WRITE-TD-LINE                                       
P25248     END-IF.                                                              
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-LINE-11       TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
W30692*    EXEC SQL                                                             
W30692*        SELECT DFLT_PRTR_ID                                              
W30692*        INTO  :USER-DFLT-PRTR-ID                                         
W30692*        FROM  TUSER                                                      
W30692*        WHERE USER_CODE = :DP020-USERID                                  
W30692*    END-EXEC.                                                            
W30692*    MOVE 'LOCAL'           TO JL-JCL-INK950-PRTDEST.                     
W30692*    MOVE 'N'               TO JL-JCL-INK950-PRT-IND.                     
W30692*    EVALUATE TRUE                                                        
W30692*        WHEN SQLCODE = ZERO                                              
W30692*            IF USER-DFLT-PRTR-ID NOT = SPACES                            
W30692*                MOVE USER-DFLT-PRTR-ID TO JL-JCL-INK950-PRTDEST          
W30692*                MOVE 'Y'               TO JL-JCL-INK950-PRT-IND          
W30692*            END-IF                                                       
W30692*        WHEN SQLCODE = +100                                              
W30692*            CONTINUE                                                     
W30692*        WHEN OTHER                                                       
W30692*            MOVE '8100-FORMAT-AND-SUBMIT'                                
W30692*                              TO DP013-PARAGRAPH                         
W30692*            MOVE 'SELECT DFLT-PRT-ID'                                    
W30692*                              TO DP013-MESSAGE-TEXT (1)                  
W30692*            MOVE SQLCA        TO DP013-SQLCA                             
W30692*            MOVE 'TUSER  '    TO DP013-DB2-TABLE-NAME (1)                
W30692*            SET DP013-DB2-ABEND                                          
W30692*                DP013-XCTL-DISPLAY-RESTART                               
W30692*                              TO TRUE                                    
W30692*            PERFORM DP013-0000-PROCESS-ABEND                             
W30692*    END-EVALUATE.                                                        
W30692*                                                                         
P25248     MOVE JL-JCL-INK950-LINE-12       TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-LINE-13       TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248**   CONTROL CARD INFORMATION (IN-STREAM DATA ON JOB CARD)                
P25248**                                    JL-JCL-INK950-UPC                   
P25248     MOVE JL-JCL-INK950-CC-INFO       TO JL-JCL-INK950-REQ-OPTION.        
P25248     MOVE JL-JCL-INK950-LINE-14       TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-LINE-EOD      TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     MOVE JL-JCL-INK950-LINE-EOF      TO TD-JCL-LINE.                     
P25248     PERFORM 8100-WRITE-TD-LINE.                                          
P25248                                                                          
P25248     PERFORM 9600-DEQUEUE-INTERNAL-READER.                                
P25248                                                                          
P25248                                                                          
P25248*******************************************************************       
P25248** WRITE A JCL LINE TO THE TRANSIENT DATA QUEUE FOR THE INTERNAL *        
P25248** READER.  ON ANY ERROR, DEQUEUE THE INTERNAL READER AND ABEND. *        
P25248*******************************************************************       
P25248 8100-WRITE-TD-LINE.                                                      
P25248     EXEC CICS                                                            
P25248         WRITEQ TD                                                        
P25248             QUEUE(TD-QUEUE-NAME)                                         
P25248             FROM(TD-JCL-LINE)                                            
P25248             LENGTH(TD-JCL-LINE-LENGTH)                                   
P25248             RESP(PV-CICS-RESP)                                           
P25248     END-EXEC.                                                            
P25248                                                                          
P25248     IF PV-CICS-RESP NOT = DFHRESP(NORMAL)                                
P25248         SET DP013-CICS-ABEND      TO TRUE                                
P25248         SET DP013-NO-ROLLBACK     TO TRUE                                
P25248         MOVE '8100-WRITE-TD-LINE' TO DP013-PARAGRAPH                     
P25248         MOVE 'ATTEMPTING TO WRITE TO INTERNAL READER TD QUEUE'           
P25248                                   TO DP013-MESSAGE-TEXT(1)               
P25248         PERFORM 9600-DEQUEUE-INTERNAL-READER                             
P25248         PERFORM DP013-0000-PROCESS-ABEND                                 
P25248     END-IF.                                                              
P25248                                                                          
P25248/                                                                 INKCS011
P25248*******************************************************************       
P25248** ENQUEUE THE TRANSIENT DATA QUEUE FOR THE INTERNAL READER.     *        
P25248** THIS WILL PREVENT INTERLEAVING OF JCL SUBMITTED CONCURRENTLY  *        
P25248** BY MULTIPLE USERS.  IF THE QUEUE IS BUSY, THIS PROGRAM WILL   *        
P25248** WAIT TO ENQUEUE IT, BY NOT SPECIFICALLY HANDLING THE QBUSY    *        
P25248** CONDITION.                                                    *        
P25248*******************************************************************       
P25248 9500-ENQUEUE-INTERNAL-READER.                                            
P25248     EXEC CICS                                                            
P25248         ENQ                                                              
P25248             RESOURCE(TD-QUEUE-NAME)                                      
P25248             LENGTH(TD-QUEUE-NAME-LENGTH)                                 
P25248             RESP(PV-CICS-RESP)                                           
P25248     END-EXEC.                                                            
P25248                                                                          
P25248     IF PV-CICS-RESP NOT = DFHRESP(NORMAL)                                
P25248         SET DP013-CICS-ABEND      TO TRUE                                
P25248         SET DP013-NO-ROLLBACK     TO TRUE                                
P25248         MOVE '9500-ENQUEUE-INTERNAL-READER'                              
P25248                                   TO DP013-PARAGRAPH                     
P25248         STRING 'ENQUEUE '            DELIMITED BY SIZE                   
P25248                TD-QUEUE-NAME         DELIMITED BY SIZE                   
P25248                ' (INTERNAL READER)'  DELIMITED BY SIZE                   
P25248             INTO DP013-MESSAGE-TEXT (1)                                  
P25248         PERFORM DP013-0000-PROCESS-ABEND                                 
P25248     END-IF.                                                              
P25248                                                                          
P25248     SET PS-INTERNAL-READER-ENQUEUED TO TRUE.                             
P25248                                                                          
P25248*******************************************************************       
P25248** DEQUEUE THE TRANSIENT DATA QUEUE FOR THE INTERNAL READER.     *        
P25248** THIS SHOULD BE DONE FOLLOWING A SUCCESSFUL REPORT REQUEST     *        
P25248** SUBMISSION, A CANCELLATION OF THE CURRENT REPORT REQUEST, OR  *        
P25248** ANY DETECTED ABENDS.  THIS WILL FREE UP THE INTERNAL READER   *        
P25248** FOR THE NEXT USER.                                            *        
P25248*******************************************************************       
P25248 9600-DEQUEUE-INTERNAL-READER.                                            
P25248     SET PS-INTERNAL-READER-DEQUEUED TO TRUE.                             
P25248                                                                          
P25248     EXEC CICS                                                            
P25248         DEQ                                                              
P25248             RESOURCE(TD-QUEUE-NAME)                                      
P25248             LENGTH(TD-QUEUE-NAME-LENGTH)                                 
P25248             RESP(PV-CICS-RESP)                                           
P25248     END-EXEC.                                                            
P25248                                                                          
P25248     IF PV-CICS-RESP NOT = DFHRESP(NORMAL)                                
P25248         SET DP013-CICS-ABEND      TO TRUE                                
P25248         SET DP013-NO-ROLLBACK     TO TRUE                                
P25248         MOVE '9600-DEQUEUE-INTERNAL-READER'                              
P25248                                   TO DP013-PARAGRAPH                     
P25248         STRING 'DEQUEUE '            DELIMITED BY SIZE                   
P25248                TD-QUEUE-NAME         DELIMITED BY SIZE                   
P25248                ' (INTERNAL READER)'  DELIMITED BY SIZE                   
P25248             INTO DP013-MESSAGE-TEXT (1)                                  
P25248         PERFORM DP013-0000-PROCESS-ABEND                                 
P25248     END-IF.                                                              
P25248                                                                          
02315 *----------------------------------------------------------------*INKCS011
02316 *    THE DB2 RESOURCE IS NOT AVAILABLE, SET THE ERROR MESSAGE    *INKCS011
02317 *    AND THE ERROR SWITCHES.                                     *INKCS011
02318 *----------------------------------------------------------------*INKCS011
02319                                                                   INKCS011
02320  9900-SOFT-ABORT.                                                 INKCS011
02321                                                                   INKCS011
02322      SET PS-ERROR                TO  TRUE.                        INKCS011
02323      SET DP020-MSG-FATAL         TO  TRUE.                        INKCS011
02324      MOVE PC-TSYMSG-00551        TO  DP020-MSG-NUMBER.            INKCS011
02325                                                                   INKCS011
02326                                                                   INKCS011
22000 *----------------------------------------------------------------*INKCS046
W25248**DP017-0000-GET-CURR-DATE-TIME                                  *INKCS046
22000 *----------------------------------------------------------------*INKCS046
W25248**   COPY DPPD017.                                                INKCS046
W25248**                                                                INKCS046
02327 *----------------------------------------------------------------*INKCS011
02328 *    ABEND PROCESSOR MODULE                                      *INKCS011
02329 *----------------------------------------------------------------*INKCS011
02330                                                                   INKCS011
02331      COPY DPPD013.                                                INKCS011
W30692                                                                  INKCS011
W30692*----------------------------------------------------------------*        
W30692*    RETRIEVE LOGON INFORMATION FOR DEFAULT PRINTER DESTINATION  *        
W30692*----------------------------------------------------------------*        
W30692     COPY DPPD026.                                                        
W28545*----------------------------------------------------------------*INKCS011
W28545*    INPD400 COMMON PRICING ROUTINES                             *INKCS011
W28545*----------------------------------------------------------------*INKCS011
W28545                                                                  INKCS011
W28545     COPY INPD400.                                                INKCS011
