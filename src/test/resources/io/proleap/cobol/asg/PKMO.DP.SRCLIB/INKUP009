       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    INKUP009.                                                 
       AUTHOR.        PAUL KEBER.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  2004-08-24.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *          INKUP009 - CUTOFF DATABASE ADJUSTMENT UPDATE          *00100000
      ******************************************************************        
      * THIS PROGRAM WILL UPDATE INT_INV_CUTOFF TABLE ROWS CUTOFF      *00100000
      * ONHAND QUANTITY AND POST FLUSH RECEIPTS QUANTITY COLUMNS USING *00100000
      * DATA FROM THE CUTOFF DATABASE ADJUSTMENT EXTRACT FILES IN THE  *00100000
      * FORMAT OF COPYBOOK INRD051.  INV1 AND FSRA TRANSACTIONS WILL   *00100000
      * UPDATE THE POST FLUSH RECEIPTS QUANTITY AND INV2 TRANSACTIONS  *00100000
      * WILL UPDATE THE CUTOFF ONHAND QUANTITY ON INT_INV_CUTOFF.  A   *00100000
      * NEW INT_INV_CUTOFF TABLE ROW WILL BE INSERTED IF THE ROW IS    *00100000
      * NOT FOUND.  THIS PROGRAM CONTAINS CHECKPOINT RESTART LOGIC.    *00100000
      * THE INPUT TO THIS PROGRAM WILL CONTAIN A CONTROL CARD FOR THE  *        
      * PROCESSING JOB NAME.                                           *        
      ******************************************************************        
      ******************** HISTORY OF CHANGES **************************00111027
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00113027
      * -------  ----------  ----------------------------------------   00114027
      * WR26600  08-24-2004  PAUL KEBER - INITIAL INSTALLATION.         00119027
W26600* WR26600  05-19-2005  R.MCDONALD - FIX PROBLEM WITH ADDING       00119027
W26600*                                   IN INV1 TRANSACTIONS TO               
W26600*                                   CUTOFF TWICE.                         
      * WR27808  05-01-2005  ERIN SEARL - MANIFESTING PROJECT.          00119027
W27808* WR27808  09-13-2005  S.JACKSON  - DON'T ALLOW POST-FL UPDATE    00119027
W27808*                                   IF LOC IS MANIFESTED          00119027
W28545* W28545   08-10-2006  ADD LOGIC  FOR DEPARTMENT LEVEL                    
W28545*                      INVENTORY PROCESS.                                 
W28545*                      -B. GRAHAM - STRATAGEM                             
W33304* W33304   04-09-2008  HOLD INVENTORY LEDGER OPEN ACROSS PERIODS          
W33304*                      CHUCK EBERT - STRATAGEM                            
      ******************************************************************00120000
                                                                        00140000
       ENVIRONMENT DIVISION.                                            00150000
                                                                        00160000
       CONFIGURATION SECTION.                                           00170000
       SOURCE-COMPUTER.        IBM-3090.                                00180000
       OBJECT-COMPUTER.        IBM-3090.                                00190000
                                                                        00160000
       INPUT-OUTPUT SECTION.                                            00200000
       FILE-CONTROL.                                                    00210000
                                                                        00211000
           SELECT CC-JOBNAME-CNTL-FILE                                          
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CUTOFF-ADJUSTS-FILE                                   00220000
               ASSIGN TO INP02.                                         00230000
                                                                        00250000
       DATA DIVISION.                                                   00260000
                                                                        00250000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  CC-JOBNAME-CNTL-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CC-JOBNAME-CNTL-REC         PIC X(80).                               
                                                                                
       FD  CUTOFF-ADJUSTS-FILE                                          00290000
           LABEL RECORDS ARE STANDARD                                   00300000
           RECORDING MODE IS F                                          00310000
           BLOCK CONTAINS 0 RECORDS.                                    00320000
       01  CUTOFF-ADJUSTS-RECORD       PIC X(48).                       00330026
                                                                        00340000
       WORKING-STORAGE SECTION.                                         00370000
                                                                        00380000
       01  FILLER                      PIC X(28)   VALUE                00381000
                'BEGINNING OF WORKING STORAGE'.                         00382000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00383000
                                                                        00384000
       01  PC-PROGRAM-CONSTANTS.                                        00385000
           05  PC-PROGRAM-NAME         PIC  X(08)  VALUE 'INKUP009'.    00387000
           05  PC-MAX-DEADLOCKS        PIC S9(03)  VALUE +3.            00389000
                                                                        00390000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00400000
           05  PE-MSG-01                       PIC X(50) VALUE          00430000
                'INT_INV_CTOFF ROW NOT FOUND FOR UPDATE           '.    00440000
           05  PE-MSG-02                       PIC X(50) VALUE          00430000
                'ATTEMPT TO UPDATE ROW ON INT_INV_CTOFF FAILED    '.    00440000
           05  PE-MSG-03                       PIC X(50) VALUE          00470000
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00480000
           05  PE-MSG-04                       PIC X(50) VALUE          00490000
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00500000
           05  PE-MSG-05                       PIC X(50) VALUE          00510000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00520000
           05  PE-MSG-06                       PIC X(50) VALUE          00530000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED '.    00540000
           05  PE-MSG-07                       PIC X(50) VALUE          00430000
                'ATTEMPT TO INSERT ROW ON INT_INV_CTOFF FAILED    '.    00440000
                                                                        00550000
       01  PS-PROGRAM-SWITCHES.                                         00560000
           05  PS-EOF-CUTOFF-ADJUSTS-FILE-SW PIC X(01)    VALUE 'N'.    00570000
               88  PS-EOF-CUTOFF-ADJUSTS-FILE             VALUE 'Y'.    00580000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00610000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00620000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00630000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00640000
                                                                        00650000
       01  PROGRAM-VARIABLES.                                           00710000
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     00720000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     00730000
W27808     05  PV-PREV-LOC-NBR                 PIC S9(04) COMP          00730000
W27808                                                    VALUE +0.     00730000
                                                                        00740000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 00750000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 00760000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 00770000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 00780000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 00790000
                                                                                
       01  PV-PROCESS-JOB-NAME-CC.                                              
           05  PV-PROCESS-JOB-NAME       PIC X(08)   VALUE SPACES.              
           05  FILLER                    PIC X(72)   VALUE SPACES.              
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                     00810000
           05  PA-RECORD-COUNTS.                                        00820000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00830000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      00840000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      00840000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      00840000
                                                                        00880000
       01  PD-PROGRAM-DISPLAYS.                                         00890000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            00900000
           05  PD-DISPLAY-TOTAL          PIC  Z,ZZZ,ZZZ,ZZZ,ZZ9.99-.    00901000
                                                                        01080000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01470000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +36  COMP. 01480000
           05  CR-CHECKPOINT-RESTART-VAR.                               01490000
               10  CR-PREV-DTL-KEY          PIC  X(07) VALUE SPACES.    01500000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01510000
                   15  CR-LOC-NBR           PIC S9(04) COMP.            00961000
                   15  CR-SKU-NBR           PIC S9(08) COMP-3.          00961000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01560000
               10  CR-UPDATE-COUNT          PIC  9(09) VALUE ZEROES.    01560000
               10  CR-INSERT-COUNT          PIC  9(09) VALUE ZEROES.    01560000
                                                                        01570000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01580000
           05  CK-SAVE-PREV-KEY         PIC  X(07) VALUE SPACES.        01590000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01600000
               10  CK-LOC-NBR           PIC S9(04) COMP.                01610000
               10  CK-SKU-NBR           PIC S9(08) COMP-3.              01620000
                                                                        01650000
      ******************************************************************01090000
      * CUTOFF DATABASE ADJUSTMENT EXTRACT INPUT FILE COPYBOOK          01100000
      ******************************************************************01110000
           COPY INRD051.                                                01120000
                                                                        01130000
      ******************************************************************01140000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01150000
      ******************************************************************01160000
           COPY DPWS004.                                                01170000
                                                                        01180000
      ******************************************************************01190000
      *  USED FOR DB2 ERRORS                                            01200000
      ******************************************************************01210000
           EXEC SQL                                                     01220000
               INCLUDE SQLCA                                            01230000
           END-EXEC.                                                    01240000
                                                                        01250000
      ******************************************************************01260000
      *  INVENTORY CUTOFF TABLE                                         01270000
      ******************************************************************01280000
           EXEC SQL                                                     01290000
               INCLUDE INT00000                                         01300000
           END-EXEC.                                                    01310000
28545                                                                           
28545 *****************************************************************         
28545 * TINCNTL DCLGEN                                                          
28545 *****************************************************************         
28545      EXEC SQL                                                             
28545          INCLUDE TINCNTL                                                  
28545      END-EXEC.                                                            
                                                                        01320000
      ******************************************************************01260000
      *  INVENTORY PARAMETER                                            01270000
      ******************************************************************01280000
W27808     EXEC SQL                                                     01290000
W27808         INCLUDE TINVPAR                                          01300000
W27808     END-EXEC.                                                    01310000
                                                                        01320000
      ***************************************************************** 01330000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01340000
      ***************************************************************** 01350000
           EXEC SQL                                                     01360000
               INCLUDE TCKRSTCN                                         01370000
           END-EXEC.                                                    01380000
                                                                        01390000
           EXEC SQL                                                     01400000
               INCLUDE TCKRSTIN                                         01410000
           END-EXEC.                                                    01420000
                                                                        01462000
      *-------------------*                                             01660000
       PROCEDURE DIVISION.                                              01670000
      *-------------------*                                             01680000
                                                                        01690000
       0000-INKUP009.                                                   01700000
                                                                        01710000
           PERFORM 1000-INITIALIZATION.                                 01720000
                                                                        01730000
           PERFORM 2000-PROCESS-INPUT                                   01740000
                UNTIL PS-EOF-CUTOFF-ADJUSTS-FILE.                       01750000
                                                                        01760000
           PERFORM 8000-EOJ-ROUTINE.                                    01770000
                                                                        01780000
           STOP RUN.                                                    01790000
                                                                        01800000
      ******************************************************************01810000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 01820000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      01830000
      ******************************************************************01840000
                                                                        01850000
       1000-INITIALIZATION.                                             01860000
                                                                        01870000
           OPEN INPUT CC-JOBNAME-CNTL-FILE                              01880000
                      CUTOFF-ADJUSTS-FILE.                              01880000
                                                                        01890000
           PERFORM 4000-READ-PROCESS-JOB-NAME.                                  
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' PROCESSING JOB NAME: ' PV-PROCESS-JOB-NAME.                
                                                                                
           IF PV-PROCESS-JOB-NAME = SPACES                                      
               MOVE +4002                 TO ABEND-CODE                         
               DISPLAY '**  ABEND     **'                                       
               DISPLAY '**  PROGRAM   **  =  INKUP009'                          
               DISPLAY '**  INVALID PROCESSING JOB NAME'                        
               CALL 'ILBOABN0'  USING ABEND-CODE                                
           END-IF.                                                              
                                                                                
           CLOSE CC-JOBNAME-CNTL-FILE.                                  06861000
                                                                        01890000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         01900000
                                                                        01910000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              01920000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           01930000
                PERFORM 7500-SELECT-RESTART-INFO                        01940000
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                   01950000
                                             CR-CHECKPOINT-RESTART-VAR  01960000
                MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY       01970000
                MOVE CR-UPDATE-COUNT          TO PA-UPDATE-COUNT        01970000
                MOVE CR-INSERT-COUNT          TO PA-INSERT-COUNT        01970000
           ELSE                                                         01980000
               SET PS-FIRST-COMMIT TO TRUE                              01990000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02000000
           END-IF.                                                      02010000
                                                                        02020000
           PERFORM 4100-READ-CUTOFF-ADJUSTS-FILE UNTIL                  02030000
              (PA-INPUT-COUNT > CR-INPUT-READ-COUNT)                    02040000
               OR                                                       02050000
               PS-EOF-CUTOFF-ADJUSTS-FILE.                              02051000
                                                                        02060000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02061000
               DISPLAY '** RESTART RUN **'                              06720800
               DISPLAY 'LOCATION NUMBER: ' IN051-LOC-NBR                06720800
               DISPLAY 'SKU NUMBER:      ' IN051-SKU-NBR                06720900
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              02067000
                        PA-INPUT-COUNT                                  02068000
           END-IF.                                                      02069000
                                                                        02069100
           IF PS-EOF-CUTOFF-ADJUSTS-FILE                                02070000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02071000
                   CONTINUE                                             02072000
               ELSE                                                     02073000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02080000
           ELSE                                                         02090000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02100000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02110000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02120000
           END-IF.                                                      02130000
                                                                        02140000
      ******************************************************************02150000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02160000
      ******************************************************************02170000
       2000-PROCESS-INPUT.                                              02180000
                                                                        02190000
W27808     IF IN051-LOC-NBR = PV-PREV-LOC-NBR                           02190000
W27808         CONTINUE                                                 02190000
W27808     ELSE                                                         02190000
W27808         PERFORM 6000-READ-TINVPAR                                02190000
W27808     END-IF.                                                      02190000
                                                                        02190000
           MOVE IN051-LOC-NBR            TO INT00000-LOC-NBR.           03520000
           MOVE IN051-SKU-NBR            TO INT00000-SKU-NBR.           03530000
           MOVE IN051-VERIFIED-DATE      TO INT00000-LAST-UPD-DTE.      03551000
           MOVE PC-PROGRAM-NAME          TO INT00000-LAST-UPD-BY-USR-ID.03551000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02200000
                                                                        02210000
           IF IN051-TRANS-INV2                                                  
               PERFORM 7000-UPDATE-INV-CTOFF-ONHAND                             
           ELSE                                                                 
W26600         IF IN051-TRANS-INV1                                              
W26600            COMPUTE IN051-QUANTITY = IN051-QUANTITY * -1                  
W26600         END-IF                                                           
W27808         IF INVPAR-MFSTNG-IND = 'N'                                       
W27808             PERFORM 7100-UPDATE-INV-POST-FLUSH                           
W27808         END-IF                                                           
           END-IF.                                                      02331000
                                                                        02340000
           IF PS-PROGRAM-DEADLOCKED                                     02350000
               CONTINUE                                                 02360000
           ELSE                                                         02370000
               PERFORM 7700-COMMIT-PROCESSING                           02380000
               PERFORM 4100-READ-CUTOFF-ADJUSTS-FILE                    02390000
           END-IF.                                                      02401000
                                                                        02410000
      ******************************************************************02700000
      * READS THE PROCESSING JOB NAME CONTROL CARD FILE.                02710000
      ******************************************************************02720000
       4000-READ-PROCESS-JOB-NAME.                                      02730000
           READ CC-JOBNAME-CNTL-FILE INTO PV-PROCESS-JOB-NAME-CC        02740000
               AT END                                                   02750000
                   MOVE +4003                 TO ABEND-CODE                     
                   DISPLAY '**  ABEND     **'                                   
                   DISPLAY '**  PROGRAM   **  =  INKUP009'                      
                   DISPLAY '**  EMPTY PROCESSING JOB NAME CONTROL CARD'         
                   CALL 'ILBOABN0'  USING ABEND-CODE.                           
                                                                        02770000
      ******************************************************************02700000
      * READS THE CUTOFF ADJUSTMENTS FILE INTO THE INRD051 LAYOUT       02710000
      ******************************************************************02720000
       4100-READ-CUTOFF-ADJUSTS-FILE.                                   02730000
           READ CUTOFF-ADJUSTS-FILE INTO IN051-CTOFF-ADJ-EXTR-RECORD    02740000
              AT END                                                    02750000
                MOVE 'Y' TO PS-EOF-CUTOFF-ADJUSTS-FILE-SW.              02760000
                                                                        02770000
           IF PS-EOF-CUTOFF-ADJUSTS-FILE                                02780000
               MOVE IN051-LOC-NBR             TO CR-LOC-NBR             02790000
               MOVE IN051-SKU-NBR             TO CR-SKU-NBR             02791000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    02795000
               MOVE PA-UPDATE-COUNT           TO CR-UPDATE-COUNT        02795000
               MOVE PA-INSERT-COUNT           TO CR-INSERT-COUNT        02795000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  02796000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       02797000
                                             TCKRSTINF-WORK-STRG-DESC   02798000
               EXEC SQL                                                 02799000
                 UPDATE TCKRSTINF                                       02799100
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      02799200
                  WHERE JOB_NAME   = :PV-PROCESS-JOB-NAME               02799300
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   02799400
               END-EXEC                                                 02799500
               IF SQLCODE = 0                                           02799700
                   CONTINUE                                             02799800
               ELSE                                                     02799900
                   MOVE PE-MSG-04            TO PV-OPERATION-ATTEMPTED  02800000
                   MOVE '* 4100-READ-CUTOFF-ADJUSTS-FILE *' TO          02800100
                        PV-PARAGRAPH-NAME                               02800200
                   PERFORM 9999-SQL-ABEND                               02800300
               END-IF                                                   02800400
           ELSE                                                         02801000
               ADD 1                         TO PA-INPUT-COUNT          02810000
           END-IF.                                                      02820000
                                                                        02830000
      ***************************************************************** 03300000
W27808* UPDATE THE INVENTORY CUTOFF TABLE ROW CUTOFF ONHAND QTY.        03310000
      ***************************************************************** 03330000
W27808 6000-READ-TINVPAR.                                               02190000
                                                                                
           MOVE IN051-LOC-NBR TO INVPAR-LOC-NBR                                 
                                 PV-PREV-LOC-NBR.                               
                                                                                
           EXEC SQL                                                             
                   SELECT MFSTNG_IND                                        0291
                     INTO :INVPAR-MFSTNG-IND                                0291
28545                FROM TINVPAR A                                             
28545                    ,TINCNTL B                                             
                    WHERE LOC_NBR           = :INVPAR-LOC-NBR               0312
                      AND ACTL_FIN_BK_DTE   = '9999-09-09'                  0313
28545                 AND A.INV_ID = B.INV_ID                                   
28545                 AND B.ACTV_IND = 'Y'                                      
W33304                AND A.LOC_INV_STAT_CDE = 'IN'                             
           END-EXEC.                                                            
                                                                        03570000
           EVALUATE SQLCODE                                             03580000
               WHEN 0                                                   03590000
                   CONTINUE                                             03591000
               WHEN OTHER                                               03700000
                   MOVE '6000-READ-TINVPAR'                             03640000
                                      TO PV-PARAGRAPH-NAME              03650000
                   MOVE PE-MSG-02     TO PV-OPERATION-ATTEMPTED         03720000
                   PERFORM 9999-SQL-ABEND                               03730000
           END-EVALUATE.                                                03740000
W27808                                                                  02190000
      ***************************************************************** 03300000
      * UPDATE THE INVENTORY CUTOFF TABLE ROW CUTOFF ONHAND QTY.        03310000
      ***************************************************************** 03330000
       7000-UPDATE-INV-CTOFF-ONHAND.                                    03350000
                                                                        03360000
           MOVE IN051-QUANTITY TO INT00000-CTOFF-OH-QTY.                03720000
                                                                        03360000
           EXEC SQL                                                     03400000
               UPDATE INT_INV_CTOFF                                     03410000
                 SET CTOFF_OH_QTY        = CTOFF_OH_QTY                 03421000
                                         + :INT00000-CTOFF-OH-QTY       03421000
                   , LAST_UPD_DTE        = :INT00000-LAST-UPD-DTE       03424000
                   , LAST_UPD_BY_USR_ID  = :INT00000-LAST-UPD-BY-USR-ID 03425000
               WHERE LOC_NBR             = :INT00000-LOC-NBR            03520000
                 AND SKU_NBR             = :INT00000-SKU-NBR            03530000
           END-EXEC                                                     03560000
                                                                        03570000
           EVALUATE SQLCODE                                             03580000
               WHEN 0                                                   03590000
                   ADD 1 TO PA-UPDATE-COUNT                             03591000
               WHEN +100                                                03590000
                   MOVE ZERO           TO INT00000-IN-TRNST-CTOFF-QTY   03720000
                   MOVE ZERO           TO INT00000-POST-FSH-RCPT-QTY    03550000
W27808             MOVE ZERO           TO INT00000-STR-EXPTED-OH-QTY            
                   PERFORM 7200-INSERT-INV-CTOFF-ROW                    03730000
               WHEN -911                                                03610000
                   ADD +1             TO PV-DEADLOCK-COUNT              03620000
                   DISPLAY                                              03621000
                        'DEADLOCK -911 IN 7000-UPDATE-INV-CTOFF-ONHAND' 03621000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03630000
                       MOVE '7000-UPDATE-INV-CTOFF-ONHAND'              03640000
                                      TO PV-PARAGRAPH-NAME              03650000
                       MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED         03651000
                       PERFORM 9000-DEADLOCK-ERROR                      03660000
                   ELSE                                                 03670000
                       PERFORM 7999-RESTART-PROCESS                     03680000
                   END-IF                                               03690000
               WHEN OTHER                                               03700000
                   MOVE '7000-UPDATE-INV-CTOFF-ONHAND'                  03640000
                                      TO PV-PARAGRAPH-NAME              03650000
                   MOVE PE-MSG-02     TO PV-OPERATION-ATTEMPTED         03720000
                   PERFORM 9999-SQL-ABEND                               03730000
           END-EVALUATE.                                                03740000
                                                                        03750000
      ***************************************************************** 03300000
      * UPDATE THE INVENTORY CUTOFF TABLE ROW POST FLUSH RECEIPT QTY.   03310000
      ***************************************************************** 03330000
       7100-UPDATE-INV-POST-FLUSH.                                      03350000
                                                                        03360000
           MOVE IN051-QUANTITY TO INT00000-POST-FSH-RCPT-QTY.           03720000
                                                                        03360000
           EXEC SQL                                                     03400000
               UPDATE INT_INV_CTOFF                                     03410000
                 SET POST_FSH_RCPT_QTY   = POST_FSH_RCPT_QTY            03423000
                                         + :INT00000-POST-FSH-RCPT-QTY  03423000
                   , LAST_UPD_DTE        = :INT00000-LAST-UPD-DTE       03424000
                   , LAST_UPD_BY_USR_ID  = :INT00000-LAST-UPD-BY-USR-ID 03425000
               WHERE LOC_NBR             = :INT00000-LOC-NBR            03520000
                 AND SKU_NBR             = :INT00000-SKU-NBR            03530000
           END-EXEC                                                     03560000
                                                                        03570000
           EVALUATE SQLCODE                                             03580000
               WHEN 0                                                   03590000
                   ADD 1 TO PA-UPDATE-COUNT                             03591000
               WHEN +100                                                03590000
                   MOVE ZERO           TO INT00000-CTOFF-OH-QTY         03720000
                   MOVE ZERO           TO INT00000-IN-TRNST-CTOFF-QTY   03720000
W27808             MOVE ZERO           TO INT00000-STR-EXPTED-OH-QTY            
                   PERFORM 7200-INSERT-INV-CTOFF-ROW                    03730000
               WHEN -911                                                03610000
                   ADD +1             TO PV-DEADLOCK-COUNT              03620000
                   DISPLAY 'DEADLOCK -911 IN 7100-UPDATE-INV-POST-FLUSH'03621000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03630000
                       MOVE '7100-UPDATE-INV-POST-FLUSH'                03640000
                                      TO PV-PARAGRAPH-NAME              03650000
                       MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED         03651000
                       PERFORM 9000-DEADLOCK-ERROR                      03660000
                   ELSE                                                 03670000
                       PERFORM 7999-RESTART-PROCESS                     03680000
                   END-IF                                               03690000
               WHEN OTHER                                               03700000
                   MOVE '7100-UPDATE-INV-POST-FLUSH'                    03640000
                                      TO PV-PARAGRAPH-NAME              03650000
                   MOVE PE-MSG-02     TO PV-OPERATION-ATTEMPTED         03720000
                   PERFORM 9999-SQL-ABEND                               03730000
           END-EVALUATE.                                                03740000
                                                                        03750000
      ***************************************************************** 03300000
      * INSERT AN INVENTORY CUTOFF TABLE ROW WITH NEW COUNTS.           03310000
      ***************************************************************** 03330000
       7200-INSERT-INV-CTOFF-ROW.                                       03350000
                                                                        03360000
           EXEC SQL                                                     03400000
               INSERT INTO INT_INV_CTOFF                                        
                      (LOC_NBR,                                         03520000
                       SKU_NBR,                                         03530000
                       CTOFF_OH_QTY,                                    03421000
                       IN_TRNST_CTOFF_QTY,                              03422000
                       POST_FSH_RCPT_QTY,                               03423000
                       LAST_UPD_DTE,                                    03424000
                       LAST_UPD_BY_USR_ID,                              03425000
W27808                 STR_EXPTED_OH_QTY)                               03425000
               VALUES (:INT00000-LOC-NBR,                                       
                       :INT00000-SKU-NBR,                                       
                       :INT00000-CTOFF-OH-QTY,                                  
                       :INT00000-IN-TRNST-CTOFF-QTY,                            
                       :INT00000-POST-FSH-RCPT-QTY,                             
                       :INT00000-LAST-UPD-DTE,                                  
                       :INT00000-LAST-UPD-BY-USR-ID,                            
W27808                 :INT00000-STR-EXPTED-OH-QTY)                             
           END-EXEC                                                     03560000
                                                                        03570000
           EVALUATE SQLCODE                                             03580000
               WHEN 0                                                   03590000
                   ADD 1 TO PA-INSERT-COUNT                             03591000
               WHEN -911                                                03610000
                   ADD +1             TO PV-DEADLOCK-COUNT              03620000
                   DISPLAY 'DEADLOCK -911 IN 7200-INSERT-INV-CTOFF-ROW' 03621000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03630000
                       MOVE '7200-INSERT-INV-CTOFF-ROW'                 03640000
                                      TO PV-PARAGRAPH-NAME              03650000
                       MOVE PE-MSG-07 TO PV-OPERATION-ATTEMPTED         03720000
                       PERFORM 9000-DEADLOCK-ERROR                      03660000
                   ELSE                                                 03670000
                       PERFORM 7999-RESTART-PROCESS                     03680000
                   END-IF                                               03690000
               WHEN OTHER                                               03700000
                   DISPLAY '**  INT_INV_CTOFF INSERT FAILED FOR LOC: '  03621000
                         INT00000-LOC-NBR '  STORE: ' INT00000-SKU-NBR          
                   MOVE '7200-INSERT-INV-CTOFF-ROW'                     03640000
                                      TO PV-PARAGRAPH-NAME              03650000
                   MOVE PE-MSG-07     TO PV-OPERATION-ATTEMPTED         03720000
                   PERFORM 9999-SQL-ABEND                               03730000
           END-EVALUATE.                                                03740000
                                                                        03750000
      ******************************************************************04630000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *04640000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *04650000
      *            CONTROL TABLE                                       *04660000
      ******************************************************************04670000
       7300-GET-COMMIT-TIMESTAMP.                                       04680000
                                                                        04690000
      * CHECKPOINT TAKEN BASED ON DB2 CHECKPOINT RESTART TABLE          04691000
           EXEC SQL                                                     04700000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       04720000
               INTO :PV-COMMIT-TIMESTAMP                                04730000
               FROM TCKRSTCNTL                                          04740000
              WHERE JOB_NAME  = :PV-PROCESS-JOB-NAME                    04750000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        04760000
           END-EXEC.                                                    04770000
                                                                        04780000
           EVALUATE TRUE                                                04790000
               WHEN SQLCODE = 0                                         04800000
                   CONTINUE                                             04820000
               WHEN SQLCODE NOT EQUAL ZERO                              04830000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME04840000
                   MOVE 'SELECT TMSTMP FROM TCKRSTCNTL'                 04841000
                                      TO PV-OPERATION-ATTEMPTED         04842000
                   PERFORM 9999-SQL-ABEND                               04850000
           END-EVALUATE.                                                04860000
                                                                        04870000
      ******************************************************************04890000
      * 7400-INIT-CHECKPOINT-SELECT                                    *04900000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *04910000
      *            IF WE ARE IN A RESTART CONDITION.                   *04920000
      ******************************************************************04930000
       7400-INIT-CHECKPOINT-SELECT.                                     04940000
                                                                        04950000
           EXEC SQL                                                     04960000
             SELECT  CKPT_STAT_CODE                                     04970000
                    ,CKPT_FRQNCY_QTY                                    04980000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         04990000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05000000
               FROM  TCKRSTCNTL                                         05010000
              WHERE  JOB_NAME  = :PV-PROCESS-JOB-NAME                   05020000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05030000
           END-EXEC.                                                    05040000
                                                                        05050000
           IF SQLCODE = 0                                               05060000
               CONTINUE                                                 05070000
           ELSE                                                         05080000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05090000
               MOVE PE-MSG-03             TO PV-OPERATION-ATTEMPTED     05100000
               PERFORM 9999-SQL-ABEND                                   05110000
           END-IF.                                                      05120000
                                                                        05140000
      ******************************************************************05150000
      * 7500-SELECT-RESTART-INFO                                       *05160000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05170000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05180000
      ******************************************************************05190000
       7500-SELECT-RESTART-INFO.                                        05200000
                                                                        05210000
           EXEC SQL                                                     05220000
             SELECT  WORK_STRG_DESC                                     05230000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05240000
               FROM  TCKRSTINF                                          05250000
              WHERE  JOB_NAME  = :PV-PROCESS-JOB-NAME                   05260000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05270000
           END-EXEC.                                                    05280000
                                                                        05290000
           IF SQLCODE = 0                                               05300000
               CONTINUE                                                 05310000
           ELSE                                                         05320000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   05330000
               MOVE 'SELECT  FROM TCKRSTINF'                            05331000
                                      TO PV-OPERATION-ATTEMPTED         05332000
               PERFORM 9999-SQL-ABEND                                   05340000
           END-IF.                                                      05350000
                                                                        05360000
      ******************************************************************05370000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *05380000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *05390000
      ******************************************************************05400000
       7600-SET-CURRENT-TIMESTAMP.                                      05410000
                                                                        05420000
           EXEC SQL                                                     05430000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           05440000
           END-EXEC.                                                    05450000
           IF SQLCODE = 0                                               05490000
               CONTINUE                                                 05500000
           ELSE                                                         05510000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 05520000
               MOVE 'SET TIMESTAMP'                                     05521000
                                      TO PV-OPERATION-ATTEMPTED         05522000
               PERFORM 9999-SQL-ABEND                                   05530000
           END-IF.                                                      05540000
                                                                        05560000
      ******************************************************************05570000
      * 7700-COMMIT-PROCESSING.                                        *05580000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *05590000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*05600000
      ******************************************************************05610000
       7700-COMMIT-PROCESSING.                                          05620000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     05630000
                                                                        05640000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          05650000
                                                                        05660000
      * PREPARE COMMIT RECORD FOR UPDATE                                05670000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                05680000
               MOVE IN051-LOC-NBR             TO CR-LOC-NBR             05720000
               MOVE IN051-SKU-NBR             TO CR-SKU-NBR             05721000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    05730000
               MOVE PA-UPDATE-COUNT           TO CR-UPDATE-COUNT        05730000
               MOVE PA-INSERT-COUNT           TO CR-INSERT-COUNT        05730000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  05740000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       05750000
                                             TCKRSTINF-WORK-STRG-DESC   05760000
               IF PS-FIRST-COMMIT                                       05770000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                05780000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                05790000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       05800000
               END-IF                                                   05810000
               PERFORM 7800-COMMIT-CHANGES                              05820000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        05830000
           END-IF.                                                      05840000
                                                                        05850000
      ******************************************************************05870000
      * 7800-COMMIT-CHANGES.                                            05880000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      05890000
      *            TAKE A COMMIT.                                       05900000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    05910000
      ******************************************************************05920000
       7800-COMMIT-CHANGES.                                             05930000
                                                                        05940000
           EXEC SQL                                                     05950000
             UPDATE TCKRSTINF                                           05960000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          05970000
              WHERE JOB_NAME       = :PV-PROCESS-JOB-NAME               05980000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   05990000
           END-EXEC.                                                    06000000
                                                                        06010000
           IF SQLCODE = 0                                               06020000
               CONTINUE                                                 06150000
           ELSE                                                         06040000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06050000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06060000
               PERFORM 9999-SQL-ABEND                                   06070000
           END-IF.                                                      06080000
                                                                        06090000
           EXEC SQL                                                     06100000
               COMMIT                                                   06110000
           END-EXEC.                                                    06120000
                                                                        06130000
           IF SQLCODE = 0                                               06140000
               ADD +1 TO PA-COMMIT-COUNT                                06030000
           ELSE                                                         06160000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06170000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06180000
               PERFORM 9999-SQL-ABEND                                   06190000
           END-IF.                                                      06200000
                                                                        06220000
      ******************************************************************06230000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06240000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06250000
      *                                                                 06260000
      ******************************************************************06270000
       7900-UPDATE-CHECKPOINT-STATUS.                                   06280000
                                                                        06290000
           EXEC SQL                                                     06300000
             UPDATE  TCKRSTCNTL                                         06310000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        06320000
              WHERE  JOB_NAME  = :PV-PROCESS-JOB-NAME                   06330000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06340000
           END-EXEC.                                                    06350000
                                                                        06360000
           IF SQLCODE = 0                                               06370000
               CONTINUE                                                 06380000
           ELSE                                                         06390000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME06400000
               PERFORM 9999-SQL-ABEND                                   06410000
           END-IF.                                                      06420000
                                                                        06430000
           EJECT                                                        06440000
                                                                        06460000
      ******************************************************************06470000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TINSCL ROW FOR UPDATE 06480000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  06490000
      ******************************************************************06500000
       7999-RESTART-PROCESS.                                            06510000
                                                                        06520000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           06530000
                                                                        06540000
           CLOSE CUTOFF-ADJUSTS-FILE.                                   06550000
                                                                        06560000
           PERFORM 7500-SELECT-RESTART-INFO.                            06570000
                                                                        06580000
           DISPLAY '**** RESTART **** '.                                06590000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 06600000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   06610000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      06610100
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        06610200
      *-- RESTART INFORMATION IN NOT CLEARED OUT.                       06610300
           IF PS-FIRST-COMMIT                                           06611000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               06612000
               MOVE ZERO TO PA-UPDATE-COUNT                             01970000
               MOVE ZERO TO PA-INSERT-COUNT                             01970000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     06613000
                      ' BEGINNING'                                      06614000
           ELSE                                                         06615000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           06620000
                        TCKRSTINF-WORK-STRG-DESC (1:30)                 06621000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 06650000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         06651000
                    CR-CHECKPOINT-RESTART-AREA                          06651100
               MOVE CR-UPDATE-COUNT          TO PA-UPDATE-COUNT         01970000
               MOVE CR-INSERT-COUNT          TO PA-INSERT-COUNT         01970000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                06651200
               DISPLAY 'LOCATION NUMBER:   ' CR-LOC-NBR                 06720800
               DISPLAY 'SKU NUMBER:        ' CR-SKU-NBR                 06720900
               DISPLAY 'INPUT RECS READ:   ' CR-INPUT-READ-COUNT        06652000
               DISPLAY 'INSERTS PROCESSED: ' CR-INSERT-COUNT            06652000
               DISPLAY 'UPDATES PROCESSED: ' CR-UPDATE-COUNT            06652000
           END-IF.                                                      06661000
                                                                        06662000
           OPEN INPUT CUTOFF-ADJUSTS-FILE.                              06670000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      06680000
                                                                        06690000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          06700000
           PERFORM 4100-READ-CUTOFF-ADJUSTS-FILE                        06710000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               06720000
               OR PS-EOF-CUTOFF-ADJUSTS-FILE.                           06720100
                                                                        06720200
           IF PS-EOF-CUTOFF-ADJUSTS-FILE                                06720300
               DISPLAY 'END OF INPUT FILE ON RESTART'                   06720400
           ELSE                                                         06720500
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              06720600
                        PA-INPUT-COUNT                                  06720700
               DISPLAY 'LOCATION NUMBER: ' IN051-LOC-NBR                06720800
               DISPLAY 'SKU NUMBER:      ' IN051-SKU-NBR                06720900
           END-IF.                                                      06723000
                                                                        06730000
      ******************************************************************06740000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *06750000
      ******************************************************************06760000
       8000-EOJ-ROUTINE.                                                06770000
                                                                        06780000
           PERFORM 7800-COMMIT-CHANGES.                                         
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       06790000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       06800000
                                                                        06810000
           DISPLAY '**********************************'.                06840000
           DISPLAY '******* INKUP009 RUN STATS *******'.                06840000
           DISPLAY '**********************************'.                06840000
           DISPLAY 'INPUT RECORDS PROCESSED: ' PA-INPUT-COUNT.          06830000
           DISPLAY 'INSERTS PROCESSED:       ' PA-INSERT-COUNT.         06840000
           DISPLAY 'UPDATES PROCESSED:       ' PA-UPDATE-COUNT.         06840000
           DISPLAY 'COMMITS TAKEN:           ' PA-COMMIT-COUNT.         06840000
                                                                        06850000
           CLOSE CUTOFF-ADJUSTS-FILE.                                   06861000
                                                                        06870000
      ******************************************************************06890000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   06910000
      ******************************************************************06920000
       9000-DEADLOCK-ERROR.                                             06930000
                                                                        06940000
               MOVE PE-MSG-06             TO PV-OPERATION-ATTEMPTED     06950000
               PERFORM 9999-SQL-ABEND.                                  06960000
                                                                        06970000
      ******************************************************************06980000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               06990000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07000000
      ******************************************************************07010000
       9999-SQL-ABEND.                                                  07020000
                                                                        07030000
           MOVE +4013                 TO ABEND-CODE.                    07040000
           DISPLAY '**  ABEND     **'.                                  07050000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              07060000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07070000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       07080000
                                                                        07090000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         07100000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        07110000
                                                                        07120000
           COPY DPPD004.                                                07130000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           07140000
