      * ------------------------------------------------------------- *         
       IDENTIFICATION DIVISION.                                                 
      * ------------------------------------------------------------- *         
       PROGRAM-ID.      DPKUT069.                                               
       AUTHOR.          LYNN KELLING.                                           
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    03-2002.                                                
                                                                                
      *****************************************************************         
      *  THIS PROGRAM WILL PUT A MESSAGE ONTO THE SPECIFIED QUEUE.    *         
      *  THIS MESSAGE IS RECOGNIZED BY THE APPLICATION.               *         
      *  THE APPLICATION WILL THEN END ITS EXECUTION.                 *         
      *                                                               *         
      *****************************************************************         
      * ------------------------------------------------------------- *         
       ENVIRONMENT DIVISION.                                                    
      * ------------------------------------------------------------- *         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.       IBM-3090.                                         
       OBJECT-COMPUTER.       IBM-3090.                                         
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT CONTROL-CARD-1                                                
                  ASSIGN TO INP01.                                              
           SELECT CONTROL-CARD-2                                                
                  ASSIGN TO INP02.                                              
           SELECT CONTROL-CARD-3                                                
                  ASSIGN TO INP03.                                              
           SELECT CONTROL-CARD-4                                                
                  ASSIGN TO INP04.                                              
                                                                                
      * ------------------------------------------------------------- *         
       DATA DIVISION.                                                           
      * ------------------------------------------------------------- *         
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-1                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-1-LINE              PIC  X(80).                         
                                                                                
       FD  CONTROL-CARD-2                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-2-LINE              PIC  X(80).                         
                                                                                
       FD  CONTROL-CARD-3                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-3-LINE              PIC  X(80).                         
                                                                                
       FD  CONTROL-CARD-4                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-4-LINE              PIC  X(80).                         
                                                                                
      * ------------------------------------------------------------- *         
       WORKING-STORAGE SECTION.                                                 
      * ------------------------------------------------------------- *         
                                                                                
       01  ABEND-CODE              PIC S9(4)  VALUE ZEROS COMP SYNC.            
           88  AC-MQ-ERROR                    VALUE +4020.                      
                                                                                
      * ------------------------------------------------------------- *         
      *   PROGRAM VARIABLES                                                     
      * ------------------------------------------------------------- *         
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH             PIC X(30)     VALUE SPACES.     01415399
           05  PV-QM-NAME               PIC X(48).                              
           05  PV-MSGBUFFER.                                                    
               10  PV-MSGBUFFER-ARRAY   PIC X(1) OCCURS 4096 TIMES.             
           05  PV-MSGLENGTH             PIC S9(9) BINARY VALUE 50.              
           05  PV-NUMPUTS               PIC S9(9) BINARY VALUE 0.               
           05  PV-ERROR-MESSAGE         PIC X(48) VALUE SPACES.                 
           05  PV-HCONN                 PIC S9(9) BINARY VALUE 0.               
           05  PV-PQ-HANDLE             PIC S9(9) BINARY VALUE 0.               
           05  PV-OPEN-OPTIONS          PIC S9(9) BINARY.                       
           05  PV-COMPLETION-CODE       PIC S9(9) BINARY.                       
           05  PV-REASON                PIC S9(9) BINARY.                       
           05  PV-CON-REASON            PIC S9(9) BINARY.                       
                                                                                
      * ------------------------------------------------------------- *         
      *   WORKING STORAGE VARIABLES                                             
      * ------------------------------------------------------------- *         
                                                                                
       01  WS-WORKING-STORAGE-VARIABLES.                                        
           05  WS-MQCONN                  PIC X(8)  VALUE 'CSQBCONN'.           
           05  WS-MQOPEN                  PIC X(8)  VALUE 'CSQBOPEN'.           
           05  WS-MQPUT                   PIC X(8)  VALUE 'CSQBPUT'.            
           05  WS-MQCLOSE                 PIC X(8)  VALUE 'CSQBCLOS'.           
           05  WS-MQDISC                  PIC X(8)  VALUE 'CSQBDISC'.           
                                                                                
      *    API control blocks                                                   
                                                                                
       01  MQM-OBJECT-DESCRIPTOR.                                               
           COPY CMQODV.                                                         
       01  MQM-MESSAGE-DESCRIPTOR.                                              
           COPY CMQMDV.                                                         
       01  MQM-PUT-MESSAGE-OPTIONS.                                             
           COPY CMQPMOV.                                                        
      *                                                                         
      *    MQV contains constants (for filling in the control blocks)           
      *    and return codes (for testing the result of a call)                  
      *                                                                         
       01  MQM-CONSTANTS.                                                       
           COPY CMQV.                                                           
      *                                                                         
      *----------------------------------------------------------------*        
      *    CONTROL CARD                                                         
      *----------------------------------------------------------------*        
       01  CC-CONTROL-CARD-1.                                                   
           05  CC-CONN-QMGR              PIC X(48).                             
           05  FILLER                    PIC X(32).                             
                                                                                
       01  CC-CONTROL-CARD-2.                                                   
           05  CC-DEST-QMGR              PIC X(48).                             
           05  FILLER                    PIC X(32).                             
                                                                                
       01  CC-CONTROL-CARD-3.                                                   
           05  CC-QUEUE-NAME             PIC X(48).                             
           05  FILLER                    PIC X(32).                             
                                                                                
       01  CC-CONTROL-CARD-4.                                                   
           05  CC-PRIORITY               PIC X(01).                             
           05  FILLER                    PIC X(79).                             
                                                                                
      *----------------------------------------------------------------*        
      *    PROGRAM CONSTANTS                                                    
      *----------------------------------------------------------------*        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME               PIC X(09) VALUE 'DPKUT069 '.           
           05  PC-FIFTY-STARS            PIC X(50) VALUE ALL '*'.               
           05  PC-MSG-EXPIRY             PIC S9(9) VALUE 36000.                 
           05  PC-MSG-TEXT               PIC X(100)                             
                                         VALUE 'END RUN OF PROGRAM'.            
           05  PC-CORRELID-ASCII         PIC X(24)                              
                                  VALUE 'ê&.•€                '.             
                                                                                
      *----------------------------------------------------------------*        
      *    PROGRAM SWITCHES                                                     
      *----------------------------------------------------------------*        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTROL-CARD-1-SW      PIC X(01) VALUE 'N'.                   
               88  PS-CONTROL-CARD-1-END           VALUE 'Y'.                   
               88  PS-CONTROL-CARD-1-START         VALUE 'N'.                   
           05  PS-CONTROL-CARD-2-SW      PIC X(01) VALUE 'N'.                   
               88  PS-CONTROL-CARD-2-END           VALUE 'Y'.                   
               88  PS-CONTROL-CARD-2-START         VALUE 'N'.                   
           05  PS-CONTROL-CARD-3-SW      PIC X(01) VALUE 'N'.                   
               88  PS-CONTROL-CARD-3-END           VALUE 'Y'.                   
               88  PS-CONTROL-CARD-3-START         VALUE 'N'.                   
           05  PS-CONTROL-CARD-4-SW      PIC X(01) VALUE 'N'.                   
               88  PS-CONTROL-CARD-4-END           VALUE 'Y'.                   
               88  PS-CONTROL-CARD-4-START         VALUE 'N'.                   
                                                                                
      * ------------------------------------------------------------- *         
       PROCEDURE DIVISION.                                                      
      * ------------------------------------------------------------- *         
       1000-MAIN SECTION.                                                       
                                                                                
      * ------------------------------------------------------------- *         
      *  READ CONTROL CARD TO GET CONNECTION QUEUE MANAGER NAME       *         
      * ------------------------------------------------------------- *         
            SET PS-CONTROL-CARD-1-START TO TRUE.                                
            OPEN INPUT CONTROL-CARD-1.                                          
            READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1                          
                AT END                                                          
                    SET PS-CONTROL-CARD-1-END                                   
                                        TO TRUE.                                
                                                                                
            IF PS-CONTROL-CARD-1-END                                            
                DISPLAY PC-PGM-NAME                                             
                    'CONTROL CARD MISSING - FOR CONNECTION QMGR NAME'           
                GO TO   4000-END-PROGRAM                                        
            ELSE                                                                
                DISPLAY PC-PGM-NAME                                             
                        '- '                                                    
                        PC-FIFTY-STARS                                          
                DISPLAY PC-PGM-NAME                                             
                        '- CONTROL CARD: '                                      
                        CC-CONTROL-CARD-1                                       
            END-IF.                                                             
                                                                                
            CLOSE CONTROL-CARD-1.                                               
                                                                                
      * ------------------------------------------------------------- *         
      *  READ CONTROL CARD TO GET DESTINATION QUEUE MANAGER NAME      *         
      * ------------------------------------------------------------- *         
            SET PS-CONTROL-CARD-2-START TO TRUE.                                
            OPEN INPUT CONTROL-CARD-2.                                          
            READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                          
                AT END                                                          
                    SET PS-CONTROL-CARD-2-END                                   
                                        TO TRUE.                                
                                                                                
            IF PS-CONTROL-CARD-2-END                                            
                DISPLAY PC-PGM-NAME                                             
                    'CONTROL CARD MISSING - FOR DESTINATION QMGR NAME'          
                GO TO   4000-END-PROGRAM                                        
            ELSE                                                                
                DISPLAY PC-PGM-NAME                                             
                        '- '                                                    
                        PC-FIFTY-STARS                                          
                DISPLAY PC-PGM-NAME                                             
                        '- CONTROL CARD: '                                      
                        CC-CONTROL-CARD-2                                       
            END-IF.                                                             
                                                                                
            CLOSE CONTROL-CARD-2.                                               
                                                                                
      * ------------------------------------------------------------- *         
      *  READ CONTROL CARD TO GET QUEUE NAME                          *         
      * ------------------------------------------------------------- *         
                                                                                
            SET PS-CONTROL-CARD-3-START TO TRUE.                                
            OPEN INPUT CONTROL-CARD-3.                                          
            READ CONTROL-CARD-3 INTO CC-CONTROL-CARD-3                          
                AT END                                                          
                    SET PS-CONTROL-CARD-3-END                                   
                                        TO TRUE.                                
                                                                                
            IF PS-CONTROL-CARD-3-END                                            
                DISPLAY PC-PGM-NAME                                             
                   'CONTROL CARD MISSING - FOR QUEUE NAME'                      
                GO TO 4000-END-PROGRAM                                          
            ELSE                                                                
                DISPLAY PC-PGM-NAME                                             
                        '- '                                                    
                        PC-FIFTY-STARS                                          
                DISPLAY PC-PGM-NAME                                             
                        '- CONTROL CARD: '                                      
                        CC-CONTROL-CARD-3                                       
            END-IF.                                                             
                                                                                
            CLOSE CONTROL-CARD-3.                                               
                                                                                
      * ------------------------------------------------------------- *         
      *  READ CONTROL CARD TO GET PRIORITY                            *         
      * ------------------------------------------------------------- *         
                                                                                
            SET PS-CONTROL-CARD-4-START TO TRUE.                                
            OPEN INPUT CONTROL-CARD-4.                                          
            READ CONTROL-CARD-4 INTO CC-CONTROL-CARD-4                          
                AT END                                                          
                    SET PS-CONTROL-CARD-4-END                                   
                                        TO TRUE.                                
                                                                                
           IF PS-CONTROL-CARD-4-END                                             
               DISPLAY PC-PGM-NAME                                              
                  'CONTROL CARD MISSING - FOR PRIORITY'                         
               GO TO 4000-END-PROGRAM                                           
           ELSE                                                                 
               DISPLAY PC-PGM-NAME                                              
                       '- '                                                     
                       PC-FIFTY-STARS                                           
               DISPLAY PC-PGM-NAME                                              
                       '- CONTROL CARD: '                                       
                       CC-CONTROL-CARD-4                                        
           END-IF.                                                              
                                                                                
           CLOSE CONTROL-CARD-4.                                                
                                                                                
           PERFORM 1200-CONNECT-QUEUE-MANAGER.                                  
                                                                                
           PERFORM 1500-OPEN-QUEUE.                                             
                                                                                
           PERFORM 2000-MQPUT-MSG.                                              
                                                                                
           PERFORM 3000-CLOSE-QUEUE.                                            
                                                                                
           PERFORM 3500-DISCONNECT-QMGR.                                        
                                                                                
           GO TO 4000-END-PROGRAM.                                              
                                                                                
      * ------------------------------------------------------------- *         
      *    Connect to the queue manager                                         
      * ------------------------------------------------------------- *         
                                                                                
       1200-CONNECT-QUEUE-MANAGER.                                              
                                                                                
           MOVE CC-CONN-QMGR TO PV-QM-NAME.                                     
           CALL WS-MQCONN USING PV-QM-NAME                                      
                                PV-HCONN                                        
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
                                                                                
      *    If connection failed then display error message                      
      *    and exit                                                             
                                                                                
           MOVE PV-REASON TO PV-CON-REASON.                                     
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
              MOVE 'MQCONN ERROR'               TO PV-ERROR-MESSAGE             
              MOVE '1000-CONNECT-QUEUE-MANAGER' TO PV-PARAGRAPH                 
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
              GO TO 4000-END-PROGRAM                                            
           END-IF.                                                              
           DISPLAY 'MQCONN SUCCESSFUL TO ', PV-QM-NAME.                         
                                                                                
      * 1200-CONNECT-QUEUE-MANAGER.                                             
                                                                                
      * ------------------------------------------------------------- *         
      *    Open the queue for output                                            
      * ------------------------------------------------------------- *         
                                                                                
       1500-OPEN-QUEUE.                                                         
                                                                                
           COMPUTE PV-OPEN-OPTIONS = MQOO-OUTPUT +                              
                                  MQOO-FAIL-IF-QUIESCING +                      
                                  MQOO-SET-IDENTITY-CONTEXT.                    
           MOVE CC-QUEUE-NAME     TO MQOD-OBJECTNAME.                           
           MOVE CC-DEST-QMGR      TO MQOD-OBJECTQMGRNAME.                       
      *                                                                         
           CALL WS-MQOPEN USING PV-HCONN                                        
                               MQM-OBJECT-DESCRIPTOR                            
                               PV-OPEN-OPTIONS                                  
                               PV-PQ-HANDLE                                     
                               PV-COMPLETION-CODE                               
                               PV-REASON.                                       
      *                                                                         
      *    If open failed then display error message                            
      *    and exit.                                                            
      *                                                                         
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
              MOVE 'MQOPEN ERROR'    TO PV-ERROR-MESSAGE                        
              MOVE '1000-OPEN-QUEUE' TO PV-PARAGRAPH                            
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
              PERFORM 3500-DISCONNECT-QMGR                                      
              GO TO   4000-END-PROGRAM                                          
           END-IF.                                                              
           DISPLAY 'MQOPEN SUCCESSFUL'.                                         
                                                                                
      * 1500-END-OPEN-QUEUE.                                                    
                                                                                
      * ------------------------------------------------------------- *         
      * PUT MESSAGE ON LOCAL QUEUE TO END THE DATA COLLECT PROCESS              
      * ------------------------------------------------------------- *         
       2000-MQPUT-MSG.                                                          
      *                                                                         
      *    Set persistence                                                      
      *                                                                         
           MOVE MQMI-NONE         TO MQMD-MSGID.                                
           MOVE MQCI-NONE         TO MQMD-CORRELID.                             
      *    MOVE PC-CORRELID-ASCII TO MQMD-CORRELID.                             
           MOVE PC-MSG-TEXT       TO PV-MSGBUFFER.                              
      *    MOVE PC-PGM-NAME       TO MQMD-USERIDENTIFIER.                       
           MOVE MQPER-PERSISTENT  TO MQMD-PERSISTENCE.                          
           MOVE CC-PRIORITY       TO MQMD-PRIORITY.                             
           MOVE MQFMT-STRING      TO MQMD-FORMAT.                               
           COMPUTE MQPMO-OPTIONS  = MQPMO-SYNCPOINT +                           
                                    MQPMO-SET-IDENTITY-CONTEXT.                 
      *                                                                         
           CALL WS-MQPUT USING PV-HCONN                                         
                               PV-PQ-HANDLE                                     
                               MQM-MESSAGE-DESCRIPTOR                           
                               MQM-PUT-MESSAGE-OPTIONS                          
                               PV-MSGLENGTH                                     
                               PV-MSGBUFFER                                     
                               PV-COMPLETION-CODE                               
                               PV-REASON                                        
      *                                                                         
      *        If put failed then display error message                         
      *        and break out of loop                                            
      *                                                                         
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
              MOVE 'MQPUT ERROR'            TO PV-ERROR-MESSAGE                 
              MOVE '2000-MQPUT-MSG' TO PV-PARAGRAPH                             
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
           ELSE                                                                 
              ADD 1 TO PV-NUMPUTS                                               
           END-IF.                                                              
                                                                                
      * 2000-END-MQPUT-MSG.                                                     
                                                                                
      * ------------------------------------------------------------- *         
      * CLOSE THE QUEUE                                                         
      * ------------------------------------------------------------- *         
                                                                                
       3000-CLOSE-QUEUE.                                                        
           CALL WS-MQCLOSE USING PV-HCONN                                       
                                PV-PQ-HANDLE                                    
                                MQCO-NONE                                       
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
              MOVE 'MQCLOSE ERROR'     TO PV-ERROR-MESSAGE                      
              MOVE '3000-CLOSE-QUEUE ' TO PV-PARAGRAPH                          
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
           ELSE                                                                 
              DISPLAY 'MQCLOSE SUCCESSFUL'                                      
           END-IF.                                                              
      * 3000-CLOSE-QUEUE.                                                       
                                                                                
      * ------------------------------------------------------------- *         
      * DISCONNECT FROM THE MQ QUEUE MANAGER                                    
      * ------------------------------------------------------------- *         
       3500-DISCONNECT-QMGR.                                                    
                                                                                
           IF PV-CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED              
              CALL WS-MQDISC USING PV-HCONN                                     
                                   PV-COMPLETION-CODE                           
                                   PV-REASON                                    
              IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                        
                 MOVE 'MQDISC ERROR'         TO PV-ERROR-MESSAGE                
                 MOVE '3500-DISCONNECT-QMGR' TO PV-PARAGRAPH                    
                 PERFORM 9000-DISPLAY-ERROR-MESSAGE                             
              ELSE                                                              
                 DISPLAY 'MQDISC SUCCESSFUL'                                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
      * 3500-END-DISCONNECT.                                                    
      * ------------------------------------------------------------- *         
      * END THE PROGRAM                                                         
      * ------------------------------------------------------------- *         
       4000-END-PROGRAM.                                                        
      *                                                                         
      *    Display the number of messages successfully put                      
      *    to the queue                                                         
      *                                                                         
           DISPLAY PV-NUMPUTS, ' MESSAGES PUT TO QUEUE'.                        
      *                                                                         
           STOP RUN.                                                            
      *                                                                         
      * ------------------------------------------------------------- *         
      * DISPLAY THE VALUES OF A FAILED MQ API COMMAND                           
      * ------------------------------------------------------------- *         
       9000-DISPLAY-ERROR-MESSAGE.                                              
      * ------------------------------------------------------------- *         
                                                                                
           DISPLAY '** ABEND ***************************************'.          
           DISPLAY '** PROGRAM:   ', PC-PGM-NAME.                               
           DISPLAY '** PARAGRAPH: ', PV-PARAGRAPH.                              
           DISPLAY '** MESSAGE:   ', PV-ERROR-MESSAGE.                          
           DISPLAY '** COMP CODE: ', PV-COMPLETION-CODE.                        
           DISPLAY '** RSN CODE:  ', PV-REASON.                                 
           DISPLAY '************************************************'.          
           SET AC-MQ-ERROR TO TRUE.                                     04354099
           CALL 'ILBOABN0' USING ABEND-CODE.                            04355099
                                                                                
      * 9000-DISPLAY-ERROR-MSG-END.                                             
