000100***************************************************************** 00010001
000200 IDENTIFICATION DIVISION.                                         00020001
000300***************************************************************** 00030001
000400 PROGRAM-ID.    XLKUT064.                                         00040001
000500 AUTHOR.        DEANNA BRANTNER.                                  00050001
000600 INSTALLATION.  KOHLS.                                            00060001
000700 DATE-WRITTEN   MAY, 2003.                                                
000800 DATE-COMPILED.                                                   00080001
000900***************************************************************** 00090001
001000*  THIS PROGRAM WILL USE THE COMBINE FILE FROM XLKUT038 AND     * 00100001
001100*  TPOPRPK, TO CREATE PO DOMAIN TEMPLATE:  XXT_PO_OTBND_ALLOC.            
001400***************************************************************** 00140001
001500 ENVIRONMENT DIVISION.                                            00150001
001600 INPUT-OUTPUT SECTION.                                            00160001
001700                                                                  00170001
001800 FILE-CONTROL.                                                    00180001
001900                                                                  00190001
002000     SELECT XLKUT038-FILE                                         00200001
002010         ASSIGN TO INP01.                                         00201001
002100                                                                  00210001
002000     SELECT TPOPRPK-FILE                                          00200001
002010         ASSIGN TO INP02.                                         00201001
002100                                                                  00210001
002110     SELECT XL20-OUT-FILE                                                 
002120         ASSIGN TO OUT01.                                               01
002130                                                                  00210001
002200***************************************************************** 00220001
002300 DATA DIVISION.                                                   00230001
002400***************************************************************** 00240001
002500 FILE SECTION.                                                    00250001
002600                                                                  00260001
002700 FD  XLKUT038-FILE                                                00270001
002800     RECORDING MODE IS F                                          00280001
002810     BLOCK CONTAINS 0 RECORDS                                     00281001
002900     LABEL RECORDS ARE STANDARD.                                  00290001
003000                                                                  00300001
003300 01  XLKUT038-RECORD              PIC X(530).                     00330001
003700                                                                  00370001
002700 FD  TPOPRPK-FILE                                                 00270001
002800     RECORDING MODE IS F                                          00280001
002810     BLOCK CONTAINS 0 RECORDS                                     00281001
002900     LABEL RECORDS ARE STANDARD.                                  00290001
003000                                                                  00300001
003300 01  TPOPRPK-RECORD               PIC X(104).                     00330001
003700                                                                  00370001
003710 FD  XL20-OUT-FILE                                                        
003720     RECORDING MODE IS F                                          00280001
003730     BLOCK CONTAINS 0 RECORDS                                     00281001
003740     LABEL RECORDS ARE STANDARD.                                  00290001
003750                                                                  00300001
003760 01  XL20-OUT-REC                 PIC X(052).                     00330001
003770                                                                  00370001
003900***************************************************************** 00390001
004000 WORKING-STORAGE SECTION.                                         00400001
004100                                                                  00410001
004200 01  PS-PROGRAM-SWITCHES.                                         00420001
004300     05  PS-EOF-XLKUT038-SW           PIC X(01)  VALUE 'N'.       00430001
004400         88  PS-EOF-XLKUT038-YES                 VALUE 'Y'.       00440001
004500         88  PS-EOF-XLKUT038-NO                  VALUE 'N'.       00450001
004300     05  PS-EOF-TPOPRPK-SW            PIC X(01)  VALUE 'N'.       00430001
004400         88  PS-EOF-TPOPRPK-YES                  VALUE 'Y'.       00440001
004500         88  PS-EOF-TPOPRPK-NO                   VALUE 'N'.       00450001
004300     05  PS-TPOPRPK-FOUND-SW          PIC X(01)  VALUE 'N'.       00430001
004400         88  PS-TPOPRPK-FOUND                    VALUE 'Y'.       00440001
004500         88  PS-TPOPRPK-NOT-FOUND                VALUE 'N'.       00450001
004300     05  PS-FIRST-TIME-SW             PIC X(01)  VALUE 'Y'.       00430001
004400         88  PS-FIRST-TIME                       VALUE 'Y'.       00440001
004500         88  PS-NOT-FIRST-TIME                   VALUE 'N'.       00450001
004600                                                                  00460001
004700 01  PC-PROGRAM-CONSTANTS.                                        00470001
004800     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT064'.00480001
005000                                                                  00500001
005100 01  PV-PROGRAM-VARIABLES.                                        00510001
005900     05  PV-RECS-READ-XLKUT038        PIC  9(09) VALUE  0.        00590001
006000     05  PV-20-RECS-WRITTEN           PIC  9(09) VALUE  0.        00600001
005900     05  PV-PREV-KEY.                                                     
005900         10  PV-PO-NBR-OLD            PIC  9(09) VALUE  0.                
005900         10  PV-PO-LNE-OLD            PIC  9(09) VALUE  0.                
005900     05  PV-CURR-KEY.                                                     
005900         10  PV-PO-NBR-CUR            PIC  9(09) VALUE  0.                
005900         10  PV-PO-LNE-CUR            PIC  9(09) VALUE  0.                
005900     05  PV-PREV-STORE-KEY            PIC  9(04) VALUE  0.                
005900     05  PV-CURR-STORE-KEY            PIC  9(04) VALUE  0.                
005900     05  PV-PREV-LINE-KEY             PIC  9(04) VALUE  0.                
005900     05  PV-CURR-LINE-KEY             PIC  9(04) VALUE  0.                
005900     05  PV-DIR-STR-IND               PIC  X(01) VALUE ' '.               
005900     05  PV-PKGNG-CDE                 PIC  X(01) VALUE ' '.               
005900     05  PV-STATUS-CDE                PIC  X(02) VALUE '  '.              
005900     05  PV-PREPK-RATO-QTY            PIC S9(04) COMP VALUE 0.            
           05  PV-CURR-PO-PREPK-ID          PIC S9(04) COMP VALUE 0.            
           05  PV-PREV-PO-PREPK-ID          PIC S9(04) COMP VALUE 0.            
007000                                                                  00700001
007100 01  PV-ABEND-FIELDS.                                             00710001
007400     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT064'.00740001
007700     05  FILLER                       PIC  X(01) VALUE SPACES.    00770005
007700     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.    00770005
007700     05  FILLER                       PIC  X(01) VALUE SPACES.    00770005
007500     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.    00750001
007700     05  FILLER                       PIC  X(01) VALUE SPACES.    00770005
007600     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.    00760001
008200                                                                  00820001
008400***********************************************************       00840001
008500*  COPYBOOK USED FOR THE XLKUT038 INPUT FILE              *       00850003
008600***********************************************************       00860001
002600     COPY XLRD029.                                                        
002800                                                                          
008400***********************************************************       00840001
008500*  COPYBOOK USED FOR THE TPOPRPK INPUT FILE               *       00850003
008600***********************************************************       00860001
002500     EXEC SQL                                                             
002600         INCLUDE TPOPRPK                                                  
002700     END-EXEC.                                                            
002800                                                                          
008900***********************************************************       00840001
009000*  COPYBOOK USED FOR THE XXT_PO_OTBND_ALLOC OUTPUT FILE                   
009100***********************************************************       00860001
       01  PC-20-OUT-RECORD.                                                    
009200     COPY XLRD020.                                                00870001
009300                                                                  00880001
010800***************************************************************** 01080001
010900 PROCEDURE DIVISION.                                              01090001
011000***************************************************************** 01100001
011100     DISPLAY '**** BEGINNING XLKUT064 ****'.                      01110001
011200     DISPLAY ' '.                                                 01120001
011300                                                                  01130001
011400     OPEN INPUT  XLKUT038-FILE                                    01140001
011500*                TPOPRPK-FILE                                     01140001
                OUTPUT XL20-OUT-FILE.                                   01140001
                                                                        01150001
           INITIALIZE XL020-DATA-AREA.                                          
                                                                        01150001
           PERFORM 1000-READ-XLKUT038-INPUT-FILE.                               
           MOVE XL029-PO-NBR      TO  PV-PO-NBR-OLD                             
                                      PV-PO-NBR-CUR.                            
           MOVE XL029-PO-LNE-NBR  TO  PV-PO-LNE-OLD                             
                                      PV-PO-LNE-CUR.                            
                                                                                
           INITIALIZE XL020-DATA-AREA.                                          
                                                                                
012100     PERFORM 2000-PROCESS-INPUT-FILE                              01210001
012200       UNTIL PS-EOF-XLKUT038-YES.                                 01220001
012300                                                                  01230001
012310     CLOSE XLKUT038-FILE                                                01
012320*          TPOPRPK-FILE                                                 01
                 XL20-OUT-FILE.                                                 
012500                                                                  01250001
012600     DISPLAY '  INPUT XLKUT038 RECS    = ' PV-RECS-READ-XLKUT038.         
012700     DISPLAY '  OUTPUT XLRD020 RECS    = ' PV-20-RECS-WRITTEN.          01
012800     DISPLAY ' '.                                                 01280001
012900     DISPLAY '****  END OF  XLKUT064  ****'.                      01290001
013000                                                                  01300001
013100     GOBACK.                                                      01310001
013200                                                                  01320001
013400***************************************************************** 01340001
013500*  READ A XLKUT038 RECORD                                         01350003
013600***************************************************************** 01360001
       1000-READ-XLKUT038-INPUT-FILE.                                           
                                                                        01380001
           MOVE '1000-READ-XLKUT038-INPUT-FILE' TO PV-ABEND-PARAGRAPH.          
                                                                        01400001
           INITIALIZE XL029-DATA.                                               
                                                                        01402003
           READ XLKUT038-FILE                                           01410004
                INTO XL029-DATA                                                 
                  AT END                                                01430001
                     SET PS-EOF-XLKUT038-YES TO TRUE                            
           END-READ.                                                    01490001
                                                                        01500001
           IF PS-EOF-XLKUT038-YES                                               
              CONTINUE                                                          
           ELSE                                                                 
              ADD +1 TO PV-RECS-READ-XLKUT038                                   
              MOVE XL029-PO-NBR      TO PV-PO-NBR-CUR                           
              MOVE XL029-PO-LNE-NBR  TO PV-PO-LNE-CUR                           
           END-IF.                                                              
                                                                        01510001
015200***************************************************************** 01520001
015300*  PROCESS INPUT FILE                                           * 01530003
015400***************************************************************** 01540001
       2000-PROCESS-INPUT-FILE.                                         01550001
                                                                        01560001
           MOVE '2000-PROCESS-INPUT-FILE ' TO PV-ABEND-PARAGRAPH.       01570001
                                                                        01560001
           IF PV-CURR-KEY   =  PV-PREV-KEY                                      
               PERFORM 3000-CHECK-REC-TYPE                                      
           ELSE                                                                 
               IF PV-CURR-KEY   >  PV-PREV-KEY                                  
                   PERFORM 4000-CHECK-LINE-NBR                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE PV-CURR-KEY   TO  PV-PREV-KEY.                                  
           PERFORM 1000-READ-XLKUT038-INPUT-FILE.                               
                                                                        01580001
      ***************************************************************** 01520001
      *  EVALUATE CUR RECORD TYPE.                                    * 01530003
      ***************************************************************** 01540001
       3000-CHECK-REC-TYPE.                                             01550001
                                                                        01560001
           MOVE '3000-CHECK-REC-TYPE' TO PV-ABEND-PARAGRAPH.                    
                                                                        01580001
           EVALUATE XL029-RECORD-TYPE                                           
               WHEN '1'                                                         
                   MOVE XL029-PURCHS-PO-NBR       TO XL020-PO-NBR               
                   MOVE XL029-PURCHS-PKGNG-CDE    TO PV-PKGNG-CDE               
                   MOVE XL029-PURCHS-DIR-STR-IND  TO PV-DIR-STR-IND             
                   MOVE XL029-PURCHS-STATUS-CDE   TO PV-STATUS-CDE              
               WHEN '2'                                                         
                   PERFORM 3100-MOVE-TPURITM-INFO                               
               WHEN '3'                                                         
                   PERFORM 3200-MOVE-TALLPLN-INFO                               
               WHEN '4'                                                         
                   PERFORM 3300-MOVE-TMESTDS-INFO                               
                   IF PV-DIR-STR-IND = 'N'                                      
                       IF PV-CURR-PO-PREPK-ID > 0                               
                           IF PV-CURR-PO-PREPK-ID = PV-PREV-PO-PREPK-ID         
                               IF PV-CURR-LINE-KEY  =  PV-PREV-LINE-KEY         
                                   PERFORM 7000-WRITE-OUTPUT                    
                                   MOVE XL029-MESTDS-PO-LNE-NBR  TO             
                                            PV-PREV-LINE-KEY                    
                               END-IF                                           
                           END-IF                                               
                       ELSE                                                     
                           PERFORM 7000-WRITE-OUTPUT                            
                       END-IF                                                   
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
      ***************************************************************** 01520001
      *  MOVE TPURITM INFO TO EXTRACT FILE                            * 01530003
      ***************************************************************** 01540001
       3100-MOVE-TPURITM-INFO.                                          01550001
                                                                        01560001
           MOVE '3100-MOVE-TPURITM-INFO ' TO PV-ABEND-PARAGRAPH.                
                                                                                
           MOVE XL029-PURITM-PO-NBR          TO XL020-PO-NBR.                   
           MOVE XL029-PURITM-PO-PREPK-ID     TO PV-CURR-PO-PREPK-ID.            
           IF XL029-PURITM-STATUS-CDE = 'CM' OR 'CA' OR 'CB'                    
               MOVE 'Y'                      TO XL020-SKU-CNCLD-IND             
           ELSE                                                                 
               IF PV-STATUS-CDE       = 'CM' OR 'CA'                            
                   MOVE 'Y'                  TO XL020-SKU-CNCLD-IND             
               ELSE                                                             
                   MOVE 'N'                  TO XL020-SKU-CNCLD-IND             
               END-IF                                                           
           END-IF.                                                              
           IF XL029-PURITM-PO-PREPK-ID > 0                                      
               MOVE XL029-PURITM-PREPK-RATO-QTY  TO PV-PREPK-RATO-QTY           
               SET PS-TPOPRPK-NOT-FOUND TO TRUE                                 
               SET PS-EOF-TPOPRPK-NO    TO TRUE                                 
               IF XL029-PURITM-PO-NBR NOT = POPRPK-PO-NBR                       
                   OPEN INPUT  TPOPRPK-FILE                                     
                   PERFORM 5000-READ-TPOPRPK-INPUT-FILE                         
                                                 UNTIL PS-TPOPRPK-FOUND         
                   CLOSE TPOPRPK-FILE                                           
               ELSE                                                             
                   IF XL029-PURITM-PO-NBR = POPRPK-PO-NBR                       
                       IF XL029-PURITM-PO-PREPK-ID = POPRPK-PO-PREPK-ID         
                            MOVE XL029-PURITM-PO-PREPK-ID  TO                   
                                      PV-CURR-PO-PREPK-ID                       
                                      PV-PREV-PO-PREPK-ID                       
                            PERFORM 5100-MOVE-TPOPRPK-INFO                      
                       ELSE                                                     
                           OPEN INPUT  TPOPRPK-FILE                             
                           PERFORM 5000-READ-TPOPRPK-INPUT-FILE                 
                                                 UNTIL PS-TPOPRPK-FOUND         
                           CLOSE TPOPRPK-FILE                                   
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               MOVE XL029-PURITM-RMS-PO-SKU-NBR  TO XL020-SKU-NBR               
           END-IF.                                                              
                                                                                
      ***************************************************************** 01520001
      *  MOVE TALLPLN INFO TO EXTRACT FILE                            * 01530003
      ***************************************************************** 01540001
       3200-MOVE-TALLPLN-INFO.                                                  
                                                                                
           MOVE '3200-MOVE-TALLPLN-INFO ' TO PV-ABEND-PARAGRAPH.                
                                                                                
           MOVE XL029-ALLPLN-PO-NBR      TO XL020-PO-NBR.                       
           MOVE XL029-ALLPLN-DEST-NBR    TO XL020-LOC-NBR.                      
                                                                                
      ***************************************************************** 01520001
      *  MOVE TMESTDS INFO TO EXTRACT FILE                            * 01530003
      ***************************************************************** 01540001
       3300-MOVE-TMESTDS-INFO.                                                  
                                                                                
           MOVE '3300-MOVE-TMESTDS-INFO ' TO PV-ABEND-PARAGRAPH.                
                                                                                
           MOVE XL029-MESTDS-PO-NBR         TO XL020-PO-NBR.                    
           MOVE XL029-MESTDS-PO-LNE-NBR     TO PV-CURR-LINE-KEY.                
           MOVE XL029-MESTDS-STR-NBR        TO XL020-STR-NBR.                   
           MOVE XL029-MESTDS-RNK-SEQ-NBR    TO XL020-RNK-SEQ-NBR.               
           MOVE XL029-MESTDS-ALLOC-SEQ-CDE  TO XL020-ALLOC-SEQ-CDE.             
           MOVE XL029-MESTDS-STR-CNSTR-CDE  TO XL020-STR-CNSTR-CDE.             
           IF PV-CURR-PO-PREPK-ID > 0                                           
               IF PV-PREPK-RATO-QTY > 0                                         
                   COMPUTE XL020-ALLOC-QTY =                                    
                       XL029-MESTDS-STR-DISTRO-QTY / PV-PREPK-RATO-QTY          
                   COMPUTE XL020-STR-CNSTR-QTY =                                
                       XL029-MESTDS-STR-CNSTR-QTY / PV-PREPK-RATO-QTY           
               ELSE                                                             
                   MOVE ZEROES TO XL020-ALLOC-QTY                               
                                  XL020-STR-CNSTR-QTY                           
               END-IF                                                           
           ELSE                                                                 
               MOVE XL029-MESTDS-STR-DISTRO-QTY TO XL020-ALLOC-QTY              
               MOVE XL029-MESTDS-STR-CNSTR-QTY  TO XL020-STR-CNSTR-QTY          
           END-IF.                                                              
                                                                                
      ***************************************************************** 01520001
      *  EVALUATE PREV LINE NUMBER.                                   * 01530003
      ***************************************************************** 01540001
       4000-CHECK-LINE-NBR.                                             01550001
                                                                        01560001
           MOVE '4000-CHECK-LINE-NBR' TO PV-ABEND-PARAGRAPH.                    
                                                                                
           EVALUATE PV-PO-LNE-OLD                                               
               WHEN  0                                                          
                   EVALUATE PV-PO-LNE-CUR                                       
                       WHEN  0                                                  
                          INITIALIZE PV-PREPK-RATO-QTY                          
                          PERFORM 3000-CHECK-REC-TYPE                           
                       WHEN  OTHER                                              
                          PERFORM 3000-CHECK-REC-TYPE                           
                   END-EVALUATE                                                 
               WHEN OTHER                                                       
                   INITIALIZE PV-PREPK-RATO-QTY                                 
                   PERFORM 3000-CHECK-REC-TYPE                                  
           END-EVALUATE.                                                        
                                                                                
      ***************************************************************** 01340001
      *  READ A TPOPRPK RECORD                                          01350003
      ***************************************************************** 01360001
       5000-READ-TPOPRPK-INPUT-FILE.                                            
                                                                        01380001
           MOVE '5000-READ-TPOPRPK-INPUT-FILE' TO PV-ABEND-PARAGRAPH.           
                                                                        01400001
           IF PS-EOF-TPOPRPK-YES                                                
               SET PS-TPOPRPK-FOUND TO TRUE                                     
           ELSE                                                                 
               INITIALIZE DCLTPOPRPK                                            
               READ TPOPRPK-FILE                                                
                    INTO DCLTPOPRPK                                             
                      AT END                                                    
                         SET PS-EOF-TPOPRPK-YES TO TRUE                         
               END-READ                                                         
               IF XL029-PURITM-PO-NBR = POPRPK-PO-NBR                           
                   IF XL029-PURITM-PO-PREPK-ID = POPRPK-PO-PREPK-ID             
                       MOVE XL029-PURITM-PO-PREPK-ID  TO                        
                                      PV-CURR-PO-PREPK-ID                       
                                      PV-PREV-PO-PREPK-ID                       
                       MOVE XL029-PURITM-PO-LNE-NBR   TO                        
                                      PV-CURR-LINE-KEY                          
                                      PV-PREV-LINE-KEY                          
                       PERFORM 5100-MOVE-TPOPRPK-INFO                           
                       SET PS-TPOPRPK-FOUND TO TRUE                             
                   END-IF                                                       
               ELSE                                                             
                   IF PS-EOF-TPOPRPK-YES                                        
                        SET PS-TPOPRPK-FOUND TO TRUE                            
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ***************************************************************** 01520001
      *  MOVE TPOPRPK INFO TO EXTRACT FILE                            * 01530003
      ***************************************************************** 01540001
       5100-MOVE-TPOPRPK-INFO.                                          01550001
                                                                        01560001
           MOVE '5100-MOVE-TPOPRPK-INFO ' TO PV-ABEND-PARAGRAPH.                
                                                                                
           MOVE POPRPK-PO-NBR   TO  XL020-PO-NBR.                               
           IF PV-CURR-PO-PREPK-ID > 0                                           
               MOVE POPRPK-PK-SKU-NBR    TO  XL020-SKU-NBR                      
           END-IF.                                                              
                                                                                
021600***************************************************************** 02160001
021700*  WRITE A RECORDS TO THE LOGISTICS SKU OUTPUT FILE             * 02170003
021800***************************************************************** 02180001
021900 7000-WRITE-OUTPUT.                                               02190003
022000                                                                  02200001
022010     WRITE XL20-OUT-REC  FROM                                             
022020         XL020-RECORD                                                   07
022030     END-WRITE.                                                   03940007
024400     ADD +1                 TO PV-20-RECS-WRITTEN.                        
027200                                                                  02720001
