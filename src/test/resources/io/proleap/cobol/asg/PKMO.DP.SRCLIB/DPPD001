00001 ******************************************************************01/26/90
00002 *  COPY MEMBER  DPPD001.                                         *DPPD001 
00003 *                                                                *   LV020
00004 *  THIS COPY MEMBER CONTAINS THE PROCEDURE DIVISION STATMENTS    *DPPD001 
00005 *  THAT ARE REQUIRED TO INVOKE THE TRANSACTION PRESENTATION      *   CL**9
00006 *  MODULE (DPKUT901) OF THE DB2 CHECKPOINT RESTART SYSTEM.       *   CL**9
00007 *                                                                *DPPD001 
00008 ******************************************************************DPPD001 
00009 *                                                                *DPPD001 
00010 *  LISTED BELOW ARE NOTES ON WHAT NEEDS TO BE DEFINED IN THE     *   CL**9
00011 *  DATA AND PROCEDURE DIVISIONS TO USE THIS COPY MEMEBER:        *   CL**9
00012 *                                                                *   CL**9
00013 *  DEFINE A 01 GROUP LEVEL NAME IN WORKING-STORAGE THAT CONTAINS *   CL**7
00014 *  ALL THE DATA THAT NEEDS TO BE SAVED SO THAT IF AN ABEND       *   CL**7
00015 *  OCCURS AND THE MAINTENANCE PROGRAM IS RESTARTED, THE CONTENTS *   CL**7
00016 *  OF WORKING-STORAGE CAN BE RESTORED FROM THE LAST CHECKPOINT   *   CL**7
00017 *  THAT WAS TAKEN BEFORE THE MAINTENANCE PROGRAM ABENDED AND     *   CL**7
00018 *  PROCESSING CAN RESUME FROM THAT POINT IN TIME.                *   CL**7
00019 *                                                                *DPPD001 
00020 *  DEFINE A FIELD IN WORKING-STORAGE THAT CONTAINS THE LENGTH    *   CL**7
00021 *  OF THE WORKING-STORAGE AREA OF DATA BEING SAVED FOR A RE-     *   CL**9
00022 *  START.                                                        *   CL**9
00023 *                                                                *   CL**7
00024 *  ENSURE THAT THE ASSOCIATED WORKING-STORAGE COPY MEMBER        *   CL**9
00025 *  (DPLS001) IS COPIED INTO THE WORKING-STORAGE SECTION OF YOUR  *   CL**7
00026 *  DB2 MAINTENANCE PROGRAM.                                      *   CL**7
00027 *                                                                *   CL**7
00028 *  ENSURE THAT COPY MEMBER (SQLCA2) IS COPIED INTO THE WORKING-  *   CL*17
00029 *  STORAGE SECTION OF YOUR DB2 MAINTENANCE PROGRAM.              *   CL*17
00030 *                                                                *   CL*17
00031 *  ENSURE THAT THE COPY STATEMENT DPLS001 IS ENCLOSED WITH THE   *   CL**9
00032 *  EXEC SQL STATEMENTS.                                          *   CL**9
00033 *                                                                *   CL**2
00034 *  ENSURE THAT THE FOLLOWING STATEMENTS ARE CODED WITHIN THE     *   CL**5
00035 *  INITIALIZATION ROUTINE OF YOUR PROGRAM:                       *   CL**5
00036 *                                                                *DPPD001 
00037 *    1.  MOVE 'PPPPPPPP' TO DP001-PLAN-NAME.                     *   CL**5
00038 *        (INSERT YOUR PROGRAM NAME HERE IN PLACE OF PPPPPPPP,    *   CL**5
00039 *         THE PROGRAM NAME SHOULD BE THE SAME AS THE DB2 PLAN    *   CL**5
00040 *         NAME THE PROGRAM IS BOUND TO).                         *   CL**5
00041 *    2.  MOVE 'JJJJJJJJ' TO DP001-JOB-NAME.                      *   CL**5
00042 *        (INSERT THE NAME OF THE JOB THAT THE PROGRAM IS EXE-    *   CL**5
00043 *         CUTED IN, IN PLACE OF JJJJJJJJ).                       *   CL**5
00044 *                                                                *DPPD001 
00045 *  CODE THE FOLLOWING STATEMENTS IN YOUR PROGRAM WHENEVER YOU    *   CL**5
00046 *  ARE INVOKING THE TRANSACTION PRESENTATION MODULE:             *   CL**5
00047 *                                                                *   CL**5
00048 *    1.  MOVE 'FFFFF' TO DP001-FUNC-CODE.                        *   CL**5
00049 *        (INSERT THE FUNCTION YOU WISH THE TRANSACTION PRESEN-   *   CL**5
00050 *         TATION MODULE TO PERFORM IN PLACE OF FFFFF.  VALID     *   CL**5
00051 *         FUNCTION CODES ARE: OPEN, TRANS, CKPT & CLOSE).        *   CL**5
00052 *    2.  MOVE THE LENGTH OF THE WORKING-STORAGE AREA BEING       *   CL**6
00053 *        SAVED FOR A RESTART TO DP001-WORK-STRG-LENGTH.          *   CL**6
00054 *    3.  MOVE THE WORKING-STORAGE AREA BEING SAVED FOR A         *   CL**6
00055 *        RESTART TO DP001-WORK-STRG-AREA.  (IMPORTANT!! THE      *   CL**6
00056 *        LENGTH OF THE WORKING-STORAGE AREA MUST BE MOVED        *   CL**6
00057 *        BEFORE THE ACTUAL WORKING-STORAGE AREA IS MOVED, IF     *   CL**6
00058 *        IT ISN'T WHATEVER LENGTH IS IN THE DP001-WORK-STRG-     *   CL**6
00059 *        LENGTH IS THE NUMBER OF BYTES OF THE WORKING-STORAGE    *   CL**6
00060 *        AREA THAT WILL BE SAVED).                               *   CL**7
00061 *        THE WORKING-STORAGE AREA BEING SAVED FOR A RESTART      *   CL*10
00062 *        IS BEING PASSED TO THE TRANSACTION PRESENTATION MODULE  *   CL*10
00063 *        EVERYTIME IT'S BEING INVOKED BECAUSE WE ARE NOT SURE    *   CL*10
00064 *        WHEN THE PRESENATION MODULE IS GOING TO TAKE A CHECK-   *   CL*10
00065 *        POINT, THIS IS DETERMINED BASED ON THE CHECKPOINT       *   CL*10
00066 *        CONTROLS DEFINED FOR THE MAINTENANCE PROGRAM.           *   CL*10
00067 *    4.  PERFORM Z900-CALL-TRANS-PRES-MODULE.                    *   CL**6
00068 *                                                                *   CL**5
00069 ******************************************************************DPPD001 
00043 * CHG0260107 - 8/24/2021 - CONVERTING CALL OF DPKUT901 FROM      *        
00043 *                          STATIC TO DYNAMIC.                    *        
00044 ******************************************************************DPPD001 
00070  EJECT                                                               CL**9
00071  Z900-CALL-TRANS-PRES-MODULE.                                        CL**5
00072 ******************************************************************DPPD001 
00073 *  CALL THE TRANSACTION PRESENTATION MODULE.                     *   CL**7
00074 ******************************************************************DPPD001 
00075                                                                      CL**7
00076      CALL DP991-TRANS-PRES-SUBROUTINE                                     
00077                              USING DP001-TRANS-PRES-PARAMETERS            
00077                                    DP001-TRANS-RECORD                     
00079                                    DP001-WORK-STRG-AREA.                  
00079                                                                      CL**7
00080      IF DP001-BAD-SQLCODE                                            CL**9
00081          PERFORM Z999-SQL-ERROR                                      CL**9
00082      ELSE                                                            CL**9
00083          IF (DP001-INVALID-FUNC-CODE                                 CL*20
00084           OR DP001-JOB-PLAN-DOESNT-EXIST                             CL*16
00085           OR DP001-PREP-TRANS-NOT-CREATED                            CL**9
00086           OR DP001-CKPT-CNTLS-IN-USE                                 CL*11
00087           OR DP001-BAD-WS-LENGTH)                                    CL*20
00088              PERFORM Z910-FORMAT-MSG-AND-ABEND                       CL**9
00089          END-IF                                                      CL**9
00090      END-IF.                                                         CL**9
00091  EJECT                                                               CL**8
00092  Z910-FORMAT-MSG-AND-ABEND.                                          CL**8
00093 ******************************************************************DPPD001 
00094 *  FORMAT AND DISPLAY AN ABEND MESSAGE CONTAINING PERTINANT      *   CL**8
00095 *  INFORMATION RETURNED FROM THE TRANSACTION PRESENTATION MODULE *   CL**8
00096 *  IN REGARD TO THE BAD RETURN CODE ENCOUNTERED AND TERMINATE    *   CL**8
00097 *  THE PROGRAM.                                                  *   CL**8
00098 ******************************************************************DPPD001 
00099                                                                      CL**9
00100      DISPLAY '** ABEND       ** = ' DP001-MESSAGE.                   CL**9
00101      DISPLAY '** PROGRAM     ** = ' DP001-PLAN-NAME.                 CL**9
00102      DISPLAY '** PARAMETERS  **'.                                    CL**9
00103      DISPLAY '    FUNCTION CODE = ' DP001-FUNC-CODE.                 CL**9
00104      DISPLAY '         JOB NAME = ' DP001-JOB-NAME.                  CL**9
00105      DISPLAY '        PLAN NAME = ' DP001-PLAN-NAME.                 CL**9
00106                                                                      CL*15
00107      MOVE DP001-RET-CODE TO ABEND-CODE.                              CL**9
00108      CALL 'ILBOABN0' USING ABEND-CODE.                               CL**9
00109 *                                                                 DPPD001 
00110  Z999-SQL-ERROR.                                                  DPPD001 
00111 ******************************************************************DPPD001 
00112 *  THIS MODULE PERFORMS THE ABEND IN THE EVENT THE RETURN CODE   *   CL**9
00113 *  FROM THE TRANSACTION PRESENTATION MODULE INDICATES AN SQL     *   CL**9
00114 *  ERROR.                                                        *   CL**9
00115 ******************************************************************DPPD001 
00116                                                                      CL**9
00117      MOVE DP001-SQLCODE TO SQLCODE2.                                 CL**9
00118      MOVE DP001-SQLERRD3 TO SQLERRD3.                                CL**9
00119      DISPLAY '** ABEND         ** = ' DP001-MESSAGE.                 CL**9
00120      DISPLAY '** PROGRAM       ** = ' DP001-PLAN-NAME.               CL**9
00121      DISPLAY '** FUNCTION CODE ** = ' DP001-FUNC-CODE.               CL**9
00122      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                DPPD001 
00123      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                DPPD001 
00124      DISPLAY '** SQLERRMC         ** = ' DP001-SQLERRMC.             CL**9
00125      DISPLAY '**         SQLWARN0 ** = ' DP001-SQLWARN0.             CL*12
00126      DISPLAY '**         SQLWARN1 ** = ' DP001-SQLWARN1.             CL*12
00127      DISPLAY '**         SQLWARN2 ** = ' DP001-SQLWARN2.             CL*12
00128      DISPLAY '**         SQLWARN3 ** = ' DP001-SQLWARN3.             CL*12
00129      DISPLAY '**         SQLWARN4 ** = ' DP001-SQLWARN4.             CL*12
00130      DISPLAY '**         SQLWARN5 ** = ' DP001-SQLWARN5.             CL*12
00131      DISPLAY '**         SQLWARN6 ** = ' DP001-SQLWARN6.             CL*12
00132      DISPLAY '**         SQLWARN7 ** = ' DP001-SQLWARN7.             CL*12
00133                                                                      CL*15
00134      MOVE DP001-RET-CODE TO ABEND-CODE.                              CL**9
00135      CALL 'ILBOABN0' USING ABEND-CODE.                               CL**9
00136  EJECT                                                               CL**9
