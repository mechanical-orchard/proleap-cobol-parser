       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.      XSKUT135.                                       00030000
       AUTHOR.          CHERI PARMLEY.                                  00040000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00050000
       DATE-WRITTEN.    09/08/11.                                       00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *  MODULE TYPE:   COBOL2 BATCH. MUST BE COMPILED WITH 'N' = NO    00100000
      *                 OPTION, USES DB2 WITHIN THE DP MODULE ONLY      00110000
      *                 JOB STEP WILL HAVE DB CONTROL CARD FOR XSKUT135 00120000
      *                                                                 00130000
      * THIS PROGRAM PREPARES THE CONTROL CARD FILE TO BE PROCESSED     00140000
      * BY XSKUP135 UNDER THE DB2 CHECKPOINT RESTART SYSTEM.            00150000
      *                                                                 00160000
      * THE PROGRAM READS THE CONTROL CARD UNLOAD FILE,FORMATS          00170000
      * RECORDS AND SETS A CHECKPOINT INDICATOR ON THE RECORD BASED     00180000
      * ON HOW CHECKPOINTS WILL BE TAKEN BY THE MAINTENANCE PROGRAM.    00190000
      * AFTER A RECORD HAS BEEN REFORMATTED IT IS PASSED TO THE         00200000
      * TRANSACTION PREPARATION MODULE (DPKUT900) WHICH DOES SOME MORE  00210000
      * PROCESSING ON THE RECORDS AND WRITES THE REFORMATTED RECORDS    00220000
      * OUT TO A SEQUENTIAL FILE.                                       00230000
      *                                                                 00240000
      * THE PROGRAM COMMUNICATES WITH THE TRANSACTION PREPARATION       00250000
      * MODULE USING FUNCTION CODES (OPEN, WRITE, & CLOSE) AND CHECKS   00260000
      * THE RETURN CODE AFTER EACH REQUEST.  IF AN INVALID RETURN CODE  00270000
      * IS RECEIVED, ANY INFORMATION THAT MAY BE HELPFUL IN RESOLVING   00280000
      * THE BAD RETURN CODE IS DISPLAYED ON SYSOUT AND THE PROGRAM      00290000
      * ABENDS.                                                         00300000
      *                                                                 00310000
      *========================== CHANGE LOG ===========================00320000
      *  AUTH        BY        DATE         DESCRIPTION                 00330001
      *=================================================================00340000
      * WR40189    C. PARMLEY  09-08-11     INITIAL IMPLEMENTATION.     00350001
      *                                                                 00360000
260107* CHG0260107 JIM HUNTER  08-19-21     ADD COPYBOOK DPWS990 TO MAKE00361001
260107*                                     DPKUT900 CALL DYNAMIC.      00362001
      ******************************************************************00370000
                                                                        00380000
       ENVIRONMENT DIVISION.                                            00390000
       CONFIGURATION SECTION.                                           00400000
                                                                        00410000
       SOURCE-COMPUTER.    IBM-3090.                                    00420000
       OBJECT-COMPUTER.    IBM-3090.                                    00430000
                                                                        00440000
       INPUT-OUTPUT SECTION.                                            00450000
       FILE-CONTROL.                                                    00460000
                                                                        00470000
           SELECT UNLOAD-FILE   ASSIGN TO INP01.                        00480000
                                                                        00490000
       DATA DIVISION.                                                   00500000
       FILE SECTION.                                                    00510000
                                                                        00520000
       FD  UNLOAD-FILE                                                  00530000
           RECORDING MODE IS F                                          00540000
           LABEL RECORDS ARE STANDARD                                   00550000
           RECORD CONTAINS 80 CHARACTERS                                00560000
           BLOCK CONTAINS 0 RECORDS                                     00570000
           DATA RECORD IS UNLOAD-REC.                                   00580000
       01  UNLOAD-REC                       PIC  X(80).                 00590000
                                                                        00600000
      ***************************************************************** 00610000
      *                       WORKING STORAGE                           00620000
      ***************************************************************** 00630000
       WORKING-STORAGE SECTION.                                         00640000
                                                                        00650000
       01  W-START                          PIC  X(40)                  00660000
           VALUE 'XSKUT135 - WORKING STORAGE BEGINS HERE'.              00670000
                                                                        00680000
                                                                        00690000
      ***************************************************************** 00700000
      *                         SWITCHES                                00710000
      ***************************************************************** 00720000
       01  PROGRAM-SWITCHES.                                            00730000
           05  PS-INPUT-UNLOAD-EOF-SW       PIC  X(01) VALUE 'N'.       00740000
               88  PS-INPUT-UNLOAD-EOF                 VALUE 'Y'.       00750000
           05  PS-FIRST-TRANS-READ-SW       PIC  X(01) VALUE 'Y'.       00760000
               88  FIRST-TRANS-READ                    VALUE 'Y'.       00770000
                                                                        00780000
      ******************************************************************00790000
      *                       ACCUMULATORS                              00800000
      ******************************************************************00810000
       01  PROGRAM-ACCUMULATORS.                                        00820000
           05  PA-READ                      PIC  9(11) VALUE ZEROS.     00830000
           05  PA-WRITTEN                   PIC  9(11) VALUE ZEROS.     00840000
                                                                        00850000
      ******************************************************************00860000
      *                           CONSTANTS                             00870000
      ******************************************************************00880000
       01  PROGRAM-CONSTANTS.                                           00890000
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XSKUT135'.00900000
                                                                        00910000
           05  PC-TRAN-PREP-FUNC-CODES.                                 00920000
               10  PC-TRAN-PREP-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   00930000
               10  PC-TRAN-PREP-FUNC-WRITE  PIC  X(05) VALUE 'WRITE'.   00940000
               10  PC-TRAN-PREP-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   00950000
                                                                        00960000
           05  PC-JOB-NAME                  PIC  X(08) VALUE 'XSK921  '.00970000
           05  PC-PLAN-NAME                 PIC  X(08) VALUE 'XSKUP135'.00980000
           05  PC-TRANS-RECORD-LENGTH       PIC S9(04) VALUE +130 COMP. 00990000
                                                                        01000000
      ******************************************************************01010000
      *                          VARIABLES                              01020000
      ******************************************************************01030000
       01  PROGRAM-VARIABLES.                                           01040000
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.    01050000
           05  PV-OPERATION-MSG             PIC  X(60) VALUE SPACES.    01060000
           05  PV-RET-CODE                  PIC  9(04) VALUE ZEROS.     01070000
                                                                        01080000
           05  PV-CHECKPOINT-TYPE           PIC  X(01) VALUE SPACE.     01090000
               88  CONTROL-BREAK                       VALUE 'C'.       01100000
               88  LOGICAL-TRANSACTION                 VALUE 'L'.       01110000
               88  TIME-INTERVAL                       VALUE 'T'.       01120000
               88  REQUESTED-BY-PROGRAM                VALUE 'R'.       01130000
                                                                        01140000
           05  PV-INTEGER                   PIC S9(11) VALUE +0.        01150000
           05  PV-DECIMAL                   PIC S9(11) VALUE +0.        01160000
                                                                        01170000
       01  XS-RTV-KEY.                                                  01180000
           05  XS-RTV-LOC-NBR               PIC   9(04).                01190000
           05  XS-RTV-XFER-TYP              PIC   9(02).                01200000
           05  XS-RTV-BEG-DTE               PIC   X(10).                01210000
       01  PV-HOLD-KEY.                                                 01210100
           05  PV-LOC-NBR                   PIC   9(04).                01210200
           05  PV-XFER-TYP                  PIC   9(02).                01210300
           05  PV-BEG-DTE                   PIC   X(10).                01210400
      ******************************************************************01210500
      *               SYSTEM TIME AND DATE VARIABLES                    01210600
      ******************************************************************01210700
       01  SYS-DATE-TIME.                                               01210800
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    01210900
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    01211000
                                                                        01212000
       01  ST-BEG-TIME.                                                 01213000
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     01214000
           05  FILLER                       PIC  X(01) VALUE ':'.       01215000
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     01216000
                                                                        01217000
       01  ST-END-TIME.                                                 01218000
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     01219000
           05  FILLER                       PIC  X(01) VALUE ':'.       01220000
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     01230000
                                                                        01240000
       01  SD-EDIT-DATE.                                                01250000
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     01260000
           05  FILLER                       PIC  X(01) VALUE '-'.       01270000
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     01280000
           05  FILLER                       PIC  X(01) VALUE '-'.       01290000
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     01300000
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     01310000
                                                                        01320000
      ******************************************************************01330000
      *                        ERROR HANDLING                           01340000
      ******************************************************************01350000
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROS      01360000
                                                       COMP SYNC.       01370000
           88  ABEND-4002-READ-ERROR             VALUE +4002.           01380000
           88  ABEND-4444-FORCED-ABEND           VALUE +4444.           01390000
                                                                        01400000
                                                                        01410000
      ******************************************************************01420000
      * RECORD LAYOUT FOR UNLOAD KEY                                    01430000
      ******************************************************************01440000
           COPY XSRD151.                                                01450000
                                                                        01460000
      ******************************************************************01470000
      *  TRANSACTION PREPARATION MODULE LINKAGE SECTION                 01480000
      ******************************************************************01490000
260107     COPY DPWS990.                                                01500001
                                                                        01501001
           COPY DPLS000.                                                01502001
                                                                        01510000
      ******************************************************************01520000
      *  SQL COMMUNICATIONS WORK AREA                                   01530000
      ******************************************************************01540000
           COPY SQLCA2.                                                 01550000
                                                                        01560000
                                                                        01570000
       PROCEDURE DIVISION.                                              01580000
      ******************************************************************01590000
      * MAINLINE-PROCESSING                                             01600000
      ******************************************************************01610000
                                                                        01620000
           PERFORM A100-INITIALIZE.                                     01630000
                                                                        01640000
           PERFORM B100-PROCESS-UNLOAD-INPUT-FILE                       01650000
             UNTIL PS-INPUT-UNLOAD-EOF.                                 01660000
                                                                        01670000
           PERFORM Z990-END-OF-JOB-ROUTINE.                             01680000
                                                                        01690000
           GOBACK.                                                      01700000
                                                                        01710000
                                                                        01720000
      ******************************************************************01730000
      * PREFORMS TASK INITIATION                                        01740000
      ******************************************************************01750000
       A100-INITIALIZE.                                                 01760000
                                                                        01770000
           OPEN INPUT UNLOAD-FILE.                                      01780000
                                                                        01790000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01800000
                                                                        01810000
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            01820000
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            01830000
                                                                        01840000
           MOVE SYS-DATE (1:2) TO SD-CENT.                              01850000
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              01860000
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             01870000
           MOVE SYS-DATE (7:2) TO SD-DAY.                               01880000
                                                                        01890000
           PERFORM C100-ISSUE-OPEN-REQUEST.                             01900000
                                                                        01910000
           PERFORM C200-READ-UNLOAD-INPUT-FILE.                         01920000
                                                                        01930000
           IF PS-INPUT-UNLOAD-EOF                                       01940000
               MOVE 'C200-READ-UNLOAD-INPUT-FILE' TO PV-PARAGRAPH-NAME  01950000
               MOVE '** EMPTY UNLOAD FILE - NO DATA **'                 01960000
                                                  TO PV-OPERATION-MSG   01970000
           END-IF.                                                      01980000
                                                                        01990000
                                                                        02000000
      ******************************************************************02010000
      * PROCESS THE UNLOAD FILE. IF A RECORD IS READ, GATHER WHATEVER   02020000
      * OTHER DATA IS NECESSARY TO TAKE CHECKPOINTS AND ISSUE A WRITE   02030000
      * REQUEST TO THE TRANSACTION PREPARATION PROGRAM.                 02040000
      ******************************************************************02050000
       B100-PROCESS-UNLOAD-INPUT-FILE.                                  02060000
                                                                        02070000
           IF FIRST-TRANS-READ                                          02080000
               MOVE XS151-LOC-NBR         TO PV-LOC-NBR                 02090000
               MOVE XS151-XFER-TYP        TO PV-XFER-TYP                02100000
               MOVE XS151-BEG-DTE         TO PV-BEG-DTE                 02110000
               MOVE 'N'                   TO PS-FIRST-TRANS-READ-SW     02120000
           END-IF.                                                      02130000
                                                                        02140000
           IF CONTROL-BREAK                                             02150000
               PERFORM C300-CKPT-BY-CNTL-BREAK                          02160000
           ELSE                                                         02170000
               IF LOGICAL-TRANSACTION                                   02180000
                   PERFORM C400-CKPT-BY-LOGICAL-TRANS                   02190000
               ELSE                                                     02200000
                   PERFORM C500-CKPT-BY-TIME-OR-REQUEST                 02210000
               END-IF                                                   02220000
           END-IF.                                                      02230000
                                                                        02240000
           PERFORM C200-READ-UNLOAD-INPUT-FILE.                         02250000
                                                                        02260000
                                                                        02270000
      ******************************************************************02280000
      * ISSUE AN 'OPEN' REQUEST TO THE TRANSACTION PREPARATION MODULE   02290000
      ******************************************************************02300000
       C100-ISSUE-OPEN-REQUEST.                                         02310000
                                                                        02320000
           MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.              02330000
           MOVE PC-JOB-NAME            TO DP000-JOB-NAME.               02340000
           MOVE PC-PLAN-NAME           TO DP000-PLAN-NAME.              02350000
                                                                        02360000
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         02370000
                                                                        02380000
           MOVE DP000-CKPT-TYPE-CODE   TO PV-CHECKPOINT-TYPE.           02390000
                                                                        02400000
                                                                        02410000
      ******************************************************************02420000
      * READ THE UNLOAD INPUT FILE                                      02430000
      ******************************************************************02440000
       C200-READ-UNLOAD-INPUT-FILE.                                     02450000
                                                                        02460000
           READ UNLOAD-FILE INTO XS151-RTV-REC                          02470000
             AT END SET PS-INPUT-UNLOAD-EOF TO TRUE.                    02480000
                                                                        02490000
           IF PS-INPUT-UNLOAD-EOF                                       02500000
               CONTINUE                                                 02510000
           ELSE                                                         02520000
               MOVE XS151-LOC-NBR         TO XS-RTV-LOC-NBR             02530000
               MOVE XS151-XFER-TYP        TO XS-RTV-XFER-TYP            02540000
               MOVE XS151-BEG-DTE         TO XS-RTV-BEG-DTE             02550000
               ADD +1 TO PA-READ                                        02560000
           END-IF.                                                      02570000
                                                                        02580000
                                                                        02590000
      ******************************************************************02600000
      * THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSACATION  02610000
      * RECORD "ON" IF THERE IS A CHANGE IN THE KEY. AFTER              02620000
      * THE TRANSACTION RECORD HAS BEEN FORMATTED, A WRITE REQUEST IS   02630000
      * ISSUED TO THE TRANSACTION PREPARATION MODULE.                   02640000
      ******************************************************************02650000
       C300-CKPT-BY-CNTL-BREAK.                                         02660000
                                                                        02670000
           IF  XS-RTV-KEY      = PV-HOLD-KEY                            02680000
               MOVE 'N' TO DP000-CHECKPOINT-IND                         02690000
           ELSE                                                         02700000
               MOVE 'Y' TO DP000-CHECKPOINT-IND                         02710000
               MOVE XS151-LOC-NBR         TO PV-LOC-NBR                 02720000
               MOVE XS151-XFER-TYP        TO PV-XFER-TYP                02730000
               MOVE XS151-BEG-DTE         TO PV-BEG-DTE                 02740000
           END-IF.                                                      02750000
                                                                        02760000
           MOVE PC-TRANS-RECORD-LENGTH       TO DP000-TRANS-REC-LENGTH. 02770000
           MOVE XS151-RTV-REC                TO DP000-TRANS-REC.        02780000
           MOVE PC-TRAN-PREP-FUNC-WRITE      TO DP000-FUNC-CODE.        02790000
           MOVE PC-JOB-NAME                  TO DP000-JOB-NAME.         02800000
           MOVE PC-PLAN-NAME                 TO DP000-PLAN-NAME.        02810000
                                                                        02820000
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         02830000
                                                                        02840000
           ADD +1 TO PA-WRITTEN.                                        02850000
                                                                        02860000
                                                                        02870000
      ******************************************************************02880000
      * THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSACTION   02890000
      * RECORD "ON".  THE UNLOAD PROGRAM WILL CHECK THE CHECKPOINT      02900000
      * FREQUENCY TO KNOW WHETHER A CHECKPOINT SHOULD BE TAKEN. AFTER   02910000
      * THE TRANSACTION RECORD HAS BEEN FORMATTED, A WRITE REQUEST IS   02920000
      * ISSUED TO THE TRANSACTION PREPARATION MODULE.                   02930000
      ******************************************************************02940000
       C400-CKPT-BY-LOGICAL-TRANS.                                      02950000
                                                                        02960000
           MOVE 'Y'                          TO DP000-CHECKPOINT-IND.   02970000
           MOVE PC-TRANS-RECORD-LENGTH       TO DP000-TRANS-REC-LENGTH. 02980000
           MOVE XS151-RTV-REC                TO DP000-TRANS-REC.        02990000
           MOVE PC-TRAN-PREP-FUNC-WRITE      TO DP000-FUNC-CODE.        03000000
           MOVE PC-JOB-NAME                  TO DP000-JOB-NAME.         03010000
           MOVE PC-PLAN-NAME                 TO DP000-PLAN-NAME.        03020000
                                                                        03030000
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03040000
                                                                        03050000
           ADD +1 TO PA-WRITTEN.                                        03060000
                                                                        03070000
                                                                        03080000
      ******************************************************************03090000
      * THIS ROUTINE BUILDS THE TRANSACTION RECORD AND ISSUES A WRITE   03100000
      * REQUEST TO THE TRANSACTION PREPARATION MODULE IF CHECKPOINTS ARE03110000
      * TAKEN BY TIME INTERVAL (SECONDS) OR REQUESTED BY THE MAINTENANCE03120000
      * PROGRAM.                                                        03130000
      ******************************************************************03140000
       C500-CKPT-BY-TIME-OR-REQUEST.                                    03150000
                                                                        03160000
           MOVE 'N' TO DP000-CHECKPOINT-IND.                            03170000
                                                                        03180000
           MOVE PC-TRANS-RECORD-LENGTH       TO DP000-TRANS-REC-LENGTH. 03190000
           MOVE XS151-RTV-REC                TO DP000-TRANS-REC.        03200000
           MOVE PC-TRAN-PREP-FUNC-WRITE      TO DP000-FUNC-CODE.        03210000
           MOVE PC-JOB-NAME                  TO DP000-JOB-NAME.         03220000
           MOVE PC-PLAN-NAME                 TO DP000-PLAN-NAME.        03230000
                                                                        03240000
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03250000
                                                                        03260000
           ADD +1 TO PA-WRITTEN.                                        03270000
                                                                        03280000
                                                                        03290000
      ******************************************************************03300000
      * ISSUE A 'CLOSE' REQUEST TO THE TRANSACTION PREPARATION MODULE   03310000
      ******************************************************************03320000
       C600-ISSUE-CLOSE-REQUEST.                                        03330000
                                                                        03340000
           MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.             03350000
           MOVE PC-JOB-NAME             TO DP000-JOB-NAME.              03360000
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.             03370000
                                                                        03380000
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03390000
                                                                        03400000
                                                                        03410000
      ******************************************************************03420000
      * VERIFY THE RETURN CODE ON THE CALLED MODULE                     03430000
      ******************************************************************03440000
       Z100-CALL-TRANS-PREP-MODULE.                                     03450000
                                                                        03460000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         03470000
                                                                        03480000
           IF  DP000-GOOD-RET-CODE                                      03490000
               CONTINUE                                                 03500000
                                                                        03510000
           ELSE                                                         03520000
               MOVE 'Z100-CALL-TRANS-PREP-MODULE' TO PV-PARAGRAPH-NAME  03530000
               MOVE '** BAD CALL TO TRANS PREP MODULE **'               03540000
                                                  TO PV-OPERATION-MSG   03550000
               MOVE DP000-RET-CODE                TO PV-RET-CODE        03560000
                                                                        03570000
               SET ABEND-4444-FORCED-ABEND TO TRUE                      03580000
                                                                        03590000
               PERFORM Z200-ABEND                                       03600000
           END-IF.                                                      03610000
                                                                        03620000
                                                                        03630000
      ******************************************************************03640000
      * NON-DB2 ABEND                                                   03650000
      ******************************************************************03660000
       Z200-ABEND.                                                      03670000
                                                                        03680000
           PERFORM Z999-CONTROLS.                                       03690000
                                                                        03700000
           DISPLAY '                     '.                             03710000
           DISPLAY '** ABEND           **'.                             03720000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          03730000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        03740000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         03750000
           DISPLAY '** RETURN CODE     ** = ' PV-RET-CODE.              03760000
                                                                        03770000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03780000
                                                                        03790000
                                                                        03800000
      ******************************************************************03810000
      * PREFORMS TASK TERMINATION PROCESSING                            03820000
      ******************************************************************03830000
       Z990-END-OF-JOB-ROUTINE.                                         03840000
                                                                        03850000
           CLOSE UNLOAD-FILE.                                           03860000
                                                                        03870000
           PERFORM C600-ISSUE-CLOSE-REQUEST.                            03880000
                                                                        03890000
           PERFORM Z999-CONTROLS.                                       03900000
                                                                        03910000
                                                                        03920000
      ******************************************************************03930000
      * DISPLAY CONTROLS FROM THE PROGRAM                               03940000
      ******************************************************************03950000
       Z999-CONTROLS.                                                   03960000
                                                                        03970000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 03980000
                                                                        03990000
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            04000000
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            04010000
                                                                        04020000
           DISPLAY '                    '.                              04030000
           DISPLAY '**********************'.                            04040000
           DISPLAY '   XSKUT135 SUMMARY   '.                            04050000
           DISPLAY '   TRANS PREP DETAILS '.                            04060000
           DISPLAY '**********************'.                            04070000
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         04080000
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          04090000
           DISPLAY 'END TIME  = ' ST-END-TIME.                          04100000
           DISPLAY '======================'.                            04110000
           DISPLAY 'RECORDS READ    = ' PA-READ.                        04120000
           DISPLAY '                  '.                                04130000
           DISPLAY 'RECORDS WRITTEN = ' PA-WRITTEN.                     04140000
           DISPLAY '                  '.                                04150000
                                                                        04160000
                                                                        04170000
      ******************************************************************04180000
      * TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES          04190000
      ******************************************************************04200000
           COPY DPPD000.                                                04210000
                                                                        04220000
      ******************************************************************04230000
