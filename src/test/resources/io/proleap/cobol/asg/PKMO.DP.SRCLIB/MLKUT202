       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT202.                                       
       AUTHOR.                  PAUL KEBER.                                     
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            01/21/2005.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  MLKUT202 - COMMON RECALC CALCULATION PROGRAM                  *        
      *                                                                *        
      *  THIS PROGRAM TAKES IN THE COMMON RECALC EXTRACT AND FILLS IN  *        
      *  THE CALCULATED FIELDS: ENDING-INVENTORY-RETAIL,               *        
      *  CUMULATIVE-MARKUP-PERCENT, ENDING-INVENTORY-COST, SALES-      *        
      *  COST, AND GROSS-MARGIN.                                       *        
      *  THE INPUT SHOULD BE SORTED BY DEPT, MAJ-CLASS, SUB-CLASS,     *        
W28582*  LOCATION, VENDOR (ENT-ID), BRAND (LABEL SEQUENCE #), DATALESS *        
W28582*  KEY, THEN FISCAL DATE.                                        *        
      *                                                                *        
      *  THE SHRINK PRO-RATION LOGIC WILL BE SEPARATED TO MLKUT203.    *        
      *                                                                *        
      *  THIS PROGRAM WILL END WITH A 4095 RETURN CODE IF ANY          *        
      *  CUMULATIVE-MARKUP-PERCENT VALUES ARE BEYOND THE MAXIMUM LIMIT.*        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * W20445   01-21-2005  PAUL KEBER - INITIAL CREATION.            *        
      * W26006   03-09-2005  V. LEE YANG - MAKE THE CMU PERCENT MSG    *        
      *                      INTO A REPORT AND DISPLAY PROC DEPT       *        
      * W23843   03-29-2005  VL YANG - REMOVE SHRINK PRORATION LOGIC   *        
23843L* W23843   03-29-2005  VL YANG - CLARIFY CARRY-FORWARD LOGIC     *        
23843E* W23843   04-14-2005  VL YANG - EXPAND NUMBER OF DIGIT ON COUNT *        
      *                      AND ADD KEY TO DISPLAY OF NO EIR/EIC      *        
      *                      CARRY FORWARD GENERATED.                  *        
X23843* W23843   04-19-2005  VL YANG - ADD TABLE NAME TO CUM PERCENT   *        
      *                      EXCEEDED REPORT.                          *        
W28582* W28582   12-27-2005  C. MCCAMMON - BRAND VENDOR - HANDLE BRAND *        
W28582*                      (LABEL SEQ #) & DATALESS KEY (ML-LWST-ID).*        
      *          10-11-2006  L. MSIKINYA - BRAND VENDOR - CMU% CALC    *        
      *                      CHANGES. CHANGE RECORD LENGTH FROM 324 TO *        
      *                      400. CHANGE CALC-SEASON-TO-DATE-PURCH,    *        
      *                      CALC-CUMM-TO-DATE-PURCH, AND GROSS MARGIN *        
      *                      CALCULATION.                              *        
J28582* W28582   11-15-2006  J. SELWITSCHKA                            *        
      *                      REMOVE PV-SEAS-INV-COST-ADJ1 AND          *        
      *                      PV-SEAS-INV-COST-ADJ2 FROM                *        
      *                      PV-CUM-TO-DATE-PURCH-COST CALCULATION.    *        
      *                      MOVE CALCULATED FIELDS TO MLRD070.        *        
S28582* W28582   12-20-2006  J. SELWITSCHKA                            *        
      *                      CARRY FORWARD SEASON TO DATE INVENTORY    *        
      *                      COST ADJUSTMENTS TO EIC AND ADJUST        *        
      *                      CUM MARKUP PERCENT.                       *        
A28582* W28582   01-06-2007  J. SELWITSCHKA                            *        
      *                      CALCULATE CUM-TO-DATE-PURCH-COST USING    *        
      *                      PREVIOUS EIC AND PREVIOUS EIR.            *        
      *                      REMOVE PREVIOUS CARRY FORWARD CHANGE.     *        
B28582* W28582   01-08-2007  ROD JANSSEN                                        
B28582*                      AIM/BP EIR ISSUE AND DATA INTEGRITY.....           
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT REQUEST-CNTL-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT COMMON-RECALC-INPUT                                           
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT COMMON-RECALC-EXTRACT                                         
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT RECALC-MESSAGE-REPORT                                         
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  REQUEST-CNTL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  REQUEST-CNTL-REC                PIC X(080).                          
                                                                                
       FD  COMMON-RECALC-INPUT                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  COMMON-RECALC-IN-REC            PIC X(400).                          
      *01  COMMON-RECALC-IN-REC            PIC X(324).                          
                                                                                
       FD  COMMON-RECALC-EXTRACT                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  COMMON-RECALC-REC               PIC X(400).                          
      *01  COMMON-RECALC-REC               PIC X(324).                          
                                                                                
       FD  RECALC-MESSAGE-REPORT                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
26006      RECORD CONTAINS 100 CHARACTERS                                       
26006      DATA RECORD IS RECALC-MESSAGE-REC.                                   
26006  01  RECALC-MESSAGE-REC              PIC X(100).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-PROGRAM-NAME             PIC  X(08) VALUE 'MLKUT202'.         
           05  PC-MAX-RETRIES              PIC S9(04)      VALUE +3             
                               COMP.                                            
26006      05  PC-MAX-RPT-LINES            PIC S9(05)      VALUE +75            
26006                          COMP-3.                                          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-REQUEST-CNTL      PIC X           VALUE 'N'.           
               88  END-OF-REQUEST-CNTL                     VALUE 'Y'.           
               88  NOT-END-OF-REQUEST-CNTL                 VALUE 'N'.           
           05  PS-END-OF-COMMON-RECALC     PIC X           VALUE 'N'.           
               88  END-OF-COMMON-RECALC                    VALUE 'Y'.           
               88  NOT-END-OF-COMMON-RECALC                VALUE 'N'.           
           05  PS-END-OF-PROCESS-RECALC    PIC X           VALUE 'N'.           
               88  END-OF-PROCESS-RECALC                   VALUE 'Y'.           
               88  NOT-END-OF-PROCESS-RECALC               VALUE 'N'.           
           05  PS-FISCAL-PROCESSING-SW     PIC X           VALUE ' '.           
               88  PS-FISCAL-MN-PROCESSING                 VALUE 'M'.           
               88  PS-FISCAL-WK-PROCESSING                 VALUE 'W'.           
           05  PS-END-OF-CARRY-FWD         PIC X           VALUE 'N'.           
               88  END-OF-CARRY-FWD                        VALUE 'Y'.           
               88  NOT-END-OF-CARRY-FWD                    VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLES BEGIN ***'.                               
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
26006      05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
26006      05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
26006          10  PV-PROCESSING-CC        PIC X(02).                           
26006          10  PV-PROCESSING-YY        PIC X(02).                           
26006          10  PV-PROC-DSH1            PIC X(01).                           
26006          10  PV-PROCESSING-MM        PIC X(02).                           
26006          10  PV-PROC-DSH2            PIC X(01).                           
26006          10  PV-PROCESSING-DD        PIC X(02).                           
26006      05  PV-TODAYS-DATE.                                                  
26006          10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
26006          10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
26006          10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
26006      05  PV-TODAYS-TIME.                                                  
26006          10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
26006          10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
26006          10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
26006          10  FILLER                  PIC 9(02)       VALUE ZEROES.        
26006      05  PV-RPT-LINE                 PIC S9(05)      VALUE ZEROES         
26006                          COMP-3.                                          
26006      05  PV-RPT-PAGE                 PIC S9(05)      VALUE ZEROES         
26006                          COMP-3.                                          
           05  PV-REQUEST-COUNT            PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-CURR-EXTRACTED           PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-CURR-READ                PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-CARRY-FWD                PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SQL-SUB                  PIC S9(03)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OPERATION-ATTEMPTED      PIC X(30)       VALUE SPACES.        
           05  PV-PARAGRAPH-NAME           PIC X(30)       VALUE SPACES.        
           05  PV-CUM-MKUP-PCT-MAX         PIC S9(9)V9(7)  VALUE ZEROES         
                               COMP-3.                                          
A28582     05  PV-PREV-CUM-MKUP-PCT        PIC S9(4)V9(7)  VALUE ZEROES         
A28582                         COMP-3.                                          
A28582*    05  PV-SEAS-INV-COST-ADJ        PIC S9(11)V9(2) VALUE ZEROES         
A28582*                        COMP-3.                                          
           05  PV-FIRST-FSCL-END-DTE       PIC X(10)       VALUE SPACES.        
           05  PV-KEY-DATA.                                                     
23843L         10  PV-PREV-FSCL-END-DTE    PIC X(10)       VALUE SPACES.        
23843L         10  PV-PREV-DEPT-NBR        PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
23843L         10  PV-PREV-MAJ-CL-NBR      PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
23843L         10  PV-PREV-SUB-CL-NBR      PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
23843L         10  PV-PREV-LOC-NBR         PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
23843L         10  PV-PREV-ENT-ID          PIC S9(9)       VALUE ZEROES         
                               COMP.                                            
W28582         10  PV-PREV-LBL-SEQ-NBR     PIC S9(9)       VALUE ZEROES         
W28582                         COMP.                                            
W28582         10  PV-PREV-ML-LWST-ID      PIC S9(9)       VALUE ZEROES         
W28582                         COMP.                                            
           05  PV-CUM-TO-DATE-VARIABLES.                                        
               10  PV-CUM-TO-DATE-PURCH-RETAIL                                  
                                           PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-CUM-TO-DATE-PURCH-COST                                    
                                           PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-CUM-SEASON-MKUP      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-PREV-FISCAL-DATA.                                             
               10  PV-PREV-END-INV-RTL-AMT PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-PREV-END-INV-COST-AMT                                     
                                           PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  FILLER                  PIC X(18)       VALUE SPACES.        
           05  PV-SEASON-TO-DATE-DATA.                                          
               10  PV-SEAS-RCPT-RTL-AMT PIC S9(11)V9(2) VALUE ZEROES            
                               COMP-3.                                          
               10  PV-SEAS-RCPT-NET-COST-AMT                                    
                                           PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-PADJ-RTL-AMT PIC S9(11)V9(2) VALUE ZEROES            
                               COMP-3.                                          
               10  PV-SEAS-PADJ-NET-COST-AMT                                    
                                           PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-MKUP-RTL-AMT PIC S9(11)V9(2) VALUE ZEROES            
                               COMP-3.                                          
               10  PV-SEAS-XFER-IN-RTL-AMT                                      
                                           PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-XFER-OUT-RTL-AMT                                     
                                           PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-INV-COST-ADJ1                                        
                                           PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-INV-COST-ADJ2                                        
                                           PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** ABEND AREA BEGIN ***'.                                          
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  TABLE-AREA-ABEND                            VALUE +4002.         
           88  LOGICAL-ISSUE-ABEND                         VALUE +4012.         
           88  SQL-ABEND-CODE                              VALUE +4013.         
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** REPORT REQUEST CONTROL REC ***'.                                
                                                                                
       01  RQ-CNTL-TYPE-AREA.                                                   
           05  RQ-CNTL-TYPE-IND            PIC  X(01)      VALUE SPACE.         
               88  RQ-COMMENTS                             VALUE '*'.           
           05  RQ-CNTL-TYPE-SW             PIC  X(08)      VALUE SPACES.        
               88  RQ-CNTL-TYPE-WEEKLY                     VALUE                
                   'WEEKLY  '.                                                  
               88  RQ-CNTL-TYPE-MONTHLY                    VALUE                
                   'MONTHLY '.                                                  
           05  FILLER                      PIC  X(01)      VALUE SPACE.         
           05  RQ-CNTL-TYPE-DATE           PIC  X(10)      VALUE SPACES.        
           05  FILLER REDEFINES RQ-CNTL-TYPE-DATE.                              
               10  RQ-CNTL-TYPE-DATE-CCYY  PIC  X(04).                          
               10  FILLER                  PIC  X(01).                          
                   88  RQ-CNTL-TYPE-DATE-VALID-DASH-1      VALUE '-'.           
               10  RQ-CNTL-TYPE-DATE-MM    PIC  X(02).                          
               10  FILLER                  PIC  X(01).                          
                   88  RQ-CNTL-TYPE-DATE-VALID-DASH-2      VALUE '-'.           
               10  RQ-CNTL-TYPE-DATE-DD    PIC  X(02).                          
X23843     05  FILLER                      PIC  X(01)      VALUE SPACE.         
X23843     05  RQ-CNTL-TABLE-NAME          PIC  X(18)      VALUE SPACES.        
X23843     05  FILLER                      PIC  X(41)      VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** INPUT/OUTPUT RECORD AREA ***'.                                  
                                                                                
      ******************************************************************        
      *  NEXT COMMON RECALC DATA EXTRACT RECORD TO PROCESS                      
      ******************************************************************        
       01  PV-NEXT-RECALC-INPUT-REC.                                            
           05  FILLER                      PIC X(08)       VALUE SPACES.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  PV-NEXT-FSCL-END-DTE        PIC X(10)       VALUE SPACES.        
           05  PV-NEXT-DEPT-NBR            PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NEXT-MAJ-CL-NBR          PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NEXT-SUB-CL-NBR          PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NEXT-LOC-NBR             PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
           05  PV-NEXT-ENT-ID              PIC S9(9)       VALUE ZEROES         
                               COMP.                                            
W28582     05  FILLER                      PIC X(02)       VALUE SPACES.        
W28582     05  PV-NEXT-LBL-SEQ-NBR         PIC S9(9)       VALUE ZEROES         
W28582                         COMP.                                            
W28582     05  PV-NEXT-ML-LWST-ID          PIC S9(9)       VALUE ZEROES         
W28582                         COMP.                                            
W28582     05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(032)      VALUE SPACES.        
           05  FILLER                      PIC X(124)      VALUE SPACES.        
           05  FILLER                      PIC X(064)      VALUE SPACES.        
           05  FILLER                      PIC X(024)      VALUE SPACES.        
           05  FILLER                      PIC X(024)      VALUE SPACES.        
           05  FILLER                      PIC X(085)      VALUE SPACES.        
                                                                                
      ******************************************************************        
      *  COMMON RECALC DATA EXTRACT                                             
      ******************************************************************        
      *01  ML070-COMMON-RECALC-EXTRACT.                                         
           COPY MLRD070.                                                        
                                                                                
B28582******************************************************************        
B28582*  STANDARD KOHL'S DATE ROUTINE WORK AREA.                                
B28582******************************************************************        
B28582     COPY DPWS500.                                                        
B28582                                                                          
26006 ******************************************************************        
26006 *  STANDARD KOHL'S REPORT HEADER                                          
26006 ******************************************************************        
26006 *01  DP100O-STANDRD-HEADER-100-COLS.                                      
26006 *01  DP100O-CNFDL-HDR-100-COLS.                                           
26006      COPY DPWS100O.                                                       
                                                                                
       01  RP-RECALC-REPORT-AREA.                                               
X23843     05  RP-RECALC-REPORT-TITLE-1.                                        
               10  FILLER                  PIC X(31)       VALUE SPACES.        
               10  FILLER                  PIC X(39)       VALUE                
                   'MERCHANDISE LEDGER CMU% EXCEEDED REPORT'.                   
               10  FILLER                  PIC X(31)       VALUE SPACES.        
                                                                                
X23843     05  RP-RECALC-REPORT-TITLE-2.                                        
X23843         10  FILLER                  PIC X(36)       VALUE SPACES.        
X23843         10  FILLER                  PIC X(11)       VALUE                
X23843             'FOR TABLE: '.                                               
X23843         10  RP-TABLE-NAME           PIC X(18)       VALUE SPACES.        
X23843         10  FILLER                  PIC X(36)       VALUE SPACES.        
                                                                                
           05  RP-RECALC-REPORT-HEADER.                                         
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  FILLER                  PIC X(04)       VALUE 'DEPT'.        
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  FILLER                  PIC X(04)       VALUE 'MCLS'.        
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  FILLER                  PIC X(04)       VALUE 'SCLS'.        
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  FILLER                  PIC X(04)       VALUE 'LOCS'.        
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  FILLER                  PIC X(09)       VALUE                
                   '  ENT-ID '.                                                 
W28582         10  FILLER                  PIC X(01)       VALUE SPACES.        
W28582         10  FILLER                  PIC X(09)       VALUE                
W28582             'LBL SEQ# '.                                                 
W28582         10  FILLER                  PIC X(01)       VALUE SPACES.        
W28582         10  FILLER                  PIC X(09)       VALUE                
W28582             ' BV KEY  '.                                                 
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  FILLER                  PIC X(10)       VALUE                
                   'FISCAL DTE'.                                                
               10  FILLER                  PIC X(02)       VALUE SPACES.        
               10  FILLER                  PIC X(20)       VALUE                
                   '  CALC CUM MARKUP % '.                                      
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  FILLER                  PIC X(14)       VALUE                
                   'USED CUM MKUP '.                                            
26006          10  FILLER                  PIC X(22)       VALUE SPACES.        
                                                                                
           05  RP-RECALC-REPORT-DETAIL.                                         
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  RP-DEPT-NBR             PIC Z999        VALUE ZEROES.        
X23843         10  RP-DEPT-NBR-X REDEFINES RP-DEPT-NBR                          
X23843                                     PIC X(04).                           
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  RP-MAJ-CL-NBR           PIC Z999        VALUE ZEROES.        
X23843         10  RP-MAJ-CL-NBR-X REDEFINES RP-MAJ-CL-NBR                      
X23843                                     PIC X(04).                           
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  RP-SUB-CL-NBR           PIC Z999        VALUE ZEROES.        
X23843         10  RP-SUB-CL-NBR-X REDEFINES RP-SUB-CL-NBR                      
X23843                                     PIC X(04).                           
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  RP-LOC-NBR              PIC 9999        VALUE ZEROES.        
X23843         10  RP-LOC-NBR-X REDEFINES RP-LOC-NBR                            
X23843                                     PIC X(04).                           
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  RP-ENT-ID               PIC ZZZZZZ999   VALUE ZEROES.        
X23843         10  RP-ENT-ID-X REDEFINES RP-ENT-ID                              
X23843                                     PIC X(09).                           
W28582         10  FILLER                  PIC X(01)       VALUE SPACES.        
W28582         10  RP-LBL-SEQ-NBR          PIC ZZZZZZ999   VALUE ZEROES.        
W28582         10  RP-LBL-SEQ-NBR-X REDEFINES RP-LBL-SEQ-NBR                    
W28582                                     PIC X(09).                           
W28582         10  FILLER                  PIC X(01)       VALUE SPACES.        
W28582         10  RP-ML-LWST-ID           PIC ZZZZZZ999   VALUE ZEROES.        
W28582         10  RP-ML-LWST-ID-X REDEFINES RP-ML-LWST-ID                      
W28582                                     PIC X(09).                           
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  RP-FSCL-END-DTE         PIC X(10)       VALUE SPACES.        
               10  FILLER                  PIC X(02)       VALUE SPACES.        
               10  RP-CUM-CALC             PIC ZZZ,ZZZ,ZZ9.9(7)-                
                                                           VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  RP-CUM-USED             PIC Z,ZZ9.9(7)- VALUE ZEROES.        
               10  FILLER                  PIC X(02)       VALUE SPACES.        
26006          10  FILLER                  PIC X(22)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
      ****************************************************                      
      *  M/L DATE TABLE                                  *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      *  COMMUNICATION AREAS FOR DB2                     *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  ERROR MESSAGE AREA FOR DB2                                             
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ****************************************************                      
      *  ML CARRY FOWARD DATE CURSOR                                            
      ****************************************************                      
           EXEC SQL  DECLARE CARRY-FWD-CSR CURSOR FOR                           
               SELECT DISTINCT                                                  
                       FSCL_WK_END_DTE                                          
                 FROM                                                           
                       TDTATTR                                                  
                 WHERE                                                          
                       KY_DTE         >  :ML070-FSCL-END-DTE                    
B28582             AND KY_DTE <= :DTATTR-KY-DTE                                 
B28582********     AND KY_DTE <= :RQ-CNTL-TYPE-DATE                             
                 ORDER BY                                                       
                       FSCL_WK_END_DTE                                          
                 WITH UR                                                        
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
           PERFORM 4010-READ-COMMON-RECALC.                                     
           IF END-OF-COMMON-RECALC                                              
               SET END-OF-PROCESS-RECALC    TO TRUE                             
           ELSE                                                                 
               MOVE ML070-FSCL-END-DTE      TO PV-FIRST-FSCL-END-DTE            
           END-IF.                                                              
                                                                                
           PERFORM 2000-PROCESS-RECALC                                          
               UNTIL END-OF-PROCESS-RECALC.                                     
                                                                                
           PERFORM 3000-FINALIZE.                                               
                                                                                
26006      IF PV-RPT-PAGE > 0                                                   
              MOVE 4095 TO RETURN-CODE                                          
           ELSE                                                                 
              MOVE 0    TO RETURN-CODE                                          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  REQUEST-CNTL-FILE                                        
                       COMMON-RECALC-INPUT                                      
                OUTPUT COMMON-RECALC-EXTRACT                                    
                       RECALC-MESSAGE-REPORT.                                   
                                                                                
26006      ACCEPT PV-TODAYS-DATE FROM DATE.                                     
26006      ACCEPT PV-TODAYS-TIME FROM TIME.                                     
26006                                                                           
26006      MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
26006      MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
26006      MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
26006                                                                           
26006      MOVE '-' TO PV-PROC-DSH1.                                            
26006      MOVE '-' TO PV-PROC-DSH2.                                            
26006                                                                           
26006      IF PV-TODAYS-DATE-YY > '80'                                          
26006          MOVE '19' TO PV-PROCESSING-CC                                    
26006      ELSE                                                                 
26006          MOVE '20' TO PV-PROCESSING-CC                                    
26006      END-IF.                                                              
26006                                                                           
26006      MOVE PV-TODAYS-DATE-MM  TO DP100O-RUN-MONTH.                         
26006      MOVE PV-TODAYS-DATE-DD  TO DP100O-RUN-DAY.                           
26006      MOVE PV-TODAYS-DATE-YY  TO DP100O-RUN-YEAR.                          
26006      MOVE PV-TODAYS-TIME-HR  TO DP100O-RUN-HOUR.                          
26006      MOVE PV-TODAYS-TIME-MIN TO DP100O-RUN-MINUTE.                        
26006      MOVE PC-PROGRAM-NAME    TO DP100O-PROGRAM-NAME.                      
26006      MOVE '1'                TO DP100O-REPORT-NUMBER.                     
                                                                                
           DISPLAY '********************************************'.              
           DISPLAY '*** MLKUT202 MESSAGES'.                                     
           DISPLAY '********************************************'.              
           DISPLAY '*** CONTROL/FISCAL DATES BEING PROCESSED.'                  
                                                                                
           PERFORM 1200-PROCESS-RQ-CNTL-TYPE.                                   
                                                                                
           IF PV-REQUEST-COUNT > ZEROES                                         
               IF PV-REQUEST-COUNT > +1                                         
                   DISPLAY ' ** MULTIPLE REQUEST CONTROL: '                     
                           PV-REQUEST-COUNT                                     
                   DISPLAY ' ** USE THE LAST REQUEST'                           
               END-IF                                                           
X23843         MOVE RQ-CNTL-TABLE-NAME   TO RP-TABLE-NAME                       
           ELSE                                                                 
               DISPLAY ' *** NO CONTROL/FISCAL DATE TO PROCESS.'                
               MOVE '1000-HOUSEKEEPING'  TO PV-PARAGRAPH-NAME                   
               SET LOGICAL-ISSUE-ABEND   TO TRUE                                
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           IF RQ-CNTL-TYPE-WEEKLY                                               
               SET PS-FISCAL-WK-PROCESSING TO TRUE                              
           ELSE                                                                 
               SET PS-FISCAL-MN-PROCESSING TO TRUE                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * PROCESS REQUEST CONTROL CARD AND/OR GET REQUEST DATES                   
      ******************************************************************        
       1200-PROCESS-RQ-CNTL-TYPE.                                               
                                                                                
           DISPLAY 'EXTRACT REQUESTED:'.                                        
           DISPLAY '  ----+----1----+----2----+----3----+----4'.                
           PERFORM 4000-READ-REQUEST-CNTL.                                      
                                                                                
           MOVE ZEROES             TO PV-REQUEST-COUNT.                         
                                                                                
           PERFORM                                                              
               UNTIL END-OF-REQUEST-CNTL                                        
                                                                                
               DISPLAY '  ' RQ-CNTL-TYPE-AREA                                   
               IF RQ-COMMENTS                                                   
                   CONTINUE                                                     
               ELSE                                                             
                   IF RQ-CNTL-TYPE-DATE-CCYY IS NUMERIC                         
                       AND RQ-CNTL-TYPE-DATE-MM IS NUMERIC                      
                       AND RQ-CNTL-TYPE-DATE-DD IS NUMERIC                      
                       AND RQ-CNTL-TYPE-DATE-VALID-DASH-1                       
                       AND RQ-CNTL-TYPE-DATE-VALID-DASH-2                       
                       ADD +1                  TO PV-REQUEST-COUNT              
                       DISPLAY ' ** TYPE: ' RQ-CNTL-TYPE-SW                     
                               ', DATE: ' RQ-CNTL-TYPE-DATE                     
                   END-IF                                                       
               END-IF                                                           
                                                                                
               PERFORM 4000-READ-REQUEST-CNTL                                   
                                                                                
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      * PROCESS THE CURRENT DATA                                                
      ******************************************************************        
       2000-PROCESS-RECALC.                                                     
                                                                                
           MOVE PV-NEXT-RECALC-INPUT-REC TO ML070-COMMON-RECALC-EXTRACT.        
      *    READ AHEAD TO THE NEXT RECORD TO DETERMINE KEY DATA BREAK            
      *    FOR SHRINK RETAIL AMOUNT CALCULATION.                                
           PERFORM 4010-READ-COMMON-RECALC.                                     
                                                                                
23843L     IF ML070-DEPT-NBR   NOT = PV-PREV-DEPT-NBR                           
26006          DISPLAY ' PROCESSING DEPT/DATE ' ML070-DEPT-NBR                  
26006                  '/' ML070-FSCL-END-DTE                                   
26006                  ' ... ' FUNCTION CURRENT-DATE                            
26006      END-IF.                                                              
                                                                                
           IF PS-FISCAL-WK-PROCESSING                                           
23843L         AND ML070-DEPT-NBR   = PV-PREV-DEPT-NBR                          
23843L         AND ML070-MAJ-CL-NBR = PV-PREV-MAJ-CL-NBR                        
23843L         AND ML070-SUB-CL-NBR = PV-PREV-SUB-CL-NBR                        
23843L         AND ML070-LOC-NBR    = PV-PREV-LOC-NBR                           
23843L         AND ML070-ENT-ID     = PV-PREV-ENT-ID                            
W28582         AND ML070-LBL-SEQ-NBR = PV-PREV-LBL-SEQ-NBR                      
W28582         AND ML070-ML-LWST-ID  = PV-PREV-ML-LWST-ID                       
23843L         MOVE PV-PREV-END-INV-RTL-AMT                                     
23843L             TO ML070-PREV-END-INV-RTL-AMT                                
23843L         MOVE PV-PREV-END-INV-COST-AMT                                    
23843L             TO ML070-PREV-END-INV-COST-AMT                               
23843L     ELSE                                                                 
23843L         MOVE ML070-PREV-END-INV-RTL-AMT                                  
23843L             TO PV-PREV-END-INV-RTL-AMT                                   
23843L         MOVE ML070-PREV-END-INV-COST-AMT                                 
23843L             TO PV-PREV-END-INV-COST-AMT                                  
23843L     END-IF.                                                              
                                                                                
23843L     MOVE ML070-FSCL-END-DTE  TO PV-PREV-FSCL-END-DTE.                    
23843L     MOVE ML070-DEPT-NBR      TO PV-PREV-DEPT-NBR.                        
23843L     MOVE ML070-MAJ-CL-NBR    TO PV-PREV-MAJ-CL-NBR.                      
23843L     MOVE ML070-SUB-CL-NBR    TO PV-PREV-SUB-CL-NBR.                      
23843L     MOVE ML070-LOC-NBR       TO PV-PREV-LOC-NBR.                         
23843L     MOVE ML070-ENT-ID        TO PV-PREV-ENT-ID.                          
W28582     MOVE ML070-LBL-SEQ-NBR   TO PV-PREV-LBL-SEQ-NBR.                     
W28582     MOVE ML070-ML-LWST-ID    TO PV-PREV-ML-LWST-ID.                      
                                                                                
      *** CALCULATE THE ENDING INVENTORY RETAIL AMOUNT.                         
           PERFORM 2200-CALC-ENDINV-RETAIL.                                     
                                                                                
      *** CALCULATE THE SEASON TO DATE PURCHASES.                               
           PERFORM 2300-CALC-SEASON-TO-DATE-PURCH.                              
                                                                                
      *** CALCULATE THE CUMULATIVE TO DATE PURCHASES.                           
           PERFORM 2400-CALC-CUMM-TO-DATE-PURCH.                                
                                                                                
      *** CALCULATE THE CUMULATIVE MARKUP PERCENT                               
           PERFORM 2500-CALC-CUMM-MARKUP-PCT.                                   
                                                                                
      *** CALCULATE THE ENDING INVENTORY COST AMOUNT                            
           PERFORM 2600-CALC-END-INV-COST.                                      
                                                                                
      *** CALCULATE THE SALES COST AMOUNT                                       
           PERFORM 2700-CALC-SALES-COST-AMT.                                    
                                                                                
      *** CALCULATE THE GROSS MARGIN AMOUNT                                     
           PERFORM 2800-CALC-GROSS-MARGIN-AMT.                                  
                                                                                
           PERFORM 5000-WRITE-COMMON-RECALC.                                    
                                                                                
      *** SAVE ENDING VALUES OF CURRENT WEEK FOR NEXT WEEK'S PREVIOUS           
           MOVE ML070-CURR-END-INV-RTL-AMT  TO PV-PREV-END-INV-RTL-AMT.         
           MOVE ML070-CURR-END-INV-COST-AMT TO PV-PREV-END-INV-COST-AMT.        
                                                                                
23843L* WEEKLY PROCESS?                                                         
23843L     IF PS-FISCAL-WK-PROCESSING                                           
23843L* CHANGE IN MBS HIERARCHY?                                                
23843L         IF ML070-DEPT-NBR       NOT = PV-NEXT-DEPT-NBR                   
23843L             OR ML070-MAJ-CL-NBR NOT = PV-NEXT-MAJ-CL-NBR                 
23843L             OR ML070-SUB-CL-NBR NOT = PV-NEXT-SUB-CL-NBR                 
23843L             OR ML070-LOC-NBR    NOT = PV-NEXT-LOC-NBR                    
23843L             OR ML070-ENT-ID     NOT = PV-NEXT-ENT-ID                     
W28582             OR ML070-LBL-SEQ-NBR NOT = PV-NEXT-LBL-SEQ-NBR               
W28582             OR ML070-ML-LWST-ID  NOT = PV-NEXT-ML-LWST-ID                
23843L* GENERATE CARRY FORWARD ROW, IF WE HAVE NOT REACH THE END                
23843E* OF THE RECALC PERIODS, I.E. CURRENT ROW DATE 2005-01-15 AND             
23843L* REQUEST WEEKEND DATE OF 2005-01-29.                                     
23843E             IF ML070-FSCL-END-DTE < RQ-CNTL-TYPE-DATE                    
23843L                 IF ML070-CURR-END-INV-RTL-AMT = ZEROES                   
23843L                     AND ML070-CURR-END-INV-COST-AMT = ZEROES             
B28582                      NEXT SENTENCE                                       
                       ELSE                                                     
B28582                     MOVE RQ-CNTL-TYPE-DATE  TO DTATTR-KY-DTE             
                           PERFORM 4400-OPEN-CARRY-FWD-CSR                      
                           PERFORM 4450-FETCH-CARRY-FWD-CSR                     
                           PERFORM 2900-CARRY-FORWARD                           
                               UNTIL END-OF-CARRY-FWD                           
                           PERFORM 4475-CLOSE-CARRY-FWD-CSR                     
                       END-IF                                                   
                   END-IF                                                       
B28582* GENERATE CARRY FORWARD ROW, TO FILL A GAP IN FISCAL PERIODS             
B28582* BETWEEN TO FISCAL DATES, I.E. CURRENT ROW DATE 2005-01-08 AND           
B28582* REQUEST WEEKEND DATE OF 2005-01-22. IF ROW DATE IS 01-15 AND            
B28582* NEXT DATE IS 01-22, NORMAL RECALC LOGIC WILL HANDLE THE SCENARIO        
B28582         ELSE                                                             
B28582         IF ML070-FSCL-END-DTE NOT = PV-NEXT-FSCL-END-DTE                 
B28582           PERFORM 3100-CALC-DATES                                        
B28582           IF ML070-FSCL-END-DTE < DPG55-DB2-ISO-DATE-FMT                 
B28582             IF ML070-CURR-END-INV-RTL-AMT = ZEROES  AND                  
B28582                ML070-CURR-END-INV-COST-AMT = ZEROES                      
B28582               NEXT SENTENCE                                              
B28582             ELSE                                                         
B28582               MOVE DPG55-DB2-ISO-DATE-FMT TO DTATTR-KY-DTE               
B28582               IF DTATTR-KY-DTE > RQ-CNTL-TYPE-DATE                       
B28582                 MOVE RQ-CNTL-TYPE-DATE  TO DTATTR-KY-DTE                 
B28582               END-IF                                                     
B28582               PERFORM 4400-OPEN-CARRY-FWD-CSR                            
B28582               PERFORM 4450-FETCH-CARRY-FWD-CSR                           
B28582               PERFORM 2900-CARRY-FORWARD                                 
B28582                   UNTIL END-OF-CARRY-FWD                                 
B28582               PERFORM 4475-CLOSE-CARRY-FWD-CSR                           
B28582             END-IF                                                       
B28582           END-IF                                                         
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF END-OF-COMMON-RECALC                                              
               SET END-OF-PROCESS-RECALC    TO TRUE                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2200-CALC-ENDINV-RETAIL.                                                
      * CALCULATE THE ENDING INVENTORY OF CURRENT WEEK BASED ON THE             
      * ENDING INVENTORY FROM THE PRIOR WEEK                                    
      ******************************************************************        
       2200-CALC-ENDINV-RETAIL.                                                 
                                                                                
           COMPUTE ML070-CURR-END-INV-RTL-AMT =                                 
                   (  PV-PREV-END-INV-RTL-AMT                                   
                    + ML070-CURR-RCPT-RTL-AMT                                   
                    + ML070-CURR-PADJ-RTL-AMT                                   
                    + ML070-CURR-MKUP-RTL-AMT                                   
                    + ML070-CURR-XFER-IN-RTL-AMT                                
                    - ML070-CURR-SLS-RTL-AMT                                    
                    - ML070-CURR-XFER-OUT-RTL-AMT                               
                    - ML070-CURR-MKDN-RTL-AMT                                   
                    - ML070-CURR-INV-ADJ-RTL-AMT                                
                    - ML070-CURR-SHNK-RTL-AMT ).                                
                                                                                
      ******************************************************************        
      * 2300-CALC-SEASON-TO-DATE-PURCH.                                         
      * CALCULATE CURRENT SEASON RETAIL/COST AMTS FROM SEASON BEGINNING         
      ******************************************************************        
       2300-CALC-SEASON-TO-DATE-PURCH.                                          
                                                                                
           ADD ML070-CURR-RCPT-RTL-AMT                                          
               ML070-SEAS-RCPT-RTL-AMT                                          
                 GIVING PV-SEAS-RCPT-RTL-AMT.                                   
                                                                                
           ADD ML070-CURR-RCPT-NET-COST-AMT                                     
               ML070-SEAS-RCPT-NET-COST-AMT                                     
               GIVING PV-SEAS-RCPT-NET-COST-AMT.                                
                                                                                
           ADD ML070-INV-COST-ADJ1                                              
               ML070-SEAS-INV-COST-ADJ1                                         
               GIVING PV-SEAS-INV-COST-ADJ1.                                    
                                                                                
           ADD ML070-INV-COST-ADJ2                                              
               ML070-SEAS-INV-COST-ADJ2                                         
               GIVING PV-SEAS-INV-COST-ADJ2.                                    
                                                                                
           ADD ML070-CURR-PADJ-RTL-AMT                                          
               ML070-SEAS-PADJ-RTL-AMT                                          
               GIVING PV-SEAS-PADJ-RTL-AMT.                                     
                                                                                
           ADD ML070-CURR-PADJ-NET-COST-AMT                                     
               ML070-SEAS-PADJ-NET-COST-AMT                                     
               GIVING PV-SEAS-PADJ-NET-COST-AMT.                                
                                                                                
           ADD ML070-CURR-MKUP-RTL-AMT                                          
               ML070-SEAS-MKUP-RTL-AMT                                          
               GIVING PV-SEAS-MKUP-RTL-AMT.                                     
                                                                                
           ADD ML070-CURR-XFER-IN-RTL-AMT                                       
               ML070-SEAS-XFER-IN-RTL-AMT                                       
               GIVING PV-SEAS-XFER-IN-RTL-AMT.                                  
                                                                                
           ADD ML070-CURR-XFER-OUT-RTL-AMT                                      
               ML070-SEAS-XFER-OUT-RTL-AMT                                      
               GIVING PV-SEAS-XFER-OUT-RTL-AMT.                                 
                                                                                
      ******************************************************************        
      * 2400-CALC-CUMM-TO-DATE-PURCH.                                           
      * DETERMINE CUMULATIVE RETAIL AND COST                                    
      * ADD PRIOR SEASON END INV AMTS TO CURRENT SEASON-TO-DATE AMTS            
      ******************************************************************        
       2400-CALC-CUMM-TO-DATE-PURCH.                                            
                                                                                
           COMPUTE PV-CUM-TO-DATE-PURCH-RETAIL =                                
                                          (  ML070-BEG-SEAS-INV-RTL-AMT         
                                           + PV-SEAS-XFER-IN-RTL-AMT            
                                           - PV-SEAS-XFER-OUT-RTL-AMT           
                                           + PV-SEAS-RCPT-RTL-AMT               
                                           + PV-SEAS-PADJ-RTL-AMT               
                                           + PV-SEAS-MKUP-RTL-AMT).             
                                                                                
A28582*    COMPUTE PV-CUM-TO-DATE-PURCH-COST   =                                
A28582*                                  (  ML070-BEG-SEAS-INV-COST-AMT         
A28582*                                   + PV-SEAS-RCPT-NET-COST-AMT           
A28582*                                   + PV-SEAS-PADJ-NET-COST-AMT).         
J28582*                                   + PV-SEAS-PADJ-NET-COST-AMT           
J28582*                                   + PV-SEAS-INV-COST-ADJ1               
J28582*                                   + PV-SEAS-INV-COST-ADJ2).             
A28582     IF  ML070-SEAS-INV-COST-ADJ1 = ZEROES                                
A28582     AND ML070-SEAS-INV-COST-ADJ2 = ZEROES                                
A28582          COMPUTE PV-CUM-TO-DATE-PURCH-COST   =                           
A28582                                   (  ML070-BEG-SEAS-INV-COST-AMT         
A28582                                    + PV-SEAS-RCPT-NET-COST-AMT           
A28582                                    + PV-SEAS-PADJ-NET-COST-AMT)          
A28582     ELSE                                                                 
A28582* PREVIOUS EIR CANNOT EQUAL ZERO IN DIVISOR ('ON SIZE ERROR')             
A28582         IF ML070-PREV-END-INV-RTL-AMT = 0                                
A28582             MOVE 1 TO PV-PREV-CUM-MKUP-PCT                               
A28582         ELSE                                                             
A28582             COMPUTE PV-PREV-CUM-MKUP-PCT ROUNDED =                       
A28582                     1 - ( ML070-PREV-END-INV-COST-AMT /                  
A28582                           ML070-PREV-END-INV-RTL-AMT )                   
A28582         END-IF                                                           
A28582         COMPUTE PV-CUM-TO-DATE-PURCH-COST ROUNDED =                      
A28582             ((  PV-CUM-TO-DATE-PURCH-RETAIL                              
A28582               - ML070-CURR-XFER-IN-RTL-AMT                               
A28582               + ML070-CURR-XFER-OUT-RTL-AMT                              
A28582               - ML070-CURR-RCPT-RTL-AMT                                  
A28582               - ML070-CURR-PADJ-RTL-AMT                                  
A28582               - ML070-CURR-MKUP-RTL-AMT)                                 
A28582               * ( 1 - PV-PREV-CUM-MKUP-PCT )) +                          
A28582              (  ML070-CURR-RCPT-NET-COST-AMT                             
A28582               + ML070-CURR-PADJ-NET-COST-AMT )                           
A28582     END-IF.                                                              
                                                                                
J28582     MOVE PV-CUM-TO-DATE-PURCH-RETAIL                                     
J28582       TO ML070-CUM-TO-DATE-PURCH-RTL.                                    
J28582     MOVE PV-CUM-TO-DATE-PURCH-COST                                       
J28582       TO ML070-CUM-TO-DATE-PURCH-COST.                                   
                                                                                
                                                                                
      ******************************************************************        
      * 2500-CALC-CUMM-MARKUP-PCT.                                              
      * FINAL CALCULATION OF CUMULATIVE MARKUP PERCENT                          
      ******************************************************************        
       2500-CALC-CUMM-MARKUP-PCT.                                               
                                                                                
           COMPUTE PV-CUM-SEASON-MKUP = (  PV-CUM-TO-DATE-PURCH-RETAIL          
                                         - PV-CUM-TO-DATE-PURCH-COST  ).        
      * CUM SEASON RETAIL CANNOT EQUAL ZERO IN DIVISOR ('ON SIZE ERROR')        
           IF PV-CUM-TO-DATE-PURCH-RETAIL = 0                                   
                MOVE 100 TO ML070-CURR-CUM-MKUP-PCT                             
           ELSE                                                                 
                COMPUTE PV-CUM-MKUP-PCT-MAX ROUNDED =                           
               (PV-CUM-SEASON-MKUP * 100) / PV-CUM-TO-DATE-PURCH-RETAIL         
                MOVE PV-CUM-MKUP-PCT-MAX TO ML070-CURR-CUM-MKUP-PCT             
                IF ( PV-CUM-MKUP-PCT-MAX > 9999.9999999 OR                      
                     PV-CUM-MKUP-PCT-MAX < -9999.9999999  )                     
                    PERFORM 6000-WRITE-RECALC-MESSAGE                           
                END-IF                                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2600-CALC-END-INV-COST.                                                 
      * FINAL CALCULATION OF ENDING INVENTORY COST AMOUNT                       
      ******************************************************************        
       2600-CALC-END-INV-COST.                                                  
                                                                                
           COMPUTE ML070-CURR-END-INV-COST-AMT ROUNDED =                        
                        ( (100 - ML070-CURR-CUM-MKUP-PCT)  *                    
                               ( ML070-CURR-END-INV-RTL-AMT ) ) / 100.          
S28582*                                                                         
S28582*    COMPUTE PV-SEAS-INV-COST-ADJ ROUNDED =                               
S28582*         ( ML070-SEAS-INV-COST-ADJ1 + ML070-SEAS-INV-COST-ADJ2 ).        
S28582*                                                                         
S28582*    COMPUTE ML070-CURR-END-INV-COST-AMT ROUNDED =                        
S28582*            ML070-CURR-END-INV-COST-AMT + PV-SEAS-INV-COST-ADJ.          
S28582*                                                                         
S28582*    IF PV-SEAS-INV-COST-ADJ = ZERO                                       
S28582*        CONTINUE                                                         
S28582*    ELSE                                                                 
S28582*****IF THERE ARE ADJUSTMENTS TO ENDING INVENTORY COST FROM               
S28582*****SMOOTHING, RE-CALCULATE THE CUMULATIVE MARKUP PERCENT                
S28582*        PERFORM 2650-CALC-CUMM-MARKUP-PCT                                
S28582*    END-IF.                                                              
                                                                                
S28582******************************************************************        
S28582* 2650-CALC-CUMM-MARKUP-PCT.                                              
S28582* ADJUST CUMULATIVE MARKUP PERCENT                                        
S28582******************************************************************        
S28582*2650-CALC-CUMM-MARKUP-PCT.                                               
S28582*                                                                         
S28582* EIR CANNOT EQUAL ZERO IN DIVISOR ('ON SIZE ERROR')                      
S28582*    IF ML070-CURR-END-INV-RTL-AMT = 0                                    
S28582*         MOVE 100 TO ML070-CURR-CUM-MKUP-PCT                             
S28582*    ELSE                                                                 
S28582*         COMPUTE PV-CUM-MKUP-PCT-MAX ROUNDED = ( 1 -                     
S28582*    ( ML070-CURR-END-INV-COST-AMT / ML070-CURR-END-INV-RTL-AMT ))        
S28582*         * 100                                                           
S28582*         MOVE PV-CUM-MKUP-PCT-MAX TO ML070-CURR-CUM-MKUP-PCT             
S28582*         IF ( PV-CUM-MKUP-PCT-MAX > 9999.9999999 OR                      
S28582*              PV-CUM-MKUP-PCT-MAX < -9999.9999999  )                     
S28582*             PERFORM 6000-WRITE-RECALC-MESSAGE                           
S28582*         END-IF                                                          
S28582*    END-IF.                                                              
S28582*                                                                         
      ******************************************************************        
      * 2700-CALC-SALES-COST-AMT.                                               
      * FINAL CALCULATION OF SALES COST AMOUNT                                  
      ******************************************************************        
       2700-CALC-SALES-COST-AMT.                                                
                                                                                
           COMPUTE ML070-CURR-SLS-COST-AMT =                                    
                 (  PV-PREV-END-INV-COST-AMT                                    
                  + ML070-CURR-RCPT-NET-COST-AMT                                
                  + ML070-CURR-PADJ-NET-COST-AMT                                
                  - ML070-CURR-END-INV-COST-AMT ).                              
                                                                                
      ******************************************************************        
      * 2800-CALC-GROSS-MARGIN-AMT.                                             
      * FINAL CALCULATIONS OF GROSS MARGIN AMOUNT                               
      ******************************************************************        
       2800-CALC-GROSS-MARGIN-AMT.                                              
                                                                                
      *    IF ML070-CURR-SLS-RTL-AMT = 0                                        
      *        MOVE 0 TO ML070-CURR-GRO-MRGN-AMT                                
      *    ELSE                                                                 
      *        COMPUTE ML070-CURR-GRO-MRGN-AMT =                                
      *              ML070-CURR-SLS-RTL-AMT - ML070-CURR-SLS-COST-AMT           
      *    END-IF.                                                              
                                                                                
           COMPUTE ML070-CURR-GRO-MRGN-AMT =                                    
                 ML070-CURR-SLS-RTL-AMT - ML070-CURR-SLS-COST-AMT.              
                                                                                
      ******************************************************************        
      * 2900-CARRY-FORWARD.                                                     
      * GENERATE CARRY FORWARD ROWS UNTIL THE END OF RECALC PERIODS             
      ******************************************************************        
       2900-CARRY-FORWARD.                                                      
                                                                                
           ADD +1                        TO PV-CARRY-FWD.                       
                                                                                
           MOVE ZEROES                   TO ML070-CURR-RCPT-RTL-AMT             
                                            ML070-CURR-PADJ-RTL-AMT             
                                            ML070-CURR-MKUP-RTL-AMT             
                                            ML070-CURR-MKDN-RTL-AMT             
                                            ML070-CURR-XFER-IN-RTL-AMT          
                                            ML070-CURR-XFER-OUT-RTL-AMT         
                                            ML070-CURR-SLS-RTL-AMT              
                                            ML070-CURR-INV-ADJ-RTL-AMT          
                                            ML070-CURR-SHNK-RTL-AMT             
                                            ML070-CURR-RCPT-NET-COST-AMT        
                                            ML070-CURR-PADJ-NET-COST-AMT        
                                            ML070-SEAS-RCPT-RTL-AMT             
                                            ML070-SEAS-RCPT-NET-COST-AMT        
                                            ML070-SEAS-PADJ-RTL-AMT             
                                            ML070-SEAS-PADJ-NET-COST-AMT        
                                            ML070-SEAS-MKUP-RTL-AMT             
                                            ML070-SEAS-XFER-IN-RTL-AMT          
                                            ML070-SEAS-XFER-OUT-RTL-AMT         
                                            ML070-BEG-SEAS-INV-RTL-AMT          
                                            ML070-BEG-SEAS-INV-COST-AMT         
                                            ML070-SHNK-RTL-AMT                  
                                            ML070-SHNK-SLS-RTL-AMT              
B28582                                      ML070-INV-COST-ADJ1                 
B28582                                      ML070-INV-COST-ADJ2                 
B28582                                      ML070-END-INV-COST-AMT1             
B28582                                      ML070-END-INV-COST-AMT2             
B28582                                      ML070-TGT-CUM-MKUP-PCT              
B28582                                      ML070-ADJ-GRO-MRGN-AMT.             
                                                                                
           SET ML070-PREV-FISCAL-TYPE    TO TRUE.                               
           MOVE PV-PREV-END-INV-RTL-AMT  TO ML070-PREV-END-INV-RTL-AMT.         
           MOVE PV-PREV-END-INV-COST-AMT TO ML070-PREV-END-INV-COST-AMT.        
           MOVE DTATTR-FSCL-WK-END-DTE   TO ML070-FSCL-END-DTE.                 
                                                                                
           PERFORM 5000-WRITE-COMMON-RECALC.                                    
                                                                                
           PERFORM 4450-FETCH-CARRY-FWD-CSR.                                    
                                                                                
      ******************************************************************        
      * FINALIZE THE REPORT AND CLOSE OUT FILES                                 
      ******************************************************************        
       3000-FINALIZE.                                                           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' **** CURRENT INPUT:        ' PV-CURR-READ.                 
           DISPLAY '      CARRY FORWARD:        ' PV-CARRY-FWD.                 
           DISPLAY ' **** CURRENT EXTRACTED:    ' PV-CURR-EXTRACTED.            
                                                                                
           CLOSE   REQUEST-CNTL-FILE                                            
                   COMMON-RECALC-INPUT                                          
                   COMMON-RECALC-EXTRACT                                        
                   RECALC-MESSAGE-REPORT.                                       
                                                                                
B28582*****************************************************************         
B28582* 3100-CALC-DATES.                                                        
B28582*****************************************************************         
B28582 3100-CALC-DATES.                                                         
B28582                                                                          
B28582     INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.                 
B28582                                                                          
B28582     SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                              
B28582     SET DPG51-DECREMENT-DATE       TO TRUE.                              
B28582     SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                              
B28582                                                                          
B28582     MOVE PV-NEXT-FSCL-END-DTE      TO DPG52-LK-DATE-INPUT.               
B28582     MOVE 7                         TO DPG51-INCR-DECR-DAYS-9.            
B28582                                                                          
B28582     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
B28582                      USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.          
B28582                                                                          
B28582     IF (DPG54-SEVERE-ERROR OR DPG54-WARNING-ERROR)                       
B28582         MOVE PV-NEXT-FSCL-END-DTE   TO DPG55-DB2-ISO-DATE-FMT            
B28582     END-IF.                                                              
B28582                                                                          
      ******************************************************************        
      * READ CONTROL CARD                                                       
      ******************************************************************        
       4000-READ-REQUEST-CNTL.                                                  
                                                                                
           READ REQUEST-CNTL-FILE INTO RQ-CNTL-TYPE-AREA                        
               AT END SET END-OF-REQUEST-CNTL TO TRUE.                          
                                                                                
      ******************************************************************        
      * READ CURRENT DATA                                                       
      ******************************************************************        
       4010-READ-COMMON-RECALC.                                                 
                                                                                
           READ COMMON-RECALC-INPUT  INTO PV-NEXT-RECALC-INPUT-REC              
               AT END      SET END-OF-COMMON-RECALC TO TRUE                     
               NOT AT END  ADD +1 TO PV-CURR-READ                               
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * OPEN CARRY FORWARD CURSOR TO GET A LIST OF FISCAL DATES                 
      ******************************************************************        
       4400-OPEN-CARRY-FWD-CSR.                                                 
                                                                                
           EXEC SQL                                                             
               OPEN CARRY-FWD-CSR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               SET NOT-END-OF-CARRY-FWD      TO TRUE                            
           ELSE                                                                 
               DISPLAY ' *** FSCL_DTE RANGE >  ' ML070-FSCL-END-DTE             
                       ' <= ' RQ-CNTL-TYPE-DATE                                 
               MOVE '4400-OPEN-CARRY-FWD-CSR   ' TO PV-PARAGRAPH-NAME           
               MOVE 'OPEN CARRY-FWD-CSR   ' TO PV-OPERATION-ATTEMPTED           
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * FETCH/PROCESS CARRY FORWARD CURSOR                                      
      ******************************************************************        
       4450-FETCH-CARRY-FWD-CSR.                                                
                                                                                
           EXEC SQL                                                             
              FETCH CARRY-FWD-CSR                                               
              INTO                                                              
                   :DTATTR-FSCL-WK-END-DTE                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET END-OF-CARRY-FWD      TO TRUE                            
               WHEN OTHER                                                       
                   DISPLAY ' *** FSCL_DTE RANGE >  ' ML070-FSCL-END-DTE         
                           ' <= ' RQ-CNTL-TYPE-DATE                             
                   MOVE '4450-FETCH-CARRY-FWD-CSR'                              
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'FETCH FROM PARTN-CNTL-CSR'                             
                       TO PV-OPERATION-ATTEMPTED                                
                   PERFORM 9950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * CLOSE CARRY FORWARD CURSOR                                              
      ******************************************************************        
       4475-CLOSE-CARRY-FWD-CSR.                                                
                                                                                
           EXEC SQL                                                             
                CLOSE CARRY-FWD-CSR                                             
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               DISPLAY ' *** FSCL_DTE RANGE >  ' ML070-FSCL-END-DTE             
                       ' <= ' RQ-CNTL-TYPE-DATE                                 
               MOVE '4475-CLOSE-CARRY-FWD-CSR   ' TO PV-PARAGRAPH-NAME          
               MOVE 'CLOSE CARRY-FWD-CSR   ' TO PV-OPERATION-ATTEMPTED          
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * WRITE OUT COMMON-RECALC EXTRACT                                         
      ******************************************************************        
       5000-WRITE-COMMON-RECALC.                                                
                                                                                
           WRITE COMMON-RECALC-REC FROM ML070-COMMON-RECALC-EXTRACT.            
           ADD +1 TO PV-CURR-EXTRACTED.                                         
                                                                                
      ******************************************************************        
      * WRITE OUT RECALC-MESSAGE                                                
      ******************************************************************        
       6000-WRITE-RECALC-MESSAGE.                                               
                                                                                
26006      IF PV-RPT-PAGE = ZEROES OR PV-RPT-LINE >= PC-MAX-RPT-LINES           
26006          ADD +1                       TO PV-RPT-PAGE                      
26006          MOVE PV-RPT-PAGE             TO DP100O-PAGE-NUMBER               
26006          WRITE RECALC-MESSAGE-REC                                         
26006              FROM DP100O-STANDRD-HEADER-100-COLS                          
26006              AFTER ADVANCING PAGE                                         
26006          WRITE RECALC-MESSAGE-REC                                         
X23843             FROM RP-RECALC-REPORT-TITLE-1                                
26006              AFTER ADVANCING 1 LINES                                      
X23843         WRITE RECALC-MESSAGE-REC                                         
X23843             FROM RP-RECALC-REPORT-TITLE-2                                
X23843             AFTER ADVANCING 1 LINES                                      
26006          WRITE RECALC-MESSAGE-REC                                         
26006              FROM RP-RECALC-REPORT-HEADER                                 
26006              AFTER ADVANCING 2 LINES                                      
26006          MOVE +5                      TO PV-RPT-LINE                      
26006      END-IF.                                                              
                                                                                
           MOVE ML070-FSCL-END-DTE      TO RP-FSCL-END-DTE.                     
           IF ML070-DEPT-NBR   = ZEROES                                         
               MOVE SPACES                  TO RP-DEPT-NBR-X                    
           ELSE                                                                 
               MOVE ML070-DEPT-NBR          TO RP-DEPT-NBR                      
           END-IF.                                                              
           IF ML070-MAJ-CL-NBR = ZEROES                                         
               MOVE SPACES                  TO RP-MAJ-CL-NBR-X                  
           ELSE                                                                 
               MOVE ML070-MAJ-CL-NBR        TO RP-MAJ-CL-NBR                    
           END-IF.                                                              
           IF ML070-SUB-CL-NBR = ZEROES                                         
               MOVE SPACES                  TO RP-SUB-CL-NBR-X                  
           ELSE                                                                 
               MOVE ML070-SUB-CL-NBR        TO RP-SUB-CL-NBR                    
           END-IF.                                                              
           IF ML070-LOC-NBR    = ZEROES                                         
               MOVE SPACES                  TO RP-LOC-NBR-X                     
           ELSE                                                                 
               MOVE ML070-LOC-NBR           TO RP-LOC-NBR                       
           END-IF.                                                              
           IF ML070-ENT-ID     = ZEROES                                         
               MOVE SPACES                  TO RP-ENT-ID-X                      
           ELSE                                                                 
               MOVE ML070-ENT-ID            TO RP-ENT-ID                        
           END-IF.                                                              
W28582     IF ML070-LBL-SEQ-NBR  = ZEROES                                       
W28582         MOVE SPACES                  TO RP-LBL-SEQ-NBR-X                 
W28582     ELSE                                                                 
W28582         MOVE ML070-LBL-SEQ-NBR       TO RP-LBL-SEQ-NBR                   
W28582     END-IF.                                                              
W28582     IF ML070-ML-LWST-ID   = ZEROES                                       
W28582         MOVE SPACES                  TO RP-ML-LWST-ID-X                  
W28582     ELSE                                                                 
W28582         MOVE ML070-ML-LWST-ID        TO RP-ML-LWST-ID                    
W28582     END-IF.                                                              
           MOVE PV-CUM-MKUP-PCT-MAX     TO RP-CUM-CALC.                         
           MOVE ML070-CURR-CUM-MKUP-PCT TO RP-CUM-USED.                         
26006      WRITE RECALC-MESSAGE-REC FROM RP-RECALC-REPORT-DETAIL                
26006          AFTER ADVANCING 1 LINES.                                         
26006      ADD +1                       TO PV-RPT-LINE.                         
                                                                                
      ******************************************************************        
      * 9950-SQL-ABEND.                                                         
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9950-SQL-ABEND.                                                          
                                                                                
           SET SQL-ABEND-CODE TO TRUE.                                          
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
      ******************************************************************        
      * 9999-ABEND.                                                             
      *   THIS ROUTINE FORCES AN ABEND                                          
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**'                                                         
           DISPLAY '**'                                                         
           DISPLAY '**********************************'                         
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
