000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.    XLKUT046.                                                 
000500 AUTHOR.        PAUL OMAN.                                                
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN   APRIL, 2003.                                              
000800 DATE-COMPILED.                                                           
000900****************************************************************          
001000*    THIS PROGRAM WILL COMPARE RMS PO / LEGACY PO              *          
001100* PO DOMAIN TEMPLATE.........:  XXT_OTBND_ALLOC                *          
001200* PO DOMAIN TEMPLATE COPYBOOK:  XLRD020                        *          
001300*                                                              *          
001310*        INPUT COMES TO THIS PROGRAM FROM XLKUT040 AND MUST    *          
001320*    BE SORTED BY THE COMPLETE RECORD. THAT WAY, IF YOU HAVE   *          
001330*    DUPLICATE KEY FIELDS THAT YOU ARE MATCHING ON, BUT THE    *          
001340*    DATA IS DIFFERENT, THEY WILL MATCH.  IF YOU DON'T SORT    *          
001350*    BY THE COMPLETE RECORD, RECORDS WITH DUPLICATE KEY        *          
001360*    FIELDS COULD POSSIBLY CREATE MISLEADING DATA MISMATCHING  *          
001370*    RECORDS WHEN NONE REALLY EXIST.                           *          
001380*                                                              *          
001390*        THIS PROGRAM MATCHES THE LEGACY AND RMS FILES BY PO   *          
001391*    NUMBER, SKU NUMBER, AND LOCATION NUMBER. IT               *          
001392*    WRITES ERROR RECORDS OUT FOR ANY UNMATCHED RECORDS. IT    *          
001393*    ALSO CHECKS TO SEE IF THERE ARE ANY DATA DIFFERENCES FOR  *          
001394*    THOSE RECORDS WHOSE PO NUMBERS MATCH. IF THERE ARE ANY,   *          
001395*    AN ERROR RECORD IS WRITTEN INDICATING THE FIELDS THAT     *          
001396*    ARE DIFFERENT.                                            *          
001397*        THERE ARE SOME MOVES OF FIELDS COMMENTED OUT. THIS    *          
001398*    HAS BEEN DONE BECAUSE SOME OF THESE FIELDS AREN'T AVAIL-  *          
001399*    ABLE ON ONE OF THE SYSTEMS.  YOU MAY FIND THESE BY DOING  *          
001400*    A F '**??'.                                               *          
001410*****************************************************************         
004300****************************************************************          
      * DATE  07/29/2014                                                        
      *  CHANGE MADE: REMOVED THE CHECKS FOR BELOW FIELDS                       
      *      - ALLOC SEQ CDE                                                    
      *      - STR CNSTR CDE                                                    
      * MADE BY: CHERI PARMLEY           TKID: TKMAF76                          
      * CHANGES TAGGED AS 'CLP 07/29/14' CR : CHG0088280                        
004300****************************************************************          
      * DATE  08/04/2014                                                        
      *  CHANGE MADE: REMOVED THE CHECKS FOR BELOW FIELDS                       
      *      - STR CNSTR QTY                                                    
      *      - RNK SEQ NBR                                                      
      * MADE BY: CHERI PARMLEY           TKID: TKMAF76                          
      * CHANGES TAGGED AS 'CLP 08/04/14' CR : CHG00?????                        
004300****************************************************************          
001500 ENVIRONMENT DIVISION.                                                    
001600 INPUT-OUTPUT SECTION.                                                    
001700                                                                          
001800 FILE-CONTROL.                                                            
001900                                                                          
002000     SELECT RMS-PO-FILE                                                   
002010         ASSIGN TO INP01.                                                 
002100                                                                          
002110     SELECT LEGACY-FILE                                                   
002120         ASSIGN TO INP02.                                                 
002130                                                                          
002140     SELECT ERROR-FILE                                                    
002150         ASSIGN TO OUT01.                                                 
002160                                                                          
002200*****************************************************************         
002300 DATA DIVISION.                                                           
002400*****************************************************************         
002500 FILE SECTION.                                                            
002600                                                                          
002700 FD  RMS-PO-FILE                                                          
002800     RECORDING MODE IS F                                                  
002810     BLOCK CONTAINS 0 RECORDS                                             
002900     LABEL RECORDS ARE STANDARD.                                          
003000                                                                          
003300 01  RMS-PO-RECORD                PIC X(52).                              
003700                                                                          
003710 FD  LEGACY-FILE                                                          
003720     RECORDING MODE IS F                                                  
003730     BLOCK CONTAINS 0 RECORDS                                             
003740     LABEL RECORDS ARE STANDARD.                                          
003750                                                                          
003760 01  LEGACY-RECORD                PIC X(52).                              
003770                                                                          
003800                                                                          
003810 FD  ERROR-FILE                                                           
003820     RECORDING MODE IS F                                                  
003830     BLOCK CONTAINS 0 RECORDS                                             
003840     LABEL RECORDS ARE STANDARD.                                          
003850                                                                          
003860 01  ERROR-RECORD                 PIC X(830).                             
003870                                                                          
003880                                                                          
003900*****************************************************************         
004000 WORKING-STORAGE SECTION.                                                 
004100                                                                          
004200 01  PS-PROGRAM-SWITCHES.                                                 
004300     05  PS-EOF-INPUT1-SW             PIC X(01)  VALUE 'N'.               
004400         88  PS-EOF-INPUT1-YES                   VALUE 'Y'.               
004500         88  PS-EOF-INPUT1-NO                    VALUE 'N'.               
004510     05  PS-EOF-INPUT2-SW             PIC X(01)  VALUE 'N'.               
004520         88  PS-EOF-INPUT2-YES                   VALUE 'Y'.               
004530         88  PS-EOF-INPUT2-NO                    VALUE 'N'.               
004540     05  PS-EMAIL-SW                  PIC X(01)  VALUE 'N'.               
004550         88  PS-EMAIL-YES                        VALUE 'Y'.               
004560         88  PS-EMAIL-NO                         VALUE 'N'.               
004600                                                                          
004700 01  PC-PROGRAM-CONSTANTS.                                                
004800     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT046'.        
004900     05  PC-FIVE-BLANKS               PIC  X(05) VALUE SPACES.            
005000                                                                          
005001 01  PC-SORT-KEY.                                                         
005002     05  PC-SKU-NBR                   PIC  9(08) VALUE ZEROES.            
005003     05  PC-LOC-NBR                   PIC  9(04) VALUE ZEROES.            
005004     05  PC-STR-NBR                   PIC  9(04) VALUE ZEROES.            
005005                                                                          
005100 01  PV-PROGRAM-VARIABLES.                                                
005900     05  PV-RECORDS-READ-1            PIC  9(09) VALUE  0.                
005910     05  PV-RECORDS-READ-2            PIC  9(09) VALUE  0.                
006000     05  PV-RECORDS-WRITTEN           PIC  9(09) VALUE  0.                
006010     05  PV-RECORDS-ON-BOTH           PIC  9(09) VALUE  0.                
006020     05  PV-RECORDS-ON-RMS-ONLY       PIC  9(09) VALUE  0.                
006030     05  PV-RECORDS-ON-LEGACY-ONLY    PIC  9(09) VALUE  0.                
006040     05  PV-RECORDS-DATA-MISMATCH     PIC  9(09) VALUE  0.                
006100     05  PV-ALL-NINES                 PIC  9(09) VALUE 99999999.          
007000                                                                          
007100 01  PV-ABEND-FIELDS.                                                     
007200     05  PV-ABEND-CODE                PIC  S9(4) VALUE +0                 
007300                                                 COMP SYNC.               
007400     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT046'.        
007500     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
007600     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
007700     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
008200                                                                          
008300 01  PV-RMS-INPUT.                                                        
009206     COPY XLRD020.                                                        
009300                                                                          
009400 01  PV-LEGACY-INPUT.                                                     
009500     COPY XLRD020.                                                        
009600                                                                          
009601 01  PV-ERROR-OUTPUT.                                                     
009602     COPY XLRD023.                                                        
009603                                                                          
010800*****************************************************************         
010900 PROCEDURE DIVISION.                                                      
011000*****************************************************************         
011010     DISPLAY ' '.                                                         
011100     DISPLAY '**** BEGINNING XLKUT046 ****'.                              
011300                                                                          
011400     OPEN INPUT  RMS-PO-FILE                                              
011401                 LEGACY-FILE                                              
011410          OUTPUT ERROR-FILE.                                              
011420     SET PS-EMAIL-NO       TO TRUE.                                       
011500                                                                          
011600     PERFORM 1000-READ-RMS-INPUT.                                         
011610     PERFORM 1100-READ-LEGACY-INPUT.                                      
011700                                                                          
012100     PERFORM 2000-PROCESS-INPUT-FILE                                      
012200       UNTIL PS-EOF-INPUT1-YES AND                                        
012210             PS-EOF-INPUT2-YES.                                           
012300                                                                          
012310     CLOSE RMS-PO-FILE                                                    
012320           LEGACY-FILE                                                    
012330           ERROR-FILE.                                                    
012500                                                                          
012600     DISPLAY ' '.                                                         
012601     DISPLAY '  INPUT RMS PO REC = ' PV-RECORDS-READ-1.                   
012610     DISPLAY '  INPUT LEGACY REC = ' PV-RECORDS-READ-2.                   
012611     DISPLAY ' '.                                                         
012612     DISPLAY '          XLRD020 - XXT-OTBND-ALLOC'.                       
012614     DISPLAY 'FILES ARE MATCHED BY PO NUMBER - 8 POSITIONS'.              
012615     DISPLAY '                     SKU-NBR   - 8 POSITIONS'.              
012616     DISPLAY '                     LOC-NBR   - 4 POSITIONS'.              
012617     DISPLAY '                     STR-NBR   - 4 POSITIONS'.              
012620     DISPLAY ' '.                                                         
012621     DISPLAY PC-FIVE-BLANKS                                               
012622             'RECORDS ON BOTH FILES ---------> '                          
012623              PV-RECORDS-ON-BOTH.                                         
012630     DISPLAY ' '.                                                         
012710     DISPLAY '  OUTPUT RECORDS WRITTEN ------> '                          
012720              PV-RECORDS-WRITTEN.                                         
012760     DISPLAY PC-FIVE-BLANKS                                               
012770             'RECORDS ON RMS ONLY -----------> '                          
012781              PV-RECORDS-ON-RMS-ONLY                                      
012782             ' - "A" RECORD CODE'.                                        
012790     DISPLAY PC-FIVE-BLANKS                                               
012791             'RECORDS ON LEGACY ONLY --------> '                          
012793              PV-RECORDS-ON-LEGACY-ONLY                                   
012794             ' - "B" RECORD CODE'.                                        
012795     DISPLAY PC-FIVE-BLANKS                                               
012796             'RECORDS WITH DATA MISMATCH ----> '                          
012798              PV-RECORDS-DATA-MISMATCH                                    
012799             ' - "C" RECORD CODE'.                                        
012800     DISPLAY ' '.                                                         
012900     DISPLAY '****  END OF  XLKUT046  ****'.                              
012910     DISPLAY ' '.                                                         
013000                                                                          
013100*    SET RETURN CODE IF E-MAIL SWITCH IS TRUE.                            
013200     IF PS-EMAIL-YES                                                      
013300         MOVE 4095 TO RETURN-CODE                                         
013301     END-IF.                                                              
013302     GOBACK.                                                              
013303                                                                          
013304                                                                          
013400*****************************************************************         
013500*  READ A RMS PO RECORD                                                   
013600*****************************************************************         
013700 1000-READ-RMS-INPUT.                                                     
013800                                                                          
013900     MOVE '1000-READ-RMS-INPUT    ' TO PV-ABEND-PARAGRAPH.                
014000                                                                          
014010     INITIALIZE PV-RMS-INPUT.                                             
014020                                                                          
014100     READ RMS-PO-FILE                                                     
014200          INTO PV-RMS-INPUT                                               
014300            AT END                                                        
014400               SET PS-EOF-INPUT1-YES TO TRUE                              
014900     END-READ.                                                            
015000                                                                          
015010     IF PS-EOF-INPUT1-YES                                                 
015020        MOVE PV-ALL-NINES TO XL020-PO-NBR OF PV-RMS-INPUT                 
015021                             XL020-SKU-NBR OF PV-RMS-INPUT                
015022                             XL020-LOC-NBR OF PV-RMS-INPUT                
015023                             XL020-STR-NBR OF PV-RMS-INPUT                
015030     ELSE                                                                 
015040        ADD +1 TO PV-RECORDS-READ-1                                       
015050     END-IF.                                                              
015100                                                                          
015101*****************************************************************         
015102*  READ A LEGACY RECORD                                                   
015103*****************************************************************         
015104 1100-READ-LEGACY-INPUT.                                                  
015105                                                                          
015106     MOVE '1100-READ-LEGACY-INPUT' TO PV-ABEND-PARAGRAPH.                 
015107                                                                          
015108     INITIALIZE PV-LEGACY-INPUT.                                          
015109                                                                          
015110     READ LEGACY-FILE                                                     
015111          INTO PV-LEGACY-INPUT                                            
015112            AT END                                                        
015113               SET PS-EOF-INPUT2-YES TO TRUE                              
015114     END-READ.                                                            
015115                                                                          
015116     IF PS-EOF-INPUT2-YES                                                 
015117        MOVE PV-ALL-NINES TO XL020-PO-NBR OF PV-LEGACY-INPUT              
015118                             XL020-SKU-NBR OF PV-LEGACY-INPUT             
015119                             XL020-LOC-NBR OF PV-LEGACY-INPUT             
015120                             XL020-STR-NBR OF PV-LEGACY-INPUT             
015121     ELSE                                                                 
015122        ADD +1 TO PV-RECORDS-READ-2                                       
015123     END-IF.                                                              
015130                                                                          
015200*****************************************************************         
015300*  REFORMAT THE RMS PO EXTRACT FILE                                       
015400*****************************************************************         
015500 2000-PROCESS-INPUT-FILE.                                                 
015600                                                                          
015700     MOVE '2000-PROCESS-INPUT-FILE ' TO PV-ABEND-PARAGRAPH.               
015800                                                                          
015900     IF XL020-PO-NBR   OF PV-RMS-INPUT    <                               
016000        XL020-PO-NBR   OF PV-LEGACY-INPUT                                 
016001*******************************                                           
016002***  ON RMS NOT ON LEGACY                                                 
016003*******************************                                           
015900         IF  XL020-ALLOC-QTY OF PV-RMS-INPUT = '00000000.0000'            
016149              PERFORM 1000-READ-RMS-INPUT                                 
016116         ELSE                                                             
016004              ADD 1 TO PV-RECORDS-ON-RMS-ONLY                             
016113              SET XL023-ON-RMS-ONLY TO TRUE                               
016114              PERFORM 3400-WRITE-RMS-ERROR                                
016115              PERFORM 1000-READ-RMS-INPUT                                 
016116         END-IF                                                           
016116     ELSE                                                                 
01611          IF XL020-PO-NBR  OF PV-RMS-INPUT    >                            
016118            XL020-PO-NBR  OF PV-LEGACY-INPUT                              
016119*******************************                                           
016120***   ON LEGACY NOT ON RMS                                                
016121*******************************                                           
016122           IF XL020-SKU-CNCLD-IND OF PV-LEGACY-INPUT = 'Y'                
016125               PERFORM 1100-READ-LEGACY-INPUT                             
016126            ELSE                                                          
016122               ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                         
016123               SET XL023-ON-LEGACY-ONLY TO TRUE                           
016124               PERFORM 4000-WRITE-LEGACY-ERROR                            
016125               PERFORM 1100-READ-LEGACY-INPUT                             
016126            END-IF                                                        
016126        ELSE                                                              
016127*******************************                                           
016128***  ON RMS AND ON LEGACY                                                 
016129*******************************                                           
016130           PERFORM 2400-CHECK-KEY-VALUES                                  
716131        END-IF                                                            
016132     END-IF.                                                              
016133                                                                          
016134****************************************************************          
016135*    CHECK ALL THE KEY VALUES FOR MISMATCHES.                  *          
016136*                                                              *          
016138****************************************************************          
016139 2400-CHECK-KEY-VALUES.                                                   
016140                                                                          
016141     MOVE '2400-CHECK-KEY-VALUES ' TO PV-ABEND-PARAGRAPH.                 
016142                                                                          
016143     EVALUATE TRUE                                                        
015900*        WHEN XL020-ALLOC-QTY OF PV-RMS-INPUT = '00000000.0000'           
016140*             PERFORM 1000-READ-RMS-INPUT                                 
016150                                                                          
015900*        WHEN XL020-ALLOC-QTY OF PV-LEGACY-INPUT = '00000000.0000'        
016156*             PERFORM 1100-READ-LEGACY-INPUT                              
016150                                                                          
016144         WHEN XL020-SKU-NBR OF PV-RMS-INPUT <                             
016145              XL020-SKU-NBR OF PV-LEGACY-INPUT                            
015900              IF XL020-ALLOC-QTY OF PV-RMS-INPUT = '00000000.0000'        
015900                  CONTINUE                                                
015900              ELSE                                                        
016146                  ADD 1 TO PV-RECORDS-ON-RMS-ONLY                         
016147                  SET XL023-ON-RMS-ONLY TO TRUE                           
016148                  PERFORM 3400-WRITE-RMS-ERROR                            
015900              END-IF                                                      
016149              PERFORM 1000-READ-RMS-INPUT                                 
016150                                                                          
016151         WHEN XL020-SKU-NBR OF PV-RMS-INPUT >                             
016152              XL020-SKU-NBR OF PV-LEGACY-INPUT                            
016122              IF XL020-SKU-CNCLD-IND OF PV-LEGACY-INPUT = 'Y'             
016156                  PERFORM 1100-READ-LEGACY-INPUT                          
016153              ELSE                                                        
016153                  ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                      
016154                  SET XL023-ON-LEGACY-ONLY TO TRUE                        
016155                  PERFORM 4000-WRITE-LEGACY-ERROR                         
016156                  PERFORM 1100-READ-LEGACY-INPUT                          
016153              END-IF                                                      
016157                                                                          
016158         WHEN XL020-LOC-NBR OF PV-RMS-INPUT <                             
016159              XL020-LOC-NBR OF PV-LEGACY-INPUT                            
015900              IF XL020-ALLOC-QTY OF PV-RMS-INPUT = '00000000.0000'        
015900                  CONTINUE                                                
015900              ELSE                                                        
016160                  ADD 1 TO PV-RECORDS-ON-RMS-ONLY                         
016161                  SET XL023-ON-RMS-ONLY TO TRUE                           
016162                  PERFORM 3400-WRITE-RMS-ERROR                            
015900              END-IF                                                      
016163              PERFORM 1000-READ-RMS-INPUT                                 
016164                                                                          
016165         WHEN XL020-LOC-NBR OF PV-RMS-INPUT >                             
016166              XL020-LOC-NBR OF PV-LEGACY-INPUT                            
016122              IF XL020-SKU-CNCLD-IND OF PV-LEGACY-INPUT = 'Y'             
016170                  PERFORM 1100-READ-LEGACY-INPUT                          
016167              ELSE                                                        
016167                  ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                      
016168                  SET XL023-ON-LEGACY-ONLY TO TRUE                        
016169                  PERFORM 4000-WRITE-LEGACY-ERROR                         
016170                  PERFORM 1100-READ-LEGACY-INPUT                          
016153              END-IF                                                      
016171                                                                          
016172         WHEN XL020-STR-NBR OF PV-RMS-INPUT <                             
016173              XL020-STR-NBR OF PV-LEGACY-INPUT                            
015900              IF XL020-ALLOC-QTY OF PV-RMS-INPUT = '00000000.0000'        
015900                  CONTINUE                                                
015900              ELSE                                                        
016174                  ADD 1 TO PV-RECORDS-ON-RMS-ONLY                         
016175                  SET XL023-ON-RMS-ONLY TO TRUE                           
016176                  PERFORM 3400-WRITE-RMS-ERROR                            
016153              END-IF                                                      
016177              PERFORM 1000-READ-RMS-INPUT                                 
016178                                                                          
016179         WHEN XL020-STR-NBR OF PV-RMS-INPUT >                             
016180              XL020-STR-NBR OF PV-LEGACY-INPUT                            
016122              IF XL020-SKU-CNCLD-IND OF PV-LEGACY-INPUT = 'Y'             
016184                  PERFORM 1100-READ-LEGACY-INPUT                          
016181              ELSE                                                        
016181                  ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                      
016182                  SET XL023-ON-LEGACY-ONLY TO TRUE                        
016183                  PERFORM 4000-WRITE-LEGACY-ERROR                         
016184                  PERFORM 1100-READ-LEGACY-INPUT                          
016181              END-IF                                                      
016185                                                                          
016186         WHEN  OTHER                                                      
016187                 ADD 1 TO PV-RECORDS-ON-BOTH                              
016194                 SET XL023-ON-BOTH-RMS-LEGACY TO TRUE                     
016195                 PERFORM 3400-WRITE-RMS-ERROR                             
016196                 PERFORM 4000-WRITE-LEGACY-ERROR                          
016197                 PERFORM 1000-READ-RMS-INPUT                              
016198                 PERFORM 1100-READ-LEGACY-INPUT                           
016200                                                                          
016201     END-EVALUATE.                                                        
016210                                                                          
021600*****************************************************************         
021700*  WRITE A RMS RECORD TO THE ERROR FILE                                   
021800*****************************************************************         
021900 3400-WRITE-RMS-ERROR.                                                    
022000                                                                          
022100     SET XL023-XXT-PO-OTBND-ALLOC TO TRUE.                                
022200     MOVE XL020-PO-NBR    OF PV-RMS-INPUT TO XL023-PO-SORT-KEY-PO.        
022300     MOVE XL020-SKU-NBR   OF PV-RMS-INPUT  TO PC-SKU-NBR.                 
022400     MOVE XL020-LOC-NBR OF PV-RMS-INPUT    TO PC-LOC-NBR.                 
022410     MOVE XL020-STR-NBR OF PV-RMS-INPUT    TO PC-STR-NBR.                 
022500     MOVE PC-SORT-KEY     TO XL023-PO-SORT-KEY-DATA.                      
022610     INITIALIZE XL023-OTBND-ALLOC.                                        
022700                                                                          
022800     MOVE XL020-PO-NBR OF PV-RMS-INPUT    TO                              
022810                             XL023-SB-PO-NBR-RMS.                         
022900     MOVE XL020-SKU-NBR  OF PV-RMS-INPUT  TO                              
022910                             XL023-SB-SKU-NBR-RMS.                        
023000     MOVE XL020-LOC-NBR OF PV-RMS-INPUT   TO                              
023100                             XL023-SB-LOC-NBR-RMS.                        
023110     MOVE XL020-STR-NBR OF PV-RMS-INPUT   TO                              
023120                             XL023-SB-STR-NBR-RMS.                        
023130                                                                          
023180     MOVE XL020-RNK-SEQ-NBR    OF PV-RMS-INPUT   TO                       
023181                       XL023-SB-RNK-SEQ-NBR-RMS.                          
023190     MOVE XL020-ALLOC-QTY      OF PV-RMS-INPUT   TO                       
023191                       XL023-SB-ALLOC-QTY-RMS.                            
023200     MOVE XL020-ALLOC-SEQ-CDE  OF PV-RMS-INPUT   TO                       
023210                       XL023-SB-ALLOC-SEQ-CDE-RMS.                        
023300     MOVE XL020-STR-CNSTR-CDE  OF PV-RMS-INPUT   TO                       
023310                       XL023-SB-STR-CNSTR-CDE-RMS.                        
023400     IF XL020-STR-CNSTR-QTY OF PV-RMS-INPUT  NUMERIC                      
023500         MOVE XL020-STR-CNSTR-QTY  OF PV-RMS-INPUT   TO                   
023600                            XL023-SB-STR-CNSTR-QTY-RMS                    
023700     ELSE                                                                 
023800         MOVE ZEROES  TO    XL023-SB-STR-CNSTR-QTY-RMS                    
024500     END-IF.                                                              
024501                                                                          
024502     IF XL023-ON-BOTH-RMS-LEGACY                                          
024503         CONTINUE                                                         
024504     ELSE                                                                 
024505         PERFORM P5000-WRITE-ERROR-RECORD                                 
024508     END-IF.                                                              
024600                                                                          
024700*****************************************************************         
024800*  WRITE A LEGACY RECORD TO THE ERROR FILE                                
024900*****************************************************************         
025000 4000-WRITE-LEGACY-ERROR.                                                 
025100                                                                          
025200     IF XL023-ON-BOTH-RMS-LEGACY                                          
025300         CONTINUE                                                         
025400     ELSE                                                                 
025510         SET XL023-XXT-PO-OTBND-ALLOC TO TRUE                             
025600         MOVE XL020-PO-NBR OF PV-LEGACY-INPUT                             
025700                                        TO XL023-PO-SORT-KEY-PO           
025800         MOVE XL020-SKU-NBR OF PV-LEGACY-INPUT TO PC-SKU-NBR              
025900         MOVE XL020-LOC-NBR OF PV-LEGACY-INPUT TO PC-LOC-NBR              
025910         MOVE XL020-STR-NBR OF PV-LEGACY-INPUT TO PC-STR-NBR              
026000         MOVE PC-SORT-KEY     TO XL023-PO-SORT-KEY-DATA                   
026100         INITIALIZE XL023-OTBND-ALLOC                                     
026200     END-IF.                                                              
026300                                                                          
026310     MOVE XL020-PO-NBR OF PV-LEGACY-INPUT TO                              
026320                             XL023-SB-PO-NBR-LGCY.                        
026330     MOVE XL020-SKU-NBR  OF PV-LEGACY-INPUT TO                            
026340                             XL023-SB-SKU-NBR-LGCY.                       
026350     MOVE XL020-LOC-NBR OF PV-LEGACY-INPUT TO                             
026360                             XL023-SB-LOC-NBR-LGCY.                       
026370     MOVE XL020-STR-NBR OF PV-LEGACY-INPUT TO                             
026380                             XL023-SB-STR-NBR-LGCY.                       
026381                                                                          
026700     MOVE XL020-RNK-SEQ-NBR   OF PV-LEGACY-INPUT TO                       
026710                       XL023-SB-RNK-SEQ-NBR-LGCY.                         
026800     MOVE XL020-ALLOC-QTY     OF PV-LEGACY-INPUT TO                       
026810                       XL023-SB-ALLOC-QTY-LGCY.                           
026900     MOVE XL020-ALLOC-SEQ-CDE OF PV-LEGACY-INPUT TO                       
026910                       XL023-SB-ALLOC-SEQ-CDE-LGCY.                       
027000     MOVE XL020-STR-CNSTR-CDE OF PV-LEGACY-INPUT TO                       
027010                       XL023-SB-STR-CNSTR-CDE-LGCY.                       
027100     MOVE XL020-STR-CNSTR-QTY OF PV-LEGACY-INPUT TO                       
027510                       XL023-SB-STR-CNSTR-QTY-LGCY.                       
027600                                                                          
027700     IF XL023-ON-BOTH-RMS-LEGACY                                          
027800         PERFORM 4300-COMPARE                                             
028000     ELSE                                                                 
028010         PERFORM P5000-WRITE-ERROR-RECORD                                 
028302     END-IF.                                                              
028303                                                                          
028500                                                                          
028600*****************************************************************         
028700*  COMPARE FIELDS TO FIND DISCREPANCIES.                                  
028800*****************************************************************         
028900 4300-COMPARE.                                                            
029000                                                                          
029100     IF XL023-SB-PO-NBR-RMS  NOT = XL023-SB-PO-NBR-LGCY                   
029200         MOVE '*'  TO  XL023-SB-PO-NBR-DIFF                               
029300     END-IF.                                                              
029301                                                                          
029800     IF XL023-SB-SKU-NBR-RMS NOT =                                        
029900                                 XL023-SB-SKU-NBR-LGCY                    
029901         MOVE '*'  TO  XL023-SB-SKU-NBR-DIFF                              
029902     END-IF.                                                              
029910                                                                          
030100     IF XL023-SB-LOC-NBR-RMS NOT =                                        
030200                                 XL023-SB-LOC-NBR-LGCY                    
030201         MOVE '*'  TO  XL023-SB-LOC-NBR-DIFF                              
030202     END-IF.                                                              
030210                                                                          
030400     IF XL023-SB-STR-NBR-RMS NOT =                                        
030500                                 XL023-SB-STR-NBR-LGCY                    
030501         MOVE '*'  TO  XL023-SB-STR-NBR-DIFF                              
030502     END-IF.                                                              
030510                                                                          
030810                                                                          
016122     IF XL020-SKU-CNCLD-IND OF PV-LEGACY-INPUT = 'Y'                      
031110        CONTINUE                                                          
031110     ELSE                                                                 
      *CLP 08/04/14 - START                                                     
030700*        IF XL023-SB-RNK-SEQ-NBR-RMS NOT =                                
030800*                                    XL023-SB-RNK-SEQ-NBR-LGCY            
030801*            MOVE '*'  TO  XL023-SB-RNK-SEQ-NBR-DIFF                      
      *        END-IF                                                           
      *CLP 08/04/14 - START                                                     
031000         IF XL023-SB-ALLOC-QTY-RMS   NOT =                                
031100                                     XL023-SB-ALLOC-QTY-LGCY              
031101             MOVE '*'  TO  XL023-SB-ALLOC-QTY-DIFF                        
031102         END-IF                                                           
      *CLP 07/29/14 - START                                                     
031300*        IF XL023-SB-ALLOC-SEQ-CDE-RMS NOT =                              
031400*                                    XL023-SB-ALLOC-SEQ-CDE-LGCY          
031401*            MOVE '*'  TO  XL023-SB-ALLOC-SEQ-CDE-DIFF                    
031402*        END-IF                                                           
031600*        IF XL023-SB-STR-CNSTR-CDE-RMS NOT =                              
031700*                                    XL023-SB-STR-CNSTR-CDE-LGCY          
031701*            MOVE '*'  TO  XL023-SB-STR-CNSTR-CDE-DIFF                    
031702*        END-IF                                                           
      *CLP 07/29/14 - END                                                       
      *CLP 08/04/14 - START                                                     
031900*        IF XL023-SB-STR-CNSTR-QTY-RMS NOT =                              
032000*                                    XL023-SB-STR-CNSTR-QTY-LGCY          
032100*            MOVE '*'  TO  XL023-SB-STR-CNSTR-QTY-DIFF                    
032200*        END-IF                                                           
      *CLP 08/04/14 - END                                                       
032200     END-IF.                                                              
032201                                                                          
032202     IF  XL023-SB-PO-NBR-DIFF        = '*' OR                             
032203         XL023-SB-SKU-NBR-DIFF       = '*' OR                             
032204         XL023-SB-LOC-NBR-DIFF       = '*' OR                             
032205         XL023-SB-STR-NBR-DIFF       = '*' OR                             
032206         XL023-SB-RNK-SEQ-NBR-DIFF   = '*' OR                             
032207         XL023-SB-ALLOC-QTY-DIFF     = '*' OR                             
032208         XL023-SB-ALLOC-SEQ-CDE-DIFF = '*' OR                             
032209         XL023-SB-STR-CNSTR-CDE-DIFF = '*' OR                             
032210         XL023-SB-STR-CNSTR-QTY-DIFF = '*'                                
032211                                                                          
032220         ADD 1 TO PV-RECORDS-DATA-MISMATCH                                
032221         PERFORM P5000-WRITE-ERROR-RECORD                                 
032234     END-IF.                                                              
032235                                                                          
032236 P5000-WRITE-ERROR-RECORD.                                                
032237                                                                          
032238     ADD +1 TO PV-RECORDS-WRITTEN.                                        
032239     WRITE ERROR-RECORD FROM PV-ERROR-OUTPUT.                             
032240                                                                          
032241*    SET SWITCH FOR E-MAIL NOTIFICATION OF ERROR.                         
032242*    SET PS-EMAIL-YES       TO TRUE.                                      
032250                                                                          
