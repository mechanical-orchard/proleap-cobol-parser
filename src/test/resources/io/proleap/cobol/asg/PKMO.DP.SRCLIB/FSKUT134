      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      *                                                                         
       PROGRAM-ID.    FSKUT134.                                                 
       AUTHOR.        BOB FORD.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  NOV 15, 2004.                                             
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      *   THIS PROGRAM WAS CLONED FROM FSKUT102 AS A REPLACEMENT TO             
      *   CREATE THE STYLE SUMMARY EXTRACT FILES.                               
      *   THE PROCESS HAS CHANGED TO USE EXTRACTS AND UNLOADS UPSTREAM          
      *   TO OBTAIN AND MERGE THE DATA, ELIMINATING MUCH OF THE DB2             
      *   PROCESSING IN THIS PROGRAM,                                           
      *                                                                         
      *   THIS PROGRAM CREATES THE FOLLOWING THREE WEEKLY SKU EXTRACT           
      *   FILES:                                                                
      *        REGULAR PRICED ITEMS                                             
      *        CLEARANCE PRICED ITEMS                                           
      *        E-COMMERCE ITEMS                                                 
      *                                                                         
      *   THE PROGRAM READS FROM THE FOLLOWING TABLES:                          
      *        TCOLOR, TENTNME, TMDARVS, TMDSEAR, TMITGPL, TSIZE, TSK,          
      *        TSKSTWKS, TSKXREF, XMT_LOC, TVNDSTL, TWEBITM.                    
      *   THE REMAINING DATA COMES FROM UNLOADS OF TSKST, TUPC, TUPCPLS,        
      *       TSKSTOO                                                           
      *                                                                         
      *   THE OUTPUT WILL BE USED TO CREATE THE FOLLOWING REPORTS:              
      *        UNIT CONTROL STYLE SUMMARY                                       
      *        UNIT CONTROL STYLE SUMMARY - E-COMMERCE                          
      *        STYLE SUMMARY RECAP                                              
      *        AGING REPORT                                                     
      *        WEEKLY SALES BY PRICE POINT                                      
      *        VENDOR STYLE & COLOR                                             
      *        SUBCLASS, VENDOR, STYLE, COLOR & GROUP                           
      *                                                                         
      *   THE OUTPUT FILE RECORD LAYOUTS ARE:                                   
      *       FSRD125 - RECORDS WRITTEN TO THE REG & CLR FILES                  
      *       FSRD126 - RECORDS WRITTEN TO THE E-COMMERCE FILE                  
      *   NOTE:  RECORD FORMATS FSRD125 & FSRD126 ARE IDENTICAL.                
      *   ----   THE REASON THAT THERE ARE TWO FORMATS IS SO THAT               
      *          THE RECORDS CAN BE BUILT SIMULTANEOUSLY.                       
      *                                                                         
      *   THE INPUT CONTROL CARD IS THE SATURDAY WEEK ENDING DATE IN            
      *   YYYY-MM-DD FORMAT.                                                    
      *                                                                         
      ******************************************************************        
      *  CHANGE LOG:                                                            
      *  11/15/04 - BOB FORD - NEW/CLONED PROGRAM                               
      *             (WR #26357) CHANGED PROGRAM TO USE AN INPUT FILE OF         
      *             SUMMARIZED TSKST, TUPC, TUPCPLS, AND TSKSTOO DATA           
      *             INSTEAD OF DOING DB2 READS TO GET THE DATA HERE.            
      * -------------------------------------------------------------- *00360011
      *  03/29/05 - ANNE KINTOPF                                        00310011
      *             (WR# 26354) CHANGED FOR STORE NUMBER EXPANSION      00320011
      *             AND TO HANDLE NEW NUMERIC STR COLUMN ON TSKSTWKS    00320011
KP0206* -------------------------------------------------------------- *00360011
KP0206*  02/08/06 - KRISTIN PETERSON                                    00310011
KP0206*             (PM00847333) CHANGE TO NOT PERFORM UNNECESSARY      00320011
KP0206*             DATBASE I/O FOR ITEMS WHERE RPT-IND = 'N'.          00320011
RK0906*  09/06/06 - RICH KELLEN                                                 
RK0906*             (WR #30352) CHANGE INPUT LENGTH TO 136 TO USE               
RK0906*             LESS SPACE WHEN SORTING.                                    
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *                                                                         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-CARD-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT ITEM-LOC-EXTRACT-FILE                                         
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT WEEKLY-SKU-EXTRACT-REG-FILE                                   
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT WEEKLY-SKU-EXTRACT-CLR-FILE                                   
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT WEEKLY-SKU-EXTRACT-ECOM-FILE                                  
               ASSIGN TO OUT03.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CD-RECORD                        PIC X(80).                          
                                                                                
       FD  ITEM-LOC-EXTRACT-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
RK0906     RECORD CONTAINS 136 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
RK0906 01  ITEM-LOC-EXTRACT-REC             PIC X(136).                         
                                                                                
       FD  WEEKLY-SKU-EXTRACT-REG-FILE                                          
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  WEEKLY-EXTRACT-REG-RECORD        PIC X(300).                         
                                                                                
       FD  WEEKLY-SKU-EXTRACT-CLR-FILE                                          
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  WEEKLY-EXTRACT-CLR-RECORD        PIC X(300).                         
                                                                                
       FD  WEEKLY-SKU-EXTRACT-ECOM-FILE                                         
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  WEEKLY-EXTRACT-ECOM-RECORD       PIC X(300).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROES             
                                                       COMP SYNC.               
                                                                                
      ******************************************************************        
      *  WORK FIELDS FOR THE FISCAL CALENDAR ROUTINE                            
      ******************************************************************        
           COPY DPWS500.                                                        
                                                                                
      ******************************************************************        
      *  WORK FIELDS FOR NON-INTELLIGENT SKU                                    
      ******************************************************************        
           COPY ISWS006.                                                        
                                                                                
      ******************************************************************        
      *  COPYBOOK FOR ITEM-LOC-EXTRACT INPUT FILE                               
      ******************************************************************        
           COPY FSRD123.                                                        
                                                                                
      ******************************************************************        
      *  WEEKLY SKU EXTRACT FILE RECORD LAYOUT - OUTPUT                         
      ******************************************************************        
           COPY FSRD125.                                                        
                                                                                
      ******************************************************************        
      *  E-COMMERCE WEEKLY SKU EXTRACT FILE RECORD LAYOUT - OUTPUT              
      ******************************************************************        
           COPY FSRD126.                                                        
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-ISO-FMT-DATE.                                                 
               10  CC-ISO-FMT-CC       PIC  X(02)    VALUE SPACE.               
               10  CC-ISO-FMT-YY       PIC  X(02)    VALUE SPACE.               
               10  CC-ISO-FMT-DASH1    PIC  X        VALUE SPACE.               
               10  CC-ISO-FMT-MM       PIC  X(02)    VALUE SPACE.               
               10  CC-ISO-FMT-DASH2    PIC  X        VALUE SPACE.               
               10  CC-ISO-FMT-DD       PIC  X(02)    VALUE SPACE.               
           05  FILLER                  PIC  X(70)    VALUE SPACE.               
                                                                                
      ******************************************************************        
      *    REFORMAT THE CONTROL DATE INTO TIMESTAMP FORMAT                      
      ******************************************************************        
       01  CD-TIMESTAMP-FMT-DATE            PIC X(26)  VALUE SPACE.             
                                                                                
       01  CD-ISO-FMT-CONTROL-DATE.                                             
           05  CD-ISO-FMT-DATE              PIC X(10)  VALUE SPACE.             
           05  FILLER                       PIC X(16)  VALUE                    
                                                   '-23.59.00.000000'.          
       01  SA-SAVE-AREAS.                                                       
           05  SA-BGHT-DATE-FULL.                                               
               10  SA-BGHT-DATE-CC          PIC X(02)  VALUE SPACES.            
               10  SA-BGHT-DATE-YY          PIC X(02)  VALUE SPACES.            
               10  SA-BGHT-DATE-MM          PIC X(02)  VALUE SPACES.            
                                                                                
           05  SA-CC-DATE.                                                      
               10  SA-CC-MM                 PIC X(02)  VALUE SPACES.            
               10  SA-CC-DD                 PIC X(02)  VALUE SPACES.            
               10  SA-CC-YY                 PIC X(02)  VALUE SPACES.            
      ******************************************************************        
      *    REFORMATTED UPCPLS-UPC-STAT-CHG-DTE TO FIT THE FORMAT                
      *    BEING USED IN THE COPYBOOKS FSRD125 AND FSRD126                      
      ******************************************************************        
           05  SA-STAT-DATE                 PIC S9(07) VALUE ZEROS.             
           05  SA-STAT-DATE-REFORMAT  REDEFINES SA-STAT-DATE.                   
               10  SA-RECORD-BLANK          PIC S9(01).                         
               10  SA-STAT1-DATE-YY         PIC S9(02).                         
               10  SA-STAT1-DATE-MM         PIC S9(02).                         
               10  SA-STAT1-DATE-DD         PIC S9(02).                         
           05  SA-STAT-DATE-FULL.                                               
               10  SA-STAT-DATE-CC          PIC X(02)  VALUE SPACES.            
               10  SA-STAT-DATE-YY          PIC X(02)  VALUE SPACES.            
               10  SA-HYPHEN1               PIC X(01)  VALUE SPACES.            
               10  SA-STAT-DATE-MM          PIC X(02)  VALUE SPACES.            
               10  SA-HYPHEN2               PIC X(01)  VALUE SPACES.            
               10  SA-STAT-DATE-DD          PIC X(02)  VALUE SPACES.            
           05  SA-FSCL-BOW-DTE-CURR         PIC X(10)  VALUE SPACES.            
           05  SA-FSCL-BOW-DTE-WK1          PIC X(10)  VALUE SPACES.            
           05  SA-FSCL-BOW-DTE-WK2          PIC X(10)  VALUE SPACES.            
           05  SA-FSCL-BOW-DTE-WK3          PIC X(10)  VALUE SPACES.            
           05  SA-FSCL-BOW-DTE-WK4          PIC X(10)  VALUE SPACES.            
           05  SA-FSCL-BOW-DTE-WK5          PIC X(10)  VALUE SPACES.            
                                                                                
       01  IND-VARIABLES.                                                       
           05  IND-ON-ORDER                 PIC S9(04) COMP.                    
           05  IND-OTB-PER-NBR              PIC S9(04) COMP.                    
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-CSR-SW             PIC X(1)   VALUE SPACES.            
               88  PS-END-OF-CSR                       VALUE 'Y'.               
           05  PS-END-OF-STORE-TABLE-SW     PIC X(1)   VALUE SPACES.            
               88  PS-END-OF-STORE-TABLE               VALUE 'Y'.               
               88  PS-NOT-END-OF-STORE-TABLE           VALUE 'N'.               
           05  PS-ITEM-LOC-EOF-SW           PIC X(1)   VALUE SPACES.            
               88  PS-ITEM-LOC-EOF                     VALUE 'Y'.               
           05  PS-INPUT-REC-IS-STR-ZERO-SW  PIC X(1)   VALUE SPACE.             
               88  PS-INPUT-REC-IS-STR-ZERO            VALUE 'Y'.               
               88  PS-INPUT-REC-NOT-STR-ZERO           VALUE 'N'.               
           05  PS-XREF-TSK-FOUND-SW         PIC X(1)   VALUE SPACE.             
               88  PS-XREF-TSK-FOUND                   VALUE 'Y'.               
               88  PS-XREF-TSK-NOT-FOUND               VALUE 'N'.               
           05  PS-STORE-ECOM-LOC-SW         PIC X(1)   VALUE SPACE.             
               88  PS-STORE-IS-ECOM-LOC                VALUE 'Y'.               
               88  PS-STORE-IS-NOT-ECOM-LOC            VALUE 'N'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME          PIC X(30)  VALUE SPACES.              
           05  PV-DB2-OPERATION-ATTEMPTED PIC X(30)  VALUE SPACES.              
           05  PV-RETRY-CNT               PIC S9(04) VALUE 0                    
                                                     COMP SYNC.                 
           05  PV-PREV-GMA-NBR              PIC X(02)  VALUE SPACES.            
           05  PV-PREV-DMA-NBR              PIC X(02)  VALUE SPACES.            
           05  PV-PREV-DPT-NBR              PIC X(03)  VALUE SPACES.            
           05  PV-PREV-BYR-NBR              PIC X(03)  VALUE SPACES.            
           05  PV-LOC-NBR-9                 PIC 9(5)   VALUE ZEROES.            
           05  PV-LOC-NBR-X REDEFINES                                           
               PV-LOC-NBR-9.                                                    
               10  FILLER                   PIC X(2).                           
               10  PV-LOC-NBR-ALPHA         PIC X(3).                           
           05  PV-PRD-GP-NBR-9              PIC 9(8)   VALUE ZEROES.            
           05  PV-PRD-GP-NBR-X REDEFINES                                        
               PV-PRD-GP-NBR-9              PIC X(8).                           
           05  PV-ITEM-RCP-SW               PIC X(2)   VALUE SPACES.            
               88  PV-ITEM-IS-RCP-25                   VALUE '25'.              
               88  PV-ITEM-IS-RCP-30                   VALUE '30'.              
               88  PV-ITEM-IS-NOT-RCP                  VALUE '  '.              
           05  PV-TUPCPLS-STR-000-VALUES.                                       
               10  PV-000-UNIT-COST-AMT     PIC S9(07)V9(03)                    
                                                       VALUE +0 COMP-3.         
               10  PV-000-CUR-UNIT-RTL-AMT  PIC S9(05)V9(02)                    
                                                       VALUE +0 COMP-3.         
               10  PV-000-STAT-CHNGD-DATE   PIC S9(07)V                         
                                                       VALUE +0 COMP-3.         
               10  PV-000-UPC-ID            PIC S9(9)  VALUE +0 COMP.           
               10  PV-000-ITEM-NUMBER       PIC S9(15) VALUE +0 COMP-3.         
           05  PV-LEVEL-TABLE-SIZE-DISP     PIC ZZ,ZZ9 VALUE ZERO.              
           05  PV-SIZE-TABLE-SIZE-DISP      PIC ZZ,ZZ9 VALUE ZERO.              
           05  PV-STORE-TABLE-SIZE-DISP     PIC ZZ,ZZ9 VALUE ZERO.              
           05  PV-INPUT-CNT-DISP            PIC ZZZ,ZZZ,ZZ9                     
                                                       VALUE ZERO.              
           05  PV-STR-ZERO-INP-CNT-DISP     PIC ZZZ,ZZZ,ZZ9                     
                                                       VALUE ZERO.              
           05  PV-STR-ZERO-FND-CNT-DISP     PIC ZZZ,ZZZ,ZZ9                     
                                                       VALUE ZERO.              
           05  PV-SELECT-TMDARVS-CNT-DISP   PIC ZZZ,ZZZ,ZZ9                     
                                                       VALUE ZERO.              
           05  PV-SELECT-TMITGPL-CNT-DISP   PIC ZZZ,ZZZ,ZZ9                     
                                                       VALUE ZERO.              
           05  PV-SELECT-TWEBITM-CNT-DISP   PIC ZZZ,ZZZ,ZZ9                     
                                                       VALUE ZERO.              
           05  PV-TSKSTWKS-FETCHS-DISP      PIC ZZZ,ZZZ,ZZ9                     
                                                       VALUE ZERO.              
           05  PV-REG-EXTRACT-REC-CNT-DISP  PIC ZZZ,ZZZ,ZZ9                     
                                                       VALUE ZERO.              
           05  PV-CLR-EXTRACT-REC-CNT-DISP  PIC ZZZ,ZZZ,ZZ9                     
                                                       VALUE ZERO.              
           05  PV-ECOM-EXTRACT-REC-CNT-DISP PIC ZZZ,ZZZ,ZZ9                     
                                                       VALUE ZERO.              
                                                                                
       01  PC-PROGRAM-COUNTERS.                                                 
           05  PC-INPUT-CNT                 PIC S9(9)  VALUE +0 COMP-3.         
           05  PC-STORE-ZERO-INPUT-CNT      PIC S9(9)  VALUE +0 COMP-3.         
           05  PC-STORE-ZERO-FOUND-CNT      PIC S9(9)  VALUE +0 COMP-3.         
           05  PC-SELECT-TMDARVS-CNT        PIC S9(9)  VALUE +0 COMP-3.         
           05  PC-SELECT-TMITGPL-CNT        PIC S9(9)  VALUE +0 COMP-3.         
           05  PC-SELECT-TWEBITM-CNT        PIC S9(9)  VALUE +0 COMP-3.         
           05  PC-REG-EXTRACT-REC-CNT       PIC S9(9)  VALUE +0 COMP-3.         
           05  PC-CLR-EXTRACT-REC-CNT       PIC S9(9)  VALUE +0 COMP-3.         
           05  PC-ECOM-EXTRACT-REC-CNT      PIC S9(9)  VALUE +0 COMP-3.         
           05  PC-TSKSTWKS-FETCHS           PIC S9(9)  VALUE +0 COMP-3.         
           05  PC-NBR-STORES                PIC S9(5)  VALUE +0 COMP-3.         
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'FSKUT134'.        
           05  PC-NULL                      PIC S9(3)  VALUE -1.                
           05  PC-DEC-7                     PIC 9(1)   VALUE 7.                 
           05  PC-DEC-14                    PIC 9(2)   VALUE 14.                
           05  PC-DEC-21                    PIC 9(2)   VALUE 21.                
           05  PC-DEC-28                    PIC 9(2)   VALUE 28.                
           05  PC-DEC-35                    PIC 9(2)   VALUE 35.                
           05  PC-30                        PIC X(2)   VALUE '30'.              
           05  PC-REC-INDCT                 PIC X(02)  VALUE '90'.              
           05  PC-RCP-STATUS                PIC X(02)  VALUE '25'.              
           05  PC-MAX-RETRY-CNT             PIC S9(04) VALUE 3                  
                                                       COMP SYNC.               
           05  PC-MORE-THAN-MAX-RETRY       PIC S9(04) VALUE 4                  
                                                       COMP SYNC.               
                                                                                
       01  PD-PROGRAM-DISPLAYS.                                                 
           05  PD-DISP1-ABEND-LINE1.                                            
               10  PD-DISP1-TEXT1           PIC X(50)  VALUE                    
                  '**************************************************'.         
                                                                                
           05  PD-DISP1-ABEND-LINE2.                                            
               10  PD-DISP1-TEXT2           PIC X(50)  VALUE                    
                  '*** ABEND                                      ***'.         
                                                                                
           05  PD-DISP1-ABEND-LINE3.                                            
               10  PD-DISP1-TEXT3           PIC X(16)  VALUE                    
                                                    '*** PROGRAM   = '.         
               10  PD-DISP1-PROGRAM         PIC X(8)   VALUE SPACES.            
               10  PD-DISP1-TEXT4           PIC X(26)  VALUE                    
                                          '                       ***'.         
                                                                                
           05  PD-DISP1-ABEND-LINE4.                                            
               10  PD-DISP1-TEXT5           PIC X(16)  VALUE                    
                                                    '*** PARAGRAPH = '.         
               10  PD-DISP1-PARAGRAPH       PIC X(30)  VALUE SPACES.            
               10  PD-DISP1-TEXT6           PIC X(4)   VALUE    ' ***'.         
                                                                                
           05  PD-DISP1-ABEND-LINE5.                                            
               10  PD-DISP1-TEXT7           PIC X(4)   VALUE                    
                                                                '*** '.         
               10  PD-DISP1-MESSAGE         PIC X(42)  VALUE SPACES.            
               10  PD-DISP1-TEXT8           PIC X(4)   VALUE    ' ***'.         
                                                                                
       01  FS-FILE-STATUS-CODES                        COMP-4 SYNC.             
           05  FS-TSKSTWKS-CODE             PIC S9(9)  VALUE ZERO.              
               88  FS-TSKSTWKS-NOT-FOUND               VALUE +100.              
                                                                                
       01  TABLE-MAX.                                                           
           05  LEVEL-TABLE-MAX              PIC 9(5)   VALUE  1000.             
           05  SIZE-TABLE-MAX               PIC 9(5)   VALUE 20000.             
           05  STORE-TABLE-MAX              PIC 9(5)   VALUE  9999.             
                                                                                
       01  LEVELS-TABLE.                                                        
           05  LEVEL-TABLE-SIZE             PIC  9(5).                          
           05  LEVEL-ENTRY      OCCURS 1 TO 1000 TIMES                          
                                DEPENDING ON LEVEL-TABLE-SIZE                   
                                ASCENDING KEY IS DPT-NBR                        
                                INDEXED BY LEVEL-INDEX.                         
               10  GMA-NBR                  PIC X(02).                          
               10  DMA-NBR                  PIC X(02).                          
               10  DPT-NBR                  PIC X(03).                          
               10  BYR-NBR                  PIC X(03).                          
                                                                                
       01  SIZE-TABLE.                                                          
           05  SIZE-TABLE-SIZE              PIC  9(5).                          
           05  SIZE-ENTRY       OCCURS 1 TO 20000 TIMES                         
                                DEPENDING ON SIZE-TABLE-SIZE                    
                                ASCENDING KEY IS KOHLS-SIZE-CDE                 
                                INDEXED BY SIZE-INDEX.                          
               10  KOHLS-SIZE-CDE           PIC S9(9) COMP.                     
               10  KOHLS-DESC               PIC X(10).                          
                                                                                
       01  STORE-TABLE.                                                         
           05  STORE-TABLE-SIZE             PIC  9(5).                          
           05  STORE-ENTRY      OCCURS 1 TO 9999 TIMES                          
                                DEPENDING ON STORE-TABLE-SIZE                   
                                ASCENDING KEY IS LOC-NBR                        
                                INDEXED BY STORE-INDEX.                         
               10  LOC-NBR                 PIC S9(4) COMP.                      
               10  ECOM-LOC-IND            PIC X(01).                           
                                                                                
      * COPY DB2 ERROR MESSAGES FORMAT                                          
                                                                                
           COPY DPWS004.                                                        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  COLOR TABLE                                                            
      ****                                                                      
           EXEC SQL                                                             
               INCLUDE TCOLOR                                                   
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  NRF SIZE TABLE                                                         
      ****                                                                      
           EXEC SQL                                                             
               INCLUDE TSIZE                                                    
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  SKU TABLE                                                              
      ****                                                                      
           EXEC SQL                                                             
               INCLUDE TSK                                                      
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  WEEKLY SALES TABLE                                                     
      ****                                                                      
           EXEC SQL                                                             
              INCLUDE TSKSTWKS                                                  
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  CROSS REFERENCE TABLE                                                  
      ****                                                                      
           EXEC SQL                                                             
              INCLUDE TSKXREF                                                   
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  STORE TABLE                                                            
      ****                                                                      
           EXEC SQL                                                             
              INCLUDE XMT00001                                                  
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  VENDOR STYLE TABLE                                                     
      ****                                                                      
           EXEC SQL                                                             
              INCLUDE TVNDSTL                                                   
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  MERCHANDISE AREA VENDOR STYLE TABLE                                    
      ****                                                                      
           EXEC SQL                                                             
              INCLUDE TMDARVS                                                   
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  ENTERPRISE NAME TABLE                                                  
      ****                                                                      
           EXEC SQL                                                             
               INCLUDE TENTNME                                                  
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  MERCHANDISE AREA TABLE                                                 
      ****                                                                      
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      ****                                                                      
      *  MERCHANDISE ITEM GROUP PRICE AD LOCATION TABLE                         
      ****                                                                      
           EXEC SQL                                                             
              INCLUDE TMITGPL                                                   
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  WEB ITEM TABLE                                                         
      ****                                                                      
           EXEC SQL                                                             
              INCLUDE TWEBITM                                                   
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      * CURSOR TO RETRIEVE MERCHANDISE AREA INFO                                
      ***************************************************************           
           EXEC SQL                                                             
           DECLARE LEVEL_CSR  CURSOR  FOR                                       
             SELECT A.GMA_NBR                                                   
                  , B.DMA_NBR                                                   
                  , C.DEPT_NBR                                                  
                  , C.BYR_NBR                                                   
               FROM TMDSEAR A                                                   
                  , TMDSEAR B                                                   
                  , TMDSEAR C                                                   
              WHERE   A.OPERCO_NBR = '03'                                       
                AND   A.GMA_NBR    = B.GMA_NBR                                  
                AND   B.DMA_NBR    = C.DMA_NBR                                  
                AND   A.MDSELV_CDE = 'GMA'                                      
                AND   B.MDSELV_CDE = 'DMA'                                      
                AND   C.MDSELV_CDE = 'DPT'                                      
                AND ((A.STRT_DTE  <= :CD-ISO-FMT-DATE)                          
                AND  ((A.END_DTE  >= :CD-ISO-FMT-DATE)                          
                 OR   (A.END_DTE  IS NULL)))                                    
                AND ((B.STRT_DTE  <= :CD-ISO-FMT-DATE)                          
                AND  ((B.END_DTE  >= :CD-ISO-FMT-DATE)                          
                 OR   (B.END_DTE  IS NULL)))                                    
                AND ((C.STRT_DTE  <= :CD-ISO-FMT-DATE)                          
                AND  ((C.END_DTE  >= :CD-ISO-FMT-DATE)                          
                 OR   (C.END_DTE  IS NULL)))                                    
             ORDER BY C.DEPT_NBR                                                
             OPTIMIZE FOR 1000 ROWS                                             
             WITH UR                                                            
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      * CURSOR TO RETRIEVE SIZE INFO                                            
      ***************************************************************           
           EXEC SQL                                                             
           DECLARE  SIZE_CSR  CURSOR  FOR                                       
             SELECT KOHLS_SIZE_CDE                                              
                  , KOHLS_DESC                                                  
               FROM TSIZE                                                       
              WHERE ((EFF_BEG_DTE   <= :CD-ISO-FMT-DATE)                        
                AND  ((EFF_END_DTE  >= :CD-ISO-FMT-DATE)                        
                 OR   (EFF_END_DTE  IS NULL)))                                  
             ORDER BY KOHLS_SIZE_CDE                                            
             OPTIMIZE FOR 13000 ROWS                                            
             WITH UR                                                            
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      * CURSOR TO RETRIEVE 'NOT CLOSED' STORES (OPEN STORES AND NEW             
      * STORES NOT YET OPEN) FROM THE STORE TABLE.                              
      ***************************************************************           
           EXEC SQL                                                             
           DECLARE  LOC_CSR    CURSOR  FOR                                      
             SELECT LOC_NBR                                                     
                   ,E_BUS_IND                                                   
               FROM XMT_LOC                                                     
              WHERE LOC_TYP_CDE    IN ('CS','DC','DS','RV','NA')                
                AND ((CLO_DTE  >= :CD-ISO-FMT-DATE)                             
                 OR  (CLO_DTE  IS NULL))                                        
             ORDER BY LOC_NBR                                                   
             WITH UR                                                            
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      * CURSOR TO RETRIEVE STORE 000 SALES FOR CURRENT WK & PREV 4 WKS          
      ***************************************************************           
           EXEC SQL                                                             
               DECLARE WEEKLY_SALES_CSR CURSOR FOR                              
               SELECT    SUM(SALES_UNIT_QTY)                                    
                        ,FSCL_BOW_DTE                                           
               FROM    TSKSTWKS                                                 
               WHERE ITEM_NMBR      = :SKSTWKS-ITEM-NMBR                        
                 AND STR_NBR        = :SKSTWKS-STR-NBR                          
                 AND FSCL_BOW_DTE IN (:SA-FSCL-BOW-DTE-CURR                     
                                     ,:SA-FSCL-BOW-DTE-WK1                      
                                     ,:SA-FSCL-BOW-DTE-WK2                      
                                     ,:SA-FSCL-BOW-DTE-WK3                      
                                     ,:SA-FSCL-BOW-DTE-WK4)                     
               GROUP BY FSCL_BOW_DTE                                            
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       1000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM 2000-INITIALIZATION.                                         
                                                                                
           PERFORM 3000-PROCESS-SKUS                                            
                       UNTIL PS-ITEM-LOC-EOF.                                   
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      * PERFORM ALL PROGRAM INITIALIZATION.                                     
      ******************************************************************        
       2000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  CONTROL-CARD-FILE                                        
                       ITEM-LOC-EXTRACT-FILE                                    
                OUTPUT WEEKLY-SKU-EXTRACT-REG-FILE                              
                       WEEKLY-SKU-EXTRACT-CLR-FILE                              
                       WEEKLY-SKU-EXTRACT-ECOM-FILE.                            
                                                                                
           INITIALIZE FS125-SKU-EXTRACT-RECORD.                                 
           INITIALIZE FS126-ECOM-SKU-EXTRACT-RECORD.                            
                                                                                
           PERFORM 2100-READ-CONTROL-CARD-FILE.                                 
           PERFORM 2200-GET-6WEEKS-FSCL-BOW-DTE.                                
                                                                                
           PERFORM 7000-READ-ITEM-LOC-INPUT-FILE.                               
                                                                                
      **** ABEND IF INPUT IS EMPTY, OR IF FIRST RECORD IS                       
      **** NOT A STORE ZERO RECORD.                                             
           IF  PS-ITEM-LOC-EOF                                                  
               MOVE '2000-INITIALIZATION'                                       
                                              TO PV-PARAGRAPH-NAME              
               MOVE 'ITEM LOC INPT FILE EMPTY' TO PD-DISP1-MESSAGE              
               PERFORM 9950-USER-ABEND                                          
           ELSE                                                                 
               IF  PS-INPUT-REC-IS-STR-ZERO                                     
                   NEXT SENTENCE                                                
               ELSE                                                             
                   MOVE '2000-INITIALIZATION'                                   
                                              TO PV-PARAGRAPH-NAME              
                   MOVE 'FIRST ITEM LOC RECORD IS NOT STORE 000'                
                                              TO PD-DISP1-MESSAGE               
                   PERFORM 9950-USER-ABEND                                      
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 2300-LOAD-MDSE-LEVEL-TBL.                                    
           PERFORM 2400-LOAD-SIZE-TBL.                                          
           PERFORM 2500-LOAD-LOC-TBL.                                           
                                                                                
      ******************************************************************        
      *  READ THE CONTROL CARD FILE                                    *        
      ******************************************************************        
       2100-READ-CONTROL-CARD-FILE.                                             
                                                                                
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
               AT END DISPLAY '** ABEND ** IN PROGRAM FSKUT134'                 
                      DISPLAY '** DATE CONTROL FILE EMPTY'                      
                      MOVE +4003 TO ABEND-CODE                                  
                      CALL 'ILBOABN0' USING ABEND-CODE.                         
                                                                                
           DISPLAY 'FSKUT134 DATE CONTROL CARD:  ' CC-ISO-FMT-DATE.             
           MOVE CC-ISO-FMT-DATE          TO CD-ISO-FMT-DATE.                    
           MOVE CD-ISO-FMT-CONTROL-DATE  TO CD-TIMESTAMP-FMT-DATE.              
                                                                                
       2200-GET-6WEEKS-FSCL-BOW-DTE.                                            
                                                                                
           MOVE '*2200-GET-6WEEKS-FSCL-BOW-DTE*'                                
                                         TO PV-PARAGRAPH-NAME.                  
      ******************************************************************        
      *  USE DATE ROUTINE INITIALLY TO VALIDATE THAT CONTROL CARD IS A *        
      *  SATURDAY AND RETURN FIRST OF 6 WEEKS SALES INDICATORS.        *        
      *  CALL DATE ROUTINE 5 MORE TIMES DECREMENTING CONTROL CARD DATE *        
      *  7 DAYS MORE EACH TIME IN ORDER TO GET OTHER 5 WEEKS SALES     *        
      *  INDICATORS NEEDED FOR WEEKLY SALES CURSOR.                    *        
      ******************************************************************        
      *USE DATE ROUTINE TO CHECK IF SAT AND GET FISCAL BEGINNING OF WEEK        
      *GET FIRST DAY OF WEEK DATE FOR CURRENT WEEK.                             
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2   TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-ISO-FMT-DATE          TO DPG52-LK-DATE-INPUT.                
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
                   PERFORM 9990-CALENDAR-ABEND                                  
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
           IF  DPG55-NUMERIC-WEEK-DAY-SAT                                       
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = FSKUT134'                                      
               DISPLAY '** PARAGRAPH = 2200-GET-6WEEKS-FSCL-BOW-DTE'            
               DISPLAY '** CONTROL CARD DATE NOT A SATURDAY DATE.'              
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO SA-FSCL-BOW-DTE-CURR.              
                                                                                
      *GET CONTROL DATE MINUS 1WEEK                                             
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2   TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-ISO-FMT-DATE          TO DPG52-LK-DATE-INPUT.                
           SET DPG51-DECREMENT-DATE      TO TRUE.                               
           MOVE PC-DEC-7                 TO DPG51-INCR-DECR-DAYS-9.             
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
                   PERFORM 9990-CALENDAR-ABEND                                  
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO SA-FSCL-BOW-DTE-WK1.               
                                                                                
      *GET CONTROL DATE MINUS 2WEEKS                                            
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2   TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-ISO-FMT-DATE          TO DPG52-LK-DATE-INPUT.                
           SET DPG51-DECREMENT-DATE      TO TRUE.                               
           MOVE PC-DEC-14                TO DPG51-INCR-DECR-DAYS-9.             
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
                   PERFORM 9990-CALENDAR-ABEND                                  
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO SA-FSCL-BOW-DTE-WK2.               
                                                                                
      *GET CONTROL DATE MINUS 3WEEKS                                            
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2   TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-ISO-FMT-DATE          TO DPG52-LK-DATE-INPUT.                
           SET DPG51-DECREMENT-DATE      TO TRUE.                               
           MOVE PC-DEC-21                TO DPG51-INCR-DECR-DAYS-9.             
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
                   PERFORM 9990-CALENDAR-ABEND                                  
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO SA-FSCL-BOW-DTE-WK3.               
                                                                                
      *GET CONTROL DATE MINUS 4WEEKS                                            
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2   TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-ISO-FMT-DATE          TO DPG52-LK-DATE-INPUT.                
           SET DPG51-DECREMENT-DATE      TO TRUE.                               
           MOVE PC-DEC-28                TO DPG51-INCR-DECR-DAYS-9.             
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
                   PERFORM 9990-CALENDAR-ABEND                                  
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO SA-FSCL-BOW-DTE-WK4.               
                                                                                
      *GET CONTROL DATE MINUS 5WEEKS                                            
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2   TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-ISO-FMT-DATE          TO DPG52-LK-DATE-INPUT.                
           SET DPG51-DECREMENT-DATE      TO TRUE.                               
           MOVE PC-DEC-35                TO DPG51-INCR-DECR-DAYS-9.             
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
                   PERFORM 9990-CALENDAR-ABEND                                  
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO SA-FSCL-BOW-DTE-WK5.               
                                                                                
           DISPLAY 'CURRENT BOW SALES DATE    =  ' SA-FSCL-BOW-DTE-CURR.        
           DISPLAY 'WEEK 2  BOW SALES DATE    =  ' SA-FSCL-BOW-DTE-WK1.         
           DISPLAY 'WEEK 3  BOW SALES DATE    =  ' SA-FSCL-BOW-DTE-WK2.         
           DISPLAY 'WEEK 4  BOW SALES DATE    =  ' SA-FSCL-BOW-DTE-WK3.         
           DISPLAY 'WEEK 5  BOW SALES DATE    =  ' SA-FSCL-BOW-DTE-WK4.         
           DISPLAY 'WEEK 6  BOW SALES DATE    =  ' SA-FSCL-BOW-DTE-WK5.         
                                                                                
                                                                                
      ******************************************************************        
      *  LOAD WORKING-STORAGE MERCHANDISE LEVEL TABLE WITH GMA, DMA,            
      *  & DEPT INFO FROM TMDSEAR                                               
      ******************************************************************        
       2300-LOAD-MDSE-LEVEL-TBL.                                                
                                                                                
           MOVE '*2300-LOAD-MDSE-LEVEL-TBL *' TO PV-PARAGRAPH-NAME.             
           EXEC SQL                                                             
               OPEN LEVEL_CSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
              WHEN OTHER                                                        
                MOVE 'OPEN ON LEVEL CURSOR' TO                                  
                    PV-DB2-OPERATION-ATTEMPTED                                  
                PERFORM 9999-SQL-ERROR                                          
           END-EVALUATE.                                                        
                                                                                
           MOVE ZERO TO LEVEL-TABLE-SIZE.                                       
           MOVE 'N' TO PS-END-OF-CSR-SW.                                        
           PERFORM 2301-FETCH-LEVEL-CSR.                                        
                                                                                
           PERFORM VARYING LEVEL-INDEX FROM 1 BY 1                              
                             UNTIL LEVEL-INDEX > LEVEL-TABLE-MAX                
                                OR PS-END-OF-CSR                                
             MOVE MDSEAR-GMA-NBR   TO GMA-NBR (LEVEL-INDEX)                     
             MOVE MDSEAR-DMA-NBR   TO DMA-NBR (LEVEL-INDEX)                     
             MOVE MDSEAR-DEPT-NBR  TO DPT-NBR (LEVEL-INDEX)                     
             MOVE MDSEAR-BYR-NBR   TO BYR-NBR (LEVEL-INDEX)                     
             ADD 1 TO LEVEL-TABLE-SIZE                                          
             PERFORM 2301-FETCH-LEVEL-CSR                                       
           END-PERFORM.                                                         
                                                                                
           IF LEVEL-INDEX >  LEVEL-TABLE-MAX                                    
             DISPLAY '*********** ABEND ************************'               
             DISPLAY ' TABLE OVERFLOW ON LEVELS-TABLE           '               
             DISPLAY ' INCREASE LEVEL-TABLE-MAX LIMIT           '               
             DISPLAY ' CURRENT LIMIT = ' LEVEL-TABLE-MAX                        
             DISPLAY '******************************************'               
             DISPLAY '  '                                                       
             MOVE +4004 TO ABEND-CODE                                           
             CALL 'ILBOABN0' USING ABEND-CODE                                   
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               CLOSE LEVEL_CSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
              WHEN OTHER                                                        
                MOVE 'CLOSE ON LEVEL CURSOR' TO                                 
                    PV-DB2-OPERATION-ATTEMPTED                                  
                PERFORM 9999-SQL-ERROR                                          
           END-EVALUATE.                                                        
                                                                                
       2301-FETCH-LEVEL-CSR.                                                    
                                                                                
           MOVE '*2301-FETCH-LEVEL-CSR.        *' TO PV-PARAGRAPH-NAME.         
                                                                                
           EXEC SQL                                                             
               FETCH LEVEL_CSR                                                  
               INTO :MDSEAR-GMA-NBR                                             
                  , :MDSEAR-DMA-NBR                                             
                  , :MDSEAR-DEPT-NBR                                            
                  , :MDSEAR-BYR-NBR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN +0                                                            
                   CONTINUE                                                     
             WHEN +100                                                          
                   SET PS-END-OF-CSR TO TRUE                                    
             WHEN OTHER                                                         
                   MOVE 'FETCH ON LEVEL CURSOR' TO                              
                        PV-DB2-OPERATION-ATTEMPTED                              
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  LOAD WORKING-STORAGE SIZE TABLE WITH SIZE CODE/DESC                    
      ******************************************************************        
       2400-LOAD-SIZE-TBL.                                                      
                                                                                
           MOVE '*2400-LOAD-SIZE-TBL      *' TO PV-PARAGRAPH-NAME.              
           EXEC SQL                                                             
               OPEN SIZE_CSR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
              WHEN OTHER                                                        
                MOVE 'OPEN ON SIZE CURSOR' TO                                   
                    PV-DB2-OPERATION-ATTEMPTED                                  
                PERFORM 9999-SQL-ERROR                                          
           END-EVALUATE.                                                        
                                                                                
           MOVE ZERO TO SIZE-TABLE-SIZE.                                        
           MOVE 'N' TO PS-END-OF-CSR-SW.                                        
           PERFORM 2401-FETCH-SIZE-CSR.                                         
                                                                                
           PERFORM VARYING SIZE-INDEX FROM 1 BY 1                               
                             UNTIL SIZE-INDEX > SIZE-TABLE-MAX                  
                                OR PS-END-OF-CSR                                
             MOVE SIZE-KOHLS-SIZE-CDE TO  KOHLS-SIZE-CDE(SIZE-INDEX)            
             MOVE SIZE-KOHLS-DESC     TO  KOHLS-DESC    (SIZE-INDEX)            
             ADD 1 TO SIZE-TABLE-SIZE                                           
             PERFORM 2401-FETCH-SIZE-CSR                                        
           END-PERFORM.                                                         
                                                                                
           IF SIZE-INDEX >  SIZE-TABLE-MAX                                      
             DISPLAY '*********** ABEND ************************'               
             DISPLAY ' TABLE OVERFLOW ON SIZE-TABLE             '               
             DISPLAY ' INCREASE SIZE-TABLE-MAX LIMIT            '               
             DISPLAY ' CURRENT LIMIT = ' SIZE-TABLE-MAX                         
             DISPLAY '******************************************'               
             DISPLAY '  '                                                       
             MOVE +4004 TO ABEND-CODE                                           
             CALL 'ILBOABN0' USING ABEND-CODE                                   
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               CLOSE SIZE_CSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
              WHEN OTHER                                                        
                MOVE 'CLOSE ON SIZE CURSOR' TO                                  
                    PV-DB2-OPERATION-ATTEMPTED                                  
                PERFORM 9999-SQL-ERROR                                          
           END-EVALUATE.                                                        
                                                                                
       2401-FETCH-SIZE-CSR.                                                     
                                                                                
           MOVE '*2401-FETCH-SIZE-CSR.        *' TO PV-PARAGRAPH-NAME.          
                                                                                
           EXEC SQL                                                             
               FETCH SIZE_CSR                                                   
               INTO :SIZE-KOHLS-SIZE-CDE                                        
                  , :SIZE-KOHLS-DESC                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN +0                                                            
                   CONTINUE                                                     
             WHEN +100                                                          
                   SET PS-END-OF-CSR TO TRUE                                    
             WHEN OTHER                                                         
                   MOVE 'FETCH ON SIZE CURSOR' TO                               
                        PV-DB2-OPERATION-ATTEMPTED                              
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  LOAD WORKING-STORAGE STORE TABLE WITH STORE NUMBER & E-COMM            
      *  LOCATION INDICATOR.                                                    
      ******************************************************************        
       2500-LOAD-LOC-TBL.                                                       
                                                                                
           MOVE '*2500-LOAD-LOC-TBL     *' TO PV-PARAGRAPH-NAME.                
           EXEC SQL                                                             
               OPEN LOC_CSR                                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
              WHEN OTHER                                                        
                MOVE 'OPEN ON STORE CURSOR' TO                                  
                    PV-DB2-OPERATION-ATTEMPTED                                  
                PERFORM 9999-SQL-ERROR                                          
           END-EVALUATE.                                                        
                                                                                
           MOVE ZERO TO STORE-TABLE-SIZE.                                       
           MOVE 'N' TO PS-END-OF-CSR-SW.                                        
           PERFORM 2501-FETCH-LOC-CSR.                                          
                                                                                
           PERFORM VARYING STORE-INDEX FROM 1 BY 1                              
                             UNTIL STORE-INDEX > STORE-TABLE-MAX                
                                OR PS-END-OF-CSR                                
             MOVE XMT00001-LOC-NBR    TO LOC-NBR(STORE-INDEX)                   
             MOVE XMT00001-E-BUS-IND TO ECOM-LOC-IND(STORE-INDEX)               
             ADD 1 TO STORE-TABLE-SIZE                                          
             PERFORM 2501-FETCH-LOC-CSR                                         
           END-PERFORM.                                                         
                                                                                
           IF STORE-INDEX >  STORE-TABLE-MAX                                    
             DISPLAY '*********** ABEND ************************'               
             DISPLAY ' TABLE OVERFLOW ON STORE-TABLE            '               
             DISPLAY ' INCREASE STORE-TABLE-MAX LIMIT           '               
             DISPLAY ' CURRENT LIMIT = ' STORE-TABLE-MAX                        
             DISPLAY '******************************************'               
             DISPLAY '  '                                                       
             MOVE +4004 TO ABEND-CODE                                           
             CALL 'ILBOABN0' USING ABEND-CODE                                   
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               CLOSE LOC_CSR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
              WHEN OTHER                                                        
                MOVE 'CLOSE ON STORE CURSOR' TO                                 
                    PV-DB2-OPERATION-ATTEMPTED                                  
                PERFORM 9999-SQL-ERROR                                          
           END-EVALUATE.                                                        
                                                                                
       2501-FETCH-LOC-CSR.                                                      
                                                                                
           MOVE '*2501-FETCH-LOC-CSR.       *' TO PV-PARAGRAPH-NAME.            
                                                                                
           EXEC SQL                                                             
               FETCH LOC_CSR                                                    
               INTO :XMT00001-LOC-NBR                                           
                  , :XMT00001-E-BUS-IND                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN +0                                                            
                   CONTINUE                                                     
             WHEN +100                                                          
                   SET PS-END-OF-CSR TO TRUE                                    
             WHEN OTHER                                                         
                   MOVE 'FETCH ON STORE CURSOR' TO                              
                        PV-DB2-OPERATION-ATTEMPTED                              
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS ALL THE SKUS ON THE ITEM-LOC FILE STORE-ZERO RECS               
      ******************************************************************        
       3000-PROCESS-SKUS.                                                       
                                                                                
           IF PS-INPUT-REC-IS-STR-ZERO                                          
               SET PS-INPUT-REC-NOT-STR-ZERO TO TRUE                            
KP0205         MOVE SPACES TO PV-ITEM-RCP-SW                                    
               PERFORM 7100-SELECT-XREF-TSK                                     
               IF PS-XREF-TSK-FOUND                                             
                   IF FS123-REGN-PRIC-IND = 'Y'                                 
                       MOVE FS123-UPC-STAT-CDE TO PV-ITEM-RCP-SW                
                   ELSE                                                         
                       MOVE SPACES TO PV-ITEM-RCP-SW                            
                   END-IF                                                       
                   PERFORM 4000-PROCESS-STORE-ZERO                              
                   IF FS123-REGN-PRIC-IND = 'Y'                                 
                       PERFORM 4010-PROCESS-RCP                                 
                   ELSE                                                         
                       PERFORM 4020-PROCESS-NON-RCP                             
                   END-IF                                                       
               ELSE                                                             
                   DISPLAY '*** XREF-UPC NOT FOUND FOR ITEM NBR '               
                           FS123-ITEM-NMBR ' ***'                               
                   PERFORM 7000-READ-ITEM-LOC-INPUT-FILE                        
                     UNTIL PS-INPUT-REC-IS-STR-ZERO                             
                        OR PS-ITEM-LOC-EOF                                      
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * PROCESS THE INPUT STORE ZERO RECORD                                     
      *   - THIS PARAGRAPH WILL PROCESS THE STORE ZERO RECORD, AND              
      *     DRIVE PROCESSING OF THE INDIVIDUAL STORES FOR THAT ITEM             
      *     UNTIL THE NEXT STORE ZERO RECORD IS READ.                           
      ******************************************************************        
       4000-PROCESS-STORE-ZERO.                                                 
                                                                                
           INITIALIZE FS125-SKU-EXTRACT-RECORD.                                 
           INITIALIZE FS126-ECOM-SKU-EXTRACT-RECORD.                            
           MOVE +0 TO PC-NBR-STORES                                             
                      PV-000-UNIT-COST-AMT                                      
                      PV-000-CUR-UNIT-RTL-AMT                                   
                      PV-000-STAT-CHNGD-DATE                                    
                      PV-000-UPC-ID.                                            
                                                                                
      **** SAVE THE CURRENT ITEM NUMBER BEING PROCESSED.                        
           MOVE FS123-ITEM-NMBR TO PV-000-ITEM-NUMBER.                          
                                                                                
      **** MOVE STORE 000 VALUES TO OUTPUT RECORD.  THESE FIELDS ARE            
      **** FILLED WITH THE SAME VALUES REGARDLESS OF UPC-STAT-CDE.              
           MOVE SKXREF-DEPT          TO NI-DEPT-9-3.                            
           MOVE NI-DEPT-X-3          TO FS125-MERCH-DEPT-NMBR.                  
                                                                                
           MOVE SKXREF-CLASS         TO NI-CLASS-9-2.                           
           MOVE NI-CLASS-X-2         TO FS125-MAJOR-CLASS.                      
                                                                                
           MOVE SKXREF-SUBCLASS      TO NI-SUB-CLASS-9-2                        
           MOVE NI-SUB-CLASS-X-2     TO FS125-SUBCLASS.                         
                                                                                
           MOVE SKXREF-SKU           TO NI-SKU-9                                
           MOVE NI-SKU               TO FS125-SKU.                              
                                                                                
           MOVE FS123-GP-CDE         TO FS125-GROUP-CODE.                       
           MOVE FS123-INTR-UPC-DESC  TO FS125-SKU-DESCRIPTION.                  
           MOVE FS123-CSTM-CLOR-DESC TO FS125-COLOR-DESC.                       
           MOVE FS123-RPLNMT-IND     TO FS125-RPLNMT-IND.                       
           MOVE FS123-KY-ITM-IND     TO FS125-KY-ITM-IND.                       
           MOVE FS123-SEA-SEQ-NBR    TO FS125-SEA-SEQ-NBR.                      
           MOVE TSK-FS-RPT-IND       TO FS125-FS-RPT-IND.                       
                                                                                
           MOVE FS123-LAST-SALE-DATE  TO FS125-LAST-SALE-DATE.                  
           MOVE FS123-FIRST-SALE-DATE TO FS125-FIRST-SALE-DATE.                 
           MOVE FS123-LAST-RCVD-DATE  TO FS125-LAST-RCVD-DATE.                  
           MOVE FS123-FIRST-RCVD-DATE TO FS125-FIRST-RCVD-DATE.                 
                                                                                
      **** REFORMAT THE CONTROL CARD DATE AND MOVE TO OUTPUT RECORD.            
           MOVE CC-ISO-FMT-DD        TO SA-CC-DD.                               
           MOVE CC-ISO-FMT-MM        TO SA-CC-MM.                               
           MOVE CC-ISO-FMT-YY        TO SA-CC-YY.                               
           MOVE SA-CC-DATE           TO FS125-CUR-WK-ENDING-DATE.               
                                                                                
      **** MOVE CONSTANT VALUE TO OUTPUT RECORD.                                
           MOVE PC-REC-INDCT         TO FS125-RECORD-INDICATOR.                 
                                                                                
      **** FILL OUTPUT RECORD WITH VALUES FROM THE WORKING-STORAGE              
      **** LEVELS-TABLE AND SIZE-TABLE                                          
           IF  NI-DEPT-X-3 = PV-PREV-DPT-NBR                                    
               MOVE PV-PREV-GMA-NBR TO FS125-MERCH-DIV-CODE                     
               MOVE PV-PREV-DMA-NBR TO FS125-DMM-NMBR                           
               MOVE PV-PREV-BYR-NBR TO FS125-BUYER-NMBR                         
           ELSE                                                                 
               PERFORM 4100-SEARCH-LEVEL-LOOKUP-TBL                             
           END-IF.                                                              
                                                                                
           PERFORM 4200-SEARCH-SIZE-LOOKUP-TBL.                                 
                                                                                
KP0206     IF  TSK-FS-RPT-IND = 'Y'                                             
KP0206         PERFORM 7200-SELECT-VENDOR-STYLE-INFO                            
KP0206         IF  FS123-MEITGP-NBR > 0                                         
KP0206             PERFORM 7300-SELECT-TMITGPL                                  
KP0206         END-IF                                                           
KP0206         PERFORM 7400-SELECT-TWEBITM                                      
KP0206     END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PROCESSES RCP ITEMS.  IT FIRST SAVES THE STORE           
      * ZERO DATA THAT IS REQUIRED FOR RCP PROCESSING OF STORES NOT             
      * FOUND ON TUPCPLS.  IT THEN INITIATES THE RCP STORE-LEVEL                
      * PROCESSING.                                                             
      ******************************************************************        
       4010-PROCESS-RCP.                                                        
                                                                                
           IF  PV-ITEM-IS-RCP-25                                                
               MOVE PC-RCP-STATUS TO FS125-RCP-STATUS-CODE                      
           ELSE                                                                 
               MOVE SPACES        TO FS125-RCP-STATUS-CODE                      
           END-IF.                                                              
                                                                                
           MOVE 'N'            TO FS125-EBUS-LOC-IND.                           
                                                                                
      **** SAVE THE STORE ZERO DATA.  THIS DATA WILL BE                         
      **** WRITTEN ON REG EXTRACT RECORDS FOR RCP ITEMS THAT ARE                
      **** NOT ON CLEARANCE FOR A SPECIFIC STORE (STORE NOT ON TUPCPLS)         
                                                                                
           PERFORM 4510-SAVE-STORE-000-DATA.                                    
                                                                                
           PERFORM 7000-READ-ITEM-LOC-INPUT-FILE.                               
                                                                                
           SET STORE-INDEX TO 1.                                                
           PERFORM  4015-PROCESS-RCP-STORES                                     
             UNTIL (PS-INPUT-REC-IS-STR-ZERO                                    
                OR  PS-ITEM-LOC-EOF).                                           
                                                                                
           SET PS-NOT-END-OF-STORE-TABLE TO TRUE.                               
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS STORE-LEVEL PROCESSING FOR RCP ITEMS.           
      * ALL STORES ON THE TSKST UNLOAD FILE ARE PROCESSED.  ALSO,               
      * STORES IN THE WORKING-STORAGE STORE-TABLE THAT ARE NOT ON               
      * THE TSKST UNLOAD ARE PROCESSED AND AN EXTRACT RECORD IS                 
      * WRITTEN IF ON-ORDER QTY FOR THAT ITEM IS GREATER THAN ZERO.             
      * A MATCH/MERGE OF STORES ON THE TSKST UNLOAD FILE AND THE STORES         
      * IN THE STORE-TABLE DRIVES THE RCP STORE-LEVEL PROCESSING.               
      ******************************************************************        
       4015-PROCESS-RCP-STORES.                                                 
                                                                                
           IF  FS123-LOC-NBR = LOC-NBR (STORE-INDEX)                            
               PERFORM 4016-PROCESS-RCP-STORE                                   
           ELSE                                                                 
               IF (FS123-LOC-NBR > LOC-NBR (STORE-INDEX)                        
               AND NOT PS-END-OF-STORE-TABLE)                                   
               OR  PS-INPUT-REC-IS-STR-ZERO                                     
               OR  PS-ITEM-LOC-EOF                                              
                   CONTINUE                                                     
               ELSE                                                             
                   IF (FS123-LOC-NBR < LOC-NBR (STORE-INDEX)                    
                   OR  PS-END-OF-STORE-TABLE)                                   
                       PERFORM 4016-PROCESS-RCP-STORE                           
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 4018-INIT-FIELDS-FOR-RCP.                                    
                                                                                
           IF  FS123-LOC-NBR = LOC-NBR (STORE-INDEX)                            
               PERFORM 7000-READ-ITEM-LOC-INPUT-FILE                            
               SET STORE-INDEX UP BY 1                                          
               IF  STORE-INDEX > STORE-TABLE-SIZE                               
                   SET PS-END-OF-STORE-TABLE TO TRUE                            
               END-IF                                                           
           ELSE                                                                 
               IF (FS123-LOC-NBR > LOC-NBR (STORE-INDEX)                        
               AND NOT PS-END-OF-STORE-TABLE)                                   
               OR  PS-INPUT-REC-IS-STR-ZERO                                     
               OR  PS-ITEM-LOC-EOF                                              
                   SET STORE-INDEX UP BY 1                                      
                   IF  STORE-INDEX > STORE-TABLE-SIZE                           
                       SET PS-END-OF-STORE-TABLE TO TRUE                        
                   END-IF                                                       
               ELSE                                                             
                   IF (FS123-LOC-NBR < LOC-NBR (STORE-INDEX)                    
                   OR  PS-END-OF-STORE-TABLE)                                   
                       PERFORM 7000-READ-ITEM-LOC-INPUT-FILE                    
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS STANDARD PROCESSING TO WRITE THE                
      * EXTRACT RECORDS FOR STORES ON THE TSKST UNLOAD FILE.                    
      ******************************************************************        
       4016-PROCESS-RCP-STORE.                                                  
                                                                                
           PERFORM 4500-FORMAT-INPUT-DATA                                       
           MOVE FS123-UPC-STAT-CDE TO FS125-STATUS-CODE.                        
                                                                                
      **** MOVE STORE-LEVEL DATA FROM THE INPUT RECORD TO THE                   
      **** REG/CLR EXTRACT RECORD.                                              
           MOVE FS123-ITEM-NMBR          TO FS125-ITEM-NMBR.                    
           MOVE FS123-LOC-NBR            TO FS125-LOC-NBR.                      
           MOVE FS123-ONHAND-QTY         TO FS125-CUR-ONHAND-QTY.               
           MOVE FS123-YTD-SALES-UNIT-QTY TO FS125-YTD-SALES-UNIT-QTY.           
                                                                                
           MOVE FS123-LOC-NBR TO SKSTWKS-STR-NBR                                
                                                                                
KP0206     IF  FS123-FOUND-ON-TSKST-IND = 'Y'                                   
KP0206     AND TSK-FS-RPT-IND = 'Y'                                             
               PERFORM 4600-PROCESS-WEEKLY-SALES                                
           END-IF.                                                              
                                                                                
           PERFORM 7610-FORMAT-ONORD-DATA                                       
                                                                                
           IF  FS123-ONHAND-QTY > 0                                             
               MOVE +1 TO FS125-NBR-STORES                                      
           ELSE                                                                 
               MOVE  0 TO FS125-NBR-STORES                                      
           END-IF.                                                              
                                                                                
      **** DO NOT WRITE AN E-COMMERCE EXTRACT REC FOR CLEARANCE ITEMS           
KP0205     IF  FS123-UPC-STAT-CDE = PC-30                                       
               NEXT SENTENCE                                                    
           ELSE                                                                 
               IF  ECOM-LOC-IND (STORE-INDEX) = 'Y'                             
      ********     COPY THE DATA FROM THE REG/CLR RECORD FORMAT TO THE          
      ********     E-COMMERCE RECORD FORMAT                                     
                   MOVE FS123-ONORD-QTY TO FS125-ON-ORD-QTY                     
                   IF FS123-MIN-OTB-PER-NBR = SPACES                            
                       MOVE '00'          TO FS125-ORD-BGHT-YY                  
                       MOVE '00'          TO FS125-ORD-BGHT-MM                  
                   ELSE                                                         
                       MOVE FS123-MIN-OTB-PER-NBR TO SA-BGHT-DATE-FULL          
                       MOVE SA-BGHT-DATE-YY TO FS125-ORD-BGHT-YY                
                       MOVE SA-BGHT-DATE-MM TO FS125-ORD-BGHT-MM                
                   END-IF                                                       
                   MOVE FS125-SKU-EXTRACT-RECORD                                
                     TO FS126-ECOM-SKU-EXTRACT-RECORD                           
      ********     COPY THE STORE-LEVEL DATE FIELDS                             
                   MOVE FS123-LAST-SALE-DATE                                    
                                         TO FS126-LAST-SALE-DATE                
                   MOVE FS123-FIRST-SALE-DATE                                   
                                         TO FS126-FIRST-SALE-DATE               
                   MOVE FS123-LAST-RCVD-DATE                                    
                                         TO FS126-LAST-RCVD-DATE                
                   MOVE FS123-FIRST-RCVD-DATE                                   
                                         TO FS126-FIRST-RCVD-DATE               
                   MOVE 'Y' TO FS126-EBUS-LOC-IND                               
                   PERFORM 6100-WRITE-ECOM-EXTRACT-REC                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 6000-WRITE-EXTRACT-REC.                                      
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS THE PROCESSING REQUIRED TO WRITE                
      * THE EXTRACT RECORDS FOR STORES THAT ARE NOT ON THE TSKST                
      * UNLOAD FILE, BUT HAVE THE ITEM ON ORDER.  THESE RECORDS WILL            
      * NOT INCLUDE SALES OR ON-HAND DATA.                                      
      ******************************************************************        
       4017-PROCESS-RCP-OO-FOR-STORE.                                           
                                                                                
           PERFORM 7610-FORMAT-ONORD-DATA                                       
           IF FS125-ON-ORD-QTY > 0                                              
               MOVE PV-000-ITEM-NUMBER TO FS125-ITEM-NMBR                       
               MOVE LOC-NBR (STORE-INDEX) TO FS125-LOC-NBR                      
               MOVE FS123-UPC-STAT-CDE TO FS125-STATUS-CODE                     
               PERFORM 4520-FORMAT-INPUT-000-DATA                               
               IF ECOM-LOC-IND (STORE-INDEX) = 'Y'                              
KP0205             AND PV-ITEM-IS-RCP-25                                        
  ********             COPY THE DATA FROM THE REG/CLR RECORD FORMAT             
  ********             TO THE E-COMMERCE RECORD FORMAT                          
                   MOVE FS125-SKU-EXTRACT-RECORD                                
                     TO FS126-ECOM-SKU-EXTRACT-RECORD                           
  ********             ZERO OUT THE DATE FIELDS - DO NOT HAVE                   
  ********             STORE-SPECIFIC DATA                                      
                   MOVE ZEROS TO FS126-LAST-SALE-DATE                           
                                 FS126-FIRST-SALE-DATE                          
                                 FS126-LAST-RCVD-DATE                           
                                 FS126-FIRST-RCVD-DATE                          
                   MOVE 'Y' TO FS126-EBUS-LOC-IND                               
                   PERFORM 6100-WRITE-ECOM-EXTRACT-REC                          
               END-IF                                                           
               PERFORM 6000-WRITE-EXTRACT-REC                                   
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * INITIALIZE THE STORE SPECIFIC FIELDS ON THE REG/CLR OUTPUT              
      * RECORD AS REQUIRED FOR RCP STORE LEVEL PROCESSING.                      
      ******************************************************************        
       4018-INIT-FIELDS-FOR-RCP.                                                
                                                                                
           MOVE ZEROES TO FS125-UNIT-COST-AMT                                   
                          FS125-CUR-UNIT-RTL-AMT                                
                          FS125-CUR-ONHAND-QTY                                  
                          FS125-YTD-SALES-UNIT-QTY                              
                          FS125-CUR-SALES-UNIT-QTY                              
                          FS125-WK1-SALES-UNIT-QTY                              
                          FS125-WK2-SALES-UNIT-QTY                              
                          FS125-WK3-SALES-UNIT-QTY                              
                          FS125-WK4-SALES-UNIT-QTY                              
                          FS125-WK5-SALES-UNIT-QTY                              
                          FS125-ON-ORD-QTY                                      
                          FS125-ITEM-NMBR                                       
                          FS125-NBR-STORES                                      
                          FS125-STAT-CHNGD-DATE.                                
                                                                                
           MOVE SPACES TO FS125-STATUS-CODE                                     
                          FS125-ORD-BGHT-YY                                     
                          FS125-ORD-BGHT-MM.                                    
                                                                                
           MOVE 'N' TO    FS125-EBUS-LOC-IND.                                   
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PROCESSES NON-RCP ITEMS.  IT FIRST FORMATS THE           
      * STORE ZERO DATA THAT IS REQUIRED FOR THESE ITEMS.  IT THEN              
      * INITIATES THE STORE-LEVEL NON-RCP PROCESSING.                           
      ******************************************************************        
       4020-PROCESS-NON-RCP.                                                    
                                                                                
           MOVE SPACES TO FS125-RCP-STATUS-CODE.                                
           MOVE 'N'    TO FS125-EBUS-LOC-IND.                                   
                                                                                
      **** MOVE STORE ZERO DATA FROM THE TSKST UNLOAD RECORD TO THE             
      **** REG/CLR EXTRACT RECORD.                                              
           MOVE FS123-ITEM-NMBR          TO FS125-ITEM-NMBR.                    
           MOVE FS123-LOC-NBR            TO FS125-LOC-NBR.                      
           MOVE FS123-ONHAND-QTY         TO FS125-CUR-ONHAND-QTY.               
           MOVE FS123-YTD-SALES-UNIT-QTY TO FS125-YTD-SALES-UNIT-QTY.           
                                                                                
      **** MOVE THE TUPCPLS STORE ZERO DATA TO THE REG/CLR EXTRACT              
      **** RECORD.  THIS STORE ZERO DATA WILL ALSO BE COPIED TO                 
      **** ANY NON-RCP STORE-LEVEL E-COMMERCE EXTRACT RECORDS WRITTEN.          
           PERFORM 4500-FORMAT-INPUT-DATA.                                      
                                                                                
           MOVE ZERO   TO SKSTWKS-STR-NBR.                                      
KP0206     IF  FS123-FOUND-ON-TSKST-IND = 'Y'                                   
KP0206     AND TSK-FS-RPT-IND = 'Y'                                             
               PERFORM 4600-PROCESS-WEEKLY-SALES                                
           END-IF.                                                              
           MOVE FS123-ONORD-QTY      TO FS125-ON-ORD-QTY                        
           IF FS123-MIN-OTB-PER-NBR = SPACES                                    
               MOVE '00'                  TO FS125-ORD-BGHT-YY                  
               MOVE '00'                  TO FS125-ORD-BGHT-MM                  
           ELSE                                                                 
               MOVE FS123-MIN-OTB-PER-NBR TO SA-BGHT-DATE-FULL                  
               MOVE SA-BGHT-DATE-YY       TO FS125-ORD-BGHT-YY                  
               MOVE SA-BGHT-DATE-MM       TO FS125-ORD-BGHT-MM                  
           END-IF.                                                              
                                                                                
           PERFORM 7000-READ-ITEM-LOC-INPUT-FILE.                               
                                                                                
           SET STORE-INDEX TO 1.                                                
           PERFORM  4025-PROCESS-NON-RCP-STORES                                 
             UNTIL  PS-END-OF-STORE-TABLE                                       
               AND (PS-INPUT-REC-IS-STR-ZERO                                    
                OR  PS-ITEM-LOC-EOF).                                           
                                                                                
           SET PS-NOT-END-OF-STORE-TABLE TO TRUE.                               
           MOVE PC-NBR-STORES TO FS125-NBR-STORES.                              
                                                                                
           PERFORM 6000-WRITE-EXTRACT-REC.                                      
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH DRIVES STORE-LEVEL PROCESSING FOR NON-RCP                
      * ITEMS.  INDIVIDUAL STORES ARE PROCESSED TO DETERMINE THE                
      * NUMBER OF STORES WITH AN ON-HAND QTY GREATER THAN ZERO.                 
      * ALSO, AN E-COMMERCE EXTRACT RECORD IS WRITTEN WHEN AN                   
      * E-COMMERCE LOCATION IS ENCOUNTERED.  A MATCH/MERGE OF STORES            
      * ON THE TSKST UNLOAD FILE AND THE STORES IN THE STORE-TABLE              
      * DRIVES THE STORE-LEVEL PROCESSING.                                      
      ******************************************************************        
       4025-PROCESS-NON-RCP-STORES.                                             
                                                                                
           IF  FS123-LOC-NBR = LOC-NBR (STORE-INDEX)                            
               PERFORM 4026-PROCESS-NON-RCP-TSKST-STR                           
           ELSE                                                                 
               IF (FS123-LOC-NBR > LOC-NBR (STORE-INDEX)                        
               AND NOT PS-END-OF-STORE-TABLE)                                   
               OR  PS-INPUT-REC-IS-STR-ZERO                                     
               OR  PS-ITEM-LOC-EOF                                              
                   PERFORM 4028-PROCESS-NON-RCP-OO-STR                          
               ELSE                                                             
                   IF (FS123-LOC-NBR < LOC-NBR (STORE-INDEX)                    
                   OR  PS-END-OF-STORE-TABLE)                                   
                       PERFORM 4026-PROCESS-NON-RCP-TSKST-STR                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  FS123-LOC-NBR = LOC-NBR (STORE-INDEX)                            
               PERFORM 7000-READ-ITEM-LOC-INPUT-FILE                            
               SET STORE-INDEX UP BY 1                                          
               IF  STORE-INDEX > STORE-TABLE-SIZE                               
                   SET PS-END-OF-STORE-TABLE TO TRUE                            
               END-IF                                                           
           ELSE                                                                 
               IF (FS123-LOC-NBR > LOC-NBR (STORE-INDEX)                        
               AND NOT PS-END-OF-STORE-TABLE)                                   
               OR  PS-INPUT-REC-IS-STR-ZERO                                     
               OR  PS-ITEM-LOC-EOF                                              
                   SET STORE-INDEX UP BY 1                                      
                   IF  STORE-INDEX > STORE-TABLE-SIZE                           
                       SET PS-END-OF-STORE-TABLE TO TRUE                        
                   END-IF                                                       
               ELSE                                                             
                   IF (FS123-LOC-NBR < LOC-NBR (STORE-INDEX)                    
                   OR  PS-END-OF-STORE-TABLE)                                   
                       PERFORM 7000-READ-ITEM-LOC-INPUT-FILE                    
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS STANDARD PROCESSING FOR STORES ON THE           
      * TSKST UNLOAD FILE.                                                      
      ******************************************************************        
       4026-PROCESS-NON-RCP-TSKST-STR.                                          
                                                                                
           IF  FS123-ONHAND-QTY > 0                                             
               ADD +1 TO PC-NBR-STORES                                          
           END-IF.                                                              
                                                                                
      **** DO NOT WRITE AN E-COMMERCE EXTRACT REC FOR CLEARANCE ITEMS           
           IF  FS125-STATUS-CODE = '30'                                         
               NEXT SENTENCE                                                    
           ELSE                                                                 
               IF  ECOM-LOC-IND (STORE-INDEX) = 'Y'                             
                   PERFORM 4027-FORMAT-TSKST-NON-RCP-ECOM                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH FORMATS THE E-COMMERCE RECORD FOR A STORE ON             
      * THE TSKST UNLOAD FILE.                                                  
      ******************************************************************        
       4027-FORMAT-TSKST-NON-RCP-ECOM.                                          
                                                                                
      **** COPY THE SKU LEVEL DATA FROM THE REG/CLR RECORD FORMAT               
      **** TO THE E-COMMERCE RECORD FORMAT                                      
           MOVE FS125-SKU-EXTRACT-RECORD                                        
             TO FS126-ECOM-SKU-EXTRACT-RECORD.                                  
                                                                                
      **** MOVE STORE-LEVEL DATA FROM THE TSKST UNLOAD RECORD TO THE            
      **** E-COMMERCE EXTRACT RECORD.                                           
           MOVE FS123-ITEM-NMBR          TO FS126-ITEM-NMBR.                    
           MOVE FS123-LOC-NBR            TO FS126-LOC-NBR.                      
           MOVE FS123-ONHAND-QTY         TO FS126-CUR-ONHAND-QTY.               
           MOVE FS123-YTD-SALES-UNIT-QTY TO FS126-YTD-SALES-UNIT-QTY.           
           MOVE FS123-LAST-SALE-DATE     TO FS126-LAST-SALE-DATE.               
           MOVE FS123-FIRST-SALE-DATE    TO FS126-FIRST-SALE-DATE.              
           MOVE FS123-LAST-RCVD-DATE     TO FS126-LAST-RCVD-DATE.               
           MOVE FS123-FIRST-RCVD-DATE    TO FS126-FIRST-RCVD-DATE.              
                                                                                
           MOVE 'Y' TO FS126-EBUS-LOC-IND.                                      
           PERFORM 4030-INIT-WKS-OO-FIELDS.                                     
                                                                                
           MOVE FS123-ONORD-QTY          TO FS126-ON-ORD-QTY.                   
           SET PS-STORE-IS-ECOM-LOC TO TRUE.                                    
                                                                                
           MOVE FS123-LOC-NBR TO SKSTWKS-STR-NBR                                
                                                                                
KP0206     IF  FS123-FOUND-ON-TSKST-IND = 'Y'                                   
KP0206     AND TSK-FS-RPT-IND = 'Y'                                             
               PERFORM 4600-PROCESS-WEEKLY-SALES                                
           END-IF.                                                              
           SET PS-STORE-IS-NOT-ECOM-LOC TO TRUE.                                
                                                                                
           PERFORM 7620-FORMAT-ECOM-ONORD-DATA.                                 
           IF  FS123-ONHAND-QTY > 0                                             
               MOVE +1 TO FS126-NBR-STORES                                      
           ELSE                                                                 
               MOVE  0 TO FS126-NBR-STORES                                      
           END-IF.                                                              
                                                                                
           PERFORM 6100-WRITE-ECOM-EXTRACT-REC.                                 
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS THE MINIMAL PROCESSING REQUIRED FOR             
      * STORES THAT ARE NOT ON THE TSKST UNLOAD FILE, BUT HAVE THE              
      * ITEM ON ORDER.                                                          
      ******************************************************************        
       4028-PROCESS-NON-RCP-OO-STR.                                             
                                                                                
      **** DO NOT WRITE AN E-COMMERCE EXTRACT REC FOR CLEARANCE ITEMS           
           IF  FS125-STATUS-CODE = '30'                                         
               NEXT SENTENCE                                                    
           ELSE                                                                 
               IF  ECOM-LOC-IND (STORE-INDEX) = 'Y'                             
                   PERFORM 4029-FORMAT-OO-NON-RCP-ECOM                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH FORMATS THE E-COMMERCE RECORD FOR A STORE THAT           
      * IS NOT ON THE TSKST UNLOAD FILE, BUT HAS THE ITEM ON ORDER.             
      * THESE RECORDS WILL NOT INCLUDE SALES OR ON-HAND DATA.                   
      ******************************************************************        
       4029-FORMAT-OO-NON-RCP-ECOM.                                             
                                                                                
      **** COPY THE SKU LEVEL DATA FROM THE REG/CLR RECORD FORMAT               
      **** TO THE E-COMMERCE RECORD FORMAT                                      
           MOVE FS125-SKU-EXTRACT-RECORD                                        
             TO FS126-ECOM-SKU-EXTRACT-RECORD.                                  
                                                                                
           PERFORM 4030-INIT-WKS-OO-FIELDS                                      
           PERFORM 7620-FORMAT-ECOM-ONORD-DATA                                  
      *    MOVE FS123-ONORD-QTY      TO FS126-ON-ORD-QTY                        
           IF FS126-ON-ORD-QTY > 0                                              
               MOVE PV-000-ITEM-NUMBER    TO FS126-ITEM-NMBR                    
               MOVE LOC-NBR (STORE-INDEX) TO FS126-LOC-NBR                      
               MOVE ZERO TO FS126-CUR-ONHAND-QTY                                
                            FS126-YTD-SALES-UNIT-QTY                            
                            FS126-NBR-STORES                                    
      ********     ZERO OUT THE DATE FIELDS - DO NOT HAVE                       
      ********     STORE-SPECIFIC DATA                                          
               MOVE ZERO TO FS126-LAST-SALE-DATE                                
                            FS126-FIRST-SALE-DATE                               
                            FS126-LAST-RCVD-DATE                                
                            FS126-FIRST-RCVD-DATE                               
               MOVE 'Y'  TO FS126-EBUS-LOC-IND                                  
               PERFORM 6100-WRITE-ECOM-EXTRACT-REC                              
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * INITIALIZE THE WEEKLY SALES AND ON-ORDER FIELDS ON THE                  
      * E-COMMERCE EXTRACT RECORD BEFORE SELECTING THIS DATA FOR THE            
      * E-COMMERCE STORE.                                                       
      ******************************************************************        
       4030-INIT-WKS-OO-FIELDS.                                                 
                                                                                
           MOVE ZEROES TO FS126-CUR-SALES-UNIT-QTY                              
                          FS126-WK1-SALES-UNIT-QTY                              
                          FS126-WK2-SALES-UNIT-QTY                              
                          FS126-WK3-SALES-UNIT-QTY                              
                          FS126-WK4-SALES-UNIT-QTY                              
                          FS126-WK5-SALES-UNIT-QTY                              
                          FS126-ON-ORD-QTY.                                     
           MOVE '00'                  TO FS126-ORD-BGHT-YY                      
           MOVE '00'                  TO FS126-ORD-BGHT-MM.                     
                                                                                
                                                                                
      ******************************************************************        
      * SEARCH THE MERCHANDISE LEVEL TABLE FOR SKU ATTRIBUTES                   
      ******************************************************************        
       4100-SEARCH-LEVEL-LOOKUP-TBL.                                            
                                                                                
           SET LEVEL-INDEX TO 1.                                                
           SEARCH ALL LEVEL-ENTRY                                               
             AT END                                                             
               DISPLAY 'NO ENTRY FOUND IN LEVEL-TABLE FOR DEPT '                
                        NI-DEPT-X-3                                             
             WHEN DPT-NBR(LEVEL-INDEX) = NI-DEPT-X-3                            
                MOVE DPT-NBR (LEVEL-INDEX) TO PV-PREV-DPT-NBR                   
                MOVE GMA-NBR (LEVEL-INDEX) TO FS125-MERCH-DIV-CODE              
                                              PV-PREV-GMA-NBR                   
                MOVE DMA-NBR (LEVEL-INDEX) TO FS125-DMM-NMBR                    
                                              PV-PREV-DMA-NBR                   
                MOVE BYR-NBR (LEVEL-INDEX) TO FS125-BUYER-NMBR                  
                                              PV-PREV-BYR-NBR                   
           END-SEARCH.                                                          
                                                                                
                                                                                
      ******************************************************************        
      * SEARCH THE SIZE TABLE FOR SKU ATTRIBUTES                                
      ******************************************************************        
       4200-SEARCH-SIZE-LOOKUP-TBL.                                             
                                                                                
           SET SIZE-INDEX TO 1.                                                 
           SEARCH ALL SIZE-ENTRY                                                
             AT END                                                             
               DISPLAY 'NO ENTRY FOUND IN SIZE-TABLE FOR NRF SIZE CDE '         
                        FS123-KOHLS-SIZE-CDE                                    
             WHEN KOHLS-SIZE-CDE(SIZE-INDEX) = FS123-KOHLS-SIZE-CDE             
               MOVE KOHLS-DESC  (SIZE-INDEX) TO FS125-SIZE-DESC                 
           END-SEARCH.                                                          
                                                                                
                                                                                
      ******************************************************************        
      *  FORMAT THE DATA ON THE EXTRACT RECORD.                                 
      ******************************************************************        
       4500-FORMAT-INPUT-DATA.                                                  
                                                                                
           MOVE FS123-UPC-STAT-CDE      TO FS125-STATUS-CODE.                   
           MOVE FS123-UNT-COST-AMT      TO FS125-UNIT-COST-AMT.                 
           MOVE FS123-UNT-RTL-AMT       TO FS125-CUR-UNIT-RTL-AMT.              
      **** CHANGE THE DATE FORMAT FROM (YYYY-MM-DD) TO (0YYMMDD)                
           MOVE FS123-STAT-CHG-DTE       TO SA-STAT-DATE-FULL.                  
           MOVE SA-STAT-DATE-YY          TO SA-STAT1-DATE-YY.                   
           MOVE SA-STAT-DATE-MM          TO SA-STAT1-DATE-MM.                   
           MOVE SA-STAT-DATE-DD          TO SA-STAT1-DATE-DD.                   
           MOVE SA-STAT-DATE             TO FS125-STAT-CHNGD-DATE.              
                                                                                
                                                                                
      ******************************************************************        
      *  SAVE THE STORE ZERO DATA FOR RCP PROCESSING.                           
      ******************************************************************        
       4510-SAVE-STORE-000-DATA.                                                
                                                                                
           MOVE FS123-UNT-COST-AMT      TO PV-000-UNIT-COST-AMT.                
           MOVE FS123-UNT-RTL-AMT       TO PV-000-CUR-UNIT-RTL-AMT.             
      **** CHANGE THE DATE FORMAT FROM (YYYY-MM-DD) TO (0YYMMDD)                
           MOVE FS123-STAT-CHG-DTE      TO SA-STAT-DATE-FULL.                   
           MOVE SA-STAT-DATE-YY         TO SA-STAT1-DATE-YY.                    
           MOVE SA-STAT-DATE-MM         TO SA-STAT1-DATE-MM.                    
           MOVE SA-STAT-DATE-DD         TO SA-STAT1-DATE-DD.                    
           MOVE SA-STAT-DATE            TO PV-000-STAT-CHNGD-DATE.              
           MOVE FS123-INTR-UPC-ID       TO PV-000-UPC-ID.                       
                                                                                
      ******************************************************************        
      *  FORMAT THE STORE ZERO DATA ON THE REG EXTRACT RECORD.                  
      ******************************************************************        
       4520-FORMAT-INPUT-000-DATA.                                              
                                                                                
           MOVE PV-000-UNIT-COST-AMT     TO FS125-UNIT-COST-AMT.                
           MOVE PV-000-CUR-UNIT-RTL-AMT  TO FS125-CUR-UNIT-RTL-AMT.             
           MOVE PV-000-STAT-CHNGD-DATE   TO FS125-STAT-CHNGD-DATE.              
                                                                                
                                                                                
      ******************************************************************        
      *  GET SALES FOR CURRENT WEEK AND PREVIOUS 5 WEEKS                        
      ******************************************************************        
       4600-PROCESS-WEEKLY-SALES.                                               
                                                                                
           MOVE FS123-ITEM-NMBR TO SKSTWKS-ITEM-NMBR.                           
           EXEC SQL                                                             
               OPEN WEEKLY_SALES_CSR                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'OPEN WEEKLY SALES CURSOR'                              
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
           MOVE SQLCODE TO FS-TSKSTWKS-CODE.                                    
           PERFORM 4601-FETCH-WEEKLY-SALES                                      
              UNTIL FS-TSKSTWKS-NOT-FOUND.                                      
                                                                                
           EXEC SQL                                                             
               CLOSE WEEKLY_SALES_CSR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'CLOSE WEEKLY SALES CURSOR'                             
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * FETCH THE WEEKLY SALES FROM THE TSKSTWKS TABLE FOR THE         *        
      * WEEK JUST ENDED PLUS THE 5 PREVIOUS WEEKS.                     *        
      ******************************************************************        
       4601-FETCH-WEEKLY-SALES.                                                 
                                                                                
           MOVE '*4601-FETCH-WEEKLY-SALES     *' TO PV-PARAGRAPH-NAME.          
                                                                                
           PERFORM VARYING PV-RETRY-CNT FROM 1 BY 1                             
             UNTIL PV-RETRY-CNT > PC-MAX-RETRY-CNT                              
                                                                                
             MOVE ZEROS  TO SKSTWKS-SALES-UNIT-QTY                              
             MOVE SPACES TO SKSTWKS-FSCL-BOW-DTE                                
                                                                                
             EXEC SQL                                                           
                  FETCH WEEKLY_SALES_CSR                                        
                   INTO   :SKSTWKS-SALES-UNIT-QTY                               
                         ,:SKSTWKS-FSCL-BOW-DTE                                 
             END-EXEC                                                           
                                                                                
             MOVE SQLCODE TO FS-TSKSTWKS-CODE                                   
                                                                                
             EVALUATE TRUE                                                      
                WHEN SQLCODE = +0                                               
                    ADD +1 TO PC-TSKSTWKS-FETCHS                                
                    MOVE PC-MORE-THAN-MAX-RETRY TO PV-RETRY-CNT                 
                    IF  PS-STORE-IS-ECOM-LOC                                    
                        PERFORM 4603-FORMAT-ECOM-TSKSTWKS-DATA                  
                    ELSE                                                        
                        PERFORM 4602-FORMAT-TSKSTWKS-DATA                       
                    END-IF                                                      
                WHEN FS-TSKSTWKS-NOT-FOUND                                      
                   MOVE PC-MORE-THAN-MAX-RETRY TO PV-RETRY-CNT                  
                WHEN SQLCODE = -911                                             
                WHEN SQLCODE = -913                                             
                   CONTINUE                                                     
                WHEN OTHER                                                      
                   PERFORM 9999-SQL-ERROR                                       
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  (SQLWARN0 NOT = SPACES                                           
           OR   (SQLCODE > 0 AND SQLCODE NOT = +100)                            
           OR    SQLCODE < 0)                                                   
               PERFORM 9999-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  FORMAT THE DATA RETRIEVED FROM TSKSTWKS ON THE REG/CLR EXTRACT         
      *  RECORD                                                                 
      ******************************************************************        
       4602-FORMAT-TSKSTWKS-DATA.                                               
                                                                                
           IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-CURR                       
              MOVE SKSTWKS-SALES-UNIT-QTY                                       
                TO FS125-CUR-SALES-UNIT-QTY                                     
           ELSE                                                                 
              IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-WK1                     
                 MOVE SKSTWKS-SALES-UNIT-QTY                                    
                   TO FS125-WK1-SALES-UNIT-QTY                                  
              ELSE                                                              
                 IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-WK2                  
                    MOVE SKSTWKS-SALES-UNIT-QTY                                 
                      TO FS125-WK2-SALES-UNIT-QTY                               
                 ELSE                                                           
                    IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-WK3               
                       MOVE SKSTWKS-SALES-UNIT-QTY                              
                         TO FS125-WK3-SALES-UNIT-QTY                            
                    ELSE                                                        
                       IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-WK4            
                          MOVE SKSTWKS-SALES-UNIT-QTY                           
                            TO FS125-WK4-SALES-UNIT-QTY                         
                       ELSE                                                     
                          IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-WK5         
                             MOVE SKSTWKS-SALES-UNIT-QTY                        
                               TO FS125-WK5-SALES-UNIT-QTY                      
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  FORMAT THE DATA RETRIEVED FROM TSKSTWKS ON THE E-COMMERCE              
      *  EXTRACT RECORD                                                         
      ******************************************************************        
       4603-FORMAT-ECOM-TSKSTWKS-DATA.                                          
                                                                                
           IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-CURR                       
              MOVE SKSTWKS-SALES-UNIT-QTY                                       
                TO FS126-CUR-SALES-UNIT-QTY                                     
           ELSE                                                                 
              IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-WK1                     
                 MOVE SKSTWKS-SALES-UNIT-QTY                                    
                   TO FS126-WK1-SALES-UNIT-QTY                                  
              ELSE                                                              
                 IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-WK2                  
                    MOVE SKSTWKS-SALES-UNIT-QTY                                 
                      TO FS126-WK2-SALES-UNIT-QTY                               
                 ELSE                                                           
                    IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-WK3               
                       MOVE SKSTWKS-SALES-UNIT-QTY                              
                         TO FS126-WK3-SALES-UNIT-QTY                            
                    ELSE                                                        
                       IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-WK4            
                          MOVE SKSTWKS-SALES-UNIT-QTY                           
                            TO FS126-WK4-SALES-UNIT-QTY                         
                       ELSE                                                     
                          IF SKSTWKS-FSCL-BOW-DTE = SA-FSCL-BOW-DTE-WK5         
                             MOVE SKSTWKS-SALES-UNIT-QTY                        
                               TO FS126-WK5-SALES-UNIT-QTY                      
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE REGULAR OR CLEARANCE SKU EXTRACT RECORD                      
      ******************************************************************        
       6000-WRITE-EXTRACT-REC.                                                  
                                                                                
           IF  FS125-STATUS-CODE = '30'                                         
               PERFORM 6010-WRITE-CLR-EXTRACT-REC                               
           ELSE                                                                 
               PERFORM 6020-WRITE-REG-EXTRACT-REC                               
           END-IF.                                                              
                                                                                
                                                                                
       6010-WRITE-CLR-EXTRACT-REC.                                              
                                                                                
           ADD +1 TO PC-CLR-EXTRACT-REC-CNT.                                    
           WRITE WEEKLY-EXTRACT-CLR-RECORD                                      
             FROM FS125-SKU-EXTRACT-RECORD.                                     
                                                                                
                                                                                
       6020-WRITE-REG-EXTRACT-REC.                                              
                                                                                
           ADD +1 TO PC-REG-EXTRACT-REC-CNT.                                    
           WRITE WEEKLY-EXTRACT-REG-RECORD                                      
             FROM FS125-SKU-EXTRACT-RECORD.                                     
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE E-COMMERCE SKU EXTRACT RECORD                                
      ******************************************************************        
       6100-WRITE-ECOM-EXTRACT-REC.                                             
                                                                                
           ADD +1 TO PC-ECOM-EXTRACT-REC-CNT.                                   
           WRITE WEEKLY-EXTRACT-ECOM-RECORD                                     
             FROM FS126-ECOM-SKU-EXTRACT-RECORD.                                
                                                                                
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE ITEM-LOC FILE                               
      ******************************************************************        
       7000-READ-ITEM-LOC-INPUT-FILE.                                           
                                                                                
           READ ITEM-LOC-EXTRACT-FILE                                           
               INTO FS123-FS-MERGED-RECORD                                      
               AT END                                                           
                   SET PS-ITEM-LOC-EOF TO TRUE                                  
               NOT AT END                                                       
                   ADD +1 TO PC-INPUT-CNT                                       
                   IF FS123-LOC-NBR = ZERO                                      
                       ADD +1 TO PC-STORE-ZERO-INPUT-CNT                        
                       SET PS-INPUT-REC-IS-STR-ZERO TO TRUE                     
                   END-IF.                                                      
                                                                                
      ******************************************************************        
      * SELECT DATA FOR THE ITEM ON THE STORE ZERO RECORD                       
      ******************************************************************        
       7100-SELECT-XREF-TSK.                                                    
                                                                                
           MOVE FS123-ITEM-NMBR TO SKXREF-ITM-NBR.                              
                                                                                
           EXEC SQL                                                             
             SELECT A.DEPT                                                      
                  , A.CLASS                                                     
                  , A.SUBCLASS                                                  
                  , A.SKU                                                       
                  , B.FS_RPT_IND                                                
               INTO  :SKXREF-DEPT                                               
                  ,  :SKXREF-CLASS                                              
                  ,  :SKXREF-SUBCLASS                                           
                  ,  :SKXREF-SKU                                                
                  ,  :TSK-FS-RPT-IND                                            
               FROM  TSKXREF A                                                  
                  ,  TSK B                                                      
              WHERE   A.ITM_NBR       = :SKXREF-ITM-NBR                         
                AND   A.ITM_NBR       = B.ITEM_NMBR                             
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   SET PS-XREF-TSK-FOUND TO TRUE                                
                   ADD +1 TO PC-STORE-ZERO-FOUND-CNT                            
                   IF  FS123-REGN-PRIC-IND = 'Y'                                
                       MOVE FS123-UPC-STAT-CDE TO PV-ITEM-RCP-SW                
                   ELSE                                                         
                       MOVE SPACES TO PV-ITEM-RCP-SW                            
                   END-IF                                                       
               WHEN +100                                                        
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7100-SELECT-XREF-TSK'                                  
                                        TO PV-PARAGRAPH-NAME                    
                   MOVE 'SELECT STORE ZERO'                                     
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                   DISPLAY SPACES                                               
                   DISPLAY 'ITEM NUMBER = ' FS123-ITEM-NMBR                     
                   DISPLAY SPACES                                               
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * SELECT VENDOR STYLE INFO FROM TMDARVS & TVNDSTL                         
      ******************************************************************        
       7200-SELECT-VENDOR-STYLE-INFO.                                           
                                                                                
           EXEC SQL                                                             
             SELECT A.STKR_NBR                                                  
                  , A.LBL_SEQ_NBR                                               
                  , A.HNG_FOLD_SEQ_NBR                                          
                  , B.VS_NBR                                                    
                  , C.ENT_NAME_DESC                                             
               INTO  :MDARVS-STKR-NBR                                           
                  ,  :MDARVS-LBL-SEQ-NBR                                        
                  ,  :MDARVS-HNG-FOLD-SEQ-NBR                                   
                  ,  :VNDSTL-VS-NBR                                             
                  ,  :ENTNME-ENT-NAME-DESC                                      
               FROM  TMDARVS A                                                  
                  ,  TVNDSTL B                                                  
                  ,  TENTNME C                                                  
              WHERE   A.SUB_CL_MDSEAR_ID = :FS123-SUB-CL-MDSEAR-ID              
                AND   A.VS_ID            = :FS123-VS-ID                         
                AND   A.VS_ID            = B.VS_ID                              
                AND   B.ENT_ID           = C.ENT_ID                             
                AND ((B.EFF_BEG_DTE     <= :CD-ISO-FMT-DATE)                    
                AND ((B.EFF_END_DTE     >= :CD-ISO-FMT-DATE)                    
                 OR  (B.EFF_END_DTE     IS NULL)))                              
                AND   C.TYPE_CDE         = '01'                                 
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   ADD +1 TO PC-SELECT-TMDARVS-CNT                              
                   MOVE MDARVS-STKR-NBR     TO FS125-STICKER-NBR                
                   MOVE MDARVS-LBL-SEQ-NBR  TO FS125-LBL-SEQ-NBR                
                   MOVE MDARVS-HNG-FOLD-SEQ-NBR                                 
                                            TO FS125-HNG-FLD-SEQ-NBR            
                   MOVE VNDSTL-VS-NBR                                           
                                            TO FS125-LONG-VENDOR-STYLE          
                   MOVE ENTNME-ENT-NAME-DESC                                    
                                            TO FS125-IN-HSE-VNDR-NAME           
               WHEN +100                                                        
                   DISPLAY '*** VNDR STYLE DATA NOT FOUND FOR ITEM NBR '        
                           FS123-ITEM-NMBR ' DEPT '                             
                           FS125-MERCH-DEPT-NMBR ' ***'                         
               WHEN OTHER                                                       
                   MOVE '7200-SELECT-VENDOR-STYLE-INFO'                         
                                        TO PV-PARAGRAPH-NAME                    
                   MOVE 'SELECT TMDARVS/TVNDSTL/TENTNME'                        
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                   DISPLAY SPACES                                               
                   DISPLAY 'SUB_CL_MDSEAR_ID = ' FS123-SUB-CL-MDSEAR-ID         
                   DISPLAY 'VS_ID            = ' FS123-VS-ID                    
                   DISPLAY SPACES                                               
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * SELECT GROUP ITEM DATA FROM TMITGPL                                     
      ******************************************************************        
       7300-SELECT-TMITGPL.                                                     
                                                                                
           EXEC SQL                                                             
             SELECT  MITGPL_QTY                                                 
                  ,  GP_AMT                                                     
               INTO  :MITGPL-MITGPL-QTY                                         
                  ,  :MITGPL-GP-AMT                                             
               FROM  TMITGPL                                                    
              WHERE  MEITGP_NBR   = :FS123-MEITGP-NBR                           
                AND  OPERCO_NBR   = '03'                                        
                AND ((BEG_DTE     <= :CD-ISO-FMT-DATE)                          
                AND ((END_DTE     >= :CD-ISO-FMT-DATE)                          
                 OR  (END_DTE     IS NULL)))                                    
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   ADD +1 TO PC-SELECT-TMITGPL-CNT                              
                   MOVE MITGPL-MITGPL-QTY  TO FS125-GP-QTY                      
                   MOVE MITGPL-GP-AMT      TO FS125-GP-RTL-AMT                  
               WHEN +100                                                        
                   DISPLAY '*** GRP ITEM DATA NOT FOUND FOR ITEM NBR '          
                           FS123-ITEM-NMBR ' DEPT '                             
                           FS125-MERCH-DEPT-NMBR ' ***'                         
               WHEN OTHER                                                       
                   MOVE '7300-SELECT-TMITGPL'                                   
                                        TO PV-PARAGRAPH-NAME                    
                   MOVE 'SELECT TMITGPL'                                        
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                   DISPLAY SPACES                                               
                   DISPLAY 'MEITGP_NBR = ' FS123-MEITGP-NBR                     
                   DISPLAY SPACES                                               
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * SELECT WEB ITEM DATA FROM TWEBITM.                                      
      ******************************************************************        
       7400-SELECT-TWEBITM.                                                     
                                                                                
           EXEC SQL                                                             
             SELECT  ASRT_BEG_DTE                                               
                  ,  ASRT_END_DTE                                               
                  ,  PRD_GP_NBR                                                 
                  ,  PRD_GP_IND                                                 
               INTO  :WEBITM-ASRT-BEG-DTE                                       
                  ,  :WEBITM-ASRT-END-DTE                                       
                  ,  :WEBITM-PRD-GP-NBR                                         
                  ,  :WEBITM-PRD-GP-IND                                         
               FROM  TWEBITM                                                    
              WHERE  INTR_UPC_ID    = :FS123-INTR-UPC-ID                        
                AND ((EFF_BEG_TMST <= :CD-TIMESTAMP-FMT-DATE)                   
                AND ((EFF_END_TMST >= :CD-TIMESTAMP-FMT-DATE)                   
                 OR  (EFF_END_TMST IS NULL)))                                   
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   ADD +1 TO PC-SELECT-TWEBITM-CNT                              
                   MOVE WEBITM-ASRT-BEG-DTE TO FS125-ASRT-BEG-DTE               
                   MOVE WEBITM-ASRT-END-DTE TO FS125-ASRT-END-DTE               
                   MOVE WEBITM-PRD-GP-NBR   TO PV-PRD-GP-NBR-9                  
                   MOVE PV-PRD-GP-NBR-X     TO FS125-PRD-GP-NBR                 
                   MOVE WEBITM-PRD-GP-IND   TO FS125-PRD-GP-IND                 
               WHEN +100                                                        
                   MOVE SPACES              TO FS125-ASRT-BEG-DTE               
                                               FS125-ASRT-END-DTE               
                                               FS125-PRD-GP-NBR                 
                   MOVE 'N'                 TO FS125-PRD-GP-IND                 
               WHEN OTHER                                                       
                   MOVE '7400-SELECT-TWEBITM'                                   
                                        TO PV-PARAGRAPH-NAME                    
                   MOVE 'SELECT TWEBITM'                                        
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                   DISPLAY SPACES                                               
                   DISPLAY 'UPC-INTR-UPC-ID = ' FS123-INTR-UPC-ID               
                   DISPLAY SPACES                                               
                   PERFORM 9999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  FORMAT THE DATA RETRIEVED FROM TSKSTOO ON THE REG/CLR EXTRACT          
      *  RECORD                                                                 
      ******************************************************************        
       7610-FORMAT-ONORD-DATA.                                                  
                                                                                
           MOVE FS123-ONORD-QTY      TO FS125-ON-ORD-QTY                        
           IF FS123-MIN-OTB-PER-NBR = SPACES                                    
               MOVE '00'                  TO FS125-ORD-BGHT-YY                  
               MOVE '00'                  TO FS125-ORD-BGHT-MM                  
           ELSE                                                                 
               MOVE FS123-MIN-OTB-PER-NBR TO SA-BGHT-DATE-FULL                  
               MOVE SA-BGHT-DATE-YY       TO FS125-ORD-BGHT-YY                  
               MOVE SA-BGHT-DATE-MM       TO FS125-ORD-BGHT-MM                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  FORMAT THE DATA RETRIEVED FROM TSKSTOO ON THE E-COMMERCE               
      *  EXTRACT RECORD                                                         
      ******************************************************************        
       7620-FORMAT-ECOM-ONORD-DATA.                                             
                                                                                
           IF FS123-MIN-OTB-PER-NBR = SPACES                                    
               MOVE '00'                  TO FS126-ORD-BGHT-YY                  
               MOVE '00'                  TO FS126-ORD-BGHT-MM                  
           ELSE                                                                 
               MOVE FS123-MIN-OTB-PER-NBR TO SA-BGHT-DATE-FULL                  
               MOVE SA-BGHT-DATE-YY       TO FS126-ORD-BGHT-YY                  
               MOVE SA-BGHT-DATE-MM       TO FS126-ORD-BGHT-MM                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 9000-END-OF-JOB   DISPLAY RECORD COUNTS AND CLOSE FILES        *        
      ******************************************************************        
       9000-END-OF-JOB.                                                         
                                                                                
           MOVE LEVEL-TABLE-SIZE        TO PV-LEVEL-TABLE-SIZE-DISP.            
           MOVE SIZE-TABLE-SIZE         TO PV-SIZE-TABLE-SIZE-DISP.             
           MOVE STORE-TABLE-SIZE        TO PV-STORE-TABLE-SIZE-DISP.            
           MOVE PC-INPUT-CNT            TO PV-INPUT-CNT-DISP.                   
           MOVE PC-STORE-ZERO-INPUT-CNT TO PV-STR-ZERO-INP-CNT-DISP.            
           MOVE PC-STORE-ZERO-FOUND-CNT TO PV-STR-ZERO-FND-CNT-DISP.            
           MOVE PC-SELECT-TMDARVS-CNT   TO PV-SELECT-TMDARVS-CNT-DISP.          
           MOVE PC-SELECT-TMITGPL-CNT   TO PV-SELECT-TMITGPL-CNT-DISP.          
           MOVE PC-SELECT-TWEBITM-CNT   TO PV-SELECT-TWEBITM-CNT-DISP.          
           MOVE PC-TSKSTWKS-FETCHS      TO PV-TSKSTWKS-FETCHS-DISP.             
           MOVE PC-REG-EXTRACT-REC-CNT  TO PV-REG-EXTRACT-REC-CNT-DISP.         
           MOVE PC-CLR-EXTRACT-REC-CNT  TO PV-CLR-EXTRACT-REC-CNT-DISP.         
           MOVE PC-ECOM-EXTRACT-REC-CNT TO PV-ECOM-EXTRACT-REC-CNT-DISP.        
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'TOTAL LEVEL TBL RECS      =      '                          
                                          PV-LEVEL-TABLE-SIZE-DISP.             
           DISPLAY 'TOTAL SIZE TBL RECS       =      '                          
                                          PV-SIZE-TABLE-SIZE-DISP.              
           DISPLAY 'TOTAL STORE TBL RECS      =      '                          
                                          PV-STORE-TABLE-SIZE-DISP.             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'TSKST INPUT RECORDS READ  = ' PV-INPUT-CNT-DISP.            
           DISPLAY 'STORE ZERO RECORDS READ   = '                               
                                          PV-STR-ZERO-INP-CNT-DISP.             
           DISPLAY 'STORE ZERO RECORDS FOUND  = '                               
                                          PV-STR-ZERO-FND-CNT-DISP.             
           DISPLAY 'SELECTS ON TMDARVS        = '                               
                                          PV-SELECT-TMDARVS-CNT-DISP.           
           DISPLAY 'SELECTS ON TMITGPL        = '                               
                                          PV-SELECT-TMITGPL-CNT-DISP.           
           DISPLAY 'SELECTS ON TWEBITM        = '                               
                                          PV-SELECT-TWEBITM-CNT-DISP.           
           DISPLAY 'FETCHES ON TSKSTWKS       = '                               
                                          PV-TSKSTWKS-FETCHS-DISP.              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'REG EXTRACT RECS WRITTEN  = '                               
                                          PV-REG-EXTRACT-REC-CNT-DISP.          
           DISPLAY 'CLR EXTRACT RECS WRITTEN  = '                               
                                          PV-CLR-EXTRACT-REC-CNT-DISP.          
           DISPLAY 'ECOM EXTRACT RECS WRITTEN = '                               
                                          PV-ECOM-EXTRACT-REC-CNT-DISP.         
                                                                                
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE CONTROL-CARD-FILE                                              
                 ITEM-LOC-EXTRACT-FILE                                          
                 WEEKLY-SKU-EXTRACT-REG-FILE                                    
                 WEEKLY-SKU-EXTRACT-CLR-FILE                                    
                 WEEKLY-SKU-EXTRACT-ECOM-FILE.                                  
                                                                                
      ******************************************************************        
      *   USER ABEND                                                            
      ******************************************************************        
       9950-USER-ABEND.                                                         
                                                                                
           MOVE PC-PROGRAM-NAME   TO PD-DISP1-PROGRAM.                          
           MOVE PV-PARAGRAPH-NAME TO PD-DISP1-PARAGRAPH.                        
                                                                                
           DISPLAY PD-DISP1-ABEND-LINE1.                                        
           DISPLAY PD-DISP1-ABEND-LINE1.                                        
           DISPLAY PD-DISP1-ABEND-LINE2.                                        
           DISPLAY PD-DISP1-ABEND-LINE3.                                        
           DISPLAY PD-DISP1-ABEND-LINE4.                                        
           DISPLAY PD-DISP1-ABEND-LINE5.                                        
           DISPLAY PD-DISP1-ABEND-LINE1.                                        
           DISPLAY PD-DISP1-ABEND-LINE1.                                        
                                                                                
           MOVE +4003 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *****************************************************************         
      * 9990-CALENDAR ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN             
      * THE EVENT THAT AN ERROR OCCURS WHEN CALLING THE DATE ROUTINE.           
      *****************************************************************         
       9990-CALENDAR-ABEND.                                                     
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY '** PGM = ' PC-PROGRAM-NAME.                                 
           DISPLAY '** PARAGRAPH = ' PV-PARAGRAPH-NAME.                         
           DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'.                   
           DISPLAY '** USING CONTROL CARD DATE AS INPUT.'.                      
           DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE.                   
           DISPLAY '** DPG51 = ' DPG51.                                         
           DISPLAY '** DPG52 = ' DPG52.                                         
           DISPLAY '** DPG53 = ' DPG53.                                         
           DISPLAY '** DPG54 = ' DPG54.                                         
           MOVE +4003 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *****************************************************************         
      * 9999-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      *****************************************************************         
       9999-SQL-ERROR.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
      *****************************************************************         
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      *****************************************************************         
           COPY DPPD004.                                                        
                                                                                
           EXEC SQL                                                             
             COMMIT                                                             
           END-EXEC.                                                            
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABNO' USING ABEND-CODE.                                    
