00001 *-----------------------*                                         02/25/93
00002  IDENTIFICATION DIVISION.                                         MBKUT009
00003 *-----------------------*                                            LV004
00004  PROGRAM-ID.    MBKUT009.                                            CL**3
00005  AUTHOR.        MIKE FURLICK.                                        CL**3
00006  DATE-WRITTEN.  FEBRUARY, 1993.                                      CL**3
00007                                                                      CL**3
00008 *---------------------------------------------------------------*    CL**3
00009 *                                                               *    CL**3
00010 *    THIS PROGRAM PROCESSES THE FLAT FILE PRODUCED IN MBKUT005. *    CL**3
00011 *                                                               *    CL**3
00012 *    THE FLAT FILE IS IN THE FORMAT OF THE APLAVL ROW.  FOR     *    CL**3
00013 *    EACH RECORD READ, A ROW IS INSERTED INTO TAPLAVL.          *    CL**3
00014 *                                                               *    CL**3
00015 *---------------------------------------------------------------*    CL**3
00016                                                                      CL**3
00017  ENVIRONMENT DIVISION.                                               CL**3
00018  CONFIGURATION SECTION.                                              CL**3
00019  SOURCE-COMPUTER. IBM-3090.                                          CL**3
00020  OBJECT-COMPUTER. IBM-3090.                                          CL**3
00021                                                                      CL**3
00022  INPUT-OUTPUT SECTION.                                               CL**3
00023  FILE-CONTROL.                                                       CL**3
00024                                                                      CL**3
00025      SELECT APLAVL-LOAD-FILE                                         CL**3
00026          ASSIGN TO INP01.                                            CL**3
00027                                                                      CL**3
00028  DATA DIVISION.                                                      CL**3
00029  FILE SECTION.                                                       CL**3
00030                                                                      CL**3
00031  FD  APLAVL-LOAD-FILE                                                CL**3
00032      RECORDING MODE IS F                                             CL**3
00033      BLOCK CONTAINS 0 CHARACTERS                                     CL**3
00034      LABEL RECORDS ARE STANDARD.                                     CL**3
00035                                                                      CL**3
00036  01  APLAVL-LOAD-RECORD            PIC X(28).                        CL**3
00037  EJECT                                                               CL**3
00038  WORKING-STORAGE SECTION.                                            CL**3
00039                                                                      CL**3
00040  01  PC-PROGRAM-CONSTANTS.                                           CL**3
00041      05  PC-MAX-RETRIES            PIC S9(3)  VALUE +3               CL**3
00042                                               COMP-3.                CL**3
00043                                                                      CL**3
00044  01  PS-PROGRAM-SWITCHES.                                            CL**3
00045      05  PS-DATA-SW                PIC X      VALUE 'N'.             CL**3
00046          88  PS-END-OF-DATA                   VALUE 'Y'.             CL**3
00047          88  PS-NOT-END-OF-DATA               VALUE 'N'.             CL**3
00048                                                                      CL**3
00049  01  PV-PROGRAM-VARIABLES.                                           CL**3
00050      05  PV-WRITE-COUNT            PIC S9(9)  VALUE ZERO             CL**3
00051                                               COMP-3.                CL**3
00052      05  PV-READ-COUNT             PIC S9(9)  VALUE ZERO             CL**3
00053                                               COMP-3.                CL**3
00054      05  PV-PARAGRAPH-NAME         PIC X(30)  VALUE SPACE.           CL**3
00055      05  PV-DB2-OPER-ATTEMPTED     PIC X(30)  VALUE SPACE.           CL**3
00056      05  PV-SQL-SUB                PIC S9(4)  VALUE ZERO             CL**3
00057                                               COMP.                  CL**3
00058                                                                      CL**3
00059  01  ABEND-CODE                    PIC S9(4)  VALUE ZERO             CL**3
00060                                               COMP.                  CL**3
00061      88  ABEND-4001                           VALUE 4001.            CL**3
00062      88  ABEND-4002                           VALUE 4002.            CL**3
00063      88  ABEND-4003                           VALUE 4003.            CL**3
00064      88  ABEND-4004                           VALUE 4004.            CL**3
00065      88  ABEND-4005                           VALUE 4005.            CL**3
00066      88  ABEND-4006                           VALUE 4006.            CL**3
00067      88  ABEND-4013                           VALUE 4013.            CL**3
00068      EJECT                                                           CL**3
00069 *                                                                    CL**3
00070 *    APLAVL DCLGEN                                                   CL**3
00071 *                                                                    CL**3
00072      EXEC SQL                                                        CL**3
00073          INCLUDE TAPLAVL                                             CL**3
00074      END-EXEC.                                                       CL**3
00075 *                                                                    CL**3
00076 *    SQL COMMUNICATIONS AREA DCLGEN                                  CL**3
00077 *                                                                    CL**3
00078      EXEC SQL                                                        CL**3
00079          INCLUDE SQLCA                                               CL**3
00080      END-EXEC.                                                       CL**3
00081 *                                                                    CL**3
00082 *    ERROR MESSAGE AREA FOR DB2                                      CL**3
00083 *                                                                    CL**3
00084      COPY DPWS004.                                                   CL**3
00085  EJECT                                                               CL**3
00086  PROCEDURE DIVISION.                                                 CL**3
00087                                                                      CL**3
00088      PERFORM 1000-MAINLINE.                                          CL**3
00089      GOBACK.                                                         CL**3
00090                                                                      CL**3
00091  1000-MAINLINE.                                                      CL**3
00092                                                                      CL**3
00093      PERFORM 2000-OPEN-INPUT.                                        CL**3
00094      PERFORM 3000-PROCESS-APLAVL                                     CL**3
00095              UNTIL PS-END-OF-DATA.                                   CL**3
00096      DISPLAY '* * * * * * * * * * * * * * * * * * * * * * *'.        CL**3
00097      PERFORM 4000-CLOSE-APLAVL.                                      CL**3
00098      PERFORM 9000-DISPLAY-TOTALS.                                    CL**3
00099                                                                      CL**3
00100  EJECT                                                               CL**3
00101 *---------------------------------------------------------------*    CL**3
00102 *                                                               *    CL**3
00103 *    THIS ROUTINE SIMPLY OPENS THE INPUT FILE.                  *    CL**3
00104 *                                                               *    CL**3
00105 *---------------------------------------------------------------*    CL**3
00106                                                                      CL**3
00107  2000-OPEN-INPUT.                                                    CL**3
00108                                                                      CL**3
00109      OPEN INPUT APLAVL-LOAD-FILE.                                    CL**3
00110      PERFORM 3100-READ-APLAVL.                                       CL**3
00111                                                                      CL**3
00112  EJECT                                                               CL**3
00113 *---------------------------------------------------------------*    CL**3
00114 *                                                              *     CL**3
00115 *    PROCESS THE APLAVL FLAT FILE.                             *     CL**3
00116 *                                                              *     CL**3
00117 *---------------------------------------------------------------*    CL**3
00118                                                                      CL**3
00119  3000-PROCESS-APLAVL.                                                CL**3
00120                                                                      CL**3
00121      ADD 1                 TO  PV-READ-COUNT.                        CL**3
00122      PERFORM 3200-INSERT-APLAVL.                                     CL**3
00123      IF  SQLCODE EQUAL ZERO                                          CL**4
00124          ADD 1             TO  PV-WRITE-COUNT                        CL**4
00125      END-IF.                                                         CL**4
00126      PERFORM 3100-READ-APLAVL.                                       CL**3
00127                                                                      CL**3
00128 *--------------------------------------------------------------*     CL**3
00129 *                                                              *     CL**3
00130 *    HERE WE READ THE BATCH CONTROL FILE.                      *     CL**3
00131 *                                                              *     CL**3
00132 *--------------------------------------------------------------*     CL**3
00133                                                                      CL**3
00134  3100-READ-APLAVL.                                                   CL**3
00135                                                                      CL**3
00136      READ APLAVL-LOAD-FILE                                           CL**3
00137           INTO DCLTAPLAVL                                            CL**3
00138           AT END SET PS-END-OF-DATA TO TRUE                          CL**3
00139      END-READ.                                                       CL**3
00140                                                                      CL**3
00141 *--------------------------------------------------------------*     CL**3
00142 *                                                              *     CL**3
00143 *    HERE WE INSERT THE APLAVL ROW.                            *     CL**3
00144 *                                                              *     CL**3
00145 *--------------------------------------------------------------*     CL**3
00146                                                                      CL**3
00147  3200-INSERT-APLAVL.                                                 CL**3
00148                                                                      CL**3
00149      PERFORM                                                         CL**3
00150          WITH TEST AFTER                                             CL**3
00151          VARYING PV-SQL-SUB                                          CL**3
00152          FROM 1 BY 1                                                 CL**3
00153          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**3
00154                    OR SQLCODE = 0                                    CL**3
00155                    OR SQLCODE = -803)                                CL**3
00156                                                                      CL**3
00157          EXEC SQL                                                    CL**3
00158               INSERT                                                 CL**3
00159               INTO TAPLAVL                                           CL**3
00160                      (APLSYS_CDE                                     CL**3
00161                    ,  MDSEAR_DKEY                                    CL**3
00162                    ,  MDSEAR_STRT_DTE                                CL**3
00163                    ,  MDSEAR_END_DTE                                 CL**3
00164                    ,  OPERCO_NBR)                                    CL**3
00165               VALUES                                                 CL**3
00166                     (:APLAVL-APLSYS-CDE                              CL**3
00167                    , :APLAVL-MDSEAR-DKEY                             CL**3
00168                    , :APLAVL-MDSEAR-STRT-DTE                         CL**3
00169                    , :APLAVL-MDSEAR-END-DTE                          CL**3
00170                    , :APLAVL-OPERCO-NBR)                             CL**3
00171          END-EXEC                                                    CL**3
00172                                                                      CL**3
00173          EVALUATE TRUE                                               CL**3
00174              WHEN SQLCODE = 0                                        CL**3
00175              WHEN SQLCODE = -803                                     CL**3
00176              WHEN SQLCODE = -904                                     CL**3
00177              WHEN SQLCODE = -911                                     CL**3
00178                  CONTINUE                                            CL**3
00179              WHEN SQLWARN NOT = SPACES                               CL**3
00180              WHEN SQLCODE NOT = ZERO                                 CL**3
00181                  MOVE '3100-INSERT-APLAVL'                           CL**3
00182                                TO  PV-PARAGRAPH-NAME                 CL**3
00183                  MOVE 'INSERT FOR APLAVL'                            CL**3
00184                                TO  PV-DB2-OPER-ATTEMPTED             CL**3
00185                  PERFORM 9900-SQL-ABEND                              CL**3
00186          END-EVALUATE                                                CL**3
00187      END-PERFORM.                                                    CL**3
00188                                                                      CL**3
00189      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**3
00190          MOVE '3200-INSERT-APLAVL'                                   CL**3
00191                                TO  PV-PARAGRAPH-NAME                 CL**3
00192          MOVE 'OPEN RETRIES EXCEEDED'                                CL**3
00193                                TO  PV-DB2-OPER-ATTEMPTED             CL**3
00194          PERFORM 9900-SQL-ABEND                                      CL**3
00195      END-IF.                                                         CL**3
00196                                                                      CL**3
00197 *--------------------------------------------------------------*     CL**3
00198 *                                                              *     CL**3
00199 *    CLOSE THE FLAT FILE.                                      *     CL**3
00200 *                                                              *     CL**3
00201 *--------------------------------------------------------------*     CL**3
00202                                                                      CL**3
00203  4000-CLOSE-APLAVL.                                                  CL**3
00204                                                                      CL**3
00205      CLOSE APLAVL-LOAD-FILE.                                         CL**3
00206                                                                      CL**3
00207 *--------------------------------------------------------------*     CL**3
00208 *                                                              *     CL**3
00209 *    DISPLAY THE NUMBER OF RECORDS READ/WRITTEN.               *     CL**3
00210 *                                                              *     CL**3
00211 *--------------------------------------------------------------*     CL**3
00212                                                                      CL**3
00213  9000-DISPLAY-TOTALS.                                                CL**3
00214                                                                      CL**3
00215      DISPLAY ' '.                                                    CL**3
00216      DISPLAY '* * * * * * * * * * * * * * * * * * *'.                CL**3
00217      DISPLAY '*                                   *'.                CL**3
00218      DISPLAY '*       **MBKUT009 COUNTS**         *'.                CL**3
00219      DISPLAY '*  '                                                   CL**3
00220              PV-READ-COUNT                                           CL**3
00221              ' INPUT RECORDS READ     *'.                            CL**3
00222      DISPLAY '*  '                                                   CL**3
00223              PV-WRITE-COUNT                                          CL**3
00224              ' APLSYS RECORDS CREATED *'.                            CL**3
00225      DISPLAY '*                                   *'.                CL**3
00226      DISPLAY '* * * * * * * * * * * * * * * * * * *'.                CL**3
00227 *                                                                    CL**3
00228  EJECT                                                               CL**3
00229 *--------------------------------------------------------------*     CL**3
00230 *                                                              *     CL**3
00231 *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *     CL**3
00232 *                                                              *     CL**3
00233 *--------------------------------------------------------------*     CL**3
00234                                                                      CL**3
00235  9900-SQL-ABEND.                                                     CL**3
00236                                                                      CL**3
00237      SET ABEND-4013             TO TRUE.                             CL**3
00238      DISPLAY '**  ABEND     **'.                                     CL**3
00239      DISPLAY '**  PROGRAM   **  =  MBKUT005'.                        CL**3
00240      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.               CL**3
00241      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.           CL**3
00242 *                                                                    CL**3
00243 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE            CL**3
00244 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.           CL**3
00245 *                                                                    CL**3
00246      COPY DPPD004.                                                   CL**3
00247                                                                      CL**3
00248      CALL 'ILBOABN0'  USING ABEND-CODE.                              CL**3
