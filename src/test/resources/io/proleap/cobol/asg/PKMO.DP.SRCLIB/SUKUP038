000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000210*****************************************************************         
000300* THIS PROGRAM WILL PROCESS A FLAT FILE WITH TRIPS THAT HAVE    *         
000400* BEEN ASSUMPTIVE RECEIVED.  A ROW WILL BE INSERTED INTO        *         
000500* SUT_TRIP_WRKR_AUD TO INDICATE THAT THE TRIP WAS ASSUMPTIVE    *         
000510* RECEIVED.                                                     *         
000511* INPUT FILE WAS CREATED BY SUKUT165                            *         
000520*****************************************************************         
000530                                                                          
000700 PROGRAM-ID.             SUKUP038.                                        
000800 AUTHOR.                 DENISE HAHN.                                     
000900 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
001000 DATE-WRITTEN            FEBRUARY 2021.                                   
001100 DATE-COMPILED.                                                           
001200                                                                          
001300 ENVIRONMENT DIVISION.                                                    
001400 CONFIGURATION SECTION.                                                   
001500 SOURCE-COMPUTER.        IBM-3090.                                        
001600 OBJECT-COMPUTER.        IBM-3090.                                        
001700 INPUT-OUTPUT SECTION.                                                    
001800 FILE-CONTROL.                                                            
001900                                                                          
002000     SELECT INPUT-EXTRACT-FILE                                            
002100         ASSIGN TO INP01.                                                 
002200                                                                          
002300 DATA DIVISION.                                                           
002400 FILE SECTION.                                                            
002500                                                                          
002600 FD  INPUT-EXTRACT-FILE                                                   
002700     RECORDING MODE IS F                                                  
002800     LABEL RECORDS ARE STANDARD                                           
002900     RECORD CONTAINS 100 CHARACTERS                                       
003000     BLOCK CONTAINS 0  RECORDS.                                           
003100 01  INPUT-EXTRACT-REC                PIC X(100).                         
003200                                                                          
003300 WORKING-STORAGE SECTION.                                                 
003400 01  PS-SWITCHES.                                                         
003500     05  PS-EOF-EXTRACT-SW            PIC X(01) VALUE 'N'.                
003600         88  PS-EOF-EXTRACT-YES                 VALUE 'Y'.                
003610     05  PS-TRIP-PREV-ASSUM-SW        PIC X(01) VALUE 'N'.                
003620         88  PS-TRIP-PREV-ASSUM-NO              VALUE 'N'.                
003630         88  PS-TRIP-PREV-ASSUM-YES             VALUE 'Y'.                
003700                                                                          
003730                                                                          
003800 01  PV-PROGRAM-VARIABLES.                                                
003900     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
004000                                                 COMP SYNC.               
004100         88  ABEND-4013-DB2-ERROR                VALUE +4013.             
004200     05  PV-DISP-NBR                  PIC ZZZ,ZZZ,ZZZ.                    
004210     05  PV-PREV-ASSUM-CNT            PIC S9(09)  COMP.                   
004300     05  PV-EXTRACT-COUNT             PIC S9(05) COMP-3                   
004400                                          VALUE ZERO.                     
004500     05  PV-INSERT-COUNT              PIC S9(05) COMP-3                   
004600                                          VALUE ZERO.                     
004900     05  PV-RETRY-COUNTER             PIC S9(03) COMP-3                   
005000                                        VALUE 3.                          
005100     05  PC-MAX-RETRIES               PIC S9(03) COMP-3                   
005200                                        VALUE 3.                          
005300     05  PV-ABEND-PARAGRAPH           PIC X(35) VALUE SPACES.             
005400                                                                          
005500 01  PV-DISP-KEY-MSG.                                                     
006200     05  FILLER                      PIC X(08) VALUE                      
006300         '; TRIP= '.                                                      
006400     05  PV-KEY-TRIP-ID              PIC 9(09) VALUE ZEROES.              
007400                                                                          
007500                                                                          
007600     COPY SURD152.                                                        
007700                                                                          
007800     EXEC SQL                                                             
007900         INCLUDE SUT00006                                                 
008000     END-EXEC.                                                            
008100                                                                          
008200     EXEC SQL                                                             
008300         INCLUDE SQLCA                                                    
008400     END-EXEC.                                                            
008500                                                                          
008600     COPY SQLCA2.                                                         
008700     COPY DPWS004.                                                        
008800 EJECT                                                                    
008900                                                                          
009000 PROCEDURE DIVISION.                                                      
009100     PERFORM 1000-INITIALIZE.                                             
009200     PERFORM 2000-PROCESS                                                 
009300             UNTIL PS-EOF-EXTRACT-YES                                     
009400     PERFORM 9000-EOJ.                                                    
009500                                                                          
009600     GOBACK.                                                              
009700                                                                          
009800 1000-INITIALIZE.                                                         
009900                                                                          
010000     OPEN INPUT INPUT-EXTRACT-FILE                                        
010100                                                                          
010200     PERFORM 8000-READ-EXTRACT-FILE                                       
010300     IF PS-EOF-EXTRACT-YES                                                
010400         DISPLAY ' SUKUP038 - INPUT FILE IS EMPTY '                       
010500     END-IF.                                                              
010600                                                                          
010700 2000-PROCESS.                                                            
010800                                                                          
010801     SET  PS-TRIP-PREV-ASSUM-NO  TO  TRUE.                                
010802     MOVE SU152-TRIP-ID          TO  SUT00006-OTBND-TRIP-ID.              
010803     MOVE 'ASSUMPTV'             TO  SUT00006-USR-ID.                     
010804                                                                          
010805     PERFORM 2100-CHECK-PREV-ASSUM.                                       
010806                                                                          
010807     IF  PS-TRIP-PREV-ASSUM-NO                                            
010830         EVALUATE TRUE                                                    
010831             WHEN SU152-REG-TRLR-DAYS                                     
010832                  MOVE 'SUKUP38A'  TO SUT00006-CRE-BY-PGM-NM              
010833             WHEN SU152-VIRTL-TRLR-DAYS                                   
010834                  MOVE 'SUKUP38B'  TO SUT00006-CRE-BY-PGM-NM              
010835             WHEN SU152-REG-TRLR-INV                                      
010836                  MOVE 'SUKUP38C'  TO SUT00006-CRE-BY-PGM-NM              
010837             WHEN SU152-VIRTL-TRLR-INV                                    
010838                  MOVE 'SUKUP38D'  TO SUT00006-CRE-BY-PGM-NM              
010839             WHEN SU152-REG-TRLR-RFID                                     
010840                  MOVE 'SUKUP38E'  TO SUT00006-CRE-BY-PGM-NM              
010841             WHEN SU152-VIRTL-TRLR-RFID                                   
010842                  MOVE 'SUKUP38F'  TO SUT00006-CRE-BY-PGM-NM              
010843             WHEN SU152-FORCED-EARLY                                      
010844                  MOVE 'SUKUP38G'  TO SUT00006-CRE-BY-PGM-NM              
010845             WHEN SU152-FORCE-EXCEPTION                                   
010846                  MOVE 'SUKUP38H'  TO SUT00006-CRE-BY-PGM-NM              
010847             WHEN OTHER                                                   
010850                  MOVE 'SUKUP038'  TO SUT00006-CRE-BY-PGM-NM              
010860         END-EVALUATE                                                     
010900                                                                          
011100         PERFORM 7100-INSERT-WRKR-AUD                                     
011200     END-IF.                                                              
011300                                                                          
011400     PERFORM 8000-READ-EXTRACT-FILE.                                      
011401                                                                          
011410 2100-CHECK-PREV-ASSUM.                                                   
011420                                                                          
011430     MOVE '2100-CHECK-PREV-ASSUM'     TO PV-ABEND-PARAGRAPH               
011433                                                                          
011495     EXEC SQL                                                             
011496          SELECT  COUNT(*)                                                
011497            INTO  :PV-PREV-ASSUM-CNT                                      
011498            FROM  SUT_TRIP_WRKR_AUD                                       
011499            WHERE OTBND_TRIP_ID  = :SUT00006-OTBND-TRIP-ID                
011500              AND USR_ID         = :SUT00006-USR-ID                       
011501            FETCH FIRST 1 ROW ONLY                                        
011502             WITH UR                                                      
011504     END-EXEC                                                             
011509                                                                          
011510     EVALUATE TRUE                                                        
011511         WHEN SQLCODE = ZERO                                              
011512              IF  PV-PREV-ASSUM-CNT  > 0                                  
011513                  SET  PS-TRIP-PREV-ASSUM-YES  TO  TRUE                   
011514              ELSE                                                        
011515                  SET  PS-TRIP-PREV-ASSUM-NO   TO  TRUE                   
011516              END-IF                                                      
011517                                                                          
011518         WHEN SQLCODE = +100                                              
011520              SET  PS-TRIP-PREV-ASSUM-NO   TO  TRUE                       
011521                                                                          
011522         WHEN SQLCODE = -904                                              
011523         WHEN SQLCODE = -911                                              
011524         WHEN SQLCODE = -913                                              
011525             CONTINUE                                                     
011526                                                                          
011527         WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                          
011528             DISPLAY '**************************************'             
011529             DISPLAY 'WARNING ISSUED '                                    
011530             DISPLAY 'CHECK PREV ASSUMPTIVE RECV            '             
011531             DISPLAY  PV-DISP-KEY-MSG                                     
011532             DISPLAY '**************************************'             
011533             PERFORM 9800-SQL-WARNING                                     
011534                                                                          
011535         WHEN SQLCODE < ZERO                                              
011536             DISPLAY '**************************************'             
011537             DISPLAY 'ERROR ISSUED ON                       '             
011538             DISPLAY ' CHECK PREV ASSUMPTIVE RECV           '             
011539             DISPLAY PV-DISP-KEY-MSG                                      
011540             DISPLAY '**************************************'             
011541             PERFORM 9900-SQL-ERROR                                       
011542     END-EVALUATE.                                                        
011560                                                                          
011600                                                                          
021300 7100-INSERT-WRKR-AUD.                                                    
021310                                                                          
021400     MOVE '7100-INSERT-WRKR-AUD'      TO PV-ABEND-PARAGRAPH               
021500     PERFORM                                                              
021600         WITH TEST AFTER                                                  
021700         VARYING PV-RETRY-COUNTER                                         
021800         FROM 1 BY 1                                                      
021900               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                 
022000                  OR  (SQLCODE = -803)                                    
022100                  OR  (SQLCODE = +0))                                     
022296                                                                          
022300     EXEC SQL                                                             
022400         INSERT INTO SUT_TRIP_WRKR_AUD                                    
022500         (                                                                
022600             OTBND_TRIP_ID                                                
022700            ,USR_ID                                                       
022800            ,CRE_TMST                                                     
022900            ,CRE_BY_PGM_NM                                                
023400         )                                                                
023500             VALUES                                                       
023600         (                                                                
023700             :SUT00006-OTBND-TRIP-ID                                      
023800         ,   :SUT00006-USR-ID                                             
023900         ,   CURRENT TIMESTAMP                                            
024000         ,   :SUT00006-CRE-BY-PGM-NM                                      
024500         )                                                                
024600     END-EXEC                                                             
024700                                                                          
024800     EVALUATE TRUE                                                        
024900         WHEN SQLCODE = ZERO                                              
025000             ADD 1                    TO PV-INSERT-COUNT                  
025300         WHEN SQLCODE = -904                                              
025400         WHEN SQLCODE = -911                                              
025500         WHEN SQLCODE = -913                                              
025600             CONTINUE                                                     
025700                                                                          
025800         WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                          
025900             DISPLAY '**************************************'             
026000             DISPLAY 'WARNING ISSUED '                                    
026100             DISPLAY 'INSERT SUT_TRIP_WRKR_AUD              '             
026200             DISPLAY  PV-DISP-KEY-MSG                                     
026300             DISPLAY '**************************************'             
026400             PERFORM 9800-SQL-WARNING                                     
026500                                                                          
026600         WHEN SQLCODE < ZERO                                              
026700             DISPLAY '**************************************'             
026800             DISPLAY 'ERROR ISSUED ON                       '             
026900             DISPLAY 'INSERT SUT_TRIP_WRKR_AUD              '             
027000             DISPLAY PV-DISP-KEY-MSG                                      
027100             DISPLAY '**************************************'             
027200             PERFORM 9900-SQL-ERROR                                       
027300         END-EVALUATE                                                     
027400     END-PERFORM.                                                         
027500                                                                          
027600     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
027700         SQLCODE NOT = ZERO                                               
027800         DISPLAY '**************************************'                 
027900         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
028000         DISPLAY 'INSERT SUT_TRIP_WRKR_AUD              '                 
028100         DISPLAY PV-DISP-KEY-MSG                                          
028200         DISPLAY '**************************************'                 
028300         PERFORM 9900-SQL-ERROR                                           
028400     END-IF.                                                              
028500                                                                          
028600 8000-READ-EXTRACT-FILE.                                                  
028700                                                                          
028800     READ INPUT-EXTRACT-FILE                                              
028900         INTO SU152-ASSUMPTIVE-CTN-REC                                    
029000     AT END                                                               
029100         SET PS-EOF-EXTRACT-YES       TO TRUE                             
029200     NOT AT END                                                           
029300         ADD 1                        TO PV-EXTRACT-COUNT                 
029400     END-READ.                                                            
029500                                                                          
029600 9000-EOJ.                                                                
029700     CLOSE      INPUT-EXTRACT-FILE                                        
029800                                                                          
029900     DISPLAY SPACES.                                                      
030000     DISPLAY ' SUKUP038'                                                  
030100     DISPLAY SPACES.                                                      
030200     MOVE PV-EXTRACT-COUNT  TO PV-DISP-NBR                                
030300     DISPLAY 'INPUT COUNT:  '  PV-DISP-NBR.                               
030600     MOVE PV-INSERT-COUNT   TO PV-DISP-NBR                                
030700     DISPLAY 'INSERT COUNT: '  PV-DISP-NBR.                               
030800                                                                          
030900 9800-SQL-WARNING.                                                        
031000                                                                          
031100     MOVE SQLCODE               TO SQLCODE2.                              
031200     MOVE SQLERRD(3)            TO SQLERRD3.                              
031300     DISPLAY '** ABEND **       ** = SQLWARNING'.                         
031400     DISPLAY '** PROGRAM        ** = SUKUP038'.                           
031500     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
031600     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
031700     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
031800                                                                          
031900     COPY DPPD004.                                                        
032000     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
032100     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
032200                                                                          
032300 9900-SQL-ERROR.                                                          
032400                                                                          
032500     MOVE SQLCODE               TO SQLCODE2.                              
032600     MOVE SQLERRD(3)            TO SQLERRD3.                              
032700     DISPLAY '** ABEND **       ** = SQLERROR'.                           
032800     DISPLAY '** PROGRAM        ** = SUKUP038'.                           
032900     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
033000                                                                          
033100     COPY DPPD004.                                                        
033200     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
033300     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
