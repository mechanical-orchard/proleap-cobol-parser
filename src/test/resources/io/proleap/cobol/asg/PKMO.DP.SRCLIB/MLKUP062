       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUP062.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/05/2004.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  PARTITION BALANCE UPDATE                                      *        
      *                                                                *        
      *  THIS PROGRAM TAKES THE CONTROL RECORD AND UPDATE THE ACTUAL   *        
      *  PARTITION BALANCE TABLES.                                     *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      *                                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PARTN-INFO-EXTRACT                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT PARTN-CNTL-RPT                                                
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  PARTN-INFO-EXTRACT                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PARTN-INFO-REC                  PIC X(80).                           
                                                                                
       FD  PARTN-CNTL-RPT                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 075 CHARACTERS                                       
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE                     PIC X(075).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  PARTITION INFORMATION DATA EXTRACT                                     
      ******************************************************************        
      *01  ML060-PARTN-INFO-EXTRACT.                                            
           COPY MLRD060.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** REPORT RECORD/LAYOUT BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  HEADER LINE                                                            
      ******************************************************************        
      *01  DP075O-STANDARD-HEADER-75-COLS.                                      
      *01  DP075O-CNFDL-HDR-075-COLS.                                           
           COPY DPWS075O.                                                       
                                                                                
       01  RP-REPORT-TITLE-1.                                                   
           05  FILLER                      PIC X(22)       VALUE SPACES.        
           05  RP-REPORT-TITLE             PIC X(31)       VALUE                
               'PARTITION BALANCE TABLE REPORT'.                                
           05  FILLER                      PIC X(22)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-1.                                                  
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE                
               'LEVEL   '.                                                      
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(04)       VALUE                
               'DEPT'.                                                          
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               'BEGIN DATE'.                                                    
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               ' END DATE '.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(05)       VALUE                
               'GROUP'.                                                         
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE                
               'FUNCTION'.                                                      
           05  FILLER                      PIC X(10)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-2.                                                  
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(08)      VALUE ALL '-'.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(04)      VALUE ALL '-'.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(10)      VALUE ALL '-'.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(10)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(05)      VALUE ALL '-'.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(08)      VALUE ALL '-'.        
           05  FILLER                      PIC X(10)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-DETAIL.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-CONTROL-LEVEL            PIC X(08)       VALUE SPACES.        
               88  RP-MONTHLY-LEVEL                        VALUE                
                   'MONTHLY '.                                                  
               88  RP-WEEKLY-LEVEL                         VALUE                
                   'WEEKLY  '.                                                  
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  RP-DEPT-NBR                 PIC Z9(03)      VALUE ZEROES.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  RP-EFF-BEG-DATE             PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  RP-EFF-END-DATE             PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(06)       VALUE SPACES.        
           05  RP-DEPT-GROUP-CDE           PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  RP-FUNCTION                 PIC X(08)       VALUE SPACES.        
               88  RP-UPDATE-FUNCTION                      VALUE                
                   'UPDATE  '.                                                  
               88  RP-INSERT-FUNCTION                      VALUE                
                   'INSERT  '.                                                  
               88  RP-INSERT-UPDATE-FUNCTION               VALUE                
                   'INS/UPDT'.                                                  
           05  FILLER                      PIC X(10)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-END.                                                       
           05  FILLER                      PIC X(18)       VALUE SPACES.        
           05  FILLER                      PIC X(39)       VALUE                
               '*****  E N D   O F   R E P O R T  *****'.                       
           05  FILLER                      PIC X(18)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-PRINT-LINE-MAX           PIC S9(04)      VALUE +57            
                               COMP.                                            
           05  PC-RETRIES-MAX              PIC S9(04)      VALUE +3             
                               COMP.                                            
           05  PC-NULL-DTE                 PIC X(10)       VALUE                
                                                           '9999-12-31'.        
           05  PC-Y                        PIC X           VALUE 'Y'.           
           05  PC-N                        PIC X           VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-PARTN-INFO        PIC X           VALUE 'N'.           
               88  END-OF-PARTN-INFO                       VALUE 'Y'.           
               88  NOT-END-OF-PARTN-INFO                   VALUE 'N'.           
           05  PS-FIRST-TIME-THRU          PIC X           VALUE 'N'.           
               88  FIRST-TIME-THRU                         VALUE 'Y'.           
               88  NOT-FIRST-TIME-THRU                     VALUE 'N'.           
           05  PS-RECORD-STATUS            PIC X           VALUE SPACE.         
               88  IGNORE-THIS-RECORD                      VALUE 'I'.           
               88  ACCEPT-THIS-RECORD                      VALUE 'A'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-PAGE-COUNT               PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PRINT-LINE-COUNT         PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ADVANCE-LINE             PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-RETRIES-COUNT            PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ACTIVE-CARDS             PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-COMMENT-CARDS            PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RECORD-READ              PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RECORD-INSERTED          PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RECORD-UPDATED           PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RECORD-IGNORED           PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TEMP-DEPT-GP-CDE         PIC X(01)       VALUE SPACES.        
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PREV-TM-LVL-CDE          PIC X(01)       VALUE SPACES.        
           05  PV-PREV-DEPT-NBR            PIC 9(04)       VALUE ZEROES.        
           05  PV-PREV-BEG-DATE            PIC X(10)       VALUE SPACES.        
           05  PV-PREV-DEPT-GP-CDE         PIC X(01)       VALUE SPACES.        
           05  PV-PREV-RECORD-ID           PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  CONTROL-CARD-ABEND                          VALUE +4001.         
           88  TABLE-AREA-ABEND                            VALUE +4002.         
           88  NON-SQL-ABEND-CODE                          VALUE +4004.         
           88  LOGICAL-ISSUE-ABEND                         VALUE +4012.         
           88  SQL-ABEND-CODE                              VALUE +4016.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_PARTN_BAL - ML'S PARTITION BALANCE TABLE               *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00002                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TDTATTR - ML'S DATE ATTRIBUTE TABLE                        *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z100-READ-PARTN-INFO.                                        
                                                                                
           PERFORM B200-PROCESS-PARTN-INFO                                      
               UNTIL END-OF-PARTN-INFO.                                         
                                                                                
           IF PV-RECORD-READ > ZEROES                                           
               AND PV-RECORD-INSERTED > ZEROES                                  
               AND PV-RECORD-UPDATED > ZEROES                                   
      *  PRINT LAST REPORT-LINE                                                 
               PERFORM Z700-PRINT-DETAILS                                       
           ELSE                                                                 
               SET CONTROL-CARD-ABEND     TO TRUE                               
               DISPLAY ' *** NOTHING TO PROCESS, CHECK INPUT FILE'              
               PERFORM Z999-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM B900-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           MOVE PV-TODAYS-DATE-MM  TO DP075O-RUN-MONTH.                         
           MOVE PV-TODAYS-DATE-DD  TO DP075O-RUN-DAY.                           
           MOVE PV-TODAYS-DATE-YY  TO DP075O-RUN-YEAR.                          
           MOVE PV-TODAYS-TIME-HR  TO DP075O-RUN-HOUR.                          
           MOVE PV-TODAYS-TIME-MIN TO DP075O-RUN-MINUTE.                        
           MOVE 'MLKUP062'         TO DP075O-PROGRAM-NAME.                      
                                                                                
           OPEN INPUT  PARTN-INFO-EXTRACT                                       
                OUTPUT PARTN-CNTL-RPT.                                          
                                                                                
           SET FIRST-TIME-THRU     TO TRUE.                                     
                                                                                
      ******************************************************************        
      *    PROCESS PARTITION INFO                                               
      ******************************************************************        
       B200-PROCESS-PARTN-INFO.                                                 
                                                                                
           IF ML060-ACTIVE-CARD                                                 
               ADD +1                      TO PV-ACTIVE-CARDS                   
               MOVE ML060-TM-LVL-CODE      TO MLT00002-TBL-TM-LVL-CDE           
               MOVE ML060-DEPT-NBR-9       TO MLT00002-DEPT-NBR                 
               MOVE ML060-EFF-BEG-DTE      TO MLT00002-EFF-BEG-DTE              
               MOVE ML060-DEPT-GP-CDE      TO MLT00002-DEPT-GP-CDE              
                                                                                
               PERFORM Z200-CHECK-EXISTING-ROW                                  
                                                                                
               IF ACCEPT-THIS-RECORD                                            
                   IF (ML060-EFF-BEG-DTE NOT = PV-PREV-BEG-DATE                 
                       OR ML060-TM-LVL-CODE NOT = PV-PREV-TM-LVL-CDE            
                       OR ML060-DEPT-GP-CDE NOT = PV-PREV-DEPT-GP-CDE           
                       OR ML060-DEPT-NBR-9  NOT = PV-PREV-DEPT-NBR)             
                       AND NOT-FIRST-TIME-THRU                                  
      *  PRINT PREVIOUS REPORT LINE                                             
                       PERFORM Z700-PRINT-DETAILS                               
                   END-IF                                                       
      *  BUILD CURRENT REPORT-LINE                                              
                   PERFORM C100-BUILD-RP-DETAIL                                 
                                                                                
                   IF (SQLCODE = +0 OR SQLCODE = +100)                          
      *  INSERT CURRENT ROW                                                     
                       PERFORM Z400-INSERT-PARTN-BAL                            
                   END-IF                                                       
                                                                                
                   MOVE ML060-TM-LVL-CODE      TO PV-PREV-TM-LVL-CDE            
                   MOVE ML060-DEPT-GP-CDE      TO PV-PREV-DEPT-GP-CDE           
                   MOVE ML060-EFF-BEG-DTE      TO PV-PREV-BEG-DATE              
                   MOVE ML060-DEPT-NBR-9       TO PV-PREV-DEPT-NBR              
                   MOVE PV-RECORD-READ         TO PV-PREV-RECORD-ID             
                   SET NOT-FIRST-TIME-THRU     TO TRUE                          
               ELSE                                                             
                   IF PV-RECORD-READ - PV-PREV-RECORD-ID < 2                    
                       DISPLAY ' PREVIOUS RECORD: '                             
                               PV-PREV-TM-LVL-CDE  ' '                          
                               PV-PREV-DEPT-NBR    ' '                          
                               PV-PREV-BEG-DATE    ' '                          
                               PV-PREV-DEPT-GP-CDE                              
                   END-IF                                                       
                   DISPLAY '  RECORD IGNORED: '                                 
                           ML060-TM-LVL-CODE   ' '                              
                           ML060-DEPT-NBR-9    ' '                              
                           ML060-EFF-BEG-DTE   ' '                              
                           ML060-DEPT-GP-CDE                                    
                   ADD +1                      TO PV-RECORD-IGNORED             
               END-IF                                                           
           ELSE                                                                 
               ADD +1                      TO PV-COMMENT-CARDS                  
           END-IF.                                                              
                                                                                
           PERFORM Z100-READ-PARTN-INFO.                                        
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B900-FINALIZE.                                                           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' **** RECORDS READ:      '                                  
                   PV-RECORD-READ.                                              
           DISPLAY ' **** RECORD IGNORED:    '                                  
                   PV-RECORD-IGNORED.                                           
           DISPLAY ' **** ACTIVE RECORDS:    '                                  
                   PV-ACTIVE-CARDS.                                             
           DISPLAY ' **** COMMENTED RECORDS: '                                  
                   PV-COMMENT-CARDS.                                            
           DISPLAY ' **** ROWS INSERTED:     '                                  
                   PV-RECORD-INSERTED.                                          
           DISPLAY ' **** ROWS UPDATED:      '                                  
                   PV-RECORD-UPDATED.                                           
                                                                                
           PERFORM Z500-COMMIT.                                                 
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-END                                 
               AFTER ADVANCING 3 LINES.                                         
                                                                                
           CLOSE   PARTN-INFO-EXTRACT                                           
                   PARTN-CNTL-RPT.                                              
                                                                                
      ******************************************************************        
      *    BUILD REPORT DETAIL LINE                                             
      ******************************************************************        
       C100-BUILD-RP-DETAIL.                                                    
                                                                                
           IF ML060-TM-LVL-CODE NOT = PV-PREV-TM-LVL-CDE                        
               MOVE ZEROES             TO PV-PRINT-LINE-COUNT                   
           END-IF.                                                              
                                                                                
           IF ML060-TM-WEEKLY-LVL-CODE                                          
               SET RP-WEEKLY-LEVEL         TO TRUE                              
           ELSE                                                                 
               SET RP-MONTHLY-LEVEL        TO TRUE                              
           END-IF.                                                              
                                                                                
           MOVE ML060-DEPT-NBR-9       TO RP-DEPT-NBR.                          
           MOVE ML060-EFF-BEG-DTE      TO RP-EFF-BEG-DATE.                      
           MOVE ML060-DEPT-GP-CDE      TO RP-DEPT-GROUP-CDE.                    
                                                                                
      ******************************************************************        
      *    READ CONTROL CARD                                                    
      ******************************************************************        
       Z100-READ-PARTN-INFO.                                                    
                                                                                
           ADD +1                      TO PV-RECORD-READ.                       
           READ PARTN-INFO-EXTRACT INTO ML060-PARTN-INFO-EXTRACT                
               AT END SET END-OF-PARTN-INFO TO TRUE.                            
                                                                                
      ******************************************************************        
      *    GET END DATE FOR UPDATING PARTITION BALANCE ROW                      
      ******************************************************************        
       Z200-CHECK-EXISTING-ROW.                                                 
                                                                                
           SET ACCEPT-THIS-RECORD      TO TRUE.                                 
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                       A.FSCL_MN_END_DTE                                        
                      ,A.FSCL_WK_END_DTE                                        
                      ,B.DEPT_GP_CDE                                            
                  INTO                                                          
                       :DTATTR-FSCL-MN-END-DTE                                  
                      ,:DTATTR-FSCL-WK-END-DTE                                  
                      ,:PV-TEMP-DEPT-GP-CDE                                     
                  FROM                                                          
                       TDTATTR       A                                          
                      ,MLT_PARTN_BAL B                                          
                  WHERE                                                         
                       KY_DTE + 1 DAY = :MLT00002-EFF-BEG-DTE                   
                   AND TBL_TM_LVL_CDE    = :MLT00002-TBL-TM-LVL-CDE             
                   AND DEPT_NBR          = :MLT00002-DEPT-NBR                   
                   AND :MLT00002-EFF-BEG-DTE                                    
                           BETWEEN EFF_BEG_DTE AND EFF_END_DTE                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
      *  UPDATE PREVIOUS ROW'S END-DATE                                         
                   IF PV-TEMP-DEPT-GP-CDE NOT = MLT00002-DEPT-GP-CDE            
                       PERFORM Z300-UPDATE-PARTN-BAL                            
                   ELSE                                                         
      *  SET SWITCH TO IGNORE THIS RECORD                                       
                       SET IGNORE-THIS-RECORD      TO TRUE                      
                       MOVE PV-PREV-BEG-DATE       TO RP-EFF-BEG-DATE           
                   END-IF                                                       
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY ' LVL-CDE: '                                         
                           MLT00002-TBL-TM-LVL-CDE                              
                           ' DEPT: '                                            
                           MLT00002-DEPT-NBR                                    
                           ' EFF-BEG-DTE: '                                     
                           MLT00002-EFF-BEG-DTE                                 
                           ' DEPT-GP-CDE: '                                     
                           MLT00002-DEPT-GP-CDE                                 
                   MOVE 'CHECK FOR EXISTING ROW' TO PV-OPER-ATTEMPTED           
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    END DATE CURRENT ROW THEN CALL INSERT ROUTINE TO CREATE NEW          
      ******************************************************************        
       Z300-UPDATE-PARTN-BAL.                                                   
                                                                                
      *  LOAD END-DATE ON UPDATE REPORT LINE                                    
           IF ML060-TM-WEEKLY-LVL-CODE                                          
               MOVE DTATTR-FSCL-WK-END-DTE TO MLT00002-EFF-END-DTE              
           ELSE                                                                 
               MOVE DTATTR-FSCL-MN-END-DTE TO MLT00002-EFF-END-DTE              
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                       MLT_PARTN_BAL                                            
                 SET                                                            
                       EFF_END_DTE = :MLT00002-EFF-END-DTE                      
                 WHERE                                                          
                       TBL_TM_LVL_CDE    = :MLT00002-TBL-TM-LVL-CDE             
                   AND DEPT_NBR          = :MLT00002-DEPT-NBR                   
                   AND :MLT00002-EFF-BEG-DTE                                    
                           BETWEEN EFF_BEG_DTE AND EFF_END_DTE                  
                   AND DEPT_GP_CDE      <> :MLT00002-DEPT-GP-CDE                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   IF RP-INSERT-FUNCTION                                        
                       SET RP-INSERT-UPDATE-FUNCTION TO TRUE                    
                   ELSE                                                         
                       SET RP-UPDATE-FUNCTION      TO TRUE                      
                   END-IF                                                       
                   MOVE MLT00002-EFF-END-DTE   TO RP-EFF-END-DATE               
                   ADD +1                      TO PV-RECORD-UPDATED             
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY ' LVL-CDE: '                                         
                           MLT00002-TBL-TM-LVL-CDE                              
                           ' DEPT: '                                            
                           MLT00002-DEPT-NBR                                    
                           ' EFF-BEG-DTE: '                                     
                           MLT00002-EFF-BEG-DTE                                 
                           ' DEPT-GP-CDE: '                                     
                           MLT00002-DEPT-GP-CDE                                 
                   MOVE 'UPDATE PARTN-BAL ROW' TO PV-OPER-ATTEMPTED             
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    INSERT ROWS TO PARTITION CONTROL TABLE                               
      ******************************************************************        
       Z400-INSERT-PARTN-BAL.                                                   
                                                                                
           MOVE PC-NULL-DTE            TO MLT00002-EFF-END-DTE                  
                                                                                
           MOVE 'INSERT PARTN-BAL ROW' TO PV-OPER-ATTEMPTED.                    
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRIES-COUNT FROM 1 BY 1                             
               UNTIL PV-RETRIES-COUNT > PC-RETRIES-MAX                          
                   OR SQLCODE = +0                                              
               EXEC SQL                                                         
                   INSERT                                                       
                     INTO MLT_PARTN_BAL                                         
                          (                                                     
                            TBL_TM_LVL_CDE                                      
                           ,DEPT_NBR                                            
                           ,EFF_BEG_DTE                                         
                           ,EFF_END_DTE                                         
                           ,DEPT_GP_CDE                                         
                          )                                                     
                     VALUES                                                     
                          (                                                     
                            :MLT00002-TBL-TM-LVL-CDE                            
                           ,:MLT00002-DEPT-NBR                                  
                           ,:MLT00002-EFF-BEG-DTE                               
                           ,:MLT00002-EFF-END-DTE                               
                           ,:MLT00002-DEPT-GP-CDE                               
                          )                                                     
               END-EXEC                                                         
                                                                                
               IF SQLCODE NOT = +0                                              
                   DISPLAY ' *** INSERT RETRY #'                                
                           PV-RETRIES-COUNT                                     
                           ' FAIL WITH SQLCODE = ' SQLCODE                      
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           IF SQLCODE = +0                                                      
               SET RP-INSERT-FUNCTION      TO TRUE                              
               MOVE SPACES                 TO RP-EFF-END-DATE                   
               ADD +1                      TO PV-RECORD-INSERTED                
           ELSE                                                                 
               DISPLAY ' *** TM-LVL-CDE: '                                      
                       MLT00002-TBL-TM-LVL-CDE                                  
                       ', DEPT-NBR: '                                           
                       MLT00002-DEPT-NBR                                        
                       ', EFF-BEG-DTE: '                                        
                       MLT00002-EFF-BEG-DTE                                     
                       ', EFF-END-DTE: '                                        
                       MLT00002-EFF-END-DTE                                     
                       ', GP-CDE: '                                             
                       MLT00002-DEPT-GP-CDE                                     
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    COMMIT ROWS TO PARTITION CONTROL TABLE                               
      ******************************************************************        
       Z500-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'COMMIT INSERTED ROWS' TO PV-OPER-ATTEMPTED                 
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PRINT THE REPORT DETAILS                                             
      ******************************************************************        
       Z700-PRINT-DETAILS.                                                      
                                                                                
           IF PV-PRINT-LINE-COUNT > PC-PRINT-LINE-MAX                           
               OR PV-PRINT-LINE-COUNT = ZEROES                                  
               PERFORM Z750-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-DETAIL                              
               AFTER ADVANCING PV-ADVANCE-LINE LINES.                           
                                                                                
           ADD  PV-ADVANCE-LINE TO PV-PRINT-LINE-COUNT.                         
           MOVE 1               TO PV-ADVANCE-LINE.                             
           MOVE SPACES TO RP-REPORT-DETAIL.                                     
                                                                                
      ******************************************************************        
      *    PRINT THE REPORT HEADINGS                                            
      ******************************************************************        
       Z750-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP075O-PAGE-NUMBER.                            
           MOVE 1             TO DP075O-REPORT-NUMBER.                          
           WRITE REPORT-LINE FROM DP075O-STANDARD-HEADER-75-COLS                
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-TITLE-1.                            
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-HEADER-1                            
               AFTER ADVANCING 2 LINES.                                         
           WRITE REPORT-LINE FROM RP-REPORT-HEADER-2.                           
                                                                                
           MOVE 6 TO PV-PRINT-LINE-COUNT.                                       
           MOVE 2 TO PV-ADVANCE-LINE.                                           
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           SET SQL-ABEND-CODE         TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MLKUP062'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                           
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
