      *------------------------------------------------------------     00010001
       IDENTIFICATION DIVISION.                                         00020001
      *------------------------------------------------------------     00030001
       PROGRAM-ID.    EPKUT224.                                         00040002
       AUTHOR.        DAVID WELLER.                                     00050002
       DATE-WRITTEN.  JANUARY, 2009.                                    00060001
                                                                        00070001
      *------------------------------------------------------------     00080001
      *    SEPARATES THE MATCHED RECORDS INTO OFFERS THAT HAVE          00090001
      *    EXCLUSIONS AND OFFERS THAT DO NOT HAVE EXCLUSIONS.           00100001
      *------------------------------------------------------------     00110001
      * THE INPUT TO THIS PROGRAM ARE ITEM LEVEL OFFERS EXTRACT FILE    00120001
      * WITH OFFERS ACTIVE IN A GIVEN DATE RANGE.  THE SECOND INPUT IS  00130001
      * THE STORE GROUP FILE ACTIVE IN THE GIVEN DATE RANGE.            00140016
      *                                                                 00150001
      * THE PROGRAM COMPARES THE TWO FILES AND THE RECORDS THAT MATCH   00151001
      * ARE THEN WRITEN OUT TO TWO SEPARATE FILES DEPENDING ON          00152001
      * WHETHER OR NOT AN EXCLUSION EXISTS:                             00153001
      *  WITH-EXCL-FILE : THIS FILE CONTAINS OFFERS WITH                00154003
      *                   EXCLUSIONS.                                   00155003
      *  WITHOUT-EXCL-FILE : THIS FILE CONTAINS OFFERS WITHOUT          00156003
      *                      EXCLUSIONS.                                00157003
      *------------------------------------------------------------     00159001
      * HISTORY LOG:                                                    00160001
      *                                                                 00170001
      *------------------------------------------------------------     00180001
      * DATE: 01/04/2009                                                00190001
      * WR/IR#:  WR34297                                                00200017
      * PROGRAMMER: DAVID WELLER                                        00210001
      * DESCRIPTION: INITIAL CREATION                                   00220001
      *                                                                 00230001
      *------------------------------------------------------------     00240001
       ENVIRONMENT DIVISION.                                            00250001
      *------------------------------------------------------------     00260001
       CONFIGURATION SECTION.                                           00270001
       SOURCE-COMPUTER. IBM-3090.                                       00280001
       OBJECT-COMPUTER. IBM-3090.                                       00290001
                                                                        00300001
       INPUT-OUTPUT SECTION.                                            00310001
                                                                        00320001
       FILE-CONTROL.                                                    00330001
                                                                        00340001
           SELECT OFFERS-DATA-FILE                                      00350001
             ASSIGN TO INP01.                                           00360001
                                                                        00370001
           SELECT OFFERS-STORE-GROUP-FILE                               00380001
             ASSIGN TO INP02.                                           00390001
                                                                        00400001
           SELECT OFFERS-WITH-EXCL-FILE                                 00410001
             ASSIGN TO OUT01.                                           00411001
                                                                        00412001
           SELECT OFFERS-WITHOUT-EXCL-FILE                              00413001
             ASSIGN TO OUT02.                                           00414001
                                                                        00415001
                                                                        00416001
      *-------------------------------------------------------------    00417001
       DATA DIVISION.                                                   00418001
      *-------------------------------------------------------------    00419001
       FILE SECTION.                                                    00420001
                                                                        00430001
       FD  OFFERS-DATA-FILE                                             00440001
           RECORDING MODE IS F                                          00450001
           LABEL RECORDS ARE STANDARD                                   00460001
           DATA RECORD IS OFFERS-DATA-REC.                              00470001
                                                                        00480001
       01  OFFERS-DATA-REC                               PIC X(176).    00490001
      * COPYBOOK IS EPRD205                                             00500001
                                                                        00510001
       FD  OFFERS-STORE-GROUP-FILE                                      00520001
           RECORDING MODE IS F                                          00530001
           LABEL RECORDS ARE STANDARD                                   00540001
           DATA RECORD IS OFFERS-STORE-GROUP-REC.                       00550001
                                                                        00560001
       01  OFFERS-STORE-GROUP-REC                        PIC X(050).    00570001
      * COPYBOOK IS EPRD206                                             00580001
                                                                        00590001
       FD  OFFERS-WITH-EXCL-FILE                                        00600001
           RECORDING MODE IS F                                          00610001
           LABEL RECORDS ARE STANDARD                                   00620001
           DATA RECORD IS OFFERS-WITH-EXCL-REC.                         00630001
                                                                        00640001
       01  OFFERS-WITH-EXCL-REC                          PIC X(176).    00650004
      * COPYBOOK IS EPRD205                                             00660002
                                                                        00670001
       FD  OFFERS-WITHOUT-EXCL-FILE                                     00680001
           RECORDING MODE IS F                                          00690001
           LABEL RECORDS ARE STANDARD                                   00700001
           DATA RECORD IS OFFERS-WITHOUT-EXCL-REC.                      00710001
                                                                        00720001
       01  OFFERS-WITHOUT-EXCL-REC                        PIC X(187).   00730013
      * COPYBOOK IS EPRD261                                             00740013
                                                                        00750001
      *------------------------------------------------------------     00760001
       WORKING-STORAGE SECTION.                                         00770001
      *------------------------------------------------------------     00780001
       01  AC-ABEND-CODE                  PIC S9(04) VALUE ZERO         00790013
                                                     COMP SYNC.         00800013
           88  AC-4004-TABLE-OVERFLOW-ERROR          VALUE +4004.       00820013
                                                                        00830001
      *------------------------------------------------------------     00840001
      * PC- PROGRAM CONSTANTS                                           00850001
      *------------------------------------------------------------     00860001
       01  PC-PROGRAM-CONSTANTS.                                        00870001
           05  PC-PROGRAM-NAME            PIC X(08) VALUE 'EPKUT224'.   00880002
                                                                        00910001
      *------------------------------------------------------------     00920001
      * PS- PROGRAM-SWITCHES.                                           00930001
      *------------------------------------------------------------     00940001
       01  PS-PROGRAM-SWITCHES.                                         00950001
                                                                        00960001
           05  PS-END-OF-DATA-SW              PIC  X(01) VALUE 'N'.     00970002
               88  PS-END-DATA-FILE                      VALUE 'Y'.     00980002
               88  PS-NOT-END-DATA-FILE                  VALUE 'N'.     00990001
           05  PS-END-OF-STORE-GROUP-SW       PIC  X(01) VALUE 'N'.     01000001
               88  PS-END-STORE-GROUP-FILE               VALUE 'Y'.     01010001
               88  PS-NOT-END-STORE-GROUP-FILE           VALUE 'N'.     01020001
           05  PS-END-OF-PROCESSING-SW        PIC  X(01) VALUE 'N'.     01030002
               88  PS-END-OF-PROCESSING                  VALUE 'Y'.     01040001
               88  PS-NOT-END-OF-PROCESSING              VALUE 'N'.     01050001
                                                                        01180001
      *------------------------------------------------------------     01190001
      * PV- PROGRAM VARIABLES                                           01200001
      *------------------------------------------------------------     01210001
       01  PV-PROGRAM-VARIABLES.                                        01220001
           05  PV-OFFER-WITH-EXCL-KEY.                                  01221003
               10  PV-OWE-EVENT-ID         PIC 9(10)  VALUE ZEROS.      01222003
               10  PV-OWE-OFFER-ID         PIC 9(10)  VALUE ZEROS.      01223003
               10  PV-OWE-OFR-SECT-TYP-CDE PIC X(01)  VALUE SPACE.      01224003
           05  PV-PARAGRAPH-NAME           PIC X(35)  VALUE SPACES.     01230001
           05  PV-OPERATION-MSG-1          PIC X(40)  VALUE SPACES.     01240001
           05  PV-OPERATION-MSG-2          PIC X(40)  VALUE SPACES.     01250001
           05  PV-DATA-REC-READ            PIC 9(09)  VALUE 0.          01260001
           05  PV-STORE-GROUP-REC-READ     PIC 9(09)  VALUE 0.          01270001
           05  PV-REC-WITH-EXCL-CNT        PIC 9(09)  VALUE 0.          01300001
           05  PV-REC-WITHOUT-EXCL-CNT     PIC 9(09)  VALUE 0.          01310001
                                                                        01320012
       01  PV-STORE-GROUP-TABLE.                                        01330012
           05  PV-SG-OFFER-KEY.                                         01340012
               10  PV-SG-EVENT-ID          PIC 9(10)  VALUE ZEROS.      01350012
               10  PV-SG-OFFER-ID          PIC 9(10)  VALUE ZEROS.      01360012
               10  PV-SG-OFR-SECT-TYP-CDE  PIC X(01)  VALUE SPACE.      01370012
           05  PV-SG-COUNT                 PIC S9(4)  COMP              01371013
                                                      VALUE +0.         01372013
           05  PV-SG-OCCURRENCE  OCCURS    1000 TIMES                   01380012
                                           INDEXED BY PV-SG-NDX.        01381013
               10  PV-SG-STR-GRP-TYP-CDE   PIC 9(05).                   01390012
               10  PV-SG-STR-GRP-ID        PIC 9(05).                   01400012
               10  PV-SG-IE-IND            PIC X(01).                   01410013
                                                                        02000001
      *----------------------------------------------------------       02010001
      * COPYBOOK USED FOR KOHLS DATE ROUTINE                            02020001
      *----------------------------------------------------------       02030001
           COPY DPWS500.                                                02040001
                                                                        02050001
      *----------------------------------------------------------       02060001
      * COPYBOOK USED TO READ OFFERS DATA FILE                          02070013
      *----------------------------------------------------------       02080001
           COPY EPRD205.                                                02090001
                                                                        02100001
      *----------------------------------------------------------       02110001
      * COPYBOOK USED TO READ OFFERS STRGRP FILE                        02120001
      *----------------------------------------------------------       02130001
           COPY EPRD206.                                                02140001
                                                                        02150013
      *----------------------------------------------------------       02160013
      * COPYBOOK USED TO WRITE THE OFFERS WITH EXCLUSIONS FILE          02180013
      *----------------------------------------------------------       02190013
           COPY EPRD261.                                                02191013
                                                                        02200001
      *------------------------------------------------------------     02210001
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         02220001
      *------------------------------------------------------------     02230001
           COPY DPWS004.                                                02240001
                                                                        02250001
      *------------------------------------------------------------     02260001
       PROCEDURE DIVISION.                                              02270001
      *------------------------------------------------------------     02280001
       0000-MAINLINE.                                                   02290001
                                                                        02300001
           PERFORM 1000-INITIALIZE.                                     02310001
                                                                        02320001
           PERFORM 2000-PROCESS-OFR-DATA                                02330001
                  UNTIL  PS-END-OF-PROCESSING.                          02340001
                                                                        02350001
           PERFORM 9200-WRAPUP.                                         02360001
                                                                        02370001
           GOBACK.                                                      02380001
                                                                        02390001
                                                                        02420001
      *-------------------------------------------------------------    02430001
      * 1000-INITIALIZE.                                                02440001
      *     INITIALIZES VARIABLES, OPENS ANY FILES, AND READS           02450001
      *     THE INPUT FILES, SAVES VARIABLES, AND CLOSES THE INPUT      02460001
      *     FILES.                                                      02470001
      *-------------------------------------------------------------    02480001
       1000-INITIALIZE.                                                 02490001
           MOVE '1000-INITIALIZE               '                        02500001
             TO PV-PARAGRAPH-NAME.                                      02510001
                                                                        02520001
           SET PS-NOT-END-OF-PROCESSING TO TRUE.                        02530001
                                                                        02540001
           OPEN INPUT                                                   02550001
               OFFERS-STORE-GROUP-FILE,                                 02560001
               OFFERS-DATA-FILE.                                        02570001
                                                                        02580001
           OPEN OUTPUT                                                  02590001
               OFFERS-WITH-EXCL-FILE,                                   02600001
               OFFERS-WITHOUT-EXCL-FILE.                                02610001
                                                                        02620001
           PERFORM 8100-READ-DATA-FILE.                                 02630001
                                                                        02640001
           IF PS-END-DATA-FILE                                          02650001
              SET PS-END-OF-PROCESSING TO TRUE                          02660001
              DISPLAY '  DATA FILE IS EMPTY  '                          02670002
           ELSE                                                         02680001
              SET PS-NOT-END-OF-PROCESSING TO TRUE                      02690001
              PERFORM 8200-READ-STORE-GROUP-FILE                        02700001
           END-IF.                                                      02710001
                                                                        02720001
           IF PS-END-STORE-GROUP-FILE                                   02730001
              DISPLAY '  STORE GROUP FILE IS EMPTY  '                   02740001
              SET PS-END-STORE-GROUP-FILE TO TRUE                       02741002
           END-IF.                                                      02760001
                                                                        02770001
                                                                        02800001
      *----------------------------------------------------------       02810001
      * 2000-PROCESS-OFR-DATA                                           02820001
      *                                                                 02830001
      *  THIS PARAGRAPH PERFORMS THE FOLLOWING                          02840001
      *  (1) READ THE OFFERS DATA FILE                                  02850001
      *  (2) READ THE OFFERS STRGRP FILE                                02880001
      *      AND CALL PARAGRAPH TO MATCH THE FILES                      02881001
      *----------------------------------------------------------       02890001
       2000-PROCESS-OFR-DATA.                                           02900001
                                                                        03170001
           EVALUATE TRUE                                                03180001
                                                                        03181002
               WHEN PS-END-DATA-FILE                                    03183003
                   SET PS-END-OF-PROCESSING TO TRUE                     03184009
                                                                        03185002
               WHEN PS-END-STORE-GROUP-FILE                             03190001
                AND PS-NOT-END-DATA-FILE                                03200001
                   IF EP205-OFFER-KEY = PV-OFFER-WITH-EXCL-KEY          03200103
                       PERFORM 8300-WRITE-WITH-EXCL-FILE                03200203
                   ELSE                                                 03200303
                       PERFORM 8400-FORMAT-WITHOUT-EXCL-FILE            03200413
                   END-IF                                               03200503
                   PERFORM 8100-READ-DATA-FILE                          03202002
                                                                        03203002
               WHEN PS-NOT-END-STORE-GROUP-FILE                         03204002
                AND PS-NOT-END-DATA-FILE                                03205002
                   PERFORM 2300-MATCH-DATA-STRGRP                       03205102
                                                                        03209002
           END-EVALUATE.                                                03260001
                                                                        03270001
      *----------------------------------------------------------       03880001
      * 2300-MATCH-DATA-STRGRP.                                         03890002
      *                                                                 03900001
      *  THIS PARAGRAPH MATCHES THE PROMOTIONS RECORD AGAINST THE OFFER 03910001
      *  RECORDS                                                        03920001
      *----------------------------------------------------------       03930001
       2300-MATCH-DATA-STRGRP.                                          03940002
                                                                        03940102
      * OFFER-KEY ON BOTH FILES IS IN DESCENDING ORDER                  03941002
           IF EP205-OFFER-KEY < EP206-OFFER-KEY                         03942003
      * THIS STORE GROUP HAS NO MATCHING DATA                           03942102
               PERFORM 8200-READ-STORE-GROUP-FILE                       03942202
           ELSE                                                         03943002
               IF EP205-OFFER-KEY = EP206-OFFER-KEY                     03944002
                   IF EP206-STR-GP-EXCLUDED                             03944103
                       MOVE EP206-OFFER-KEY TO PV-OFFER-WITH-EXCL-KEY   03944303
      *  TABLE THE STR-GRP-TYP-CDE AND STR-GRP-ID AND IE-IND            03944403
      *  SAVE THE OFFER KEY OF WHAT IS IN THE TABLE                     03944503
                   ELSE                                                 03944612
                       PERFORM 3000-TABLE-STORE-GROUP                   03944713
                   END-IF                                               03944802
                   PERFORM 8200-READ-STORE-GROUP-FILE                   03944908
               ELSE                                                     03945002
      * NOW WE KNOW EP205-OFFER-KEY > EP206-OFFER-KEY                   03945103
                   IF EP205-OFFER-KEY = PV-OFFER-WITH-EXCL-KEY          03945603
                       PERFORM 8300-WRITE-WITH-EXCL-FILE                03945703
                   ELSE                                                 03945803
                       PERFORM 8400-FORMAT-WITHOUT-EXCL-FILE            03945913
                   END-IF                                               03946003
                   PERFORM 8100-READ-DATA-FILE                          03946103
               END-IF                                                   03947002
           END-IF.                                                      03948002
                                                                        05250001
      *----------------------------------------------------------       05270012
      * 3000-TABLE-STORE-GROUP.                                         05280012
      *                                                                 05290012
      *  THIS PARAGRAPH TABLES STORE GROUP INFORMATION                  05300012
      *----------------------------------------------------------       05320012
       3000-TABLE-STORE-GROUP.                                          05321012
           IF EP206-OFFER-KEY = PV-SG-OFFER-KEY                         05322013
               NEXT SENTENCE                                            05322113
           ELSE                                                         05322213
               PERFORM 4000-CLEAR-SG-TABLE                              05322313
           END-IF.                                                      05322413
           PERFORM 4100-ADD-TO-SG-TABLE.                                05323013
                                                                        05330012
      *---------------------------------------------------------------* 06400001
      *    CLEAR THE STORE GROUP TABLE                                  06410013
      *---------------------------------------------------------------* 06420001
       4000-CLEAR-SG-TABLE.                                             06420113
           MOVE EP206-OFFER-KEY TO PV-SG-OFFER-KEY.                     06420213
           MOVE +0              TO PV-SG-COUNT.                         06420313
           SET PV-SG-NDX        TO 1.                                   06420415
                                                                        06420513
      *---------------------------------------------------------------* 06420613
      *    ADD TO THE STORE GROUP TABLE                                 06420713
      *---------------------------------------------------------------* 06420813
       4100-ADD-TO-SG-TABLE.                                            06420913
           IF PV-SG-NDX         < 1000                                  06421013
               ADD 1            TO PV-SG-COUNT                          06421213
               MOVE EP206-STR-GRP-TYP-CDE                               06421313
                                TO PV-SG-STR-GRP-TYP-CDE(PV-SG-NDX)     06421413
               MOVE EP206-STR-GRP-ID                                    06421513
                                TO PV-SG-STR-GRP-ID(PV-SG-NDX)          06421613
               MOVE EP206-IE-IND                                        06421713
                                TO PV-SG-IE-IND(PV-SG-NDX)              06421813
               SET PV-SG-NDX    UP BY 1                                 06421915
           ELSE                                                         06422013
               DISPLAY 'PV-SG-NDX LIMIT EXCEEDED'                       06422113
               SET AC-4004-TABLE-OVERFLOW-ERROR TO TRUE                 06422213
               PERFORM 9910-ABEND                                       06422313
           END-IF.                                                      06422413
                                                                        06422513
      *---------------------------------------------------------------* 06422613
      *    CLOSES THE DATA FILE.                                        06423013
      *---------------------------------------------------------------* 06424013
       8000-CLOSE-OPEN-READ-DATA.                                       06430002
                                                                        06440001
           CLOSE OFFERS-DATA-FILE.                                      06450001
                                                                        06540001
      *---------------------------------------------------------------* 06570001
      *    READ DATA INPUT FILE  (INP01)                                06580006
      *---------------------------------------------------------------* 06590001
       8100-READ-DATA-FILE.                                             06600001
                                                                        06610001
           READ OFFERS-DATA-FILE INTO EP205-OFFER-EXTRACT               06620001
           AT END                                                       06630001
               SET PS-END-DATA-FILE TO TRUE                             06640010
           NOT AT END                                                   06650001
               SET PS-NOT-END-DATA-FILE TO TRUE                         06660010
               ADD 1 TO PV-DATA-REC-READ                                06662010
           END-READ.                                                    06670001
                                                                        06700001
      *---------------------------------------------------------------* 06730001
      *    READ STORE GROUP INPUT FILE  (INP02)                         06740003
      *---------------------------------------------------------------* 06750001
       8200-READ-STORE-GROUP-FILE.                                      06760001
                                                                        06770001
           READ OFFERS-STORE-GROUP-FILE INTO EP206-STORE-LIST-RCD       06780001
           AT END                                                       06790001
                SET PS-END-STORE-GROUP-FILE TO TRUE                     06800001
           NOT AT END                                                   06810001
                SET PS-NOT-END-STORE-GROUP-FILE TO TRUE                 06820001
                ADD 1 TO PV-STORE-GROUP-REC-READ                        06820110
           END-READ.                                                    06830001
                                                                        06860001
      *---------------------------------------------------------------* 06890001
      *    WRITE RECORDS TO OFFERS WITH EXCLUSIONS FILE (OUT01)       * 06900001
      *---------------------------------------------------------------* 06910001
       8300-WRITE-WITH-EXCL-FILE.                                       06920003
                                                                        06930001
           MOVE EP205-OFFER-EXTRACT TO OFFERS-WITH-EXCL-REC.            06940003
                                                                        06950001
           WRITE OFFERS-WITH-EXCL-REC.                                  06960001
                                                                        06970001
           ADD 1 TO PV-REC-WITH-EXCL-CNT.                               06980001
                                                                        06990001
      *---------------------------------------------------------------* 07020001
      *    WRITE RECORDS TO WITHOUT EXCL FILE (OUT02)                   07030001
      *---------------------------------------------------------------* 07040001
       8400-FORMAT-WITHOUT-EXCL-FILE.                                   07050013
                                                                        07060001
           IF EP205-OFFER-KEY = PV-SG-OFFER-KEY                         07061013
               PERFORM 8410-WRITE-WITHOUT-EXCL-FILE                     07072013
                   VARYING PV-SG-NDX FROM 1 BY 1                        07073013
                   UNTIL PV-SG-NDX > PV-SG-COUNT                        07074013
           END-IF.                                                      07075013
      *---------------------------------------------------------------* 07110113
      *    WRITE RECORDS TO WITHOUT EXCL FILE (OUT02)                   07110213
      *---------------------------------------------------------------* 07110313
       8410-WRITE-WITHOUT-EXCL-FILE.                                    07110413
                                                                        07110514
           MOVE EP205-OFFER-EXTRACT                                     07110614
                                TO EP261-OFFER-EXTRACT-DATA.            07110714
           MOVE PV-SG-STR-GRP-TYP-CDE(PV-SG-NDX)                        07111113
                                TO EP261-STR-GRP-TYP-CDE.               07111213
           MOVE PV-SG-STR-GRP-ID(PV-SG-NDX)                             07111313
                                TO EP261-STR-GRP-ID.                    07111413
           MOVE PV-SG-IE-IND(PV-SG-NDX)                                 07111513
                                TO EP261-SG-IE-IND.                     07111613
           MOVE EP261-OFFER-EXTRACT                                     07111714
                                TO OFFERS-WITHOUT-EXCL-REC.             07111814
                                                                        07111913
           WRITE OFFERS-WITHOUT-EXCL-REC.                               07112013
                                                                        07112113
           ADD 1 TO PV-REC-WITHOUT-EXCL-CNT.                            07112213
                                                                        07112313
      ******************************************************************07112413
      *     ABEND PROGRAM WHEN ERROR OCCURS.                           *07112513
      ******************************************************************07113013
       9910-ABEND.                                                      07114013
                                                                        07115013
           PERFORM 9920-DISPLAY-TOTALS.                                 07116013
                                                                        07117013
           DISPLAY '** ABEND           **'.                             07118013
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          07119013
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07119113
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       07119213
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       07119313
                                                                        07119413
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07119513
                                                                        07120001
      *-------------------------------------------------------------    07150001
      * 9200-WRAPUP.                                                    07160001
      *-------------------------------------------------------------    07170001
       9200-WRAPUP.                                                     07180001
                                                                        07190001
           MOVE '9200-WRAPUP                     ' TO PV-PARAGRAPH-NAME.07200001
                                                                        07210001
           PERFORM 9920-DISPLAY-TOTALS.                                 07220001
                                                                        07230001
           DISPLAY '****************************************'.          07240001
           DISPLAY '**          EPKUT224 END              **'.          07250002
           DISPLAY '****************************************'.          07260001
           CLOSE                                                        07270001
               OFFERS-STORE-GROUP-FILE,                                 07280001
               OFFERS-DATA-FILE,                                        07290001
               OFFERS-WITH-EXCL-FILE,                                   07300001
               OFFERS-WITHOUT-EXCL-FILE.                                07310001
                                                                        07320001
                                                                        07500001
      ******************************************************************07520001
      *     DISPLAY END OF JOB TOTALS                                  *07530001
      ******************************************************************07540001
       9920-DISPLAY-TOTALS.                                             07550001
                                                                        07560001
           DISPLAY '***********************************************'.   07570001
           DISPLAY '**     EOJ STATISTICS FOR EPKUT224           **'    07580002
           DISPLAY '***********************************************'.   07590001
           DISPLAY '                                               '.   07600001
           DISPLAY '                 INPUT COUNTS                  '.   07610001
           DISPLAY '    DATA RECORDS READ       : '                     07620001
                        PV-DATA-REC-READ.                               07630001
           DISPLAY '    STORE GROUP RECORDS READ     : '                07640001
                        PV-STORE-GROUP-REC-READ.                        07650001
           DISPLAY '    RECORDS WRITEN TO WITH EXCL FILE  : '           07700001
                        PV-REC-WITH-EXCL-CNT.                           07710001
           DISPLAY '    RECORDS WRITEN TO WITHOUT EXCL FILE  : '        07720001
                        PV-REC-WITHOUT-EXCL-CNT.                        07730001
                                                                        07760001
