      ******************************************************************00010094
       IDENTIFICATION DIVISION.                                         00020094
      ******************************************************************00030094
      *                                                                 00040094
       PROGRAM-ID.    IMKUT200.                                         00050094
       AUTHOR.        BOB MCASEY.                                       00060094
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00070094
       DATE-WRITTEN.  OCTOBER ,2001                                     00080094
       DATE-COMPILED.                                                   00090094
      *                                                                 00100094
      ******************************************************************00110094
      *   THIS PROGRAM WILL CHECK PO, PERM, AND RTV FOR THE SKU.       *00120094
      *   IF FOUND ON ANY OF THE ABOVE, THIS SKU WILL BE BYPASSED      *00130094
      *   BECAUSE THE SKU MUST NOT EXIST ON ANY OTHER SYSTEMS IN ORDER *00140094
      *   TO BE DELETED.  WHEN AN ITEM IS NOT FOUND AN ANY OF THE ABOVE*00150094
      *   SYSTEMS, IT WILL BE WRITTEN OUT TO THE DELETE FILE. IF IT IS *00160094
      *   FOUND ON ANY OF THE ABOVE SYSTEMS, IT WILL BE WRITTEN OUT TO *00170094
      *   THE INVALID FILE WITH A 'Y' IN THE APPROPRIATE REASON FIELD. *00180094
      ******************************************************************00190094
      *  CHANGED:                                                      *00200094
      *                                                                *00210094
      *    WR/PITS#     DATE        DESCRIPTION                        *00220094
      *    --------   ----------   ---------------------------------   *00230094
      *                                                                *00240094
      *    WR24363    12-09-2002    NI-SKU - RECOMPILE USING NEW       *00250094
      *                             VERSIONS OF IMCC020 & IMCC021.     *00260094
      *    WR27472    09-21-2004    NEW PERM CHANGES - USE XMT TABLES. *00261099
      *                                                                *00270099
      *    WR28405    02-24-2005    RTV SQL CHANGE.                    *00270199
      *                                                                *00270299
      *    WR28439    03-01-2005    STORE EXPANSION CHANGE             *00270399
      *                                                                *00270499
      *    WR29925    03-02-2006    MODIFIED LOGIC FOR TSKST PASS OR   *00270599
      *                             FAIL TEST.  IF BOTH THE LAST SALE  *00270699
      *                             AND LAST RECEIVED DATE = 0 AND SKU *00270799
      *                             WAS CREATED MORE THAN 400 DAYS AGO,*00270899
      *                             LET IT CONTINUE WITH REST OF EDITS.*00270999
      *                             IF THE LAST SALE DATE IS MORE THAN *00271099
      *                             400 DAYS AGO AND LAST RECEIVED DATE*00271199
      *                             IS ZERO, LET SKU CONTINUE WITH REST*00271299
      *                             OF EDITS.  IF LAST RECEIVED DATE IS*00271399
      *                             MORE THAN 400 DAYS AGO, AND LAST   *00271499
      *                             SALE DATE IS ZERO, LET SKU CONTINUE*00271599
      *                             WITH REST OF EDITS.                *00271699
      *    WR29925    04-17-2006    INCREASE INPUT FILE TO 100 BYTES   *00271799
      ******************************************************************00272094
       ENVIRONMENT DIVISION.                                            00280094
      ******************************************************************00290094
      *                                                                 00300094
       CONFIGURATION SECTION.                                           00310094
       SOURCE-COMPUTER.    IBM-3090.                                    00320094
       OBJECT-COMPUTER.    IBM-3090.                                    00330094
      *                                                                 00340094
       INPUT-OUTPUT SECTION.                                            00350094
       FILE-CONTROL.                                                    00360094
      *                                                                 00370094
           SELECT STYLE-INPUT-FILE                                      00380094
               ASSIGN TO INP01.                                         00390094
           SELECT CONTROL-CARD-FILE                                     00400094
               ASSIGN TO INP02.                                         00410094
      *                                                                 00420094
           SELECT STYLE-OUTPUT-FILE                                     00430094
               ASSIGN TO OUT01.                                         00440094
           SELECT INVALID-STYL-FILE                                     00450094
               ASSIGN TO OUT02.                                         00460094
      *                                                                 00470094
      ******************************************************************00480094
       DATA DIVISION.                                                   00490094
      ******************************************************************00500094
      *                                                                 00510094
       FILE SECTION.                                                    00520094
      *                                                                 00530094
       FD  STYLE-INPUT-FILE                                             00540094
           RECORDING MODE IS F                                          00550099
           BLOCK CONTAINS 0 RECORDS.                                    00560094
       01  SD-INPUT-RECORD         PIC X(100).                          00570099
      *                                                                 00580094
       FD  CONTROL-CARD-FILE                                            00590094
           RECORDING MODE IS F                                          00600094
           BLOCK CONTAINS 0 RECORDS.                                    00610094
       01  SD-CONTROL-RECORD       PIC X(80).                           00620094
      *                                                                 00630094
       FD  STYLE-OUTPUT-FILE                                            00640094
           RECORDING MODE IS F                                          00650094
           BLOCK CONTAINS 0 RECORDS.                                    00660094
       01  SD-OUTPUT-RECORD        PIC X(100).                          00670094
      *                                                                 00680094
       FD  INVALID-STYL-FILE                                            00690094
           RECORDING MODE IS F                                          00700094
           BLOCK CONTAINS 0 RECORDS.                                    00710094
       01  SD-INVALID-REC          PIC X(100).                          00720094
      *                                                                 00730094
       WORKING-STORAGE SECTION.                                         00740094
      ******************************************************************00750094
      *  INPUT/OUTPUT FILE RECORD LAYOUT                                00760094
      ******************************************************************00770094
           COPY IMRD020.                                                00780094
      ******************************************************************00790094
      *  OUTPUT FILE INVALID RECORD LAYOUT                              00800094
      ******************************************************************00810094
           COPY IMRD021.                                                00820094
      ****************************************************************  00830094
      * RECORD LAYOUT FOR CONTROL CARD FILE.                            00840094
      ****************************************************************  00850094
       01  CC-CONTROL-CARD.                                             00860094
           05  CC-EXTRACT-DAYS              PIC 9(03).                  00870094
           05  FILLER                       PIC X(77).                  00880094
      *                                                                 00890094
       01  ABEND-CODE                      PIC S9(04)    VALUE ZERO     00900094
                                                         COMP SYNC.     00910094
      *                                                                 00920094
       01  SA-SAVE-AREAS.                                               00930094
           05  SA-OUT-RECORD               PIC X(80)    VALUE SPACES.   00940094
           05  SA-VS-ID                    PIC  S9(09)  COMP            00950094
                                                        VALUE ZERO.     00960094
           05  SA-SUB-CL-MDSEAR-ID         PIC  S9(06)V COMP-3          00970094
                                                        VALUE ZERO.     00980094
           05  SA-CUTOFF-DATE-CCYYMMDD.                                 00990094
               10  SA-CUTOFF-DATE-CCYY     PIC  9(04)   VALUE 0.        01000094
               10  SA-CUTOFF-DATE-MM       PIC  9(02)   VALUE 0.        01010094
               10  SA-CUTOFF-DATE-DD       PIC  9(02)   VALUE 0.        01020094
           05  SA-DATE.                                                 01030094
               10  SA-YEAR                 PIC 9(2)     VALUE ZERO.     01040094
               10  SA-MONTH                PIC 9(2)     VALUE ZERO.     01050094
               10  SA-DAY                  PIC 9(2)     VALUE ZERO.     01060094
           05  SA-DATE2.                                                01070094
               10  SA-MONTH2               PIC 9(2)     VALUE ZERO.     01080094
               10  SA-DAY2                 PIC 9(2)     VALUE ZERO.     01090094
               10  SA-YEAR2                PIC 9(2)     VALUE ZERO.     01100094
      *                                                                 01110094
       01  PA-PROGRAM-ACCUMULATORS.                                     01120094
           05  PA-TUPC-SELECTS             PIC S9(7)    VALUE ZERO      01130099
                                                        COMP-3.         01140094
           05  PA-PERM-SELECTS             PIC S9(7)    VALUE ZERO      01141099
                                                        COMP-3.         01142099
           05  PA-RTV-SELECTS              PIC S9(7)    VALUE ZERO      01150094
                                                        COMP-3.         01160094
           05  PA-PO-SELECTS               PIC S9(7)    VALUE ZERO      01170094
                                                        COMP-3.         01180094
           05  PA-WRITE-COUNT              PIC S9(7)    VALUE ZERO      01190094
                                                        COMP-3.         01200094
           05  PA-READ-COUNT               PIC 9(07)    VALUE ZERO.     01210099
**TEMP*                                                 COMP-3.         01220099
           05  PA-COUNT                    PIC S9(7)    VALUE ZERO      01230094
                                                        COMP-3.         01240094
           05  TUPC-COUNT                  PIC S9(7)    VALUE ZERO      01241099
                                                        COMP-3.         01242099
           05  PA-BYPASSED-SKU-COUNT       PIC S9(7)    VALUE ZERO      01250094
                                                        COMP-3.         01260094
           05  PA-WRITTEN-STYLE-COUNT      PIC S9(7)    VALUE ZERO      01270094
                                                        COMP-3.         01280094
           05  PA-PERM-EDIT-FAIL           PIC S9(7)    VALUE ZERO      01290094
                                                        COMP-3.         01300094
           05  PA-RTV-EDIT-FAIL            PIC S9(7)    VALUE ZERO      01310094
                                                        COMP-3.         01320094
           05  PA-TSKST-EDIT-FAIL          PIC S9(7)    VALUE ZERO      01330094
                                                        COMP-3.         01340094
           05  PA-TSKST-DTE-EDIT-FAIL      PIC S9(7)    VALUE ZERO      01350094
                                                        COMP-3.         01360094
           05  PA-PO-EDIT-FAIL             PIC S9(7)    VALUE ZERO      01370094
                                                        COMP-3.         01380094
           05  PA-RETRY-COUNT              PIC S9(04)   VALUE 0         01390094
                                                        COMP SYNC.      01400094
      *                                                                 01410094
       01  PC-PROGRAM-CONSTANTS.                                        01420094
           05  PC-PROGRAM-NAME             PIC X(08)     VALUE          01430094
               'IMKUT200'.                                              01440094
           05  PC-DISPLAY-PERM-MSG         PIC X(26)     VALUE          01450094
                     'EXISTS ON PERM - NO DELETE'.                      01460094
           05  PC-DISPLAY-PO-MSG           PIC X(26)     VALUE          01470094
                     'EXISTS ON PO  -  NO DELETE'.                      01480094
           05  PC-DISPLAY-RTV-MSG          PIC X(26)     VALUE          01490094
                     'EXISTS ON RTV  - NO DELETE'.                      01500094
           05  PC-DISPLAY-MISC-MSG         PIC X(26)     VALUE          01510094
                     'ERROR RETRIEVING DATA FOR:'.                      01520094
           05  PC-MAX-RETRY-COUNT          PIC S9(04)    VALUE 3        01530094
                                                       COMP SYNC.       01540094
           05  PC-MORE-THAN-MAX-RETRY      PIC S9(04)    VALUE 4        01550094
                                                       COMP SYNC.       01560094
      *                                                                 01570094
       01  PS-PROGRAM-SWITCHES.                                         01580094
           05  PS-END-OF-CONTROL-CARDS-SW  PIC X(01)  VALUE 'N'.        01590094
               88  END-OF-CONTROL-CARDS               VALUE 'Y'.        01600094
           05  PS-END-OF-INPUT-FILE-SW     PIC X(01)  VALUE 'N'.        01610094
               88  PS-END-OF-INPUT-FILE               VALUE 'Y'.        01620094
           05  PS-NO-PO-EXISTS-SW          PIC X(01)  VALUE 'N'.        01630094
               88  PS-NO-PO-EXISTS                    VALUE 'Y'.        01640094
           05  PS-NO-PERM-EXISTS-SW        PIC X(01)  VALUE 'N'.        01650094
               88  PS-NO-PERM-EXISTS                  VALUE 'Y'.        01660094
           05  PS-NO-RTV-EXISTS-SW         PIC X(01)  VALUE 'N'.        01670094
               88  PS-NO-RTV-EXISTS                   VALUE 'Y'.        01680094
           05  PS-NO-TSK-ITEM-EXISTS-SW    PIC X(01)  VALUE 'N'.        01690094
               88  PS-NO-TSK-ITEM-EXISTS              VALUE 'Y'.        01700094
           05  PS-TSKST-PASS-DATE-SW       PIC X(01)  VALUE 'N'.        01710094
               88  PS-TSKST-PASS-DATE                 VALUE 'Y'.        01720094
           05  PS-ITEM-NOT-FOUND-SW        PIC X(01)  VALUE 'N'.        01730094
               88  PS-ITEM-FOUND                      VALUE 'Y'.        01740094
               88  PS-ITEM-NOT-FOUND                  VALUE 'N'.        01750094
      *                                                                 01760094
       01  PV-PROGRAM-VARIABLES.                                        01770094
           05  PV-SUB                      PIC S9(03)     COMP-3        01780094
                                                          VALUE 1.      01790094
           05  PV-REMAINDER                PIC 9(04)V99   VALUE 0       01800099
                                                          COMP-3.       01810099
           05  PV-RESULT                   PIC 9(05)      VALUE 0       01820099
                                                          COMP-3.       01830099
           05  PV-CURRENT-TMST             PIC X(26)      VALUE SPACES. 01840094
                                                                        01850094
           05  PV-TSKST-LAST-RCVD-DATE-ISO.                             01860094
               10  PV-TSKST-LAST-RCVD-CC      PIC  9(02)  VALUE 0.      01870094
               10  PV-TSKST-LAST-RCVD-DATE    PIC  9(06)  VALUE 0.      01880094
               10  FILLER REDEFINES PV-TSKST-LAST-RCVD-DATE.            01890094
                   15  PV-TSKST-LAST-RCVD-YY  PIC  9(02).               01900094
                   15  PV-TSKST-LAST-RCVD-MM  PIC  9(02).               01910094
                   15  PV-TSKST-LAST-RCVD-DD  PIC  9(02).               01920094
                                                                        01930094
           05  PV-TSKST-LAST-SALE-DATE-ISO.                             01940094
               10  PV-TSKST-LAST-SALE-CC      PIC  9(02)  VALUE 0.      01950094
               10  PV-TSKST-LAST-SALE-DATE    PIC  9(06)  VALUE 0.      01960094
               10  FILLER REDEFINES PV-TSKST-LAST-SALE-DATE.            01970094
                   15  PV-TSKST-LAST-SALE-YY  PIC  9(02).               01980094
                   15  PV-TSKST-LAST-SALE-MM  PIC  9(02).               01990094
                   15  PV-TSKST-LAST-SALE-DD  PIC  9(02).               02000094
                                                                        02010094
       01  PW-PROGRAM-WORKAREA.                                         02020094
           05  PW-PARA-NAME                PIC X(35).                   02030094
                                                                        02040094
      ******************************************************************02050094
      *  WORK FIELDS FOR THE FISCAL CALENDAR ROUTINE.                   02060094
      ******************************************************************02070094
           COPY DPWS500.                                                02080094
      *                                                                 02090094
      *                                                                 02100094
           COPY SQLCA2.                                                 02110094
      *                                                                 02120094
      ******************************************************************02130094
      *  TSKSTOO - PO - SKU STORE ON ORDER                              02140094
      ******************************************************************02150094
      *                                                                 02160094
           EXEC SQL                                                     02170094
                INCLUDE TSKSTOO                                         02180094
           END-EXEC.                                                    02190094
      *                                                                 02200094
      ******************************************************************02210094
      *  TCPPCDT - PE CENTRAL PPC DETAIL TABLE                          02220094
      ******************************************************************02230094
      *                                                                 02240094
           EXEC SQL                                                     02250094
                INCLUDE TCPPCDT                                         02260094
           END-EXEC.                                                    02270094
      *                                                                 02280094
      ******************************************************************02290094
      *  TDFCTOR - RV - DEFECT ORDER TABLE                              02300094
      ******************************************************************02310094
      *                                                                 02320094
           EXEC SQL                                                     02330094
                INCLUDE TDFCTOR                                         02340094
           END-EXEC.                                                    02350094
      *                                                                 02360094
      ******************************************************************02370094
      *  TDFMEIT - RV-DEFECT ORDER MERCHANDISE ITEM TABLE               02380094
      ******************************************************************02390094
      *                                                                 02400094
           EXEC SQL                                                     02410094
                INCLUDE TDFMEIT                                         02420094
           END-EXEC.                                                    02430094
      *                                                                 02440094
      ******************************************************************02450094
      *  TCENPPC - PE CENTRAL PERM PRICE CHANGE TABLE                   02460094
      ******************************************************************02470094
      *                                                                 02480094
           EXEC SQL                                                     02490094
                INCLUDE TCENPPC                                         02500094
           END-EXEC.                                                    02510094
      *                                                                 02520094
      ******************************************************************02530094
      *  TSKST   - FS - SKU STORE TABLE                                 02540094
      ******************************************************************02550094
      *                                                                 02560094
           EXEC SQL                                                     02570094
                INCLUDE TSKST                                           02580094
           END-EXEC.                                                    02590094
      *                                                                 02600094
      ***************************************************************** 02610094
      *  TMDARVS DCLGEN                                                 02620094
      ***************************************************************** 02630094
           EXEC SQL                                                     02640094
               INCLUDE TMDARVS                                          02650094
           END-EXEC.                                                    02660094
                                                                        02661099
      ******************************************************************02662099
      *  TUPC DCLGEN                                                    02663099
      ******************************************************************02664099
           EXEC SQL                                                     02666099
                INCLUDE TUPC                                            02667099
           END-EXEC.                                                    02668099
0904RM**-WR27472 - NEW PERM CHANGES                                     02670099
0904RM***************************************************************** 02670199
0904RM*  XMT_PRCHG DCLGEN                                               02672099
0904RM***************************************************************** 02673099
0904RM     EXEC SQL                                                     02674099
0904RM         INCLUDE XMT00009                                         02675099
0904RM     END-EXEC.                                                    02676099
                                                                        02677099
0904RM***************************************************************** 02677199
0904RM*  XMT_STR_GP_PRCHG DCLGEN                                        02677299
0904RM***************************************************************** 02677399
0904RM     EXEC SQL                                                     02677499
0904RM         INCLUDE XMT00010                                         02677599
0904RM     END-EXEC.                                                    02677699
                                                                        02677799
0904RM***************************************************************** 02678099
0904RM*  XMT_PRCHG_MDSE DCLGEN                                          02679099
0904RM***************************************************************** 02679199
0904RM     EXEC SQL                                                     02679299
0904RM         INCLUDE XMT00011                                         02679399
0904RM     END-EXEC.                                                    02679499
                                                                        02679599
      *                                                                 02680094
           EXEC SQL                                                     02690094
                INCLUDE SQLCA                                           02700094
           END-EXEC.                                                    02710094
                                                                        02720094
      ******************************************************************02730094
      *  DB2 ERROR MESSAGES FORMAT AREA.                                02740094
      ******************************************************************02750094
           COPY DPWS004.                                                02760094
                                                                        02770094
           EJECT                                                        02780094
      *                                                                 02790094
      ******************************************************************02800094
       PROCEDURE DIVISION.                                              02810094
      ******************************************************************02820094
      *                                                                 02830094
           EJECT                                                        02840094
       0000-IMKUT200.                                                   02850094
      ******************************************************************02860094
      * MAIN PROCESSING ROUTINE                                        *02870094
      ******************************************************************02880094
                                                                        02890094
           PERFORM 1000-HOUSEKEEPING.                                   02900094
      *                                                                 02910094
           PERFORM 2000-PROCESS-DELETIONS                               02920094
             UNTIL PS-END-OF-INPUT-FILE.                                02930094
      *                                                                 02940094
           PERFORM 1500-END-OF-JOB.                                     02950094
           GOBACK.                                                      02960094
      *                                                                 02970094
                                                                        02980094
       1000-HOUSEKEEPING.                                               02990094
                                                                        03000094
           MOVE '*1000-HOUSEKEEPING.              *' TO PW-PARA-NAME.   03010094
                                                                        03020094
           OPEN INPUT  STYLE-INPUT-FILE                                 03030094
                       CONTROL-CARD-FILE                                03040094
                OUTPUT INVALID-STYL-FILE                                03050094
                       STYLE-OUTPUT-FILE.                               03060094
                                                                        03070094
           PERFORM 6000-READ-SKU-DELETES.                               03080094
           IF PS-END-OF-INPUT-FILE                                      03090094
               DISPLAY '********** IMKUT200 ***********'                03100094
               DISPLAY 'SKU DELETES INPUT FILE IS EMPTY'                03110094
               DISPLAY '*******************************'                03120094
           END-IF.                                                      03130094
                                                                        03140094
           ACCEPT SA-DATE                  FROM DATE.                   03150094
                                                                        03160094
           DISPLAY 'CURRENT DATE = ' SA-DATE.                           03170094
                                                                        03180094
                                                                        03190094
           MOVE SA-YEAR                    TO SA-YEAR2.                 03200094
           MOVE SA-MONTH                   TO SA-MONTH2.                03210094
           MOVE SA-DAY                     TO SA-DAY2.                  03220094
                                                                        03230094
           PERFORM 6200-READ-CONTROL-CARD.                              03240094
      *                                                                 03250094
           INITIALIZE DPG51                                             03260094
                      DPG52                                             03270094
                      DPG53                                             03280094
                      DPG54                                             03290094
                      DPG55                                             03300094
                      DPG56.                                            03310094
      *                                                                 03320094
           MOVE CC-EXTRACT-DAYS            TO DPG51-INCR-DECR-DAYS-9.   03330094
           MOVE SA-DATE2                   TO DPG52-LK-DATE-INPUT.      03340094
      *                                                                 03350094
           SET DPG51-ACTUAL-CALENDAR-ONLY                               03360094
               DPG51-DECREMENT-DATE                                     03370094
               DPG52-LK-DTE-GREG           TO TRUE.                     03380094
      *                                                                 03390094
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                03400094
                                                   DPG52                03410094
                                                   DPG53                03420094
                                                   DPG54                03430094
                                                   DPG55                03440094
                                                   DPG56.               03450094
      *                                                                 03460094
           IF  DPG54-SEVERE-ERROR                                       03470094
            OR DPG54-DATE-INVALID                                       03480094
               DISPLAY '** IMKUT200 ABEND **'                           03490094
               DISPLAY '** FISCAL DATE SUBROUTINE ERROR; MESSAGE = '    03500094
                       DPG54-ERROR-MESSAGE                              03510094
               MOVE +4019                  TO ABEND-CODE                03520094
               CALL 'ILBOABN0' USING ABEND-CODE                         03530094
           ELSE                                                         03540094
               IF  DPG54-WARNING-ERROR                                  03550094
                   DISPLAY '** IMKUT200 ADVISORY **'                    03560094
                   DISPLAY '** FISCAL DATE ROUTINE WARNING = '          03570094
                           DPG54-ERROR-MESSAGE                          03580094
               END-IF                                                   03590094
           END-IF.                                                      03600094
                                                                        03610094
           MOVE DPG55-DB2-ISO-DATE-YR      TO SA-CUTOFF-DATE-CCYY.      03620094
           MOVE DPG55-DB2-ISO-DATE-MO      TO SA-CUTOFF-DATE-MM.        03630094
           MOVE DPG55-DB2-ISO-DATE-DY      TO SA-CUTOFF-DATE-DD.        03640094
                                                                        03650094
           DISPLAY 'CUTOFF DATE  = ' SA-CUTOFF-DATE-CCYYMMDD.           03660094
                                                                        03670094
                                                                        03680094
       1500-END-OF-JOB.                                                 03690094
                                                                        03700094
      ******************************************************************03710094
      *  CLOSE FILES AND DISPLAY COUNTS                                *03720094
      ******************************************************************03730094
                                                                        03740094
           CLOSE STYLE-INPUT-FILE                                       03750094
                 CONTROL-CARD-FILE                                      03760094
                 INVALID-STYL-FILE                                      03770094
                 STYLE-OUTPUT-FILE.                                     03780094
                                                                        03790094
           DISPLAY '***************************'                        03800094
           DISPLAY 'END OF JOB STATS'                                   03810094
           DISPLAY '# OF RECS READ        : = ' PA-READ-COUNT.          03820094
0904RM*    DISPLAY '# OF PO   FAILS       : = ' PA-PO-SELECTS.          03830099
           DISPLAY '# OF PERM FAILS       : = ' PA-PERM-SELECTS.        03840094
           DISPLAY '# OF RV   FAILS       : = ' PA-RTV-SELECTS.         03850094
           DISPLAY '# OF TUPC SELECTS     : = ' PA-TUPC-SELECTS.        03851099
           DISPLAY '# OF TSKST FAILS      : = ' PA-TSKST-EDIT-FAIL.     03860094
           DISPLAY '# OF TSKST DATE FAILS : = ' PA-TSKST-DTE-EDIT-FAIL. 03870094
           DISPLAY '# OF BYPASSED SKUS    : = ' PA-BYPASSED-SKU-COUNT.  03880094
           DISPLAY '# OF RECS WRITTEN     : = ' PA-WRITE-COUNT.         03890094
                                                                        03900094
       2000-PROCESS-DELETIONS.                                          03910094
           MOVE '*2000-PROCESS-DELETIONS.         *' TO PW-PARA-NAME.   03920094
      ******************************************************************03930094
      *   AT A BREAK IN STYLE, CHECK TABLE AND WRITE TO OUTPUT IF      *03940094
      *   NECESSARY                                                    *03950094
      ******************************************************************03960094
           MOVE 'N' TO PS-TSKST-PASS-DATE-SW                            03970094
                       PS-NO-PERM-EXISTS-SW                             03980094
                       PS-NO-PO-EXISTS-SW                               03990094
                       PS-NO-RTV-EXISTS-SW.                             04000094
                                                                        04010094
      ******************************************************************04020094
      * PO, PERM AND RTV ARE ALL CHECKED AND IF ROW IS PRESENT ON ANY  *04030094
      * OF THESE TABLES - THE RECORD IS NOT WRITTEN TO THE DELETION    *04040094
      * FILE FOR THIS SKU/ITEM.                                        *04050094
      ******************************************************************04060094
           PERFORM 3000-SELECT-TSKST.                                   04070094
           IF PS-TSKST-PASS-DATE                                        04080094
0904RM*  WR27274 - EDIT IS NO LONGER NEEDED, COVERED IN IMKUT205        04081099
0904RM*        PERFORM 3200-CHECK-PO-SYSTEM                             04090099
0904RM*        IF PS-NO-PO-EXISTS                                       04100099
               PERFORM 3400-CHECK-PERM-SYSTEM                           04110099
               IF PS-NO-PERM-EXISTS                                     04120099
                   PERFORM 3600-CHECK-RTV-SYSTEM                        04130099
                   IF PS-NO-RTV-EXISTS                                  04140099
                      PERFORM 3800-SELECT-RMS-STYL-ID                   04150099
                      PERFORM 8000-WRITE-SKU-DELETE-FILE                04160099
                   END-IF                                               04170099
               END-IF                                                   04180099
0904RM*        END-IF                                                   04190099
           END-IF.                                                      04200094
                                                                        04210094
0904RM*    IF PS-NO-PO-EXISTS      AND                                  04220099
0904RM     IF PS-NO-PERM-EXISTS    AND                                  04230099
              PS-NO-RTV-EXISTS     AND                                  04240094
              PS-TSKST-PASS-DATE                                        04250094
               CONTINUE                                                 04260094
           ELSE                                                         04270094
               ADD 1  TO PA-BYPASSED-SKU-COUNT                          04280094
               PERFORM 8200-WRITE-BYPASSED-SKU                          04290094
           END-IF.                                                      04300094
                                                                        04310094
           PERFORM 6000-READ-SKU-DELETES.                               04320094
                                                                        04330094
       3000-SELECT-TSKST.                                               04340094
           MOVE '*3000-SELECT-TSKST.              *' TO PW-PARA-NAME    04350094
      ******************************************************************04360094
      * CHECK TSKST FOR ITEM                                           *04370094
      ******************************************************************04380094
                                                                        04390094
           EXEC SQL                                                     04400094
              SELECT  LAST_SALE_DATE                                    04410094
                    , LAST_RCVD_DATE                                    04420094
                INTO :TSKST-LAST-SALE-DATE                              04430094
                    ,:TSKST-LAST-RCVD-DATE                              04440094
                FROM TSKST                                              04450094
               WHERE ITEM_NMBR         = :IM020-ITM-NBR                 04460094
                 AND LOC_NBR           = SMALLINT(0)                    04470099
             WITH UR                                                    04480094
           END-EXEC.                                                    04490094
                                                                        04500094
**TEMP*          AND LAST_SALE_DATE    > 0                              04510094
           EVALUATE TRUE                                                04520094
               WHEN SQLCODE = 0                                         04530094
                 PERFORM 4000-TEST-DATES                                04540094
               WHEN SQLCODE = +100                                      04550094
                   ADD 1 TO PA-TSKST-EDIT-FAIL                          04560094
                   MOVE IM020-STYLE-DELETE-REC TO                       04570094
                        IM021-STYLE-DELETE-INV-REC                      04580094
                   MOVE 'Y' TO IM021-FAIL-TSKST                         04590094
               WHEN OTHER                                               04600094
                 PERFORM 9980-SQL-ERROR                                 04610094
           END-EVALUATE.                                                04620094
                                                                        04630094
       3200-CHECK-PO-SYSTEM.                                            04640094
           MOVE '*3200-CHECK-PO-SYSTEM.           *' TO PW-PARA-NAME.   04650094
      ******************************************************************04660094
      * CHECK P.O. SYSTEM FOR ANY ACTIVITY FOR THIS ITEM.              *04670094
      * MORE THAN 1 ROW IS OK.  THIS JUST CHECKS FOR THE EXISTENCE     *04680094
      * THE ITEM ON ANY PURCHASE ORDERS.                               *04690094
      ******************************************************************04700094
                                                                        04710094
           PERFORM VARYING PA-RETRY-COUNT FROM 1 BY 1                   04720094
             UNTIL PA-RETRY-COUNT > PC-MAX-RETRY-COUNT                  04730094
             EXEC SQL                                                   04740094
                 SELECT 1                                               04750094
                   INTO :PA-COUNT                                       04760094
                   FROM SYSIBM.SYSDUMMY1                                04770094
                  WHERE EXISTS                                          04780094
                       (SELECT 1                                        04790094
                           FROM TSKSTOO                                 04800094
                          WHERE ITM_NBR     = :IM020-ITM-NBR)           04810094
               WITH UR                                                  04820094
             END-EXEC                                                   04830094
                                                                        04840094
             EVALUATE TRUE                                              04850094
                 WHEN SQLCODE = 0                                       04860094
                   ADD 1 TO PA-PO-SELECTS                               04870094
                   MOVE PC-MORE-THAN-MAX-RETRY TO PA-RETRY-COUNT        04880094
                   MOVE IM020-STYLE-DELETE-REC TO                       04890094
                        IM021-STYLE-DELETE-INV-REC                      04900094
                   MOVE 'Y'                    TO IM021-FAIL-PO         04910094
                 WHEN SQLCODE = +100                                    04920094
                 WHEN SQLCODE = -305                                    04930094
                   SET PS-NO-PO-EXISTS TO TRUE                          04940094
                   MOVE PC-MORE-THAN-MAX-RETRY TO PA-RETRY-COUNT        04950094
                 WHEN SQLCODE = -911                                    04960094
                 WHEN SQLCODE = -913                                    04970094
                   CONTINUE                                             04980094
                 WHEN OTHER                                             04990094
                   PERFORM 9980-SQL-ERROR                               05000094
             END-EVALUATE                                               05010094
           END-PERFORM.                                                 05020094
                                                                        05030094
       3400-CHECK-PERM-SYSTEM.                                          05040094
           MOVE '*3400-CHECK-PERM-SYSTEM.           *' TO PW-PARA-NAME. 05050094
      ******************************************************************05060094
      * CHECK PERM SYSTEM FOR ANY ACTIVITY FOR THIS ITEM.              *05070094
      * MORE THAN 1 ROW IS OK.  THIS JUST CHECKS FOR THE EXISTENCE     *05080094
      * THE ITEM ON ANY PERM DOCUMENTS.                                *05090094
      ******************************************************************05100094
                                                                        05110094
           PERFORM VARYING PA-RETRY-COUNT FROM 1 BY 1                   05120094
             UNTIL PA-RETRY-COUNT > PC-MAX-RETRY-COUNT                  05130094
             EXEC SQL                                                   05140094
0904RM*          SELECT 1                                               05150099
0904RM*           INTO :PA-COUNT                                        05160099
0904RM*           FROM SYSIBM.SYSDUMMY1                                 05170099
0904RM*             WHERE EXISTS                                        05180099
0904RM*                (SELECT 1                                        05190099
0904RM*                     FROM TCPPCDT A                              05200099
0904RM*                        , TCENPPC B                              05210099
0904RM*                WHERE A.TSK_ITEM_NMBR = :IM020-ITM-NBR           05220099
0904RM*                  AND A.OPERCO_NBR    = '03'                     05230099
0904RM*                  AND B.OPERCO_NBR    = A.OPERCO_NBR             05240099
0904RM*                  AND A.CENPPC_NBR    = B.CENPPC_NBR             05250099
0904RM*                  AND B.STATUS_CDE IN ('AP','IP',                05260099
0904RM*                                       'PA','PE','RJ'))          05270099
0904RM          SELECT 1                                                05271099
0904RM            INTO :PA-COUNT                                        05271199
0904RM            FROM XMT_PRCHG A                                      05271299
0904RM               , XMT_STR_GP_PRCHG B                               05271399
0904RM               , XMT_PRCHG_MDSE C                                 05271499
0904RM           WHERE A.PRCHG_ID = B.PRCHG_ID                          05271599
0904RM             AND A.PRCHG_ID = C.PRCHG_ID                          05271699
0904RM             AND A.PRCHG_ST_CDE IN ('PND','APR')                  05271799
0904RM             AND A.EVNT_CTG_CDE IN ('ACT','CLR')                  05271899
0904RM             AND B.STR_GP_TYP_CDE = C.STR_GP_TYP_CDE              05271999
0904RM             AND B.STR_GP_ID = C.STR_GP_ID                        05272099
0904RM             AND B.PRCHG_EFF_DTE > CURRENT DATE                   05273099
0904RM             AND C.MDSE_HIER_CDE = 'SKU'                          05274099
0904RM             AND C.SKU_NBR = :IM020-SKU                           05275099
0904RM           FETCH FIRST 1 ROWS ONLY                                05276099
             END-EXEC                                                   05290094
                                                                        05300094
             EVALUATE TRUE                                              05310094
                 WHEN SQLCODE = 0                                       05320094
                   ADD 1 TO PA-PERM-SELECTS                             05330094
                   MOVE PC-MORE-THAN-MAX-RETRY TO PA-RETRY-COUNT        05340094
                   MOVE IM020-STYLE-DELETE-REC TO                       05350094
                        IM021-STYLE-DELETE-INV-REC                      05360094
                   MOVE 'Y'                    TO IM021-FAIL-PERM       05370094
                 WHEN SQLCODE = +100                                    05380094
                 WHEN SQLCODE = -305                                    05390094
                   SET PS-NO-PERM-EXISTS TO TRUE                        05400094
                   MOVE PC-MORE-THAN-MAX-RETRY TO PA-RETRY-COUNT        05410094
                 WHEN SQLCODE = -911                                    05420094
                 WHEN SQLCODE = -913                                    05430094
                   CONTINUE                                             05440094
                 WHEN OTHER                                             05450094
                   PERFORM 9980-SQL-ERROR                               05460094
             END-EVALUATE                                               05470094
                                                                        05480094
           END-PERFORM.                                                 05490094
                                                                        05500094
       3600-CHECK-RTV-SYSTEM.                                           05510094
           MOVE '*3600-CHECK-RTV-SYSTEM.           *' TO PW-PARA-NAME.  05520094
      ******************************************************************05530094
      * CHECK RTV SYSTEM FOR ANY ACTIVITY FOR THIS ITEM.               *05540094
      * MORE THAN 1 ROW IS OK.  THIS JUST CHECKS FOR THE EXISTENCE     *05550094
      * THE ITEM ON ANY RTV DOCUMENTS.                                 *05560094
      ******************************************************************05570094
                                                                        05580094
           PERFORM VARYING PA-RETRY-COUNT FROM 1 BY 1                   05590094
             UNTIL PA-RETRY-COUNT > PC-MAX-RETRY-COUNT                  05600094
             EXEC SQL                                                   05610094
                 SELECT 1                                               05620094
                  INTO :PA-COUNT                                        05630094
                  FROM SYSIBM.SYSDUMMY1                                 05640094
                    WHERE EXISTS                                        05650094
                     (SELECT 1                                          05660094
                        FROM TDFCTOR A                                  05670094
                           , TDFMEIT B                                  05680094
                       WHERE A.DFCT_ORD_ID   = B.DFCT_ORD_ID            05690094
                         AND B.DFCT_ITEM_NBR  = :IM020-ITM-NBR          05700094
                         AND B.DFCT_RET_DTE > (CURRENT DATE - 1 YEAR)   05710094
                         AND A.DFCT_ORD_STAT_CDE <> 'A')                05720099
               WITH UR                                                  05750094
             END-EXEC                                                   05760094
                                                                        05770094
             EVALUATE TRUE                                              05780094
                 WHEN SQLCODE = 0                                       05790094
                   ADD 1 TO PA-RTV-SELECTS                              05800094
                   MOVE IM020-STYLE-DELETE-REC TO                       05810094
                        IM021-STYLE-DELETE-INV-REC                      05820094
                   MOVE 'Y'                TO IM021-FAIL-RTV            05830094
                   MOVE PC-MORE-THAN-MAX-RETRY TO PA-RETRY-COUNT        05840094
                 WHEN SQLCODE = +100                                    05850094
                 WHEN SQLCODE = -305                                    05860094
                   SET PS-NO-RTV-EXISTS TO TRUE                         05870094
                   MOVE PC-MORE-THAN-MAX-RETRY TO PA-RETRY-COUNT        05880094
                 WHEN SQLCODE = -911                                    05890094
                 WHEN SQLCODE = -913                                    05900094
                   CONTINUE                                             05910094
                 WHEN OTHER                                             05920094
                   PERFORM 9980-SQL-ERROR                               05930094
             END-EVALUATE                                               05940094
           END-PERFORM.                                                 05950094
                                                                        05960094
       3800-SELECT-RMS-STYL-ID.                                         05970094
           MOVE '*3800-SELECT-RMS-STYL-ID          *' TO PW-PARA-NAME.  05980094
      ******************************************************************05990094
      * SELECT RMS STYLE IDS FROM TMDARVS.                             *06000094
      ******************************************************************06010094
      *                                                                 06020094
           EXEC SQL                                                     06030094
              SELECT  RMS_STYL_ID                                       06040094
                INTO :MDARVS-RMS-STYL-ID                                06050094
                FROM TMDARVS                                            06060094
               WHERE SUB_CL_MDSEAR_ID  = :IM020-SUB-CL-MDSEAR-ID        06070094
                 AND VS_ID             = :IM020-VS-ID                   06080094
             WITH UR                                                    06090094
           END-EXEC.                                                    06100094
                                                                        06110094
           EVALUATE TRUE                                                06120094
               WHEN SQLCODE = 0                                         06130094
                 CONTINUE                                               06140094
               WHEN SQLCODE = +100                                      06150094
**TEMP*          DISPLAY ' RMS STYLE NOT FOUND : '                      06160094
**TEMP*                IM020-SUB-CL-MDSEAR-ID ' ' IM020-VS-ID           06170094
                 MOVE 0 TO MDARVS-RMS-STYL-ID                           06180094
               WHEN OTHER                                               06190094
                 PERFORM 9980-SQL-ERROR                                 06200094
           END-EVALUATE.                                                06210094
                                                                        06220094
       3900-TUPC-EFF-BEG-CK.                                            06221099
           MOVE '*3900-TUPC-EFF-BEG-CK.         *' TO PW-PARA-NAME.     06222099
      ******************************************************************06223099
      * CHECK IF SKUS EFFECTIVE BEGIN DATE IS MORE THAN 400 DAYS AGO   *06224099
      ******************************************************************06227099
           MOVE IM020-INTR-UPC-ID TO UPC-UPC-ID.                        06228099
           PERFORM VARYING PA-RETRY-COUNT FROM 1 BY 1                   06229099
             UNTIL PA-RETRY-COUNT > PC-MAX-RETRY-COUNT                  06229199
             EXEC SQL                                                   06229299
                SELECT 1                                                06229399
                  INTO :TUPC-COUNT                                      06229499
                  FROM TUPC                                             06229799
                  WHERE UPC_ID = :UPC-UPC-ID                            06229899
                    AND EFF_BEG_DTE < (CURRENT DATE - 400 DAYS)         06229999
                    AND EFF_END_DTE IS NULL                             06230099
                 FETCH FIRST 1 ROWS ONLY                                06230199
             END-EXEC                                                   06230699
                                                                        06230799
             EVALUATE TRUE                                              06230899
                 WHEN SQLCODE = 0                                       06230999
                   ADD 1 TO PA-TUPC-SELECTS                             06231099
                   MOVE PC-MORE-THAN-MAX-RETRY TO PA-RETRY-COUNT        06231499
                 WHEN SQLCODE = +100                                    06231599
                   MOVE PC-MORE-THAN-MAX-RETRY TO PA-RETRY-COUNT        06231699
                 WHEN SQLCODE = -911                                    06232099
                 WHEN SQLCODE = -913                                    06232199
                   CONTINUE                                             06232299
                 WHEN OTHER                                             06232399
                   PERFORM 9980-SQL-ERROR                               06232499
             END-EVALUATE                                               06232599
           END-PERFORM.                                                 06232699
                                                                        06232799
       4000-TEST-DATES.                                                 06233094
           MOVE '*4000-TEST-DATES.                *' TO PW-PARA-NAME.   06240094
      ******************************************************************06250094
      * CHECK IF THE LAST RECEIVED AND LAST SALE DATES ARE WITHIN THE  *06260094
      * CUTOFF DATE. IF THE LAST SALE AND LAST RECEIVED DATE BOTH      *06270099
      * EQUAL ZERO, CHECK IF EFFECTIVE BEGIN DATE ON TUPC IS MORE THAN *06271099
      * 400 DAYS AGO, IF SO LET THIS SKU CONTINUE THROUGH. IF LAST SALE*06272099
      * OR LAST RECEIVED DATE = 0 AND OTHER ONE IS MORE THAN 400 DAYS  *06273099
      * OLD, LET THIS SKU CONTINUE THROUGH.                             06274099
      ******************************************************************06280094
           MOVE 'N' TO PS-TSKST-PASS-DATE-SW.                           06290094
                                                                        06300094
           MOVE TSKST-LAST-RCVD-DATE TO PV-TSKST-LAST-RCVD-DATE.        06310094
           IF TSKST-LAST-RCVD-DATE = ZERO                               06320094
               MOVE ZERO TO PV-TSKST-LAST-RCVD-CC                       06330094
           ELSE                                                         06340094
               IF PV-TSKST-LAST-RCVD-YY > 80                            06350094
                   MOVE 19 TO PV-TSKST-LAST-RCVD-CC                     06360094
               ELSE                                                     06370094
                   MOVE 20 TO PV-TSKST-LAST-RCVD-CC                     06380094
               END-IF                                                   06390094
           END-IF.                                                      06400094
                                                                        06410094
           MOVE TSKST-LAST-SALE-DATE TO PV-TSKST-LAST-SALE-DATE.        06420094
           IF TSKST-LAST-SALE-DATE = ZERO                               06430094
               MOVE ZERO TO PV-TSKST-LAST-SALE-CC                       06440094
           ELSE                                                         06450094
               IF PV-TSKST-LAST-SALE-YY > 80                            06460094
                   MOVE 19 TO PV-TSKST-LAST-SALE-CC                     06470094
               ELSE                                                     06480094
                   MOVE 20 TO PV-TSKST-LAST-SALE-CC                     06490094
               END-IF                                                   06500094
           END-IF.                                                      06510094
                                                                        06520094
           IF ((PV-TSKST-LAST-RCVD-DATE-ISO < SA-CUTOFF-DATE-CCYYMMDD)  06530094
                AND                                                     06540094
               (PV-TSKST-LAST-RCVD-DATE-ISO > '00000000'))              06550094
           AND ((PV-TSKST-LAST-SALE-DATE-ISO < SA-CUTOFF-DATE-CCYYMMDD) 06560094
                AND                                                     06570094
               (PV-TSKST-LAST-SALE-DATE-ISO > '00000000'))              06580094
               SET PS-TSKST-PASS-DATE TO TRUE                           06590094
           ELSE                                                         06640094
             IF PV-TSKST-LAST-SALE-DATE-ISO = '00000000'                06640199
               AND PV-TSKST-LAST-RCVD-DATE-ISO = '00000000'             06640299
                  PERFORM 3900-TUPC-EFF-BEG-CK                          06640399
                  IF SQLCODE = 0                                        06640499
                     SET PS-TSKST-PASS-DATE TO TRUE                     06640599
                  END-IF                                                06641099
             ELSE                                                       06642099
                 IF PV-TSKST-LAST-RCVD-DATE-ISO <                       06642199
                        SA-CUTOFF-DATE-CCYYMMDD  AND                    06642299
                     PV-TSKST-LAST-SALE-DATE-ISO = '00000000'           06642499
                     SET PS-TSKST-PASS-DATE TO TRUE                     06642599
                 ELSE                                                   06642699
                   IF PV-TSKST-LAST-SALE-DATE-ISO <                     06642799
                          SA-CUTOFF-DATE-CCYYMMDD  AND                  06642899
                       PV-TSKST-LAST-RCVD-DATE-ISO = '00000000'         06642999
                       SET PS-TSKST-PASS-DATE TO TRUE                   06643099
                   END-IF                                               06643199
                 END-IF                                                 06643299
             END-IF                                                     06643499
           END-IF.                                                      06740094
                                                                        06740199
           IF PS-TSKST-PASS-DATE                                        06740299
               MOVE PV-TSKST-LAST-SALE-DATE-ISO TO                      06741099
                    IM020-LAST-SALES-DTE                                06742099
               MOVE PV-TSKST-LAST-RCVD-DATE-ISO TO                      06743099
                    IM020-LAST-RECEIPT-DTE                              06744099
           ELSE                                                         06744199
               ADD 1    TO PA-TSKST-DTE-EDIT-FAIL                       06744299
               MOVE IM020-STYLE-DELETE-REC TO                           06744399
                    IM021-STYLE-DELETE-INV-REC                          06744499
               MOVE 'Y' TO IM021-FAIL-DTE-TSKST                         06744599
               MOVE PV-TSKST-LAST-SALE-DATE-ISO TO                      06744699
                    IM021-LAST-SALES-DTE                                06744799
               MOVE PV-TSKST-LAST-RCVD-DATE-ISO TO                      06744899
                    IM021-LAST-RECEIPT-DTE                              06744999
           END-IF.                                                      06745099
                                                                        06750094
      *                                                                 06760094
       6000-READ-SKU-DELETES.                                           06770094
           MOVE '*6000-READ-SKU-DELETES.          *' TO PW-PARA-NAME.   06780094
      ******************************************************************06790094
      * READ SKU DELETES FILE.                                         *06800094
      ******************************************************************06810094
           READ STYLE-INPUT-FILE INTO IM020-STYLE-DELETE-REC            06820094
               AT END                                                   06830094
                      SET PS-END-OF-INPUT-FILE TO TRUE.                 06840094
                                                                        06850094
           IF PS-END-OF-INPUT-FILE                                      06860094
               CONTINUE                                                 06870094
           ELSE                                                         06880094
               DIVIDE PA-READ-COUNT BY 50000 GIVING PV-RESULT           06890099
                   REMAINDER PV-REMAINDER                               06900094
               IF (PV-REMAINDER = 0) AND (PA-READ-COUNT > 1)            06910099
1202RM            AND (PV-RESULT > 0)                                   06910199
                   DISPLAY '# OF RECS READ        : = '                 06920094
                            PA-READ-COUNT                               06930094
0904RM*            DISPLAY '# OF PO   FAILS       : = '                 06940099
0904RM*                     PA-PO-SELECTS                               06950099
                   DISPLAY '# OF PERM FAILS       : = '                 06960094
                            PA-PERM-SELECTS                             06970094
                   DISPLAY '# OF TUPC SELECTS     : = '                 06971099
                            PA-TUPC-SELECTS                             06972099
                   DISPLAY '# OF RV   FAILS       : = '                 06980094
                            PA-RTV-SELECTS                              06990094
                   DISPLAY '# OF TSKST FAILS      : = '                 07000094
                            PA-TSKST-EDIT-FAIL                          07010094
                   DISPLAY '# OF TSKST DATE FAILS : = '                 07020094
                            PA-TSKST-DTE-EDIT-FAIL                      07030094
                   DISPLAY '# OF BYPASSED SKUS    : = '                 07040094
                            PA-BYPASSED-SKU-COUNT                       07050094
                   DISPLAY '# OF RECS WRITTEN     : = '                 07060094
                            PA-WRITE-COUNT                              07070094
                   DISPLAY ' '                                          07080094
               END-IF                                                   07090094
               ADD 1 TO PA-READ-COUNT                                   07100099
           END-IF.                                                      07110094
                                                                        07120094
      *                                                                 07130094
       6200-READ-CONTROL-CARD.                                          07140094
           MOVE '*6200-READ-CONTROL-CARD.         *' TO PW-PARA-NAME.   07150094
      ******************************************************************07160094
      * READ DATE CONTROL FILE.                                        *07170094
      ******************************************************************07180094
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                  07190094
               AT END SET END-OF-CONTROL-CARDS TO TRUE.                 07200094
                                                                        07210094
           IF END-OF-CONTROL-CARDS                                      07220094
               DISPLAY '*** NO DATE CONTROL CARD ***'                   07230094
               PERFORM 9990-ERROR                                       07240094
           ELSE                                                         07250094
               DISPLAY ' NUMBER OF DAYS TO SUBTRACT FROM CURRENT DATE ' 07260094
               CC-EXTRACT-DAYS                                          07270094
           END-IF.                                                      07280094
                                                                        07290094
       8000-WRITE-SKU-DELETE-FILE.                                      07300094
           MOVE '*8000-WRITE-SKU-DELETE-FILE.   *' TO PW-PARA-NAME.     07310094
      ******************************************************************07320094
      * WRITE STYLE DELETE RECORDS                                     *07330094
      ******************************************************************07340094
           INITIALIZE IM020-ENT-ID.                                     07350094
           MOVE MDARVS-RMS-STYL-ID          TO IM020-RMS-STYL-ID.       07360094
           WRITE SD-OUTPUT-RECORD         FROM IM020-STYLE-DELETE-REC.  07370094
           ADD +1 TO PA-WRITE-COUNT.                                    07380094
      *                                                                 07390094
       8200-WRITE-BYPASSED-SKU.                                         07400094
           MOVE '*8200-WRITE-BYPASSED-SKU.      *' TO PW-PARA-NAME.     07410094
      ******************************************************************07420094
      * WRITE BYPASSED STYLE DELETE RECORDS                            *07430094
      ******************************************************************07440094
                                                                        07450094
           INITIALIZE IM021-ENT-ID                                      07460094
                      IM021-RMS-STYL-ID.                                07470094
           WRITE SD-INVALID-REC FROM      IM021-STYLE-DELETE-INV-REC.   07480094
      *                                                                 07490094
       9980-SQL-ERROR.                                                  07500094
      ******************************************************************07510094
      * ABEND ROUTINE FOR BAD SQL CALLS                                *07520094
      ******************************************************************07530094
           DISPLAY '** ABEND **          = SQL ERROR'.                  07540094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07550094
           DISPLAY '** PARAGRAPH **      = ' PW-PARA-NAME.              07560094
      *                                                                 07570094
      ******************************************************************07580094
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    07590094
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         07600094
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     07610094
      * FORMAT.                                                         07620094
      ******************************************************************07630094
           COPY DPPD004.                                                07640094
                                                                        07650094
             EXEC SQL                                                   07660094
                COMMIT                                                  07670094
             END-EXEC.                                                  07680094
      *                                                                 07690094
           MOVE +4013                      TO ABEND-CODE.               07700094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07710094
                                                                        07720094
       9990-ERROR.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PW-PARA-NAME.              07780094
           MOVE +4016                    TO ABEND-CODE.                 07790094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
