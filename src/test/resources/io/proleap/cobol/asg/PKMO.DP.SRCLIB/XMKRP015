       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      XMKRP015.                                       00020000
       AUTHOR.          PAUL KEBER.                                     00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    08/17/2006.                                     00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      ** XMKRP015 - PRIVATE BRAND COST FACTOR DATA INTEGRITY REPORT     00080001
      ******************************************************************00090000
      ** THIS PROGRAM VALIDATES THE DATA ON XMT_PRVT_BRND_FCTR IS THE   00100001
      ** SAME AS THE DATA IN THE SOR (K_EMT_PRVT_BRND_COST_FCTR).       00110001
      **   CREATED FOR PRIVATE BRAND ACCOUNTING WR30627                 00120000
      ******************************************************************00121000
                                                                        00122300
       ENVIRONMENT DIVISION.                                            00122400
       CONFIGURATION SECTION.                                           00122500
       SOURCE-COMPUTER.        IBM-3090.                                00122600
       OBJECT-COMPUTER.        IBM-3090.                                00122700
       INPUT-OUTPUT SECTION.                                            00122800
       FILE-CONTROL.                                                    00122900
                                                                        00123000
           SELECT SOR-PRVT-BRND-EXTRACT-FILE                            00124004
                  ASSIGN TO INP01.                                      00125000
           SELECT XM-PRVT-BRND-EXTRACT-FILE                             00126001
                  ASSIGN TO INP02.                                      00127000
           SELECT XM-PRVT-BRND-INTEG-RPT-FILE                           00128001
                  ASSIGN TO RPT01.                                      00129000
                                                                        00130000
       DATA DIVISION.                                                   00140000
       FILE SECTION.                                                    00150000
                                                                        00160000
       FD  SOR-PRVT-BRND-EXTRACT-FILE                                   00170004
           RECORDING MODE F                                             00180000
           BLOCK CONTAINS 0 RECORDS                                     00190000
           LABEL RECORDS ARE OMITTED                                    00200000
           DATA RECORD IS SOR-PRVT-BRND-EXTRACT-RECORD.                 00210004
       01  SOR-PRVT-BRND-EXTRACT-RECORD    PIC X(34).                   00220004
                                                                        00230000
       FD  XM-PRVT-BRND-EXTRACT-FILE                                    00240001
           RECORDING MODE F                                             00250000
           BLOCK CONTAINS 0 RECORDS                                     00260000
           LABEL RECORDS ARE OMITTED                                    00270000
           DATA RECORD IS XM-PRVT-BRND-EXTRACT-RECORD.                  00280001
       01  XM-PRVT-BRND-EXTRACT-RECORD     PIC X(27).                   00290001
                                                                        00300000
       FD  XM-PRVT-BRND-INTEG-RPT-FILE                                  00310001
           RECORDING MODE IS F                                          00320000
           BLOCK CONTAINS 0  RECORDS.                                   00330000
       01  XM-PRVT-BRND-INTEG-RPT-RECORD   PIC  X(132).                 00340002
                                                                        00350000
       WORKING-STORAGE SECTION.                                         00360000
                                                                        00370000
       01  FILLER                              PIC X(41)                00380000
           VALUE 'WORKING STORAGE STARTS HERE FOR XMKRP015'.            00390000
                                                                        00400000
       01  PS-PROGRAM-SWITCHES.                                         00410000
           05  PS-END-OF-SOR-PRCHG-SW          PIC X(01) VALUE 'N'.     00420000
               88  PS-END-OF-SOR-PRVT-BRND               VALUE 'Y'.     00430004
               88  PS-NOT-END-OF-SOR-PRVT-BRND           VALUE 'N'.     00440004
           05  PS-END-OF-XM-PRVT-BRND-SW       PIC X(01) VALUE 'N'.     00450001
               88  PS-END-OF-XM-PRVT-BRND                VALUE 'Y'.     00460001
               88  PS-NOT-END-OF-XM-PRVT-BRND            VALUE 'N'.     00470001
           05  PS-ERROR-FOUND-SW               PIC X(01) VALUE 'N'.     00480000
               88  PS-ERROR-FOUND                        VALUE 'Y'.     00490000
               88  PS-NO-ERROR-FOUND                     VALUE 'N'.     00500000
                                                                        00510000
       01  PC-PROGRAM-CONSTANTS.                                        00520000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)  VALUE         00530000
                                                          'XMKRP015'.   00540000
           05  PC-REPORT-NBR-1                 PIC  9(01) VALUE 1.      00550000
           05  PC-ONE                          PIC  9(01) VALUE 1.      00560000
           05  PC-NULL                         PIC S9(04) VALUE -1.     00570000
           05  PC-MAX-REPORT-LINES             PIC S9(05) VALUE +62     00580002
                                                          COMP-3.       00590000
           05  PC-DEFAULT-DATE                 PIC  X(10) VALUE         00591003
               '9999-09-09'.                                            00592003
                                                                        00600000
           05  PC-NO-ERROR-MESSAGE             PIC  X(132) VALUE        00610002
               'NO ERRORS'.                                             00620000
                                                                        00630000
           05  PC-MISSING-FROM-SOR-PRVT-BRND   PIC  X(50) VALUE         00640001
               'RECORD NOT FOUND ON SOR PRIVATE BRAND EXTRACT.    '.    00650001
                                                                        00660000
           05  PC-MISSING-FROM-XM-PRVT-BRND    PIC  X(50) VALUE         00670001
               'RECORD NOT FOUND ON XM PRIVATE BRAND EXTRACT.     '.    00680001
                                                                        00690000
           05  PC-EXTRA-FOUND-SOR-PRVT-BRND    PIC  X(50) VALUE         00700001
               'ADDITIONAL DATA FOUND ON SOR PRIVATE BRND EXTRACT.'.    00710001
                                                                        00720000
           05  PC-EXTRA-FOUND-XM-PRVT-BRND    PIC  X(50) VALUE          00730001
               'ADDITIONAL DATA FOUND ON XM PRIVATE BRAND EXTRACT.'.    00740001
                                                                        00750000
           05  PC-FIELD-DOES-NOT-MATCH        PIC  X(25) VALUE          00760000
               ' - FIELD DOES NOT MATCH. '.                             00770000
                                                                        00780000
                                                                        00790000
       01  PV-MISCELLANEOUS.                                            00800003
           05  PV-TEMP-PRVT-BRND-ID            PIC S9(11) COMP          00810001
                                                          VALUE ZEROS.  00820000
           05  PV-PRINT-RECORD                 PIC X(132) VALUE SPACE.  00830002
           05  PV-SAVE-PRINT-RECORD            PIC X(132) VALUE SPACE.  00840002
           05  PV-LINE-SPACING                 PIC S9(04) VALUE ZEROES  00850000
                                                          COMP SYNC.    00860000
           05  PV-XM015-LOD-PCT                PIC 9(03).99 VALUE ZERO. 00861004
           05  PV-CURR-TIME.                                            00870000
              10  PV-CURR-TIME-HH              PIC  9(02) VALUE ZEROES. 00880000
              10  PV-CURR-TIME-MM              PIC  9(02) VALUE ZEROES. 00890000
              10  PV-CURR-TIME-SSHH            PIC  9(04) VALUE ZEROES. 00900000
                                                                        00910000
           05  PV-CURR-DATE-CCYYMMDD.                                   00920000
               10  PV-CURR-DATE-CCYY           PIC  9(04) VALUE ZEROES. 00930000
               10  PV-CURR-DATE-MM             PIC  9(02) VALUE ZEROES. 00940000
               10  PV-CURR-DATE-DD             PIC  9(02) VALUE ZEROES. 00950000
                                                                        00960000
           05  PV-XMKEY-XM015.                                          00961003
              10  PV-XMKEY-DEPT-NBR            PIC  9(04).              00962003
              10  PV-XMKEY-EFF-BEG-DTE         PIC  X(10).              00963003
                                                                        00965003
           05  PV-HMKEY-HM014.                                          00966003
              10  PV-HMKEY-DEPT-NBR            PIC  9(04).              00967003
              10  PV-HMKEY-EFF-BEG-DTE         PIC  X(10).              00968003
                                                                        00969103
       01  PV-PROGRAM-COUNTER.                                          00970000
           05  PV-PAGE-CNT                     PIC  9(05) VALUE ZEROS.  00980000
           05  PV-LINE-CNT                     PIC S9(07) VALUE ZEROS.  00990000
           05  PV-SOR-PRVT-BRND-RECS-READ-CNT  PIC  9(09) VALUE ZEROS.  01000001
           05  PV-XM-EXTRACT-REC-READ-CNT      PIC  9(09) VALUE ZEROS.  01010000
           05  PV-SOR-PRVT-BRND-RECS-UNIQUE    PIC  9(09) VALUE ZEROS.  01020001
           05  PV-XM-EXTRACT-REC-UNIQUE        PIC  9(09) VALUE ZEROS.  01030000
           05  PV-SOR-XM-COMMON-CNT            PIC  9(09) VALUE ZEROS.  01040000
           05  PV-SOR-XM-TOTAL-CNT             PIC  9(09) VALUE ZEROS.  01050000
           05  PV-ERROR-RECORD-WRITTEN-CNT     PIC  9(09) VALUE ZEROS.  01060000
           05  PV-VALID-MATCH-CNT              PIC  9(09) VALUE ZEROS.  01070000
           05  PV-TOTAL-ERROR-CNT              PIC  9(09) VALUE ZEROS.  01080000
           05  PV-RECORD-ERR-CNT               PIC  9(09) VALUE ZEROS.  01090000
                                                                        01100000
      ******************************************************************01110000
      *  TOP OF PAGE HEADING LINE FOR PRINTING A 132 CHARACTER REPORT.  01120001
      ******************************************************************01130000
           COPY DPWS132.                                                01140001
                                                                        01150000
       01  HL2-HEADING-LINE-2.                                          01160000
           05  FILLER                  PIC X(46)   VALUE SPACES.        01170002
           05  FILLER                  PIC X(40)   VALUE                01180001
               'XMT_PRVT_BRND_FCTR DATA INTEGRITY ERRORS'.              01190001
           05  FILLER                  PIC X(46)   VALUE SPACES.        01200002
                                                                        01210000
       01  HL3-HEADING-LINE-3.                                          01220000
           05  FILLER                  PIC X(02)   VALUE SPACES.        01230000
           05  FILLER                  PIC X(17)   VALUE                01240002
               'DEPT  EFF BEG DTE'.                                     01260002
           05  FILLER                  PIC X(04)   VALUE SPACES.        01270002
           05  FILLER                  PIC X(10)   VALUE                01280002
               'SOR VALUE '.                                            01300002
           05  FILLER                  PIC X(02)   VALUE SPACES.        01310000
           05  FILLER                  PIC X(10)   VALUE                01320002
               'XM VALUE  '.                                            01340002
           05  FILLER                  PIC X(02)   VALUE SPACES.        01350000
           05  FILLER                  PIC X(50)   VALUE                01360000
      ********* 123456789-123456789-123456789-123456789-123456789-      01370000
               'ERROR MESSAGE                                     '.    01380000
           05  FILLER                  PIC X(35)   VALUE SPACES.        01390002
                                                                        01400000
       01  HL4-HEADING-LINE-4.                                          01410000
           05  FILLER                  PIC X(02)   VALUE SPACES.        01420000
           05  FILLER                  PIC X(17)   VALUE                01430002
      ********* 123456789-123456789-123456789-123456789-123456789-      01440000
               '-----------------'.                                     01450002
           05  FILLER                  PIC X(04)   VALUE SPACES.        01460002
           05  FILLER                  PIC X(10)   VALUE                01470002
      ********* 123456789-123456789-123456789-123456789-                01480000
               '----------'.                                            01490002
           05  FILLER                  PIC X(02)   VALUE SPACES.        01500000
           05  FILLER                  PIC X(10)   VALUE                01510002
      ********* 123456789-123456789-123456789-123456789-                01520000
               '----------'.                                            01521002
           05  FILLER                  PIC X(02)   VALUE SPACES.        01540000
           05  FILLER                  PIC X(50)   VALUE                01550000
      ********* 123456789-123456789-123456789-123456789-123456789-      01560000
               '--------------------------------------------------'.    01570000
           05  FILLER                  PIC X(35)   VALUE SPACES.        01580002
                                                                        01590000
      ******************************************************************01600000
      *  DETAIL LINE LAYOUT FOR THE ERROR REPORT.                       01610000
      ******************************************************************01620000
       01  DL1-DETAIL-LINE-1.                                           01630000
           05  FILLER                  PIC X(02)     VALUE SPACES.      01640000
           05  DL1-KEY.                                                 01650000
               10  DL1-DEPT-NBR        PIC Z(04)     VALUE ZEROES.      01660001
               10  FILLER              PIC X(02)     VALUE SPACE.       01670001
               10  DL1-EFF-BEG-DTE     PIC X(10)     VALUE SPACES.      01671001
           05  FILLER                  PIC X(05)     VALUE SPACES.      01680001
           05  DL1-SOR-VALUE           PIC X(10)     VALUE SPACES.      01690001
           05  FILLER                  PIC X(02)     VALUE SPACES.      01700000
           05  DL1-XM-VALUE            PIC X(10)     VALUE SPACES.      01710001
           05  FILLER                  PIC X(02)     VALUE SPACES.      01720000
           05  DL1-ERROR-MESSAGE.                                       01730000
               10  DL1-ERROR-FIELD     PIC X(25)     VALUE SPACES.      01740000
               10  DL1-ERROR-MSG       PIC X(25)     VALUE SPACES.      01750000
           05  FILLER                  PIC X(35)     VALUE SPACES.      01760001
                                                                        01770000
                                                                        01780000
      ******************************************************************01790000
      *  TRAILER LAYOUT FOR THE ERROR REPORT.                           01800000
      ******************************************************************01810000
       01  TR1-TRAILER-LINE-1.                                          01820000
           05  FILLER                  PIC X(25)   VALUE                01830000
               ' TOTAL NUMBER OF ERRORS: '.                             01840000
           05  TR1-TOTAL-ERROR-CNT     PIC Z(09)   VALUE ZEROS.         01850000
           05  FILLER                  PIC X(98)   VALUE SPACES.        01860002
                                                                        01870000
       01  TR2-TRAILER-LINE-2.                                          01880000
           05  FILLER                  PIC X(25)   VALUE                01890000
               ' NOT FOUND ON SOR      : '.                             01900000
           05  TR2-SOR-MISSING-CNT     PIC Z(09)   VALUE ZEROS.         01910000
           05  FILLER                  PIC X(98)   VALUE SPACES.        01920002
                                                                        01930000
       01  TR3-TRAILER-LINE-3.                                          01940000
           05  FILLER                  PIC X(25)   VALUE                01950000
               ' NOT FOUND ON XM       : '.                             01960000
           05  TR3-XM-MISSING-CNT      PIC Z(09)   VALUE ZEROS.         01970000
           05  FILLER                  PIC X(98)   VALUE SPACES.        01980002
                                                                        01990000
       01  TR4-TRAILER-LINE-4.                                          02000000
           05  FILLER                  PIC X(25)   VALUE                02010000
               ' RECORD LEVEL ERRORS   : '.                             02020000
           05  TR4-RECORD-ERR-CNT      PIC Z(09)   VALUE ZEROS.         02030000
           05  FILLER                  PIC X(98)   VALUE SPACES.        02040002
                                                                        02050000
      ******************************************************************02060000
      **COPYBOOKS                                                       02070000
      ******************************************************************02080000
                                                                        02090000
      ***SOR K_EMT_PRVT_BRND_COST_FCTR EXTRACT FILE LAYOUT              02100001
           COPY HMRD014.                                                02110001
                                                                        02120000
      ***XMT_PRVT_BRND_FCTR EXTRACT FILE LAYOUT                         02130001
           COPY XMRD015.                                                02140001
                                                                        02150000
      ***************************************************************** 02160000
       PROCEDURE DIVISION.                                              02170000
      ***************************************************************** 02180000
       0000-MAINLINE.                                                   02190000
                                                                        02200000
           PERFORM 1000-INITIALIZATION.                                 02210000
                                                                        02220000
           PERFORM 2000-PROCESS-RECORDS                                 02230000
             UNTIL PS-END-OF-SOR-PRVT-BRND                              02240001
                OR PS-END-OF-XM-PRVT-BRND.                              02250001
                                                                        02260000
           PERFORM 2200-PROCESS-EXTRA-RECORDS                           02270000
             UNTIL PS-END-OF-SOR-PRVT-BRND                              02280001
               AND PS-END-OF-XM-PRVT-BRND.                              02290001
                                                                        02300000
           IF PV-ERROR-RECORD-WRITTEN-CNT = 0                           02310000
               MOVE PC-NO-ERROR-MESSAGE TO DL1-DETAIL-LINE-1            02320000
               MOVE DL1-DETAIL-LINE-1   TO PV-PRINT-RECORD              02330000
               MOVE +2                  TO PV-LINE-SPACING              02340000
               PERFORM 8900-WRITE-REPORT-LINE                           02350000
           ELSE                                                         02360000
               MOVE PV-ERROR-RECORD-WRITTEN-CNT TO                      02370000
                      TR4-RECORD-ERR-CNT                                02380000
               MOVE TR4-TRAILER-LINE-4  TO PV-PRINT-RECORD              02390000
               MOVE +2                  TO PV-LINE-SPACING              02400000
               PERFORM 8900-WRITE-REPORT-LINE                           02410000
                                                                        02420000
               MOVE PV-XM-EXTRACT-REC-UNIQUE   TO                       02430000
                      TR2-SOR-MISSING-CNT                               02440000
               MOVE TR2-TRAILER-LINE-2  TO PV-PRINT-RECORD              02450000
               MOVE +1                  TO PV-LINE-SPACING              02460000
               PERFORM 8900-WRITE-REPORT-LINE                           02470000
                                                                        02480000
               MOVE PV-SOR-PRVT-BRND-RECS-UNIQUE TO                     02490001
                      TR3-XM-MISSING-CNT                                02500000
               MOVE TR3-TRAILER-LINE-3  TO PV-PRINT-RECORD              02510000
               PERFORM 8900-WRITE-REPORT-LINE                           02520000
                                                                        02530000
               MOVE PV-TOTAL-ERROR-CNT          TO                      02540000
                      TR1-TOTAL-ERROR-CNT                               02550000
               MOVE TR1-TRAILER-LINE-1  TO PV-PRINT-RECORD              02560000
               PERFORM 8900-WRITE-REPORT-LINE                           02570000
                                                                        02580000
           END-IF.                                                      02590000
                                                                        02600000
           PERFORM 9000-END-OF-PGM.                                     02610000
                                                                        02620000
           IF  PV-ERROR-RECORD-WRITTEN-CNT > 0                          02621001
               MOVE 4095 TO RETURN-CODE                                 02622001
           END-IF.                                                      02623001
                                                                        02624001
           GOBACK.                                                      02630000
                                                                        02640000
      ******************************************************************02650000
      *1000-INITIALIZATION.                                             02660000
      * INITIALIZATION AREA                                             02670000
      ******************************************************************02680000
       1000-INITIALIZATION.                                             02690000
                                                                        02700000
           INITIALIZE PV-PROGRAM-COUNTER                                02710000
                      HMRD014-PRVT-BRND-RECORD                          02720003
                      XM015-PRVT-BRND-UNLOAD.                           02730003
                                                                        02740000
           OPEN INPUT  SOR-PRVT-BRND-EXTRACT-FILE                       02750001
                       XM-PRVT-BRND-EXTRACT-FILE.                       02760001
                                                                        02770000
           OPEN OUTPUT XM-PRVT-BRND-INTEG-RPT-FILE.                     02780001
                                                                        02790000
           PERFORM 7000-READ-SOR-PRVT-BRND-EXTRCT.                      02800001
           PERFORM 7100-READ-XM-PRVT-BRND-EXTRACT.                      02810001
                                                                        02820000
           ACCEPT PV-CURR-TIME FROM TIME.                               02830000
           MOVE PV-CURR-TIME-HH          TO DP132-RUN-HOUR.             02840001
           MOVE PV-CURR-TIME-MM          TO DP132-RUN-MINUTE.           02850001
                                                                        02860000
           MOVE FUNCTION CURRENT-DATE (1:8)                             02870000
                                         TO PV-CURR-DATE-CCYYMMDD.      02880000
                                                                        02890000
           MOVE PV-CURR-DATE-MM          TO DP132-RUN-MONTH.            02900001
           MOVE PV-CURR-DATE-DD          TO DP132-RUN-DAY.              02910001
           MOVE PV-CURR-DATE-CCYY        TO DP132-RUN-YEAR.             02920001
                                                                        02930000
           MOVE PC-CURRENT-PROGRAM-NAME  TO DP132-PROGRAM-NAME.         02940001
                                                                        02950000
           PERFORM 8700-REPORT-HEADER-ROUTINE.                          02960000
                                                                        02970000
      ******************************************************************02980000
      *2000-PROCESS-RECORDS.                                            02990000
      * THIS SECTION COMPARES RECORDS ON THE SOR PRICE CHANGE FILE &    03000000
      * XM DOMAIN PRCHG EXTRACT FILE. IF KEYS MATCH, PROGRAM LOOKS AT   03010000
      * DETAIL FIELDS AND CHECKS THAT THEY MATCH, DIFFERENCES ARE       03020000
      * REPORTED. IF THE KEY (PRCHG ID) DOES NOT APPEAR ON ONE FILE,    03030000
      * BUT IS ON THE OTHER FILE THEN AN APPROPRIATE MESSAGE WILL BE    03040000
      * WRITTEN TO THE ERROR FILE, ALONG WITH THE DATA THAT WAS ON      03050000
      * THE CORRESPONDING FILES.                                        03060000
      ******************************************************************03070000
       2000-PROCESS-RECORDS.                                            03080000
                                                                        03090000
           INITIALIZE DL1-DETAIL-LINE-1.                                03100000
                                                                        03110000
           EVALUATE TRUE                                                03160000
              WHEN PV-HMKEY-HM014 = PV-XMKEY-XM015                      03170003
                   ADD +1 TO PV-SOR-XM-COMMON-CNT                       03180000
                   PERFORM 2100-COMPARE-FIELDS                          03190000
                   PERFORM 7000-READ-SOR-PRVT-BRND-EXTRCT               03200004
                   PERFORM 7100-READ-XM-PRVT-BRND-EXTRACT               03210001
              WHEN PV-HMKEY-HM014 < PV-XMKEY-XM015                      03220003
                   PERFORM 2400-MOVE-SOR-VALUES                         03270000
                   MOVE PC-MISSING-FROM-XM-PRVT-BRND                    03280001
                      TO DL1-ERROR-MESSAGE                              03290000
                   MOVE DL1-DETAIL-LINE-1 TO PV-PRINT-RECORD            03300000
                   ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                03310000
                   ADD +1 TO PV-SOR-PRVT-BRND-RECS-UNIQUE               03320001
                   ADD +1 TO PV-TOTAL-ERROR-CNT                         03330000
                   PERFORM 8900-WRITE-REPORT-LINE                       03340000
                   PERFORM 7000-READ-SOR-PRVT-BRND-EXTRCT               03350001
              WHEN PV-HMKEY-HM014 > PV-XMKEY-XM015                      03360003
                   PERFORM 2500-MOVE-XM-VALUES                          03370000
                   MOVE PC-MISSING-FROM-SOR-PRVT-BRND                   03380001
                      TO DL1-ERROR-MESSAGE                              03390000
                   MOVE DL1-DETAIL-LINE-1 TO PV-PRINT-RECORD            03400000
                   ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                03410000
                   ADD +1 TO PV-XM-EXTRACT-REC-UNIQUE                   03420000
                   ADD +1 TO PV-TOTAL-ERROR-CNT                         03430000
                   PERFORM 8900-WRITE-REPORT-LINE                       03440000
                   PERFORM 7100-READ-XM-PRVT-BRND-EXTRACT               03450001
           END-EVALUATE.                                                03460000
                                                                        03470000
      ******************************************************************03480000
      *2100-COMPARE-FIELDS.                                             03490000
      * COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   03500000
      ******************************************************************03510000
       2100-COMPARE-FIELDS.                                             03520000
                                                                        03530000
           SET PS-NO-ERROR-FOUND                   TO TRUE.             03540000
                                                                        03550000
           INITIALIZE DL1-DETAIL-LINE-1.                                03560000
                                                                        03570000
           IF HM014-PRVT-BRND-EFF-END-DTE EQUAL PC-DEFAULT-DATE         03760003
               MOVE SPACES TO HM014-PRVT-BRND-EFF-END-DTE               03770003
           END-IF.                                                      03780000
                                                                        03790000
           IF XM015-EFF-END-DTE-IS-NULL                                 03791003
               MOVE SPACES TO XM015-EFF-END-DTE                         03792003
           END-IF.                                                      03793003
                                                                        03794003
           IF HM014-PRVT-BRND-EFF-END-DTE NOT EQUAL XM015-EFF-END-DTE   03800003
               SET PS-ERROR-FOUND  TO TRUE                              03801003
               ADD +1                           TO PV-TOTAL-ERROR-CNT   03802003
               MOVE HM014-PRVT-BRND-DEPT-NBR    TO DL1-DEPT-NBR         03803003
               MOVE HM014-PRVT-BRND-EFF-BEG-DTE TO DL1-EFF-BEG-DTE      03804003
               MOVE HM014-PRVT-BRND-EFF-END-DTE TO DL1-SOR-VALUE        03804103
               MOVE XM015-EFF-END-DTE           TO DL1-XM-VALUE         03805003
               MOVE 'EFF_END_DTE              ' TO DL1-ERROR-FIELD      03806003
               MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG        03807003
               MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD      03808003
               PERFORM 8900-WRITE-REPORT-LINE                           03809003
               INITIALIZE DL1-DETAIL-LINE-1                             03809103
           END-IF.                                                      03809203
                                                                        03970000
           MOVE XM015-LOD-PCT                   TO PV-XM015-LOD-PCT.    03971004
           IF HM014-PRVT-BRND-LOD-PCT NOT EQUAL PV-XM015-LOD-PCT        03980005
               SET PS-ERROR-FOUND  TO TRUE                              03990000
               ADD +1                           TO PV-TOTAL-ERROR-CNT   04000000
               MOVE HM014-PRVT-BRND-DEPT-NBR    TO DL1-DEPT-NBR         04001003
               MOVE HM014-PRVT-BRND-EFF-BEG-DTE TO DL1-EFF-BEG-DTE      04002003
               MOVE HM014-PRVT-BRND-LOD-PCT     TO DL1-SOR-VALUE        04003003
               MOVE PV-XM015-LOD-PCT            TO DL1-XM-VALUE         04004005
               MOVE 'LOD_PCT                  ' TO DL1-ERROR-FIELD      04040003
               MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG        04050000
               MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD      04060000
               PERFORM 8900-WRITE-REPORT-LINE                           04070000
               INITIALIZE DL1-DETAIL-LINE-1                             04080000
           END-IF.                                                      04090000
                                                                        04100000
           IF  PS-ERROR-FOUND                                           04290000
               ADD +1 TO PV-RECORD-ERR-CNT                              04300000
               ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                    04310000
           ELSE                                                         04320000
               ADD +1 TO PV-VALID-MATCH-CNT                             04330000
           END-IF.                                                      04340000
                                                                        04350000
      ******************************************************************04360000
      **  PROCESSES THRU END OF FILE FOR REMAINING RECORDS              04370000
      ******************************************************************04380000
       2200-PROCESS-EXTRA-RECORDS.                                      04390000
                                                                        04400000
           INITIALIZE DL1-DETAIL-LINE-1.                                04410000
                                                                        04420000
           IF  PS-NOT-END-OF-SOR-PRVT-BRND                              04430001
               IF PV-HMKEY-HM014 NOT = PV-XMKEY-XM015                   04440003
                  PERFORM 2400-MOVE-SOR-VALUES                          04450000
                  MOVE PC-EXTRA-FOUND-SOR-PRVT-BRND                     04460001
                     TO DL1-ERROR-MESSAGE                               04470000
                  MOVE DL1-DETAIL-LINE-1   TO PV-PRINT-RECORD           04480000
                  ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                 04490000
                  ADD +1 TO PV-SOR-PRVT-BRND-RECS-UNIQUE                04500001
                  ADD +1 TO PV-TOTAL-ERROR-CNT                          04510000
                  PERFORM 8900-WRITE-REPORT-LINE                        04520000
               END-IF                                                   04530000
               PERFORM 7000-READ-SOR-PRVT-BRND-EXTRCT                   04540001
           END-IF.                                                      04550000
                                                                        04560000
           IF  PS-NOT-END-OF-XM-PRVT-BRND                               04570001
               IF PV-HMKEY-HM014 NOT = PV-XMKEY-XM015                   04571003
                  PERFORM 2500-MOVE-XM-VALUES                           04590000
                  MOVE PC-EXTRA-FOUND-XM-PRVT-BRND                      04600001
                      TO DL1-ERROR-MESSAGE                              04610000
                  MOVE DL1-DETAIL-LINE-1   TO PV-PRINT-RECORD           04620000
                  ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                 04630000
                  ADD +1 TO PV-XM-EXTRACT-REC-UNIQUE                    04640000
                  ADD +1 TO PV-TOTAL-ERROR-CNT                          04650000
                  PERFORM 8900-WRITE-REPORT-LINE                        04660000
               END-IF                                                   04670000
               PERFORM 7100-READ-XM-PRVT-BRND-EXTRACT                   04680001
           END-IF.                                                      04690000
                                                                        04700000
      ******************************************************************04710000
      * 2300-CHECK-IF-NEW-PG-NEEDED.                                    04720000
      *     CHECKS TO SEE IF A NEW PAGE IS NEEDED.                      04730000
      ***************************************************************** 04740000
       2300-CHECK-IF-NEW-PG-NEEDED.                                     04750000
                                                                        04760000
           IF PV-LINE-CNT > PC-MAX-REPORT-LINES                         04770000
              MOVE PV-PRINT-RECORD       TO  PV-SAVE-PRINT-RECORD       04780000
              PERFORM 8700-REPORT-HEADER-ROUTINE                        04790000
              MOVE PV-SAVE-PRINT-RECORD  TO PV-PRINT-RECORD             04800000
           END-IF.                                                      04810000
                                                                        04820000
      ******************************************************************04830000
      * 2400-MOVE-SOR-VALUES.                                           04840000
      *     MOVES THE SOR ITEM PRCHG VALUES TO THE ERROR RECORD.        04850000
      ***************************************************************** 04860000
       2400-MOVE-SOR-VALUES.                                            04870000
                                                                        04880000
           MOVE HM014-PRVT-BRND-DEPT-NBR                                04890003
                                   TO DL1-DEPT-NBR.                     04900003
           MOVE HM014-PRVT-BRND-EFF-BEG-DTE                             04901003
                                   TO DL1-EFF-BEG-DTE.                  04902003
                                                                        04910000
      ******************************************************************04920000
      * 2500-MOVE-XM-VALUES.                                            04930000
      *     MOVES THE XM PRCHG VALUES TO THE ERROR RECORD.              04940000
      ***************************************************************** 04950000
       2500-MOVE-XM-VALUES.                                             04960000
                                                                        04970000
           MOVE XM015-DEPT-NBR     TO DL1-DEPT-NBR.                     04980003
           MOVE XM015-EFF-BEG-DTE  TO DL1-EFF-BEG-DTE.                  04990003
                                                                        05000000
      ***************************************************************** 05010000
      *7000-READ-SOR-PRVT-BRND-EXTRCT.                                  05020001
      *  READ SOR PRIVATE BRAND EXTRACT FILE                            05030001
      ***************************************************************** 05040000
       7000-READ-SOR-PRVT-BRND-EXTRCT.                                  05050001
                                                                        05060000
           IF  PS-NOT-END-OF-SOR-PRVT-BRND                              05070001
               READ SOR-PRVT-BRND-EXTRACT-FILE                          05080001
                  INTO HMRD014-PRVT-BRND-RECORD                         05090003
                    AT END                                              05100000
                       SET PS-END-OF-SOR-PRVT-BRND TO TRUE              05110001
                       MOVE HIGH-VALUES TO PV-HMKEY-HM014               05120003
                    NOT AT END                                          05130000
                       ADD +1 TO PV-SOR-PRVT-BRND-RECS-READ-CNT         05140001
                       MOVE HM014-PRVT-BRND-DEPT-NBR                    05141003
                                            TO PV-HMKEY-DEPT-NBR        05141103
                       MOVE HM014-PRVT-BRND-EFF-BEG-DTE                 05142003
                                            TO PV-HMKEY-EFF-BEG-DTE     05143003
               END-READ                                                 05150000
           END-IF.                                                      05160000
                                                                        05170000
      ***************************************************************** 05180000
      *7100-READ-XM-PRVT-BRND-EXTRACT.                                  05190001
      * READ XM EXTRACT FILE                                            05200000
      ***************************************************************** 05210000
       7100-READ-XM-PRVT-BRND-EXTRACT.                                  05220001
                                                                        05230000
           IF  PS-NOT-END-OF-XM-PRVT-BRND                               05240001
               READ XM-PRVT-BRND-EXTRACT-FILE                           05250001
                 INTO XM015-PRVT-BRND-UNLOAD                            05260003
                   AT END                                               05270000
                       SET PS-END-OF-XM-PRVT-BRND TO TRUE               05280001
                       MOVE HIGH-VALUES TO PV-XMKEY-XM015               05281003
                   NOT AT END                                           05300000
                       ADD +1 TO PV-XM-EXTRACT-REC-READ-CNT             05310000
                       MOVE XM015-DEPT-NBR  TO PV-XMKEY-DEPT-NBR        05312003
                       MOVE XM015-EFF-BEG-DTE                           05313003
                                            TO PV-XMKEY-EFF-BEG-DTE     05314003
               END-READ                                                 05320000
           END-IF.                                                      05330000
                                                                        05340000
      ******************************************************************05350000
      * 8700-REPORT-HEADER-ROUTINE.                                     05360000
      *     PRINT THE HEADINGS FOR THE REPORT.                          05370000
      ******************************************************************05380000
       8700-REPORT-HEADER-ROUTINE.                                      05390000
                                                                        05400000
           MOVE ZEROS                  TO PV-LINE-CNT.                  05410000
           ADD +1                      TO PV-PAGE-CNT.                  05420000
           MOVE PV-PAGE-CNT            TO DP132-PAGE-NUMBER.            05430001
                                                                        05440000
           MOVE PC-REPORT-NBR-1        TO DP132-REPORT-NUMBER.          05450001
           MOVE DP132-STANDRD-HEADER-132-COLS                           05460004
                                       TO PV-PRINT-RECORD.              05470000
                                                                        05480000
           PERFORM 8800-WRITE-RPT-TOP-OF-PAGE.                          05490000
                                                                        05500000
           MOVE HL2-HEADING-LINE-2     TO PV-PRINT-RECORD.              05510000
           MOVE +2                     TO PV-LINE-SPACING.              05520000
           PERFORM 8900-WRITE-REPORT-LINE.                              05530000
                                                                        05540000
           MOVE HL3-HEADING-LINE-3     TO PV-PRINT-RECORD.              05550000
           MOVE +3                     TO PV-LINE-SPACING.              05560000
           PERFORM 8900-WRITE-REPORT-LINE.                              05570000
                                                                        05580000
           MOVE HL4-HEADING-LINE-4     TO PV-PRINT-RECORD.              05590000
           MOVE +1                     TO PV-LINE-SPACING.              05600000
           PERFORM 8900-WRITE-REPORT-LINE.                              05610000
                                                                        05620000
      ******************************************************************05630000
      * 8800-WRITE-RPT-TOP-OF-PAGE                                      05640000
      *     WRITE FIRST REPORT LINE AFTER PAGE BREAK.                   05650000
      ******************************************************************05660000
       8800-WRITE-RPT-TOP-OF-PAGE.                                      05670000
                                                                        05680000
           WRITE XM-PRVT-BRND-INTEG-RPT-RECORD                          05690001
             FROM PV-PRINT-RECORD                                       05700000
               AFTER ADVANCING PAGE.                                    05710000
                                                                        05720000
           MOVE +1 TO PV-LINE-CNT.                                      05730000
                                                                        05740000
      ******************************************************************05750000
      * 8900-WRITE-REPORT-LINE                                          05760000
      *     WRITE REPORT LINE AFTER ADVANCING LINE SPACING.             05770000
      ******************************************************************05780000
       8900-WRITE-REPORT-LINE.                                          05790000
                                                                        05800000
           PERFORM 2300-CHECK-IF-NEW-PG-NEEDED.                         05810000
                                                                        05820000
           WRITE XM-PRVT-BRND-INTEG-RPT-RECORD                          05830001
             FROM PV-PRINT-RECORD                                       05840000
               AFTER ADVANCING PV-LINE-SPACING.                         05850000
                                                                        05860000
           ADD PV-LINE-SPACING TO PV-LINE-CNT.                          05870000
                                                                        05880000
      ****************************************************************  05890000
      *END OF PROGRAM ROUTINE                                           05900000
      ****************************************************************  05910000
       9000-END-OF-PGM.                                                 05920000
                                                                        05930000
           DISPLAY '******** XMKRP015 COUNTS *********'.                05940000
           DISPLAY 'INTEGRITY CHECK - FOR XMT_PRVT_BRND_FCTR'.          05950001
           DISPLAY 'SOR K_EMT_PRVT_BRND_COST_FCTR REC READ CNT= '       05960001
                           PV-SOR-PRVT-BRND-RECS-READ-CNT.              05970001
           DISPLAY 'XMT_PRVT_BRND_FCTR READ COUNT= '                    05980001
                 PV-XM-EXTRACT-REC-READ-CNT.                            05981001
           DISPLAY '=============================== '                   05990001
           DISPLAY 'UNIQUE TO SOR ONLY  - ',                            06000001
                 PV-SOR-PRVT-BRND-RECS-UNIQUE                           06001001
           DISPLAY 'UNIQUE TO XM  ONLY  - ', PV-XM-EXTRACT-REC-UNIQUE   06010000
           DISPLAY 'COMMON TO BOTH FILES- ',  PV-SOR-XM-COMMON-CNT      06020000
           COMPUTE PV-SOR-XM-TOTAL-CNT =  PV-SOR-XM-COMMON-CNT          06030000
                 + PV-SOR-PRVT-BRND-RECS-UNIQUE                         06040001
                 + PV-XM-EXTRACT-REC-UNIQUE                             06041001
           DISPLAY 'TOTAL COMMON + UNIQUE=', PV-SOR-XM-TOTAL-CNT        06050000
           DISPLAY '=============================== '                   06060001
           DISPLAY ' ERROR MSGS WRITTEN = ' PV-TOTAL-ERROR-CNT.         06070001
           DISPLAY ' TOTAL RECORDS ERROR= ' PV-ERROR-RECORD-WRITTEN-CNT.06080000
           DISPLAY ' VALID MATCH COUNT  = ' PV-VALID-MATCH-CNT.         06090001
           DISPLAY '                              '.                    06100000
           DISPLAY '**********************************'.                06110000
                                                                        06120000
           CLOSE SOR-PRVT-BRND-EXTRACT-FILE                             06170004
                 XM-PRVT-BRND-EXTRACT-FILE                              06180001
                 XM-PRVT-BRND-INTEG-RPT-FILE.                           06190001
                                                                        06200000
