      ******************************************************************00010068
      *  BVPD100 - BRAND VENDOR BEGINNING OF MONTH LOOKUP MODULE.       00020068
      *                                                                 00030068
      *-----------------------------------------------------------------00040068
      * CHG ID/                                                        *00050068
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00060068
      * -------  ----------  ----------------------------------------  *00070068
      * WR34517  2008-05-12  J SELWITSCHKA                             *00080068
      *                      INITIAL IMPLEMENTATION                    *00090068
RONNA *          2013-05-09  RONNA MCDONALD                            *00091068
RONNA *                      COMMENT OUT DISPLAY TO IMPROVE PERFORMANCE*00092068
      *-----------------------------------------------------------------00093068
      *PURPOSE: BV BEGINNING OF MONTH MODULE COPY CODE.                 00094068
      *         RETRIEVES BEGINNING OF MONTH BRAND AND VENDOR FOR A SKU.00095068
      *                                                                 00096068
      *NOTE:    REFER TO MODULE BVWS100 FOR PROCESSING OPTIONS.         00097068
      *         THE INPUT FIELDS WILL BE INITIALIZED AFTER EACH         00098068
      *         PERFORM OF THE MODULE.                                  00099068
      *                                                                 00100068
      *         THIS MODULE USES THE BRAND VENDOR RELATIONSHIP UTILITY  00110068
      *         (BVPD001) TO GET THE BRAND VENDOR RELATIONSHIP. ANY     00120068
      *         PROGRAM THAT USES THIS MODULE MUST ALSO USE COPYBOOKS   00130068
      *         BVPD001 AND BVWS001.                                    00131068
      *                                                                 00132068
      *-----------------------------------------------------------------00133068
      *  HOW TO USE THIS COPYBOOK WITH BVWS100:                         00134068
      *-----------------------------------------------------------------00135068
      *  1.  FILL THE INPUT FIELDS WITH THE DATA REQUIRED TO RETRIEVE   00136068
      *                                                                 00137068
      *      THE BEGINNING OF MONTH DATA:                               00138068
      *      IF DOING LOOKUP USING SKU:                                 00138168
      *      - SET BV101-PV-SKU-LOOKUP TO TRUE.                         00138268
      *        FILL BV101-PV-SKU-IN. (REQUIRED FOR SKU LOOKUP)          00138368
      *      IF DOING LOOKUP USING ITM-NBR:                             00138468
      *      - SET BV101-PV-ITM-LOOKUP TO TRUE.                         00138568
      *        FILL BV101-PV-ITM-NBR-IN. (REQUIRED FOR ITM LOOKUP)      00138668
      *      EFFECTIVE DATE (USED TO GET THE BEGINNING-OF-MONTH FISCAL  00138768
      *      DATE):                                                     00138868
      *      - BV101-PV-EFF-DTE-IN (IF NOT FILLED, CURRENT DATE WILL BE 00138968
      *        USED).                                                   00139068
      *                                                                 00139168
      *  2.  BEGINNING OF MONTH LOOKUP.                                 00139268
      *      A.  INPUT(S) BUILD.                                        00139368
      *      - IF DATE IS NOT PROVIDED, GET THE CURRENT DATE.           00139468
      *      - USE THE DATE PASSED OR THE CURRENT DATE TO OBTAIN THE    00139568
      *        BEGINNING OF MONTH DATE FROM THE DATE ATTRIBUTE TABLE    00139668
      *        (TDTATTR).                                               00139768
      *      - FOR SKU LOOKUP, USE SKU-NBR AND BEGINNING OF MONTH DATE  00139868
      *        TO GET BRAND AND VENDOR FROM THE BOM TABLE               00139968
      *        (XMT_BOM_SKU_ATTR).                                      00140068
      *      - FOR ITM LOOKUP, USE ITM-NBR AND BEGINNING OF MONTH DATE  00140168
      *        TO GET BRAND AND VENDOR FROM THE BOM TABLE.              00140268
      *        (XMT_BOM_SKU_ATTR).                                      00140368
      *                                                                 00140468
      *      - ENTRY PROCEDURE: BV101-LOOKUP-BOM                        00140568
      *                                                                 00140668
      *      B.  RESULTS.                                               00140768
      *      - PRICE LOOKUP RETURN CODE VARIABLES ARE DEFINED HERE.     00140868
      *        (BV101-PV-COMMON-AREA)                                   00140968
      *         - RETURN STATUS CODE                                    00141068
      *         - RETURN STATUS CODE REASON TEXT                        00142068
      *                                                                 00143068
      *      - ON A SUCCESSFUL EXECUTION THE FOLLOWING DATA WILL BE     00144068
      *        MOVED TO THE FOLLOWING WORKING STORAGE FIELDS            00145068
      *        (BV101-BOM-RESULT-AREA)                                  00146068
      *         - ENT ID (VENDOR)           BV101-BOM-ENT-ID-OUT        00147068
      *         - LBL SEQ NBR (BRAND)       BV101-BOM-LBL-SEQ-NBR-OUT   00148068
      *         - ML LO MDSE LVL ID         BV101-BOM-LO-MDSE-LVL-ID-OUT00149068
      *           (BV RELATIONSHIP ID OR DKEY)                          00150068
      *         - DEPT-NBR                  BV101-BOM-DEPT-NBR          00160068
      *         - MAJ CLASS                 BV101-BOM-MAJ-CL-OUT        00170068
      *         - SUB CLASS                 BV101-BOM-SUB-CL-OUT        00180068
      *                                                                 00190068
      *      - THE BEGINNING-OF-MONTH DATE WILL BE RETURNED IN A        00200068
      *        SEPARATE AREA (ASSIGNED ONCE ON THE FIRST PERFORM OF     00210068
      *        THE MODULE)                                              00220068
      *         - BEGINNING OF MONTH DATE   BV101-BOM-FSCL-DTE-OUT      00230068
      *                                                                 00240068
      *-----------------------------------------------------------------00250068
      *                                                                 00260068
      *  3.  ERROR HANDLING PASSED TO THE SOURCE MODULE.                00270068
      *                                                                 00280068
      ******************************************************************00290068
                                                                        00300068
      ******************************************************************00310068
      ******************************************************************00320068
      ******************************************************************00330068
      * BV101-LOOKUP-BOM. CHECK THE BEGINNING OF MONTH ATTRIBUTES TABLE 00340068
      *    FOR THE GIVEN SKU OR ITM, AND BOM-FSCL-DTE.                  00350068
      *  - INITIALIZE OUTPUT VALUES BEFORE EXECUTING                    00360068
      *    (EXCEPT BOM-FSCL-DATE). THE BOM-FSCL-DTE WILL BE ASSIGNED    00370068
      *    ON THE FIRST PERFORM OF THE MODULE.                          00380068
      *  - INITIALIZE ALL INPUT VALUES BEFORE RETURNING TO MAIN MODULE  00390068
      ******************************************************************00400068
       BV101-LOOKUP-BOM.                                                00410068
                                                                        00420068
      *    DISPLAY 'BV101 LOOKUP BOM'                                   00430068
           INITIALIZE BV101-PV-COMMON-AREA.                             00440068
           INITIALIZE BV101-BOM-RESULT-AREA.                            00450068
           INITIALIZE BV101-PV-WORK-AREA.                               00460068
           SET BV101-PV-BOM-NOT-FOUND       TO TRUE.                    00470068
           SET BV101-PV-BOM-NOT-FOUND-VALID TO TRUE.                    00480068
                                                                        00490068
           MOVE ZEROES         TO SQLCODE.                              00500068
                                                                        00510068
           IF BV101-PV-SKU-LOOKUP OR BV101-PV-ITM-LOOKUP                00520068
               CONTINUE                                                 00530068
           ELSE                                                         00540068
               SET BV101-PV-BOM-ERROR           TO TRUE                 00550068
               SET BV101-PV-INVALID-LOOKUP-TYPE TO TRUE                 00560068
               DISPLAY 'BV101 INVALID OR MISSING LOOKUP TYPE'           00570068
           END-IF.                                                      00580068
                                                                        00590068
           IF BV101-PV-SKU-LOOKUP                                       00600068
               IF BV101-PV-SKU-IN NOT NUMERIC                           00610068
                   SET BV101-PV-BOM-ERROR       TO TRUE                 00620068
                   SET BV101-PV-INVALID-SKU TO TRUE                     00630068
                   DISPLAY 'BV101 INVALID SKU'                          00640068
               END-IF                                                   00650068
           END-IF.                                                      00660068
                                                                        00670068
           IF BV101-PV-ITM-LOOKUP                                       00680068
               IF BV101-PV-ITM-NBR-IN NOT NUMERIC                       00690068
                   SET BV101-PV-BOM-ERROR       TO TRUE                 00700068
                   SET BV101-PV-INVALID-ITM TO TRUE                     00710068
                   DISPLAY 'BV101 INVALID ITM'                          00720068
               END-IF                                                   00730068
           END-IF.                                                      00740068
                                                                        00750068
           IF NOT BV101-PV-INVALID-KEY-INPUT                            00760068
               IF BV101-PS-NEW-BOM-DTE-YES                              00770068
                   PERFORM BV101-SET-DATE                               00780068
                   PERFORM BV101-GET-BOM-DATE                           00790068
RONNA*****         DISPLAY 'BV101 BOM DATE: ', BV101-BOM-FSCL-DTE-OUT   00800068
                   SET BV101-PS-NEW-BOM-DTE-NO TO TRUE                  00810068
               END-IF                                                   00820068
                                                                        00830068
               IF BV101-PV-INVALID-DTE                                  00840068
                   CONTINUE                                             00850068
               ELSE                                                     00860068
                   PERFORM                                              00870068
                       WITH TEST AFTER                                  00880068
                       VARYING BV101-PV-SQL-SUB FROM 1 BY 1             00890068
                       UNTIL (BV101-PV-SQL-SUB > BV101-PV-MAX-RETRIES)  00900068
                           OR (SQLCODE = 0)                             00910068
                           OR (SQLCODE = +100)                          00920068
                       IF BV101-PV-SKU-LOOKUP                           00930068
                           MOVE BV101-PV-SKU-IN TO BV101-PV-WORK-SKU    00940068
                           EXEC SQL                                     00950068
                           SELECT DEPT_NBR                              00960068
                                 ,ENT_ID                                00970068
                                 ,LBL_SEQ_NBR                           00980068
                             INTO :XMT00023-DEPT-NBR                    00990068
                                 ,:XMT00023-ENT-ID                      01000068
                                 ,:XMT00023-LBL-SEQ-NBR                 01010068
                             FROM XMT_BOM_SKU_ATTR                      01020068
                            WHERE SKU_NBR       = :BV101-PV-WORK-SKU    01030068
                              AND FSCL_BOM_DATE =                       01040068
                                                 :XMT00023-FSCL-BOM-DATE01050068
                           END-EXEC                                     01060068
                       ELSE                                             01070068
                           MOVE BV101-PV-ITM-NBR-IN TO XMT00023-ITM-NBR 01080068
                           EXEC SQL                                     01090068
                           SELECT DEPT_NBR                              01100068
                                 ,ENT_ID                                01110068
                                 ,LBL_SEQ_NBR                           01120068
                             INTO :XMT00023-DEPT-NBR                    01130068
                                 ,:XMT00023-ENT-ID                      01140068
                                 ,:XMT00023-LBL-SEQ-NBR                 01150068
                             FROM XMT_BOM_SKU_ATTR                      01160068
                            WHERE ITM_NBR       = :XMT00023-ITM-NBR     01170068
                              AND FSCL_BOM_DATE =                       01180068
                                                 :XMT00023-FSCL-BOM-DATE01190068
                           END-EXEC                                     01200068
                       END-IF                                           01210068
                   END-PERFORM                                          01220068
                                                                        01230068
                   EVALUATE TRUE                                        01240068
                       WHEN SQLCODE = 0                                 01250068
                           SET BV101-PV-BOM-FOUND    TO TRUE            01260068
                           SET BV101-PS-BOM-FOUND    TO TRUE            01270068
                           SET BV101-PV-BOM-FOUND-VALID TO TRUE         01280068
      *    GET THE BV RELATIONSHIP ID (DKEY) FOR THE DEPT, VENDOR       01290068
      *    (ENT-ID) AND BRAND (LBL-SEQ-NBR)                             01300068
                           INITIALIZE BV003-PV-INPUT-AREA               01310068
                           MOVE XMT00023-ENT-ID                         01320068
                                            TO BV101-BOM-ENT-ID-OUT     01330068
                                               BV003-PV-ENT-ID-IN-9     01340068
                           MOVE XMT00023-LBL-SEQ-NBR                    01350068
                                            TO BV101-BOM-LBL-SEQ-NBR-OUT01360068
                                               BV003-PV-BRAND-IN-9      01370068
                           MOVE BV101-PV-DEPT-IN-9                      01380068
                                            TO BV101-BOM-DEPT-NBR-OUT   01390068
                                               BV003-PV-DEPT-IN-9       01400068
                           MOVE BV101-BOM-FSCL-DTE-OUT                  01410068
                                            TO BV003-PV-EFF-DTE-IN      01420068
                           SET BV003-PV-BRND-VND-DPT-LOOKUP TO TRUE     01430068
                           PERFORM BV003-LOOKUP-MAIN                    01440068
                           IF BV003-UNKNOWN-INS                         01450068
                              PERFORM BV101-COMMIT                      01460068
                           END-IF                                       01470068
                           MOVE BV003-PV-RLTNSHP-ID-OUT                 01480068
                             TO BV101-BOM-LO-MDSE-LVL-ID-OUT            01490068
                       WHEN SQLCODE = +100                              01500068
                           SET BV101-PS-BOM-NOT-FOUND TO TRUE           01510068
                           SET BV101-PV-BOM-NOT-FOUND-VALID TO TRUE     01520068
                       WHEN SQLCODE = -904                              01530068
                       WHEN SQLCODE = -913                              01540068
                           CONTINUE                                     01550068
                       WHEN OTHER                                       01560068
                           SET BV101-PV-BOM-SQL-ERROR       TO TRUE     01570068
                           SET BV101-PV-UNDEFINED-SQL-ERROR TO TRUE     01580068
                           MOVE SQLCODE     TO BV101-PV-BOM-SQLCDE      01590068
                           STRING 'PARA: BV101-LOOKUP-BOM - BOM ERROR'  01600068
                                                     DELIMITED BY SIZE  01610068
                                  BV101-PV-SKU-IN DELIMITED BY SIZE     01620068
                                  '  ' DELIMITED BY SIZE                01630068
                                            INTO BV101-PV-PARAGRAPH     01640068
                   END-EVALUATE                                         01650068
               END-IF                                                   01660068
           END-IF.                                                      01670068
                                                                        01680068
           IF BV101-PV-SQL-SUB > BV101-PV-MAX-RETRIES                   01690068
               SET BV101-PV-BOM-SQL-ERROR       TO TRUE                 01700068
               SET BV101-PV-DEADLOCK            TO TRUE                 01710068
               DISPLAY 'MAX RETRIES EXCEEDED FOR BOM TABLE'             01720068
               MOVE SQLCODE     TO BV101-PV-BOM-SQLCDE                  01730068
               STRING 'PARA: BV101-LOOKUP-BOM - BOM ERROR'              01740068
                                                     DELIMITED BY SIZE  01750068
                                     BV101-PV-SKU-IN DELIMITED BY SIZE  01760068
                                                '  ' DELIMITED BY SIZE  01770068
                                            INTO BV101-PV-PARAGRAPH     01780068
           END-IF.                                                      01790068
                                                                        01800068
      ******************************************************************01810068
      * CHECK THAT THE DATE FIELD MEETS INITIAL REQUIREMENTS            01820068
      * IF NO DATE IS PROVIDED, SET TO CURRENT DATE.                    01830068
      ******************************************************************01840068
       BV101-SET-DATE.                                                  01850068
                                                                        01860068
           IF BV101-PV-EFF-DTE-IN = SPACES OR                           01870068
              BV101-PV-EFF-DTE-IN = ZEROES                              01880068
               EXEC SQL                                                 01890068
                  SELECT CURRENT DATE                                   01900068
                  INTO :BV101-PV-EFF-DTE-IN                             01910068
                  FROM SYSIBM.SYSDUMMY1                                 01920068
                  WITH UR                                               01930068
               END-EXEC                                                 01940068
                                                                        01950068
               EVALUATE TRUE                                            01960068
                 WHEN SQLCODE = 0                                       01970068
                     MOVE BV101-PV-EFF-DTE-IN                           01980068
                                   TO BV101-PV-EFF-BEG-DTE-IN           01990068
                                      BV101-PV-EFF-BEG-DTE              02000068
                     MOVE SQLCODE     TO BV101-PV-BOM-SQLCDE            02010068
                     MOVE SPACES      TO BV101-PV-PARAGRAPH             02020068
                 WHEN OTHER                                             02030068
                     SET BV101-PV-INVALID-DTE         TO TRUE           02040068
                     SET BV101-PV-BOM-SQL-ERROR       TO TRUE           02050068
                     MOVE SQLCODE     TO BV101-PV-BOM-SQLCDE            02060068
                     STRING '  PARA: BV101-SET-DATE - '                 02070068
                                            DELIMITED BY SIZE           02080068
                         BV101-PV-EFF-DTE-IN DELIMITED BY SIZE          02090068
                         '  ' DELIMITED BY SIZE                         02100068
                         BV101-PV-SKU-IN DELIMITED BY SIZE              02110068
                         '  ' DELIMITED BY SIZE                         02120068
                         BV101-PV-ITM-NBR-IN DELIMITED BY SIZE          02130068
                         '  ' DELIMITED BY SIZE                         02140068
                                   INTO BV101-PV-PARAGRAPH              02150068
               END-EVALUATE                                             02160068
           ELSE                                                         02170068
               MOVE BV101-PV-EFF-DTE-IN TO BV101-PV-EFF-BEG-DTE-IN      02180068
                                           BV101-PV-EFF-BEG-DTE         02190068
               IF BV101-PV-EFF-DTE-IN  = SPACES                         02200068
                  OR BV101-PV-EFF-BEG-DTE-CCYY > '2100'                 02210068
                  OR BV101-PV-EFF-BEG-DTE-CCYY < '1980'                 02220068
                  OR BV101-PV-EFF-BEG-DTE-MM   <   '01'                 02230068
                  OR BV101-PV-EFF-BEG-DTE-MM   >   '12'                 02240068
                  OR BV101-PV-EFF-BEG-DTE-DD   <   '01'                 02250068
                  OR BV101-PV-EFF-BEG-DTE-DD   >   '31'                 02260068
                   SET BV101-PV-BOM-ERROR           TO TRUE             02270068
                   SET BV101-PV-INVALID-DTE         TO TRUE             02280068
                   STRING '  PARA: BV101-SET-DATE - '                   02290068
                                             DELIMITED BY SIZE          02300068
                          BV101-PV-EFF-DTE-IN DELIMITED BY SIZE         02310068
                          '  ' DELIMITED BY SIZE                        02320068
                          BV101-PV-SKU-IN DELIMITED BY SIZE             02330068
                          '  ' DELIMITED BY SIZE                        02340068
                          BV101-PV-ITM-NBR-IN DELIMITED BY SIZE         02350068
                          '  ' DELIMITED BY SIZE                        02360068
                                    INTO BV101-PV-PARAGRAPH             02370068
               ELSE                                                     02380068
               IF BV101-PV-EFF-BEG-DTE-CCYY NOT NUMERIC OR              02390068
                  BV101-PV-EFF-BEG-DTE-MM   NOT NUMERIC OR              02400068
                  BV101-PV-EFF-BEG-DTE-DD   NOT NUMERIC                 02410068
                   SET BV101-PV-BOM-ERROR           TO TRUE             02420068
                   SET BV101-PV-INVALID-DTE         TO TRUE             02430068
                   STRING '  PARA: BV101-SET-DATE - '                   02440068
                                             DELIMITED BY SIZE          02450068
                          BV101-PV-EFF-DTE-IN DELIMITED BY SIZE         02460068
                          '  ' DELIMITED BY SIZE                        02470068
                          BV101-PV-SKU-IN DELIMITED BY SIZE             02480068
                          '  ' DELIMITED BY SIZE                        02490068
                          BV101-PV-ITM-NBR-IN DELIMITED BY SIZE         02500068
                          '  ' DELIMITED BY SIZE                        02510068
                                    INTO BV101-PV-PARAGRAPH             02520068
           ELSE                                                         02530068
               MOVE BV101-PV-EFF-DTE-IN TO BV101-PV-EFF-BEG-DTE-IN      02540068
                                           BV101-PV-EFF-BEG-DTE         02550068
               MOVE SQLCODE     TO BV101-PV-BOM-SQLCDE                  02560068
               MOVE SPACES      TO BV101-PV-PARAGRAPH                   02570068
           END-IF.                                                      02580068
                                                                        02590068
      ******************************************************************02600068
      * GET THE FISCAL BEGINNING OF MONTH DATE                          02610068
      ******************************************************************02620068
       BV101-GET-BOM-DATE.                                              02630068
                                                                        02640068
           MOVE BV101-PV-EFF-DTE-IN TO DTATTR-KY-DTE.                   02650068
                                                                        02660068
           PERFORM                                                      02670068
               WITH TEST AFTER                                          02680068
               VARYING BV101-PV-SQL-SUB FROM 1 BY 1                     02690068
               UNTIL (BV101-PV-SQL-SUB > BV101-PV-MAX-RETRIES)          02700068
                   OR (SQLCODE = 0)                                     02710068
                   OR (SQLCODE = +100)                                  02720068
               EXEC SQL                                                 02730068
                  SELECT FSCL_MN_BEG_DTE                                02740068
                  INTO :DTATTR-FSCL-MN-BEG-DTE                          02750068
                  FROM TDTATTR                                          02760068
                  WHERE KY_DTE      = :DTATTR-KY-DTE                    02770068
                  WITH UR                                               02780068
               END-EXEC                                                 02790068
                                                                        02800068
               EVALUATE TRUE                                            02810068
                 WHEN SQLCODE = 0                                       02820068
                     MOVE DTATTR-FSCL-MN-BEG-DTE                        02830068
                       TO XMT00023-FSCL-BOM-DATE                        02840068
                          BV101-BOM-FSCL-DTE-OUT                        02850068
                 WHEN SQLCODE = -904                                    02860068
                 WHEN SQLCODE = -911                                    02870068
                     CONTINUE                                           02880068
                 WHEN OTHER                                             02890068
                     SET BV101-PV-UNDEFINED-SQL-ERROR TO TRUE           02900068
                     SET BV101-PV-BOM-SQL-ERROR       TO TRUE           02910068
                     MOVE SQLCODE     TO BV101-PV-BOM-SQLCDE            02920068
                     STRING '  PARA: BV101-GET-BOM-DATE - '             02930068
                                            DELIMITED BY SIZE           02940068
                         BV101-PV-EFF-DTE-IN DELIMITED BY SIZE          02950068
                         '  ' DELIMITED BY SIZE                         02960068
                                   INTO BV101-PV-PARAGRAPH              02970068
               END-EVALUATE                                             02980068
           END-PERFORM.                                                 02990068
                                                                        03000068
           IF BV101-PV-SQL-SUB > BV101-PV-MAX-RETRIES                   03010068
               SET BV101-PV-BOM-SQL-ERROR TO TRUE                       03020068
               SET BV101-PV-BOM-NOT-FOUND TO TRUE                       03030068
               MOVE SQLCODE               TO BV101-PV-BOM-SQLCDE        03040068
               STRING                                                   03050068
                   '  PARA: MAX RETRY BV101-GET-BOM-DATE - '            03060068
                                         DELIMITED BY SIZE              03070068
                   BV101-PV-EFF-DTE-IN DELIMITED BY SIZE                03080068
                   '  ' DELIMITED BY SIZE                               03090068
                                    INTO BV101-PV-PARAGRAPH             03100068
           END-IF.                                                      03110068
                                                                        03120068
      ******************************************************************03130068
      *  PERFORM A DB2 COMMIT                                           03140068
      ******************************************************************03150068
       BV101-COMMIT.                                                    03160068
           EXEC SQL                                                     03170068
               COMMIT                                                   03180068
           END-EXEC.                                                    03190068
                                                                        03200068
           EVALUATE TRUE                                                03210068
               WHEN SQLCODE = +0                                        03220068
                   CONTINUE                                             03230068
               WHEN SQLWARN NOT = SPACE                                 03240068
               WHEN SQLCODE NOT = +0                                    03250068
                     SET BV101-PV-UNDEFINED-SQL-ERROR TO TRUE           03260068
                     SET BV101-PV-BOM-SQL-ERROR       TO TRUE           03270068
                     MOVE SQLCODE     TO BV101-PV-BOM-SQLCDE            03280068
                     STRING '  PARA: BV003-COMMIT - '                   03290068
                                            DELIMITED BY SIZE           03300068
                         BV101-PV-EFF-DTE-IN DELIMITED BY SIZE          03310068
                         '  ' DELIMITED BY SIZE                         03320068
                                   INTO BV101-PV-PARAGRAPH              03330068
                   DISPLAY 'BV101-COMMIT'                               03340068
           END-EVALUATE.                                                03350068
                                                                        03360068
      ******************** END OF BVPD100 COPYBOOK *********************03370068
