000100*****************************************************************         
000200**                                                                        
000300**   Format an XML message.                                               
000400**   This routine is used to aid in debugging the XML PARSE               
000500**   and XML GENERATE commands in a COBOL program by formatting           
000600**   an XML message into a more readable format.                          
000700**                                                                        
000800**   Your program is responsible for displaying the formatted             
000900**   message and handling errors.  Sample procedures are included         
001000**   at the bottom of this copybook, but they are commented out.          
001100**   These procedures are not included in this copybook so it can         
001200**   be used in Batch & CICS programs since we can't use DISPLAY          
001300**   in a CICS program.  If your program is a Batch program, you          
001400**   can get these procedures simply by including the copybooks           
001500**   ISWS007, ISPD007, ISWS009 & ISPD009.                                 
001600**                                                                        
001700**   This copybook is used in conjunction with ISWS008.                   
001800**                                                                        
001900*****************************************************************         
002000                                                                          
002100 9555-FORMAT-XML.                                                         
002200                                                                          
002300     MOVE 99999  TO XML-FORMATTED-LINES.                                  
002400     MOVE SPACES TO XML-FORMATTED-MESSAGE.                                
002500     MOVE 0      TO XML-FORMATTED-LINES.                                  
002600                                                                          
002700     XML PARSE XML-UNFORMATTED-MSG                                        
002800         PROCESSING PROCEDURE                                             
002900             9556-PROCESS-UNFORMATTED-XML                                 
003000         ON EXCEPTION                                                     
003100             PERFORM 9557-XML-ERROR                                       
003200         NOT ON EXCEPTION                                                 
003300             PERFORM 9558-XML-SUCCESS                                     
003400     END-XML.                                                             
003500                                                                          
003600                                                                          
003700******************************************************************        
003800* HERE IS WHERE THE FORMATTING OCCURS.                                    
003900******************************************************************        
004000 9556-PROCESS-UNFORMATTED-XML.                                            
004100                                                                          
004200     COMPUTE XML-TXT-LENGTH = FUNCTION LENGTH(XML-TEXT).                  
004300                                                                          
004400     EVALUATE XML-EVENT                                                   
004500                                                                          
004600       WHEN 'VERSION-INFORMATION'                                         
004700         IF NOT XML-DOCUMENT-START                                        
004800           STRING '<?xml'                                                 
004900             DELIMITED BY SIZE INTO XML-BUFFER                            
005000             WITH POINTER XML-POS                                         
005100           SET XML-DOCUMENT-START TO TRUE                                 
005200         END-IF                                                           
005300         STRING ' version="' XML-TEXT(1:XML-TXT-LENGTH)                   
005400           DELIMITED BY SIZE INTO XML-BUFFER                              
005500           WITH POINTER XML-POS                                           
005600                                                                          
005700       WHEN 'ENCODING-DECLARATION'                                        
005800         IF NOT XML-DOCUMENT-START                                        
005900           STRING '<?xml'                                                 
006000             DELIMITED BY SIZE INTO XML-BUFFER                            
006100             WITH POINTER XML-POS                                         
006200           SET XML-DOCUMENT-START TO TRUE                                 
006300         END-IF                                                           
006400         STRING ' encoding="' XML-TEXT(1:XML-TXT-LENGTH)                  
006500           DELIMITED BY SIZE INTO XML-BUFFER                              
006600           WITH POINTER XML-POS                                           
006700                                                                          
006800       WHEN 'STANDALONE-DECLARATION'                                      
006900         IF NOT XML-DOCUMENT-START                                        
007000           STRING '<?xml'                                                 
007100             DELIMITED BY SIZE INTO XML-BUFFER                            
007200             WITH POINTER XML-POS                                         
007300           SET XML-DOCUMENT-START TO TRUE                                 
007400         END-IF                                                           
007500         STRING ' standalone="' XML-TEXT(1:XML-TXT-LENGTH) '"'            
007600           DELIMITED BY SIZE INTO XML-BUFFER                              
007700           WITH POINTER XML-POS                                           
007800                                                                          
007900       WHEN 'START-OF-ELEMENT'                                            
008000         IF XML-DOCUMENT-START                                            
008100           MOVE '?>' TO XML-BUFFER(XML-POS:2)                             
008200           ADD 1 TO XML-FORMATTED-LINES                                   
008300           MOVE XML-BUFFER(1:XML-POS + 1)                                 
008400             TO XML-FMT-MSG(XML-FORMATTED-LINES)                          
008500           MOVE 1 TO XML-POS                                              
008600           SET XML-DOCUMENT-RESET TO TRUE                                 
008700         END-IF                                                           
008800         IF XML-ELEMENT-OPEN                                              
008900           MOVE '>' TO XML-BUFFER(XML-POS:1)                              
009000           ADD 1 TO XML-FORMATTED-LINES                                   
009100           MOVE XML-BUFFER(1:XML-POS)                                     
009200             TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)          
009300           ADD 2 TO XML-DEPTH                                             
009400           MOVE 1 TO XML-POS                                              
009500         END-IF                                                           
009600         STRING '<' XML-TEXT(1:XML-TXT-LENGTH)                            
009700           DELIMITED BY SIZE INTO XML-BUFFER                              
009800           WITH POINTER XML-POS                                           
009900         MOVE XML-TEXT TO XML-LATEST-ELEMENT                              
010000         MOVE XML-TXT-LENGTH TO XML-LATEST-LENGTH                         
010100         SET XML-ELEMENT-OPEN TO TRUE                                     
010200         SET XML-NO-CONTENT   TO TRUE                                     
010300                                                                          
010400       WHEN 'ATTRIBUTE-NAME'                                              
010500         MOVE 1 TO XML-ATTR-POS                                           
010600         STRING ' ' XML-TEXT(1:XML-TXT-LENGTH) '='                        
010700           DELIMITED BY SIZE INTO XML-ATTR-BUFFER                         
010800           WITH POINTER XML-ATTR-POS                                      
010900                                                                          
011000       WHEN 'ATTRIBUTE-CHARACTER'                                         
011100       WHEN 'ATTRIBUTE-CHARACTERS'                                        
011200**** If the line is going to be long, wrap attribute to next line.        
011300         IF (XML-DEPTH + XML-ATTR-POS + XML-POS + XML-TXT-LENGTH)         
011400          >  XML-WRAP-LENGTH                                              
011500           ADD 1 TO XML-FORMATTED-LINES                                   
011600           MOVE XML-BUFFER(1:XML-POS - 1)                                 
011700             TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)          
011800           MOVE 1 TO XML-POS                                              
011900         END-IF                                                           
012000         STRING XML-ATTR-BUFFER(1:XML-ATTR-POS - 1)                       
012100                '"' XML-TEXT(1:XML-TXT-LENGTH) '"'                        
012200           DELIMITED BY SIZE INTO XML-BUFFER                              
012300           WITH POINTER XML-POS                                           
012400                                                                          
012500       WHEN 'CONTENT-CHARACTER'                                           
012600       WHEN 'CONTENT-CHARACTERS'                                          
012700         SET XML-HAS-CONTENT TO TRUE                                      
012800         IF XML-ELEMENT-OPEN                                              
012900           STRING '>'                                                     
013000             DELIMITED BY SIZE INTO XML-BUFFER                            
013100             WITH POINTER XML-POS                                         
013200           SET XML-ELEMENT-CLOSED TO TRUE                                 
013300         END-IF                                                           
013400**** If the contents appended to the end of the tag is going              
013500**** to be fairly long, put the contents on a separate line.              
013600         IF (XML-TXT-LENGTH                                               
013700           + XML-DEPTH                                                    
013800           + (XML-POS * 2)                                                
013900            ) > XML-WRAP-LENGTH                                           
014000           ADD 1 TO XML-FORMATTED-LINES                                   
014100           MOVE XML-BUFFER(1:XML-POS - 1)                                 
014200             TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)          
014300           ADD 2 TO XML-DEPTH                                             
014400**** Make sure contents fit on one line.  If not, wrap it.                
014500           IF (XML-DEPTH + XML-TXT-LENGTH) < XML-MAX-LINE-LENGTH          
014600             ADD 1 TO XML-FORMATTED-LINES                                 
014700             MOVE XML-TEXT(1:XML-TXT-LENGTH)                              
014800               TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)        
014900           ELSE                                                           
015000             PERFORM  VARYING XML-POS FROM 1 BY XML-WRAP-LENGTH           
015100                        UNTIL XML-POS > XML-TXT-LENGTH                    
015200               IF (XML-POS + XML-WRAP-LENGTH) <= XML-TXT-LENGTH           
015300                 ADD 1 TO XML-FORMATTED-LINES                             
015400                 MOVE XML-TEXT(XML-POS:XML-WRAP-LENGTH)                   
015500                   TO XML-FMT-MSG(XML-FORMATTED-LINES)                    
015600                                 (XML-DEPTH + 1:)                         
015700               ELSE                                                       
015800                 ADD 1 TO XML-FORMATTED-LINES                             
015900                 MOVE XML-TEXT(XML-POS                                    
016000                             :(XML-TXT-LENGTH - XML-POS + 1))             
016100                   TO XML-FMT-MSG(XML-FORMATTED-LINES)                    
016200                                 (XML-DEPTH + 1:)                         
016300               END-IF                                                     
016400             END-PERFORM                                                  
016500           END-IF                                                         
016600           MOVE 1 TO XML-POS                                              
016700         ELSE                                                             
016800           STRING XML-TEXT(1:XML-TXT-LENGTH)                              
016900             DELIMITED BY SIZE INTO XML-BUFFER                            
017000             WITH POINTER XML-POS                                         
017100         END-IF                                                           
017200                                                                          
017300       WHEN 'COMMENT'                                                     
017400         IF XML-DOCUMENT-START                                            
017500           MOVE '?>' TO XML-BUFFER(XML-POS:2)                             
017600           ADD 1 TO XML-FORMATTED-LINES                                   
017700           MOVE XML-BUFFER(1:XML-POS + 1)                                 
017800             TO XML-FMT-MSG(XML-FORMATTED-LINES)                          
017900           MOVE 1 TO XML-POS                                              
018000           SET XML-DOCUMENT-RESET TO TRUE                                 
018100         END-IF                                                           
018200         IF XML-ELEMENT-OPEN                                              
018300           MOVE '>' TO XML-BUFFER(XML-POS:1)                              
018400           ADD 1 TO XML-FORMATTED-LINES                                   
018500           MOVE XML-BUFFER(1:XML-POS)                                     
018600             TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)          
018700           ADD 2 TO XML-DEPTH                                             
018800           MOVE 1 TO XML-POS                                              
018900           SET XML-ELEMENT-CLOSED TO TRUE                                 
019000         END-IF                                                           
019100         ADD 1 TO XML-FORMATTED-LINES                                     
019200         STRING '<!--' XML-TEXT(1:XML-TXT-LENGTH) '-->'                   
019300           DELIMITED BY SIZE INTO XML-BUFFER                              
019400           WITH POINTER XML-POS                                           
019500         MOVE XML-BUFFER(1:XML-POS)                                       
019600           TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)            
019700         MOVE 1 TO XML-POS                                                
019800                                                                          
019900       WHEN 'PROCESSING-INSTRUCTION-TARGET'                               
020000         IF XML-DOCUMENT-START                                            
020100           MOVE '?>' TO XML-BUFFER(XML-POS:2)                             
020200           ADD 1 TO XML-FORMATTED-LINES                                   
020300           MOVE XML-BUFFER(1:XML-POS + 1)                                 
020400             TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)          
020500           MOVE 1 TO XML-POS                                              
020600           SET XML-DOCUMENT-RESET TO TRUE                                 
020700         END-IF                                                           
020800         IF XML-ELEMENT-OPEN                                              
020900           MOVE '>' TO XML-BUFFER(XML-POS:1)                              
021000           ADD 1 TO XML-FORMATTED-LINES                                   
021100           MOVE XML-BUFFER(1:XML-POS)                                     
021200             TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)          
021300           ADD 2 TO XML-DEPTH                                             
021400           MOVE 1 TO XML-POS                                              
021500           SET XML-ELEMENT-CLOSED TO TRUE                                 
021600         END-IF                                                           
021700         STRING '<?' XML-TEXT(1:XML-TXT-LENGTH)                           
021800           DELIMITED BY SIZE INTO XML-BUFFER                              
021900           WITH POINTER XML-POS                                           
022000                                                                          
022100       WHEN 'PROCESSING-INSTRUCTION-DATA'                                 
022200         STRING ' ' XML-TEXT(1:XML-TXT-LENGTH) '?>'                       
022300           DELIMITED BY SIZE INTO XML-BUFFER                              
022400           WITH POINTER XML-POS                                           
022500         ADD 1 TO XML-FORMATTED-LINES                                     
022600         MOVE XML-BUFFER(1:XML-POS - 1)                                   
022700           TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)            
022800         MOVE 1 TO XML-POS                                                
022900                                                                          
023000       WHEN 'START-OF-CDATA-SECTION'                                      
023100         IF XML-DOCUMENT-START                                            
023200           MOVE '?>' TO XML-BUFFER(XML-POS:2)                             
023300           ADD 1 TO XML-FORMATTED-LINES                                   
023400           MOVE XML-BUFFER(1:XML-POS + 2)                                 
023500             TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)          
023600           MOVE 1 TO XML-POS                                              
023700           SET XML-DOCUMENT-RESET TO TRUE                                 
023800         END-IF                                                           
023900         IF XML-ELEMENT-OPEN                                              
024000           MOVE '>' TO XML-BUFFER(XML-POS:1)                              
024100           ADD 1 TO XML-FORMATTED-LINES                                   
024200           MOVE XML-BUFFER(1:XML-POS)                                     
024300             TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)          
024400           ADD 2 TO XML-DEPTH                                             
024500           MOVE 1 TO XML-POS                                              
024600           SET XML-ELEMENT-CLOSED TO TRUE                                 
024700         END-IF                                                           
024800         STRING '<![CDATA[' XML-TEXT(1:XML-TXT-LENGTH)                    
024900           DELIMITED BY SIZE INTO XML-BUFFER                              
025000           WITH POINTER XML-POS                                           
025100                                                                          
025200       WHEN 'END-OF-CDATA-SECTION'                                        
025300         STRING XML-TEXT(1:XML-TXT-LENGTH) ']]>'                          
025400           DELIMITED BY SIZE INTO XML-BUFFER                              
025500           WITH POINTER XML-POS                                           
025600         ADD 1 TO XML-FORMATTED-LINES                                     
025700         MOVE XML-BUFFER(1:XML-POS - 1)                                   
025800           TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)            
025900         MOVE 1 TO XML-POS                                                
026000                                                                          
026100       WHEN 'END-OF-ELEMENT'                                              
026200         IF XML-POS = 1                                                   
026300           IF XML-DEPTH >= 2                                              
026400             SUBTRACT 2 FROM XML-DEPTH                                    
026500           END-IF                                                         
026600         END-IF                                                           
026700         IF XML-HAS-CONTENT                                               
026800         OR XML-TEXT(1:XML-TXT-LENGTH)                                    
026900            NOT = XML-LATEST-ELEMENT(1:XML-LATEST-LENGTH)                 
027000           STRING '</' XML-TEXT(1:XML-TXT-LENGTH) '>'                     
027100             DELIMITED BY SIZE INTO XML-BUFFER                            
027200             WITH POINTER XML-POS                                         
027300         ELSE                                                             
027400           STRING '/>'                                                    
027500             DELIMITED BY SIZE INTO XML-BUFFER                            
027600             WITH POINTER XML-POS                                         
027700           SET XML-ELEMENT-CLOSED TO TRUE                                 
027800         END-IF                                                           
027900         ADD 1 TO XML-FORMATTED-LINES                                     
028000         MOVE XML-BUFFER(1:XML-POS - 1)                                   
028100           TO XML-FMT-MSG(XML-FORMATTED-LINES)(XML-DEPTH + 1:)            
028200         MOVE 1 TO XML-POS                                                
028300                                                                          
028400       WHEN OTHER                                                         
028500         CONTINUE                                                         
028600                                                                          
028700     END-EVALUATE.                                                        
028800                                                                          
028900                                                                          
029000*9557-XML-ERROR.                                                          
029100*                                                                         
029200*    PERFORM 9000-EXPLAIN-XML-RESPONSE                                    
029300*    PERFORM  VARYING XML-LINE-IDX FROM 1 BY 1                            
029400*               UNTIL XML-LINE-IDX > XML-RESPONSE-LINES                   
029500*        DISPLAY XML-MSG(XML-LINE-IDX)                                    
029600*    END-PERFORM.                                                         
029700*    MOVE +4018         TO XML-FORMAT-ABEND-CODE.                         
029800*    CALL 'ILBOABN0' USING XML-FORMAT-ABEND-CODE.                         
029900*                                                                         
030000*                                                                         
030100*9558-XML-SUCCESS.                                                        
030200*                                                                         
030300*    DISPLAY ' '.                                                         
030400*    DISPLAY 'XML DOCUMENT SUCCESSFULLY FORMATTED'.                       
030500*    DISPLAY ' '.                                                         
030600*    PERFORM  VARYING XML-FMT-IDX FROM 1 BY 1                             
030700*               UNTIL XML-FMT-IDX > XML-FORMATTED-LINES                   
030800*        DISPLAY XML-FMT-MSG(XML-FMT-IDX)                                 
030900*    END-PERFORM.                                                         
