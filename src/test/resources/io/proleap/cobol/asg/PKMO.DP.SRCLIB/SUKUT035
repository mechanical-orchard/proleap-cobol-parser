000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             SUKUT035.                                        
000600 AUTHOR.                 PATRICK BURKE.                                   
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            JULY, 2000                                       
000900 DATE-COMPILED.                                                           
001000*****************************************************************         
001100*                                                                         
001200*****************************************************************         
001300*                                                               *         
001400*  THIS PROGRAM WILL CREATE A REPORT EXTRACT TO BE USED IN      *         
001500*  SUKRP007 -PO/STORE UNIT ADJUSTMENTS.                         *         
001600*                                                               *         
001700*  INPUT FOR THIS PROGRAM IS CREATED OUT OF SUKUT034.           *         
001800*****************************************************************         
001900*                                                                         
002000*****************************************************************         
002100 ENVIRONMENT DIVISION.                                                    
002200*****************************************************************         
002300                                                                          
002400 CONFIGURATION SECTION.                                                   
002500                                                                          
002600 SOURCE-COMPUTER.        IBM-3090.                                        
002700 OBJECT-COMPUTER.        IBM-3090.                                        
002800                                                                          
002900 INPUT-OUTPUT SECTION.                                                    
003000                                                                          
003100 FILE-CONTROL.                                                            
003200*                                                                         
003300     SELECT EXTRACT-FILE                                                  
003400         ASSIGN TO INP01.                                                 
003500                                                                          
003600     SELECT REPORT-FILE                                                   
003700         ASSIGN TO OUT01.                                                 
003800                                                                          
003900*                                                                         
004000*****************************************************************         
004100 DATA DIVISION.                                                           
004200*****************************************************************         
004300*                                                                         
004400 FILE SECTION.                                                            
004500*                                                                         
004600 FD  EXTRACT-FILE                                                         
004700     RECORDING MODE IS F                                                  
004800     RECORD CONTAINS 200 CHARACTERS                                       
004900     LABEL RECORDS ARE STANDARD                                           
005000     DATA RECORD IS EXTRACT-RECORD.                                       
005100 01  EXTRACT-RECORD                 PIC X(200).                           
005200                                                                          
005300 FD  REPORT-FILE                                                          
005400     RECORDING MODE IS F                                                  
005500     RECORD CONTAINS 200 CHARACTERS                                       
005600     LABEL RECORDS ARE OMITTED                                            
005700     DATA RECORD IS REPORT-RECORD.                                        
005800 01  REPORT-RECORD                  PIC X(200).                           
005900*                                                                         
006000*                                                                         
006100 WORKING-STORAGE SECTION.                                                 
006200*                                                                         
006300**************************************************************            
006400* FORMAT OF THE CONTROL CARD INFORMATION PASSED INTO THE PGM *            
006500**************************************************************            
006600 01  CC-CONTROL-CARD.                                                     
006700     05  CC-PRINTER-ID                PIC  X(08).                         
006800     05  CC-PRTR-CNTL-IND             PIC  X(01).                         
006900     05  CC-TRACK-NBR                 PIC  9(01).                         
007000     05  FILLER                       PIC  X(01).                         
007100     05  CC-CUT-REPORT                PIC  X(01).                         
007200         88 CC-CUT-REPORT-AT-END                   VALUE 'C'.             
007300     05  CC-REQUESTOR-ID              PIC  X(08).                         
007400     05  CC-PRINT-SUKRP002-IND        PIC  X(01).                         
007500         88 CC-PRINT-SUKRP002                      VALUE 'Y'.             
007600     05  CC-PRINT-SUKRP003-IND        PIC  X(01).                         
007700         88 CC-PRINT-SUKRP003                      VALUE 'Y'.             
007800     05  CC-PRINT-SUKRP004-IND        PIC  X(01).                         
007900         88 CC-PRINT-SUKRP004                      VALUE 'Y'.             
008000     05  CC-PRINT-SUKRP005-IND        PIC  X(01).                         
008100         88 CC-PRINT-SUKRP005                      VALUE 'Y'.             
008200     05  CC-PRINT-SUKRP006-IND        PIC  X(01).                         
008300         88 CC-PRINT-SUKRP006                      VALUE 'Y'.             
008400     05  CC-PRINT-SUKRP007-IND        PIC  X(01).                         
008500         88 CC-PRINT-SUKRP007                      VALUE 'Y'.             
008600     05  CC-PRINT-SUKRP008-IND        PIC  X(01).                         
008700         88 CC-PRINT-SUKRP008                      VALUE 'Y'.             
008800     05  CC-PRINT-SUKRP023-IND        PIC  X(01).                         
008900         88 CC-PRINT-SUKRP023                      VALUE 'Y'.             
009000*                                                                         
009100 01 WS-CALC-FIELDS.                                                       
009200    05  WS-RECORDS-READ-QTY          PIC S9(07)    COMP-3                 
009300                                                   VALUE ZEROS.           
009400    05  WS-RECORDS-WRITE-QTY         PIC S9(07)    COMP-3                 
009500                                                   VALUE ZEROS.           
009600*                                                                         
009700 01  PROGRAM-SWITCHES.                                                    
009800     05  PS-END-OF-FILE-SW           PIC X(03)     VALUE 'N'.             
009900         88  PS-END-OF-FILE                        VALUE 'Y'.             
010000     05  PS-FIRST-TIME-SW            PIC X(01)     VALUE 'N'.             
010100         88  PS-FIRST-TIME-YES                     VALUE 'Y'.             
010200         88  PS-FIRST-TIME-NO                      VALUE 'N'.             
010300     05  PS-END-OF-CURSOR-SW         PIC X(03)     VALUE 'N'.             
010400         88 PS-END-OF-CURSOR                       VALUE 'Y'.             
010500         88 PS-NOT-END-OF-CURSOR                   VALUE 'N'.             
010600     05  PS-LBVRDA-FOUND-SW          PIC X(03)     VALUE 'N'.             
010700         88 PS-LBVRDA-FOUND                        VALUE 'Y'.             
010800         88 PS-LBVRDA-NOT-FOUND                    VALUE 'N'.             
010900     05  PS-END-OF-TABLE-SW          PIC X(03)     VALUE 'N'.             
011000         88 PS-END-OF-TABLE                        VALUE 'Y'.             
011100         88 PS-NOT-END-OF-TABLE                    VALUE 'N'.             
011200*                                                                         
011300     05  PC-PROGRAM-NAME             PIC  X(08)    VALUE                  
011400         'SUKUT035'.                                                      
011500*                                                                         
011600 01 PC-PROGRAM-VARIABLES.                                                 
011700     05  PV-PREV-PO-NUMBER           PIC 9(09)     VALUE ZEROS.           
011800     05  PV-PREV-REC-SEQ-NBR         PIC 9(09)     VALUE ZEROS.           
011900     05  PV-PREV-STORE-NBR           PIC 9(04)     VALUE ZEROS.           
012000     05  PV-PREV-LINE-NBR            PIC 9(09)     VALUE ZEROS.           
012100     05  PV-SHIPT-UNT-ID             PIC S9(18)V   VALUE ZERO             
012200                                                   COMP-3.                
012300     05  PV-EXCEPTION-TYPE           PIC 9(01)     VALUE ZEROS.           
012400     05  PV-SHPD-QTY                 PIC S9(18)    VALUE ZEROS.           
012500     05  PV-LINE-NBR                 PIC S9(09)    VALUE ZEROS.           
012600                                                                          
012700     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
012800                                                   COMP SYNC.             
012900     05  PV-ABEND-PARA-NAME          PIC X(30)     VALUE SPACES.          
013000     05  PV-DB2-OPERATION-MSG-1      PIC X(40)     VALUE SPACES.          
013100     05  PV-DB2-OPERATION-MSG-2      PIC X(40)     VALUE SPACES.          
013200     05  PV-DB2-TABLE-NAME           PIC X(12)     VALUE SPACES.          
013300     05  PV-SQLCODE                  PIC Z(8)9-.                          
013400                                                                          
013500                                                                          
013600                                                                          
013700                                                                          
013800*RECORD LAYOUT FOR INPUT FILE                                             
013900     COPY SURD001.                                                        
014000                                                                          
014100*RECORD LAYOUT FOR OUTPUT FILE                                            
014200     COPY SURD009.                                                        
014300                                                                          
014400 EJECT                                                                    
014500*                                                                         
014600*****************************************************************         
014700*  DB2 AREA FOR TSHUUPC                                                   
014800*****************************************************************         
014900*                                                                         
015000     EXEC SQL                                                             
015100          INCLUDE TSHUUPC                                                 
015200     END-EXEC.                                                            
015300*                                                                         
015400*****************************************************************         
015500*  DB2 AREA FOR TLBVRDA                                                   
015600*****************************************************************         
015700*                                                                         
015800     EXEC SQL                                                             
015900          INCLUDE TLBVRDA                                                 
016000     END-EXEC.                                                            
016100*                                                                         
016200*****************************************************************         
016300*** DB2 MESSAGE AREAS ****                                                
016400*****************************************************************         
016500*                                                                         
016600     COPY DPWS004.                                                        
016700*                                                                         
016800*****************************************************************         
016900*    DB2 AREA FOR COMMUNICATIONS                                          
017000*****************************************************************         
017100*                                                                         
017200     EXEC SQL                                                             
017300          INCLUDE SQLCA                                                   
017400     END-EXEC.                                                            
017500*                                                                         
017600*****************************************************************         
017700*    DB2 AREA FOR COMMUNICATIONS AREA2                                    
017800*****************************************************************         
017900*                                                                         
018000     COPY SQLCA2.                                                         
018100*                                                                         
018200*****************************************************************         
018300*    CURSOR DECLARATION FOR EXTRACT CUR                                   
018400*****************************************************************         
018500*                                                                         
018600     EXEC SQL                                                             
018700         DECLARE EXTRACT_CUR CURSOR FOR                                   
018800             SELECT TRN_NBR                                               
018900                  , TRN_LNE_NBR                                           
019000                  , SHPD_QTY                                              
019100               FROM TSHUUPC                                               
019200              WHERE SHIPT_UNT_ID = :PV-SHIPT-UNT-ID                       
019300     END-EXEC.                                                            
019400                                                                          
019500******************************************************************        
019600 PROCEDURE DIVISION.                                                      
019700******************************************************************        
019800*                                                                         
019900 0000-MAINLINE-PROCESSING.                                                
020000                                                                          
020100     DISPLAY '**** START OF SUKUT035 ****'.                               
020200     DISPLAY ' '.                                                         
020300                                                                          
020400     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
020500     DISPLAY 'CONTROL CARD = ' CC-CONTROL-CARD.                           
020600                                                                          
020700     IF CC-PRINT-SUKRP007                                                 
020800        PERFORM 1000-INITIALIZE                                           
020900*                                                                         
021000        IF  PS-END-OF-FILE                                                
021100            PERFORM 9000-END-PROCESS                                      
021200        ELSE                                                              
021300            SET PS-FIRST-TIME-NO TO TRUE                                  
021400            PERFORM 2000-MAIN-PROCESS                                     
021500               UNTIL  PS-END-OF-FILE                                      
021600            PERFORM 9000-END-PROCESS                                      
021700        END-IF                                                            
021800*                                                                         
021900     ELSE                                                                 
022000        CONTINUE                                                          
022100     END-IF.                                                              
022200*                                                                         
022300     STOP RUN.                                                            
022400*                                                                         
022500******************************************************************        
022600**  PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.             **        
022700******************************************************************        
022800 1000-INITIALIZE.                                                         
022900*                                                                         
023000     OPEN INPUT  EXTRACT-FILE                                             
023100          OUTPUT REPORT-FILE.                                             
023200*                                                                         
023300*READ FIRST INPUT RECORD.                                                 
023400     PERFORM 7000-READ-EXTRACT.                                           
023500     SET PS-FIRST-TIME-YES TO TRUE.                                       
023600                                                                          
023700     IF PS-END-OF-FILE                                                    
023800         DISPLAY 'EXTRACT FILE INP01 IS EMPTY'                            
023900     ELSE                                                                 
024000        IF SU001-ERROR-RECORD                                             
024100          PERFORM 6100-BUILD-ERROR-RECORD                                 
024200          PERFORM 7200-WRITE-EXTRACT-RECORD                               
024300          SET PS-END-OF-FILE TO TRUE                                      
024400        END-IF                                                            
024500     END-IF.                                                              
024600*                                                                         
024700******************************************************************        
024800*  THIS IS THE MAIN CONTROLLING PARAGRAPH                        *        
024900******************************************************************        
025000 2000-MAIN-PROCESS.                                                       
025100*                                                                         
025200     IF SU001-TSULBXF-USED = 'Y'                                          
025300        PERFORM 7300-SELECT-TLBVRDA                                       
025400        PERFORM 7400-PROCESS-TLBVRDA                                      
025500     ELSE                                                                 
025600        PERFORM 7500-OPEN-EXTRACT-CSR                                     
025700        PERFORM 2200-PROCESS-ROW                                          
025800           UNTIL PS-END-OF-CURSOR                                         
025900        PERFORM 7600-CLOSE-EXTRACT-CSR                                    
026000           SET PS-NOT-END-OF-CURSOR TO TRUE                               
026100     END-IF.                                                              
026200                                                                          
026300*READ NEXT INPUT RECORD.                                                  
026400     PERFORM 7000-READ-EXTRACT.                                           
026500*                                                                         
026600******************************************************************        
026700*  PROCESS A ROW, BUILD AND WRITE A RECORD                                
026800******************************************************************        
026900 2200-PROCESS-ROW.                                                        
027000*                                                                         
027100        PERFORM 7700-FETCH-EXTRACT-CSR                                    
027200          IF PS-END-OF-CURSOR                                             
027300             CONTINUE                                                     
027400          ELSE                                                            
027500          PERFORM 2500-PROCESS-INDICATORS                                 
027600          MOVE SHUUPC-TRN-LNE-NBR   TO PV-LINE-NBR                        
027700          PERFORM 6000-BUILD-OUTPUT-RECORD                                
027800          PERFORM 7200-WRITE-EXTRACT-RECORD                               
027900          END-IF.                                                         
028000*                                                                         
028100******************************************************************        
028200*  PROCESS ERROR TYPE INDICATORS                                 *        
028300******************************************************************        
028400 2500-PROCESS-INDICATORS.                                                 
028500*                                                                         
028600     EVALUATE TRUE                                                        
028700        WHEN SU001-SCANNED-IND     = 'N' OR ' '                           
028800          MOVE 1                   TO PV-EXCEPTION-TYPE                   
028900          MOVE SHUUPC-SHPD-QTY     TO PV-SHPD-QTY                         
029000          MULTIPLY SHUUPC-SHPD-QTY   BY -1                                
029100              GIVING PV-SHPD-QTY                                          
029200                                                                          
029300        WHEN SU001-MISDIRECT-IND   = 'Y'                                  
029400          MOVE 2                   TO PV-EXCEPTION-TYPE                   
029500          MOVE SHUUPC-SHPD-QTY     TO PV-SHPD-QTY                         
029600                                                                          
029700        WHEN SU001-FREE-ASTRAY-IND = 'Y'                                  
029800          MOVE 3                   TO PV-EXCEPTION-TYPE                   
029900          MOVE SHUUPC-SHPD-QTY     TO PV-SHPD-QTY                         
030000                                                                          
030100     END-EVALUATE.                                                        
030200*                                                                         
030300******************************************************************        
030400*   BUILD OUTPUT RECORD                                          *        
030500******************************************************************        
030600 6000-BUILD-OUTPUT-RECORD.                                                
030700*                                                                         
030800     INITIALIZE SU009-EXTRACT-RECORD.                                     
030900*                                                                         
031000     IF PV-LINE-NBR = 0                                                   
031100        IF LBVRDA-PREPK-ID > 0                                            
031200           MOVE LBVRDA-PREPK-ID TO PV-LINE-NBR                            
031300           MOVE 'P'             TO SU009-PREPK-IND                        
031400        END-IF                                                            
031500     END-IF.                                                              
031600*                                                                         
031700     MOVE SU001-MANIFEST-NBR     TO SU009-MANIFEST-NBR.                   
031800     MOVE SU001-PO-NUMBER        TO SU009-PO-NUMBER.                      
031900     MOVE SU001-REC-SEQ-NBR      TO SU009-REC-SEQ-NBR.                    
032000     MOVE SU001-STR-NBR          TO SU009-STR-NBR.                        
032100     MOVE PV-LINE-NBR            TO SU009-LINE-NBR.                       
032200     MOVE PV-SHPD-QTY            TO SU009-SHPD-QTY.                       
032300     MOVE PV-EXCEPTION-TYPE      TO SU009-EXCEPTION-TYPE.                 
032400     MOVE SU001-RECORD-TYPE-IND  TO SU009-RECORD-TYPE-IND.                
032500     MOVE SU001-FREE-ASTRAY-IND  TO SU009-FREE-ASTRAY-IND.                
032600     MOVE SU001-AUTO-VE-IND      TO SU009-AUTO-VE-IND.                    
032700*                                                                         
032800******************************************************************        
032900*   BUILD ERROR RECORD                                           *        
033000******************************************************************        
033100 6100-BUILD-ERROR-RECORD.                                                 
033200*                                                                         
033300     INITIALIZE SU009-EXTRACT-RECORD.                                     
033400*                                                                         
033500     MOVE SU001-MANIFEST-NBR     TO SU009-MANIFEST-NBR.                   
033600     MOVE SU001-PO-NUMBER        TO SU009-PO-NUMBER.                      
033700     MOVE SU001-REC-SEQ-NBR      TO SU009-REC-SEQ-NBR.                    
033800     MOVE SU001-STR-NBR          TO SU009-STR-NBR.                        
033900     MOVE PV-EXCEPTION-TYPE      TO SU009-EXCEPTION-TYPE.                 
034000     MOVE SU001-RECORD-TYPE-IND  TO SU009-RECORD-TYPE-IND.                
034100*                                                                         
034200*                                                                         
034300******************************************************************        
034400*  READ EXTRACT FILE                                             *        
034500******************************************************************        
034600 7000-READ-EXTRACT.                                                       
034700     READ EXTRACT-FILE INTO SU001-EXTRACT-RECORD                          
034800        AT END                                                            
034900           SET PS-END-OF-FILE        TO TRUE                              
035000        NOT AT END                                                        
035100        MOVE SU001-PO-NUMBER      TO PV-PREV-PO-NUMBER                    
035200        MOVE SU001-REC-SEQ-NBR    TO PV-PREV-REC-SEQ-NBR                  
035300        MOVE SU001-STR-NBR        TO PV-PREV-STORE-NBR                    
035400        MOVE SU001-SHIPT-UNT-ID   TO PV-SHIPT-UNT-ID                      
035500        ADD  +1                   TO WS-RECORDS-READ-QTY                  
035600*                                                                         
035700     END-READ.                                                            
035800*                                                                         
035900*                                                                         
036000******************************************************************        
036100*  WRITE EXTRACT RECORD                                          *        
036200******************************************************************        
036300 7200-WRITE-EXTRACT-RECORD.                                               
036400*                                                                         
036500     WRITE REPORT-RECORD                                                  
036600         FROM SU009-EXTRACT-RECORD                                        
036700     ADD 1                        TO WS-RECORDS-WRITE-QTY.                
036800*                                                                         
036900******************************************************************        
037000*   SELECT TLBVRDA.                                              *        
037100******************************************************************        
037200 7300-SELECT-TLBVRDA.                                                     
037300*                                                                         
037400     INITIALIZE DCLTLBVRDA.                                               
037500                                                                          
037600     EXEC SQL                                                             
037700         SELECT                                                           
037800                A.OTPK_UNT_QTY                                            
037900             ,  A.INPK_UNT_QTY                                            
038000             ,  A.INPK_CNVYB_IND                                          
038100             ,  A.PO_LNE_NBR                                              
038200             ,  A.PREPK_ID                                                
038300          INTO                                                            
038400                :LBVRDA-OTPK-UNT-QTY                                      
038500              , :LBVRDA-INPK-UNT-QTY                                      
038600              , :LBVRDA-INPK-CNVYB-IND                                    
038700              , :LBVRDA-PO-LNE-NBR                                        
038800              , :LBVRDA-PREPK-ID                                          
038900          FROM                                                            
039000                TLBVRDA A                                                 
039100         WHERE                                                            
039200                A.BRCDE_NBR          = :SU001-BRCDE-NBR                   
039300           AND  A.PO_NBR             = :SU001-PO-NUMBER                   
039400           AND  A.RCVR_SEQ_NBR       = :SU001-REC-SEQ-NBR                 
039500           AND  A.DEST_OPER_UNT_ID   = :SU001-STR-NBR                     
039600     END-EXEC                                                             
039700                                                                          
039800     EVALUATE TRUE                                                        
039900         WHEN SQLCODE = ZERO                                              
040000             SET PS-LBVRDA-FOUND  TO TRUE                                 
040100         WHEN SQLCODE = +100                                              
040200             SET PS-LBVRDA-NOT-FOUND  TO TRUE                             
040300         WHEN SQLCODE NOT EQUAL ZERO                                      
040400         WHEN SQLWARN0 NOT = SPACES                                       
040500             DISPLAY '** ABEND - SUKUT034 **'                             
040600             DISPLAY '** 7300-SELECT-TLBVRDA **'                          
040700             PERFORM 9998-SQL-ABEND                                       
040800     END-EVALUATE.                                                        
040900                                                                          
041000******************************************************************        
041100*   PROCESS, BUILD AND WRITE RECORD.                             *        
041200******************************************************************        
041300 7400-PROCESS-TLBVRDA.                                                    
041400*                                                                         
041500     IF LBVRDA-INPK-CNVYB-IND = 'Y'                                       
041600        MOVE LBVRDA-INPK-UNT-QTY   TO PV-SHPD-QTY                         
041700     ELSE                                                                 
041800        MOVE LBVRDA-OTPK-UNT-QTY   TO PV-SHPD-QTY                         
041900     END-IF.                                                              
042000                                                                          
042100     EVALUATE TRUE                                                        
042200        WHEN SU001-SCANNED-IND     = 'N' OR ' '                           
042300          MOVE 1                   TO PV-EXCEPTION-TYPE                   
042400          COMPUTE PV-SHPD-QTY = PV-SHPD-QTY * -1                          
042500        WHEN SU001-MISDIRECT-IND   = 'Y'                                  
042600          MOVE 2                   TO PV-EXCEPTION-TYPE                   
042700        WHEN SU001-FREE-ASTRAY-IND = 'Y'                                  
042800          MOVE 3                   TO PV-EXCEPTION-TYPE                   
042900     END-EVALUATE.                                                        
043000                                                                          
043100     MOVE LBVRDA-PO-LNE-NBR     TO PV-LINE-NBR.                           
043200                                                                          
043300     PERFORM 6000-BUILD-OUTPUT-RECORD.                                    
043400                                                                          
043500     PERFORM 7200-WRITE-EXTRACT-RECORD.                                   
043600*                                                                         
043700******************************************************************        
043800*   OPEN EXTRACT CURSOR.                                         *        
043900******************************************************************        
044000 7500-OPEN-EXTRACT-CSR.                                                   
044100*                                                                         
044200     EXEC SQL                                                             
044300         OPEN EXTRACT_CUR                                                 
044400     END-EXEC.                                                            
044500                                                                          
044600     EVALUATE TRUE                                                        
044700         WHEN SQLCODE = ZERO                                              
044800             CONTINUE                                                     
044900         WHEN OTHER                                                       
045000           MOVE '7300-OPEN-EXTRACT-CURSOR' TO PV-ABEND-PARA-NAME          
045100           MOVE 'SQL ERROR OPENING CURSOR' TO                             
045200                            PV-DB2-OPERATION-MSG-1                        
045300             PERFORM 9998-SQL-ABEND                                       
045400     END-EVALUATE.                                                        
045500*                                                                         
045600******************************************************************        
045700*   CLOSE EXTRACT CURSOR.                                        *        
045800******************************************************************        
045900 7600-CLOSE-EXTRACT-CSR.                                                  
046000*                                                                         
046100     EXEC SQL                                                             
046200         CLOSE EXTRACT_CUR                                                
046300     END-EXEC.                                                            
046400                                                                          
046500     EVALUATE TRUE                                                        
046600         WHEN SQLCODE = ZERO                                              
046700             CONTINUE                                                     
046800         WHEN OTHER                                                       
046900           MOVE '7300-CLOSE-EXTRACT-CURSOR' TO PV-ABEND-PARA-NAME         
047000           MOVE 'SQL ERROR CLOSING CURSOR' TO                             
047100                            PV-DB2-OPERATION-MSG-1                        
047200             PERFORM 9998-SQL-ABEND                                       
047300     END-EVALUATE.                                                        
047400*                                                                         
047500******************************************************************        
047600*   FETCH EXTRACT CURSOR.                                        *        
047700******************************************************************        
047800*                                                                         
047900 7700-FETCH-EXTRACT-CSR.                                                  
048000     EXEC SQL                                                             
048100         FETCH EXTRACT_CUR                                                
048200          INTO :SHUUPC-TRN-NBR                                            
048300             , :SHUUPC-TRN-LNE-NBR                                        
048400             , :SHUUPC-SHPD-QTY                                           
048500     END-EXEC.                                                            
048600                                                                          
048700     EVALUATE TRUE                                                        
048800         WHEN SQLCODE = ZERO                                              
048900             CONTINUE                                                     
049000         WHEN SQLCODE = +100                                              
049100             SET PS-END-OF-CURSOR TO TRUE                                 
049200         WHEN OTHER                                                       
049300           MOVE '7700-FETCH-CURSOR' TO PV-ABEND-PARA-NAME                 
049400           MOVE 'SQL ERROR FETCHING DATA' TO                              
049500                            PV-DB2-OPERATION-MSG-1                        
049600             PERFORM 9998-SQL-ABEND                                       
049700     END-EVALUATE.                                                        
049800*                                                                         
049900*                                                                         
050000******************************************************************        
050100*     CLOSE FILES.                                              **        
050200******************************************************************        
050300 9000-END-PROCESS.                                                        
050400*                                                                         
050500     CLOSE EXTRACT-FILE                                                   
050600           REPORT-FILE.                                                   
050700*                                                                         
050800     DISPLAY '**********************************'.                        
050900     DISPLAY '** SUKUT035 SUMMARY **'.                                    
051000     DISPLAY 'TOTAL INPUT RECS       = ' WS-RECORDS-READ-QTY.             
051100     DISPLAY 'TOTAL RECS WRITTEN     = ' WS-RECORDS-WRITE-QTY.            
051200     DISPLAY '**********************************'.                        
051300*                                                                         
051400******************************************************************        
051500*     SQL ERROR PROCESSING                                      **        
051600******************************************************************        
051700 9998-SQL-ABEND.                                                          
051800     MOVE SQLCODE     TO PV-SQLCODE.                                      
051900     DISPLAY '** ABEND           ** = SQLABEND'.                          
052000     DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
052100     DISPLAY '** PARAGRAPH       ** = ' PV-ABEND-PARA-NAME                
052200     DISPLAY '** TABLE AFFECTED  ** = ' PV-DB2-TABLE-NAME.                
052300     DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MSG-1.           
052400     DISPLAY '**                 ** = ' PV-DB2-OPERATION-MSG-2.           
052500     DISPLAY '** SQLCODE         ** = ' PV-SQLCODE.                       
052600     COPY DPPD004.                                                        
052700                                                                          
052800     MOVE +4013                  TO PV-ABEND-CODE.                        
052900     CALL 'ILBOABN0'   USING PV-ABEND-CODE.                               
053000*                                                                         
053100*                                                                         
