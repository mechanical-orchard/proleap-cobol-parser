000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    ARKUT106.                                                 
000300 AUTHOR.        COGNIZANT                                                 
000400 DATE-WRITTEN.  SEP 2012.                                                 
000500                                                                          
000600*************************************************************             
000700*   READS THE INPUT FILE AND UPDATE THE SAME FILE WITH      *             
000800*   UNIX FILENAMES APPENDED WITH DATE RETREIVED FROM THE    *             
000900*   ANOTHER INPUT FILE. I.E REPLACE yymmdd IN THE INPUT FILE*             
000910*   WITH DATE FROM THE ANOTHER INPUT FILE.                  *             
001000*                                                           *             
001100*   INPUT:                                                  *             
001200*     1. DATE FILE                                          *             
001210*     2. FTP COMMAND FILE                                   *             
001300*                                                           *             
001400*   OUTPUT:                                                 *             
072263*     1. NEW OUTPUT FTP DATE FILE                           *             
001600*                                                           *             
001700*************************************************************             
001800**  PROD DATE  INIT  CHNG CNTL   DESCRIPTION                              
001900**  ---------- ----  ---------   ----------------------------             
002000**  09/20/2012  CTS   CR33446    NEW PROGRAM                              
002100*                                                           *             
072263*   01/22/2014  CTS   CR72263    ADDED OUTPUT FILE TO WRITE               
072263*                                FTP DATE FILE.                           
078034*   04/03/2014  CTS   CR78034    ADDED INSPECT TO ACCEPT NEW              
078034*                                DATE FORMAT MMDDYYYY.                    
078034*                                CHANGES TAGGED WITH 078034               
002200*************************************************************             
002300*************************************************************             
002400 ENVIRONMENT DIVISION.                                                    
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800     SELECT DATE-FILE      ASSIGN TO INP01.                               
002810     SELECT FTPCARD-FILE   ASSIGN TO INP02.                               
072263     SELECT OUTPUT-FILE    ASSIGN TO OUT01.                               
002900                                                                          
003400 DATA DIVISION.                                                           
003500 FILE SECTION.                                                            
003600                                                                          
003700 FD  DATE-FILE                                                            
003800     RECORDING MODE IS F                                                  
003900     LABEL RECORDS ARE STANDARD                                           
004000     BLOCK CONTAINS 0 RECORDS                                             
004100     DATA RECORD IS DATE-REC.                                             
004200                                                                          
004300 01  DATE-REC                        PIC X(80).                           
004400                                                                          
004500 FD  FTPCARD-FILE                                                         
004600     RECORDING MODE IS F                                                  
004700     LABEL RECORDS ARE STANDARD                                           
004800     BLOCK CONTAINS 0 RECORDS                                             
004900     DATA RECORD IS FTPCARD-REC.                                          
005000                                                                          
005100 01  FTPCARD-REC                     PIC X(80).                           
005200                                                                          
072263 FD  OUTPUT-FILE                                                          
072263     RECORDING MODE IS F                                                  
072263     LABEL RECORDS ARE STANDARD                                           
072263     BLOCK CONTAINS 0 RECORDS                                             
072263     DATA RECORD IS OUTPUT-REC.                                           
005000                                                                          
072263 01  OUTPUT-REC                      PIC X(80).                           
012700******************************************************************        
012800*                       WORKING STORAGE                                   
012900******************************************************************        
012901 WORKING-STORAGE SECTION.                                                 
012902                                                                          
013200 01  W-START                         PIC  X(40)                           
013300     VALUE 'ARKUT106 - WORKING STORAGE BEGINS HERE'.                      
013500                                                                          
013600******************************************************************        
013700*                         PROGRAM CONSTANTS                               
013800******************************************************************        
013801 01  PS-PROGRAM-CONSTANTS.                                                
013802     05  PC-PROGRAM-NAME             PIC X(08)   VALUE 'ARKUT106'.        
013803                                                                          
013804******************************************************************        
013805*                         PROGRAM VARIABLE                                
013806******************************************************************        
013807 01  PV-DATE-IN.                                                          
013808     05 PV-DATE-YYMMDD              PIC X(06).                            
078034     05 PV-DATE-CC                  PIC X(02).                            
013809     05 FILLER                      PIC X(72).                            
                                                                                
078034 01  PV-DATE                        PIC X(08).                            
013810                                                                          
013811 01  PV-VARIABLES.                                                        
013812     05  PV-READ-COUNT               PIC 9(10)   VALUE ZEROS.             
078034     05  PV-TALLY-CNTR               PIC 9(02).                           
013813                                                                          
013814 01  PV-FTPCARD-FILE.                                                     
013815     05  PV-FTPCARD-RECORD           PIC X(80)   VALUE SPACES.            
013816                                                                          
013817******************************************************************        
013818*                         SWITCHES                                        
013819******************************************************************        
013820 01  PS-PROGRAM-SWITCHES.                                                 
013821     05  PS-END-OF-FILE-IND          PIC X(01)   VALUE 'N'.               
013822         88  PS-NOT-END-OF-FILE                  VALUE 'N'.               
013823         88  PS-END-OF-FILE                      VALUE 'Y'.               
013824     05  PS-END-OF-DATEFILE-IND      PIC X(01)   VALUE 'N'.               
013825         88  PS-NOT-END-OF-DATEFILE              VALUE 'N'.               
013826         88  PS-END-OF-DATEFILE                  VALUE 'Y'.               
013827                                                                          
024701******************************************************************        
024702* MISC ABEND - VARIABLES                                                  
024703******************************************************************        
024704 01  PV-MISC-ABEND-FIELDS.                                                
024705     05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.              
024706     05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.              
024707     05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.              
024708************************************************************              
024709* THIS IS A WORKING STORAGE LAYOUT FOR ABEND CODE VALUES.                 
024710*     USE THE SET COMMAND TO PROVIDE VALUES TO ABEND-CODE.                
024711************************************************************              
024712                                                                          
033300 01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC.         
033400     88  ABEND-4001-FILE-EMTY              VALUE +4001.                   
033401                                                                          
033402 PROCEDURE DIVISION.                                                      
071908******************************************************************        
071909* MAIN DRIVER                                                             
071910******************************************************************        
071911 0000-MAINLINE.                                                           
071912                                                                          
071913     PERFORM 1000-INITIALIZATION.                                         
071914                                                                          
071915     PERFORM 2000-PROCESS-FTPCARD-FILE                                    
071916       UNTIL PS-END-OF-FILE.                                              
071917                                                                          
071918     PERFORM 3000-EOJ-PROCESSING.                                         
071919                                                                          
071920     GOBACK.                                                              
071921                                                                          
071922 1000-INITIALIZATION.                                                     
071923                                                                          
071924     DISPLAY '***    ARKUT106    ***'.                                    
071925                                                                          
071926     OPEN INPUT  DATE-FILE                                                
072263                 FTPCARD-FILE.                                            
072263     OPEN OUTPUT OUTPUT-FILE.                                             
071928                                                                          
071929     PERFORM 1010-READ-DATE-FILE.                                         
071930                                                                          
087002******************************************************************        
087003* 1010-READ-DATE-FILE                                                     
087004*      READS THE FILE AND GET THE RUN DATE.                               
087005******************************************************************        
087006 1010-READ-DATE-FILE.                                                     
087007                                                                          
087008     READ DATE-FILE INTO PV-DATE-IN                                       
087009        AT END                                                            
087010           SET PS-END-OF-DATEFILE TO TRUE.                                
087011                                                                          
087012     IF  PS-END-OF-DATEFILE                                               
087013         MOVE '1010-READ-DATE-FILE'     TO PV-PARAGRAPH-NAME              
087014         MOVE 'EMPTY RUNDATE FILE  '    TO PV-OPERATION-MSG-1             
087016         SET ABEND-4001-FILE-EMTY       TO TRUE                           
087017         PERFORM 9900-ABEND-ROUTINE                                       
087018     END-IF.                                                              
087019                                                                          
087020     DISPLAY 'INPUT DATE  = ' PV-DATE-IN.                                 
087021                                                                          
095102******************************************************************        
095103* 2000-PROCESS-IN-FILE.                                                   
095104*      READ THE INPUT FILE CONTAINING THE FTP COMMANDS AND APPEND         
095105*      DATE TO UNIX FILENAMES.                                            
095106******************************************************************        
095107 2000-PROCESS-FTPCARD-FILE.                                               
095108                                                                          
095109     READ FTPCARD-FILE                                                    
095110         AT END                                                           
095111             SET PS-END-OF-FILE TO TRUE.                                  
095112                                                                          
095113     IF PS-NOT-END-OF-FILE                                                
095114         ADD +1             TO PV-READ-COUNT                              
095115         MOVE FTPCARD-REC   TO PV-FTPCARD-RECORD                          
078034         INSPECT PV-FTPCARD-RECORD TALLYING PV-TALLY-CNTR                 
078034                                   FOR ALL 'yymmdd'                       
078034     IF PV-TALLY-CNTR >= 1                                                
078034         INSPECT PV-FTPCARD-RECORD REPLACING ALL 'yymmdd'                 
078034                                   BY PV-DATE-YYMMDD                      
078034         WRITE OUTPUT-REC FROM PV-FTPCARD-RECORD                          
078034     ELSE                                                                 
078034         IF PV-TALLY-CNTR = 0                                             
078034             MOVE PV-DATE-IN(1:8)    TO PV-DATE                           
078034             INSPECT PV-FTPCARD-RECORD REPLACING ALL 'mmddyyyy'           
078034                                       BY PV-DATE                         
078034             WRITE OUTPUT-REC FROM PV-FTPCARD-RECORD                      
078034     END-IF.                                                              
095120                                                                          
095121******************************************************************        
095122* 3000-EOJ-PROCESSING.                                                    
095123*      END OF PROGRAM TASK                                                
095124*      CLOSE THE FILES                                                    
095125******************************************************************        
095126 3000-EOJ-PROCESSING.                                                     
095127                                                                          
095128     IF PS-END-OF-FILE AND PV-READ-COUNT = 0                              
095129         MOVE '0000-MAINLINE    '       TO PV-PARAGRAPH-NAME              
095130         MOVE 'EMPTY FTP CMD FILE  '    TO PV-OPERATION-MSG-1             
095131         SET ABEND-4001-FILE-EMTY       TO TRUE                           
095132         PERFORM 9900-ABEND-ROUTINE                                       
095133     END-IF                                                               
095134                                                                          
095135     CLOSE  DATE-FILE                                                     
095136            FTPCARD-FILE                                                  
072263            OUTPUT-FILE.                                                  
095302*****************************************************************         
095303* 9900-ABEND-ROUTINE                                                      
095304*     ABEND PROGRAM WHEN A ERROR OCCURS.                                  
095305*****************************************************************         
095306 9900-ABEND-ROUTINE.                                                      
095307     DISPLAY '** ABEND           **'.                                     
095308     DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
095309     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
095310     DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
095311     DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.               
095312     CALL 'ILBOABN0' USING ABEND-CODE.                                    
095313                                                                          
