000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.         RVKUT033.                                            
000400 AUTHOR.             VIRGENE LENNEMAN.                                    
000500 INSTALLATION.       KOHLS DEPARTMENT STORES.                             
000600 DATE-WRITTEN.       MAY 2008.                                            
000700 DATE-COMPILED.                                                           
000700*****************************************************************         
000700*  THIS PROGRAM PREPARES THE MOS EXTRACT FILE TO BE PROCESSED BY          
000700*  RVKUP003 UNDER THE DB2 CHECKPOINT RESTART SYSTEM.                      
000700*                                                                         
000700*  THE PROGRAM COMMUNICATES WITH THE TRANSACTION PREPARATION              
000700*  MODULE (DPKUT900) USING FUNCTION CODES (OPEN, WRITE, CLOSE)            
000700*  AND CHECKS THE RETURN CODE AFTER EACH REQUEST. IF AN INVALID           
000700*  RETURN CODE IS RECEIVED ANY INFORMATION THAT MAYBE HELPFUL IN          
000700*  RESOLVING THE BAD RETURN CODE IS DISPLAYED IN THE SYSOUT.              
000700*****************************************************************         
000700                                                                          
000800 ENVIRONMENT DIVISION.                                                    
000900 CONFIGURATION SECTION.                                                   
001000                                                                          
001100 SOURCE-COMPUTER.    IBM-3090.                                            
001200 OBJECT-COMPUTER.    IBM-3090.                                            
001300                                                                          
001400 INPUT-OUTPUT SECTION.                                                    
001500 FILE-CONTROL.                                                            
001600                                                                          
001700     SELECT IN-MOS-FILE                  ASSIGN TO INP01.                 
001800                                                                          
001900 DATA DIVISION.                                                           
002000 FILE SECTION.                                                            
002100                                                                          
002200 FD  IN-MOS-FILE                                                          
002300     LABEL RECORDS ARE STANDARD                                           
002400     RECORDING MODE IS F                                                  
002500     BLOCK CONTAINS 0 RECORDS                                             
002600     RECORD CONTAINS 260 CHARACTERS                                       
002700     DATA RECORD IS IN-MOS-RECORD.                                        
002800 01  IN-MOS-RECORD        PIC  X(260).                                    
002900                                                                          
003000*                                                                         
003100 WORKING-STORAGE SECTION.                                                 
003300 01  ABEND-CODE                  PIC S9(04)     VALUE +0  COMP.           
003400                                                                          
003500 01  PROGRAM-CONSTANTS.                                                   
003600     05  PC-PROGRAM-NAME         PIC X(08)      VALUE 'RVKUT033'.         
003700     05  PC-TRAN-PREP-FUNC-CODES.                                         
003800         10  PC-TRAN-PREP-FUNC-OPEN                                       
003900                                 PIC X(05)      VALUE 'OPEN '.            
004000         10  PC-TRAN-PREP-FUNC-WRITE                                      
004100                                 PIC X(05)      VALUE 'WRITE'.            
004200         10  PC-TRAN-PREP-FUNC-CLOSE                                      
004300                                 PIC X(05)      VALUE 'CLOSE'.            
004400     05  PC-TRANS-RECORD-LENGTH  PIC S9(04)     VALUE +260 COMP.          
004500     05  PC-JOB-NAME             PIC X(08)      VALUE 'RVK020  '.         
004600     05  PC-PLAN-NAME            PIC X(08)      VALUE 'RVKUP003'.         
004700                                                                          
004800 01  PROGRAM-SWITCHES.                                                    
005400     05  PS-TRANS-EOF-SW         PIC X(01)      VALUE 'N'.                
005500         88  PS-TRANS-EOF                       VALUE 'Y'.                
005400     05  PS-1ST-TRANS-REC-READ   PIC X(01)      VALUE 'Y'.                
005500         88  PS-1ST-TRANS-REC                   VALUE 'Y'.                
005500         88  PS-NOT-1ST-TRANS-REC               VALUE 'N'.                
004700                                                                          
004800 01  PROGRAM-VARIABLES.                                                   
004900     05  PV-CHECKPOINT-TYPE      PIC X(01)      VALUE SPACE.              
005000         88  CONTROL-BREAK                      VALUE 'C'.                
005100         88  LOGICAL-TRANSACTION                VALUE 'L'.                
005200         88  TIME-INTERVAL                      VALUE 'T'.                
005300         88  REQUESTED-BY-PROGRAM               VALUE 'R'.                
005400     05  PV-HOLD-DOC-NBR         PIC S9(07)V COMP-3.                      
005800     05  PV-CURRENT-DATE         PIC X(10).                               
005900                                                                          
006000*     MOS EXTRACT RECORD                                                  
006100     COPY RVRD056.                                                        
006200*                                                                         
006300*     TRANSACTION PREPARATION MODULE LINKAGE SECTION.                     
006400*                                                                         
015300******************************************************************        
015400*  TRANSACTION PREPARATION MODULE LINKAGE SECTION                         
015500******************************************************************        
260107     COPY DPWS990.                                                        
015700                                                                          
006500     COPY DPLS000.                                                        
006600                                                                          
006700     EXEC SQL                                                             
006800         INCLUDE SQLCA                                                    
006900     END-EXEC.                                                            
007000     COPY SQLCA2.                                                         
007100                                                                          
007200 PROCEDURE DIVISION.                                                      
007300******************************************************************        
007400* 0000-MAINLINE-PROCESSING                                       *        
007500*      CONTROLS FLOW OF THE PROGRAM.                             *        
007600******************************************************************        
007700 0000-MAINLINE-PROCESSING.                                                
007900     PERFORM 1000-INITIALIZE.                                             
008000     PERFORM 2000-PROCESS UNTIL PS-TRANS-EOF                              
008100     PERFORM 9000-EOJ.                                                    
008200                                                                          
008300     GOBACK.                                                              
008400                                                                          
008500 1000-INITIALIZE.                                                         
008700     OPEN INPUT IN-MOS-FILE.                                              
008800                                                                          
009100     EXEC SQL                                                             
009200          SET :PV-CURRENT-DATE = CURRENT DATE                             
009300     END-EXEC.                                                            
009400                                                                          
009500     PERFORM 8000-READ-MOS-TRANS-FILE.                                    
009600                                                                          
010200     PERFORM 1100-ISSUE-OPEN-REQUEST.                                     
010400                                                                          
010700 1100-ISSUE-OPEN-REQUEST.                                                 
010800                                                                          
010900     MOVE PC-TRAN-PREP-FUNC-OPEN      TO DP000-FUNC-CODE.                 
011000     MOVE PC-JOB-NAME                 TO DP000-JOB-NAME.                  
011100     MOVE PC-PLAN-NAME                TO DP000-PLAN-NAME.                 
011200     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
011300     MOVE DP000-CKPT-TYPE-CODE        TO PV-CHECKPOINT-TYPE.              
011400                                                                          
011500 2000-PROCESS.                                                            
011600     IF PS-1ST-TRANS-REC                                                  
011600         MOVE RV056-DOC-NBR       TO PV-HOLD-DOC-NBR                      
011600         SET PS-NOT-1ST-TRANS-REC TO TRUE                                 
011600     END-IF.                                                              
011400                                                                          
011600     IF CONTROL-BREAK                                                     
011600         PERFORM 2100-CKPT-BY-CNTL-BRK                                    
011600     ELSE                                                                 
011600         IF LOGICAL-TRANSACTION                                           
011600             PERFORM 2200-CKPT-BY-LOGICAL-TRANS                           
011600         ELSE                                                             
011600             PERFORM 2300-CKPT-BY-TIME-OR-REQUEST                         
011600         END-IF                                                           
011600     END-IF.                                                              
011900                                                                          
011800     PERFORM 8000-READ-MOS-TRANS-FILE.                                    
011900                                                                          
011900******************************************************************        
011900* THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSACTIONS          
011900* RECORD "ON" IF THERE IS A CHANGE IN DOCUMENT NUMBER. AFTER THE          
011900* DOCUMENT NUMEBR HAS BEEN FORMATTED A WRITE REQUEST IS ISSUED TO         
011900* THE TRANSACTION PREPARATION MODULE                                      
011900******************************************************************        
012000 2100-CKPT-BY-CNTL-BRK.                                                   
012400     IF PV-HOLD-DOC-NBR = RV056-DOC-NBR                                   
012400         MOVE 'N' TO DP000-CHECKPOINT-IND                                 
012400     ELSE                                                                 
012400*        DISPLAY 'CHECKPOINT IND SET TO Y'                                
012400*        DISPLAY 'RV056-DOC-NBR '  RV056-DOC-NBR                          
012400*        DISPLAY 'PV-HOLD-DOC-NBR '  PV-HOLD-DOC-NBR                      
012400         MOVE 'Y'           TO DP000-CHECKPOINT-IND                       
012400         MOVE RV056-DOC-NBR TO PV-HOLD-DOC-NBR                            
012400     END-IF                                                               
012400                                                                          
012200     MOVE LENGTH OF RV056-HDR-DTL-RECORD                                  
012300                                      TO DP000-TRANS-REC-LENGTH.          
012400                                                                          
012500     MOVE RV056-HDR-DTL-RECORD        TO DP000-TRANS-REC.                 
012600                                                                          
012700     MOVE PC-TRAN-PREP-FUNC-WRITE     TO DP000-FUNC-CODE.                 
012800     MOVE PC-JOB-NAME                 TO DP000-JOB-NAME.                  
012900     MOVE PC-PLAN-NAME                TO DP000-PLAN-NAME.                 
013000                                                                          
013100     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
013200                                                                          
011900******************************************************************        
011900* THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSACTIONS          
011900* RECORD "ON" IF THERE IS A CHANGE IN DOCUMENT NUMBER. AFTER THE          
011900* DOCUMENT NUMEBR HAS BEEN FORMATTED A WRITE REQUEST IS ISSUED TO         
011900* THE TRANSACTION PREPARATION MODULE                                      
011900******************************************************************        
012000 2200-CKPT-BY-LOGICAL-TRANS.                                              
012400     IF PV-HOLD-DOC-NBR = RV056-DOC-NBR                                   
012400         MOVE 'N' TO DP000-CHECKPOINT-IND                                 
012400     ELSE                                                                 
012400         MOVE 'Y' TO DP000-CHECKPOINT-IND                                 
012400         MOVE RV056-DOC-NBR TO PV-HOLD-DOC-NBR                            
012400     END-IF                                                               
012400                                                                          
012200     MOVE LENGTH OF RV056-HDR-DTL-RECORD                                  
012300                                      TO DP000-TRANS-REC-LENGTH.          
012400                                                                          
012500     MOVE RV056-HDR-DTL-RECORD        TO DP000-TRANS-REC.                 
012600                                                                          
012700     MOVE PC-TRAN-PREP-FUNC-WRITE     TO DP000-FUNC-CODE.                 
012800     MOVE PC-JOB-NAME                 TO DP000-JOB-NAME.                  
012900     MOVE PC-PLAN-NAME                TO DP000-PLAN-NAME.                 
013000                                                                          
013100     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
013200                                                                          
011900******************************************************************        
011900* THIS ROUTINE BUILDS THE TRANSACTION RECORD AND ISSUES A WRITE           
011900* REQUEST TO THE TRANSACTION PREPARATION MODULE IF CHECKPOINTS            
011900* ARE TAKEN BY TIME INTERVAL (SECONDS) OR REQUESTED BY THE                
011900* MAINTENANCE PROGRAM.                                                    
011900******************************************************************        
012000 2300-CKPT-BY-TIME-OR-REQUEST.                                            
012400     MOVE 'N'                         TO DP000-CHECKPOINT-IND             
012400                                                                          
012200     MOVE LENGTH OF RV056-HDR-DTL-RECORD                                  
012300                                      TO DP000-TRANS-REC-LENGTH.          
012400                                                                          
012500     MOVE RV056-HDR-DTL-RECORD        TO DP000-TRANS-REC.                 
012600                                                                          
012700     MOVE PC-TRAN-PREP-FUNC-WRITE     TO DP000-FUNC-CODE.                 
012800     MOVE PC-JOB-NAME                 TO DP000-JOB-NAME.                  
012900     MOVE PC-PLAN-NAME                TO DP000-PLAN-NAME.                 
013000                                                                          
013100     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
013200                                                                          
013300 8000-READ-MOS-TRANS-FILE.                                                
013500     READ IN-MOS-FILE INTO RV056-HDR-DTL-RECORD                           
013600         AT END                                                           
013700           SET PS-TRANS-EOF           TO TRUE                             
010000           DISPLAY ' RVKUT033 -- INP01 IS EMPTY '                         
013800     END-READ.                                                            
013900                                                                          
014000 9000-EOJ.                                                                
014100     CLOSE IN-MOS-FILE.                                                   
014200                                                                          
014600     PERFORM 9100-ISSUE-CLOSE-REQUEST.                                    
014800                                                                          
014900 9100-ISSUE-CLOSE-REQUEST.                                                
015100     MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.                     
015200     MOVE PC-JOB-NAME             TO DP000-JOB-NAME.                      
015300     MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.                     
015400                                                                          
015500     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
015600                                                                          
015700     COPY DPPD000.                                                        
015800*                                                                         
015900*     TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES.             
016000*                                                                         
