00001 *************************                                         09/29/88
00002  IDENTIFICATION DIVISION.                                         DPKUT006
00003 *************************                                            LV007
00004  PROGRAM-ID.      DPKUT006.                                          CL**7
00005  AUTHOR.          D.MEDORA, CAP GEMINI AMERICA.                      CL**3
00006  INSTALLATION.    KOHLS DEPARTMENT STORES.                           CL**7
00007  DATE-WRITTEN.    AUGUST 12, 1988.                                   CL**2
00008  DATE-COMPILED.                                                      CL**3
00009                                                                      CL**7
00010 *             THIS PROGRAM USES THE SORTED OUTPUT FROM PROGRAM       CL**7
00011 *             DPKUT003, DDNAME TEMPPGMI, AND THE SORTED OUTPUT       CL**7
00012 *             FROM DPKUT002, DDNAME PROCTBLO, AS INPUT TO CREATE     CL**7
00013 *             A FILE TO BE USED AS INPUT TO PROGRAM DPKUT004         CL**7
00014 *             WHICH CREATES FILE USED TO LOAD ONE OF THE SYSTEM      CL**7
00015 *             CROSS REFERENCE TABLES:                                CL**7
00016 *                           PROGRAM TABLE                            CL**7
00017                                                                      CL**7
00018  ENVIRONMENT DIVISION.                                               CL**7
00019  CONFIGURATION SECTION.                                              CL**7
00020  SOURCE-COMPUTER. IBM-3090.                                          CL**7
00021  OBJECT-COMPUTER. IBM-3090.                                          CL**7
00022                                                                      CL**7
00023  INPUT-OUTPUT SECTION.                                               CL**7
00024  FILE-CONTROL.                                                       CL**7
00025      SELECT PROCSTEP-TABLE-FILE        ASSIGN TO INP01.              CL**5
00026      SELECT TEMP-PROGRAM-DESC-FILE     ASSIGN TO INP02.              CL**5
00027      SELECT PGM-DATA-FILE              ASSIGN TO OUT01.              CL**5
00028                                                                      CL**7
00029 ***************                                                      CL**7
00030  DATA DIVISION.                                                      CL**7
00031 ***************                                                      CL**7
00032  FILE SECTION.                                                       CL**7
00033  FD  TEMP-PROGRAM-DESC-FILE                                          CL**7
00034                              BLOCK CONTAINS 0 RECORDS                CL**7
00035                              RECORD CONTAINS 60 CHARACTERS           CL**7
00036                              LABEL RECORDS ARE STANDARD              CL**7
00037                              RECORDING MODE F                        CL**7
00038                              DATA RECORD IS TEMP-PROGRAM-DESC-REC.   CL**7
00039  01  TEMP-PROGRAM-DESC-REC   PIC X(60).                              CL**7
00040                                                                      CL**7
00041  FD  PROCSTEP-TABLE-FILE                                             CL**7
00042                              BLOCK CONTAINS 0 RECORDS                CL**7
00043                              RECORD CONTAINS 24 CHARACTERS           CL**7
00044                              LABEL RECORDS ARE STANDARD              CL**7
00045                              RECORDING MODE F                        CL**7
00046                              DATA RECORD IS PROCSTEP-TABLE-REC.      CL**7
00047  01  PROCSTEP-TABLE-REC      PIC X(24).                              CL**7
00048                                                                      CL**7
00049  FD  PGM-DATA-FILE                                                   CL**7
00050                              BLOCK CONTAINS 0 RECORDS                CL**7
00051                              RECORD CONTAINS 60 CHARACTERS           CL**7
00052                              LABEL RECORDS ARE STANDARD              CL**7
00053                              RECORDING MODE F                        CL**7
00054                              DATA RECORD IS PGM-TABLE-DATA-REC.      CL**7
00055  01  PGM-DATA-REC            PIC X(60).                              CL**7
00056                                                                      CL**7
00057      EJECT                                                           CL**7
00058                                                                      CL**7
00059  WORKING-STORAGE SECTION.                                            CL**7
00060                                                                      CL**7
00061 **   TEMP-PROGRAM-DESC-FILE LAYOUT (INPUT)  **                       CL**7
00062                                                                      CL**7
00063  01  TEMPPGMI-RECORD.                                                CL**7
00064      05  TEMPPGMI-PGM-NAME-PAN.                                      CL**7
00065          10  TEMPPGMI-PGM-NAME   PIC X(08)      VALUE SPACES.        CL**7
00066          10  FILLER              PIC X(02)      VALUE SPACES.        CL**7
00067      05  TEMPPGMI-PGM-DESC       PIC X(50)      VALUE SPACES.        CL**7
00068                                                                      CL**7
00069                                                                      CL**7
00070 **   PROCSTEP-TABLE-FILE LAYOUT (INPUT)  **                          CL**7
00071                                                                      CL**7
00072  01  PROCTBLI-RECORD.                                                CL**7
00073      05  PROCTBLI-PROC-NAME      PIC X(08)      VALUE SPACES.        CL**7
00074      05  PROCTBLI-PROCSTEP-NAME  PIC X(08)      VALUE SPACES.        CL**7
00075      05  PROCTBLI-PROGRAM-NAME   PIC X(08)      VALUE SPACES.        CL**7
00076                                                                      CL**7
00077                                                                      CL**7
00078 **   PGM-DATA-FILE LAYOUT (OUTPUT)  **                               CL**7
00079                                                                      CL**7
00080  01  PGMDATAO-RECORD.                                                CL**7
00081      05  PGMDATAO-PGM-NAME-PAN.                                      CL**7
00082          10  PGMDATAO-PGM-NAME   PIC X(08)      VALUE SPACES.        CL**7
00083          10  FILLER              PIC X(02)      VALUE SPACES.        CL**7
00084      05  PGMDATAO-PGM-DESC       PIC X(50)      VALUE SPACES.        CL**7
00085                                                                      CL**7
00086                                                                      CL**7
00087  01  WS-WORK-AREAS.                                                  CL**7
00088      05  WS-LAST-PROCTBLI-PGM-NAME                                   CL**7
00089                                  PIC X(08)      VALUE SPACES.        CL**7
00090      05  PROCTBLI-KEY            PIC X(08)      VALUE SPACES.        CL**3
00091      05  TEMPPGMI-KEY            PIC X(08)      VALUE SPACES.        CL**3
00092                                                                      CL**7
00093                                                                      CL**7
00094  01  PROGRAM-SWITCHES.                                               CL**7
00095      05  PS-NO                   PIC X          VALUE 'N'.           CL**7
00096      05  PS-YES                  PIC X          VALUE 'Y'.           CL**7
00097      05  PS-EOF-TEMPPGMI         PIC X          VALUE 'N'.           CL**7
00098          88  EOF-TEMPPGMI                       VALUE 'Y'.           CL**7
00099      05  PS-EOF-PROCTBLI         PIC X          VALUE 'N'.           CL**7
00100          88  EOF-PROCTBLI                       VALUE 'Y'.           CL**7
00101                                                                      CL**7
00102      EJECT                                                           CL**7
00103 ********************                                                 CL**7
00104  PROCEDURE DIVISION.                                                 CL**7
00105 ********************                                                 CL**7
00106                                                                      CL**7
00107                                                                      CL**7
00108      OPEN INPUT  TEMP-PROGRAM-DESC-FILE                              CL**7
00109                  PROCSTEP-TABLE-FILE                                 CL**7
00110           OUTPUT PGM-DATA-FILE.                                      CL**7
00111                                                                      CL**7
00112                                                                      CL**7
00113      PERFORM X100-READ-TEMPPGMI-FILE  THRU  X100-EXIT.               CL**3
00114                                                                      CL**7
00115                                                                      CL**7
00116      PERFORM X200-READ-PROCTBLI-FILE  THRU  X200-EXIT.               CL**3
00117      MOVE PROCTBLI-PROGRAM-NAME   TO WS-LAST-PROCTBLI-PGM-NAME.      CL**7
00118                                                                      CL**7
00119                                                                      CL**7
00120      PERFORM A100-PROCESS-PGMDATAO-FILE  THRU  A100-EXIT             CL**4
00121              UNTIL PROCTBLI-KEY  EQUAL TO  HIGH-VALUES               CL**3
00122              AND   TEMPPGMI-KEY  EQUAL TO  HIGH-VALUES.              CL**3
00123                                                                      CL**7
00124                                                                      CL**7
00125      CLOSE       TEMP-PROGRAM-DESC-FILE                              CL**7
00126                  PROCSTEP-TABLE-FILE                                 CL**7
00127                  PGM-DATA-FILE.                                      CL**7
00128                                                                      CL**7
00129                                                                      CL**7
00130      MOVE ZEROES TO RETURN-CODE.                                     CL**3
00131      GOBACK.                                                         CL**3
00132                                                                      CL**7
00133      EJECT                                                           CL**7
00134                                                                      CL**7
00135 ******************************************************************   CL**7
00136 *** A100 THIS PARAGRAPH PROCESSES THE INPUT FILES BY MATCHING THE    CL**7
00137 ***      PROGRAM NAMES FROM EACH TO PRODUCE A COMPLETE LIST IN THE   CL**7
00138 ***      OUTPUT FILE.                                                CL**7
00139 ******************************************************************   CL**7
00140  A100-PROCESS-PGMDATAO-FILE.                                         CL**7
00141                                                                      CL**7
00142                                                                      CL**7
00143      IF TEMPPGMI-KEY  EQUAL TO  PROCTBLI-KEY                         CL**3
00144          MOVE TEMPPGMI-RECORD         TO PGMDATAO-RECORD             CL**3
00145          WRITE PGM-DATA-REC         FROM PGMDATAO-RECORD             CL**3
00146          PERFORM X100-READ-TEMPPGMI-FILE  THRU  X100-EXIT            CL**3
00147          MOVE PROCTBLI-PROGRAM-NAME TO WS-LAST-PROCTBLI-PGM-NAME     CL**3
00148          PERFORM X200-READ-PROCTBLI-FILE  THRU  X200-EXIT            CL**3
00149                  UNTIL PROCTBLI-PROGRAM-NAME  NOT EQUAL TO           CL**3
00150                                        WS-LAST-PROCTBLI-PGM-NAME     CL**3
00151                  OR    EOF-PROCTBLI                                  CL**3
00152      ELSE                                                            CL**3
00153          IF TEMPPGMI-KEY  GREATER THAN  PROCTBLI-KEY                 CL**3
00154              MOVE SPACES                  TO PGMDATAO-RECORD         CL**3
00155              MOVE PROCTBLI-PROGRAM-NAME   TO PGMDATAO-PGM-NAME       CL**3
00156              WRITE PGM-DATA-REC         FROM PGMDATAO-RECORD         CL**3
00157              MOVE PROCTBLI-PROGRAM-NAME TO                           CL**3
00158                                        WS-LAST-PROCTBLI-PGM-NAME     CL**3
00159              PERFORM X200-READ-PROCTBLI-FILE  THRU  X200-EXIT        CL**3
00160                      UNTIL PROCTBLI-PROGRAM-NAME  NOT EQUAL TO       CL**3
00161                                        WS-LAST-PROCTBLI-PGM-NAME     CL**3
00162                      OR    EOF-PROCTBLI                              CL**3
00163          ELSE                                                        CL**3
00164              IF TEMPPGMI-KEY  LESS THAN  PROCTBLI-KEY                CL**3
00165                  MOVE TEMPPGMI-RECORD         TO PGMDATAO-RECORD     CL**3
00166                  WRITE PGM-DATA-REC         FROM PGMDATAO-RECORD     CL**3
00167                  PERFORM X100-READ-TEMPPGMI-FILE  THRU  X100-EXIT    CL**6
00168              END-IF                                                  CL**6
00169          END-IF                                                      CL**6
00170      END-IF.                                                         CL**6
00171                                                                      CL**7
00172  A100-EXIT.                                                          CL**3
00173      EXIT.                                                           CL**7
00174                                                                      CL**3
00175      EJECT                                                           CL**7
00176                                                                      CL**7
00177 ******************************************************************   CL**7
00178 *** X100 THIS PARAGRAPH READS THE INPUT FILE FROM "DPKUT003".        CL**7
00179 ******************************************************************   CL**7
00180  X100-READ-TEMPPGMI-FILE.                                            CL**7
00181                                                                      CL**7
00182                                                                      CL**7
00183      READ TEMP-PROGRAM-DESC-FILE                                     CL**7
00184                                 INTO TEMPPGMI-RECORD                 CL**7
00185          AT END                                                      CL**7
00186              MOVE PS-YES          TO PS-EOF-TEMPPGMI                 CL**7
00187              MOVE HIGH-VALUES     TO  TEMPPGMI-KEY                   CL**3
00188              GO TO X100-EXIT.                                        CL**3
00189                                                                      CL**7
00190      MOVE TEMPPGMI-PGM-NAME       TO  TEMPPGMI-KEY.                  CL**3
00191                                                                      CL**7
00192  X100-EXIT.                                                          CL**3
00193      EXIT.                                                           CL**7
00194      EJECT                                                           CL**7
00195                                                                      CL**7
00196 ******************************************************************   CL**7
00197 *** X200 THIS PARAGRAPH READS THE INPUT FILE FROM "DPKUT002".        CL**7
00198 ******************************************************************   CL**7
00199  X200-READ-PROCTBLI-FILE.                                            CL**7
00200                                                                      CL**7
00201                                                                      CL**7
00202      READ PROCSTEP-TABLE-FILE   INTO PROCTBLI-RECORD                 CL**7
00203          AT END                                                      CL**7
00204              MOVE PS-YES          TO PS-EOF-PROCTBLI                 CL**7
00205              MOVE HIGH-VALUES     TO PROCTBLI-KEY                    CL**3
00206              GO TO X200-EXIT.                                        CL**3
00207                                                                      CL**7
00208      MOVE PROCTBLI-PROGRAM-NAME   TO PROCTBLI-KEY.                   CL**3
00209                                                                      CL**7
00210  X200-EXIT.                                                          CL**3
00211      EXIT.                                                           CL**7
00212      EJECT                                                           CL**7
