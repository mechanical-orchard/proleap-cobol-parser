       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.  MAKUT130.                                                   
      ******************************************************************        
      * THIS PROGRAM APPENDS AND INITIALIZES AN "ONHAND" FIELD TO      *        
      * RECORDS UNLOADED FROM DB2 TABLE TSKSTOO.                       *        
      ******************************************************************        
      * WR25478 - LINDA GRIESBACH                                      *        
      *           STORE NBR EXPANSION                                  *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 14 CHARACTERS.                                       
       01  INPUT-RECORD                    PIC X(14).                           
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 21 CHARACTERS                                        
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS PRINT-LINE.                                           
       01  OUTPUT-RECORD                   PIC X(21).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-SWITCHES.                                                         
           05  END-OF-FILE-SW              PIC  X(1)   VALUE 'N'.               
               88  END-OF-FILE                         VALUE 'Y'.               
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-WORK-STR-NBR-1           PIC S9(4)   COMP-3.                  
           05  WS-WORK-STR-NBR-2           PIC S9(4).                           
           05  WS-WORK-STR-NBR-3           PIC 9(4).                            
                                                                                
       01  WS-IN-TSKSTOO-RCD.                                                   
           05  WS-IN-ITM-NBR               PIC S9(15)  COMP-3.                  
           05  WS-IN-STR-NBR               PIC S9(4)   COMP.                    
           05  WS-IN-ONORD-UNT-QTY         PIC S9(9)   COMP.                    
                                                                                
       01  WS-OUT-TSKSTOO-RCD.                                                  
           05  WS-OUT-ITM-NBR              PIC S9(15)  COMP-3.                  
           05  WS-OUT-STR-NBR              PIC X(4).                            
           05  WS-OUT-ONHAND-UNT-QTY       PIC S9(7)   COMP-3.                  
           05  WS-OUT-ONORD-UNT-QTY        PIC S9(9)   COMP-3.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
       M0000-MAINLINE.                                                          
                                                                                
           OPEN INPUT  INPUT-FILE                                               
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM A1000-REFORMAT-TSKSTOO-FILE                                  
              THRU A1000-EXIT                                                   
              UNTIL END-OF-FILE.                                                
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       A1000-REFORMAT-TSKSTOO-FILE.                                             
                                                                                
           READ INPUT-FILE                                                      
               INTO WS-IN-TSKSTOO-RCD                                           
                   AT END                                                       
                       MOVE 'Y' TO END-OF-FILE-SW                               
                       GO TO A1000-EXIT.                                        
                                                                                
           MOVE WS-IN-ITM-NBR       TO WS-OUT-ITM-NBR.                          
           MOVE WS-IN-STR-NBR       TO WS-WORK-STR-NBR-1.                       
           MOVE WS-WORK-STR-NBR-1   TO WS-WORK-STR-NBR-2.                       
           MOVE WS-WORK-STR-NBR-2   TO WS-WORK-STR-NBR-3.                       
           MOVE WS-WORK-STR-NBR-3   TO WS-OUT-STR-NBR.                          
           MOVE ZEROS               TO WS-OUT-ONHAND-UNT-QTY.                   
           MOVE WS-IN-ONORD-UNT-QTY TO WS-OUT-ONORD-UNT-QTY.                    
                                                                                
           WRITE OUTPUT-RECORD FROM WS-OUT-TSKSTOO-RCD.                         
                                                                                
       A1000-EXIT.                                                              
           EXIT.                                                                
