00001  ID  DIVISION.                                                    06/23/95
00002  PROGRAM-ID.         ETKUT039.                                    ETKUT039
00003  AUTHOR.             STEVE FLUNKER.                                  LV002
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                        CL**1
00005  DATE-WRITTEN.       JUN 1995.                                       CL**1
00006  DATE-COMPILED.                                                      CL**1
00007                                                                      CL**1
00008 ******************************************************************   CL**1
00009 *  ETKUT039 - INVOICE RECYCLCE REVALIDATION UTILITY(TRAN PREP)       CL**1
00010 *  WORK REQUEST #  - 3044                                            CL**1
00011 ******************************************************************   CL**1
00012 *  THIS PROGRAM WILL CREATE A FILE WHICH HAS DATA TO BE UPDATED/     CL**1
00013 *  DELETED BY THE NEXT PROGRAM.  THIS WILL ALLOW FOR CHECKPOINT      CL**1
00014 *  RESTART ON THE UPDATE/DELETE.                                     CL**1
00015 ******************************************************************   CL**1
C94517*  CHANGED 11/06/2014    BY COGNIZANT       WR: CHG0094517                
C94517*                                                                         
C94517* EXTENDED PO# FROM 7 TO 8 DIGITS WHERE NEEDED.                           
C94517* RECOMPILED FOR APRD050 8 DIGITS PO# EXPANDED.                           
C94517*-----------------------------------------------------------------        
260107*  DATE  08/19/21                                                         
260107*  CHANGE MADE:                                                           
260107*   ADD COPYBOOK DPWS990 TO MAKE CALL TO DPKUT900 DYNAMIC                 
260107*  MADE BY: JIM HUNTER              WR# : CHG0260107                      
260107*-----------------------------------------------------------------        
00016 *                                                                    CL**1
00017  ENVIRONMENT DIVISION.                                               CL**1
00018 *                                                                    CL**1
00019  CONFIGURATION SECTION.                                              CL**1
00020 *                                                                    CL**1
00021  SOURCE-COMPUTER.        IBM-3090.                                   CL**1
00022  OBJECT-COMPUTER.        IBM-3090.                                   CL**1
00023 *                                                                    CL**1
00024  INPUT-OUTPUT SECTION.                                               CL**1
00025  FILE-CONTROL.                                                       CL**1
00026      SELECT  INV-STATUS-FILE                                         CL**1
00027          ASSIGN TO INP01.                                            CL**1
00028                                                                      CL**1
00029      SELECT  INV-OUTPUT-FILE                                         CL**1
00030          ASSIGN TO OUT02.                                            CL**1
00031                                                                      CL**1
00032  DATA DIVISION.                                                      CL**1
00033  FILE SECTION.                                                       CL**1
00034                                                                      CL**1
00035  FD  INV-STATUS-FILE                                                 CL**1
00036      LABEL RECORDS ARE STANDARD                                      CL**1
00037      RECORDING MODE IS F                                             CL**1
00038      BLOCK CONTAINS 0 RECORDS                                        CL**1
00039      RECORD CONTAINS 385 CHARACTERS                                  CL**1
00040      DATA RECORD IS INV-STATUS-RECORD.                               CL**1
00041                                                                      CL**1
00042  01  INV-STATUS-RECORD.                                              CL**1
00043      05  INV-STATUS-DB-KEY.                                          CL**1
00044          10  INV-STATUS-TP-DKEY           PIC S9(09) COMP.           CL**1
00045          10  INV-STATUS-TP-DKEY-TMST      PIC  X(26).                CL**1
00046          10  INV-STATUS-TRN-SET-SEQ-NBR   PIC S9(09) COMP.           CL**1
00047      COPY APRD050.                                                   CL**1
00048      05  INV-STATUS-UPD-DB-FLAG           PIC X.                     CL**1
00049          88  INV-UPDATE-DB-RECORD             VALUE 'Y'.             CL**1
00050          88  INV-NO-DB-ACTION                 VALUE 'N'.             CL**1
00051                                                                      CL**1
00052  FD  INV-OUTPUT-FILE                                                 CL**1
00053      LABEL RECORDS ARE STANDARD                                      CL**1
00054      RECORDING MODE IS F                                             CL**1
00055      BLOCK CONTAINS 0 RECORDS                                        CL**1
00056      RECORD CONTAINS 350 CHARACTERS                                  CL**1
00057      DATA RECORD IS INV-OUTPUT-RECORD.                               CL**1
00058                                                                      CL**1
00059  01  INV-OUTPUT-RECORD.                                              CL**1
00060      05  INV-OUTPUT-INVKEY               PIC X(50).                  CL**1
00061      05  INV-OUTPUT-DATA                 PIC X(300).                 CL**1
00062                                                                      CL**1
00063  WORKING-STORAGE SECTION.                                            CL**1
00064 *                                                                    CL**1
00065  01  INV-UPD-DEL-RECORD.                                             CL**1
00066      05 UD-TABLE-NAME             PIC X(07).                         CL**1
00067      05 UD-TP-DKEY                PIC S9(9) COMP.                    CL**1
00068      05 UD-TP-DKEY-TMST           PIC X(26).                         CL**1
00069      05 UD-DATA-AREA              PIC X(354).                        CL**1
00070      05 UD-TFGTRHL-AREA REDEFINES UD-DATA-AREA.                      CL**1
00071         10  UD-TRN-SET-CDE        PIC X(3).                          CL**1
00072         10  UD-TRN-DTL-TXT        PIC X(22).                         CL**1
00073         10  UD-RLSE-IND           PIC X(01).                         CL**1
00074         10  UD-RLSE-DTE           PIC X(10).                         CL**1
00075         10  UD-USER-ID            PIC X(08).                         CL**1
00076         10  FILLER                PIC X(310).                        CL**1
00077      05 UD-TTRPOHL-AREA REDEFINES UD-DATA-AREA.                      CL**1
00078         10  UD-PO-NBR             PIC 9(9).                          CL**1
00079         10  UD-DEPT-NBR           PIC X(03).                         CL**1
00080         10  UD-PO-RLSE-IND        PIC X(01).                         CL**1
00081         10  FILLER                PIC X(341).                        CL**1
00082      05 UD-TTRPOHL-AREA REDEFINES UD-DATA-AREA.                      CL**1
00083         10  UD-TRN-SET-SEQ-NBR PIC S9(9) COMP.                       CL**1
00084         10  UD-TRN-SET-DATA.                                         CL**1
00085             15  UD-INVKEY             PIC X(50).                     CL**1
00086             15  UD-INV-DATA           PIC X(300).                    CL**1
00087      05 UD-FLAG                   PIC X.                             CL**1
00088         88  UD-UPDATE-DB              VALUE 'U'.                     CL**1
00089         88  UD-DELETE-DB              VALUE 'D'.                     CL**1
00090         88  NO-DB-ACTION              VALUE ' '.                     CL**1
00091                                                                      CL**1
00092  01  WS-WORKING-STORAGE.                                             CL**1
00093      05  WS-NOSKU.                                                   CL**1
00094          10  FILLER                 PIC X(05) VALUE 'NOSKU'.         CL**1
00095          10  WS-NOSKU-CNT           PIC 9(03) VALUE ZERO.            CL**1
00096                                                                      CL**1
00097      05  WS-SAVE-AREA.                                               CL**1
00098          10 WS-SAVE-TP-DKEY         PIC S9(9) COMP.                  CL**1
00099          10 WS-SAVE-TP-DKEY-TMST    PIC X(26) VALUE SPACES.          CL**1
00100          10 WS-SAVE-TRN-SET-SEQ-NBR PIC S9(09) COMP.                 CL**1
00101          10 WS-SAVE-H-PO-NBR        PIC 9(9).                        CL**1
00102          10 WS-SAVE-H-DEPT-NBR      PIC X(03).                       CL**1
00103      05  WS-TP-ID-QUAL              PIC X(02) VALUE SPACES.          CL**1
00104      05  WS-TP-ID                   PIC X(15) VALUE SPACES.          CL**1
00105      05  WS-UNUPCS-UPC              PIC S9(15)V COMP-3.              CL**1
00106 *                                                                    CL**1
00107  01  PV-PROGRAM-VARIABLES.                                           CL**1
00108      05  PV-CHECKPOINT-TYPE           PIC  X     VALUE SPACE.        CL**1
00109          88  CONTROL-BREAK                       VALUE 'C'.          CL**1
00110          88  LOGICAL-TRANSACTION                 VALUE 'L'.          CL**1
00111          88  TIME-INTERVAL                       VALUE 'T'.          CL**1
00112          88  REQUESTED-BY-PROGRAM                VALUE 'R'.          CL**1
00113          88  NO-COMMIT                           VALUE ' '.          CL**1
00114      05  PV-DB2-KEYS.                                                CL**1
00115          10  PV-DB2-STR-NBR           PIC  X(03).                    CL**1
00116          10  PV-DB2-DOC-NBR           PIC S9(07) COMP-3.             CL**1
00117          10  PV-DB2-DEPT-NBR          PIC  X(03).                    CL**1
00118          10  PV-DB2-CL-NBR            PIC  X(03).                    CL**1
00119          10  FILLER                   REDEFINES                      CL**1
00120              PV-DB2-CL-NBR.                                          CL**1
00121              15  PV-DB2-CL-NBR-1      PIC  X.                        CL**1
00122              15  PV-DB2-CL-NBR-2-3    PIC  X(02).                    CL**1
00123          10  PV-DB2-SEQ-NBR           PIC  X(03).                    CL**1
00124          10  PV-DB2-ROW-NBR           PIC S9(04) COMP.               CL**1
00125          10  PV-DB2-INV-DKEY          PIC S9(09) COMP.               CL**1
00126      05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.         CL**1
00127      05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.        CL**1
00128      05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.        CL**1
00129                                                                      CL**1
00130 ******************************************************************   CL**1
00131 * TRANSACTION PREPARATION MODULE LINKAGE SECTION.                    CL**1
00132 ******************************************************************   CL**1
00133                                                                      CL**1
260107     COPY DPWS990.                                                   CL**1
00133                                                                      CL**1
00134      COPY DPLS000.                                                   CL**1
00135                                                                      CL**1
00136 *                                                                    CL**1
00137  01  PC-PROGRAM-CONSTANTS.                                           CL**1
00138      05  PC-MAX-RETRIES          PIC S9(03)   VALUE +03              CL**1
00139                                               COMP-3.                CL**1
00140      05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'ETKUT039'.   CL**1
00141      05  PC-TRAN-PREP-FUNC-CODES.                                    CL**1
00142          10  PC-TRAN-PREP-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.      CL**1
00143          10  PC-TRAN-PREP-FUNC-WRITE  PIC  X(05) VALUE 'WRITE'.      CL**1
00144          10  PC-TRAN-PREP-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.      CL**1
00145      05  PC-JOB-NAME                  PIC  X(08) VALUE 'ETK142  '.   CL**1
00146      05  PC-CKPT-PLAN-NAME            PIC  X(08) VALUE 'ETKUP039'.   CL**1
00147      05  PC-OPERCO-NBR                PIC  X(02) VALUE '03'.         CL**1
00148 *                                                                    CL**1
00149  01  PS-PROGRAM-SWITCHES.                                            CL**1
00150      05  PS-END-OF-FILE             PIC X     VALUE 'N'.             CL**1
00151          88  PS-EOF                           VALUE 'Y'.             CL**1
00152      05  PS-ONE-DEPARTMENT          PIC X     VALUE 'Y'.             CL**1
00153          88  ONE-DEPT                         VALUE 'Y'.             CL**1
00154          88  MULTIPLE-DEPTS                   VALUE 'N'.             CL**1
00155      05  PS-CATALOG-ERROR           PIC X     VALUE 'N'.             CL**1
00156          88  CATALOG-ERROR                    VALUE 'Y'.             CL**1
00157          88  CATALOG-IS-OK                    VALUE 'N'.             CL**1
00158      05  PS-KEEP-THIS-INV           PIC X     VALUE 'Y'.             CL**1
00159          88  KEEP-THIS-INV                    VALUE 'Y'.             CL**1
00160          88  KICK-OUT-THIS-INV                VALUE 'N'.             CL**1
00161      05  PS-ALL-GOOD-UPCS           PIC X     VALUE 'Y'.             CL**1
00162          88  ALL-GOOD-UPCS                    VALUE 'Y'.             CL**1
00163          88  NOT-ALL-GOOD-UPCS                VALUE 'N'.             CL**1
00164 *                                                                    CL**1
00165 ******************************************************************   CL**1
00166 *  SQL ABEND WORKING STORAGE                                         CL**1
00167 ******************************************************************   CL**1
00168      COPY DPWS004.                                                   CL**1
00169      EJECT                                                           CL**1
00170 *                                                                    CL**1
00171 *****************************************************************    CL**1
00172 *****  DB2 PO ON HOLD TABLE DCLGEN                                   CL**1
00173 *****************************************************************    CL**1
00174 *                                                                    CL**1
00175      EXEC SQL                                                        CL**1
00176          INCLUDE TTRPOHL                                             CL**1
00177      END-EXEC.                                                       CL**1
00178 *                                                                    CL**1
00179 *****************************************************************    CL**1
00180 *****  DB2 EDI FUNCTIONAL GROUP ON HOLD TABLE                        CL**1
00181 *****************************************************************    CL**1
00182 *                                                                    CL**1
00183      EXEC SQL                                                        CL**1
00184          INCLUDE TFGTRHL                                             CL**1
00185      END-EXEC.                                                       CL**1
00186 *                                                                    CL**1
00187 *****************************************************************    CL**1
00188 *****  DB2 EDI FUNCTIONAL GROUP ON HOLD DETAIL TABLE                 CL**1
00189 *****************************************************************    CL**1
00190 *                                                                    CL**1
00191      EXEC SQL                                                        CL**1
00192          INCLUDE TFGTRDT                                             CL**1
00193      END-EXEC.                                                       CL**1
00194 *                                                                    CL**1
00195 *****************************************************************    CL**1
00196 *****  DB2 EDI TRADING PARTNER TABLE DCLGEN                          CL**1
00197 *****************************************************************    CL**1
00198 *                                                                    CL**1
00199      EXEC SQL                                                        CL**1
00200          INCLUDE TTRDPRT                                             CL**1
00201      END-EXEC.                                                       CL**1
00202 *                                                                    CL**1
00203 *****************************************************************    CL**1
00204 *****  DB2 EDI TRADING PARTNER TRANSMISSION TABLE DCLGEN             CL**1
00205 *****************************************************************    CL**1
00206 *                                                                    CL**1
00207      EXEC SQL                                                        CL**1
00208          INCLUDE TTRDTRN                                             CL**1
00209      END-EXEC.                                                       CL**1
00210 *                                                                    CL**1
00211 *****************************************************************    CL**1
00212 * DB2 COMMUNICATIONS AREA                                            CL**1
00213 *****************************************************************    CL**1
00214                                                                      CL**1
00215      EXEC SQL                                                        CL**1
00216          INCLUDE SQLCA                                               CL**1
00217      END-EXEC.                                                       CL**1
00218 *                                                                    CL**1
00219      COPY SQLCA2.                                                    CL**1
00220                                                                      CL**1
00221  01  ABEND-CODE               PIC S9(04)  VALUE ZERO                 CL**1
00222                                           COMP SYNC.                 CL**1
00223      EJECT                                                           CL**1
00224  LINKAGE SECTION.                                                    CL**1
00225 *                                                                    CL**1
00226  PROCEDURE DIVISION.                                                 CL**1
00227 *                                                                    CL**1
00228 ******************************************************************   CL**1
00229 * MAIN-LINE LOGIC.                                                   CL**1
00230 ******************************************************************   CL**1
00231  A100-MAIN-MODULE.                                                   CL**1
00232 *                                                                    CL**1
00233      OPEN INPUT  INV-STATUS-FILE.                                    CL**1
00234      OPEN OUTPUT INV-OUTPUT-FILE.                                    CL**1
00235 *                                                                    CL**1
00236      MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.                 CL**1
00237      PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                         CL**1
00238      MOVE DP000-CKPT-TYPE-CODE TO PV-CHECKPOINT-TYPE.                CL**1
00239                                                                      CL**1
00240      PERFORM Z100-READ-INPUT-FILE.                                   CL**1
00241 *                                                                    CL**1
00242      PERFORM B100-PROCESS-INV-FILE                                   CL**1
00243              UNTIL PS-EOF.                                           CL**1
00244 *                                                                    CL**1
00245      CLOSE INV-STATUS-FILE                                           CL**1
00246            INV-OUTPUT-FILE.                                          CL**1
00247                                                                      CL**1
00248      MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.                CL**1
00249      PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                         CL**1
00250 *                                                                    CL**1
00251      STOP RUN.                                                       CL**1
00252 *                                                                    CL**1
00253 ******************************************************************   CL**1
00254 *  PROCESS ALL INV RECORDS FROM INPUT FILE.                          CL**1
00255 ******************************************************************   CL**1
00256  B100-PROCESS-INV-FILE.                                              CL**1
00257 *                                                                    CL**1
00258       IF AP050-INV-INTRCHG                                           CL**1
00259          MOVE INV-STATUS-TP-DKEY         TO WS-SAVE-TP-DKEY          CL**1
00260          MOVE INV-STATUS-TP-DKEY-TMST    TO WS-SAVE-TP-DKEY-TMST     CL**1
00261          MOVE INV-STATUS-TRN-SET-SEQ-NBR                             CL**1
00262                            TO WS-SAVE-TRN-SET-SEQ-NBR                CL**1
00263          MOVE AP050-KOHLS-PO-NBR         TO WS-SAVE-H-PO-NBR         CL**1
00264          MOVE AP050-DEPT-NBR-ON-PO       TO WS-SAVE-H-DEPT-NBR       CL**1
00265 *                                                                    CL**1
00266          IF AP050-NEW-INV-GOOD                                       CL**1
00267             MOVE AP050-KEY                TO INV-OUTPUT-INVKEY       CL**1
00268             MOVE AP050-DATA-AREA          TO INV-OUTPUT-DATA         CL**1
00269             PERFORM Z200-WRITE-OUTPUT-FILE                           CL**1
00270             SET LOGICAL-TRANSACTION TO TRUE                          CL**1
00271             SET UD-DELETE-DB      TO TRUE                            CL**1
00272             PERFORM C300-UPD-DEL-TFGTRDT-ROW                         CL**1
00273             PERFORM Z300-WRITE-UPD-DEL-RECORD                        CL**1
00274 *                                                                    CL**1
00275             MOVE SPACES TO INV-UPD-DEL-RECORD                        CL**1
00276             PERFORM Z100-READ-INPUT-FILE                             CL**1
00277 *                                                                    CL**1
00278             PERFORM UNTIL PS-EOF OR AP050-INV-INTRCHG                CL**1
00279                MOVE AP050-KEY       TO INV-OUTPUT-INVKEY             CL**1
00280                MOVE AP050-DATA-AREA TO INV-OUTPUT-DATA               CL**1
00281                PERFORM Z200-WRITE-OUTPUT-FILE                        CL**1
00282                SET NO-COMMIT        TO TRUE                          CL**1
00283                SET UD-DELETE-DB     TO TRUE                          CL**1
00284                PERFORM C300-UPD-DEL-TFGTRDT-ROW                      CL**1
00285                PERFORM Z300-WRITE-UPD-DEL-RECORD                     CL**1
00286 *                                                                    CL**1
00287                MOVE SPACES TO INV-UPD-DEL-RECORD                     CL**1
00288                PERFORM Z100-READ-INPUT-FILE                          CL**1
00289             END-PERFORM                                              CL**1
00290 *                                                                    CL**1
00291             MOVE SPACES TO INV-UPD-DEL-RECORD                        CL**1
00292             SET NO-COMMIT TO TRUE                                    CL**1
00293             SET UD-DELETE-DB      TO TRUE                            CL**1
00294             PERFORM C200-UPD-DEL-TTRPOHL-ROW                         CL**1
00295             PERFORM Z300-WRITE-UPD-DEL-RECORD                        CL**1
00296 *                                                                    CL**1
00297             MOVE SPACES TO INV-UPD-DEL-RECORD                        CL**1
00298             SET NO-COMMIT TO TRUE                                    CL**1
00299             SET UD-UPDATE-DB      TO TRUE                            CL**1
00300             PERFORM C100-UPD-DEL-TFGTRHL-ROW                         CL**1
00301             PERFORM Z300-WRITE-UPD-DEL-RECORD                        CL**1
00302          ELSE                                                        CL**1
00303             IF INV-UPDATE-DB-RECORD                                  CL**1
00304                SET LOGICAL-TRANSACTION TO TRUE                       CL**1
00305                SET UD-UPDATE-DB      TO TRUE                         CL**1
00306                PERFORM C300-UPD-DEL-TFGTRDT-ROW                      CL**1
00307                PERFORM Z300-WRITE-UPD-DEL-RECORD                     CL**1
00308 *                                                                    CL**1
00309                IF AP050-PO-NOT-FOUND AND                             CL**1
00310                   NOT AP050-PO-ON-HOLD                               CL**1
00311                   PERFORM C400-SELECT-TTRPOHL                        CL**1
00312                   IF SQLCODE = +000                                  CL**1
00313                      SET NO-COMMIT         TO TRUE                   CL**1
00314                      SET UD-UPDATE-DB      TO TRUE                   CL**1
00315                      PERFORM C200-UPD-DEL-TTRPOHL-ROW                CL**1
00316                      PERFORM Z300-WRITE-UPD-DEL-RECORD               CL**1
00317                   ELSE                                               CL**1
00318                      DISPLAY '** ABEND     **'                       CL**1
00319                      DISPLAY '** TTRPOHL RECORD NOT FOUND'           CL**1
00320                      DISPLAY '** PROGRAM   ** =  ETKUT039'           CL**1
00321                      MOVE 'B100-PROCESS-INV-FILE'                    CL**1
00322                                    TO  PV-PARAGRAPH-NAME             CL**1
00323                      MOVE 'FIND DEPT ON HELD INV'                    CL**1
00324                                    TO  PV-DB2-OPER-ATTEMPTED         CL**1
00325                      PERFORM Z900-SQL-ABEND                          CL**1
00326                   END-IF                                             CL**1
00327                END-IF                                                CL**1
00328             END-IF                                                   CL**1
00329 *                                                                    CL**1
00330             MOVE SPACES TO INV-UPD-DEL-RECORD                        CL**1
00331             PERFORM Z100-READ-INPUT-FILE                             CL**1
00332 *                                                                    CL**1
00333             PERFORM UNTIL AP050-INV-INTRCHG OR                       CL**1
00334                           PS-EOF                                     CL**1
00335                IF AP050-INV-DETAILS                                  CL**1
00336                   IF INV-UPDATE-DB-RECORD                            CL**1
00337                      SET NO-COMMIT    TO TRUE                        CL**1
00338                      SET UD-UPDATE-DB TO TRUE                        CL**1
00339                      PERFORM C300-UPD-DEL-TFGTRDT-ROW                CL**1
00340                      PERFORM Z300-WRITE-UPD-DEL-RECORD               CL**1
00341                   END-IF                                             CL**1
00342                END-IF                                                CL**1
00343 *                                                                    CL**1
00344                MOVE SPACES TO INV-UPD-DEL-RECORD                     CL**1
00345                PERFORM Z100-READ-INPUT-FILE                          CL**1
00346             END-PERFORM                                              CL**1
00347          END-IF                                                      CL**1
00348       ELSE                                                           CL**1
00349          DISPLAY '**  ABEND     **'                                  CL**1
00350          DISPLAY '**  RECORDS IN THE INPUT FILE WERE'                CL**1
00351          DISPLAY '**  OUT OF SEQUENCE.    '                          CL**1
00352          DISPLAY '**  PROGRAM   **  =  ETKUT039'                     CL**1
00353          DISPLAY '**  PARAGRAPH **  = B100-PROCESS-FILE'             CL**1
00354          MOVE +4007          TO ABEND-CODE                           CL**1
00355          CALL 'ILBOABN0'  USING ABEND-CODE                           CL**1
00356       END-IF.                                                        CL**1
00357 *                                                                    CL**1
00358 ******************************************************************   CL**1
00359 *  WRITE RECORD TO INPUT FILE FOR UD-ION INTO THE TFGTRHL TABL       CL**1
00360 ******************************************************************   CL**1
00361  C100-UPD-DEL-TFGTRHL-ROW.                                           CL**1
00362 *                                                                    CL**1
00363      MOVE 'TFGTRHL'                TO UD-TABLE-NAME.                 CL**1
00364      MOVE WS-SAVE-TP-DKEY          TO UD-TP-DKEY.                    CL**1
00365      MOVE WS-SAVE-TP-DKEY-TMST     TO UD-TP-DKEY-TMST.               CL**1
00366      MOVE WS-SAVE-TRN-SET-SEQ-NBR  TO UD-TRN-SET-SEQ-NBR.            CL**1
00367      MOVE '810'                    TO UD-TRN-SET-CDE.                CL**1
00368      MOVE AP050-VENDOR-INVOICE-NBR TO UD-TRN-DTL-TXT.                CL**1
00369      MOVE 'Y'                      TO UD-RLSE-IND.                   CL**1
00370      MOVE '9999-09-09'             TO UD-RLSE-DTE.                   CL**1
00371      MOVE 'ETKUP037'               TO UD-USER-ID.                    CL**1
00372 *                                                                    CL**1
00373 ******************************************************************   CL**1
00374 *  WRITE RECORD TO INPUT FILE FOR UD-ION INTO THE TFGTPOL TABL       CL**1
00375 ******************************************************************   CL**1
00376  C200-UPD-DEL-TTRPOHL-ROW.                                           CL**1
00377 *                                                                    CL**1
00378      MOVE 'TTRPOHL'                TO UD-TABLE-NAME.                 CL**1
00379      MOVE WS-SAVE-TP-DKEY          TO UD-TP-DKEY.                    CL**1
00380      MOVE WS-SAVE-TP-DKEY-TMST     TO UD-TP-DKEY-TMST.               CL**1
00381      MOVE WS-SAVE-TRN-SET-SEQ-NBR  TO UD-TRN-SET-SEQ-NBR.            CL**1
00382      MOVE AP050-KOHLS-PO-NBR       TO UD-PO-NBR.                     CL**1
00383      MOVE AP050-DEPT-NBR-ON-PO     TO UD-DEPT-NBR.                   CL**1
00384      MOVE 'N'                      TO UD-PO-RLSE-IND.                CL**1
00385 *                                                                    CL**1
00386 ******************************************************************   CL**1
00387 *  WRITE RECORD TO INPUT FILE FOR UD-ION INTO THE TFGTRDT TABL       CL**1
00388 ******************************************************************   CL**1
00389  C300-UPD-DEL-TFGTRDT-ROW.                                           CL**1
00390 *                                                                    CL**1
00391      MOVE 'TFGTRDT'           TO UD-TABLE-NAME.                      CL**1
00392      MOVE INV-STATUS-TP-DKEY  TO UD-TP-DKEY.                         CL**1
00393      MOVE INV-STATUS-TP-DKEY-TMST                                    CL**1
00394                               TO UD-TP-DKEY-TMST.                    CL**1
00395      MOVE INV-STATUS-TRN-SET-SEQ-NBR                                 CL**1
00396                               TO UD-TRN-SET-SEQ-NBR.                 CL**1
00397      MOVE AP050-KEY           TO UD-INVKEY.                          CL**1
00398      MOVE AP050-DATA-AREA     TO UD-INV-DATA.                        CL**1
00399 *                                                                    CL**1
00400 ******************************************************************   CL**1
00401 *  FIND IF THE UPC REQUEST IS BACK FROM THE CATALOG                  CL**1
00402 ******************************************************************   CL**1
00403  C400-SELECT-TTRPOHL.                                                CL**1
00404 *                                                                    CL**1
00405      PERFORM                                                         CL**1
00406          WITH TEST AFTER                                             CL**1
00407          VARYING PV-SQL-SUB                                          CL**1
00408          FROM 1 BY 1                                                 CL**1
00409          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**1
00410                    OR SQLCODE = 0                                    CL**1
00411                    OR SQLCODE = +100)                                CL**1
00412 *                                                                    CL**1
00413          EXEC SQL                                                    CL**1
00414              SELECT A.DEPT_NBR                                       CL**1
00415              INTO :TRPOHL-DEPT-NBR                                   CL**1
00416              FROM TTRPOHL A                                          CL**1
00417              WHERE A.TP_DKEY      = :WS-SAVE-TP-DKEY                 CL**1
00418                AND A.TP_DKEY_TMST = :WS-SAVE-TP-DKEY-TMST            CL**1
00419          END-EXEC                                                    CL**1
00420 *                                                                    CL**1
00421          EVALUATE TRUE                                               CL**1
00422              WHEN SQLCODE = -904                                     CL**1
00423              WHEN SQLCODE = -911                                     CL**1
00424              WHEN SQLCODE = 100                                      CL**1
00425              WHEN SQLCODE = 0                                        CL**1
00426                  CONTINUE                                            CL**1
00427              WHEN SQLWARN NOT = SPACES                               CL**1
00428              WHEN SQLCODE NOT = ZERO                                 CL**1
00429                  MOVE 'C400-SELECT-TTRPOHL'                          CL**1
00430                                TO  PV-PARAGRAPH-NAME                 CL**1
00431                  MOVE 'FIND UPC ON CATALOG'                          CL**1
00432                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00433                  PERFORM Z900-SQL-ABEND                              CL**1
00434          END-EVALUATE                                                CL**1
00435      END-PERFORM.                                                    CL**1
00436 *                                                                    CL**1
00437      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**1
00438          MOVE 'C400-SELECT-TTRPOHL'                                  CL**1
00439                                TO  PV-PARAGRAPH-NAME                 CL**1
00440          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**1
00441                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00442          PERFORM Z900-SQL-ABEND                                      CL**1
00443      END-IF.                                                         CL**1
00444 *                                                                    CL**1
00445 ******************************************************************   CL**1
00446 * Z100-READ-INPUT-FILE                                               CL**1
00447 ******************************************************************   CL**1
00448  Z100-READ-INPUT-FILE.                                               CL**1
00449 *                                                                    CL**1
00450      READ INV-STATUS-FILE                                            CL**1
00451             AT END SET PS-EOF TO TRUE.                               CL**1
00452 *                                                                    CL**1
00453 ******************************************************************   CL**1
00454 * Z200-WRITE-OUTPUT-FILE                                             CL**1
00455 ******************************************************************   CL**1
00456  Z200-WRITE-OUTPUT-FILE.                                             CL**1
00457 *                                                                    CL**1
00458      WRITE INV-OUTPUT-RECORD.                                        CL**1
00459 *                                                                    CL**1
00460 ******************************************************************   CL**1
00461 * Z300-WRITE-UPD-DEL-RECORD                                          CL**1
00462 ******************************************************************   CL**1
00463  Z300-WRITE-UPD-DEL-RECORD.                                          CL**1
00464 *                                                                    CL**1
00465 *    *--------------------------------------------------------       CL**1
00466 *    * THERE ARE NO CONTROL BREAKS IN THIS PROGRAM.  TAKING          CL**1
00467 *    * CHECKPOINTS BY CONTROL BREAK HAS THE SAME EFFECT AS           CL**1
00468 *    * TAKING CHECKPOINTS BY LOGICAL TRANSACTION.                    CL**1
00469 *    *--------------------------------------------------------       CL**1
00470      IF LOGICAL-TRANSACTION                                          CL**1
00471      OR CONTROL-BREAK                                                CL**1
00472          MOVE 'Y' TO DP000-CHECKPOINT-IND                            CL**1
00473      ELSE                                                            CL**1
00474          MOVE 'N' TO DP000-CHECKPOINT-IND                            CL**1
00475      END-IF.                                                         CL**1
00476                                                                      CL**1
00477      MOVE LENGTH OF INV-UPD-DEL-RECORD                               CL**1
00478                                   TO DP000-TRANS-REC-LENGTH.         CL**1
00479      MOVE INV-UPD-DEL-RECORD      TO DP000-TRANS-REC.                CL**1
00480      MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.                CL**1
00481      PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                         CL**1
00482                                                                      CL**1
00483 ******************************************************************   CL**1
00484 * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION         CL**1
00485 * PREPARATION MODULE.                                                CL**1
00486 ******************************************************************   CL**1
00487  Z400-BUILD-COMM-AREA-TRAN-PREP.                                     CL**1
00488                                                                      CL**1
00489      MOVE PC-JOB-NAME             TO DP000-JOB-NAME.                 CL**1
00490      MOVE PC-CKPT-PLAN-NAME       TO DP000-PLAN-NAME.                CL**1
00491      PERFORM Z900-CALL-TRANS-PREP-MODULE.                            CL**1
00492                                                                      CL**1
00493 ******************************************************************   CL**1
00494 *     TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES.        CL**1
00495 ******************************************************************   CL**1
00496                                                                      CL**1
00497      COPY DPPD000.                                                   CL**1
00498                                                                      CL**1
00499 *                                                                    CL**1
00500 ******************************************************************   CL**1
00501 *  STANDARD DB2 ERROR ABEND ROUTINE                                  CL**1
00502 *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                CL**1
00503 ******************************************************************   CL**1
00504  Z900-SQL-ABEND.                                                     CL**1
00505                                                                      CL**1
00506      MOVE +4013                 TO ABEND-CODE.                       CL**1
00507      DISPLAY '**  ABEND     **'.                                     CL**1
00508      DISPLAY '**  PROGRAM   **  =  ETKUT039'.                        CL**1
00509      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.               CL**1
00510      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.           CL**1
00511                                                                      CL**1
00512 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE            CL**1
00513 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.           CL**1
00514                                                                      CL**1
00515      COPY DPPD004.                                                   CL**1
00516                                                                      CL**1
00517      CALL 'ILBOABN0'  USING ABEND-CODE.                              CL**1
