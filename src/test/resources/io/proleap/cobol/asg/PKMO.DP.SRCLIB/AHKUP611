000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    AHKUP611.                                                 
000300 AUTHOR.        MIKE FRIESS.                                              
000400 INSTALLATION.  KOHLS.                                                    
000500 DATE-WRITTEN   OCT, 2003.                                                
000600 DATE-COMPILED.                                                           
000700                                                                          
000800* ************************************************************** *        
000900* * AHKUP611.  "ARCHIVE HISTORY" PROJECT.  DELETE PROGRAM.     * *        
001000* *                                                            * *        
001100* * PURPOSE:                                                   * *        
001200* * DELETE OBSOLETE ROWS FROM DOMESTIC TRANSPORT AUDIT         * *        
001300* * TABLES                                                     * *        
001600* *                                                            * *        
001700* * PROCESSING OVERVIEW:                                       * *        
001800* * THIS PROGRAM READS A CONTROL FILE TO DETERMINE CUTOFF DATE * *        
001810* * FOR DELETE PROCESS AND WHAT TABLE TO DELETE FROM.          * *        
001900* * 1) READ CONTROL CARD FOR DATE.                             * *        
002000* * 2) CURSOR THROUGH FIRST TABLE UNTIL END OF CURSOR          * *        
002010* *    A. DELETE ROW IF LAST UPDATE DATE LESS THAN CUTOFF.     * *        
002100* *    B. DO A DB2 COMMIT AFTER X ROWS DELETED (WHERE X =      * *        
002101* *       PC-HOW-OFTEN-TO-COMMIT) OR END OF CURSOR.            * *        
002103* * 3) REPEAT STEPS 1 THRU 2 UNTIL END OF CONTROL CARDS.       * *        
003900**************************************************************** *        
004202 ENVIRONMENT DIVISION.                                                    
004203 CONFIGURATION SECTION.                                                   
004204 SOURCE-COMPUTER.        IBM-3090.                                        
004300 OBJECT-COMPUTER.        IBM-3090.                                        
004400                                                                          
004500 INPUT-OUTPUT SECTION.                                                    
004600 FILE-CONTROL.                                                            
004700                                                                          
004810     SELECT IN-CONTROL-FILE               ASSIGN TO INP01.                
005000                                                                          
005100******************************************************************        
005200 DATA DIVISION.                                                           
005300 FILE SECTION.                                                            
005400                                                                          
006000                                                                          
006001**** AHCC603                                                              
006010 FD  IN-CONTROL-FILE                                                      
006020     RECORDING MODE IS F                                                  
006030     LABEL RECORDS ARE STANDARD                                           
006040     BLOCK CONTAINS 0 RECORDS.                                            
006050 01  IN-CONTROL-RECORD               PIC  X(080).                         
006800                                                                          
006900                                                                          
007000 WORKING-STORAGE SECTION.                                                 
007100                                                                          
007200 01  PC-PROGRAM-CONSTANTS.                                                
007300     05  PC-CURRENT-PROGRAM-NAME     PIC X(08) VALUE 'AHKUP611'.          
007400     05  PC-PGM-PROGRESS-DESC.                                            
007500         10  PC-BEGINNING            PIC X(09) VALUE 'BEGINNING'.         
007600         10  PC-CONTINUING           PIC X(10) VALUE                      
007700                                               'CONTINUING'.              
007800         10  PC-ENDING               PIC X(06) VALUE 'ENDING'.            
007900         10  PC-ABENDING             PIC X(08) VALUE 'ABENDING'.          
008000     05  PC-ABEND-INFO.                                                   
008100         10  PC-ABEND                PIC X(11) VALUE                      
008200                                               '** ABEND **'.             
008300         10  PC-ABEND-MSG-PARA       PIC X(15) VALUE                      
008400                                               '- PARA       : '.         
008500         10  PC-ABEND-MSG-DISPLAY-PARA                                    
008600                                     PIC X(15) VALUE                      
008700                                               '  CALLED PARA: '.         
008800         10  PC-ABEND-MSG-NUMBER     PIC X(15) VALUE                      
008900                                               '  FOR ABEND# : '.         
009000         10  PC-ABEND-MSG-DESC       PIC X(15) VALUE                      
009100                                               '- ERROR IS   : '.         
009200         10  PC-ABEND-MSG-INDENT-15  PIC X(15) VALUE SPACES.              
009300         10  PC-ABEND-MSG-ACTION     PIC X(15) VALUE                      
009400                                               '- ACTION     : '.         
009500     05  PC-HOW-OFTEN-TO-COMMIT      PIC 9(9)  VALUE 0100.                
009600                                                                          
009700 01  PS-PROGRAM-SWITCHES.                                                 
009800     05  PS-CONTROL-EOF-SW           PIC X(01) VALUE 'N'.                 
009900         88  PS-CONTROL-EOF                    VALUE 'Y'.                 
010000         88  PS-CONTROL-NOT-EOF                VALUE 'N'.                 
010100     05  PS-CURSOR-HAS-DATA-SW       PIC X(01) VALUE 'N'.                 
010200         88  PS-CURSOR-HAS-DATA                VALUE 'Y'.                 
010300         88  PS-CURSOR-HAS-NO-DATA             VALUE 'N'.                 
010400     05  PS-TABLE-LOOP-DONE-SW       PIC X(01) VALUE 'N'.                 
010500         88  PS-TABLE-LOOP-DONE                VALUE 'Y'.                 
010600         88  PS-TABLE-LOOP-NOT-DONE            VALUE 'N'.                 
010700                                                                          
010800 01  PV-PROGRAM-VARIABLES.                                                
011400     05  PV-TOTAL-DELETE-OPS         PIC  9(9) VALUE 0.                   
011500     05  PV-TABLE-DELETE-OPS         PIC  9(9) VALUE 0.                   
011510     05  PV-COMMIT-DELETE-OPS        PIC  9(9) VALUE 0.                   
011600     05  PV-PARAGRAPH-NAME           PIC X(30) VALUE SPACES.              
011700     05  PV-DB2-OPERATION-MSG-1      PIC X(79) VALUE SPACES.              
011800     05  PV-DB2-TABLE-NAME           PIC X(18) VALUE SPACES.              
012100     05  PV-SQLCODE                  PIC +999  VALUE ZERO.                
012200                                                                          
012300     05  PV-TMST                     PIC X(26) VALUE SPACES.              
012400     05  PV-TIMESTAMP-DETAIL                                              
012500         REDEFINES                                                        
012600         PV-TMST.                                                         
012700         10  PV-TIMESTAMP-19.                                             
012800             15  PV-TMS-DATE.                                             
012900                 20  PV-TMS-YEAR.                                         
013000                     25  PV-TMS-CC   PIC X(02).                           
013100                     25  PV-TMS-YY   PIC X(02).                           
013200                 20  FILLER          PIC X(01).                           
013300                 20  PV-TMS-MONTH    PIC X(02).                           
013400                 20  FILLER          PIC X(01).                           
013500                 20  PV-TMS-DAY      PIC X(02).                           
013600             15  FILLER              PIC X(01).                           
013700             15  PV-TMS-HOUR         PIC X(02).                           
013800             15  FILLER              PIC X(01).                           
013900             15  PV-TMS-MINUTE       PIC X(02).                           
014000             15  FILLER              PIC X(01).                           
014100             15  PV-TMS-SECOND       PIC X(02).                           
014200         10  FILLER                  PIC X(01).                           
014300         10  PV-TMS-MSECOND          PIC X(06).                           
014301                                                                          
014302     05  PV-NULL                     PIC S9(04) VALUE +0                  
014303                                                   COMP.                  
014304                                                                          
014310     05  PV-CONTROL-RECORD.                                               
014320         10  FILLER                  PIC X(16).                           
014321         10  PV-TABLE-NAME           PIC X(07).                           
014322         10  FILLER                  PIC X(27).                           
014330         10  PV-PURGE-DATE           PIC X(10).                           
014340         10  FILLER                  PIC X(20).                           
014400                                                                          
014500 01  DM-DISPLAY-MESSAGE.                                                  
014600     05  DM-01-10.                                                        
014700         10  DM-PROGRAM              PIC X(08) VALUE SPACE.               
014800         10  FILLER                  PIC X(02) VALUE ': '.                
014900     05  DM-11-80.                                                        
015010         10  FILLER                  PIC X(25) VALUE                      
015020                        'CREATE TSHPTPO EXTRACT - '.                      
015030         10  FILLER                  PIC X(33) VALUE                      
015100                        'MATCH PO DRIVER & CREATE_DATE LT '.              
015400         10  DM-PURGE-DATE           PIC X(10) VALUE SPACE.               
015410         10  FILLER                  PIC X(01) VALUE SPACE.               
015500         10  FILLER                  PIC X(01) VALUE ALL '*'.             
015600     05  DM-81-120.                                                       
015700         10  FILLER                  PIC X(40) VALUE ALL '*'.             
015800                                                                          
015900 01  DP-DISPLAY-PROGRESS-MSG.                                             
016000     05  DP-PROGRAM                  PIC X(08) VALUE SPACE.               
016100     05  FILLER                      PIC X(02) VALUE SPACE.               
016200     05  DP-STATUS-DESCRIPTION       PIC X(10) VALUE SPACE.               
016300     05  FILLER                      PIC X(04) VALUE ' AT '.              
016400     05  DP-TIMESTAMP-19             PIC X(19) VALUE SPACE.               
016500     05  FILLER                      PIC X(03) VALUE '.  '.               
016600     05  FILLER                      PIC X(03) VALUE                      
016700                                         'IN:'.                           
016800     05  DP-IN-CNT                   PIC ZZZ,ZZZ,ZZ9                      
016900                                               VALUE ZERO.                
017000     05  FILLER                      PIC X(02) VALUE ', '.                
017600     05  FILLER                      PIC X(14) VALUE                      
017700                                         'ROWS SELECTED:'.                
017800     05  DP-ROWS-SELECTED-CNT        PIC ZZZ,ZZZ,ZZ9                      
017900                                               VALUE ZERO.                
018000     05  FILLER                      PIC X(02) VALUE ', '.                
018100     05  FILLER                      PIC X(04) VALUE                      
018200                                         'OUT:'.                          
018300     05  DP-OUT-CNT                  PIC ZZZ,ZZZ,ZZ9                      
018301                                               VALUE ZERO.                
018310     05  FILLER                      PIC X(02) VALUE ', '.                
018320     05  FILLER                      PIC X(03) VALUE                      
018330                                         'PO:'.                           
018400     05  DP-PO-NBR                   PIC ZZZZZZZZ9                        
018410                                               VALUE ZERO.                
018500     05  FILLER                      PIC X(01) VALUE '.'.                 
018600                                                                          
018700 01  ABEND-CODE                      PIC S9(04)                           
018800                                               VALUE ZEROES               
018900                                               COMP SYNC.                 
019000                                                                          
019100*---------------------------------------------------------------*         
019200*  DB2 FORMATTED MESSAGE AREA                                             
019300*---------------------------------------------------------------*         
019400     COPY DPWS004.                                                        
020200                                                                          
020300*---------------------------------------------------------------*         
020400*    DB2 AREA FOR SHIPPED PO AUDIT                              *         
020500*---------------------------------------------------------------*         
020600     EXEC SQL                                                             
020700          INCLUDE TAUSHPO                                                 
020800     END-EXEC.                                                            
020900                                                                          
020901*---------------------------------------------------------------*         
020902*    DB2 AREA FOR SHIPMENT AUDIT                                *         
020903*---------------------------------------------------------------*         
020904     EXEC SQL                                                             
020905          INCLUDE TAUSHIP                                                 
020906     END-EXEC.                                                            
020907                                                                          
020910*---------------------------------------------------------------*         
020920*    DB2 AREA FOR TRIP                                          *         
020930*---------------------------------------------------------------*         
020940     EXEC SQL                                                             
020950          INCLUDE TAUTRIP                                                 
020960     END-EXEC.                                                            
020970                                                                          
020980*---------------------------------------------------------------*         
020990*    DB2 AREA FOR VEHICLE                                       *         
020991*---------------------------------------------------------------*         
020992     EXEC SQL                                                             
020993          INCLUDE TAUVHCL                                                 
020994     END-EXEC.                                                            
020995                                                                          
021000*---------------------------------------------------------------*         
021100*    DB2 AREA FOR COMMUNICATIONS                                *         
021200*---------------------------------------------------------------*         
021300     EXEC SQL                                                             
021400          INCLUDE SQLCA                                                   
021500     END-EXEC.                                                            
021600                                                                          
021700*---------------------------------------------------------------*         
022200     EXEC SQL                                                             
022300         DECLARE TAUSHPO-CURSOR CURSOR WITH HOLD FOR                      
022400           SELECT DISTINCT SHIPT_ID                                       
022500                          ,PO_NBR                                         
027502           FROM   TAUSHPO                                                 
027505          WHERE  :PV-PURGE-DATE > DATE(CRE_TMST)                          
027600     END-EXEC.                                                            
027800                                                                          
027810*---------------------------------------------------------------*         
027820     EXEC SQL                                                             
027830         DECLARE TAUSHIP-CURSOR CURSOR WITH HOLD FOR                      
027840           SELECT DISTINCT TRIP_ID                                        
027850                          ,SHIPT_ID                                       
027860           FROM   TAUSHIP                                                 
027870          WHERE  :PV-PURGE-DATE > LAST_UPD_DTE                            
027880     END-EXEC.                                                            
027890                                                                          
027891*---------------------------------------------------------------*         
027892     EXEC SQL                                                             
027893         DECLARE TAUTRIP-CURSOR CURSOR WITH HOLD FOR                      
027894           SELECT DISTINCT TRIP_ID                                        
027895                          ,OWNER_SCAC_ID                                  
027896                          ,VEH_NBR                                        
027897           FROM   TAUTRIP                                                 
027898          WHERE  :PV-PURGE-DATE > LAST_UPD_DTE                            
027899     END-EXEC.                                                            
027900                                                                          
027901*---------------------------------------------------------------*         
027902     EXEC SQL                                                             
027903         DECLARE TAUVHCL-CURSOR CURSOR WITH HOLD FOR                      
027905           SELECT DISTINCT OWNER_SCAC_ID                                  
027906                          ,VEH_NBR                                        
027907           FROM   TAUVHCL                                                 
027908          WHERE  :PV-PURGE-DATE > LAST_UPD_DTE                            
027909     END-EXEC.                                                            
027910                                                                          
027920 PROCEDURE DIVISION.                                                      
028000********************                                                      
028100                                                                          
028200 0000-PROGRAM-MAINLINE.                                                   
028300                                                                          
028400     PERFORM 1000-INITIALIZE.                                             
028500                                                                          
028700     PERFORM 2000-PROCESS-CONTROL                                         
028800         UNTIL PS-CONTROL-EOF.                                            
028900                                                                          
029000     PERFORM 6000-EOJ-ROUTINE.                                            
029100     STOP RUN.                                                            
029200                                                                          
029300                                                                          
029400* ************************************************************** *        
029500* * INITIALIZATIONS                                            * *        
029600* ************************************************************** *        
029700 1000-INITIALIZE.                                                         
029800                                                                          
029900     MOVE PC-CURRENT-PROGRAM-NAME TO DM-PROGRAM.                          
030500     PERFORM 9000-OPEN-CONTROL-FILE.                                      
030800                                                                          
030900* ************************************************************** *        
032000 2000-PROCESS-CONTROL.                                                    
032100                                                                          
032200     PERFORM 9050-READ-CONTROL-FILE.                                      
032201     INITIALIZE PV-TABLE-DELETE-OPS.                                      
032202     SET PS-TABLE-LOOP-NOT-DONE TO TRUE.                                  
032210                                                                          
032220     EVALUATE TRUE                                                        
032230                                                                          
032300       WHEN PS-CONTROL-EOF                                                
032400         CONTINUE                                                         
032401                                                                          
032402       WHEN PV-TABLE-NAME = 'TAUSHPO'                                     
032403         PERFORM 9100-OPEN-TAUSHPO-CURSOR                                 
032404         IF PS-CURSOR-HAS-DATA                                            
032405           DISPLAY '*** START ' PV-TABLE-NAME                             
032406           PERFORM 3000-PROCESS-TAUSHPO                                   
032407               UNTIL PS-TABLE-LOOP-DONE                                   
032408           DISPLAY PV-TABLE-NAME ' TOTAL DELETE OPERATIONS - '            
032409                   PV-TABLE-DELETE-OPS                                    
032410           DISPLAY '   '                                                  
032411           PERFORM 9190-CLOSE-TAUSHPO-CURSOR                              
032412         END-IF                                                           
032414                                                                          
032415       WHEN PV-TABLE-NAME = 'TAUSHIP'                                     
032416         PERFORM 9200-OPEN-TAUSHIP-CURSOR                                 
032417         IF PS-CURSOR-HAS-DATA                                            
032418           DISPLAY '*** START ' PV-TABLE-NAME                             
032419           PERFORM 3200-PROCESS-TAUSHIP                                   
032420               UNTIL PS-TABLE-LOOP-DONE                                   
032421           DISPLAY PV-TABLE-NAME ' TOTAL DELETE OPERATIONS - '            
032422                   PV-TABLE-DELETE-OPS                                    
032423           DISPLAY '   '                                                  
032424           PERFORM 9290-CLOSE-TAUSHIP-CURSOR                              
032425         END-IF                                                           
032426                                                                          
032427       WHEN PV-TABLE-NAME = 'TAUTRIP'                                     
032428         PERFORM 9300-OPEN-TAUTRIP-CURSOR                                 
032429         IF PS-CURSOR-HAS-DATA                                            
032430           DISPLAY '*** START ' PV-TABLE-NAME                             
032431           PERFORM 3400-PROCESS-TAUTRIP                                   
032432               UNTIL PS-TABLE-LOOP-DONE                                   
032433           DISPLAY PV-TABLE-NAME ' TOTAL DELETE OPERATIONS - '            
032434                   PV-TABLE-DELETE-OPS                                    
032435           DISPLAY '   '                                                  
032436           PERFORM 9390-CLOSE-TAUTRIP-CURSOR                              
032437         END-IF                                                           
032438                                                                          
032439       WHEN PV-TABLE-NAME = 'TAUVHCL'                                     
032440         PERFORM 9400-OPEN-TAUVHCL-CURSOR                                 
032441         IF PS-CURSOR-HAS-DATA                                            
032442           DISPLAY '*** START ' PV-TABLE-NAME                             
032443           PERFORM 3600-PROCESS-TAUVHCL                                   
032444               UNTIL PS-TABLE-LOOP-DONE                                   
032445           DISPLAY PV-TABLE-NAME ' TOTAL DELETE OPERATIONS - '            
032446                   PV-TABLE-DELETE-OPS                                    
032447           DISPLAY '   '                                                  
032448           PERFORM 9490-CLOSE-TAUVHCL-CURSOR                              
032449         END-IF                                                           
032450                                                                          
032451     END-EVALUATE.                                                        
032452* ************************************************************** *        
032453 3000-PROCESS-TAUSHPO.                                                    
032454                                                                          
032455     PERFORM 9150-FETCH-TAUSHPO-CURSOR.                                   
032456     EVALUATE TRUE                                                        
032457                                                                          
032458       WHEN PS-TABLE-LOOP-DONE                                            
032460         PERFORM 4000-COMMIT                                              
032470       WHEN OTHER                                                         
032471         PERFORM 3100-DELETE-TAUSHPO                                      
032472         IF PV-COMMIT-DELETE-OPS = PC-HOW-OFTEN-TO-COMMIT                 
032475            PERFORM 4000-COMMIT                                           
032476            DISPLAY '     SHIP ID/PO ' AUSHPO-SHIPT-ID '/'                
032477                                     AUSHPO-PO-NBR                        
032478         END-IF                                                           
032479                                                                          
032480     END-EVALUATE.                                                        
036700                                                                          
036801* ************************************************************** *        
036802 3100-DELETE-TAUSHPO.                                                     
036803                                                                          
036804     EXEC SQL                                                             
036805         DELETE                                                           
036806           FROM TAUSHPO                                                   
036807          WHERE SHIPT_ID = :AUSHPO-SHIPT-ID                               
036808            AND PO_NBR   = :AUSHPO-PO-NBR                                 
036809     END-EXEC.                                                            
036810                                                                          
036811     EVALUATE TRUE                                                        
036812         WHEN SQLCODE = ZERO                                              
036813              ADD +1 TO PV-COMMIT-DELETE-OPS                              
036814         WHEN SQLCODE = +100                                              
036815            DISPLAY ' DLT ROW NOT FOUND'                                  
036816                    ' SHIP ID/PO ' AUSHPO-SHIPT-ID '/'                    
036817                                     AUSHPO-PO-NBR                        
036818         WHEN SQLWARN0 NOT EQUAL SPACE                                    
036819         WHEN SQLCODE NOT EQUAL ZERO                                      
036820              MOVE '3100-DELETE-TAUSHPO'                                  
036821                                 TO PV-PARAGRAPH-NAME                     
036822              MOVE 'DELETE'                                               
036823                                 TO PV-DB2-OPERATION-MSG-1                
036824              MOVE 'TAUSHPO'     TO PV-DB2-TABLE-NAME                     
036825              PERFORM 9999-DB2-ABEND                                      
036826     END-EVALUATE.                                                        
036827                                                                          
036828* ************************************************************** *        
036829 3200-PROCESS-TAUSHIP.                                                    
036830                                                                          
036831     PERFORM 9250-FETCH-TAUSHIP-CURSOR.                                   
036832     EVALUATE TRUE                                                        
036833                                                                          
036834       WHEN PS-TABLE-LOOP-DONE                                            
036835         PERFORM 4000-COMMIT                                              
036836       WHEN OTHER                                                         
036837         PERFORM 3300-DELETE-TAUSHIP                                      
036838         IF PV-COMMIT-DELETE-OPS = PC-HOW-OFTEN-TO-COMMIT                 
036840            PERFORM 4000-COMMIT                                           
036841            DISPLAY '     SHIP ID/TRIP ' AUSHIP-SHIPT-ID '/'              
036842                                     AUSHIP-TRIP-ID                       
036845         END-IF                                                           
036846                                                                          
036847     END-EVALUATE.                                                        
036848                                                                          
036849* ************************************************************** *        
036850 3300-DELETE-TAUSHIP.                                                     
036851                                                                          
036852     EXEC SQL                                                             
036853         DELETE                                                           
036854           FROM TAUSHIP                                                   
036855          WHERE SHIPT_ID = :AUSHIP-SHIPT-ID                               
036856            AND (TRIP_ID  = :AUSHIP-TRIP-ID                               
036857                  OR TRIP_ID IS NULL)                                     
036858     END-EXEC.                                                            
036859                                                                          
036860     EVALUATE TRUE                                                        
036861         WHEN SQLCODE = ZERO                                              
036862              ADD +1 TO PV-COMMIT-DELETE-OPS                              
036863         WHEN SQLCODE = +100                                              
036864            DISPLAY ' DLT ROW NOT FOUND'                                  
036865                    ' SHIP ID/TRIP ' AUSHIP-SHIPT-ID '/'                  
036866                                     AUSHIP-TRIP-ID                       
036867         WHEN SQLWARN0 NOT EQUAL SPACE                                    
036868         WHEN SQLCODE NOT EQUAL ZERO                                      
036869              MOVE '3300-DELETE-TAUSHIP'                                  
036870                                 TO PV-PARAGRAPH-NAME                     
036871              MOVE 'DELETE'                                               
036872                                 TO PV-DB2-OPERATION-MSG-1                
036873              MOVE 'TAUSHIP'     TO PV-DB2-TABLE-NAME                     
036874              PERFORM 9999-DB2-ABEND                                      
036875     END-EVALUATE.                                                        
036876                                                                          
036877* ************************************************************** *        
036878 3400-PROCESS-TAUTRIP.                                                    
036879                                                                          
036880     PERFORM 9350-FETCH-TAUTRIP-CURSOR.                                   
036881     EVALUATE TRUE                                                        
036882                                                                          
036883       WHEN PS-TABLE-LOOP-DONE                                            
036884         PERFORM 4000-COMMIT                                              
036885       WHEN OTHER                                                         
036886         PERFORM 3500-DELETE-TAUTRIP                                      
036887         IF PV-COMMIT-DELETE-OPS = PC-HOW-OFTEN-TO-COMMIT                 
036888            PERFORM 4000-COMMIT                                           
036889            DISPLAY '     TRIP/SCAC/VEHICLE '                             
036890                                    AUTRIP-TRIP-ID  '/'                   
036891                                    AUTRIP-OWNER-SCAC-ID  '/'             
036892                                    AUTRIP-VEH-NBR                        
036893         END-IF                                                           
036894                                                                          
036895     END-EVALUATE.                                                        
036896                                                                          
036897* ************************************************************** *        
036898 3500-DELETE-TAUTRIP.                                                     
036899                                                                          
036900     EXEC SQL                                                             
036901         DELETE                                                           
036902           FROM TAUTRIP                                                   
036903          WHERE TRIP_ID         = :AUTRIP-TRIP-ID                         
036904            AND OWNER_SCAC_ID   = :AUTRIP-OWNER-SCAC-ID                   
036905            AND (VEH_NBR = :AUTRIP-VEH-NBR                                
036906                  OR VEH_NBR IS NULL)                                     
036907     END-EXEC.                                                            
036908                                                                          
036909     EVALUATE TRUE                                                        
036910         WHEN SQLCODE = ZERO                                              
036911              ADD +1 TO PV-COMMIT-DELETE-OPS                              
036912         WHEN SQLCODE = +100                                              
036913            DISPLAY ' DLT ROW NOT FOUND'                                  
036914                    ' TRIP/SCAC/VEHICLE '                                 
036915                                    AUTRIP-TRIP-ID  '/'                   
036916                                    AUTRIP-OWNER-SCAC-ID  '/'             
036917                                    AUTRIP-VEH-NBR                        
036918         WHEN SQLWARN0 NOT EQUAL SPACE                                    
036919         WHEN SQLCODE NOT EQUAL ZERO                                      
036920              MOVE '3500-DELETE-TAUTRIP'                                  
036921                                 TO PV-PARAGRAPH-NAME                     
036922              MOVE 'DELETE'                                               
036923                                 TO PV-DB2-OPERATION-MSG-1                
036924              MOVE 'TAUTRIP'     TO PV-DB2-TABLE-NAME                     
036925              PERFORM 9999-DB2-ABEND                                      
036926     END-EVALUATE.                                                        
036927                                                                          
036928* ************************************************************** *        
036929 3600-PROCESS-TAUVHCL.                                                    
036930                                                                          
036931     PERFORM 9450-FETCH-TAUVHCL-CURSOR.                                   
036932     EVALUATE TRUE                                                        
036933                                                                          
036934       WHEN PS-TABLE-LOOP-DONE                                            
036935         PERFORM 4000-COMMIT                                              
036936       WHEN OTHER                                                         
036937         PERFORM 3700-DELETE-TAUVHCL                                      
036938         IF PV-COMMIT-DELETE-OPS = PC-HOW-OFTEN-TO-COMMIT                 
036939            PERFORM 4000-COMMIT                                           
036940            DISPLAY '     SCAC/VEHICLE '                                  
036941                          AUVHCL-OWNER-SCAC-ID '/'                        
036942                          AUVHCL-VEH-NBR                                  
036943         END-IF                                                           
036944                                                                          
036945     END-EVALUATE.                                                        
036946                                                                          
036947* ************************************************************** *        
036948 3700-DELETE-TAUVHCL.                                                     
036949                                                                          
036950     EXEC SQL                                                             
036951         DELETE                                                           
036952           FROM TAUVHCL                                                   
036953          WHERE OWNER_SCAC_ID = :AUVHCL-OWNER-SCAC-ID                     
036954            AND VEH_NBR       = :AUVHCL-VEH-NBR                           
036955     END-EXEC.                                                            
036956                                                                          
036957     EVALUATE TRUE                                                        
036958         WHEN SQLCODE = ZERO                                              
036959              ADD +1 TO PV-COMMIT-DELETE-OPS                              
036960         WHEN SQLCODE = +100                                              
036961            DISPLAY ' DLT ROW NOT FOUND'                                  
036962                    ' SCAC/VEHICLE '                                      
036963                          AUVHCL-OWNER-SCAC-ID '/'                        
036964                          AUVHCL-VEH-NBR                                  
036965         WHEN SQLWARN0 NOT EQUAL SPACE                                    
036966         WHEN SQLCODE NOT EQUAL ZERO                                      
036967              MOVE '3700-DELETE-TAUVHCL'                                  
036968                                 TO PV-PARAGRAPH-NAME                     
036969              MOVE 'DELETE'                                               
036970                                 TO PV-DB2-OPERATION-MSG-1                
036971              MOVE 'TAUVHCL'     TO PV-DB2-TABLE-NAME                     
036972              PERFORM 9999-DB2-ABEND                                      
036973     END-EVALUATE.                                                        
036974                                                                          
036975* ************************************************************** *        
036980 4000-COMMIT.                                                             
037000                                                                          
037423     EXEC SQL                                                             
037424         COMMIT                                                           
037425     END-EXEC.                                                            
037426                                                                          
037427     EVALUATE TRUE                                                        
037428         WHEN SQLCODE = ZEROS                                             
037429             CONTINUE                                                     
037432         WHEN OTHER                                                       
037433             MOVE '4000-COMMIT'                                           
037434                                 TO PV-PARAGRAPH-NAME                     
037437             MOVE 'UNSUCCESSFUL COMMIT'                                   
037438                                 TO PV-DB2-OPERATION-MSG-1                
037444              MOVE PV-TABLE-NAME TO PV-DB2-TABLE-NAME                     
037445              PERFORM 9999-DB2-ABEND                                      
037446     END-EVALUATE.                                                        
037447                                                                          
037448     ADD PV-COMMIT-DELETE-OPS TO PV-TABLE-DELETE-OPS.                     
037449     DISPLAY  '   ' PV-TABLE-NAME ' COMMIT TAKEN.'                        
037450             '   COMMIT COUNT - ' PV-TABLE-DELETE-OPS                     
037451     INITIALIZE  PV-COMMIT-DELETE-OPS.                                    
037452                                                                          
037453* ************************************************************** *        
037454* * DISPLAY END OF RUN MESSAGE, CLOSE THE OUTPUT FILES         * *        
037455* ************************************************************** *        
037456 6000-EOJ-ROUTINE.                                                        
037457                                                                          
037458     PERFORM 9090-CLOSE-INPUT-FILES.                                      
037500                                                                          
037600     DISPLAY '*********  END OF RUN  *********'.                          
037800                                                                          
040900* ************************************************************** *        
041000 9000-OPEN-CONTROL-FILE.                                                  
041010                                                                          
041100     OPEN  INPUT  IN-CONTROL-FILE.                                        
041300                                                                          
042810* ************************************************************** *        
042820 9050-READ-CONTROL-FILE.                                                  
042821                                                                          
042830     READ IN-CONTROL-FILE                                                 
042840          INTO PV-CONTROL-RECORD                                          
042850          AT END                                                          
042865              SET PS-CONTROL-EOF TO TRUE.                                 
042900                                                                          
043000* ************************************************************** *        
043100* * CLOSE INPUT DRIVER FILE                                    * *        
043200* ************************************************************** *        
043300 9090-CLOSE-INPUT-FILES.                                                  
043310                                                                          
043400     CLOSE IN-CONTROL-FILE.                                               
043600                                                                          
043900* ************************************************************** *        
044000 9100-OPEN-TAUSHPO-CURSOR.                                                
044010                                                                          
044100     EXEC SQL                                                             
044200          OPEN TAUSHPO-CURSOR                                             
044300     END-EXEC.                                                            
044400                                                                          
044500     EVALUATE TRUE                                                        
044600         WHEN SQLCODE EQUAL ZERO                                          
044800              SET PS-CURSOR-HAS-DATA TO TRUE                              
044900         WHEN SQLCODE = +100                                              
045000              SET PS-CURSOR-HAS-NO-DATA TO TRUE                           
045100         WHEN SQLWARN0 NOT EQUAL SPACE                                    
045200         WHEN SQLCODE  NOT EQUAL ZERO                                     
045300              MOVE '9100-OPEN-TAUSHPO-CURSOR'                             
045400                                 TO PV-PARAGRAPH-NAME                     
045500              MOVE 'OPEN TABLE CURSOR'                                    
045600                                 TO PV-DB2-OPERATION-MSG-1                
045700              MOVE 'TAUSHPO'     TO PV-DB2-TABLE-NAME                     
045800              PERFORM 9999-DB2-ABEND                                      
045900     END-EVALUATE.                                                        
046000                                                                          
046100                                                                          
046200* ************************************************************** *        
046300* * FETCH TABLE CURSOR                                         * *        
046400* ************************************************************** *        
046500 9150-FETCH-TAUSHPO-CURSOR.                                               
046510                                                                          
046600     EXEC SQL                                                             
046700         FETCH TAUSHPO-CURSOR                                             
046800          INTO                                                            
046900             :AUSHPO-SHIPT-ID                                             
047000            ,:AUSHPO-PO-NBR                                               
051902     END-EXEC.                                                            
051903                                                                          
051904     EVALUATE TRUE                                                        
051905         WHEN SQLCODE = ZERO                                              
051907              CONTINUE                                                    
052000         WHEN SQLCODE = +100                                              
052100              SET PS-TABLE-LOOP-DONE TO TRUE                              
052200         WHEN SQLWARN0 NOT EQUAL SPACE                                    
052300         WHEN SQLCODE NOT EQUAL ZERO                                      
052400              MOVE '9150-FETCH-TAUSHPO-CURSOR'                            
052500                                 TO PV-PARAGRAPH-NAME                     
052600              MOVE 'FETCH TABLE CURSOR'                                   
052700                                 TO PV-DB2-OPERATION-MSG-1                
052800              MOVE 'TAUSHPO'     TO PV-DB2-TABLE-NAME                     
052900              PERFORM 9999-DB2-ABEND                                      
053000     END-EVALUATE.                                                        
053100                                                                          
053200                                                                          
053300* ************************************************************** *        
053400* * CLOSE TABLE CURSOR                                         * *        
053500* ************************************************************** *        
053600 9190-CLOSE-TAUSHPO-CURSOR.                                               
053700     EXEC SQL                                                             
053800          CLOSE TAUSHPO-CURSOR                                            
053900     END-EXEC.                                                            
054000                                                                          
054100     EVALUATE TRUE                                                        
054200         WHEN SQLCODE = ZERO                                              
054300              CONTINUE                                                    
054400         WHEN SQLWARN0 NOT EQUAL SPACE                                    
054500         WHEN SQLCODE NOT EQUAL ZERO                                      
054600              MOVE '9190-CLOSE-TAUSHPO-CURSOR'                            
054700                                 TO PV-PARAGRAPH-NAME                     
054800              MOVE 'CLOSE TABLE CURSOR'                                   
054900                                 TO PV-DB2-OPERATION-MSG-1                
055000              MOVE 'TAUSHPO'     TO PV-DB2-TABLE-NAME                     
055100              PERFORM 9999-DB2-ABEND                                      
055200     END-EVALUATE.                                                        
055300                                                                          
055310* ************************************************************** *        
055320 9200-OPEN-TAUSHIP-CURSOR.                                                
055330                                                                          
055340     EXEC SQL                                                             
055350          OPEN TAUSHIP-CURSOR                                             
055360     END-EXEC.                                                            
055370                                                                          
055380     EVALUATE TRUE                                                        
055390         WHEN SQLCODE EQUAL ZERO                                          
055391              SET PS-CURSOR-HAS-DATA TO TRUE                              
055392         WHEN SQLCODE = +100                                              
055393              SET PS-CURSOR-HAS-NO-DATA TO TRUE                           
055394         WHEN SQLWARN0 NOT EQUAL SPACE                                    
055395         WHEN SQLCODE  NOT EQUAL ZERO                                     
055396              MOVE '9200-OPEN-TAUSHIP-CURSOR'                             
055397                                 TO PV-PARAGRAPH-NAME                     
055398              MOVE 'OPEN TABLE CURSOR'                                    
055399                                 TO PV-DB2-OPERATION-MSG-1                
055400              MOVE 'TAUSHIP'     TO PV-DB2-TABLE-NAME                     
055410              PERFORM 9999-DB2-ABEND                                      
055420     END-EVALUATE.                                                        
055430                                                                          
055440                                                                          
055450* ************************************************************** *        
055460* * FETCH TABLE CURSOR                                         * *        
055470* ************************************************************** *        
055480 9250-FETCH-TAUSHIP-CURSOR.                                               
055492                                                                          
055493     EXEC SQL                                                             
055494         FETCH TAUSHIP-CURSOR                                             
055495          INTO                                                            
055496             :AUSHIP-TRIP-ID:PV-NULL                                      
055497            ,:AUSHIP-SHIPT-ID                                             
055498     END-EXEC.                                                            
055499                                                                          
055500     EVALUATE TRUE                                                        
055501         WHEN SQLCODE = ZERO                                              
055502              CONTINUE                                                    
055510         WHEN SQLCODE = +100                                              
055520              SET PS-TABLE-LOOP-DONE TO TRUE                              
055530         WHEN SQLWARN0 NOT EQUAL SPACE                                    
055540         WHEN SQLCODE NOT EQUAL ZERO                                      
055550              MOVE '9250-FETCH-TAUSHIP-CURSOR'                            
055560                                 TO PV-PARAGRAPH-NAME                     
055570              MOVE 'FETCH TABLE CURSOR'                                   
055580                                 TO PV-DB2-OPERATION-MSG-1                
055590              MOVE 'TAUSHIP'     TO PV-DB2-TABLE-NAME                     
055591              PERFORM 9999-DB2-ABEND                                      
055592     END-EVALUATE.                                                        
055593                                                                          
055594                                                                          
055595* ************************************************************** *        
055596* * CLOSE TABLE CURSOR                                         * *        
055597* ************************************************************** *        
055598 9290-CLOSE-TAUSHIP-CURSOR.                                               
055599     EXEC SQL                                                             
055600          CLOSE TAUSHIP-CURSOR                                            
055610     END-EXEC.                                                            
055620                                                                          
055630     EVALUATE TRUE                                                        
055640         WHEN SQLCODE = ZERO                                              
055650              CONTINUE                                                    
055660         WHEN SQLWARN0 NOT EQUAL SPACE                                    
055670         WHEN SQLCODE NOT EQUAL ZERO                                      
055680              MOVE '9290-CLOSE-TAUSHIP-CURSOR'                            
055690                                 TO PV-PARAGRAPH-NAME                     
055691              MOVE 'CLOSE TABLE CURSOR'                                   
055692                                 TO PV-DB2-OPERATION-MSG-1                
055693              MOVE 'TAUSHIP'     TO PV-DB2-TABLE-NAME                     
055694              PERFORM 9999-DB2-ABEND                                      
055695     END-EVALUATE.                                                        
055696                                                                          
055697* ************************************************************** *        
055698 9300-OPEN-TAUTRIP-CURSOR.                                                
055699                                                                          
055700     EXEC SQL                                                             
055710          OPEN TAUTRIP-CURSOR                                             
055720     END-EXEC.                                                            
055730                                                                          
055740     EVALUATE TRUE                                                        
055750         WHEN SQLCODE EQUAL ZERO                                          
055760              SET PS-CURSOR-HAS-DATA TO TRUE                              
055770         WHEN SQLCODE = +100                                              
055780              SET PS-CURSOR-HAS-NO-DATA TO TRUE                           
055790         WHEN SQLWARN0 NOT EQUAL SPACE                                    
055791         WHEN SQLCODE  NOT EQUAL ZERO                                     
055792              MOVE '9300-OPEN-TAUTRIP-CURSOR'                             
055793                                 TO PV-PARAGRAPH-NAME                     
055794              MOVE 'OPEN TABLE CURSOR'                                    
055795                                 TO PV-DB2-OPERATION-MSG-1                
055796              MOVE 'TAUTRIP'     TO PV-DB2-TABLE-NAME                     
055797              PERFORM 9999-DB2-ABEND                                      
055798     END-EVALUATE.                                                        
055799                                                                          
055800                                                                          
055810* ************************************************************** *        
055820* * FETCH TABLE CURSOR                                         * *        
055830* ************************************************************** *        
055840 9350-FETCH-TAUTRIP-CURSOR.                                               
055850                                                                          
055851     MOVE LOW-VALUES TO AUTRIP-VEH-NBR.                                   
055852                                                                          
055860     EXEC SQL                                                             
055870         FETCH TAUTRIP-CURSOR                                             
055880          INTO                                                            
055890             :AUTRIP-TRIP-ID                                              
055891            ,:AUTRIP-OWNER-SCAC-ID                                        
055892            ,:AUTRIP-VEH-NBR:PV-NULL                                      
055896     END-EXEC.                                                            
055897                                                                          
055898     EVALUATE TRUE                                                        
055899         WHEN SQLCODE = ZERO                                              
055900              CONTINUE                                                    
055901         WHEN SQLCODE = +100                                              
055902              SET PS-TABLE-LOOP-DONE TO TRUE                              
055903         WHEN SQLWARN0 NOT EQUAL SPACE                                    
055904         WHEN SQLCODE NOT EQUAL ZERO                                      
055910              MOVE '9350-FETCH-TAUTRIP-CURSOR'                            
055920                                 TO PV-PARAGRAPH-NAME                     
055930              MOVE 'FETCH TABLE CURSOR'                                   
055940                                 TO PV-DB2-OPERATION-MSG-1                
055950              MOVE 'TAUTRIP'     TO PV-DB2-TABLE-NAME                     
055960              PERFORM 9999-DB2-ABEND                                      
055970     END-EVALUATE.                                                        
055980                                                                          
055981* ************************************************************** *        
055982* * CLOSE TABLE CURSOR                                         * *        
055983* ************************************************************** *        
055984 9390-CLOSE-TAUTRIP-CURSOR.                                               
055985     EXEC SQL                                                             
055986          CLOSE TAUTRIP-CURSOR                                            
055987     END-EXEC.                                                            
055988                                                                          
055989     EVALUATE TRUE                                                        
055990         WHEN SQLCODE = ZERO                                              
055991              CONTINUE                                                    
055992         WHEN SQLWARN0 NOT EQUAL SPACE                                    
055993         WHEN SQLCODE NOT EQUAL ZERO                                      
055994              MOVE '9390-CLOSE-TAUTRIP-CURSOR'                            
055995                                 TO PV-PARAGRAPH-NAME                     
055996              MOVE 'CLOSE TABLE CURSOR'                                   
055997                                 TO PV-DB2-OPERATION-MSG-1                
055998              MOVE 'TAUTRIP'     TO PV-DB2-TABLE-NAME                     
055999              PERFORM 9999-DB2-ABEND                                      
056000     END-EVALUATE.                                                        
056001                                                                          
056002* ************************************************************** *        
056003 9400-OPEN-TAUVHCL-CURSOR.                                                
056004                                                                          
056005     EXEC SQL                                                             
056006          OPEN TAUVHCL-CURSOR                                             
056007     END-EXEC.                                                            
056008                                                                          
056009     EVALUATE TRUE                                                        
056010         WHEN SQLCODE EQUAL ZERO                                          
056011              SET PS-CURSOR-HAS-DATA TO TRUE                              
056012         WHEN SQLCODE = +100                                              
056013              SET PS-CURSOR-HAS-NO-DATA TO TRUE                           
056014         WHEN SQLWARN0 NOT EQUAL SPACE                                    
056015         WHEN SQLCODE  NOT EQUAL ZERO                                     
056016              MOVE '9400-OPEN-TAUVHCL-CURSOR'                             
056017                                 TO PV-PARAGRAPH-NAME                     
056018              MOVE 'OPEN TABLE CURSOR'                                    
056019                                 TO PV-DB2-OPERATION-MSG-1                
056020              MOVE 'TAUVHCL'     TO PV-DB2-TABLE-NAME                     
056021              PERFORM 9999-DB2-ABEND                                      
056022     END-EVALUATE.                                                        
056023                                                                          
056024                                                                          
056025* ************************************************************** *        
056026* * FETCH TABLE CURSOR                                         * *        
056027* ************************************************************** *        
056028 9450-FETCH-TAUVHCL-CURSOR.                                               
056029                                                                          
056030     EXEC SQL                                                             
056031         FETCH TAUVHCL-CURSOR                                             
056032          INTO                                                            
056033             :AUVHCL-OWNER-SCAC-ID                                        
056034            ,:AUVHCL-VEH-NBR                                              
056035     END-EXEC.                                                            
056036                                                                          
056037     EVALUATE TRUE                                                        
056038         WHEN SQLCODE = ZERO                                              
056039              CONTINUE                                                    
056040         WHEN SQLCODE = +100                                              
056041              SET PS-TABLE-LOOP-DONE TO TRUE                              
056042         WHEN SQLWARN0 NOT EQUAL SPACE                                    
056043         WHEN SQLCODE NOT EQUAL ZERO                                      
056044              MOVE '9450-FETCH-TAUVHCL-CURSOR'                            
056045                                 TO PV-PARAGRAPH-NAME                     
056046              MOVE 'FETCH TABLE CURSOR'                                   
056047                                 TO PV-DB2-OPERATION-MSG-1                
056048              MOVE 'TAUVHCL'     TO PV-DB2-TABLE-NAME                     
056049              PERFORM 9999-DB2-ABEND                                      
056050     END-EVALUATE.                                                        
056051                                                                          
056052                                                                          
056053* ************************************************************** *        
056054* * CLOSE TABLE CURSOR                                         * *        
056055* ************************************************************** *        
056056 9490-CLOSE-TAUVHCL-CURSOR.                                               
056057     EXEC SQL                                                             
056058          CLOSE TAUVHCL-CURSOR                                            
056059     END-EXEC.                                                            
056060                                                                          
056061     EVALUATE TRUE                                                        
056062         WHEN SQLCODE = ZERO                                              
056063              CONTINUE                                                    
056064         WHEN SQLWARN0 NOT EQUAL SPACE                                    
056065         WHEN SQLCODE NOT EQUAL ZERO                                      
056066              MOVE '9490-CLOSE-TAUVHCL-CURSOR'                            
056067                                 TO PV-PARAGRAPH-NAME                     
056068              MOVE 'CLOSE TABLE CURSOR'                                   
056070                                 TO PV-DB2-OPERATION-MSG-1                
056080              MOVE 'TAUVHCL'     TO PV-DB2-TABLE-NAME                     
056090              PERFORM 9999-DB2-ABEND                                      
056091     END-EVALUATE.                                                        
056092                                                                          
056100                                                                          
057700* ************************************************************** *        
057800* * DISPLAY INFORMATION NEEDED FOR DEBUGGING/MAINTENANCE/SUPPORT *        
057900* * AND CALL THE ABEND PROCESSOR IN THE EVENT THAT AN          * *        
058000* * SQL ERROR OR WARNING CODE OCCURS.                          * *        
058100* ************************************************************** *        
058200 9999-DB2-ABEND.                                                          
058300                                                                          
058400     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
058500             ' 9999-DB2-ABEND'.                                           
058600     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
058700             ' ** ABEND           **'.                                    
058800     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
058900             ' ** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.               
059000     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059100             ' ** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME.               
059200     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059300             ' ** OPERATION       ** = ' PV-DB2-OPERATION-MSG-1.          
059400                                                                          
059500     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059600             ' ** SQLCODE         ** = ' SQLCODE.                         
059700     MOVE SQLCODE TO PV-SQLCODE.                                          
059800     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059900             ' ** PV-SQLCODE      ** = ' PV-SQLCODE '.'.                  
060000                                                                          
060100     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
060200             ' ** SQLWARN0        ** = ' SQLWARN0 '.'.                    
060300                                                                          
060400                                                                          
060500*----------------------------------------------------------------*        
060600* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
060700* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
060800* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
060900* FORMAT.                                                        *        
061000*----------------------------------------------------------------*        
061100     COPY DPPD004.                                                        
061200                                                                          
061300     MOVE PC-ABENDING               TO DP-STATUS-DESCRIPTION.             
061500     PERFORM 9090-CLOSE-INPUT-FILES.                                      
061700                                                                          
061800*----------------------------------------------------------------*        
061900* CALL ABEND                                                     *        
062000*----------------------------------------------------------------*        
062100     MOVE +4013                  TO ABEND-CODE.                           
062200     CALL 'ILBOABN0' USING ABEND-CODE.                                    
062300*                                                                         
