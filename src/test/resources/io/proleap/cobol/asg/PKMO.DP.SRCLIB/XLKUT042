000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.    XLKUT042.                                                 
000500 AUTHOR.        DEANNA BRANTNER.                                          
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN   APRIL, 2003.                                              
000800 DATE-COMPILED.                                                           
000900****************************************************************          
001000*    THIS PROGRAM WILL COMPARE RMS PO / LEGACY PO              *          
001100* PO DOMAIN TEMPLATE.........: XXT_PO_INSTR                    *          
001200* PO DOMAIN TEMPLATE COPYBOOK: XLRD016                         *          
001300*                                                              *          
001400*        INPUT COMES TO THIS PROGRAM FROM XLKUT037 AND MUST    *          
001500*    BE SORTED BY THE PO NUMBER AND RECIP CODE.                *          
001600*                                                              *          
001700*    THIS PROGRAM MATCHES THE LEGACY AND RMS FILES BY PO       *          
001800*    NUMBER, RECIPIENT CODE AND INSTR_ID. IT                   *          
001900*    WRITES ERROR RECORDS OUT FOR ANY UNMATCHED RECORDS. IT    *          
002000*    ALSO CHECKS TO SEE IF THERE ARE ANY DATA DIFFERENCES FOR  *          
002100*    THOSE RECORDS WHOSE PO NUMBERS MATCH. IF THERE ARE ANY,   *          
002200*    AN ERROR RECORD IS WRITTEN INDICATING THE FIELDS THAT     *          
002300*    ARE DIFFERENT.                                            *          
002400*        THERE ARE SOME MOVES OF FIELDS COMMENTED OUT. THIS    *          
002500*    HAS BEEN DONE BECAUSE SOME OF THESE FIELDS AREN'T AVAIL-  *          
002600*    ABLE ON ONE OF THE SYSTEMS.  YOU MAY FIND THESE BY DOING  *          
002700*    A F '**??'.                                               *          
002800*****************************************************************         
002900 ENVIRONMENT DIVISION.                                                    
003000 INPUT-OUTPUT SECTION.                                                    
003100                                                                          
003200 FILE-CONTROL.                                                            
003300                                                                          
003400     SELECT RMS-PO-FILE                                                   
003500         ASSIGN TO INP01.                                                 
003600                                                                          
003700     SELECT LEGACY-FILE                                                   
003800         ASSIGN TO INP02.                                                 
003900                                                                          
004000     SELECT ERROR-FILE                                                    
004100         ASSIGN TO OUT01.                                                 
004200                                                                          
004300*****************************************************************         
004400 DATA DIVISION.                                                           
004500*****************************************************************         
004600 FILE SECTION.                                                            
004700                                                                          
004800 FD  RMS-PO-FILE                                                          
004900     RECORDING MODE IS F                                                  
005000     BLOCK CONTAINS 0 RECORDS                                             
005100     LABEL RECORDS ARE STANDARD.                                          
005200                                                                          
005300 01  RMS-PO-RECORD                PIC X(168).                             
005400                                                                          
005500 FD  LEGACY-FILE                                                          
005600     RECORDING MODE IS F                                                  
005700     BLOCK CONTAINS 0 RECORDS                                             
005800     LABEL RECORDS ARE STANDARD.                                          
005900                                                                          
006000 01  LEGACY-RECORD                PIC X(168).                             
006100                                                                          
006200                                                                          
006300 FD  ERROR-FILE                                                           
006400     RECORDING MODE IS F                                                  
006500     BLOCK CONTAINS 0 RECORDS                                             
006600     LABEL RECORDS ARE STANDARD.                                          
006700                                                                          
006800 01  ERROR-RECORD                 PIC X(830).                             
006900                                                                          
007000                                                                          
007100*****************************************************************         
007200 WORKING-STORAGE SECTION.                                                 
007300                                                                          
007400 01  PS-PROGRAM-SWITCHES.                                                 
007500     05  PS-EOF-INPUT1-SW             PIC X(01)  VALUE 'N'.               
007600         88  PS-EOF-INPUT1-YES                   VALUE 'Y'.               
007700         88  PS-EOF-INPUT1-NO                    VALUE 'N'.               
007800     05  PS-EOF-INPUT2-SW             PIC X(01)  VALUE 'N'.               
007900         88  PS-EOF-INPUT2-YES                   VALUE 'Y'.               
008000         88  PS-EOF-INPUT2-NO                    VALUE 'N'.               
008100     05  PS-EMAIL-SW                  PIC X(01)  VALUE 'N'.               
008200         88  PS-EMAIL-YES                        VALUE 'Y'.               
008300         88  PS-EMAIL-NO                         VALUE 'N'.               
008400                                                                          
008500 01  PC-PROGRAM-CONSTANTS.                                                
008600     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT042'.        
008700     05  PC-FIVE-BLANKS               PIC  X(05) VALUE SPACES.            
008800                                                                          
008900 01  PC-SORT-KEY.                                                         
009000     05  PC-RECIP-CDE                 PIC  X(02) VALUE SPACES.            
009100     05  PC-INSTR-ID                  PIC  9(08) VALUE ZEROES.            
009200                                                                          
009300 01  PV-PROGRAM-VARIABLES.                                                
009400     05  PV-RECORDS-READ-1            PIC  9(09) VALUE  0.                
009500     05  PV-RECORDS-READ-2            PIC  9(09) VALUE  0.                
009600     05  PV-RECORDS-WRITTEN           PIC  9(09) VALUE  0.                
009700     05  PV-RECORDS-ON-BOTH           PIC  9(09) VALUE  0.                
009800     05  PV-RECORDS-ON-RMS-ONLY       PIC  9(09) VALUE  0.                
009900     05  PV-RECORDS-ON-LEGACY-ONLY    PIC  9(09) VALUE  0.                
010000     05  PV-RECORDS-DATA-MISMATCH     PIC  9(09) VALUE  0.                
010100     05  PV-ALL-NINES                 PIC  9(08) VALUE 99999999.          
010200                                                                          
010300 01  PV-ABEND-FIELDS.                                                     
010400     05  PV-ABEND-CODE                PIC  S9(4) VALUE +0                 
010500                                                 COMP SYNC.               
010600     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT042'.        
010700     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
010800     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
010900     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
011000                                                                          
011100 01  PV-RMS-INPUT.                                                        
011200     COPY XLRD016.                                                        
011300                                                                          
011400 01  PV-LEGACY-INPUT.                                                     
011500     COPY XLRD016.                                                        
011600                                                                          
011700 01  PV-ERROR-OUTPUT.                                                     
011800     COPY XLRD023.                                                        
011900                                                                          
012000*****************************************************************         
012100 PROCEDURE DIVISION.                                                      
012200*****************************************************************         
012300     DISPLAY ' '.                                                         
012400     DISPLAY '**** BEGINNING XLKUT042 ****'.                              
012500     DISPLAY ' '.                                                         
012600                                                                          
012700     OPEN INPUT  RMS-PO-FILE                                              
012800                 LEGACY-FILE                                              
012900          OUTPUT ERROR-FILE.                                              
013000     SET PS-EMAIL-NO       TO TRUE.                                       
013100                                                                          
013200     PERFORM 1000-READ-RMS-INPUT.                                         
013300     PERFORM 1100-READ-LEGACY-INPUT.                                      
013400                                                                          
013500     PERFORM 2000-PROCESS-INPUT-FILE                                      
013600       UNTIL PS-EOF-INPUT1-YES AND                                        
013700             PS-EOF-INPUT2-YES.                                           
013800                                                                          
013900     CLOSE RMS-PO-FILE                                                    
014000           LEGACY-FILE                                                    
014100           ERROR-FILE.                                                    
014200                                                                          
014300     DISPLAY ' '.                                                         
014400     DISPLAY '  INPUT RMS PO REC = ' PV-RECORDS-READ-1.                   
014500     DISPLAY '  INPUT LEGACY REC = ' PV-RECORDS-READ-2.                   
014600     DISPLAY ' '.                                                         
014700     DISPLAY '          XLRD016'.                                         
014800     DISPLAY 'FILES ARE MATCHED BY PO NUMBER - 8 POSITIONS'.              
014900     DISPLAY '                     RECIP_CDE - 2 POSITIONS'.              
015000     DISPLAY ' '.                                                         
015100     DISPLAY PC-FIVE-BLANKS                                               
015200             'RECORDS ON BOTH FILES ---------> '                          
015300              PV-RECORDS-ON-BOTH.                                         
015400     DISPLAY ' '.                                                         
015500     DISPLAY '  OUTPUT RECORDS WRITTEN ------> '                          
015600              PV-RECORDS-WRITTEN.                                         
015700     DISPLAY PC-FIVE-BLANKS                                               
015800             'RECORDS ON RMS ONLY -----------> '                          
015900              PV-RECORDS-ON-RMS-ONLY                                      
016000             ' - "A" RECORD CODE'.                                        
016100     DISPLAY PC-FIVE-BLANKS                                               
016200             'RECORDS ON LEGACY ONLY --------> '                          
016300              PV-RECORDS-ON-LEGACY-ONLY                                   
016400             ' - "B" RECORD CODE'.                                        
016500     DISPLAY PC-FIVE-BLANKS                                               
016600             'RECORDS WITH DATA MISMATCH ----> '                          
016700              PV-RECORDS-DATA-MISMATCH                                    
016800             ' - "C" RECORD CODE'.                                        
016900     DISPLAY ' '.                                                         
017000     DISPLAY '****  END OF  XLKUT042  ****'.                              
017100     DISPLAY ' '.                                                         
017200                                                                          
017300*    SET RETURN CODE IF E-MAIL SWITCH IS TRUE.                            
017400     IF PS-EMAIL-YES                                                      
017500         MOVE 4095 TO RETURN-CODE                                         
017600     END-IF.                                                              
017700     GOBACK.                                                              
017800                                                                          
017900                                                                          
018000*****************************************************************         
018100*  READ A RMS PO RECORD                                                   
018200*****************************************************************         
018300 1000-READ-RMS-INPUT.                                                     
018400                                                                          
018500*    DISPLAY '1000-READ-RMS-INPUT'                                        
018600                                                                          
018700     MOVE '1000-READ-RMS-INPUT    ' TO PV-ABEND-PARAGRAPH.                
018800                                                                          
018900     INITIALIZE PV-RMS-INPUT.                                             
019000                                                                          
019100     READ RMS-PO-FILE                                                     
019200          INTO PV-RMS-INPUT                                               
019300            AT END                                                        
019400               SET PS-EOF-INPUT1-YES TO TRUE                              
019500     END-READ.                                                            
019600                                                                          
019700     IF PS-EOF-INPUT1-YES                                                 
019800        MOVE PV-ALL-NINES TO XL016-PO-NBR OF PV-RMS-INPUT                 
019900                             XL016-RECIP-CDE OF PV-RMS-INPUT              
020000     ELSE                                                                 
020100        ADD +1 TO PV-RECORDS-READ-1                                       
020200*        DISPLAY PV-RECORDS-READ-1                                        
020300     END-IF.                                                              
020400                                                                          
020500*        DISPLAY 'PV-RMS-INPUT = '                                        
020600*        DISPLAY  PV-RMS-INPUT.                                           
020700                                                                          
020800*****************************************************************         
020900*  READ A LEGACY RECORD                                                   
021000*****************************************************************         
021100 1100-READ-LEGACY-INPUT.                                                  
021200                                                                          
021300     MOVE '1100-READ-LEGACY-INPUT' TO PV-ABEND-PARAGRAPH.                 
021400*    DISPLAY PV-ABEND-PARAGRAPH                                           
021500                                                                          
021600     INITIALIZE PV-LEGACY-INPUT.                                          
021700                                                                          
021800     READ LEGACY-FILE                                                     
021900          INTO PV-LEGACY-INPUT                                            
022000            AT END                                                        
022100               SET PS-EOF-INPUT2-YES TO TRUE                              
022200     END-READ.                                                            
022300                                                                          
022400     IF PS-EOF-INPUT2-YES                                                 
022500        MOVE PV-ALL-NINES TO XL016-PO-NBR OF PV-LEGACY-INPUT              
022600                             XL016-RECIP-CDE OF PV-LEGACY-INPUT           
022700     ELSE                                                                 
022800        ADD +1 TO PV-RECORDS-READ-2                                       
022900*        DISPLAY PV-RECORDS-READ-2                                        
023000     END-IF.                                                              
023100                                                                          
023200*        DISPLAY 'PV-LEGACY-INPUT = '                                     
023300*        DISPLAY  PV-LEGACY-INPUT.                                        
023400                                                                          
023500*****************************************************************         
023600*  REFORMAT THE RMS PO EXTRACT FILE                                       
023700*****************************************************************         
023800 2000-PROCESS-INPUT-FILE.                                                 
023900                                                                          
024000     MOVE '2000-PROCESS-INPUT-FILE ' TO PV-ABEND-PARAGRAPH.               
024100*    DISPLAY PV-ABEND-PARAGRAPH.                                          
024200                                                                          
024300     IF XL016-PO-NBR   OF PV-RMS-INPUT    <                               
024400        XL016-PO-NBR   OF PV-LEGACY-INPUT                                 
024500******************************                                            
024600***  ON RMS NOT ON LEGACY                                                 
024700******************************                                            
024800        ADD 1 TO PV-RECORDS-ON-RMS-ONLY                                   
024900        SET XL023-ON-RMS-ONLY TO TRUE                                     
025000        PERFORM 3400-WRITE-RMS-ERROR                                      
025100        PERFORM 1000-READ-RMS-INPUT                                       
025200     ELSE                                                                 
025300        IF XL016-PO-NBR  OF PV-RMS-INPUT    >                             
025400           XL016-PO-NBR  OF PV-LEGACY-INPUT                               
025500******************************                                            
025600***   ON LEGACY NOT ON RMS                                                
025700******************************                                            
025800           ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                             
025900           SET XL023-ON-LEGACY-ONLY TO TRUE                               
026000           PERFORM 4000-WRITE-LEGACY-ERROR                                
026100           PERFORM 1100-READ-LEGACY-INPUT                                 
026200        ELSE                                                              
026300******************************                                            
026400***  ON RMS AND ON LEGACY                                                 
026500******************************                                            
026600           PERFORM 2400-CHECK-KEY-VALUES                                  
026700        END-IF                                                            
026800     END-IF.                                                              
026900                                                                          
027000****************************************************************          
027100*    CHECK ALL THE KEY VALUES FOR MISMATCHES.                  *          
027200*                                                              *          
027300*                                                              *          
027400****************************************************************          
027500 2400-CHECK-KEY-VALUES.                                                   
027600                                                                          
027700     MOVE '2400-CHECK-KEY-VALUES ' TO PV-ABEND-PARAGRAPH.                 
027800*    DISPLAY PV-ABEND-PARAGRAPH.                                          
027900                                                                          
028000     EVALUATE TRUE                                                        
028100         WHEN XL016-RECIP-CDE OF PV-RMS-INPUT <                           
028200              XL016-RECIP-CDE OF PV-LEGACY-INPUT                          
028300              ADD 1 TO PV-RECORDS-ON-RMS-ONLY                             
028400              SET XL023-ON-RMS-ONLY TO TRUE                               
028500              PERFORM 3400-WRITE-RMS-ERROR                                
028600              PERFORM 1000-READ-RMS-INPUT                                 
028700                                                                          
028800         WHEN XL016-RECIP-CDE OF PV-RMS-INPUT >                           
028900              XL016-RECIP-CDE OF PV-LEGACY-INPUT                          
029000              ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                          
029100              SET XL023-ON-LEGACY-ONLY TO TRUE                            
029200              PERFORM 4000-WRITE-LEGACY-ERROR                             
029300              PERFORM 1100-READ-LEGACY-INPUT                              
029400                                                                          
029500         WHEN XL016-RECIP-CDE OF PV-RMS-INPUT =                           
029600              XL016-RECIP-CDE OF PV-LEGACY-INPUT                          
029700              ADD 1 TO PV-RECORDS-ON-BOTH                                 
029800              SET XL023-ON-BOTH-RMS-LEGACY TO TRUE                        
029900              PERFORM 3400-WRITE-RMS-ERROR                                
030000              PERFORM 4000-WRITE-LEGACY-ERROR                             
030100                                                                          
030200* INC000000229197                                                         
030300              EVALUATE TRUE                                               
030400                 WHEN XL016-INSTR-DESC OF PV-RMS-INPUT =                  
030500                           XL016-INSTR-DESC OF PV-LEGACY-INPUT            
030600                     PERFORM 1000-READ-RMS-INPUT                          
030700                     PERFORM 1100-READ-LEGACY-INPUT                       
030800                 WHEN XL016-INSTR-DESC OF PV-RMS-INPUT <                  
030900                           XL016-INSTR-DESC OF PV-LEGACY-INPUT            
031000                     PERFORM 1000-READ-RMS-INPUT                          
031100                 WHEN XL016-INSTR-DESC OF PV-RMS-INPUT >                  
031200                           XL016-INSTR-DESC OF PV-LEGACY-INPUT            
031300                     PERFORM 1100-READ-LEGACY-INPUT                       
031400              END-EVALUATE                                                
031500* INC000000229197                                                         
031600                                                                          
031700     END-EVALUATE.                                                        
031800                                                                          
031900*****************************************************************         
032000*  WRITE A RMS RECORD TO THE ERROR FILE                                   
032100*****************************************************************         
032200 3400-WRITE-RMS-ERROR.                                                    
032300     MOVE '3400-WRITE-RMS-ERROR'      TO PV-ABEND-PARAGRAPH.              
032400                                                                          
032500*    DISPLAY PV-ABEND-PARAGRAPH.                                          
032600                                                                          
032700     SET XL023-XXT-PO-INSTR TO TRUE.                                      
032800     MOVE XL016-PO-NBR OF PV-RMS-INPUT TO XL023-PO-SORT-KEY-PO.           
032900     MOVE XL016-RECIP-CDE OF PV-RMS-INPUT TO PC-RECIP-CDE.                
033000     MOVE XL016-INSTR-ID  OF PV-RMS-INPUT TO PC-INSTR-ID.                 
033100     MOVE PC-SORT-KEY                   TO XL023-PO-SORT-KEY-DATA.        
033200     INITIALIZE XL023-PO-INSTR.                                           
033300                                                                          
033400     MOVE XL016-PO-NBR OF PV-RMS-INPUT     TO XL023-IN-PO-NBR-RMS.        
033500     MOVE XL016-RECIP-CDE OF PV-RMS-INPUT                                 
033600                                        TO XL023-IN-RECIP-CDE-RMS.        
033700     MOVE XL016-INSTR-ID OF PV-RMS-INPUT TO XL023-IN-INSTR-ID-RMS.        
033800     MOVE FUNCTION UPPER-CASE(XL016-INSTR-DESC OF PV-RMS-INPUT)           
033900                                       TO XL023-IN-INSTR-DESC-RMS.        
034000                                                                          
034100     IF XL023-ON-BOTH-RMS-LEGACY                                          
034200         CONTINUE                                                         
034300     ELSE                                                                 
034400         WRITE ERROR-RECORD FROM  PV-ERROR-OUTPUT                         
034500         ADD +1              TO   PV-RECORDS-WRITTEN                      
034600     END-IF.                                                              
034700                                                                          
034800*****************************************************************         
034900*  WRITE A LEGACY RECORD TO THE ERROR FILE                                
035000*****************************************************************         
035100 4000-WRITE-LEGACY-ERROR.                                                 
035200                                                                          
035300     MOVE '4000-WRITE-LEGACY-ERROR'   TO PV-ABEND-PARAGRAPH.              
035400                                                                          
035500*    DISPLAY PV-ABEND-PARAGRAPH.                                          
035600     IF XL023-ON-BOTH-RMS-LEGACY                                          
035700         CONTINUE                                                         
035800     ELSE                                                                 
035900         SET XL023-XXT-PO-INSTR TO TRUE                                   
036000         MOVE XL016-PO-NBR OF PV-LEGACY-INPUT                             
036100                                           TO XL023-PO-SORT-KEY-PO        
036200         MOVE XL016-RECIP-CDE OF PV-LEGACY-INPUT TO PC-RECIP-CDE          
036300         MOVE XL016-INSTR-ID  OF PV-LEGACY-INPUT TO PC-INSTR-ID           
036400         MOVE PC-SORT-KEY               TO XL023-PO-SORT-KEY-DATA         
036500         INITIALIZE XL023-PO-INSTR                                        
036600     END-IF.                                                              
036700                                                                          
036800     MOVE XL016-PO-NBR OF PV-LEGACY-INPUT                                 
036900                                         TO XL023-IN-PO-NBR-LGCY.         
037000     MOVE XL016-RECIP-CDE OF PV-LEGACY-INPUT                              
037100                                      TO XL023-IN-RECIP-CDE-LGCY.         
037200     MOVE XL016-INSTR-ID OF PV-LEGACY-INPUT                               
037300                                       TO XL023-IN-INSTR-ID-LGCY.         
037400     MOVE FUNCTION UPPER-CASE(XL016-INSTR-DESC OF PV-LEGACY-INPUT)        
037500                                     TO XL023-IN-INSTR-DESC-LGCY.         
037600                                                                          
037700     IF XL023-ON-BOTH-RMS-LEGACY                                          
037800         PERFORM 4300-COMPARE                                             
037900     ELSE                                                                 
038000         WRITE ERROR-RECORD FROM  PV-ERROR-OUTPUT                         
038100         ADD +1              TO   PV-RECORDS-WRITTEN                      
038200     END-IF.                                                              
038300                                                                          
038400*****************************************************************         
038500*  COMPARE FIELDS TO FIND DISCREPANCIES.                                  
038600*****************************************************************         
038700 4300-COMPARE.                                                            
038800     MOVE '4300-COMPARE'   TO PV-ABEND-PARAGRAPH.                         
038900                                                                          
039000*    DISPLAY PV-ABEND-PARAGRAPH.                                          
039100                                                                          
039200     IF XL023-IN-PO-NBR-RMS  NOT = XL023-IN-PO-NBR-LGCY                   
039300         MOVE '*'  TO XL023-IN-PO-NBR-DIFF                                
039400     END-IF.                                                              
039500     IF XL023-IN-RECIP-CDE-RMS  NOT = XL023-IN-RECIP-CDE-LGCY             
039600         MOVE '*'  TO XL023-IN-RECIP-CDE-DIFF                             
039700     END-IF.                                                              
039800**?? IF XL023-IN-INSTR-ID-RMS  NOT = XL023-IN-INSTR-ID-LGCY               
039900**??     MOVE '*'  TO XL023-IN-INSTR-ID-DIFF                              
040000**?? END-IF.                                                              
040100     IF XL023-IN-INSTR-DESC-LGCY(1:1) = ' '                               
040200         MOVE XL023-IN-INSTR-DESC-LGCY(2:39) TO                           
040300                               XL023-IN-INSTR-DESC-LGCY(1:40)             
040400     END-IF.                                                              
040500     IF XL023-IN-INSTR-DESC-RMS NOT = XL023-IN-INSTR-DESC-LGCY            
040600         MOVE '*'  TO XL023-IN-INSTR-DESC-DIFF                            
040700     END-IF.                                                              
040800         IF  XL023-IN-PO-NBR-DIFF      = '*' OR                           
040900             XL023-IN-RECIP-CDE-DIFF   = '*' OR                           
041000             XL023-IN-INSTR-DESC-DIFF  = '*'                              
041100                ADD 1 TO PV-RECORDS-DATA-MISMATCH                         
041200                WRITE ERROR-RECORD FROM PV-ERROR-OUTPUT                   
041300                ADD +1 TO PV-RECORDS-WRITTEN                              
041400*               SET SWITCH FOR E-MAIL NOTIFICATION OF LEGACY ERROR        
041500*               SET PS-EMAIL-YES       TO TRUE                            
041600     END-IF.                                                              
