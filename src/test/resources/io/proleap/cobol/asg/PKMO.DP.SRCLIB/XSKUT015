       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      XSKUT015.                                       00020000
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    09/30/2002.                                     00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **XS DOMAIN TABLE INTEGRITY PROCESS - FOR TABLE XST_BUS_AREA      00080000
      ******************************************************************00090000
      **THIS PROGRAM VALIDATES THE DATA IN THE XS DOMAIN IS THE SAME    00100000
      **AS THE DATA IN THE SOR - VSM.                                   00110000
      ******************************************************************00120000
      *DATE:09/30/2002   MADE BY:JULIE SMITH INITIAL PROGRAM WRITE      00130000
      ******************************************************************00140000
       ENVIRONMENT DIVISION.                                            00150000
       CONFIGURATION SECTION.                                           00160000
       SOURCE-COMPUTER.        IBM-3090.                                00170000
       OBJECT-COMPUTER.        IBM-3090.                                00180000
       INPUT-OUTPUT SECTION.                                            00190000
       FILE-CONTROL.                                                    00200000
                                                                        00210000
           SELECT CONTROL-CARD                                          00220000
                  ASSIGN TO INP00.                                      00230000
           SELECT VSM-EXTRACT                                           00240000
                  ASSIGN TO INP01.                                      00250000
           SELECT XS-EXTRACT                                            00260000
                  ASSIGN TO INP02.                                      00270000
           SELECT DIFFERENCES-FILE                                      00280000
                  ASSIGN TO OUT01.                                      00290000
                                                                        00300000
       DATA DIVISION.                                                   00310000
       FILE SECTION.                                                    00320000
                                                                        00330000
       FD  CONTROL-CARD                                                 00340000
           RECORDING MODE F                                             00350000
           BLOCK CONTAINS 0 RECORDS                                     00360000
           LABEL RECORDS ARE OMITTED                                    00370000
           DATA RECORD IS CONTROL-CARD-RECORD.                          00380000
       01  CONTROL-CARD-RECORD             PIC X(080).                  00390000
                                                                        00400000
       FD  VSM-EXTRACT                                                  00410000
           RECORDING MODE F                                             00420000
           BLOCK CONTAINS 0 RECORDS                                     00430000
           LABEL RECORDS ARE OMITTED                                    00440000
           DATA RECORD IS VSM-RECORD.                                   00450000
       01  VSM-RECORD                      PIC X(029).                  00460000
                                                                        00470000
       FD  XS-EXTRACT                                                   00480000
           RECORDING MODE F                                             00490000
           BLOCK CONTAINS 0 RECORDS                                     00500000
           LABEL RECORDS ARE OMITTED                                    00510000
           DATA RECORD IS XS-RECORD.                                    00520000
       01  XS-RECORD                       PIC X(027).                  00530000
                                                                        00540000
       FD  DIFFERENCES-FILE                                             00550000
           RECORDING MODE F                                             00560000
           BLOCK CONTAINS 0 RECORDS                                     00570000
           LABEL RECORDS ARE OMITTED                                    00580000
           DATA RECORD IS DIFFERENCES-RECORD.                           00590000
       01  DIFFERENCES-RECORD              PIC X(080).                  00600000
                                                                        00610000
       WORKING-STORAGE SECTION.                                         00620000
      ******************************************************************00630000
      **SWITCHES                                                        00640000
      ******************************************************************00650000
       01  PS-PROGRAM-SWITCHES.                                         00660000
           05  PS-END-OF-VSM-EXTRACT-SW        PIC X(01) VALUE 'N'.     00670000
               88  PS-END-OF-VSM-EXTRACT                 VALUE 'Y'.     00680000
               88  PS-NOT-END-OF-VSM-EXTRACT             VALUE 'N'.     00690000
           05  PS-END-OF-XS-EXTRACT-SW         PIC X(01) VALUE 'N'.     00700000
               88  PS-END-OF-XS-EXTRACT                  VALUE 'Y'.     00710000
               88  PS-NOT-END-OF-XS-EXTRACT              VALUE 'N'.     00720000
           05  PS-ERROR-FOUND-SW               PIC X(01) VALUE 'N'.     00730000
               88  PS-ERROR-FOUND                        VALUE 'Y'.     00740000
               88  PS-ERROR-NOT-FOUND                    VALUE 'N'.     00750000
      ******************************************************************00760000
      **CONTROL CARD LAYOUT                                             00770000
      ******************************************************************00780000
       01  CONTROL-CARD-LAYOUT.                                         00790000
           05  CC-START-POINT                  PIC 9(05) VALUE ZEROS.   00800000
           05  FILLER                          PIC X(01) VALUE SPACES.  00810000
           05  CC-READ-FREQUENCY               PIC 9(05) VALUE ZEROS.   00820000
           05  FILLER                          PIC X(69) VALUE SPACES.  00830000
      ******************************************************************00840000
      **TIMESTAMP VARIABLES                                             00850000
      ******************************************************************00860000
       01  SD-TIMESTAMP                        PIC X(26) VALUE SPACES.  00870000
       01  FILLER REDEFINES SD-TIMESTAMP.                               00880000
           05  SD-CURR-DTE                     PIC X(10).               00890000
           05  FILLER                          PIC X(01).               00900000
           05  SD-HOUR                         PIC X(02).               00910000
           05  FILLER                          PIC X(01).               00920000
           05  SD-MINUTE                       PIC X(02).               00930000
           05  FILLER                          PIC X(01).               00940000
           05  SD-SECOND                       PIC X(02).               00950000
           05  FILLER                          PIC X(01).               00960000
           05  SD-MICROSEC                     PIC X(06).               00970000
      ******************************************************************00980000
      **PROGRAM CONSTANTS                                               00990000
      ******************************************************************01000000
       01  PC-PROGRAM-CONSTANTS.                                        01010000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                01020000
                                                       VALUE 'XSKUT015'.01030000
      ******************************************************************01040000
      **PROGRAM COUNTERS                                                01050000
      ******************************************************************01060000
       01  PC-PROGRAM-COUNTER.                                          01070000
           05  PC-RECORD-COUNT                 PIC S9(09) VALUE ZEROS   01080000
                                                          COMP-3.       01090000
           05  PC-VSM-EXTRACT-RECS-READ        PIC 9(09) VALUE ZEROS.   01100000
           05  PC-XS-EXTRACT-RECS-READ         PIC 9(09) VALUE ZEROS.   01110000
           05  PC-ERROR-RECORD-WRITTEN         PIC 9(09) VALUE ZEROS.   01120000
      ******************************************************************01130000
      **ERROR HANDLING                                                  01140000
      ******************************************************************01150000
       01  PV-MISC-ABEND-FIELDS.                                        01160000
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01170000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01180000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01190000
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01200000
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01210000
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01220000
      ******************************************************************01230000
      **COPYBOOKS                                                       01240000
      ******************************************************************01250000
      ***VSM EXTRACT FILE LAYOUT                                        01260000
           COPY HMRD013.                                                01270000
      ***XS EXTRACT FILE LAYOUT                                         01280000
           COPY XSRD001.                                                01290000
      ***XS ERROR FILE LAYOUT                                           01300000
           COPY XSRD002.                                                01310000
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01320000
           COPY DPWS004.                                                01330000
      ***DB2 ABEND COPYBOOK                                             01340000
           COPY DPWS900.                                                01350000
      ***DB2 MESSAGE AREA                                               01360000
           COPY SQLCA2.                                                 01370000
      ******************************************************************01380000
      **TABLE INCLUSIONS                                                01390000
      ******************************************************************01400000
      ***DB2 COMMUNICATIONS AREA                                        01410000
           EXEC SQL                                                     01420000
                INCLUDE SQLCA                                           01430000
           END-EXEC.                                                    01440000
      ***TABLE XST_BUS_AREA                                             01450000
           EXEC SQL                                                     01460000
                INCLUDE XST00024                                        01470000
           END-EXEC.                                                    01480000
                                                                        01490000
       PROCEDURE DIVISION.                                              01500000
      ***************************************************************** 01510000
       000-MAINLINE.                                                    01520000
                                                                        01530000
           PERFORM 1000-INITIALIZATION.                                 01540000
           PERFORM 2000-PROCESS-RECORDS                                 01550000
                   UNTIL PS-END-OF-VSM-EXTRACT                          01560000
                      OR PS-END-OF-XS-EXTRACT.                          01570000
           PERFORM 3000-PROCESS-EXTRA-RECORDS                           01580000
                   UNTIL PS-END-OF-VSM-EXTRACT                          01590000
                     AND PS-END-OF-XS-EXTRACT.                          01600000
           PERFORM 9999-WRAPUP.                                         01610000
           GOBACK.                                                      01620000
                                                                        01630000
       EJECT.                                                           01640000
      ******************************************************************01650000
      **INITIALIZATION AREA                                             01660000
      ******************************************************************01670000
       1000-INITIALIZATION.                                             01680000
                                                                        01690000
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01700000
                                                                        01710000
           INITIALIZE PC-PROGRAM-COUNTER.                               01720000
                                                                        01730000
           OPEN INPUT  CONTROL-CARD                                     01740000
                       VSM-EXTRACT                                      01750000
                       XS-EXTRACT                                       01760000
                OUTPUT DIFFERENCES-FILE.                                01770000
                                                                        01780000
           PERFORM 1100-SELECT-DB2-TIME.                                01790000
           PERFORM 1200-SELECT-RECORD-COUNT.                            01800000
                                                                        01810000
           PERFORM 9999-READ-CONTROL-CARD.                              01820000
           PERFORM 9999-READ-VSM-EXTRACT CC-START-POINT TIMES.          01830000
           PERFORM 9999-READ-XS-EXTRACT  CC-START-POINT TIMES.          01840000
                                                                        01850000
       EJECT.                                                           01860000
      ******************************************************************01870000
      **RETRIEVE TIME FROM DB2                                          01880000
      ******************************************************************01890000
       1100-SELECT-DB2-TIME.                                            01900000
                                                                        01910000
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            01920000
                                                                        01930000
           EXEC SQL                                                     01940000
                SELECT CURRENT TIMESTAMP                                01950000
                INTO  :SD-TIMESTAMP                                     01960000
                FROM   SYSIBM.SYSDUMMY1                                 01970000
           END-EXEC.                                                    01980000
                                                                        01990000
           EVALUATE TRUE                                                02000000
              WHEN SQLCODE = ZERO                                       02010000
                   CONTINUE                                             02020000
              WHEN OTHER                                                02030000
               PERFORM 9999-ABEND-ROUTINE                               02040000
           END-EVALUATE.                                                02050000
                                                                        02060000
       EJECT.                                                           02070000
      ******************************************************************02080000
      **RETRIEVE TIME FROM DB2                                          02090000
      ******************************************************************02100000
       1200-SELECT-RECORD-COUNT.                                        02110000
                                                                        02120000
           MOVE '1200-SELECT-RECORD-COUNT' TO PV-PARAGRAPH-NAME.        02130000
                                                                        02140000
           EXEC SQL                                                     02150000
                SELECT COUNT(*)                                         02160000
                INTO  :PC-RECORD-COUNT                                  02170000
                FROM   XST_BUS_AREA                                     02180000
           END-EXEC.                                                    02190000
                                                                        02200000
           EVALUATE TRUE                                                02210000
              WHEN SQLCODE = ZERO                                       02220000
                   CONTINUE                                             02230000
              WHEN OTHER                                                02240000
                   PERFORM 9999-ABEND-ROUTINE                           02250000
           END-EVALUATE.                                                02260000
                                                                        02270000
       EJECT.                                                           02280000
      ******************************************************************02290000
      **PROCESSES INPUT FILE                                            02300000
      ******************************************************************02310000
       2000-PROCESS-RECORDS.                                            02320000
                                                                        02330000
           MOVE '2000-PROCESS-RECORDS' TO PV-PARAGRAPH-NAME.            02340000
                                                                        02350000
           INITIALIZE XSRD002-BUSAREA-ERR-RECORD.                       02360000
                                                                        02370000
           EVALUATE TRUE                                                02380000
              WHEN HM013-BUS-AREA-ID = XSRD001-BUS-AREA-ID              02390000
                   PERFORM 2100-COMPARE-FIELDS                          02400000
                   PERFORM 9999-READ-XS-EXTRACT  CC-READ-FREQUENCY TIMES02410000
                   PERFORM 9999-READ-VSM-EXTRACT CC-READ-FREQUENCY TIMES02420000
              WHEN HM013-BUS-AREA-ID > XSRD001-BUS-AREA-ID              02430000
                   PERFORM 9999-MOVE-XS-VALUES                          02440000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02450000
                   PERFORM 9999-READ-XS-EXTRACT                         02460000
              WHEN HM013-BUS-AREA-ID < XSRD001-BUS-AREA-ID              02470000
                   PERFORM 9999-MOVE-VSM-VALUES                         02480000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02490000
                   PERFORM 9999-READ-VSM-EXTRACT                        02500000
           END-EVALUATE.                                                02510000
                                                                        02520000
       EJECT.                                                           02530000
      ******************************************************************02540000
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   02550000
      ******************************************************************02560000
       2100-COMPARE-FIELDS.                                             02570000
                                                                        02580000
           MOVE '2100-COMPARE-FIELDS' TO PV-PARAGRAPH-NAME.             02590000
                                                                        02600000
           INITIALIZE PS-ERROR-FOUND-SW.                                02610000
                                                                        02620000
           IF  XSRD001-BUS-AREA-ID NOT EQUAL HM013-BUS-AREA-ID          02630000
               SET PS-ERROR-FOUND     TO TRUE                           02640000
           END-IF.                                                      02650000
                                                                        02660000
           IF  XSRD001-BUS-AREA-DESC NOT EQUAL HM013-BUS-AREA-DESC      02670000
               SET PS-ERROR-FOUND     TO TRUE                           02680000
           END-IF.                                                      02690000
                                                                        02700000
           IF  PS-ERROR-FOUND                                           02710000
               PERFORM 9999-MOVE-XS-VALUES                              02720000
               PERFORM 9999-MOVE-VSM-VALUES                             02730000
               PERFORM 9999-WRITE-ERROR-RECORD                          02740000
           END-IF.                                                      02750000
                                                                        02760000
       EJECT.                                                           02770000
      ******************************************************************02780000
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   02790000
      ******************************************************************02800000
       3000-PROCESS-EXTRA-RECORDS.                                      02810000
                                                                        02820000
           MOVE '3000-PROCESS-EXTRA-RECORDS' TO PV-PARAGRAPH-NAME.      02830000
                                                                        02840000
           INITIALIZE DIFFERENCES-RECORD.                               02850000
                                                                        02860000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                02870000
               PERFORM 9999-MOVE-VSM-VALUES                             02880000
               PERFORM 9999-WRITE-ERROR-RECORD                          02890000
               PERFORM 9999-READ-VSM-EXTRACT                            02900000
           END-IF.                                                      02910000
                                                                        02920000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 02930000
               PERFORM 9999-MOVE-XS-VALUES                              02940000
               PERFORM 9999-WRITE-ERROR-RECORD                          02950000
               PERFORM 9999-READ-XS-EXTRACT                             02960000
           END-IF.                                                      02970000
                                                                        02980000
       EJECT.                                                           02990000
      ***************************************************************** 03000000
      **READ CONTROL CARD FOR FREQUENCY OF COMPARE                      03010000
      ***************************************************************** 03020000
       9999-READ-CONTROL-CARD.                                          03030000
                                                                        03040000
           READ CONTROL-CARD INTO CONTROL-CARD-LAYOUT.                  03050000
                                                                        03060000
       EJECT.                                                           03070000
      ***************************************************************** 03080000
      **READ VSM EXTRACT FILE                                           03090000
      ***************************************************************** 03100000
       9999-READ-VSM-EXTRACT.                                           03110000
                                                                        03120000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03130000
               READ VSM-EXTRACT INTO HMRD013-RECORD                     03140000
                    AT END                                              03150000
                       SET PS-END-OF-VSM-EXTRACT TO TRUE                03160000
               END-READ                                                 03170000
           END-IF.                                                      03180000
                                                                        03190000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03200000
               ADD +1                       TO PC-VSM-EXTRACT-RECS-READ 03210000
           END-IF.                                                      03220000
                                                                        03230000
       EJECT.                                                           03240000
      ***************************************************************** 03250000
      **READ XS EXTRACT FILE                                            03260000
      ***************************************************************** 03270000
       9999-READ-XS-EXTRACT.                                            03280000
                                                                        03290000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03300000
               READ XS-EXTRACT INTO XSRD001-BUS-AREA-UNLOAD-FILE        03310000
                    AT END                                              03320000
                       SET PS-END-OF-XS-EXTRACT TO TRUE                 03330000
               END-READ                                                 03340000
           END-IF.                                                      03350000
                                                                        03360000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03370000
               ADD +1                       TO PC-XS-EXTRACT-RECS-READ  03380000
           END-IF.                                                      03390000
                                                                        03400000
       EJECT.                                                           03410000
      ***************************************************************** 03420000
      **MOVES XS EXTRACT VALUES TO ERROR RECORD                         03430000
      ***************************************************************** 03440000
       9999-MOVE-XS-VALUES.                                             03450000
                                                                        03460000
           MOVE XSRD001-BUS-AREA-ID   TO XSRD002-XS-BUS-AREA-ID.        03470000
           MOVE XSRD001-BUS-AREA-DESC TO XSRD002-XS-BUS-AREA-DESC.      03480000
                                                                        03490000
       EJECT.                                                           03500000
      ***************************************************************** 03510000
      **MOVES VSM EXTRACT VALUES TO ERROR RECORD                        03520000
      ***************************************************************** 03530000
       9999-MOVE-VSM-VALUES.                                            03540000
                                                                        03550000
           MOVE HM013-BUS-AREA-ID     TO XSRD002-VSM-BUS-AREA-ID.       03560000
           MOVE HM013-BUS-AREA-DESC   TO XSRD002-VSM-BUS-AREA-DESC.     03570000
                                                                        03580000
       EJECT.                                                           03590000
      ***************************************************************** 03600000
      **ERROR FILE                                                      03610000
      ***************************************************************** 03620000
       9999-WRITE-ERROR-RECORD.                                         03630000
                                                                        03640000
           WRITE DIFFERENCES-RECORD FROM XSRD002-BUSAREA-ERR-RECORD.    03650000
                                                                        03660000
           ADD +1                       TO PC-ERROR-RECORD-WRITTEN.     03670000
                                                                        03680000
       EJECT.                                                           03690000
      ****************************************************************  03700000
      *END OF PROGRAM ROUTINE                                           03710000
      ****************************************************************  03720000
       9999-WRAPUP.                                                     03730000
                                                                        03740000
           CLOSE CONTROL-CARD                                           03750000
                 VSM-EXTRACT                                            03760000
                 XS-EXTRACT                                             03770000
                 DIFFERENCES-FILE.                                      03780000
                                                                        03790000
           PERFORM 9999-DISPLAY-STATISTICS.                             03800000
                                                                        03810000
           IF  PC-ERROR-RECORD-WRITTEN > 0                              03820000
               MOVE 04               TO RETURN-CODE                     03830000
           END-IF.                                                      03840000
                                                                        03850000
       EJECT.                                                           03860000
      ****************************************************************  03870000
      *DISPLAY PROGRAM STATISTICS                                       03880000
      ****************************************************************  03890000
       9999-DISPLAY-STATISTICS.                                         03900000
                                                                        03910000
           DISPLAY '*************************************************'. 03920000
           DISPLAY '* PROGRAM..............: ' PC-CURRENT-PROGRAM-NAME. 03930000
           DISPLAY '* DATE/TIMESTAMP.......: ' SD-TIMESTAMP.            03940000
           DISPLAY '* RECORDS IN TABLE.....: ' PC-RECORD-COUNT.         03950000
           DISPLAY '* VSM EXTRACT RECORDS..: ' PC-VSM-EXTRACT-RECS-READ.03960000
           DISPLAY '* XS EXTRACT RECORDS...: ' PC-XS-EXTRACT-RECS-READ. 03970000
           DISPLAY '* ERROR RECORDS WRITTEN: ' PC-ERROR-RECORD-WRITTEN. 03980000
           DISPLAY '*************************************************'. 03990000
                                                                        04000000
       EJECT.                                                           04010000
      ******************************************************************04020000
      **SQL ABEND ROUTINE                                               04030000
      ******************************************************************04040000
       9999-ABEND-ROUTINE.                                              04050000
                                                                        04060000
           DISPLAY '** ABEND     **'.                                   04070000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        04080000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04090000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               04100000
      ******************************************************************04110000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     04120000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      04130000
      ******************************************************************04140000
           COPY DPPD004.                                                04150000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04160000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04170000
                                                                        04180000
       EJECT.                                                           04190000
