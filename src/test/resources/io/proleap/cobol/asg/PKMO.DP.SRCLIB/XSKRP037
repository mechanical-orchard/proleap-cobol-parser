       IDENTIFICATION DIVISION.                                         00010003
       PROGRAM-ID.             XSKRP037.                                00020003
       AUTHOR.                 KOHLS DEPARTMENT STORES.                 00030003
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00040003
       DATE-WRITTEN.           09/10/2004.                              00050003
       DATE-COMPILED.                                                   00060003
      ******************************************************************00070003
      ***XS DOMAIN - XST_MAJ_CLASS REPORT                               00080003
      ******************************************************************00090003
      **THIS PROGRAM FORMATS XST_MAJ_CLASS INTO A REPORT                00100003
      ******************************************************************00110003
      *DATE:09/10/2004   MADE BY:JULIE TEWES INITIAL WRITE              00120003
      ******************************************************************00130003
       ENVIRONMENT DIVISION.                                            00140003
       CONFIGURATION SECTION.                                           00150003
       SOURCE-COMPUTER.                        IBM-3090.                00160003
       OBJECT-COMPUTER.                        IBM-3090.                00170003
       INPUT-OUTPUT SECTION.                                            00180003
       FILE-CONTROL.                                                    00190003
                                                                        00200003
           SELECT INPUT-FILE                                            00210003
                  ASSIGN TO INP01.                                      00220003
           SELECT REPORT-FILE                                           00230003
                  ASSIGN TO RPT01.                                      00240003
                                                                        00250003
       DATA DIVISION.                                                   00260003
       FILE SECTION.                                                    00270003
                                                                        00280003
       FD  INPUT-FILE                                                   00290003
           RECORDING MODE IS F                                          00300003
           LABEL RECORDS ARE STANDARD                                   00310003
           BLOCK CONTAINS 0 CHARACTERS.                                 00320003
       01  INPUT-RECORD                    PIC X(100).                  00330005
                                                                        00340003
       FD  REPORT-FILE                                                  00350003
           RECORDING MODE IS F                                          00360003
           LABEL RECORDS ARE STANDARD                                   00370003
           BLOCK CONTAINS 0 CHARACTERS.                                 00380003
       01  REPORT-RECORD                   PIC X(133).                  00390003
                                                                        00400003
       WORKING-STORAGE SECTION.                                         00410003
      ******************************************************************00420003
      *SWITCHES                                                         00430003
      ******************************************************************00440003
       01  PS-PROGRAM-SWITCHES.                                         00450003
           05  PS-END-OF-INPUT-FILE-SW    PIC X(01)   VALUE 'N'.        00460003
               88  PS-EOF-INPUT-FILE                  VALUE 'Y'.        00470003
               88  PS-NOT-EOF-INPUT-FILE              VALUE 'N'.        00480003
      ******************************************************************00490003
      **TIME FIELDS                                                     00500003
      ******************************************************************00510003
       01  DF-DATETIME-FIELDS.                                          00520003
           05  DF-ACCEPT-TIME.                                          00530003
               10  DF-ACCEPT-HR             PIC 9(02) VALUE ZEROS.      00540003
               10  DF-ACCEPT-MIN            PIC 9(02) VALUE ZEROS.      00550003
               10  DF-ACCEPT-SEC            PIC 9(02) VALUE ZEROS.      00560003
               10  DF-ACCEPT-HSEC           PIC 9(02) VALUE ZEROS.      00570003
           05  DF-ACCEPT-DATE.                                          00580003
               10  DF-ACCEPT-YR             PIC 9(02) VALUE ZEROS.      00590003
               10  DF-ACCEPT-MO             PIC 9(02) VALUE ZEROS.      00600003
               10  DF-ACCEPT-DY             PIC 9(02) VALUE ZEROS.      00610003
      ******************************************************************00620003
      *PROGRAM CONSTANTS                                                00630003
      ******************************************************************00640003
       01  PC-PROGRAM-CONSTANTS.                                        00650003
           05  PC-PROGRAM-NAME            PIC X(08) VALUE 'XSKRP037'.   00660003
      ******************************************************************00670003
      **PROGRAM COUNTS                                                  00680003
      ******************************************************************00690003
       01  PC-PROGRAM-COUNTERS.                                         00700003
           05  PC-LINE-COUNT              PIC 9(03) VALUE ZEROS.        00710003
           05  PC-PAGE-COUNT              PIC 9(03) VALUE ZEROS.        00720003
           05  PC-RECORDS-READ            PIC 9(06) VALUE ZEROS.        00730003
      ***************************************************************** 00740003
      **ABEND CODE FIELDS                                               00750003
      ***************************************************************** 00760003
       01  ABEND-CODE                     PIC S9(4)                     00770003
                                                 VALUE ZEROS COMP SYNC. 00780003
       01  PV-MISC-ABEND-FIELDS.                                        00790003
           05  PV-PARAGRAPH-NAME          PIC  X(30)    VALUE SPACE.    00800003
           05  PV-OPERATION-MSG-1         PIC  X(40)    VALUE SPACE.    00810003
           05  PV-OPERATION-MSG-2         PIC  X(40)    VALUE SPACE.    00820003
           05  PV-DB2-OPERATION           PIC  X(30)    VALUE SPACES.   00830003
      ***************************************************************** 00840003
      **REPORT HEADINGS                                                 00850003
      ***************************************************************** 00860003
       01  REPORT-HEADING-1.                                            00870003
           05  FILLER                     PIC  X(56)  VALUE SPACES.     00880003
           05  FILLER                     PIC  X(22)                    00890003
                                 VALUE 'XS DOMAIN ERROR REPORT'.        00900003
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00910003
       01  REPORT-HEADING-2.                                            00920003
           05  FILLER                     PIC  X(51)  VALUE SPACES.     00930003
           05  FILLER                     PIC  X(30)                    00940003
                                 VALUE 'XST_MAJ_CLASS INTEGRITY REPORT'.00950003
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00960003
       01  REPORT-HEADING-3.                                            00970003
           05  FILLER                     PIC  X(02)  VALUE SPACES.     00980003
           05  FILLER                     PIC  X(08)                    00990003
                                 VALUE 'DEPT_NBR'.                      01000003
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01010003
           05  FILLER                     PIC  X(10)                    01020003
                                 VALUE 'MAJ_CL_NBR'.                    01030003
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01040003
           05  FILLER                     PIC  X(09)                    01050003
                                 VALUE 'MAJ_CL_NM'.                     01060003
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01070003
       01  REPORT-HEADING-4.                                            01080003
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01090003
           05  FILLER                     PIC  X(08)  VALUE ALL '-'.    01100003
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01110003
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01120003
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01130003
           05  FILLER                     PIC  X(20)  VALUE ALL '-'.    01140003
      ***************************************************************** 01150003
      **REPORT DETAIL                                                   01160003
      ***************************************************************** 01170003
       01  REPORT-DETAIL-1.                                             01180003
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01190003
           05  RD-DEPT-NBR                PIC  ZZZ9.                    01200003
           05  FILLER                     PIC  X(06)  VALUE SPACES.     01210003
           05  RD-MAJ-CL-NBR              PIC  ZZZ9.                    01220003
           05  FILLER                     PIC  X(08)  VALUE SPACES.     01230003
           05  RD-MAJ-CL-NM               PIC  X(20).                   01240003
                                                                        01250003
       01  REPORT-DETAIL-2.                                             01260003
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01270003
           05  RD-RMS-DEPT-NBR            PIC  ZZZ9.                    01280004
           05  FILLER                     PIC  X(06)  VALUE SPACES.     01290003
           05  RD-RMS-MAJ-CL-NBR          PIC  ZZZ9.                    01300004
           05  FILLER                     PIC  X(08)  VALUE SPACES.     01310003
           05  RD-RMS-MAJ-CL-NM           PIC  X(20).                   01320004
                                                                        01330003
       01  RF-END-OF-REPORT.                                            01340003
           05  FILLER                     PIC X(57)  VALUE SPACES.      01350003
           05  FILLER                     PIC X(21)                     01360003
                                 VALUE '***END OF REPORT***'.           01370003
           05  FILLER                     PIC X(57)  VALUE SPACES.      01380003
      ***************************************************************** 01390003
      **COPYBOOKS                                                       01400003
      ***************************************************************** 01410003
      ****ABEND ROUTINE INFORMATION                                     01420003
           COPY DPWS900.                                                01430003
      ****ABEND ROUTINE INFORMATION                                     01440003
           COPY DPWS004.                                                01450003
      ***STANDARD REPORT HEADER FOR A 132 COLUMN REPORT.                01460003
           COPY DPWS132O.                                               01470003
      ***INPUT RECORD                                                   01480003
           COPY XSRD068.                                                01490003
                                                                        01500003
       PROCEDURE DIVISION.                                              01510003
      ******************************************************************01520003
       0000-MAINLINE.                                                   01530003
                                                                        01540003
           PERFORM 1000-INITIALIZATION.                                 01550003
           PERFORM 2000-PROCESS-FILES                                   01560003
                   UNTIL PS-EOF-INPUT-FILE.                             01570003
           PERFORM 9999-WRAPUP.                                         01580003
           GOBACK.                                                      01590003
                                                                        01600003
       EJECT.                                                           01610003
      ******************************************************************01620003
      *INITIALIZATION ROUTINE                                           01630003
      ******************************************************************01640003
       1000-INITIALIZATION.                                             01650003
                                                                        01660003
           OPEN INPUT   INPUT-FILE                                      01670003
                OUTPUT  REPORT-FILE.                                    01680003
                                                                        01690003
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01700003
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01710003
                                                                        01720003
           PERFORM 1100-SETUP-HEADINGS.                                 01730003
                                                                        01740003
           PERFORM 9999-READ-INPUT-FILE.                                01750003
                                                                        01760003
       EJECT.                                                           01770003
      ******************************************************************01780003
      *SETS UP ALL HEADING STATIC VARIABLES FOR THE REPORT DURATION     01790003
      ******************************************************************01800003
       1100-SETUP-HEADINGS.                                             01810003
                                                                        01820003
           MOVE '1200-SETUP-HEADINGS' TO PV-PARAGRAPH-NAME.             01830003
                                                                        01840003
           MOVE DF-ACCEPT-YR          TO DP132O-RUN-YEAR.               01850003
           MOVE DF-ACCEPT-MO          TO DP132O-RUN-MONTH.              01860003
           MOVE DF-ACCEPT-DY          TO DP132O-RUN-DAY.                01870003
           MOVE DF-ACCEPT-HR          TO DP132O-RUN-HOUR.               01880003
           MOVE DF-ACCEPT-MIN         TO DP132O-RUN-MINUTE.             01890003
                                                                        01900003
           MOVE PC-PROGRAM-NAME  TO DP132O-PROGRAM-NAME.                01910003
           MOVE 1                TO DP132O-REPORT-NUMBER.               01920003
                                                                        01930003
           PERFORM 4000-PRINT-HEADINGS.                                 01940003
                                                                        01950003
       EJECT.                                                           01960003
      ******************************************************************01970003
      *PROCESS BOTH FILES                                               01980003
      ******************************************************************01990003
       2000-PROCESS-FILES.                                              02000003
                                                                        02010003
           MOVE '2000-PROCESS-FILES' TO PV-PARAGRAPH-NAME.              02020003
                                                                        02030003
           PERFORM 4100-PRINT-DETAIL.                                   02040003
                                                                        02050003
           IF  PC-LINE-COUNT > 60                                       02060003
               PERFORM 4000-PRINT-HEADINGS                              02070003
           END-IF.                                                      02080003
                                                                        02090003
           PERFORM 9999-READ-INPUT-FILE.                                02100003
                                                                        02110003
       EJECT.                                                           02120003
      ******************************************************************02130003
      *PRINTS REPORT HEADINGS AT TOP OF PAGE                            02140003
      ******************************************************************02150003
       4000-PRINT-HEADINGS.                                             02160003
                                                                        02170003
           MOVE '4000-PRINT-HEADINGS' TO PV-PARAGRAPH-NAME.             02180003
                                                                        02190003
           INITIALIZE PC-LINE-COUNT.                                    02200003
           ADD +1                     TO PC-PAGE-COUNT.                 02210003
           MOVE PC-PAGE-COUNT         TO DP132O-PAGE-NUMBER.            02220003
                                                                        02230003
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      02240003
                                                  AFTER ADVANCING PAGE. 02250003
           WRITE REPORT-RECORD FROM REPORT-HEADING-1                    02260003
                                               AFTER ADVANCING 2 LINES. 02270003
           WRITE REPORT-RECORD FROM REPORT-HEADING-2                    02280003
                                               AFTER ADVANCING 1 LINES. 02290003
           WRITE REPORT-RECORD FROM REPORT-HEADING-3                    02300003
                                               AFTER ADVANCING 3 LINES. 02310003
           WRITE REPORT-RECORD FROM REPORT-HEADING-4                    02320003
                                               AFTER ADVANCING 1 LINES. 02330003
                                                                        02340003
           ADD +7                     TO PC-LINE-COUNT.                 02350003
                                                                        02360003
       EJECT.                                                           02370003
      ******************************************************************02380003
      *PRINTS DETAIL REPORT                                             02390003
      ******************************************************************02400003
       4100-PRINT-DETAIL.                                               02410003
                                                                        02420003
           MOVE '4100-PRINT-DETAIL'    TO PV-PARAGRAPH-NAME.            02430003
                                                                        02440003
           MOVE XSRD068-XS-DEPT-NBR    TO RD-DEPT-NBR.                  02450003
           MOVE XSRD068-XS-MAJ-CL-NBR  TO RD-MAJ-CL-NBR.                02460003
           MOVE XSRD068-XS-MAJ-CL-NM   TO RD-MAJ-CL-NM.                 02470003
           MOVE XSRD068-RMS-DEPT-NBR   TO RD-RMS-DEPT-NBR.              02480004
           MOVE XSRD068-RMS-MAJ-CL-NBR TO RD-RMS-MAJ-CL-NBR.            02490004
           MOVE XSRD068-RMS-MAJ-CL-NM  TO RD-RMS-MAJ-CL-NM.             02500004
                                                                        02510003
           WRITE REPORT-RECORD FROM REPORT-DETAIL-1                     02520003
                                            AFTER ADVANCING 2 LINES.    02530003
           WRITE REPORT-RECORD FROM REPORT-DETAIL-2                     02540003
                                            AFTER ADVANCING 1 LINES.    02550003
                                                                        02560003
           ADD +3                   TO PC-LINE-COUNT.                   02570003
                                                                        02580003
       EJECT.                                                           02590003
      ******************************************************************02600003
      *READS INPUT FILE                                                 02610003
      ******************************************************************02620003
       9999-READ-INPUT-FILE.                                            02630003
                                                                        02640003
           READ INPUT-FILE INTO XSRD068-MAJCLASS-ERR-RECORD             02650003
               AT END SET PS-EOF-INPUT-FILE TO TRUE                     02660003
           END-READ.                                                    02670003
                                                                        02680003
           ADD +1  TO PC-RECORDS-READ.                                  02690003
                                                                        02700003
       EJECT.                                                           02710003
      ******************************************************************02720003
      *PROGRAM END PROCESSING                                           02730003
      ******************************************************************02740003
       9999-WRAPUP.                                                     02750003
                                                                        02760003
           WRITE REPORT-RECORD FROM RF-END-OF-REPORT                    02770003
                                    AFTER ADVANCING 2 LINES.            02780003
                                                                        02790003
           CLOSE INPUT-FILE                                             02800003
                 REPORT-FILE.                                           02810003
                                                                        02820003
           DISPLAY '*************************************************'. 02830003
           DISPLAY '* PROGRAM..................: ' PC-PROGRAM-NAME.     02840003
           DISPLAY '* RUN DATE.................: ' DF-ACCEPT-DATE.      02850003
           DISPLAY '* RUN TIME.................: ' DF-ACCEPT-TIME.      02860003
           DISPLAY '*--------- COUNTS FOR THIS RUN ---------'.          02870003
           DISPLAY '* RECORDS READ........: ' PC-RECORDS-READ.          02880003
           DISPLAY '*************************************************'. 02890003
                                                                        02900003
       EJECT.                                                           02910003
                                                                        02920003
       EJECT.                                                           02930003
