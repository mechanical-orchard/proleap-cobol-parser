      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
                                                                        00040000
       PROGRAM-ID.             TCKUT078.                                00050000
       AUTHOR.                 DAN PAYNTER                              00060000
       INSTALLATION.           KOHLS.                                   00070000
       DATE-WRITTEN            06/2023.                                 00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ***************************************************************** 00110000
      *  PURPOSE:                                                       00120000
      *  SUMMARIZE DAILY TRAILING TRANSACTION SUMMARY TOTALS CATEGORY   00130000
      ***************************************************************** 00145000
      *  06/01/2023    DAN PAYNTER CHG0289532                           00146000
      *  NEW PROGRAM TO SUMMARIZE TRAILING TRANSACTION CATEGORIES ON    00147000
      *  TSASUM IN ORDER TO AUTOMATE THE WEEKLY ADJUSTMENTS.            00147100
      ***************************************************************** 00148000
       ENVIRONMENT DIVISION.                                            00150000
      ***************************************************************** 00160000
                                                                        00170000
       CONFIGURATION SECTION.                                           00180000
                                                                        00190000
       SOURCE-COMPUTER.        IBM-3090.                                00200000
                                                                        00210000
       OBJECT-COMPUTER.        IBM-3090.                                00220000
                                                                        00230000
       INPUT-OUTPUT SECTION.                                            00240000
                                                                        00250000
       FILE-CONTROL.                                                    00260000
                                                                        00270000
           SELECT TRAN-UPDATE-FILE                                      00310000
               ASSIGN TO INP01.                                         00320000
                                                                        00330000
           SELECT TRAN-UPDT-FILE                                        00340000
               ASSIGN TO OUT01.                                         00341000
                                                                        00350000
      ******************************************************************00400000
       DATA DIVISION.                                                   00410000
      ******************************************************************00420000
                                                                        00430000
       FILE SECTION.                                                    00440000
                                                                        00450000
       FD  TRAN-UPDATE-FILE                                             00530000
           RECORDING MODE IS F                                          00540000
           BLOCK CONTAINS 0 RECORDS.                                    00550000
                                                                        00560000
       01  TRAN-UPDATE-REC             PIC X(250).                      00570000
                                                                        00590000
       FD  TRAN-UPDT-FILE                                               00600000
           RECORDING MODE IS F                                          00610000
           LABEL RECORDS ARE STANDARD                                   00620000
           BLOCK CONTAINS 0 RECORDS                                     00630000
           RECORD CONTAINS 250 CHARACTERS                               00640000
           DATA RECORD IS TRAN-UPDT-RECORD.                             00650000
                                                                        00660000
       01  TRAN-UPDT-RECORD                PIC X(250).                  00670000
                                                                        00680000
      ******************************************************************00720000
       WORKING-STORAGE SECTION.                                         00730000
      ******************************************************************00740000
       01  FILLER                            PIC X(50)  VALUE           00750000
           ' *** WORKING STORAGE STARTS HERE FOR TCKUT078 *** '.        00760000
                                                                        00770000
        01  PS-PROGRAM-SWITCHES.                                        00990000
           05  PS-END-OF-TRAN-UP-SW          PIC X(01)  VALUE 'N'.      01020000
               88  PS-NOT-END-OF-TRAN-UP-FILE           VALUE 'N'.      01030000
               88  PS-END-OF-TRAN-UP-FILE               VALUE 'Y'.      01040000
           05  PS-RECORD-READ-IN-SW          PIC X(01)  VALUE 'N'.      01050000
               88  PS-NO-RECORD-READ-SW                 VALUE 'N'.      01060000
               88  PS-RECORD-READ-SW                    VALUE 'Y'.      01070000
           05  PS-BREAK-FOUND-SW             PIC X(01)  VALUE 'N'.      01071000
               88  PS-BREAK-NOT-FOUND                   VALUE 'N'.      01072000
               88  PS-BREAK-FOUND                       VALUE 'Y'.      01073000
                                                                        01080000
      ******************************************************************01140000
      *   PROGRAM CONSTANTS                                            *01150000
      ******************************************************************01160000
       01  PC-CONSTANTS.                                                01170000
           05  PC-PROGRAM-NAME               PIC  X(08) VALUE           01190000
                                                       'TCKUT078'.      01190100
       01  PROGRAM-VARIABLES.                                           01190200
           05  PV-HOLD-SASUM-PARTN-NBR       PIC 9(4).                  01190300
           05  PV-HOLD-SASUM-STR-NBR         PIC 9(4).                  01190400
           05  PV-HOLD-SASUM-RGST-ID         PIC 9(4).                  01190500
           05  PV-HOLD-SASUM-SUM-CTG-CDE     PIC X(3).                  01190700
           05  PV-SASUM-AMOUNT               PIC S9(7)V99 COMP-3.       01190800
           05  PV-SASUM-COUNT                PIC 9(7) COMP-3.           01190900
           05  PV-TOTAL-AMOUNT               PIC S9(7)V99 COMP-3.       01191000
           05  PV-TOTAL-COUNT                PIC 9(7) COMP-3.           01191100
                                                                        01191200
                                                                        01191300
       01  PROGRAM-ACCUMULATORS.                                        01191400
           05  PA-READ                       PIC S9(11) VALUE ZERO      01191500
                                                       COMP-3.          01191600
           05  PA-TTS-COUNT                  PIC S9(9) VALUE ZERO       01192000
                                                       COMP-3.          01192100
           05  PA-TTR-COUNT                  PIC S9(9) VALUE ZERO       01192200
                                                       COMP-3.          01193000
           05  PA-TTN-COUNT                  PIC S9(9) VALUE ZERO       01194000
                                                       COMP-3.          01195000
           05  PA-TTC-COUNT                  PIC S9(9) VALUE ZERO       01196000
                                                       COMP-3.          01197000
           05  PA-TTP-COUNT                  PIC S9(9) VALUE ZERO       01198000
                                                       COMP-3.          01199000
           05  PA-TTO-COUNT                  PIC S9(9) VALUE ZERO       01200000
                                                       COMP-3.          01210000
           05  PA-DISPLAY-COUNT              PIC S9(11) VALUE ZERO.     01220000
                                                                        01230000
      ******************************************************************02087000
      *               SYSTEM TIME AND DATE VARIABLES                    02088000
      ******************************************************************02089000
       01  SYS-DATE-TIME.                                               02089100
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    02089200
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    02089300
                                                                        02089400
       01  ST-BEG-TIME.                                                 02089500
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     02089600
           05  FILLER                       PIC  X(01) VALUE ':'.       02089700
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     02089800
                                                                        02089900
       01  ST-END-TIME.                                                 02090000
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     02090100
           05  FILLER                       PIC  X(01) VALUE ':'.       02090200
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     02090300
                                                                        02090400
       01  SD-EDIT-DATE.                                                02090500
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     02090600
           05  FILLER                       PIC  X(01) VALUE '-'.       02090700
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     02090800
           05  FILLER                       PIC  X(01) VALUE '-'.       02090900
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     02091000
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     02091100
                                                                        02460000
      ***************************************************************** 02851000
      *  WS FIELDS FOR TSASUM DB2 UPDATE                                02852000
      ***************************************************************** 02853000
           COPY TCRD075 REPLACING 'TC075-' BY 'WK075-'.                 02853100
                                                                        02853200
      ***************************************************************** 02853300
      *  WS FIELDS FOR TSASUM DB2 UPDATE                                02853400
      ***************************************************************** 02853500
           COPY TCRD075.                                                02854000
                                                                        02855000
       PROCEDURE DIVISION.                                              05150000
                                                                        05160000
      ******************************************************************05170000
      * 0000-MAINLINE-PARAGRAPH                                         05180000
      ******************************************************************05190000
       0000-MAINLINE-PARAGRAPH.                                         05200000
                                                                        05210000
           PERFORM 1000-INITIALIZE-PROGRAM.                             05220000
                                                                        05230000
           IF PS-NOT-END-OF-TRAN-UP-FILE                                05240000
               PERFORM 2000-PROCESS                                     05240200
                   UNTIL PS-END-OF-TRAN-UP-FILE                         05240300
               PERFORM 7000-WRITE-TSASUM-UPDATES                        05240500
           ELSE                                                         05250000
      *        END OF FILE FOUND                                        05260000
               IF PS-RECORD-READ-SW                                     05321000
      *            BUT AT LEAST ONE RECORD WAS FOUND                    05321200
      *            THEN WE WILL NEED TO WRITE OUT THAT ONE RECORD       05321300
                   PERFORM 7000-WRITE-TSASUM-UPDATES                    05321400
               END-IF                                                   05322000
           END-IF.                                                      05330000
                                                                        05340000
           CLOSE TRAN-UPDATE-FILE                                       05460000
                 TRAN-UPDT-FILE.                                        05490200
           PERFORM 9500-CONTROLS.                                       05491000
                                                                        05500000
           GOBACK.                                                      05510000
                                                                        05520000
      ******************************************************************05530000
      * 1000-INITIALIZE-PROGRAM                                         05540000
      *     OPEN FILES.                                                 05550000
      ******************************************************************05560000
       1000-INITIALIZE-PROGRAM.                                         05570000
                                                                        05580000
           OPEN INPUT  TRAN-UPDATE-FILE                                 05590000
                OUTPUT TRAN-UPDT-FILE.                                  05601000
                                                                        05630000
           INITIALIZE PROGRAM-ACCUMULATORS.                             05640000
           MOVE ZERO                   TO PV-SASUM-AMOUNT               05650000
                                          PV-TOTAL-AMOUNT               05660000
                                          PV-SASUM-COUNT                05670000
                                          PV-TOTAL-COUNT.               05680000
           SET PS-BREAK-NOT-FOUND TO TRUE.                              05690000
      ******************************************************************06060000
      * PRIMING READ                                                    06070000
      ******************************************************************06090000
           PERFORM 8000-READ-CREDIT-FILE.                               06100000
           IF NOT PS-END-OF-TRAN-UP-FILE                                06100100
               SET PS-RECORD-READ-SW TO TRUE                            06100200
               MOVE WK075-SASUM-PARTN-NBR                               06101000
                                        TO PV-HOLD-SASUM-PARTN-NBR      06101100
               MOVE WK075-SASUM-STR-NBR TO PV-HOLD-SASUM-STR-NBR        06102000
               MOVE WK075-SASUM-RGST-ID TO PV-HOLD-SASUM-RGST-ID        06103000
               MOVE WK075-SASUM-SUM-CTG-CDE                             06104000
                                        TO PV-HOLD-SASUM-SUM-CTG-CDE    06105000
               MOVE WK075-SASUM-AMOUNT  TO PV-SASUM-AMOUNT              06106000
                                           PV-TOTAL-AMOUNT              06106100
               MOVE WK075-SASUM-COUNT   TO PV-SASUM-COUNT               06106200
                                           PV-TOTAL-COUNT               06106300
               PERFORM 8000-READ-CREDIT-FILE                            06106400
           END-IF.                                                      06107000
                                                                        06110000
      ***************************************************************** 06240000
      * 2000-PROCESS                                                    06250000
      *        PROCESS THROUGH THE CREDIT REPORT RECORDS WRITING OUT    06260000
      *        TSASUM UPDATE RECORDS.                                   06270000
      ***************************************************************** 06300000
       2000-PROCESS.                                                    06310000
                                                                        06320000
           PERFORM 2100-CHECK-FOR-BREAK.                                06330000
           PERFORM 8000-READ-CREDIT-FILE.                               06720000
                                                                        11620000
      ***************************************************************** 11621000
      * 2100-CHECK-FOR-BREAK                                            11622000
      *        CHECK IF NEW RECORD IS FOR A DIFFERENT DB2 TABLE ROW     11623000
      *        THAN THE PRIOR RECORD.                                   11624000
      ***************************************************************** 11625000
       2100-CHECK-FOR-BREAK.                                            11626000
           IF WK075-SASUM-PARTN-NBR   = PV-HOLD-SASUM-PARTN-NBR AND     11626100
              WK075-SASUM-STR-NBR     = PV-HOLD-SASUM-STR-NBR   AND     11626300
              WK075-SASUM-RGST-ID     = PV-HOLD-SASUM-RGST-ID   AND     11626500
              WK075-SASUM-SUM-CTG-CDE = PV-HOLD-SASUM-SUM-CTG-CDE       11626700
               SET PS-BREAK-NOT-FOUND TO TRUE                           11626800
               PERFORM 2500-SUMMARIZE-RECORDS                           11626900
           ELSE                                                         11627200
               SET PS-BREAK-FOUND TO TRUE                               11627300
               PERFORM 7000-WRITE-TSASUM-UPDATES                        11627400
               MOVE WK075-SASUM-PARTN-NBR                               11627500
                                        TO PV-HOLD-SASUM-PARTN-NBR      11627600
               MOVE WK075-SASUM-STR-NBR TO PV-HOLD-SASUM-STR-NBR        11627700
               MOVE WK075-SASUM-RGST-ID TO PV-HOLD-SASUM-RGST-ID        11627800
               MOVE WK075-SASUM-SUM-CTG-CDE                             11627900
                                        TO PV-HOLD-SASUM-SUM-CTG-CDE    11628000
               MOVE WK075-SASUM-AMOUNT  TO PV-SASUM-AMOUNT              11628100
                                           PV-TOTAL-AMOUNT              11628200
               MOVE WK075-SASUM-COUNT   TO PV-SASUM-COUNT               11628300
                                           PV-TOTAL-COUNT               11628400
           END-IF.                                                      11628600
                                                                        11628800
      ***************************************************************** 11628900
      * 2500-SUMMARIZE-RECORDS                                          11629000
      *        SUMMARIZE THE TSASUM UPDATE RECORDS TO MINIMIZE THE      11629100
      *        NUMBER OF UPDATES REQUIRED.                              11629200
      ***************************************************************** 11629300
       2500-SUMMARIZE-RECORDS.                                          11629400
                                                                        11629500
           MOVE WK075-SASUM-AMOUNT  TO PV-SASUM-AMOUNT.                 11630900
           COMPUTE PV-TOTAL-AMOUNT = PV-TOTAL-AMOUNT + PV-SASUM-AMOUNT. 11631000
           MOVE WK075-SASUM-COUNT   TO PV-SASUM-COUNT.                  11631100
           COMPUTE PV-TOTAL-COUNT  = PV-TOTAL-COUNT  + PV-SASUM-COUNT.  11631200
                                                                        11633800
      ******************************************************************11634000
      * 7000-WRITE-TSASUM-UPDATE                                        11640000
      *     WRITE THE OUTPUT RECORD WITH TSASUM UPDATE DETAILS.         11641000
      ******************************************************************11680000
       7000-WRITE-TSASUM-UPDATES.                                       11690000
                                                                        11760000
           INITIALIZE TC075-TRAN-UPDT-RECORD.                           11780000
           MOVE PV-HOLD-SASUM-PARTN-NBR   TO TC075-SASUM-PARTN-NBR.     11790000
           MOVE PV-HOLD-SASUM-STR-NBR     TO TC075-SASUM-STR-NBR.       11801000
           MOVE PV-HOLD-SASUM-RGST-ID     TO TC075-SASUM-RGST-ID.       11810000
           MOVE PV-HOLD-SASUM-SUM-CTG-CDE TO TC075-SASUM-SUM-CTG-CDE.   11811300
           MOVE PV-TOTAL-AMOUNT           TO TC075-SASUM-AMOUNT.        11830000
           MOVE PV-TOTAL-COUNT            TO TC075-SASUM-COUNT.         11840000
                                                                        11930000
           WRITE TRAN-UPDT-RECORD                                       11940000
               FROM TC075-TRAN-UPDT-RECORD.                             11950000
                                                                        13120000
      ******************************************************************13130000
      * 8000-READ-INPUT-FILE                                            13140000
      *     READ THE ADJUSTMENT INPUT FILE                              13150000
      ******************************************************************13160000
       8000-READ-CREDIT-FILE.                                           13170000
                                                                        13180000
           READ TRAN-UPDATE-FILE INTO WK075-TRAN-UPDT-RECORD            13190000
               AT END                                                   13200000
                   SET PS-END-OF-TRAN-UP-FILE TO TRUE                   13210000
           END-READ.                                                    13220000
           IF NOT PS-END-OF-TRAN-UP-FILE                                13220500
               ADD 1 TO PA-READ                                         13220600
           END-IF.                                                      13220700
                                                                        13221000
      ******************************************************************13421000
      * DISPLAY CONTROLS FROM THE PROGRAM                               13422000
      ******************************************************************13423000
       9500-CONTROLS.                                                   13424000
                                                                        13425000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 13426000
                                                                        13427000
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            13428000
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            13429000
                                                                        13429100
           DISPLAY '                    '.                              13429200
           DISPLAY '**********************'.                            13429300
           DISPLAY '   TCKUT078 CONTROLS  '.                            13429400
           DISPLAY '**********************'.                            13429500
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         13429600
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          13429700
           DISPLAY 'END TIME  = ' ST-END-TIME.                          13429800
           DISPLAY '======================'.                            13429900
           DISPLAY 'RECORDS READ   = ' PA-READ.                         13430000
           DISPLAY '                '.                                  13430200
           MOVE PA-READ      TO PA-DISPLAY-COUNT.                       13430300
           DISPLAY 'INPUT RECORDS READ  = ' PA-DISPLAY-COUNT.           13431100
           MOVE PA-TTS-COUNT TO PA-DISPLAY-COUNT.                       13431200
           DISPLAY 'TTS RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13431300
           MOVE PA-TTR-COUNT TO PA-DISPLAY-COUNT.                       13431400
           DISPLAY 'TTR RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13431500
           MOVE PA-TTN-COUNT TO PA-DISPLAY-COUNT.                       13431600
           DISPLAY 'TTN RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13431700
           MOVE PA-TTC-COUNT TO PA-DISPLAY-COUNT.                       13431800
           DISPLAY 'TTC RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13431900
           MOVE PA-TTP-COUNT TO PA-DISPLAY-COUNT.                       13432000
           DISPLAY 'TTP RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13432100
           MOVE PA-TTO-COUNT TO PA-DISPLAY-COUNT.                       13432200
           DISPLAY 'TTO RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13432300
           DISPLAY '                '.                                  13432400
                                                                        13432500
