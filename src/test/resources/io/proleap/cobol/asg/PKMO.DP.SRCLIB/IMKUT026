       IDENTIFICATION DIVISION.                                         09/16/94
                                                                           LV020
       PROGRAM-ID.             IMKUT026.                                   CL**1
       AUTHOR.                 PAUL KEBER.                                 CL**1
       INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL**1
       DATE-WRITTEN.           10/01/2002.                                 CL**1
       DATE-COMPILED.                                                      CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      *  IMKUT026 - TVSCHVL DESCRIPTION UPDATE TRIGGER RPC             *   CL**1
      ******************************************************************   CL**1
      * THIS STORED PROCEDURE PROGRAM WILL BE USED TO FORMAT A NON-VSM     CL**1
      * ITEM MAINTENANCE TVSCHVL DESCRIPTION CHANGE TRANSACTION RECORD     CL**1
      * FOR TICKET TYPE AND PUT IT ON THE IM.NONVSM.ITEM.CHANGES.Q         CL**1
      * MQSERIES QUEUE.                                                    CL**1
      ******************************************************************   CL**1
213345* NOVEMBER, 2018 - MAINFRAME REFACTORING PROJECT.   CHG0213345       CL**1
213345* BECAUSE THE DB2 STORED PROCEDUES WON'T BE ABLE TO USE MQ           CL**1
213345* AFTER THEY ARE REFACTORED INTO JAVA APPS, THE MQ LOGIC IS          CL**1
213345* BEING REPLACED WITH INSERTS TO THE IMT_EVNT_MSG_BRG TABLE.         CL**1
213345* THE INSERT LOGIC IS BEING COPIED FROM IMKUP002.                    CL**1
213345* IMKCS301 WILL BE CHANGED TO READ THE RECORD WITH A NEW                  
213345* 910 BUSINESS EVENT CODE AND WRITE IT TO THE                             
213345* IM.NONVSM.ITEM.CHANGES.Q WHICH IS WHAT THIS PROGRAM DID BEFORE          
213345* IT WAS REFACTORED.                                                      
      ******************************************************************   CL**1
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************   CL**1
      * PROGRAM CONSTANTS                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL**1
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME             PIC X(08)      VALUE 'IMKUT026'.         
           05  PC-OUT-MSGLENGTH        PIC S9(9)      VALUE +100 COMP.          
           05  PC-MAX-RETRIES          PIC S9(4)      VALUE +2   COMP.          
           05  PC-TKT-TYP-DESC-CHG     PIC S9(4)      VALUE +2   COMP.          
                                                                                
      ******************************************************************   CL**1
      * PROGRAM SWITCHES                                              *    CL**1
      ******************************************************************   CL**1
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW             PIC X(01)      VALUE 'N'.           CL**1
               88  PS-ERROR                           VALUE 'Y'.           CL**1
               88  PS-NO-ERROR                        VALUE 'N'.           CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      * PROGRAM VARIABLES                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL*17
       01  PV-PROGRAM-VARIABLES.                                           CL**1
           05  PV-RETRY-COUNT               PIC S9(4) VALUE +0 COMP.            
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACEs.             
           05  PV-ERROR-MESSAGE             PIC X(48) VALUE SPACES.             
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACES.             
                                                                                
       01  ABEND-CODE                       PIC S9(4) VALUE +0 COMP.            
           88  AC-DB2-ERROR                           VALUE +4013.              
                                                                                
      *****************************************************************         
      *  NON-VSM ITEM MAINTENANCE INFO COPYBOOK                                 
      ******************************************************************        
           COPY IMRD018.                                                        
                                                                           CL**1
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
213345*  ITEM EVENT MESSAGE BRIDGE TABLE (IMT_EVNT_MSG_BRG)                     
      ******************************************************************        
           EXEC SQL                                                             
213345         INCLUDE IMT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    DB2 COMMUNICATION AREA                                               
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                           CL**1
       LINKAGE SECTION.                                                         
       01  LS-CHARTC-CDE                PIC S9(04)  COMP.                       
       01  LS-CHARTC-SEQ-NBR            PIC S9(04)  COMP.                       
       01  LS-CHARTC-VAL-DESC-NEW       PIC  X(20).                             
                                                                                
       PROCEDURE DIVISION USING  LS-CHARTC-CDE                                  
                               , LS-CHARTC-SEQ-NBR                              
                               , LS-CHARTC-VAL-DESC-NEW.                        
                                                                                
213345******************************************************************   CL**1
213345*    FORMAT AND INSERT A NEW 910 BUSINESS EVENT CODE RECORD INTO          
213345*    THE IMT_EVNT_MSG_BRG TABLE.                                          
213345******************************************************************   CL**1
       1000-MAIN-MODULE.                                                        
                                                                                
           SET PS-NO-ERROR TO TRUE.                                             
                                                                                
           PERFORM 1100-CREATE-VSCHVL-DSC-CHG-REC.                              
                                                                                
           GOBACK.                                                              
                                                                                
213345******************************************************************   CL**1
213345*    CREATE A SPECIFIC TVSCHVL DESCRIPTION CHANGE MESSAGE TO BE           
213345*    INSERTED TO THE IMT_EVNT_MSG_BRG TABLE.                              
213345*    INSTEAD OF 010, THIS PROGRAM WILL NOW SET THE BUSINESS               
213345*    EVENT ID TO 910.  IMKCS301 WILL BE CHANGED TO SEND THE               
213345*    RECORD TO THE IM.NONVSM.ITEM.CHANGES.Q WHICH IS WHAT THIS            
213345*    PROGRAM DID BEFORE IT WAS REFACTORED AND DB2 STORED                  
213345*    PROCEDURES WRITTEN IN JAVA COULD NO LONGER WRITE TO MQ.              
213345******************************************************************   CL**1
       1100-CREATE-VSCHVL-DSC-CHG-REC.                                          
                                                                                
           INITIALIZE IM018-ITEM-ACTION-DATA.                                   
                                                                                
213345* THIS PROGRAM SHOULD ONLY BE INITIATED WHEN THE FOLLOWING                
213345* CONDITION IS TRUE.                                                      
           IF LS-CHARTC-CDE EQUAL PC-TKT-TYP-DESC-CHG                           
213345         MOVE 910               TO IM018-BUSINESS-EVENT-ID                
               MOVE LS-CHARTC-SEQ-NBR TO IM018-TKT-TYP-DESC-CHG-SEQ-NBR         
               MOVE LS-CHARTC-VAL-DESC-NEW                                      
                                      TO IM018-TKT-TYP-DESC-CHG-DESC            
213345         PERFORM 2000-INSERT-IMT-EVNT-MSG                                 
           END-IF.                                                              
                                                                                
213345                                                                  44710025
213345******************************************************************        
213345*  INSERT A BUSINESS EVENT RECORD TO THE ITEM EVENT MESSAGE               
213345*  BRIDGE DB2 TABLE.                                                      
213345******************************************************************        
213345 2000-INSERT-IMT-EVNT-MSG.                                        20580025
213345                                                                  20590025
213345     MOVE ZEROES              TO IMT00001-RMS-STYL-ID.                    
213345     MOVE 910                 TO IMT00001-BUS-EVNT-TYP-CDE.               
213345     MOVE IMRD018-RECORD      TO IMT00001-MSG-TXT-TEXT.                   
213345     COMPUTE IMT00001-MSG-TXT-LEN = LENGTH OF IMRD018-RECORD.             
213345                                                                  20620025
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL SQLCODE = +0                                             
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                          
213345*                                                                 20710025
213345           EXEC SQL                                               20720025
213345               INSERT INTO IMT_EVNT_MSG_BRG                               
213345                      ( CRE_TMST                                          
213345                      ,RMS_STYL_ID                                        
213345                      ,BUS_EVNT_TYP_CDE                                   
213345                      ,MSG_TXT )                                          
213345               VALUES                                                     
213345                     ( CURRENT TIMESTAMP                                  
213345                     ,:IMT00001-RMS-STYL-ID                               
213345                     ,:IMT00001-BUS-EVNT-TYP-CDE                          
213345                     ,:IMT00001-MSG-TXT)                                  
213345           END-EXEC                                               20800025
213345*                                                                 20810025
213345         EVALUATE TRUE                                            20820025
213345             WHEN SQLCODE = ZERO                                  20870025
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
213345                  CONTINUE                                        20860025
                   WHEN OTHER                                                   
213345                 MOVE '2000-INSERT-IMT-EVNT-MSG'                          
213345                                           TO PV-PARAGRAPH-NAME           
213345                 MOVE 'IMT_EVNT_MSG_BRG INSERT'                   20910025
213345                                           TO PV-DB2-OPERATION    20920025
                       PERFORM 9800-SQL-ERROR                                   
213345         END-EVALUATE                                             20960025
213345     END-PERFORM.                                                 20970025
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
213345         MOVE '2000-INSERT-IMT-EVNT-MSG'   TO PV-PARAGRAPH-NAME           
213345         MOVE 'INSERT RETRIES EXCEEDED'    TO PV-DB2-OPERATION            
               PERFORM 9800-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
213345******************************************************************   CL**1
      * 9800-SQL-ABEND - DISPLAY DB2 ERROR INFO AND ROLLBACK THE CHANGES        
213345******************************************************************   CL**1
       9800-SQL-ERROR.                                                     CL**1
                                                                           CL*13
           DISPLAY '** IMKUT026: SQLERROR IN PARAGRAPH: '                       
                   PV-PARAGRAPH-NAME.                                           
           DISPLAY '** IMKUT026: OPERATION: ' PV-DB2-OPERATION.                 
                                                                           CL*13
      ******************************************************************   CL**1
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**1
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE            CL**1
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE        CL**1
      * FORMAT.                                                            CL**1
      ******************************************************************   CL**1
           COPY DPPD004.                                                   CL**1
                                                                           CL**1
           EXEC SQL                                                             
               ROLLBACK                                                         
           END-EXEC.                                                            
                                                                                
           SET PS-ERROR TO TRUE.                                                
