      ***************************************************************** 00010099
       IDENTIFICATION DIVISION.                                         00020099
      ***************************************************************** 00030099
                                                                        00040099
       PROGRAM-ID.             ARKUT092.                                00050099
       AUTHOR.                 JAMES ALT.                               00060099
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070099
       DATE-WRITTEN            FEBRUARY 2011.                           00080099
       DATE-COMPILED.                                                   00090099
                                                                        00100099
      ***************************************************************** 00110099
      * THIS PROGRAM READS THE POS TRANSACTION FILE AND COMBINES ALL  * 00120099
      * OF THE E-COMMERCE TRANSACTIONS INTO ONE BY ORDER NUMBER.      * 00130099
      ***************************************************************** 00140099
       ENVIRONMENT DIVISION.                                            00150099
      ***************************************************************** 00160099
      * 02/13/2014 JOE GARDETTO                                         00161099
      *    -CHANGED FIELD REFERENCE AR001-BLUE-MART-ORD-NBR             00162099
      *     TO AR001-ORD-NBR.                                           00163099
      *    -CHANGED FIELD REFERENCE PV001-BLUE-MART-ORD-NBR             00163199
      *     TO PV001-ORD-NBR.                                           00163299
      ******************************************************************00163399
P9696 * 02/17/2014 COGNIZANT                                            00163499
P9696 *    -INCLUDED AR001-AUTH-REF-NBR TO ARRD001 COPYBOOK AND ADDED   00163599
P9696 *     LOGIC TO POPULATE.                                          00163699
P9696 *    -ADDED AR001-AUTH-NUMBER & AR001-AUTH-REF-NBR TO TRANSACTION 00163799
P9696 *     ROLLUP LOGIC.                                               00163899
P9696 *    -INCREASED LRECL OF INPUT AND OUTPUT FILE TO 311.            00163999
P9696 ******************************************************************00164099
P9696 * 06/30/2014 COGNIZANT                                            00165099
P9696 *    -MODIFIED TO UPDATE SAME AR001-ORG-STR-NBR FOR ALL           00166099
P9696 *     TRANSACTIONS UNDER AN ORDER WITH SAME AUTH# AND AUTH REF#   00167099
P9696 ******************************************************************00168099
CFEES * 01/30/2017 BARB SCHULTZ                                         00169099
CFEES *    -CHANGED INPUT AND OUTPUT FILE TO A LENGTH OF 385 FROM 311.  00170099
CFEES ******************************************************************00171099
SHIP  * 03/20/2019 BARB SCHULTZ                                         00172099
SHIP  *    -CHANGES DUE TO SETTLE BY SHIP.                              00173099
SHIP  *    -ADDED LOGIC TO THE ROLLUP TO NOT ROLLUP ANY OMNI TYPE       00174099
SHIP  *    -    SHIPMENTS.                                              00174199
SHIP  ******************************************************************00174299
SHIP2 * 01/20/2020 BARB SCHULTZ                                         00174399
SHIP2 *    -CHANGED KC-MULTI-CLR-SEQ-NBR TO MULT-CLR-SEQ-NBR.           00174499
SHIP2 *    -UPDATED THE PAR-SHP-IND.                                    00174599
SHIP2 ******************************************************************00174699
                                                                        00174799
       CONFIGURATION SECTION.                                           00174899
                                                                        00174999
       SOURCE-COMPUTER.        IBM-3090.                                00175099
                                                                        00175199
       OBJECT-COMPUTER.        IBM-3090.                                00175299
                                                                        00175399
       INPUT-OUTPUT SECTION.                                            00175499
                                                                        00175599
       FILE-CONTROL.                                                    00175699
                                                                        00175799
           SELECT TRXN-IN-FILE                                          00175899
               ASSIGN TO INP01.                                         00175999
                                                                        00176099
           SELECT TRXN-OUT-FILE                                         00177099
               ASSIGN TO OUT01.                                         00178099
                                                                        00179099
                                                                        00180099
      ******************************************************************00190099
       DATA DIVISION.                                                   00200099
      ******************************************************************00210099
                                                                        00220099
       FILE SECTION.                                                    00230099
                                                                        00240099
       FD  TRXN-IN-FILE                                                 00250099
           RECORDING MODE IS F                                          00260099
           BLOCK CONTAINS 0 RECORDS                                     00270099
           LABEL RECORDS ARE STANDARD.                                  00280099
                                                                        00290099
CFEES  01  TRXN-IN-RECORD  PIC X(385).                                  00300099
                                                                        00310099
       FD  TRXN-OUT-FILE                                                00320099
           RECORDING MODE IS F                                          00330099
           BLOCK CONTAINS 0 RECORDS                                     00340099
           LABEL RECORDS ARE STANDARD.                                  00350099
                                                                        00360099
CFEES  01  TRXN-OUT-RECORD PIC X(385).                                  00370099
                                                                        00380099
      ******************************************************************00390099
       WORKING-STORAGE SECTION.                                         00400099
      ******************************************************************00410099
       01  FILLER                            PIC X(50)  VALUE           00420099
           ' *** WORKING STORAGE STARTS HERE FOR ARKUT092 *** '.        00430099
                                                                        00440099
      ******************************************************************00450099
      * PROGRAM SWITCHES                                               *00460099
      ******************************************************************00470099
       01  PS-PROGRAM-SWITCHES.                                         00480099
           05  PS-END-OF-TRXN-FILE-SW        PIC X(01)  VALUE 'N'.      00490099
               88  PS-END-OF-TRXN-FILE                  VALUE 'Y'.      00500099
           05  PS-FIRST-TRXN-SW              PIC X(01)  VALUE 'N'.      00510099
               88  PS-FIRST-TRXN-YES                    VALUE 'Y'.      00520099
               88  PS-FIRST-TRXN-NO                     VALUE 'N'.      00530099
SHIP2      05  PS-ROLLUP-SW                  PIC  X(01) VALUE 'N'.      00540099
SHIP2          88  PS-ROLLUP                            VALUE 'Y'.      00550099
SHIP2          88  PS-NOT-ROLLUP                        VALUE 'N'.      00560099
SHIP2      05  PS-OMNI-ORDER-SW              PIC  X(01) VALUE 'N'.      00570099
SHIP2          88  PS-OMNI-ORDER                        VALUE 'Y'.      00580099
SHIP2          88  PS-NOT-OMNI-ORDER                    VALUE 'N'.      00590099
SHIP2      05  PS-FIRST-RECORD-SW            PIC  X(01) VALUE 'Y'.      00591099
SHIP2          88  PS-FIRST-RECORD                      VALUE 'Y'.      00592099
SHIP2          88  PS-NOT-FIRST-RECORD                  VALUE 'N'.      00593099
SHIP2      05  PS-PAR-SHP-IND-SW             PIC  X(01) VALUE 'N'.      00594099
SHIP2          88  PS-PAR-SHP-Y                         VALUE 'Y'.      00595099
SHIP2          88  PS-PAR-SHP-N                         VALUE 'N'.      00596099
                                                                        00597099
      ******************************************************************00598099
      * PROGRAM CONSTANTS                                              *00599099
      ******************************************************************00600099
       01  PC-CONSTANTS.                                                00610099
           05  PC-PROGRAM-NAME             PIC X(08)     VALUE          00620099
                                                         'ARKUT092'.    00630099
                                                                        00640099
      ******************************************************************00650099
      * PROGRAM ACCUMULATORS                                           *00660099
      ******************************************************************00670099
       01  PA-ACCUMULATORS.                                             00680099
           05  PA-TRXN-FILE-READS      PIC S9(07)       COMP-3          00690099
                                                        VALUE ZEROES.   00700099
           05  PA-TRXN-FILE-WRITES     PIC S9(07)       COMP-3          00710099
                                                        VALUE ZEROES.   00720099
           05  PA-TRXN-FILE-CHANGES    PIC S9(07)       COMP-3          00730099
                                                        VALUE ZEROES.   00740099
           05  PA-TRXN-FILE-UPDATES    PIC S9(07)       COMP-3          00750099
                                                        VALUE ZEROES.   00760099
                                                                        00770099
      ******************************************************************00780099
      * PROGRAM VARIABLES                                              *00790099
      ******************************************************************00800099
       01  PV-PROGRAM-VARIABLES.                                        00810099
           05  PV-DISPLAY-ASTERISKS    PIC  X(80) VALUE ALL '*'.        00820099
           05  PV-DISPLAY-HEADING.                                      00830099
               10 FILLER               PIC  X(30) VALUE ALL '*'.        00840099
               10 FILLER               PIC  X(20) VALUE                 00850099
                                       '  ARKUT092 COUNTS'.             00860099
               10 FILLER               PIC  X(30) VALUE ALL '*'.        00870099
           05  PV-DISPLAY-TOTALS       PIC  Z,ZZZ,ZZ9.                  00880099
                                                                        00890099
      ******************************************************************00900099
      * SAVE TRANSACTION FILE LAYOUT (REPLACE PREFIX AR001 WITH PV001) *00910099
      ******************************************************************00920099
           COPY ARRD001 REPLACING AR001 BY PV001.                       00930099
                                                                        00940099
      ******************************************************************00950099
      * TRANSACTION FILE LAYOUT                                        *00960099
      ******************************************************************00970099
           COPY ARRD001.                                                00980099
                                                                        00990099
      ******************************************************************01000099
      * PROCEDURE DIVISION.                                            *01010099
      ******************************************************************01020099
       PROCEDURE DIVISION.                                              01030099
                                                                        01040099
      ******************************************************************01050099
      * 0000-MAINLINE-PARAGRAPH                                        *01060099
      *  -  COMBINE E-COMMERCE TRANSACTIONS BY ORDER NUMBER            *01070099
      ******************************************************************01080099
       0000-MAINLINE-PARAGRAPH.                                         01090099
                                                                        01100099
           PERFORM 1000-INITIALIZE-PROGRAM.                             01110099
                                                                        01120099
           PERFORM 2000-PROCESS-TRANSACTION-DATA                        01130099
             UNTIL PS-END-OF-TRXN-FILE.                                 01140099
                                                                        01150099
           PERFORM 7000-END-PROGRAM.                                    01160099
                                                                        01170099
           GOBACK.                                                      01180099
                                                                        01190099
      ******************************************************************01200099
      * 1000-INITIALIZE-PROGRAM                                         01210099
      *  -  OPEN FILES                                                  01220099
      *  -  READ FIRST RECORD ON TRANSACTION FILE                       01230099
      *  -  SAVE KEY DATA FROM FIRST RECORD                             01240099
      ******************************************************************01250099
       1000-INITIALIZE-PROGRAM.                                         01260099
                                                                        01270099
           DISPLAY ' '.                                                 01280099
           DISPLAY PV-DISPLAY-ASTERISKS.                                01290099
           DISPLAY PV-DISPLAY-HEADING.                                  01300099
                                                                        01310099
           OPEN INPUT  TRXN-IN-FILE                                     01320099
                OUTPUT TRXN-OUT-FILE.                                   01330099
                                                                        01340099
           INITIALIZE PV001-AR-TRANSACTION-RECORD.                      01350099
                                                                        01360099
           PERFORM 3000-READ-TRXN-IN-FILE.                              01370099
                                                                        01380099
           IF PS-END-OF-TRXN-FILE                                       01390099
               DISPLAY '* WARNING:  TRANSACTION FILE WAS EMPTY'         01400099
               DISPLAY '* NO TRANSACTIONS WERE UPDATED'                 01410099
           ELSE                                                         01420099
               SET PS-FIRST-TRXN-YES    TO TRUE                         01440099
           END-IF.                                                      01450099
                                                                        01460099
                                                                        01470099
      ******************************************************************01480099
      * COMBINE ALL TRANSACTIONS FOR A SINGLE ORDER NUMBER SO THAT     *01490099
      * THE TRANSACTIONS ALL CONTAIN THE SAME KEY DATA                 *01500099
      ******************************************************************01510099
       2000-PROCESS-TRANSACTION-DATA.                                   01520099
                                                                        01530099
      * CHECK IF THE CURRENT ORDER MATCHES THE PREVIOUS ORDER           01540099
           IF  AR001-ORD-NBR   = PV001-ORD-NBR                          01550099
P9696      AND AR001-AUTH-NUMBER         = PV001-AUTH-NUMBER            01560099
P9696      AND AR001-AUTH-REF-NBR        = PV001-AUTH-REF-NBR           01570099
           AND AR001-TRANSACTION-CODE    = PV001-TRANSACTION-CODE       01580099
SHIP2      AND AR001-FULFILLMENT-TYPE    = PV001-FULFILLMENT-TYPE       01590099
SHIP       AND AR001-FULFILLMENT-TYPE    = 'S'                          01610099
               IF AR001-REFERENCE-NUMBER = PV001-REFERENCE-NUMBER       01620099
SHIP2              CONTINUE                                             01630099
               ELSE                                                     01640099
SHIP2              MOVE 'Y'             TO PS-ROLLUP-SW                 01641099
                   PERFORM 5000-OVERLAY-TRXN-KEY                        01642099
                   ADD +1               TO PA-TRXN-FILE-CHANGES         01643099
               END-IF                                                   01644099
           END-IF.                                                      01645099
                                                                        01646099
SHIP2                                                                   01680099
SHIP2      IF AR001-FULFILLMENT-TYPE = 'O'                              01690099
SHIP2         MOVE 'Y' TO PS-OMNI-ORDER-SW                              01691099
SHIP2      END-IF.                                                      01691199
SHIP2                                                                   01692599
SHIP2      IF AR001-TOT-SHP-NBR  > 1 AND                                01692699
SHIP2         PS-OMNI-ORDER-SW = 'N' AND                                01692799
SHIP2         AR001-MULT-CLR-SEQ-NBR = AR001-TOT-SHP-NBR AND            01692899
SHIP2         PS-ROLLUP-SW = 'N' AND                                    01692999
SHIP2         PS-PAR-SHP-IND-SW = 'N'                                   01693099
SHIP2          MOVE 'N' TO AR001-PAR-SHP-IND                            01694099
SHIP2          MOVE 'Y' TO PS-PAR-SHP-IND-SW                            01695099
SHIP2      ELSE                                                         01696099
SHIP2          MOVE 'Y' TO AR001-PAR-SHP-IND                            01697099
SHIP2      END-IF.                                                      01698099
SHIP2                                                                   01699099
SHIP2*** THIS IS IF THE LAST SHIPMENT CAME THE FOLLOWING DAY.           01700099
SHIP2      IF AR001-ORD-NBR NOT EQUAL PV001-ORD-NBR AND                 01701099
SHIP2         AR001-TOT-SHP-NBR  > 1 AND                                01702099
SHIP2         AR001-MULT-CLR-SEQ-NBR > 1 AND                            01702199
SHIP2         PS-OMNI-ORDER-SW = 'N' AND                                01702299
SHIP2         AR001-MULT-CLR-SEQ-NBR = AR001-TOT-SHP-NBR                01702399
SHIP2            MOVE 'Y' TO AR001-PAR-SHP-IND                          01702499
SHIP2      END-IF.                                                      01702599
SHIP2                                                                   01702699
SHIP2*** THIS IS DONE BECAUSE THE PAR SHIP IND SHOULD BE THE SAME AS    01702799
SHIP2*** THE DETAIL RECORD BEFORE IT.                                   01702899
SHIP2      IF AR001-ORD-NBR = PV001-ORD-NBR AND                         01702999
SHIP2         AR001-ITEM-DEPARTMENT = '850' OR 'TAX' OR 'FEE' OR 'TND'  01703099
SHIP2            MOVE PV001-PAR-SHP-IND TO AR001-PAR-SHP-IND            01703199
SHIP2      END-IF.                                                      01703299
SHIP2                                                                   01703399
SHIP2*** THIS IS DONE BECAUSE WE ALREADY RECEIVED THE LAST SHIPMENT     01703499
SHIP2*** AND THERE WAS ONLY S2H INVOLVED.                               01703599
SHIP2      IF PS-PAR-SHP-IND-SW = 'Y' AND                               01703699
SHIP2         PS-ROLLUP-SW = 'Y'                                        01703799
SHIP2         MOVE 'N' TO AR001-PAR-SHP-IND                             01703899
SHIP2      END-IF.                                                      01703999
SHIP2                                                                   01704099
SHIP2      IF PS-OMNI-ORDER-SW = 'Y' AND                                01704199
SHIP2         AR001-TOT-SHP-NBR > 1                                     01704299
SHIP2         MOVE 'Y' TO AR001-PAR-SHP-IND                             01704399
SHIP2      END-IF.                                                      01704499
SHIP2                                                                   01704599
SHIP2      IF AR001-MULT-CLR-SEQ-NBR = 0                                01704699
SHIP2         MOVE ' ' TO AR001-PAR-SHP-IND                             01704799
SHIP2      END-IF.                                                      01704899
                                                                        01704999
SHIP2      IF AR001-TOT-SHP-NBR  = 1                                    01705099
SHIP2         MOVE 'N' TO AR001-PAR-SHP-IND                             01705199
SHIP2      END-IF.                                                      01705299
                                                                        01705399
           PERFORM 6000-WRITE-TRXN-OUT-FILE.                            01705899
                                                                        01705999
           PERFORM 4000-SAVE-TRXN-KEY.                                  01706099
                                                                        01706199
                                                                        01706299
           PERFORM 3000-READ-TRXN-IN-FILE.                              01706399
                                                                        01706499
      ******************************************************************01706599
      * READ IN TRANSACTION RECORD                                     *01706699
      ******************************************************************01706799
       3000-READ-TRXN-IN-FILE.                                          01706899
                                                                        01706999
           INITIALIZE AR001-AR-TRANSACTION-RECORD.                      01707099
                                                                        01708099
           READ TRXN-IN-FILE INTO AR001-AR-TRANSACTION-RECORD           01709099
              AT END                                                    01710099
                 SET PS-END-OF-TRXN-FILE                                01720099
                                        TO TRUE                         01730099
           END-READ.                                                    01740099
                                                                        01750099
           IF PS-END-OF-TRXN-FILE                                       01760099
              CONTINUE                                                  01770099
           ELSE                                                         01780099
              IF  AR001-ORD-NBR NOT = SPACES                            01790099
              AND AR001-STORE-NUMBER      NOT = '0873'                  01800099
                 MOVE '0873'            TO AR001-STORE-NUMBER           01810099
                 ADD +1                 TO PA-TRXN-FILE-UPDATES         01820099
              END-IF                                                    01830099
              ADD +1                    TO PA-TRXN-FILE-READS           01840099
SHIP2         IF AR001-ORD-NBR NOT EQUAL PV001-ORD-NBR                  01850099
SHIP2            MOVE 'N' TO PS-OMNI-ORDER-SW                           01860099
SHIP2                        PS-FIRST-RECORD-SW                         01870099
SHIP2                        PS-ROLLUP-SW                               01880099
SHIP2                        PS-PAR-SHP-IND-SW                          01890099
SHIP2         END-IF                                                    01900099
           END-IF.                                                      01910099
                                                                        01920099
                                                                        01930099
      ******************************************************************01940099
      * SAVE CURRENT TRANSACTION KEY                                   *01950099
      ******************************************************************01960099
       4000-SAVE-TRXN-KEY.                                              01970099
                                                                        01980099
           MOVE AR001-TRANSACTION-DATE  TO PV001-TRANSACTION-DATE.      01990099
           MOVE AR001-TRANSACTION-TIME  TO PV001-TRANSACTION-TIME.      02000099
           MOVE AR001-STORE-NUMBER      TO PV001-STORE-NUMBER.          02010099
           MOVE AR001-REFERENCE-NUMBER  TO PV001-REFERENCE-NUMBER.      02020099
           MOVE AR001-TNDR-SEQ-NBR      TO PV001-TNDR-SEQ-NBR.          02030099
           MOVE AR001-TRANSACTION-SOURCE                                02040099
                                        TO PV001-TRANSACTION-SOURCE.    02050099
           MOVE AR001-ORD-NBR           TO PV001-ORD-NBR.               02060099
P9696      MOVE AR001-AUTH-NUMBER       TO PV001-AUTH-NUMBER.           02070099
P9696      MOVE AR001-AUTH-REF-NBR      TO PV001-AUTH-REF-NBR.          02080099
P9696      MOVE AR001-ORG-STR-NBR       TO PV001-ORG-STR-NBR.           02090099
           MOVE AR001-TRANSACTION-CODE  TO PV001-TRANSACTION-CODE.      02100099
           MOVE AR001-FULFILLMENT-TYPE  TO PV001-FULFILLMENT-TYPE.      02110099
SHIP2      MOVE AR001-PAR-SHP-IND       TO PV001-PAR-SHP-IND.           02120099
                                                                        02130099
      ******************************************************************02140099
      * OVERLAY CURRENT TRANSACTION KEY                                *02150099
      ******************************************************************02160099
       5000-OVERLAY-TRXN-KEY.                                           02170099
                                                                        02180099
           MOVE PV001-TRANSACTION-DATE  TO AR001-TRANSACTION-DATE.      02190099
           MOVE PV001-TRANSACTION-TIME  TO AR001-TRANSACTION-TIME.      02200099
           MOVE PV001-STORE-NUMBER      TO AR001-STORE-NUMBER.          02210099
           MOVE PV001-REFERENCE-NUMBER  TO AR001-REFERENCE-NUMBER.      02220099
           MOVE PV001-TNDR-SEQ-NBR      TO AR001-TNDR-SEQ-NBR.          02230099
           MOVE PV001-TRANSACTION-SOURCE                                02240099
                                        TO AR001-TRANSACTION-SOURCE.    02250099
           MOVE PV001-ORD-NBR           TO AR001-ORD-NBR.               02260099
P9696      MOVE PV001-AUTH-NUMBER       TO AR001-AUTH-NUMBER.           02270099
P9696      MOVE PV001-AUTH-REF-NBR      TO AR001-AUTH-REF-NBR.          02280099
P9696      MOVE PV001-ORG-STR-NBR       TO AR001-ORG-STR-NBR.           02290099
           MOVE PV001-TRANSACTION-CODE  TO AR001-TRANSACTION-CODE.      02300099
SHIP2      MOVE PV001-PAR-SHP-IND       TO AR001-PAR-SHP-IND.           02310099
                                                                        02320099
      ******************************************************************02330099
      * WRITE OUT TRANSACTION RECORD                                   *02340099
      ******************************************************************02350099
       6000-WRITE-TRXN-OUT-FILE.                                        02360099
                                                                        02370099
           WRITE TRXN-OUT-RECORD FROM AR001-AR-TRANSACTION-RECORD.      02380099
           ADD +1                    TO PA-TRXN-FILE-WRITES.            02390099
                                                                        02400099
      ******************************************************************02410099
      * DISPLAY COUNTERS AND CLOSE FILES                               *02420099
      ******************************************************************02430099
       7000-END-PROGRAM.                                                02440099
                                                                        02450099
           MOVE PA-TRXN-FILE-READS      TO PV-DISPLAY-TOTALS.           02460099
                                                                        02470099
           DISPLAY '* TOTAL RECORDS READ    ' PV-DISPLAY-TOTALS.        02480099
                                                                        02490099
           MOVE PA-TRXN-FILE-CHANGES    TO PV-DISPLAY-TOTALS.           02500099
                                                                        02510099
           DISPLAY '* TOTAL RECORDS CHANGED ' PV-DISPLAY-TOTALS.        02520099
                                                                        02530099
           MOVE PA-TRXN-FILE-UPDATES    TO PV-DISPLAY-TOTALS.           02540099
                                                                        02550099
           DISPLAY '* TOTAL RECORDS UPDATED ' PV-DISPLAY-TOTALS.        02560099
                                                                        02570099
           MOVE PA-TRXN-FILE-WRITES     TO PV-DISPLAY-TOTALS.           02580099
                                                                        02590099
           DISPLAY '* TOTAL RECORDS WRITTEN ' PV-DISPLAY-TOTALS.        02600099
                                                                        02610099
           DISPLAY PV-DISPLAY-ASTERISKS.                                02620099
           DISPLAY ' '.                                                 02630099
                                                                        02640099
           CLOSE TRXN-IN-FILE                                           02650099
                 TRXN-OUT-FILE.                                         02660099
                                                                        02670099
