       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP058.                                         00030000
       AUTHOR.        JACK KRAMER.                                      00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  02/17/2010.                                       00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *          MLKUP058 - PRICE CHANGE ADJUSTMENT TRANSACTION UPDATE *00100000
      *                     TABLE UPDATE (MLT_PRCHG_TRN_DTL)           *00110000
      ******************************************************************00110100
      ******************************************************************00110200
      *    WR/PITS#     DATE        DESCRIPTION                        *00110300
      *    --------   --------     ---------------------------------   *00110400
      *    WR37965    02/17/10     INITIAL CREATION                    *00110500
      ******************************************************************00110600
                                                                        00110700
       ENVIRONMENT DIVISION.                                            00110800
                                                                        00110900
       CONFIGURATION SECTION.                                           00111000
       SOURCE-COMPUTER.        IBM-3090.                                00112000
       OBJECT-COMPUTER.        IBM-3090.                                00113000
       INPUT-OUTPUT SECTION.                                            00114000
       FILE-CONTROL.                                                    00115000
           SELECT PRCHG-TRN-DTL-FILE                                    00116000
               ASSIGN TO INP01.                                         00117000
           SELECT CNTL-DATE-FILE                                        00117101
               ASSIGN TO INP02.                                         00117201
                                                                        00118000
       DATA DIVISION.                                                   00119000
       FILE SECTION.                                                    00120000
                                                                        00130000
       FD  PRCHG-TRN-DTL-FILE                                           00140000
           LABEL RECORDS ARE STANDARD                                   00150000
           RECORDING MODE IS F                                          00160000
           BLOCK CONTAINS 0 RECORDS.                                    00170000
       01  PRCHG-TRN-DTL-REC           PIC X(200).                      00180000
                                                                        00180101
       FD  CNTL-DATE-FILE                                               00181001
           LABEL RECORDS ARE STANDARD                                   00182001
           RECORDING MODE IS F                                          00183001
           BLOCK CONTAINS 0 RECORDS.                                    00184001
       01  CNTL-DATE-REC               PIC X(80).                       00185001
      *                                                                 00190000
                                                                        00200000
       WORKING-STORAGE SECTION.                                         00210000
                                                                        00220000
       01  FILLER                      PIC X(28)   VALUE                00230000
                'BEGINNING OF WORKING STORAGE'.                         00240000
                                                                        00250000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00260000
                                                                        00270000
       01  PC-PROGRAM-CONSTANTS.                                        00280000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK058'.    00290000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP058'.  00300000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00310000
                                                                        00320000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00330000
           05  PE-MSG-01                       PIC X(50) VALUE          00340000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00350000
           05  PE-MSG-02                       PIC X(50) VALUE          00360000
                'ATTEMPT TO UPDATE ROW ON MLT_PRCHG_TRN_DTL FAILED'.    00370000
           05  PE-MSG-03                       PIC X(50) VALUE          00380000
                'ATTEMPT TO INSERT ROW ON MLT_PRCHG_TRN_DTL FAILED'.    00390000
           05  PE-MSG-04                       PIC X(50) VALUE          00400000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00410000
           05  PE-MSG-05                       PIC X(50) VALUE          00420000
                'UPDATE TO TCKRSTINF FAILED                      '.     00430000
           05  PE-MSG-06                       PIC X(50) VALUE          00440000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00450000
           05  PE-MSG-07                       PIC X(50) VALUE          00460000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00470000
           05  PE-MSG-08                       PIC X(50) VALUE          00471001
                'ERROR WHILE VALIDATING CONTROL DATE'.                  00472001
      *                                                                 00480000
       01  PS-PROGRAM-SWITCHES.                                         00490000
           05  PS-EOF-PRCHG-DTL-FILE-SW     PIC X(01)     VALUE 'N'.    00500000
               88  PS-EOF-PRCHG-DTL-FILE                  VALUE 'Y'.    00510000
           05  PS-MLT00038-CURSOR-EOF-SW    PIC X(01)     VALUE 'N'.    00520000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00530000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00540000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00550000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00560000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00570000
           05  PS-CNTL-DATE-FILE-SW         PIC X(01)     VALUE 'N'.    00571001
               88  PS-END-OF-CNTL-DTE-FILE                VALUE 'Y'.    00572001
      *                                                                 00580000
       01  CC-CONTROL-CARD.                                             00581001
           05  CC-CNTL-DATE                 PIC X(10)     VALUE SPACES. 00582001
           05  FILLER                       PIC X(70)     VALUE SPACES. 00583001
      *                                                                 00584001
       01  PROGRAM-VARIABLES.                                           00640000
           05  PV-RETRY-COUNT            PIC S9(04) VALUE +0.           00650000
           05  PV-DEADLOCK-COUNT         PIC S9(04) VALUE +0.           00660000
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00670000
           05  PV-ABEND-MESSAGE          PIC  X(75) VALUE SPACES.       00671000
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00672000
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.       00673000
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.       00674000
           05  PV-DISPLAY-FIELD          PIC ZZZZZZZZ9  VALUE ZERO.     00675000
           05  PV-DB2-DATE-EXISTS        PIC   X(1) VALUE SPACES.       00675101
      *                                                                 00676000
       01  PA-PROGRAM-ACCUMULATORS.                                     00677000
           05  PA-RECORD-COUNTS.                                        00678000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00679000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      00680000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      00690000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      00700000
      *                                                                 00710000
       01  PD-PROGRAM-DISPLAYS.                                         00720000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            00730000
      *                                                                 00770000
      ******************************************************************00780000
      * MLRD090 - AUDIT DETAIL TRANSACTIONS                             00790000
      ******************************************************************00800000
           COPY MLRD090.                                                00810011
      *                                                                 00820000
      ******************************************************************00830000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                00840000
      ******************************************************************00850000
           COPY DPWS004.                                                00860000
      *                                                                 00870000
      ******************************************************************00880000
      *  USED FOR DB2 ERRORS                                            00890000
      ******************************************************************00900000
           EXEC SQL                                                     00910000
               INCLUDE SQLCA                                            00920000
           END-EXEC.                                                    00930000
      *                                                                 00940000
      ******************************************************************00941001
      * DATE TABLE                                                      00942001
      ******************************************************************00943001
           EXEC SQL                                                     00944001
             INCLUDE TDTATTR                                            00945001
           END-EXEC.                                                    00946001
      *                                                                 00947001
      ******************************************************************00950000
      *  MERCHANDISE LEDGER PURCHASE ADJUSTMENT AUDIT DETAIL            00960000
      ******************************************************************00970000
           EXEC SQL                                                     00980000
               INCLUDE MLT00038                                         00990000
           END-EXEC.                                                    01000000
      *                                                                 01010000
      ***************************************************************** 01020000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01030000
      ***************************************************************** 01040000
           EXEC SQL                                                     01050000
               INCLUDE TCKRSTCN                                         01060000
           END-EXEC.                                                    01070000
      *                                                                 01080000
           EXEC SQL                                                     01090000
               INCLUDE TCKRSTIN                                         01100000
           END-EXEC.                                                    01110000
      *                                                                 01120000
      ******************************************************************01130000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01140000
      ******************************************************************01150000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01160000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +76 COMP.  01170005
           05  CR-CHECKPOINT-RESTART-VAR.                               01180000
               10  CR-PREV-DTL-KEY.                                     01190000
                   15  CR-FSCL-WK-END-DTE   PIC X(10)  VALUE SPACES.    01200000
                   15  CR-PRCG-DTE          PIC X(10)  VALUE SPACES.    01210000
                   15  CR-DEPT-NBR          PIC S9(4)V COMP-3 VALUE +0. 01220000
                   15  CR-MAJ-CL-NBR        PIC S9(4)V COMP-3 VALUE +0. 01230000
                   15  CR-SUB-CL-NBR        PIC S9(4)V COMP-3 VALUE +0. 01240000
                   15  CR-LOC-NBR           PIC S9(4)  COMP   VALUE +0. 01250000
                   15  CR-ML-LO-MDSE-LVL-ID PIC S9(9)  COMP   VALUE +0. 01260000
                   15  CR-ORIG-ENT-ID       PIC S9(9)  COMP   VALUE +0. 01270000
                   15  CR-ORIG-LBL-SEQ-NBR  PIC S9(9)  COMP   VALUE +0. 01280000
                   15  CR-DUMMY-SKU-IND     PIC X(01) VALUE SPACES.     01290000
                   15  CR-FINCL-PRC-GP-CDE  PIC X(06) VALUE SPACES.     01300000
                   15  CR-SRC-SYS-ID        PIC S9(4)  COMP   VALUE +0. 01310000
                   15  CR-TRN-TYP-CDE       PIC X(03) VALUE SPACES.     01320000
                   15  CR-PRCHG-RSN-CDE     PIC X(02) VALUE SPACES.     01330000
                   15  CR-PRCHG-CTG-CDE     PIC X(02) VALUE SPACES.     01331000
                   15  CR-ENT-ID            PIC S9(9)  COMP   VALUE +0. 01332000
                   15  CR-LBL-SEQ-NBR       PIC S9(4)  COMP   VALUE +0. 01333000
               10  CR-INPUT-READ-COUNT      PIC 9(09) VALUE ZEROES.     01334000
      *                                                                 01335000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01336000
           05  CK-SAVE-PREV-KEY.                                        01337000
               10  CK-FSCL-WK-END-DTE     PIC X(10)  VALUE SPACES.      01338000
               10  CK-PRCG-DTE            PIC X(10)  VALUE SPACES.      01339000
               10  CK-DEPT-NBR            PIC S9(4)V COMP-3 VALUE +0.   01340000
               10  CK-MAJ-CL-NBR          PIC S9(4)V COMP-3 VALUE +0.   01350000
               10  CK-SUB-CL-NBR          PIC S9(4)V COMP-3 VALUE +0.   01360000
               10  CK-LOC-NBR             PIC S9(4)  COMP   VALUE +0.   01370000
               10  CK-ML-LO-MDSE-LVL-ID   PIC S9(9)  COMP   VALUE +0.   01380000
               10  CK-ORIG-ENT-ID         PIC S9(9)  COMP   VALUE +0.   01390000
               10  CK-ORIG-LBL-SEQ-NBR    PIC S9(9)  COMP   VALUE +0.   01391000
               10  CK-DUMMY-SKU-IND       PIC X(01) VALUE SPACES.       01392000
               10  CK-FINCL-PRC-GP-CDE    PIC X(06) VALUE SPACES.       01393000
               10  CK-SRC-SYS-ID          PIC S9(4)  COMP   VALUE +0.   01394000
               10  CK-TRN-TYP-CDE         PIC X(03) VALUE SPACES.       01395000
               10  CK-PRCHG-RSN-CDE       PIC X(02) VALUE SPACES.       01396000
               10  CK-PRCHG-CTG-CDE       PIC X(02) VALUE SPACES.       01397000
               10  CK-ENT-ID              PIC S9(9)  COMP   VALUE +0.   01398000
               10  CK-LBL-SEQ-NBR         PIC S9(4)  COMP   VALUE +0.   01399000
                                                                        01400000
      *-------------------*                                             01400100
       PROCEDURE DIVISION.                                              01400200
      *-------------------*                                             01400300
                                                                        01400400
       0000-MLKUP058.                                                   01400500
                                                                        01400600
           PERFORM 1000-INITIALIZATION.                                 01400700
      *                                                                 01400800
           PERFORM 2000-PROCESS-INPUT                                   01400900
                UNTIL PS-EOF-PRCHG-DTL-FILE.                            01401000
      *                                                                 01402000
           PERFORM 8000-EOJ-ROUTINE.                                    01403000
                                                                        01404000
           STOP RUN.                                                    01405000
                                                                        01406000
      ******************************************************************01407000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 01408000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      01409000
      ******************************************************************01410000
      *                                                                 01420000
       1000-INITIALIZATION.                                             01430000
      *                                                                 01440000
           OPEN INPUT PRCHG-TRN-DTL-FILE                                01450001
                      CNTL-DATE-FILE.                                   01451001
                                                                        01460000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         01470000
                                                                        01480000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              01490000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           01500000
               PERFORM 7500-SELECT-RESTART-INFO                         01510000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    01520000
                                            CR-CHECKPOINT-RESTART-VAR   01530000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        01540000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                01550000
               DISPLAY 'FSCL WK END DTE ' CR-FSCL-WK-END-DTE            01560000
               DISPLAY 'PRCG DATE       ' CR-PRCG-DTE                   01570000
               MOVE CR-DEPT-NBR           TO PV-DISPLAY-FIELD           01580000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              01590000
               MOVE CR-MAJ-CL-NBR         TO PV-DISPLAY-FIELD           01600000
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              01610000
               MOVE CR-SUB-CL-NBR         TO PV-DISPLAY-FIELD           01620000
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              01630000
               MOVE CR-LOC-NBR            TO PV-DISPLAY-FIELD           01640000
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              01650000
               MOVE CR-ML-LO-MDSE-LVL-ID  TO PV-DISPLAY-FIELD           01660000
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              01670000
               MOVE CR-ORIG-ENT-ID        TO PV-DISPLAY-FIELD           01680000
               DISPLAY 'ORIG ENT ID     ' PV-DISPLAY-FIELD              01690000
               MOVE CR-ORIG-LBL-SEQ-NBR   TO PV-DISPLAY-FIELD           01700000
               DISPLAY 'ORIG LBL SEQ    ' PV-DISPLAY-FIELD              01710000
               DISPLAY 'DUMMY SKU IND   ' CR-DUMMY-SKU-IND              01720000
               DISPLAY 'FINCL PRC CDE   ' CR-FINCL-PRC-GP-CDE           01730000
               MOVE CR-SRC-SYS-ID         TO PV-DISPLAY-FIELD           01740000
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              01750000
               DISPLAY 'TRN TYP CDE     ' CR-TRN-TYP-CDE                01760000
               DISPLAY 'PRCHG RSN CDE   ' CR-PRCHG-RSN-CDE              01770000
               DISPLAY 'PRCHG CTG CDE   ' CR-PRCHG-CTG-CDE              01780000
               MOVE CR-ENT-ID             TO PV-DISPLAY-FIELD           01790000
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              01800000
               MOVE CR-LBL-SEQ-NBR        TO PV-DISPLAY-FIELD           01801000
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              01802000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           01803000
           ELSE                                                         01804000
               SET PS-FIRST-COMMIT TO TRUE                              01805000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     01806000
           END-IF.                                                      01807000
      *                                                                 01808000
           PERFORM 5000-READ-DATE-CNTL-FILE                             01808101
           PERFORM 4000-READ-PRCHG-DTL-FILE UNTIL                       01809000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     01810000
            OR PS-EOF-PRCHG-DTL-FILE.                                   01820000
      *                                                                 01830000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           01840000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                01850000
                        PA-INPUT-COUNT                                  01860000
               DISPLAY 'FSCL WK END DTE ' ML090-FSCL-WK-END-DTE         01870000
               DISPLAY 'PRCG DATE       ' ML090-PRCG-DTE                01880000
               MOVE ML090-DEPT-NBR        TO PV-DISPLAY-FIELD           01890000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              01900000
               MOVE ML090-MAJ-CL-NBR      TO PV-DISPLAY-FIELD           01910000
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              01920000
               MOVE ML090-SUB-CL-NBR      TO PV-DISPLAY-FIELD           01930000
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              01940000
               MOVE ML090-LOC-NBR         TO PV-DISPLAY-FIELD           01950000
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              01960000
               MOVE ML090-ML-LO-MDSE-LVL-ID TO PV-DISPLAY-FIELD         01970000
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              01980000
               MOVE ML090-ORIG-ENT-ID     TO PV-DISPLAY-FIELD           01981000
               DISPLAY 'ORIG ENT ID     ' PV-DISPLAY-FIELD              01982000
               MOVE ML090-ORIG-LBL-SEQ-NBR TO PV-DISPLAY-FIELD          01983000
               DISPLAY 'ORIG LBL SEQ    ' PV-DISPLAY-FIELD              01984000
               DISPLAY 'DUMMY SKU IND   ' ML090-DUMMY-SKU-IND           01985000
               DISPLAY 'FINCL PRC CDE   ' ML090-FINCL-PRC-GP-CDE        01986000
               MOVE ML090-SRC-SYS-ID      TO PV-DISPLAY-FIELD           01987000
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              01988000
               DISPLAY 'TRN TYP CDE     ' ML090-TRN-TYP-CDE             01989000
               DISPLAY 'PRCHG RSN CDE   ' ML090-PRCHG-RSN-CDE           01990000
               DISPLAY 'PRCHG CTG CDE   ' ML090-PRCHG-CTG-CDE           01990100
               MOVE ML090-ENT-ID          TO PV-DISPLAY-FIELD           01990200
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              01990300
               MOVE ML090-LBL-SEQ-NBR     TO PV-DISPLAY-FIELD           01990400
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              01990500
           END-IF.                                                      01990600
                                                                        01990700
           IF PS-EOF-PRCHG-DTL-FILE                                     01990800
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       01990900
                   CONTINUE                                             01991000
               ELSE                                                     01991100
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             01991200
               END-IF                                                   01991300
           ELSE                                                         01991400
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        01991500
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  01991600
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             01991700
           END-IF.                                                      01991800
                                                                        01991900
      ******************************************************************01992000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  01993000
      ******************************************************************01994000
       2000-PROCESS-INPUT.                                              01995000
      *                                                                 01996000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          01997000
      *                                                                 01998000
           IF CC-CNTL-DATE = SPACES                                     01998301
             MOVE ML090-PRCG-DTE        TO MLT00038-PRCG-DTE            01998501
           ELSE                                                         01998601
             MOVE CC-CNTL-DATE          TO MLT00038-PRCG-DTE            01998801
           END-IF                                                       01999101
      *                                                                 01999207
           PERFORM 7200-INSERT-MLT00038-ROW.                            01999307
      *                                                                 02150000
           IF PS-PROGRAM-DEADLOCKED                                     02160000
               CONTINUE                                                 02170000
           ELSE                                                         02180000
               PERFORM 7700-COMMIT-PROCESSING                           02190000
               PERFORM 4000-READ-PRCHG-DTL-FILE                         02200000
           END-IF.                                                      02210000
      *                                                                 02220000
      *                                                                 02230000
      ******************************************************************02330000
      * READS THE CONDENSED FILE INTO THE MLT_PRCHG_TRN_DTL LAYOUT      02340000
      ******************************************************************02350000
       4000-READ-PRCHG-DTL-FILE.                                        02360000
           READ PRCHG-TRN-DTL-FILE INTO ML090-AUDIT-REC                 02370000
              AT END                                                    02380000
                MOVE 'Y' TO PS-EOF-PRCHG-DTL-FILE-SW.                   02390000
      *                                                                 02400000
           IF PS-EOF-PRCHG-DTL-FILE                                     02410000
               IF PA-INPUT-COUNT=0                                      02411010
                  INITIALIZE ML090-AUDIT-REC                            02412010
               END-IF                                                   02413010
               MOVE ML090-FSCL-WK-END-DTE     TO CR-FSCL-WK-END-DTE     02420000
               MOVE ML090-PRCG-DTE            TO CR-PRCG-DTE            02430000
               MOVE ML090-DEPT-NBR            TO CR-DEPT-NBR            02440000
               MOVE ML090-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          02450000
               MOVE ML090-SUB-CL-NBR          TO CR-SUB-CL-NBR          02460000
               MOVE ML090-LOC-NBR             TO CR-LOC-NBR             02470000
               MOVE ML090-ML-LO-MDSE-LVL-ID   TO CR-ML-LO-MDSE-LVL-ID   02480000
               MOVE ML090-ORIG-ENT-ID         TO CR-ORIG-ENT-ID         02490000
               MOVE ML090-ORIG-LBL-SEQ-NBR    TO CR-ORIG-LBL-SEQ-NBR    02500000
               MOVE ML090-DUMMY-SKU-IND       TO CR-DUMMY-SKU-IND       02510000
               MOVE ML090-FINCL-PRC-GP-CDE    TO CR-FINCL-PRC-GP-CDE    02520000
               MOVE ML090-SRC-SYS-ID          TO CR-SRC-SYS-ID          02530000
               MOVE ML090-TRN-TYP-CDE         TO CR-TRN-TYP-CDE         02540000
               MOVE ML090-PRCHG-RSN-CDE       TO CR-PRCHG-RSN-CDE       02550000
               MOVE ML090-PRCHG-CTG-CDE       TO CR-PRCHG-CTG-CDE       02560000
               MOVE ML090-ENT-ID              TO CR-ENT-ID              02570000
               MOVE ML090-LBL-SEQ-NBR         TO CR-LBL-SEQ-NBR         02580000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    02590000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  02600000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       02610000
                                             TCKRSTINF-WORK-STRG-DESC   02620000
               EXEC SQL                                                 02630000
                 UPDATE TCKRSTINF                                       02640000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      02650000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       02660000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   02670000
               END-EXEC                                                 02680000
               IF SQLCODE = 0                                           02690000
                   CONTINUE                                             02700000
               ELSE                                                     02710000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    02720000
                   MOVE '* 4000-READ-PRCHG-DTL  *' TO PV-PARAGRAPH-NAME 02730000
                   PERFORM 9999-SQL-ABEND                               02740000
               END-IF                                                   02750000
            ELSE                                                        02760000
                ADD 1                        TO PA-INPUT-COUNT          02770000
            END-IF.                                                     02780000
      *                                                                 02790000
      ******************************************************************02791001
      * READ DATE CONTROL FILE                                          02792001
      ******************************************************************02793001
       5000-READ-DATE-CNTL-FILE.                                        02794001
           READ CNTL-DATE-FILE INTO CC-CONTROL-CARD                     02795001
               AT END                                                   02796001
                   MOVE SPACES TO CC-CONTROL-CARD                       02797001
                   SET PS-END-OF-CNTL-DTE-FILE TO TRUE                  02798001
                   DISPLAY  'PROCESSING DATE CONTROL CARD EMPTY'.       02799001
           IF NOT PS-END-OF-CNTL-DTE-FILE                               02799101
               PERFORM 5001-VALIDATE-CNTL-DATE                          02799201
           END-IF.                                                      02799301
      *                                                                 02799401
      ******************************************************************02799501
      * VALIDATE CONTROL DATE                                           02799601
      ******************************************************************02799701
       5001-VALIDATE-CNTL-DATE.                                         02799801
                  EXEC SQL                                              02799901
                     SELECT 'Y'                                         02800001
                       INTO :PV-DB2-DATE-EXISTS                         02800101
                       FROM TDTATTR                                     02800201
                      WHERE KY_DTE = :CC-CNTL-DATE                      02800301
                  END-EXEC                                              02800401
                                                                        02800501
                  EVALUATE TRUE                                         02800601
                      WHEN SQLCODE = 0                                  02800701
                           CONTINUE                                     02800801
                      WHEN SQLCODE = 100                                02800901
                           DISPLAY 'INVALID DATE IN CONTROL CARD'       02801001
                           MOVE SPACES TO CC-CNTL-DATE                  02801101
                      WHEN OTHER                                        02801201
                           CONTINUE                                     02801301
                           MOVE '5001-VALIDATE-CNTL-DATE' TO            02801401
                                              PV-PARAGRAPH-NAME         02801501
                           MOVE PE-MSG-08 TO PV-OPERATION-ATTEMPTED     02801601
                           PERFORM 9999-SQL-ABEND                       02801701
                  END-EVALUATE.                                         02801801
      *                                                                 03290000
      ***************************************************************** 03291009
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON KEY. CURRENT ROW 03292009
      * AMOUNT FIELDS UPDATED TO ADD VALUES FROM INPUT REC.             03293009
      ***************************************************************** 03294009
       7100-UPDATE-MLT00038-ROW.                                        03350000
      *                                                                 03380000
           EXEC SQL                                                     03390000
               UPDATE MLT_PRCHG_TRN_DTL                                 03400000
                   SET PRCHG_RTL_AMT      = PRCHG_RTL_AMT +             03411007
                                            :ML090-PRCHG-RTL-AMT        03412007
              WHERE   FSCL_WK_END_DTE     = :ML090-FSCL-WK-END-DTE      03420000
                AND   PRCG_DTE            = :MLT00038-PRCG-DTE          03430001
                AND   DEPT_NBR            = :ML090-DEPT-NBR             03440000
                AND   MAJ_CL_NBR          = :ML090-MAJ-CL-NBR           03450000
                AND   SUB_CL_NBR          = :ML090-SUB-CL-NBR           03460000
                AND   LOC_NBR             = :ML090-LOC-NBR              03470000
                AND   ML_LO_MDSE_LVL_ID   = :ML090-ML-LO-MDSE-LVL-ID    03480000
                AND   ORIG_ENT_ID         = :ML090-ORIG-ENT-ID          03490000
                AND   ORIG_LBL_SEQ_NBR    = :ML090-ORIG-LBL-SEQ-NBR     03500000
                AND   DMY_SKU_IND         = :ML090-DUMMY-SKU-IND        03510000
                AND   FINCL_PRC_GP_CDE    = :ML090-FINCL-PRC-GP-CDE     03520000
                AND   SRC_SYS_ID          = :ML090-SRC-SYS-ID           03530000
                AND   TRN_TYP_CDE         = :ML090-TRN-TYP-CDE          03540000
                AND   PRCHG_RSN_CDE       = :ML090-PRCHG-RSN-CDE        03550000
                AND   PRCHG_CTG_CDE       = :ML090-PRCHG-CTG-CDE        03560000
                AND   ENT_ID              = :ML090-ENT-ID               03570000
                AND   LBL_SEQ_NBR         = :ML090-LBL-SEQ-NBR          03580000
           END-EXEC                                                     03590000
      *                                                                 03600000
           EVALUATE SQLCODE                                             03610000
               WHEN 0                                                   03620000
                   ADD 1 TO PA-UPDATE-COUNT                             03630007
               WHEN -911                                                03640000
                   ADD +1             TO PV-DEADLOCK-COUNT              03650000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            03660000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03670000
                       MOVE '7100-UPDATE-MLT00038-ROW' TO               03680000
                                                    PV-PARAGRAPH-NAME   03690000
                       PERFORM 9000-DEADLOCK-ERROR                      03700000
                   ELSE                                                 03710000
                       PERFORM 7999-RESTART-PROCESS                     03720000
                   END-IF                                               03730000
               WHEN OTHER                                               03740000
                   MOVE '7100-UPDATE-MLT00038-ROW' TO PV-PARAGRAPH-NAME 03750000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED03760000
                   PERFORM 9999-SQL-ABEND                               03770000
           END-EVALUATE.                                                03780000
      *                                                                 03810000
      ***************************************************************** 03820000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY DTL TABLE. INITIAL   * 03830000
      * ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW.             * 03840000
      ***************************************************************** 03860000
       7200-INSERT-MLT00038-ROW.                                        03870000
      *                                                                 03880000
           EXEC SQL                                                     03910000
               INSERT INTO MLT_PRCHG_TRN_DTL                            03920000
                    (  FSCL_WK_END_DTE                                  03930000
                     , PRCG_DTE                                         03940000
                     , DEPT_NBR                                         03950000
                     , MAJ_CL_NBR                                       03960000
                     , SUB_CL_NBR                                       03970000
                     , LOC_NBR                                          03980000
                     , ML_LO_MDSE_LVL_ID                                03990000
                     , ORIG_ENT_ID                                      04000000
                     , ORIG_LBL_SEQ_NBR                                 04010000
                     , DMY_SKU_IND                                      04020000
                     , FINCL_PRC_GP_CDE                                 04030000
                     , SRC_SYS_ID                                       04040000
                     , TRN_TYP_CDE                                      04050000
                     , PRCHG_RSN_CDE                                    04060002
                     , PRCHG_CTG_CDE                                    04070002
                     , ENT_ID                                           04080000
                     , LBL_SEQ_NBR                                      04090000
                     , PRCHG_RTL_AMT)                                   04100000
               VALUES                                                   04110000
                   (   :ML090-FSCL-WK-END-DTE                           04120000
                     , :MLT00038-PRCG-DTE                               04130001
                     , :ML090-DEPT-NBR                                  04140000
                     , :ML090-MAJ-CL-NBR                                04150000
                     , :ML090-SUB-CL-NBR                                04160000
                     , :ML090-LOC-NBR                                   04170000
                     , :ML090-ML-LO-MDSE-LVL-ID                         04180000
                     , :ML090-ORIG-ENT-ID                               04190000
                     , :ML090-ORIG-LBL-SEQ-NBR                          04200000
                     , :ML090-DUMMY-SKU-IND                             04210000
                     , :ML090-FINCL-PRC-GP-CDE                          04220000
                     , :ML090-SRC-SYS-ID                                04230000
                     , :ML090-TRN-TYP-CDE                               04240000
                     , :ML090-PRCHG-RSN-CDE                             04250000
                     , :ML090-PRCHG-CTG-CDE                             04260000
                     , :ML090-ENT-ID                                    04270000
                     , :ML090-LBL-SEQ-NBR                               04280000
                     , :ML090-PRCHG-RTL-AMT )                           04290000
           END-EXEC                                                     04300000
      *                                                                 04310000
           EVALUATE SQLCODE                                             04320000
               WHEN 0                                                   04330000
                   ADD 1 TO PA-INSERT-COUNT                             04340007
               WHEN -803                                                04341007
                   PERFORM 7100-UPDATE-MLT00038-ROW                     04342007
               WHEN -911                                                04350000
                   ADD +1             TO PV-DEADLOCK-COUNT              04360000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            04370000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04380000
                       MOVE '7200-INSERT-MLT00038-ROW' TO               04390000
                                                    PV-PARAGRAPH-NAME   04400000
                       PERFORM 9000-DEADLOCK-ERROR                      04410000
                   ELSE                                                 04420000
                       PERFORM 7999-RESTART-PROCESS                     04430000
                   END-IF                                               04440000
               WHEN OTHER                                               04450000
                   MOVE '7200-INSERT-MLT00038-ROW' TO PV-PARAGRAPH-NAME 04460000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED04470000
                   PERFORM 9999-SQL-ABEND                               04480000
           END-EVALUATE.                                                04490000
                                                                        04520000
      ******************************************************************04530000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *04540000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *04550000
      *            CONTROL TABLE                                       *04560000
      ******************************************************************04570000
       7300-GET-COMMIT-TIMESTAMP.                                       04580000
                                                                        04590000
           EXEC SQL                                                     04600000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        04610000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       04620000
               INTO :PV-COMMIT-TIMESTAMP                                04630000
               FROM TCKRSTCNTL                                          04640000
              WHERE JOB_NAME  = :PC-JOB-NAME                            04650000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        04660000
           END-EXEC.                                                    04670000
                                                                        04680000
           EVALUATE TRUE                                                04690000
               WHEN SQLCODE = 0                                         04700000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  04710000
                   CONTINUE                                             04720000
               WHEN SQLCODE NOT EQUAL ZERO                              04730000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME04740000
                   PERFORM 9999-SQL-ABEND                               04750000
           END-EVALUATE.                                                04760000
      *                                                                 04770000
      *                                                                 04780000
      ******************************************************************04790000
      * 7400-INIT-CHECKPOINT-SELECT                                    *04800000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *04810000
      *            IF WE ARE IN A RESTART CONDITION.                   *04820000
      ******************************************************************04830000
       7400-INIT-CHECKPOINT-SELECT.                                     04840000
                                                                        04850000
           EXEC SQL                                                     04860000
             SELECT  CKPT_STAT_CODE                                     04870000
                    ,CKPT_FRQNCY_QTY                                    04880000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         04890000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        04900000
               FROM  TCKRSTCNTL                                         04910000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           04920000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       04930000
           END-EXEC.                                                    04940000
                                                                        04950000
           IF SQLCODE = 0                                               04960000
               CONTINUE                                                 04970000
           ELSE                                                         04980000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  04990000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05000000
               PERFORM 9999-SQL-ABEND                                   05010000
           END-IF.                                                      05020000
      *                                                                 05030000
      *                                                                 05040000
      ******************************************************************05050000
      * 7500-SELECT-RESTART-INFO                                       *05060000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05070000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05080000
      ******************************************************************05090000
       7500-SELECT-RESTART-INFO.                                        05100000
                                                                        05110000
           EXEC SQL                                                     05120000
             SELECT  WORK_STRG_DESC                                     05130000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05140000
               FROM  TCKRSTINF                                          05150000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05160000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05170000
           END-EXEC.                                                    05180000
                                                                        05190000
           IF SQLCODE = 0                                               05200000
               CONTINUE                                                 05210000
           ELSE                                                         05220000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   05230000
               PERFORM 9999-SQL-ABEND                                   05240000
           END-IF.                                                      05250000
                                                                        05260000
      ******************************************************************05270000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *05280000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *05290000
      ******************************************************************05300000
       7600-SET-CURRENT-TIMESTAMP.                                      05310000
                                                                        05320000
           EXEC SQL                                                     05330000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           05340000
           END-EXEC.                                                    05350000
                                                                        05360000
           IF SQLCODE = 0                                               05370000
               CONTINUE                                                 05380000
           ELSE                                                         05390000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 05400000
               PERFORM 9999-SQL-ABEND                                   05410000
           END-IF.                                                      05420000
      *                                                                 05430000
      *                                                                 05440000
      ******************************************************************05450000
      * 7700-COMMIT-PROCESSING.                                        *05460000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *05470000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*05480000
      ******************************************************************05490000
       7700-COMMIT-PROCESSING.                                          05500000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     05510000
                                                                        05520000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          05530000
                                                                        05540000
      * PREPARE COMMIT RECORD FOR UPDATE                                05550000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                05560000
               MOVE ML090-FSCL-WK-END-DTE     TO CR-FSCL-WK-END-DTE     05570000
               MOVE ML090-PRCG-DTE            TO CR-PRCG-DTE            05580000
               MOVE ML090-DEPT-NBR            TO CR-DEPT-NBR            05590000
               MOVE ML090-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          05600000
               MOVE ML090-SUB-CL-NBR          TO CR-SUB-CL-NBR          05610000
               MOVE ML090-LOC-NBR             TO CR-LOC-NBR             05620000
               MOVE ML090-ML-LO-MDSE-LVL-ID   TO CR-ML-LO-MDSE-LVL-ID   05630000
               MOVE ML090-ORIG-ENT-ID         TO CR-ORIG-ENT-ID         05640000
               MOVE ML090-ORIG-LBL-SEQ-NBR    TO CR-ORIG-LBL-SEQ-NBR    05650000
               MOVE ML090-DUMMY-SKU-IND       TO CR-DUMMY-SKU-IND       05660000
               MOVE ML090-FINCL-PRC-GP-CDE    TO CR-FINCL-PRC-GP-CDE    05670000
               MOVE ML090-SRC-SYS-ID          TO CR-SRC-SYS-ID          05680000
               MOVE ML090-TRN-TYP-CDE         TO CR-TRN-TYP-CDE         05690000
               MOVE ML090-PRCHG-RSN-CDE       TO CR-PRCHG-RSN-CDE       05700000
               MOVE ML090-PRCHG-CTG-CDE       TO CR-PRCHG-CTG-CDE       05710000
               MOVE ML090-ENT-ID              TO CR-ENT-ID              05720000
               MOVE ML090-LBL-SEQ-NBR         TO CR-LBL-SEQ-NBR         05730000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    05740000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  05750000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       05760000
                                             TCKRSTINF-WORK-STRG-DESC   05770000
               IF PS-FIRST-COMMIT                                       05780000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                05790000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                05800000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       05810000
               END-IF                                                   05820000
               PERFORM 7800-COMMIT-CHANGES                              05830000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        05840000
           END-IF.                                                      05850000
      *                                                                 05860000
      *                                                                 05870000
      ******************************************************************05880000
      * 7800-COMMIT-CHANGES.                                            05890000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      05900000
      *            TAKE A COMMIT.                                       05910000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    05920000
      ******************************************************************05930000
       7800-COMMIT-CHANGES.                                             05940000
                                                                        05950000
           EXEC SQL                                                     05960000
             UPDATE TCKRSTINF                                           05970000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          05980000
              WHERE JOB_NAME       = :PC-JOB-NAME                       05990000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06000000
           END-EXEC.                                                    06010000
                                                                        06020000
           IF SQLCODE = 0                                               06030000
               CONTINUE                                                 06040000
           ELSE                                                         06050000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06060000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06070000
               PERFORM 9999-SQL-ABEND                                   06080000
           END-IF.                                                      06090000
                                                                        06100000
           EXEC SQL                                                     06110000
               COMMIT                                                   06120000
           END-EXEC.                                                    06130000
                                                                        06140000
           IF SQLCODE = 0                                               06150000
               ADD 1 TO PA-COMMIT-COUNT                                 06160000
           ELSE                                                         06170000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06180000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06190000
               PERFORM 9999-SQL-ABEND                                   06200000
           END-IF.                                                      06210000
      *                                                                 06220000
      ******************************************************************06230000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06240000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06250000
      ******************************************************************06260000
       7900-UPDATE-CHECKPOINT-STATUS.                                   06270000
                                                                        06280000
           EXEC SQL                                                     06290000
             UPDATE  TCKRSTCNTL                                         06300000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        06310000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06320000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06330000
           END-EXEC.                                                    06340000
                                                                        06350000
           IF SQLCODE = 0                                               06360000
               CONTINUE                                                 06370000
           ELSE                                                         06380000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME06390000
               PERFORM 9999-SQL-ABEND                                   06400000
           END-IF.                                                      06410000
                                                                        06420000
           EJECT                                                        06430000
      *                                                                 06440000
      *                                                                 06450000
      ******************************************************************06460000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST MLT_PRCHG_TRN_DTL     06470000
      * ROW FOR UPDATE.  FIND RESTART RECORD AND CKPT RESTART AT POINT  06480000
      * OF LAST COMMIT.                                                 06490000
      ******************************************************************06500000
       7999-RESTART-PROCESS.                                            06510000
      *                                                                 06520000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           06530000
      *                                                                 06540000
           CLOSE PRCHG-TRN-DTL-FILE                                     06550001
                 CNTL-DATE-FILE.                                        06551001
      *                                                                 06560000
           PERFORM 7500-SELECT-RESTART-INFO.                            06570000
      *                                                                 06580000
           DISPLAY '**** RESTART **** '.                                06590000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 06600000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   06610000
                                                                        06620000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      06630000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        06640000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       06650000
           IF PS-FIRST-COMMIT                                           06660000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:65)               06670000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     06680000
                      ' BEGINNING'                                      06690000
           ELSE                                                         06700000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           06710000
                        TCKRSTINF-WORK-STRG-DESC (1:65)                 06720000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 06730000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         06740000
                    CR-CHECKPOINT-RESTART-AREA                          06750000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                06760000
               DISPLAY 'FSCL WK END DTE ' CR-FSCL-WK-END-DTE            06770000
               DISPLAY 'PRCG DATE       ' CR-PRCG-DTE                   06780000
               MOVE CR-DEPT-NBR           TO PV-DISPLAY-FIELD           06790000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              06800000
               MOVE CR-MAJ-CL-NBR         TO PV-DISPLAY-FIELD           06810000
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              06820000
               MOVE CR-SUB-CL-NBR         TO PV-DISPLAY-FIELD           06830000
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              06840000
               MOVE CR-LOC-NBR            TO PV-DISPLAY-FIELD           06850000
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              06860000
               MOVE CR-ML-LO-MDSE-LVL-ID  TO PV-DISPLAY-FIELD           06870000
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              06880000
               MOVE CR-ORIG-ENT-ID        TO PV-DISPLAY-FIELD           06890000
               DISPLAY 'ORIG ENT ID     ' PV-DISPLAY-FIELD              06900000
               MOVE CR-ORIG-LBL-SEQ-NBR   TO PV-DISPLAY-FIELD           06910000
               DISPLAY 'ORIG LBL SEQ    ' PV-DISPLAY-FIELD              06920000
               DISPLAY 'DUMMY SKU IND   ' CR-DUMMY-SKU-IND              06930000
               DISPLAY 'FINCL PRC CDE   ' CR-FINCL-PRC-GP-CDE           06940000
               MOVE CR-SRC-SYS-ID         TO PV-DISPLAY-FIELD           06950000
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              06960000
               DISPLAY 'TRN TYP CDE     ' CR-TRN-TYP-CDE                06970000
               DISPLAY 'PRCHG RSN CDE   ' CR-PRCHG-RSN-CDE              06980000
               DISPLAY 'PRCHG CTG CDE   ' CR-PRCHG-CTG-CDE              06990000
               MOVE CR-ENT-ID             TO PV-DISPLAY-FIELD           07000000
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              07010000
               MOVE CR-LBL-SEQ-NBR        TO PV-DISPLAY-FIELD           07020000
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              07030000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07040000
           END-IF.                                                      07050000
                                                                        07060000
           OPEN INPUT PRCHG-TRN-DTL-FILE.                               07070000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07080000
      *                                                                 07090000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07100000
           PERFORM 4000-READ-PRCHG-DTL-FILE                             07110000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              07120000
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  07130000
                    PA-INPUT-COUNT.                                     07140000
           DISPLAY 'FSCL WK END DTE '     ML090-FSCL-WK-END-DTE.        07150000
           DISPLAY 'PRCG DATE       '     ML090-PRCG-DTE.               07160000
           MOVE ML090-DEPT-NBR          TO PV-DISPLAY-FIELD.            07170000
           DISPLAY 'DEPARTMENT NBR  '      PV-DISPLAY-FIELD.            07180000
           MOVE ML090-MAJ-CL-NBR        TO PV-DISPLAY-FIELD.            07190000
           DISPLAY 'MAJOR CLASS     '      PV-DISPLAY-FIELD.            07200000
           MOVE ML090-SUB-CL-NBR        TO PV-DISPLAY-FIELD.            07210000
           DISPLAY 'SUB CLASS       '      PV-DISPLAY-FIELD.            07220000
           MOVE ML090-LOC-NBR           TO PV-DISPLAY-FIELD.            07230000
           DISPLAY 'LOC NBR         '      PV-DISPLAY-FIELD.            07240000
           MOVE ML090-ML-LO-MDSE-LVL-ID TO PV-DISPLAY-FIELD.            07250000
           DISPLAY 'ML LWST ID      '      PV-DISPLAY-FIELD.            07260000
           MOVE ML090-ORIG-ENT-ID       TO PV-DISPLAY-FIELD.            07270000
           DISPLAY 'ORIG ENT ID     '      PV-DISPLAY-FIELD.            07280000
           MOVE ML090-ORIG-LBL-SEQ-NBR  TO PV-DISPLAY-FIELD.            07290000
           DISPLAY 'ORIG LBL SEQ    '      PV-DISPLAY-FIELD.            07300000
           DISPLAY 'DUMMY SKU IND   '      ML090-DUMMY-SKU-IND.         07310000
           DISPLAY 'FINCL PRC CDE   '      ML090-FINCL-PRC-GP-CDE.      07320000
           MOVE ML090-SRC-SYS-ID        TO PV-DISPLAY-FIELD.            07330000
           DISPLAY 'SRC SYS ID      '      PV-DISPLAY-FIELD.            07340000
           DISPLAY 'TRN TYP CDE     '      ML090-TRN-TYP-CDE.           07350000
           DISPLAY 'PRCHG RSN CDE   '      ML090-PRCHG-RSN-CDE.         07360000
           DISPLAY 'PRCHG CTG CDE   '      ML090-PRCHG-CTG-CDE.         07370000
           MOVE ML090-ENT-ID            TO PV-DISPLAY-FIELD.            07380000
           DISPLAY 'ENT-ID          '      PV-DISPLAY-FIELD.            07390000
           MOVE ML090-LBL-SEQ-NBR       TO PV-DISPLAY-FIELD.            07400000
           DISPLAY 'LBL SEQ NBR     '      PV-DISPLAY-FIELD.            07410000
      ******************************************************************07420000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07430000
      ******************************************************************07440000
       8000-EOJ-ROUTINE.                                                07450000
      *                                                                 07460000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07470000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07480000
                                                                        07490000
      *                                                                 07500000
           DISPLAY SPACES.                                              07510000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         07520000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        07530000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        07540000
           DISPLAY SPACES.                                              07550000
      *                                                                 07560000
           CLOSE PRCHG-TRN-DTL-FILE.                                    07570000
      *                                                                 07580000
      *                                                                 07590000
      ******************************************************************07600000
      *  PERFORMED BY 7100-UPDATE AND 7200-INSERT                       07610000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07620000
      ******************************************************************07630000
       9000-DEADLOCK-ERROR.                                             07640000
      *                                                                 07650000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     07660000
               PERFORM 9999-SQL-ABEND.                                  07670000
      *                                                                 07680000
      ******************************************************************07690000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07700000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07710000
      ******************************************************************07720000
       9999-SQL-ABEND.                                                  07730000
                                                                        07740000
           MOVE +4013                 TO ABEND-CODE.                    07750000
           DISPLAY '**  ABEND     **'.                                  07760000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              07770000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07780000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       07790000
                                                                        07800000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         07810000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        07820000
                                                                        07830000
           COPY DPPD004.                                                07840000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           07850000
