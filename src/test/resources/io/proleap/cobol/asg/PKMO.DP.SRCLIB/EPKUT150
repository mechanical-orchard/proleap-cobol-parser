       IDENTIFICATION DIVISION.                                         00010014
       PROGRAM-ID.    EPKUT150.                                         00020014
       AUTHOR.        TOM HANSON.                                       00030014
       DATE-WRITTEN.  JUNE, 2004.                                       00040014
                                                                        00050014
      *----------------------------------------------------------------*00060014
      *         REWARDS PARAMETER FILE HEADER RECORD CREATION           00070014
      *----------------------------------------------------------------*00080014
      *  OVERVIEW                                                       00090014
      *  - THIS PROGRAM WILL BE RESPONSIBLE FOR CREATING                00100014
      *    THE HEADER RECORDS WHICH ARE NEEDED FOR THE FILE             00110014
      *    DISTRIBUTION PROCESS.                                        00120014
      *----------------------------------------------------------------*00130014
      * HISTORY LOG:                                                    00140014
      *----------------------------------------------------------------*00150014
090204* DATE:        09/02/2004 *CHANGES DENOTED BY 090204              00160014
*     * WR/IR#:      P000693930                                         00170014
*     * PROGRAMMER:  THOMAS HANSON                                      00180014
*     * DESCRIPTION: EXTENDED FIXED LENGTH PORTION OF THE ACTIVATION    00190014
090204*              FILE BY 1 CHARACTER FOR THE MAX-RWD QTY FIELD.     00200014
      *----------------------------------------------------------------*00210014
      * DATE:        09/14/2005                                         00220014
      * WR/IR#:      WR28518                                            00230014
      * PROGRAMMER:  SCOTT BARTELT                                      00240014
      * DESCRIPTION: CHANGES FOR THE RETURN AUTOMATION PROJECT WHICH    00250014
      *              INCLUDE:                                           00260014
      *                - THE REWARD VALUE FIELD ON THE OUTPUT RECORD    00270014
      *                  IS BEING CHANGED FROM A 7 CHARACTER VARIABLE   00280014
      *                  LENGTH FIELD TO A 5 DIGIT NUMERIC FIELD.       00290014
      *                  PRIOR TO RETURN AUTOMATION THIS FIELD WAS      00300014
      *                  USED FOR DISPLAY PURPOSES ONLY.  NOW IT WILL   00310014
      *                  BE USED WITHIN A FORMULA TO CALCULATE THE      00320014
      *                  ACTIVATION AMOUNT.                             00330014
      *                - AN ADDITIONAL FIELD ADDED TO THE OUTPUT RECORD 00340014
      *                  WHICH WILL BE FOR A VARIABLE LENGTH RECEIPT    00350014
      *                  MESSAGE LINE.                                  00360014
      *----------------------------------------------------------------*00370015
      * DATE:        07/25/2016                                         00380023
      * WR/IR#:      CHG0146560                                         00390023
      * PROGRAMMER:  BASKARAN S                                         00400015
      * DESCRIPTION: CHANGED THE RECORD LENGTH OF THE ACTV FILE FROM    00410022
      *              196 TO 198 BYTES TO ACCOMMODATE VALIDITY PERIOD    00420022
      *              DAYS FROM ORACLE IN PROGRAM AUKUT015.              00430022
      *----------------------------------------------------------------*00440025
      * DATE:        07/26/2016                                         00450025
      * WR/IR#:      PROD FIX CHG0147492                                00460026
      * PROGRAMMER:  BASKARAN S                                         00470025
      * DESCRIPTION: CHANGED THE C-ACTV-RWD-FIXED-SIZE TO EXCLUDE       00480025
      *              5 DIGIT STORE. (4 DIGITS + 1 BYTE FOR ',').        00490025
      *              LENGTH REDUCED FROM 54 TO 49.                      00500025
      *----------------------------------------------------------------*00510014
       ENVIRONMENT DIVISION.                                            00520014
      *----------------------------------------------------------------*00530014
       CONFIGURATION SECTION.                                           00540014
       SOURCE-COMPUTER. IBM-3090.                                       00550014
       OBJECT-COMPUTER. IBM-3090.                                       00560014
                                                                        00570014
       INPUT-OUTPUT SECTION.                                            00580014
                                                                        00590014
       FILE-CONTROL.                                                    00600014
                                                                        00610014
           SELECT REWARD-ACTIVATION-FILE                                00620014
             ASSIGN TO INP01.                                           00630014
                                                                        00640014
           SELECT REWARD-REDEMPTION-FILE                                00650014
             ASSIGN TO INP02.                                           00660014
                                                                        00670014
           SELECT CTRL-CRD-CUR-BTCH-DTE                                 00680014
             ASSIGN TO INP03.                                           00690014
                                                                        00700014
           SELECT NW-RWD-ACTV-FILE-WITH-HEADERS                         00710014
             ASSIGN TO OUT01.                                           00720014
                                                                        00730014
           SELECT NW-RWD-REDM-FILE-WITH-HEADERS                         00740014
             ASSIGN TO OUT02.                                           00750014
                                                                        00760014
      *----------------------------------------------------------------*00770014
       DATA DIVISION.                                                   00780014
      *----------------------------------------------------------------*00790014
       FILE SECTION.                                                    00800014
                                                                        00810014
       FD  REWARD-ACTIVATION-FILE                                       00820014
           BLOCK CONTAINS 0 RECORDS                                     00830014
           LABEL RECORDS ARE STANDARD                                   00840014
           RECORDING MODE IS V                                          00850014
090204     RECORD IS VARYING FROM 1 TO 198                              00860016
              DEPENDING ON PV-RWD-ACTV-INPUT-REC-LENGTH                 00870014
           DATA RECORD IS REWARD-ACTIVATION-RECORD.                     00880014
                                                                        00890014
       01  REWARD-ACTIVATION-RECORD.                                    00900014
           05  RWD-ACTV-EXTRCT-BYTES       PIC  X(001)                  00910014
090204            OCCURS 1 TO 198 TIMES                                 00920016
                  DEPENDING ON PV-RWD-ACTV-INPUT-REC-LENGTH.            00930014
                                                                        00940014
       FD  REWARD-REDEMPTION-FILE                                       00950014
           BLOCK CONTAINS 0 RECORDS                                     00960014
           LABEL RECORDS ARE STANDARD                                   00970014
           RECORDING MODE IS V                                          00980014
           RECORD IS VARYING FROM 1 TO 66                               00990014
              DEPENDING ON PV-RWD-REDM-INPUT-REC-LENGTH                 01000014
           DATA RECORD IS REWARD-REDEMPTION-RECORD.                     01010014
                                                                        01020014
       01  REWARD-REDEMPTION-RECORD.                                    01030014
           05  RWD-REDM-EXTRCT-BYTES       PIC  X(001)                  01040014
                  OCCURS 1 TO 66 TIMES                                  01050014
                  DEPENDING ON PV-RWD-REDM-INPUT-REC-LENGTH.            01060014
                                                                        01070014
       FD  CTRL-CRD-CUR-BTCH-DTE                                        01080014
           RECORDING MODE IS F                                          01090014
           BLOCK CONTAINS 0 RECORDS                                     01100014
           LABEL RECORDS ARE STANDARD.                                  01110014
                                                                        01120014
       01  CC-DATE-RECORD   PIC X(80).                                  01130014
                                                                        01140014
       FD  NW-RWD-ACTV-FILE-WITH-HEADERS                                01150014
           RECORDING MODE IS V                                          01160014
           LABEL RECORDS ARE STANDARD                                   01170014
090204     RECORD IS VARYING FROM 1 TO 198                              01180016
           DEPENDING ON PV-RWD-ACTV-BYTE-COUNT                          01190014
           DATA RECORD IS RWD-ACTV-FILE-FORMATTED-RECORD.               01200014
                                                                        01210014
       01  NW-RWD-ACTV-FILE-FORMATTED-REC.                              01220014
           05 RWD-ACTV-FILE-BYTE           PIC X(01)                    01230014
090204                          OCCURS 1 TO 198 TIMES                   01240016
                                DEPENDING ON PV-RWD-ACTV-BYTE-COUNT.    01250014
                                                                        01260014
       FD  NW-RWD-REDM-FILE-WITH-HEADERS                                01270014
           RECORDING MODE IS V                                          01280014
           LABEL RECORDS ARE STANDARD                                   01290014
           RECORD IS VARYING FROM 1 TO 66                               01300014
           DEPENDING ON PV-RWD-REDM-BYTE-COUNT                          01310014
           DATA RECORD IS RWD-REDM-FILE-FORMATTED-RECORD.               01320014
                                                                        01330014
       01  NW-RWD-REDM-FILE-FORMATTED-REC.                              01340014
           05 RWD-REDM-FILE-BYTE           PIC X(01)                    01350014
                                    OCCURS 1 TO 66 TIMES                01360014
                                    DEPENDING ON PV-RWD-REDM-BYTE-COUNT.01370014
                                                                        01380014
      *----------------------------------------------------------------*01390014
       WORKING-STORAGE SECTION.                                         01400014
      *----------------------------------------------------------------*01410014
                                                                        01420014
      *----------------------------------------------------------------*01430014
      *  AC- ABEND CODES                                                01440014
      *----------------------------------------------------------------*01450014
       01  AC-ABEND-CODE                   PIC S9(004)  VALUE ZERO      01460014
                                                              COMP SYNC.01470014
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    01480014
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    01490014
                                                                        01500014
      *----------------------------------------------------------------*01510014
      *  PC- PROGRAM CONSTANTS                                          01520014
      *----------------------------------------------------------------*01530014
       01  PC-PROGRAM-CONSTANTS.                                        01540014
           05  PC-PROGRAM-NAME             PIC  X(008)                  01550014
                                                VALUE 'EPKUT150'.       01560014
           05  PC-REWARD-RECORD            PIC  X(007)  VALUE 'REWARD '.01570014
           05  PC-DISTHDR-RECORD           PIC  X(007)  VALUE 'DISTHDR'.01580014
           05  PC-HEAD-IND                 PIC  X(005)  VALUE '00HDR'.  01590014
           05  PC-STR-GP-DIST-NBR-EMP      PIC  X(015)                  01600014
                                                VALUE '010000000000000'.01610014
           05  PC-ACTV-RWD-FILE-NAME       PIC  X(011)                  01620014
                                                VALUE 'REWARD_ACTV'.    01630014
           05  PC-REDM-RWD-FILE-NAME       PIC  X(011)                  01640014
                                                VALUE 'REWARD_REDM'.    01650014
           05  PC-STORE-NBR-EMPTY          PIC  X(005)  VALUE '00000'.  01660014
           05  PC-DISTHDR-LNGTH-REWARD     PIC  9(003)  VALUE 060.      01670014
           05  PC-NEW-DISTHDR-LNGTH-REWARD PIC  9(003)  VALUE 064.      01680014
           05  PC-STR-GRP-ID-5000          PIC  9(005)  VALUE 05000.    01690014
           05  PC-STR-GRP-ID-00000         PIC  9(005)  VALUE 00000.    01700014
           05  PC-FIELD-TERM               PIC  X(001)  VALUE '_'.      01710014
090204     05  PC-ACTV-RWD-MX-SIZE         PIC S9(003)  VALUE 198.      01720017
           05  PC-REDM-RWD-MX-SIZE         PIC S9(003)  VALUE 66.       01730014
092305     05  PC-ACTV-RWD-FIXED-SIZE      PIC  9(002)  VALUE 49.       01740024
           05  PC-REDM-RWD-FIXED-SIZE      PIC S9(002)  VALUE 36.       01750014
                                                                        01760014
      *----------------------------------------------------------------*01770014
      *  PC- TODAYS DATE                                                01780014
      *----------------------------------------------------------------*01790014
       01  PC-TODAYS-DATE.                                              01800014
           05  PC-TODAY-CCYY               PIC  9(004)  VALUE 0000.     01810014
           05  PC-TODAY-FILL1              PIC  X(001)  VALUE SPACE.    01820014
           05  PC-TODAY-MM                 PIC  9(002)  VALUE 00.       01830014
           05  PC-TODAY-FILL2              PIC  X(001)  VALUE SPACE.    01840014
           05  PC-TODAY-DD                 PIC  9(002)  VALUE 00.       01850014
                                                                        01860014
       01  PC-TODAYS-DATE-FRM.                                          01870014
           05  PC-TODAY-FRM-CCYY           PIC  9(004)  VALUE 0000.     01880014
           05  PC-TODAY-FRM-MM             PIC  9(002)  VALUE 00.       01890014
           05  PC-TODAY-FRM-DD             PIC  9(002)  VALUE 00.       01900014
                                                                        01910014
       01  PC-TODAYS-DATE-NUMB             PIC  9(008)  VALUE 00000000. 01920014
                                                                        01930014
      *----------------------------------------------------------------*01940014
      *  PC- TIME                                                       01950014
      *----------------------------------------------------------------*01960014
       01  PC-TIME.                                                     01970014
           05  PC-TIME-HHMM                PIC  X(004).                 01980014
           05  PC-TIME-SS                  PIC  X(002).                 01990014
                                                                        02000014
      *----------------------------------------------------------------*02010014
      *  PS- PROGRAM SWITCHES                                           02020014
      *----------------------------------------------------------------*02030014
       01  PS-PROGRAM-SWITCHES.                                         02040014
           05  PS-END-OF-REWARD-ACTV-FILE-SW     PIC  X(001)  VALUE 'N'.02050014
               88  PS-END-OF-REWARD-ACTV-FILE                 VALUE 'Y'.02060014
               88  PS-NOT-END-OF-REWARD-ACTV-FILE             VALUE 'N'.02070014
                                                                        02080014
           05  PS-END-OF-REWARD-REDM-FILE-SW     PIC  X(001)  VALUE 'N'.02090014
               88  PS-END-OF-REWARD-REDM-FILE                 VALUE 'Y'.02100014
               88  PS-NOT-END-OF-REWARD-REDM-FILE             VALUE 'N'.02110014
                                                                        02120014
           05  PS-LINE-END-FOUND-SW              PIC  X(001)  VALUE 'N'.02130014
               88  PS-LINE-END-FOUND                          VALUE 'Y'.02140014
               88  PS-LINE-END-NOT-FOUND                      VALUE 'N'.02150014
                                                                        02160014
           05  PS-END-OF-DATE-SW                 PIC  X(001)  VALUE 'N'.02170014
               88  PS-END-OF-DATE-FILE                        VALUE 'Y'.02180014
                                                                        02190014
      *----------------------------------------------------------------*02200014
      *  PV- PROGRAM VARIABLES                                          02210014
      *----------------------------------------------------------------*02220014
       01 PV-PROGRAM-VARIABLES.                                         02230014
          05  PV-PARAGRAPH-NAME            PIC  X(030)  VALUE SPACES.   02240014
          05  PV-OPERATION-MSG-1           PIC  X(040)  VALUE SPACES.   02250014
          05  PV-OPERATION-MSG-2           PIC  X(040)  VALUE SPACES.   02260014
          05  PV-BYTE-COUNT-REWARD         PIC  9(004)  VALUE ZERO.     02270014
          05  PV-RWD-ACTV-BYTE-COUNT       PIC  9(004)  VALUE ZERO.     02280014
          05  PV-RWD-REDM-BYTE-COUNT       PIC  9(004)  VALUE ZERO.     02290014
          05  PV-IN-RECORD-LENGTH-REWARD   PIC  9(004)  VALUE ZERO.     02300014
          05  PV-RWD-ACTV-INPUT-REC-LENGTH PIC  9(004)  VALUE ZERO.     02310014
          05  PV-RWD-REDM-INPUT-REC-LENGTH PIC  9(004)  VALUE ZERO.     02320014
          05  PV-WRITE-REC-LNGTH           PIC  9(003)  VALUE ZERO.     02330014
          05  PV-ABEND-CODE                PIC  9(004)  VALUE ZEROS     02340014
                                                              COMP.     02350014
       01  PV-STORE-CTRL-BREAK.                                         02360014
           05  PV-CTRL-BRK-STR-LOC         PIC  9(004)  VALUE ZERO.     02370014
                                                                        02380014
       01  PV-CURR-TIME.                                                02390014
           05  PV-CURR-TIME-HH             PIC  9(002)  VALUE ZERO.     02400014
           05  PV-CURR-TIME-MM             PIC  9(002)  VALUE ZERO.     02410014
           05  PV-CURR-TIME-SS             PIC  9(002)  VALUE ZERO.     02420014
           05  PV-CURR-TIME-SSHH           PIC  9(002)  VALUE ZERO.     02430014
                                                                        02440014
       01  PV-TIME-CALC                    PIC  9(006).                 02450014
       01  PV-TIME-CALC-HHMM               PIC  9(004).                 02460014
       01  PV-TIME.                                                     02470014
           05  PV-TIME-HH                  PIC  9(002).                 02480014
           05  PV-TIME-MM                  PIC  9(002).                 02490014
           05  PV-TIME-SS                  PIC  9(002).                 02500014
                                                                        02510014
       01  PV-TIME-HHMM.                                                02520014
           05  PV-TIME-HHMM-HH             PIC  9(002).                 02530014
           05  PV-TIME-HHMM-MM             PIC  9(002).                 02540014
                                                                        02550014
       01  PV-STR-GP-DIST-NBR.                                          02560014
           05  PV-STR-GRP-TYP              PIC  9(005).                 02570014
           05  PV-STR-GRP-ID.                                           02580014
               10  PV-STR-GRP-ID-ZEROES    PIC  9(005)  VALUE ZERO.     02590014
               10  PV-STR-GRP-ID-STR-LOC   PIC  9(005)  VALUE ZERO.     02600014
                                                                        02610014
       01  PV-RWD-ACTV-INPUT-LAYOUT.                                    02620014
           05 FILLER                       PIC  X(005).                 02630014
           05 PV-RWD-ACTV-RECORD           PIC  X(193)  VALUE SPACES.   02640018
                                                                        02650014
       01  PV-RWD-REDM-INPUT-LAYOUT.                                    02660014
           05 FILLER                       PIC  X(005).                 02670014
           05 PV-RWD-REDM-RECORD           PIC  X(061)  VALUE SPACES.   02680014
                                                                        02690014
      *----------------------------------------------------------------*02700014
      *  PV- PROGRAM COUNTERS                                           02710014
      *----------------------------------------------------------------*02720014
       01 PV-PROGRAM-COUNTERS.                                          02730014
          05  PV-RWD-ACTV-RECS-READ        PIC  9(009)  VALUE 0.        02740014
          05  PV-RWD-ACTV-DUPS-RMVD        PIC  9(009)  VALUE 0.        02750014
          05  PV-RWD-ACTV-RECS-WRITTEN     PIC  9(009)  VALUE 0.        02760014
          05  PV-RWD-REDM-RECS-READ        PIC  9(009)  VALUE 0.        02770014
          05  PV-RWD-REDM-DUPS-RMVD        PIC  9(009)  VALUE 0.        02780014
          05  PV-RWD-REDM-RECS-WRITTEN     PIC  9(009)  VALUE 0.        02790014
                                                                        02800014
      *----------------------------------------------------------------*02810014
      *  PREVIOUS ACTIVATION RECORD PROCESSED                           02820014
      *----------------------------------------------------------------*02830014
       01  PV-PR-RWD-ISUD-EXTRCT-REC.                                   02840014
         05  PV-PR-RWD-ISUD-EXTR-FIXED.                                 02850014
             10  PV-PR-RWD-ISUD-STR-LOC        PIC  9(004).             02860014
             10  FILLER                        PIC  X(001)  VALUE ','.  02870014
             10  PV-PR-RWD-ISUD-EVNT-ID        PIC  X(005).             02880014
             10  FILLER                        PIC  X(001)  VALUE ','.  02890014
             10  PV-PR-RWD-ISUD-BEG-DTE        PIC  X(008).             02900014
             10  FILLER                        PIC  X(001)  VALUE ','.  02910014
             10  PV-PR-RWD-ISUD-END-DTE        PIC  X(008).             02920014
             10  FILLER                        PIC  X(001)  VALUE ','.  02930014
             10  PV-PR-RWD-ISUD-TNDR-TYP-CDE   PIC  9(003).             02940017
             10  FILLER                        PIC  X(001)  VALUE ','.  02950014
             10  PV-PR-RWD-ISUD-QUALD-PRCH-AMT PIC  9(3)V9(02).         02960014
             10  FILLER                        PIC  X(001)  VALUE ','.  02970014
             10  PV-PR-RWD-ISUD-QUALD-TOL-AMT  PIC  9(2)V9(02).         02980014
             10  FILLER                        PIC  X(001)  VALUE ','.  02990014
090204       10  PV-PR-RWD-ISUD-MAX-RWD-QTY    PIC  9(003).             03000014
             10  FILLER                        PIC  X(001)  VALUE ','.  03010014
             10  PV-PR-RWD-ISUD-VALUE-AMT      PIC  9(3)V9(02).         03020014
             10  FILLER                        PIC  X(001)  VALUE ','.  03030014
         05  PV-PR-RWD-ISUD-EXTR-VAR.                                   03040014
             10  PV-PR-RWD-ISUD-EXTR-VAR-TABLE PIC  X(01) VALUE SPACES  03050014
                                               OCCURS 144 TIMES         03060014
                                               INDEXED BY PV-PR-ACT-IDX.03070014
                                                                        03080014
      *----------------------------------------------------------------*03090014
      *  PREVIOUS REDEMPTION RECORD PROCESSED                           03100014
      *----------------------------------------------------------------*03110014
       01  PV-PR-RWD-REDM-EXTRCT-REC.                                   03120014
         05  PV-PR-RWD-REDM-EXTR-FIXED.                                 03130014
             10  PV-PR-RWD-REDM-STR-LOC        PIC  9(004).             03140014
             10  FILLER                        PIC  X(001)  VALUE ','.  03150014
             10  PV-PR-RWD-REDM-EVNT-ID        PIC  X(005).             03160014
             10  FILLER                        PIC  X(001)  VALUE ','.  03170014
             10  PV-PR-RWD-REDM-BEG-DTE        PIC  X(008).             03180014
             10  FILLER                        PIC  X(001)  VALUE ','.  03190014
             10  PV-PR-RWD-REDM-END-DTE        PIC  X(008).             03200014
             10  FILLER                        PIC  X(001)  VALUE ','.  03210014
             10  PV-PR-RWD-REDM-AUTH-TYP-CDE   PIC  9(001).             03220017
             10  FILLER                        PIC  X(001)  VALUE ','.  03230014
             10  PV-PR-RWD-REDM-DISC-TYP-CDE   PIC  X(001).             03240014
             10  FILLER                        PIC  X(001)  VALUE ','.  03250014
             10  PV-PR-RWD-REDM-DISC-AMT       PIC  9(3)V9(02).         03260014
             10  FILLER                        PIC  X(001)  VALUE ','.  03270014
             10  PV-PR-RWD-REDM-I-E-TYP-CDE    PIC  9(001).             03280014
             10  FILLER                        PIC  X(001)  VALUE ','.  03290014
         05  PV-PR-RWD-REDM-EXTR-VAR.                                   03300014
             10  PV-PR-RWD-REDM-EXTR-VAR-TABLE PIC  X(1)  VALUE SPACES  03310014
                                               OCCURS 25 TIMES          03320014
                                               INDEXED BY PV-PR-RDM-IDX.03330014
                                                                        03340014
      *----------------------------------------------------------------*03350014
      *  ACTIVATION REWARD RECORD LAYOUT                                03360014
      *----------------------------------------------------------------*03370014
           COPY EPRD139.                                                03380014
                                                                        03390014
      *----------------------------------------------------------------*03400014
      *  REDEMPTION REWARD RECORD LAYOUT                                03410014
      *----------------------------------------------------------------*03420014
           COPY EPRD140.                                                03430014
                                                                        03440014
      *----------------------------------------------------------------*03450014
      *  NEW DISTRIBUTION HEADER INPUT/OUTPUT RECORD LAYOUT             03460014
      *----------------------------------------------------------------*03470014
           COPY EPRD217.                                                03480014
                                                                        03490014
      *----------------------------------------------------------------*03500014
      *  DATE - BASED ON DAY THE JOB WAS SCHEDULED TO RUN               03510014
      *----------------------------------------------------------------*03520014
           COPY EPRD111.                                                03530014
                                                                        03540014
      *----------------------------------------------------------------*03550014
      *  DATE ROUTINE COPY MEMBERS                                      03560014
      *----------------------------------------------------------------*03570014
           COPY DPWS500.                                                03580014
                                                                        03590014
      ******************************************************************03600014
       PROCEDURE DIVISION.                                              03610014
      ******************************************************************03620014
       0000-MAINLINE.                                                   03630014
                                                                        03640014
           PERFORM 1000-INITIALIZATION.                                 03650014
                                                                        03660014
           PERFORM 2000-PROCESS-RWD-ACTV-RECORDS                        03670014
                   UNTIL PS-END-OF-REWARD-ACTV-FILE                     03680014
           PERFORM 3000-PROCESS-RWD-REDM-RECORDS                        03690014
                   UNTIL PS-END-OF-REWARD-REDM-FILE                     03700014
                                                                        03710014
           PERFORM 9000-WRAPUP.                                         03720014
                                                                        03730014
           GOBACK.                                                      03740014
                                                                        03750014
      *0000-EXIT.                                                       03760014
       EJECT.                                                           03770014
                                                                        03780014
      ******************************************************************03790014
      *  1000-INITIALIZATION.                                           03800014
      *  - INITIALIZES VARIABLES, OPENS ANY FILES, AND PERFORMS         03810014
      *    THE INITIAL FILE READS TO DETERMINE IF THE FILES ARE EMPTY.  03820014
      ******************************************************************03830014
       1000-INITIALIZATION.                                             03840014
                                                                        03850014
           DISPLAY '*---------------------------------------*'.         03860014
           DISPLAY '*   EPKUT150                            *'.         03870014
           DISPLAY '*     REWARDS ACTIVATION & REDEMPTION   *'.         03880014
           DISPLAY '*     HEADER RECORD FORMATTER           *'.         03890014
           DISPLAY '*---------------------------------------*'.         03900014
           DISPLAY ' '.                                                 03910014
                                                                        03920014
           OPEN INPUT  REWARD-ACTIVATION-FILE                           03930014
                       REWARD-REDEMPTION-FILE                           03940014
                       CTRL-CRD-CUR-BTCH-DTE.                           03950014
                                                                        03960014
           OPEN OUTPUT NW-RWD-ACTV-FILE-WITH-HEADERS                    03970014
                       NW-RWD-REDM-FILE-WITH-HEADERS.                   03980014
                                                                        03990014
           PERFORM 1100-PROCESS-DATE-CC.                                04000014
           PERFORM 1200-PROCESS-TIME.                                   04010014
                                                                        04020014
           MOVE ZERO TO PV-CTRL-BRK-STR-LOC.                            04030014
           SET PS-NOT-END-OF-REWARD-ACTV-FILE TO TRUE.                  04040014
           SET PS-NOT-END-OF-REWARD-REDM-FILE TO TRUE.                  04050014
                                                                        04060014
      *    //FIRST READ PROCESSING FOR THE REWARD ACTIVATION FILE       04070014
           PERFORM 4000-READ-RWD-ACTV-FILE.                             04080014
                                                                        04090014
      *    //FIRST READ PROCESSING FOR THE REWARD REDEMPTION FILE       04100014
           PERFORM 5000-READ-RWD-REDM-FILE.                             04110014
                                                                        04120014
                                                                        04130014
      *1000-EXIT.                                                       04140014
       EJECT.                                                           04150014
                                                                        04160014
      ******************************************************************04170014
      *  1100-PROCESS-DATE-CC                                           04180014
      ******************************************************************04190014
       1100-PROCESS-DATE-CC.                                            04200014
                                                                        04210014
           READ CTRL-CRD-CUR-BTCH-DTE INTO EP111-CC-CONTROL-CARD        04220014
               AT END                                                   04230014
                   SET PS-END-OF-DATE-FILE TO TRUE.                     04240014
                                                                        04250014
           IF PS-END-OF-DATE-FILE                                       04260014
               MOVE '1100-PROCESS-DATE-CC '  TO PV-PARAGRAPH-NAME       04270014
               MOVE 'EMPTY DATE CC        '  TO PV-OPERATION-MSG-1      04280014
               SET AC-CONTROL-CARD-ERROR     TO TRUE                    04290014
               PERFORM 9999-ABEND                                       04300014
           END-IF.                                                      04310014
                                                                        04320014
      *    //CONVERT FORMAT OF DATE FROM CCYY-MM-DD TO CCYYMMDD         04330014
           MOVE EP111-CC-CRNT-DTE            TO PC-TODAYS-DATE.         04340014
           MOVE PC-TODAY-CCYY                TO PC-TODAY-FRM-CCYY.      04350014
           MOVE PC-TODAY-MM                  TO PC-TODAY-FRM-MM.        04360014
           MOVE PC-TODAY-DD                  TO PC-TODAY-FRM-DD.        04370014
           MOVE PC-TODAYS-DATE-FRM           TO PC-TODAYS-DATE-NUMB.    04380014
                                                                        04390014
      *1100-EXIT.                                                       04400014
       EJECT                                                            04410014
                                                                        04420014
      ******************************************************************04430014
      *  1200-PROCESS-TIME                                              04440014
      ******************************************************************04450014
       1200-PROCESS-TIME.                                               04460014
                                                                        04470014
           ACCEPT PV-CURR-TIME FROM TIME.                               04480014
                                                                        04490014
           MOVE PV-CURR-TIME-HH TO PV-TIME-HH.                          04500014
           MOVE PV-CURR-TIME-MM TO PV-TIME-MM.                          04510014
           MOVE PV-CURR-TIME-SS TO PV-TIME-SS.                          04520014
                                                                        04530014
           MOVE PV-TIME TO PV-TIME-CALC.                                04540014
                                                                        04550014
      *1200-EXIT.                                                       04560014
       EJECT                                                            04570014
                                                                        04580014
      ******************************************************************04590014
      *  2000-PROCESS-RWD-ACTV-RECORDS.                                 04600014
      *  - READS IN REWARD ACTIVATION RECORDS AND INSERTS HEADER        04610014
      *    RECORDS WHEN REQUIRED.                                       04620014
      ******************************************************************04630014
       2000-PROCESS-RWD-ACTV-RECORDS.                                   04640014
                                                                        04650014
      *    //CHECK CONTROL BREAK, PRINT HEADER IF REQUIRED              04660014
           PERFORM 2100-CHECK-IF-ACTV-HDRS-NEEDED.                      04670014
                                                                        04680021
      *    //RE-WRITE ACTIVATION DETAIL RECORD OUT TO FILE WITH HDRS    04690014
           PERFORM 7200-WRITE-REWARD-ACTV-RECORD.                       04700014
                                                                        04710014
      *    //READ ANOTHER RECORD FROM THE REWARD ACTIVATION FILE        04720014
           PERFORM 4000-READ-RWD-ACTV-FILE.                             04730014
                                                                        04740014
      *2000-EXIT.                                                       04750014
       EJECT.                                                           04760014
                                                                        04770014
      ******************************************************************04780014
      *  2100-CHECK-IF-ACTV-HDRS-NEEDED                                 04790014
      *  - DETERMINE IF A HEADER RECORD NEEDS TO BE WRITTEN.            04800014
      ******************************************************************04810014
       2100-CHECK-IF-ACTV-HDRS-NEEDED.                                  04820014
                                                                        04830014
           IF EP139-RWD-ISUD-STR-LOC NOT EQUAL TO PV-CTRL-BRK-STR-LOC   04840014
                                                                        04850014
      *      //DISTRIBUTION HEADER RECORD                               04860014
             PERFORM 2110-FILL-RWD-ACTV-DIST-HDRS                       04870014
             PERFORM 7000-WRITE-ACTV-DISTHDR-RECORD                     04880014
                                                                        04890014
           END-IF.                                                      04900014
                                                                        04910014
           MOVE EP139-RWD-ISUD-STR-LOC TO PV-CTRL-BRK-STR-LOC.          04920014
                                                                        04930014
      *2100-EXIT.                                                       04940014
       EJECT.                                                           04950014
                                                                        04960014
      ******************************************************************04970014
      *  2110-FILL-RWD-ACTV-DIST-HDRS                                   04980014
      ******************************************************************04990014
       2110-FILL-RWD-ACTV-DIST-HDRS.                                    05000014
                                                                        05010014
                                                                        05020014
      *     //STORE GROUP DISTRIBUTION NUMBER                           05030014
            MOVE 'N'                        TO EP217-FILE-TYPE-APPLY.   05040014
            MOVE 'FUL'                      TO EP217-FILE-NAME-TYPE.    05050014
            MOVE ZERO                       TO EP217-EXCO-STR-NBR.      05060014
            SET EP217-XST-PS                TO TRUE.                    05070014
            MOVE '_'                        TO EP217-UNDERSCORE-1       05080014
                                               EP217-UNDERSCORE-2       05090014
                                               EP217-UNDERSCORE-3       05100014
                                               EP217-UNDERSCORE-4       05110014
                                               EP217-UNDERSCORE-5.      05120014
            MOVE PC-STR-GRP-ID-5000         TO PV-STR-GRP-TYP.          05130014
            MOVE ZERO                       TO PV-STR-GRP-ID-ZEROES.    05140014
            MOVE EP139-RWD-ISUD-STR-LOC     TO PV-STR-GRP-ID-STR-LOC.   05150014
            MOVE PV-STR-GP-DIST-NBR         TO EP217-STR-GRP-DIST-NBR.  05160014
                                                                        05170014
      *     //FILE DISTRIBUTION DATE                                    05180014
            MOVE PC-TODAYS-DATE-NUMB        TO EP217-FILE-DIST-DATE.    05190014
                                                                        05200014
      *     //FILE NAME                                                 05210014
            SET EP217-FILE-ACT TO TRUE.                                 05220014
                                                                        05230014
      *     //STORE NUMBER                                              05240014
            MOVE PV-STR-GRP-ID-STR-LOC      TO EP217-STORE-NBR.         05250014
                                                                        05260014
      *     //BATCH DATE                                                05270014
            MOVE PC-TODAYS-DATE-NUMB        TO EP217-CRT-TIMESTAMP(1:8).05280014
                                                                        05290014
      *     //CURRENT TIME                                              05300014
            MOVE PV-TIME-CALC               TO EP217-CRT-TIMESTAMP(9:6).05310014
            PERFORM 6000-CALC-TIME.                                     05320014
                                                                        05330014
                                                                        05340014
      *2110-EXIT.                                                       05350014
       EJECT.                                                           05360014
                                                                        05370014
      ******************************************************************05380014
      *  3000-PROCESS-RWD-REDM-RECORDS.                                 05390014
      *  - READS IN REWARD REDEMPTION RECORDS AND INSERTS HEADER        05400014
      *    RECORDS WHEN REQUIRED.                                       05410014
      ******************************************************************05420014
       3000-PROCESS-RWD-REDM-RECORDS.                                   05430014
                                                                        05440014
      *    //CHECK CONTROL BREAK, PRINT HEADER IF REQUIRED              05450014
           PERFORM 3100-CHECK-IF-REDM-HDRS-NEEDED.                      05460014
                                                                        05470014
      *    //RE-WRITE DETAIL RECORD OUT TO FILE WITH HEADERS            05480014
           PERFORM 7300-WRITE-REWARD-REDM-RECORD.                       05490014
                                                                        05500014
      *    //READ ANOTHER RECORD FROM THE REWARD REDEMPTION FILE        05510014
           PERFORM 5000-READ-RWD-REDM-FILE.                             05520014
                                                                        05530014
      *3000-EXIT.                                                       05540014
       EJECT.                                                           05550014
                                                                        05560014
      ******************************************************************05570014
      *  3100-CHECK-IF-REDM-HDRS-NEEDED                                 05580014
      *  - DETERMINE IF A HEADER RECORD NEEDS TO BE WRITTEN.            05590014
      ******************************************************************05600014
       3100-CHECK-IF-REDM-HDRS-NEEDED.                                  05610014
                                                                        05620014
           IF EP140-RWD-REDM-STR-LOC NOT EQUAL TO PV-CTRL-BRK-STR-LOC   05630014
                                                                        05640014
             PERFORM 3110-FILL-RWD-REDM-DIST-HDRS                       05650014
             PERFORM 7100-WRITE-REDM-DISTHDR-RECORD                     05660014
                                                                        05670014
           END-IF.                                                      05680014
                                                                        05690014
           MOVE EP140-RWD-REDM-STR-LOC TO PV-CTRL-BRK-STR-LOC.          05700014
                                                                        05710014
      *3100-EXIT.                                                       05720014
       EJECT.                                                           05730014
                                                                        05740014
      ******************************************************************05750014
      *  3110-FILL-RWD-REDM-DIST-HDRS                                   05760014
      ******************************************************************05770014
       3110-FILL-RWD-REDM-DIST-HDRS.                                    05780014
                                                                        05790014
      *     //HEADING INDICATOR                                         05800014
            MOVE 'N'                        TO EP217-FILE-TYPE-APPLY.   05810014
            MOVE 'FUL'                      TO EP217-FILE-NAME-TYPE.    05820014
            MOVE ZERO                       TO EP217-EXCO-STR-NBR.      05830014
            SET EP217-XST-PS                TO TRUE.                    05840014
            MOVE '_'                        TO EP217-UNDERSCORE-1       05850014
                                               EP217-UNDERSCORE-2       05860014
                                               EP217-UNDERSCORE-3       05870014
                                               EP217-UNDERSCORE-4       05880014
                                               EP217-UNDERSCORE-5.      05890014
                                                                        05900014
      *     //STORE GROUP DISTRIBUTION NUMBER                           05910014
            MOVE PC-STR-GRP-ID-5000         TO PV-STR-GRP-TYP.          05920014
            MOVE ZERO                       TO PV-STR-GRP-ID-ZEROES.    05930014
            MOVE EP140-RWD-REDM-STR-LOC     TO PV-STR-GRP-ID-STR-LOC.   05940014
            MOVE PV-STR-GP-DIST-NBR         TO EP217-STR-GRP-DIST-NBR.  05950014
                                                                        05960014
      *     //FILE DISTRIBUTION DATE                                    05970014
            MOVE PC-TODAYS-DATE-NUMB        TO EP217-FILE-DIST-DATE.    05980014
                                                                        05990014
      *     //FILE NAME                                                 06000014
            SET EP217-FILE-RED TO TRUE.                                 06010014
                                                                        06020014
      *     //STORE NUMBER                                              06030014
            MOVE PV-STR-GRP-ID-STR-LOC      TO EP217-STORE-NBR.         06040014
                                                                        06050014
      *     //BATCH DATE                                                06060014
            MOVE PC-TODAYS-DATE-NUMB        TO EP217-CRT-TIMESTAMP(1:8).06070014
                                                                        06080014
      *     //CURRENT TIME                                              06090014
            MOVE PV-TIME-CALC               TO EP217-CRT-TIMESTAMP(9:6).06100014
            PERFORM 6000-CALC-TIME.                                     06110014
                                                                        06120014
      *3110-EXIT.                                                       06130014
       EJECT.                                                           06140014
                                                                        06150014
      ******************************************************************06160014
      *  4000-READ-RWD-ACTV-FILE                                        06170014
      *  - READS INPUT REWARD FILE.                                     06180014
      ******************************************************************06190014
       4000-READ-RWD-ACTV-FILE.                                         06200014
                                                                        06210014
           PERFORM UNTIL (PS-END-OF-REWARD-ACTV-FILE)                   06220014
            OR (EP139-RWD-ISUD-EXTRCT-REC NOT EQUAL TO                  06230014
                PV-PR-RWD-ISUD-EXTRCT-REC)                              06240014
                                                                        06250014
               MOVE PC-ACTV-RWD-MX-SIZE TO PV-RWD-ACTV-INPUT-REC-LENGTH 06260014
                                                                        06270014
               READ REWARD-ACTIVATION-FILE                              06280014
                   INTO EP139-RWD-ISUD-EXTRCT-REC                       06290014
                   AT END                                               06300014
                       SET PS-END-OF-REWARD-ACTV-FILE TO TRUE           06310014
               END-READ                                                 06320014
                                                                        06330014
               IF PS-NOT-END-OF-REWARD-ACTV-FILE                        06340014
                  ADD 1 TO PV-RWD-ACTV-RECS-READ                        06350014
                                                                        06360014
                  IF EP139-RWD-ISUD-EXTRCT-REC EQUAL TO                 06370014
                     PV-PR-RWD-ISUD-EXTRCT-REC                          06380014
                     ADD 1 TO PV-RWD-ACTV-DUPS-RMVD                     06390014
                  END-IF                                                06400014
                                                                        06410014
               END-IF                                                   06420014
                                                                        06430014
           END-PERFORM.                                                 06440014
                                                                        06450014
           IF PS-NOT-END-OF-REWARD-ACTV-FILE                            06460014
             MOVE EP139-RWD-ISUD-EXTRCT-REC TO PV-PR-RWD-ISUD-EXTRCT-REC06470014
           ELSE                                                         06480014
             MOVE ZERO TO PV-CTRL-BRK-STR-LOC                           06490014
           END-IF.                                                      06500014
                                                                        06510014
      *4000-EXIT.                                                       06520014
       EJECT.                                                           06530014
                                                                        06540014
      ******************************************************************06550014
      *  5000-READ-RWD-REDM-FILE                                        06560014
      *  - READS INPUT REWARD FILE.                                     06570014
      ******************************************************************06580014
       5000-READ-RWD-REDM-FILE.                                         06590014
                                                                        06600014
           PERFORM UNTIL (PS-END-OF-REWARD-REDM-FILE)                   06610014
            OR (EP140-RWD-REDM-EXTRCT-REC NOT EQUAL TO                  06620014
                PV-PR-RWD-REDM-EXTRCT-REC)                              06630014
                                                                        06640014
               MOVE PC-REDM-RWD-MX-SIZE TO PV-RWD-REDM-INPUT-REC-LENGTH 06650014
                                                                        06660014
               READ REWARD-REDEMPTION-FILE                              06670014
                   INTO EP140-RWD-REDM-EXTRCT-REC                       06680014
                   AT END                                               06690014
                       SET PS-END-OF-REWARD-REDM-FILE TO TRUE           06700014
               END-READ                                                 06710014
                                                                        06720014
               IF PS-NOT-END-OF-REWARD-REDM-FILE                        06730014
                  ADD 1 TO PV-RWD-REDM-RECS-READ                        06740014
                                                                        06750014
                  IF EP140-RWD-REDM-EXTRCT-REC EQUAL TO                 06760014
                     PV-PR-RWD-REDM-EXTRCT-REC                          06770014
                     ADD 1 TO PV-RWD-REDM-DUPS-RMVD                     06780014
                  END-IF                                                06790014
                                                                        06800014
               END-IF                                                   06810014
                                                                        06820014
           END-PERFORM.                                                 06830014
                                                                        06840014
           IF PS-NOT-END-OF-REWARD-REDM-FILE                            06850014
             MOVE EP140-RWD-REDM-EXTRCT-REC TO PV-PR-RWD-REDM-EXTRCT-REC06860014
           END-IF.                                                      06870014
                                                                        06880014
      *5000-EXIT.                                                       06890014
       EJECT.                                                           06900014
                                                                        06910014
      ******************************************************************06920014
      *  6000-CALC-TIME.                                                06930014
      *  - ADD 1 SECOND TO TIME FOR EACH HEADER RECORD WRITTEN          06940014
      ******************************************************************06950014
       6000-CALC-TIME.                                                  06960014
                                                                        06970014
           MOVE PV-TIME-CALC TO PV-TIME.                                06980014
                                                                        06990014
           IF PV-TIME-SS = 59                                           07000014
              IF PV-TIME-MM = 59                                        07010014
                 IF PV-TIME-HH = 24                                     07020014
                    MOVE ZEROES TO PV-TIME-HH                           07030014
                 ELSE                                                   07040014
                    ADD +1 TO PV-TIME-HH                                07050014
                 END-IF                                                 07060014
                 MOVE ZEROES TO PV-TIME-MM                              07070014
              ELSE                                                      07080014
                 ADD +1 TO PV-TIME-MM                                   07090014
              END-IF                                                    07100014
              MOVE ZEROES TO PV-TIME-SS                                 07110014
           ELSE                                                         07120014
              ADD +1 TO PV-TIME-SS                                      07130014
           END-IF.                                                      07140014
                                                                        07150014
           MOVE PV-TIME TO PV-TIME-CALC.                                07160014
                                                                        07170014
           MOVE PV-TIME-HH TO PV-TIME-HHMM-HH.                          07180014
           MOVE PV-TIME-MM TO PV-TIME-HHMM-MM.                          07190014
                                                                        07200014
           MOVE PV-TIME-HHMM TO PV-TIME-CALC-HHMM.                      07210014
                                                                        07220014
      *6000-EXIT.                                                       07230014
       EJECT.                                                           07240014
                                                                        07250014
      ******************************************************************07260014
      *  7000-WRITE-ACTV-DISTHDR-RECORD                                 07270014
      *  - WRITES OUT THE ACTIVATION DISTRIBUTION HEADER RECORD.        07280014
      ******************************************************************07290014
       7000-WRITE-ACTV-DISTHDR-RECORD.                                  07300014
                                                                        07310014
           MOVE PC-NEW-DISTHDR-LNGTH-REWARD TO PV-RWD-ACTV-BYTE-COUNT.  07320014
           WRITE NW-RWD-ACTV-FILE-FORMATTED-REC                         07330014
               FROM EP217-HEADER-RECORD.                                07340014
                                                                        07350014
           ADD 1 TO PV-RWD-ACTV-RECS-WRITTEN.                           07360014
                                                                        07370014
      *7000-EXIT.                                                       07380014
                                                                        07390014
      ******************************************************************07400014
      *  7100-WRITE-REDM-DISTHDR-RECORD                                 07410014
      *  - WRITES OUT THE REDEMPTION DISTRIBUTION HEADER RECORD.        07420014
      ******************************************************************07430014
       7100-WRITE-REDM-DISTHDR-RECORD.                                  07440014
                                                                        07450014
           MOVE PC-NEW-DISTHDR-LNGTH-REWARD TO PV-RWD-REDM-BYTE-COUNT.  07460014
           WRITE NW-RWD-REDM-FILE-FORMATTED-REC                         07470014
               FROM EP217-HEADER-RECORD.                                07480014
                                                                        07490014
           ADD 1 TO PV-RWD-REDM-RECS-WRITTEN.                           07500014
                                                                        07510014
      *7000-EXIT.                                                       07520014
      ******************************************************************07530014
      *  7200-WRITE-REWARD-ACTV-RECORD.                                 07540014
      *  - WRITE A REWARD ACTIVATION RECORD OUT FROM THE COPY LAYOUT.   07550014
      ******************************************************************07560014
       7200-WRITE-REWARD-ACTV-RECORD.                                   07570014
                                                                        07580014
           MOVE EP139-RWD-ISUD-EXTRCT-REC TO PV-RWD-ACTV-INPUT-LAYOUT.  07590014
                                                                        07600014
           INITIALIZE PV-WRITE-REC-LNGTH.                               07610014
           SET PS-LINE-END-NOT-FOUND TO TRUE.                           07620014
           PERFORM VARYING PV-WRITE-REC-LNGTH FROM 1 BY 1               07630014
                   UNTIL ( PS-LINE-END-FOUND )                          07640014
            IF EP139-RWD-ISUD-EXTR-VAR-TABLE (PV-WRITE-REC-LNGTH)       07650014
                EQUAL TO ';'                                            07660014
                SET PS-LINE-END-FOUND TO TRUE                           07670014
                SUBTRACT 1 FROM PV-WRITE-REC-LNGTH                      07680014
             END-IF                                                     07690014
           END-PERFORM.                                                 07700014
                                                                        07710014
           ADD PC-ACTV-RWD-FIXED-SIZE TO PV-WRITE-REC-LNGTH.            07720014
           MOVE PV-WRITE-REC-LNGTH TO PV-RWD-ACTV-BYTE-COUNT.           07730014
                                                                        07740014
           WRITE NW-RWD-ACTV-FILE-FORMATTED-REC                         07750014
                                    FROM PV-RWD-ACTV-RECORD.            07760014
                                                                        07770014
           ADD 1 TO PV-RWD-ACTV-RECS-WRITTEN.                           07780014
                                                                        07790014
      *7200-EXIT.                                                       07800014
       EJECT.                                                           07810014
                                                                        07820014
      ******************************************************************07830014
      *  7300-WRITE-REWARD-REDM-RECORD.                                 07840014
      *  - WRITE A REWARD REDEMPTION RECORD OUT FROM THE COPY LAYOUT.   07850014
      ******************************************************************07860014
       7300-WRITE-REWARD-REDM-RECORD.                                   07870014
                                                                        07880014
           MOVE EP140-RWD-REDM-EXTRCT-REC TO PV-RWD-REDM-INPUT-LAYOUT.  07890014
                                                                        07900014
           INITIALIZE PV-WRITE-REC-LNGTH.                               07910014
           SET PS-LINE-END-NOT-FOUND TO TRUE.                           07920014
           PERFORM VARYING PV-WRITE-REC-LNGTH FROM 1 BY 1               07930014
                   UNTIL ( PS-LINE-END-FOUND )                          07940014
            IF EP140-RWD-REDM-EXTR-VAR-TABLE (PV-WRITE-REC-LNGTH)       07950014
                EQUAL TO ';'                                            07960014
                SET PS-LINE-END-FOUND TO TRUE                           07970014
                SUBTRACT 1 FROM PV-WRITE-REC-LNGTH                      07980014
             END-IF                                                     07990014
           END-PERFORM.                                                 08000014
                                                                        08010014
           ADD PC-REDM-RWD-FIXED-SIZE TO PV-WRITE-REC-LNGTH.            08020014
           MOVE PV-WRITE-REC-LNGTH TO PV-RWD-REDM-BYTE-COUNT.           08030014
                                                                        08040014
           WRITE NW-RWD-REDM-FILE-FORMATTED-REC                         08050014
                                    FROM PV-RWD-REDM-RECORD.            08060014
                                                                        08070014
           ADD 1 TO PV-RWD-REDM-RECS-WRITTEN.                           08080014
                                                                        08090014
      *7300-EXIT.                                                       08100014
       EJECT.                                                           08110014
                                                                        08120014
      ******************************************************************08130014
      *  9000-WRAPUP.                                                   08140014
      *  - CLOSES ALL FILES AND DISPLAYS ANY NEEDED INFORMATION.        08150014
      ******************************************************************08160014
       9000-WRAPUP.                                                     08170014
                                                                        08180014
           CLOSE CTRL-CRD-CUR-BTCH-DTE                                  08190014
                 REWARD-ACTIVATION-FILE                                 08200014
                 REWARD-REDEMPTION-FILE                                 08210014
                 NW-RWD-ACTV-FILE-WITH-HEADERS                          08220014
                 NW-RWD-REDM-FILE-WITH-HEADERS.                         08230014
                                                                        08240014
           DISPLAY 'ACTIVATION REWARD RECORDS READ FROM FILE: '         08250014
                      PV-RWD-ACTV-RECS-READ.                            08260014
           DISPLAY 'DUPLICATE ACTIVATION RECORDS REMOVED: '             08270014
                      PV-RWD-ACTV-DUPS-RMVD.                            08280014
           DISPLAY 'TOTAL RECORDS WRITTEN TO ACTIVATION FILE: '         08290014
                      PV-RWD-ACTV-RECS-WRITTEN.                         08300014
           DISPLAY ' '.                                                 08310014
           DISPLAY 'REDEMPTION REWARD RECORDS READ FROM FILE: '         08320014
                      PV-RWD-REDM-RECS-READ.                            08330014
           DISPLAY 'DUPLICATE ACTIVATION RECORDS REMOVED: '             08340014
                      PV-RWD-REDM-DUPS-RMVD.                            08350014
           DISPLAY 'TOTAL RECORDS WRITTEN TO ACTIVATION FILE: '         08360014
                      PV-RWD-REDM-RECS-WRITTEN.                         08370014
                                                                        08380014
      *9000-EXIT.                                                       08390014
       EJECT.                                                           08400014
                                                                        08410014
      ******************************************************************08420014
      *  9999-ABEND                                                     08430014
      *  - ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                   08440014
      ******************************************************************08450014
       9999-ABEND.                                                      08460014
                                                                        08470014
           DISPLAY '** ABEND           **'.                             08480014
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          08490014
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        08500014
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       08510014
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       08520014
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         08530014
                                                                        08540014
      *9999-EXIT.                                                       08550014
       EJECT.                                                           08560014
