       IDENTIFICATION DIVISION.                                         12/18/95
       PROGRAM-ID.    MAKUT038.                                         MAKUT038
       AUTHOR.        LINDA GRIESBACH.                                     LV005
      ******************************************************************      32
      * THE PURPOSE OF THIS PROGRAM IS TO CREATE OH/OO RECORDS THAT    *      32
      * WILL BE LOADED INTO THE TSKUINRU ORACLE TABLE.  THE INVENTORY  *      32
      * LOAD FILE CONTAINS A RECORD FOR EACH GRP CDE/STORE CONBINATION *      32
      * ALONG WITH THE GRP CDE/STORE, THE RECORD CONTAINS THE OH AND OO*      32
      * QUANTITIES FOR THAT GRP CDE/STORE.                             *      32
      *                                                                *      32
      * THIS PROGRAM DETERMINES THE CORRESPONDING ITEM CLUSTER AND     *      32
      * AND ORG UNIT ID'S FOR EACH GRP CDE/STORE RECORD.  IT THEN      *      32
      * CREATES AN OUTPUT RECORD CONTAINING THE ITEM/ORG CLUSTER ID'S  *      32
      * ALONG WITH THE OH/OO QUANTITIES.                               *      32
      ******************************************************************      32
      *  WR25478 - LINDA GRIESBACH                                     *      32
      *            STORE NBR EXPANSION                                 *      32
      ******************************************************************      32
221500*  04/08/2019 - CHG0221500 - JIM ARENDT                          *      32
221500*            MAINFRAME REFACTORING - INCREASING OCCURS OF        *      32
221500*            STORE TABLE (ORG-CLUSTR-ID) FROM 2000 TO 9999       *      32
221500*            TO ALLOW FOR 6666 (STOREFRONT) AND TEST LAB STORES. *      32
221500******************************************************************      32
                                                                        MAKUT038
       ENVIRONMENT DIVISION.                                            MAKUT038
       INPUT-OUTPUT SECTION.                                            MAKUT038
       FILE-CONTROL.                                                    MAKUT038
                                                                        MAKUT038
           SELECT GRP-STORE-INVENTORY-FILE                              MAKUT038
               ASSIGN TO INP01.                                         MAKUT038
                                                                        MAKUT038
           SELECT ORG-CLUSTR-FILE                                       MAKUT038
               ASSIGN TO INP02.                                         MAKUT038
                                                                        MAKUT038
           SELECT ITEM-CLUSTR-FILE                                      MAKUT038
               ASSIGN TO INP03.                                         MAKUT038
                                                                        MAKUT038
           SELECT INVENTORY-LOAD-FILE                                   MAKUT038
               ASSIGN TO OUT01.                                         MAKUT038
                                                                        MAKUT038
       DATA DIVISION.                                                   MAKUT038
       FILE SECTION.                                                    MAKUT038
       FD  GRP-STORE-INVENTORY-FILE                                     MAKUT038
           RECORDING MODE IS F                                          MAKUT038
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT038
       01  GRP-STORE-INVENTORY-RECORD  PIC X(109).                         CL**5
                                                                        MAKUT038
       FD  ORG-CLUSTR-FILE                                              MAKUT038
           RECORDING MODE IS F                                          MAKUT038
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT038
       01  ORG-CLUSTR-RECORD           PIC X(13).                          CL**5
                                                                        MAKUT038
       FD  ITEM-CLUSTR-FILE                                             MAKUT038
           RECORDING MODE IS F                                          MAKUT038
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT038
       01  ITEM-CLUSTR-RECORD          PIC X(34).                          CL**5
                                                                        MAKUT038
       FD  INVENTORY-LOAD-FILE                                          MAKUT038
           RECORDING MODE IS F                                          MAKUT038
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT038
       01  INVENTORY-LOAD-RECORD       PIC X(41).                          CL**5
                                                                        MAKUT038
                                                                        MAKUT038
       WORKING-STORAGE SECTION.                                         MAKUT038
                                                                        MAKUT038
       01  PROGRAM-VARIABLES.                                           MAKUT038
           05  PV-INV-READ-COUNT       PIC S9(13) VALUE +0 COMP.        MAKUT038
           05  PV-INV-WRITTEN-COUNT    PIC S9(13) VALUE +0 COMP.        MAKUT038
           05  PV-NO-ITEM-COUNT        PIC S9(9)  VALUE +0 COMP.        MAKUT038
           05  PV-NO-ORG-COUNT         PIC S9(9)  VALUE +0 COMP.        MAKUT038
           05  PV-SUB                  PIC S9(4)  COMP-3.                       
           05  PV-ABEND-CODE           PIC S9(4)  VALUE +0 COMP.                
           05  PV-PREV-CLUSTR-KEY.                                              
               10  PV-PREV-OWNER-DEPT  PIC X(3)   VALUE SPACES.                 
               10  FILLER              PIC X(1)   VALUE SPACE.                  
               10  PV-PREV-ITEM-CLUSTR-GRP                                      
                                       PIC X(20)  VALUE SPACES.                 
           05  PV-GRP-CLUSTR-KEY.                                               
               10  PV-GRP-CLUSTR-DEPT  PIC X(3)   VALUE SPACES.                 
               10  FILLER              PIC X(1)   VALUE SPACE.                  
               10  PV-GRP-CLUSTR-ITEM-NAME                                      
                                       PIC X(20)  VALUE SPACES.                 
           05  PV-ITEM-NAME.                                                    
               10  FILLER              PIC X(7)   VALUE SPACES.                 
               10  PV-GRP-CODE         PIC X(3)   VALUE SPACES.                 
               10  FILLER              PIC X(10)  VALUE SPACES.                 
                                                                                
       01  PROGRAM-SWITCHES.                                            MAKUT038
           05  PS-GRP-STORE-INV-EOF-SW PIC X(1)   VALUE 'N'.            MAKUT038
               88 PS-GRP-STORE-INV-EOF            VALUE 'Y'.            MAKUT038
               88 PS-GRP-STORE-INV-NOT-EOF        VALUE 'N'.            MAKUT038
           05  PS-ORG-CLUSTR-EOF-SW    PIC X(1)   VALUE 'N'.            MAKUT038
               88 PS-ORG-CLUSTR-EOF               VALUE 'Y'.            MAKUT038
               88 PS-ORG-CLUSTR-NOT-EOF           VALUE 'N'.            MAKUT038
           05  PS-ITEM-CLUSTR-EOF-SW   PIC X(1)   VALUE 'N'.            MAKUT038
               88 PS-ITEM-CLUSTR-EOF              VALUE 'Y'.            MAKUT038
               88 PS-ITEM-CLUSTR-NOT-EOF          VALUE 'N'.            MAKUT038
           05  PS-FIRST-ORG-CLUSTR-SW  PIC X(1)   VALUE 'Y'.            MAKUT038
               88 PS-FIRST-ORG-CLUSTR             VALUE 'Y'.            MAKUT038
               88 PS-NOT-FIRST-ORG-CLUSTR         VALUE 'N'.            MAKUT038
           05  PS-FIRST-ITEM-CLUSTR-SW PIC X(1)   VALUE 'Y'.            MAKUT038
               88 PS-FIRST-ITEM-CLUSTR            VALUE 'Y'.            MAKUT038
               88 PS-NOT-FIRST-ITEM-CLUSTR        VALUE 'N'.            MAKUT038
           05  PS-ITEM-CLUSTR-MATCH-SW PIC X(1)   VALUE 'N'.            MAKUT038
               88 PS-ITEM-CLUSTR-MATCH            VALUE 'Y'.            MAKUT038
               88 PS-ITEM-CLUSTR-NO-MATCH         VALUE 'N'.            MAKUT038
           05  PS-ITEM-CLUSTR-FILE-SW  PIC X(1)   VALUE 'Y'.            MAKUT038
               88 PS-ITEM-CLUSTR-IN-FILE          VALUE 'Y'.            MAKUT038
               88 PS-ITEM-CLUSTR-NOT-IN-FILE      VALUE 'N'.            MAKUT038
           05  PS-VALID-GRP-STORE-SW   PIC X(1)   VALUE 'N'.            MAKUT038
               88 PS-VALID-GRP-STORE              VALUE 'Y'.            MAKUT038
               88 PS-NOT-VALID-GRP-STORE          VALUE 'N'.            MAKUT038
                                                                        MAKUT038
       01  ORG-CLUSTR-TABLE.                                                    
221500*    05  ORG-CLUSTR-ID           OCCURS 2000 TIMES                        
221500     05  ORG-CLUSTR-ID           OCCURS 9999 TIMES                        
                                       PIC 9(9).                                
                                                                                
       01  ERROR-MESSAGE.                                                       
           05  FILLER                  PIC X(29)                                
                  VALUE '*** ERROR IN PROGRAM MAKUT038'.                        
           05  FILLER                  PIC X(14)                                
                  VALUE '; ERROR CODE ='.                                       
           05  EM-ERROR-CODE           PIC --------9                            
                                                  VALUE ZERO.                   
           05  FILLER                  PIC X(15)                                
                  VALUE '; ERROR DESC ='.                                       
           05  EM-ERROR-DESC           PIC X(65)  VALUE SPACES.                 
                                                                                
       01  LOG-MESSAGE.                                                 MAKUT038
           05  FILLER                  PIC X(23)                        MAKUT038
                   VALUE '*** PROGRAM MAKUT038 - '.                     MAKUT038
           05  LM-MESSAGE              PIC X(109) VALUE SPACES.         MAKUT038
                                                                        MAKUT038
       01  ITEM-NOT-FOUND-LOG-MSG.                                      MAKUT038
           05  FILLER                  PIC X(37)                        MAKUT038
                   VALUE 'ITEM CLUSTER ROW NOT FOUND FOR = '.           MAKUT038
           05  INF-LM-ITEM-NAME        PIC X(20)  VALUE SPACES.         MAKUT038
           05  FILLER                  PIC X(14)                                
                   VALUE ' - DEPT NBR = '.                                      
           05  INF-LM-DEPT             PIC X(3)  VALUE SPACES.          MAKUT038
                                                                                
       01  RUN-SUMMARY-LOG-MSG1.                                        MAKUT038
           05  FILLER                  PIC X(35)                        MAKUT038
                   VALUE 'GRP STORE INVENTORY RECORDS READ = '.         MAKUT038
           05  RSLM1-GRP-STOR-INV-RECS PIC Z(12)9 VALUE ZERO.           MAKUT038
                                                                        MAKUT038
       01  RUN-SUMMARY-LOG-MSG2.                                        MAKUT038
           05  FILLER                  PIC X(33)                        MAKUT038
                   VALUE 'INVENTORY LOAD RECORDS WRITTEN = '.           MAKUT038
           05  RSLM2-INV-LOAD-RECS     PIC Z(12)9 VALUE ZERO.           MAKUT038
                                                                        MAKUT038
       01  RUN-SUMMARY-LOG-MSG3.                                        MAKUT038
           05  FILLER                  PIC X(43)                        MAKUT038
                   VALUE 'NUMBER OF GRP STORE INVENTORY RECORDS DROPP'. MAKUT038
           05  FILLER                  PIC X(39)                        MAKUT038
                   VALUE 'ED DUE TO NO MATCHING ITEM CLUSTER ID= '.     MAKUT038
           05  RSLM3-EXTRACT-NO-ITEM-RECS                               MAKUT038
                                       PIC Z(8)9   VALUE ZERO.                  
                                                                        MAKUT038
       01  RUN-SUMMARY-LOG-MSG4.                                        MAKUT038
           05  FILLER                  PIC X(38)                        MAKUT038
                   VALUE 'NUMBER OF GRP STORE INVENTORY RECORDS '.      MAKUT038
           05  FILLER                  PIC X(38)                        MAKUT038
                   VALUE 'DROPPED DUE TO NO MATCHING ORG UNIT = '.      MAKUT038
           05  RSLM4-STORE-NO-ORG-RECS PIC Z(8)9   VALUE ZERO.          MAKUT038
                                                                        MAKUT038
       01  PV-ORG-CLUSTR-RECORD.                                                
           05  PV-ORG-CLUSTR-STORE     PIC X(4).                                
           05  PV-ORG-CLUSTR-ID        PIC 9(9).                                
                                                                        MAKUT038
       01  PV-ITEM-CLUSTR-RECORD.                                               
           05  PV-ITEM-CLUSTR-KEY.                                              
               10  PV-ITEM-CLUSTR-DEPT PIC X(3).                                
               10  FILLER              PIC X(1).                                
               10  PV-ITEM-CLUSTR-GRP  PIC X(20).                               
           05  FILLER                  PIC X(1).                                
           05  PV-ITEM-CLUSTR-ID       PIC 9(9).                                
                                                                                
      * FIELDS FOR THE GRP STORE INVENTORY EXTRACT FILE                 MAKUT038
           COPY MARD015.                                                MAKUT038
                                                                        MAKUT038
      * FIELDS FOR THE INVENTORY LOAD FILE                              MAKUT038
           COPY MARD016.                                                MAKUT038
                                                                        MAKUT038
                                                                        MAKUT038
       EJECT                                                            MAKUT038
       PROCEDURE DIVISION.                                              MAKUT038
                                                                        MAKUT038
       0000-MAINLINE.                                                   MAKUT038
                                                                        MAKUT038
           PERFORM 1000-INITIALIZE                                      MAKUT038
              THRU 1000-EXIT.                                                   
                                                                        MAKUT038
           PERFORM 2000-CREATE-OH-OO-FILE                               MAKUT038
              THRU 2000-EXIT                                                    
              UNTIL PS-GRP-STORE-INV-EOF.                                       
                                                                        MAKUT038
           PERFORM 3000-CLOSE                                           MAKUT038
              THRU 3000-EXIT.                                                   
                                                                        MAKUT038
           DISPLAY 'MAKUT038 - SUCCESSFUL END'.                         MAKUT038
           MOVE ZERO TO RETURN-CODE.                                    MAKUT038
           GOBACK.                                                      MAKUT038
                                                                        MAKUT038
       0000-EXIT.                                                       MAKUT038
           EXIT.                                                                
                                                                        MAKUT038
                                                                        MAKUT038
       1000-INITIALIZE.                                                 MAKUT038
                                                                        MAKUT038
           OPEN INPUT  GRP-STORE-INVENTORY-FILE                         MAKUT038
                       ORG-CLUSTR-FILE                                  MAKUT038
                       ITEM-CLUSTR-FILE                                 MAKUT038
                OUTPUT INVENTORY-LOAD-FILE.                                     
                                                                        MAKUT038
           INITIALIZE ORG-CLUSTR-TABLE.                                         
                                                                                
           PERFORM 1100-LOAD-ORG-CLUSTR-FILE                                    
              THRU 1100-EXIT                                                    
             UNTIL PS-ORG-CLUSTR-EOF.                                           
                                                                                
           PERFORM 9997-READ-ITEM-CLUSTR-FILE                                   
              THRU 9997-EXIT.                                                   
                                                                                
           PERFORM 9998-READ-GRP-STORE-INV-FILE                                 
              THRU 9998-EXIT                                                    
             UNTIL PS-VALID-GRP-STORE                                           
                OR PS-GRP-STORE-INV-EOF.                                        
                                                                        MAKUT038
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1100-LOAD-ORG-CLUSTR-FILE.                                               
                                                                        MAKUT038
           READ ORG-CLUSTR-FILE                                                 
               INTO PV-ORG-CLUSTR-RECORD                                        
                   AT END                                                       
                       IF PS-FIRST-ORG-CLUSTR                                   
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ORG CLUSTER TABLE'                       
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           SET PS-ORG-CLUSTR-EOF TO TRUE                        
                           GO TO 1100-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT038
           IF PS-FIRST-ORG-CLUSTR                                               
               SET PS-NOT-FIRST-ORG-CLUSTR TO TRUE                              
           END-IF.                                                              
                                                                                
           IF PV-ORG-CLUSTR-STORE IS NUMERIC                                    
             AND PV-ORG-CLUSTR-STORE > ZERO                                     
               MOVE PV-ORG-CLUSTR-STORE TO PV-SUB                               
               MOVE PV-ORG-CLUSTR-ID    TO ORG-CLUSTR-ID (PV-SUB)               
           END-IF.                                                              
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT038
                                                                        MAKUT038
       2000-CREATE-OH-OO-FILE.                                          MAKUT038
                                                                        MAKUT038
      * THE OH/OO INPUT FILE IS SORTED BY GRP CDE/STORE -               MAKUT038
      * IT MAY CONTAIN THE SAME GRP CDE FOR MULITPLE STORES -           MAKUT038
      * SO MULTIPLE INPUT RECORDS COULD BE BYPASSED                     MAKUT038
      * IF THE GRP CDE IS NOT IN THE ITEM-CLUSTR-FILE                   MAKUT038
                                                                        MAKUT038
           IF PV-GRP-CLUSTR-KEY = PV-ITEM-CLUSTR-KEY                            
             OR PV-GRP-CLUSTR-KEY < PV-ITEM-CLUSTR-KEY                          
             OR PS-ITEM-CLUSTR-EOF                                              
               NEXT SENTENCE                                                    
           ELSE                                                                 
               SET PS-ITEM-CLUSTR-NO-MATCH TO TRUE                              
               SET PS-ITEM-CLUSTR-IN-FILE  TO TRUE                              
               PERFORM 9997-READ-ITEM-CLUSTR-FILE                               
                  THRU 9997-EXIT                                                
                 UNTIL PS-ITEM-CLUSTR-MATCH                                     
                    OR PS-ITEM-CLUSTR-NOT-IN-FILE                               
                    OR PS-ITEM-CLUSTR-EOF                                       
           END-IF.                                                              
                                                                                
                                                                        MAKUT038
           IF PV-GRP-CLUSTR-KEY = PV-ITEM-CLUSTR-KEY                            
               MOVE MA015-STORE TO PV-SUB                                       
               MOVE ORG-CLUSTR-ID (PV-SUB)                                      
                                TO MA016-ORG-UNIT-CLUSTER-ID                    
               IF MA016-ORG-UNIT-CLUSTER-ID NOT = ZERO                          
                   MOVE PV-ITEM-CLUSTR-ID TO MA016-ITEM-CLUSTER-ID              
                   MOVE MA015-ONHAND-QTY  TO MA016-ONHAND-QTY                   
                   MOVE MA015-ONORD-QTY   TO MA016-ONORD-QTY                    
                                                                                
                   WRITE INVENTORY-LOAD-RECORD                                  
                       FROM MA016-INVENTORY-LOAD                                
                   ADD +1 TO PV-INV-WRITTEN-COUNT                               
               ELSE                                                             
                   ADD +1 TO PV-NO-ORG-COUNT                                    
               END-IF                                                           
           ELSE                                                                 
               IF PV-GRP-CLUSTR-KEY NOT = PV-PREV-CLUSTR-KEY                    
                   MOVE MA015-OWNER-DEPT TO PV-PREV-OWNER-DEPT                  
                                            INF-LM-DEPT                         
                   MOVE MA015-ITEM-NAME TO PV-PREV-ITEM-CLUSTR-GRP              
                                           INF-LM-ITEM-NAME                     
                   MOVE ITEM-NOT-FOUND-LOG-MSG TO LM-MESSAGE                    
                   DISPLAY LOG-MESSAGE UPON SYSOUT                              
               END-IF                                                           
               ADD +1 TO PV-NO-ITEM-COUNT                                       
           END-IF.                                                              
                                                                                
           SET PS-NOT-VALID-GRP-STORE TO TRUE.                          MAKUT038
           PERFORM 9998-READ-GRP-STORE-INV-FILE                                 
              THRU 9998-EXIT                                                    
             UNTIL PS-VALID-GRP-STORE                                           
                OR PS-GRP-STORE-INV-EOF.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-CLOSE.                                                              
                                                                                
           CLOSE GRP-STORE-INVENTORY-FILE                                       
                 ORG-CLUSTR-FILE                                                
                 ITEM-CLUSTR-FILE                                               
                 INVENTORY-LOAD-FILE.                                           
                                                                                
           MOVE PV-INV-READ-COUNT       TO RSLM1-GRP-STOR-INV-RECS.             
           MOVE RUN-SUMMARY-LOG-MSG1    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-INV-WRITTEN-COUNT    TO RSLM2-INV-LOAD-RECS.                 
           MOVE RUN-SUMMARY-LOG-MSG2    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-NO-ITEM-COUNT        TO RSLM3-EXTRACT-NO-ITEM-RECS.  MAKUT038
           MOVE RUN-SUMMARY-LOG-MSG3    TO LM-MESSAGE.                  MAKUT038
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT038
                                                                        MAKUT038
           MOVE PV-NO-ORG-COUNT         TO RSLM4-STORE-NO-ORG-RECS.     MAKUT038
           MOVE RUN-SUMMARY-LOG-MSG4    TO LM-MESSAGE.                  MAKUT038
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT038
                                                                        MAKUT038
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT038
                                                                        MAKUT038
       9997-READ-ITEM-CLUSTR-FILE.                                              
                                                                        MAKUT038
           READ ITEM-CLUSTR-FILE                                                
               INTO PV-ITEM-CLUSTR-RECORD                                       
                   AT END                                                       
                       IF PS-FIRST-ITEM-CLUSTR                                  
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ITEM CLUSTER TABLE'                      
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           MOVE SPACES TO PV-ITEM-CLUSTR-GRP                    
                           SET PS-ITEM-CLUSTR-EOF TO TRUE                       
                           GO TO 9997-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT038
           IF PS-FIRST-ITEM-CLUSTR                                              
               SET PS-NOT-FIRST-ITEM-CLUSTR TO TRUE                             
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT038
           IF PV-GRP-CLUSTR-KEY = PV-ITEM-CLUSTR-KEY                            
               SET PS-ITEM-CLUSTR-MATCH TO TRUE                                 
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT038
           IF PV-GRP-CLUSTR-KEY < PV-ITEM-CLUSTR-KEY                            
               SET PS-ITEM-CLUSTR-NOT-IN-FILE TO TRUE                           
           END-IF.                                                              
                                                                        MAKUT038
       9997-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9998-READ-GRP-STORE-INV-FILE.                                            
                                                                        MAKUT038
           READ GRP-STORE-INVENTORY-FILE                                MAKUT038
               INTO MA015-ROLLUP-INVENTORY                              MAKUT038
                   AT END                                               MAKUT038
                       SET PS-GRP-STORE-INV-EOF TO TRUE                 MAKUT038
                       GO TO 9998-EXIT                                          
                   NOT AT END                                           MAKUT038
                       ADD +1 TO PV-INV-READ-COUNT                      MAKUT038
           END-READ.                                                            
                                                                                
           MOVE MA015-OWNER-DEPT TO PV-GRP-CLUSTR-DEPT.                         
           MOVE MA015-ITEM-NAME  TO PV-GRP-CLUSTR-ITEM-NAME                     
                                    PV-ITEM-NAME.                               
                                                                                
           IF PV-GRP-CODE NOT = SPACES                                          
               SET PS-VALID-GRP-STORE TO TRUE                                   
           END-IF.                                                              
                                                                                
       9998-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                        MAKUT038
           MOVE ERROR-MESSAGE TO LOG-MESSAGE.                           MAKUT038
                                                                        MAKUT038
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT038
                                                                        MAKUT038
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         MAKUT038
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
