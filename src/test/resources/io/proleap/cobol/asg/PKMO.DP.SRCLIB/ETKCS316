00001  IDENTIFICATION DIVISION.                                         06/22/94
00002  PROGRAM-ID.    ETKCS316.                                         ETKCS316
00003  AUTHOR.        MARC BLAZICH - G&R.                                  LV025
00004  INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**1
00005  DATE-WRITTEN.  02/94.                                               CL**1
00006  DATE-COMPILED.                                                      CL**1
00007                                                                      CL**1
00008 *---------------------------------------------------------------*    CL**1
00009 *    TRANID      - E316                                         *    CL**1
00010 *    TYPE        - CICS ARCHITECTURE INQUIRY - DB2              *    CL**1
00011 *    DESCRIPTION - TRADING PARTNER TRANSACTION SET INQUIRY      *    CL**1
00012 *    MAPSET      - ETKM316                                      *    CL**1
00013 *                                                               *    CL**1
00014 *    THIS PROGRAM ALLOWS ONE TO VIEW TP TRANSACTION SETS FOR    *    CL**1
00015 *    A GIVEN DKEY, TRAN_SET_CDE, DIR_IND, AND PROD_TEST_IND.    *    CL**1
00016 *                                                               *    CL**1
00024 *    CHANGES:                                                   * ETKCS306
00024 *     03/20/97 TOM MEDREK - FIXED APPLICATION SPECIFIC SECTION. * ETKCS306
00024 *       ADDED NRF INDICATOR WHICH WAS ADDED TO ETWS300 &        * ETKCS306
00024 *       ETWS301 EARLIER.                                        * ETKCS306
00016 *                                                               *    CL**1
00017 *---------------------------------------------------------------*    CL**1
CICS  *  CHANGED 10/25/2022    BY BARB SCHULTZ    WR: CHG0277210      *    CL**1
CICS  *                                                               *    CL**1
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL   *    CL**1
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.    *    CL**1
00017 *---------------------------------------------------------------*    CL**1
00018                                                                      CL**1
00019  ENVIRONMENT DIVISION.                                               CL**1
00020  DATA DIVISION.                                                      CL**1
00021 /                                                                    CL**1
00022  WORKING-STORAGE SECTION.                                            CL**1
00023                                                                      CL**1
00024  01  DK-DB2-KEYS.                                                    CL**1
00025      05  DK-TP-DKEY                   PIC S9(09) VALUE +0            CL**1
00026                                                  COMP.               CL**1
00027      05  DK-TRAN-SET-CDE              PIC  X(03).                    CL**1
00028      05  DK-PROD-TEST-IND             PIC  X(01).                    CL**1
00029      05  DK-DIR-IND                   PIC  X(01).                    CL**1
00030                                                                      CL**1
00031  01  PC-PROGRAM-CONSTANTS.                                           CL**1
00032      05  PC-TRANS-ID                  PIC X(04)  VALUE 'E316'.       CL**1
00033      05  PC-CURRENT-PROGRAM-NAME      PIC X(08)  VALUE 'ETKCS316'.   CL**1
00034      05  PC-CURRENT-MAP-NAME          PIC X(08)  VALUE 'ET316A  '.   CL**1
00035      05  PC-CURRENT-MAPSET-NAME       PIC X(08)  VALUE 'ETKM316 '.   CL**1
00036      05  PC-APPL-MAP                  PIC S9(04) VALUE  +1           CL**1
00037                                                  COMP.               CL**1
00038      05  PC-MAX-RETRIES               PIC S9(04) VALUE  +3           CL**1
00039                                                  COMP.               CL**1
00040      05  PC-1                         PIC S9(04) VALUE  +1           CL**1
00041                                                  COMP.               CL**1
00042      05  PC-NULL-DTE                  PIC X(10)  VALUE               CL**2
00043                                                     '09/09/9999'.    CL*23
00044      05  PC-INTER-APPL-CODES.                                        CL**2
00045          10  PC-INTER-APPL-CRIT-SPEC  PIC X(01)  VALUE ' '.          CL**1
00046          10  PC-INTER-APPL-LIST       PIC X(01)  VALUE '5'.          CL**1
00047                                                                      CL**1
00048      05  PC-TSYMSG-NUMBERS.                                          CL**1
00049          10  PC-TSYMSG-00004      PIC  9(05)  VALUE  00004.          CL**1
00050          10  PC-TSYMSG-00005      PIC  9(05)  VALUE  00005.          CL**1
00051          10  PC-TSYMSG-00007      PIC  9(05)  VALUE  00007.          CL**1
00052          10  PC-TSYMSG-00008      PIC  9(05)  VALUE  00008.          CL**1
00053          10  PC-TSYMSG-00009      PIC  9(05)  VALUE  00009.          CL**1
00054          10  PC-TSYMSG-00010      PIC  9(05)  VALUE  00010.          CL**1
00055          10  PC-TSYMSG-00057      PIC  9(05)  VALUE  00057.          CL**1
00056          10  PC-TSYMSG-00059      PIC  9(05)  VALUE  00059.          CL**1
00057          10  PC-TSYMSG-00069      PIC  9(05)  VALUE  00069.          CL**1
00058          10  PC-TSYMSG-00070      PIC  9(05)  VALUE  00070.          CL**1
00059          10  PC-TSYMSG-00095      PIC  9(05)  VALUE  00095.          CL**1
00060          10  PC-TSYMSG-00115      PIC  9(05)  VALUE  00115.          CL**1
00061          10  PC-TSYMSG-00116      PIC  9(05)  VALUE  00116.          CL**1
00062          10  PC-TSYMSG-00118      PIC  9(05)  VALUE  00118.          CL**1
00063          10  PC-TSYMSG-00148      PIC  9(05)  VALUE  00148.          CL**1
00064 *                                                                    CL**1
00065  01  PS-PROGRAM-SWITCHES.                                            CL**1
00066      05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.            CL**1
00067          88  PS-ERROR                         VALUE  'Y'.            CL**1
00068          88  PS-NO-ERROR                      VALUE  'N'.            CL**1
00069      05  PS-COMMAREA-SW           PIC  X(01)  VALUE 'N'.             CL**1
00070          88  PS-COMMAREA-VALID                VALUE 'Y'.             CL**1
00071          88  PS-COMMAREA-NOT-VALID            VALUE 'N'.             CL**1
00072      05  PS-LIST-SW               PIC  X(01)  VALUE  'N'.            CL**1
00073          88  PS-LIST-END                      VALUE  'Y'.            CL**1
00074          88  PS-LIST-NOT-END                  VALUE  'N'.            CL**1
00075 *                                                                    CL**1
00076  01  PV-PROGRAM-VARIABLES.                                           CL**1
00077      05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0               CL**1
00078                                               COMP SYNC.             CL**1
00079      05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0               CL**1
00080                                               COMP SYNC.             CL**1
00081      05  PV-MSG-VAR-CNT           PIC S9(04)  VALUE +0               CL**1
00082                                               COMP SYNC.             CL**1
00083 /                                                                    CL**1
00084 **--------------------------------------------------------------**   CL**1
00085 ** THE LAYOUT FOR THE TEMPORARY STORAGE QUEUE CONTAINING        **   CL**1
00086 ** SELECTED LIST ITEMS TO BE PROCESSED BY THIS PROGRAM.         **   CL**1
00087 **--------------------------------------------------------------**   CL**1
00088      COPY ETWS301.                                                   CL*25
00089                                                                      CL**1
00090 /                                                                    CL**1
00091 *----------------------------------------------------------------*   CL**1
00092 *    MAP LAYOUT                                                  *   CL**1
00093 *----------------------------------------------------------------*   CL**1
00094      COPY ETKM316.                                                   CL**1
00095                                                                      CL**1
00096 /                                                                    CL**1
00097 *----------------------------------------------------------------*   CL**1
00098 *    ATTRIBUTE SETTINGS COPYBOOK.                                *   CL**1
00099 *----------------------------------------------------------------*   CL**1
00100      COPY DPWS015.                                                   CL**1
00101                                                                      CL**1
00102 /                                                                    CL**1
00103 *----------------------------------------------------------------*   CL**1
00104 *    FUNCTION KEYS COPYBOOK                                      *   CL**1
00105 *----------------------------------------------------------------*   CL**1
00106      COPY DPWS016.                                                   CL**1
00107                                                                      CL**1
00108 /                                                                    CL**1
00109 **--------------------------------------------------------------**   CL**1
CICS  ** PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.            **   CL**1
00112 **--------------------------------------------------------------**   CL**1
00113      COPY DPWS030.                                                   CL**1
00113      COPY DPWS930.                                                   CL**1
00114                                                                      CL**1
00115 /                                                                    CL**1
00116 **--------------------------------------------------------------**   CL**1
00117 *    STANDARD COMMAREA.                                              CL**1
00118 **--------------------------------------------------------------**   CL**1
00119                                                                      CL**1
00120      COPY DPWS020.                                                   CL**1
00121      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                   CL**1
00122                                                                      CL**1
00123 *****************************************************************    CL**1
00124 *    INTER-APPLICATION COMMAREA.                                     CL**1
00125 *****************************************************************    CL**1
00126                                                                      CL**1
00127          10  INTER-APPL-COMM-AREA PIC X(200).                        CL**1
00128      COPY ETWS300.                                                   CL*25
00129 *****************************************************************    CL**1
00130 *    SPECIFIC COMMMAREA FOR POKCS035.                                CL**1
00131 *****************************************************************    CL**1
00132          10  ASC-SPECIFIC-COMMAREA.                                  CL**1
00133              15  ASC-INT-APPL-FORMAT-IND   PIC  X(01).               CL**1
00134              15  ASC-USE-SELECTION-QUEUE-IND                         CL**1
00135                                            PIC  X(01).               CL**1
00136                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.    CL**1
00137              15  ASC-SEL-QUEUE-NAME        PIC  X(08).               CL**1
00138              15  ASC-NUMBER-OF-TS-ITEMS    PIC S9(05)  COMP-3.       CL**1
00139              15  ASC-NUMBER-OF-SELECTED-ITEMS                        CL**1
00140                                            PIC S9(09)  COMP-3.       CL**1
00141              15  ASC-SEL-SUB               PIC S9(04)  COMP.         CL**1
00142              15  ASC-SEL-ITEM              PIC S9(04)  COMP.         CL**1
00143              15  ASC-KEY-FIELDS.                                     CL**1
00144                  20  ASC-KEY-TP-DKEY       PIC S9(09)  COMP.         CL**1
00145                  20  ASC-KEY-TRAN-SET-CDE  PIC X(03).                CL**1
00146                  20  ASC-KEY-PROD-TEST-IND PIC X(01).                CL**1
00147                  20  ASC-KEY-DIR-IND       PIC X(01).                CL**1
00148              15  ASC-SCREEN-FIELDS.                                  CL**1
00149                  20  ASC-ENT-ID                    PIC  9(09).       CL**1
00150                  20  ASC-VENDOR-NAME               PIC  X(30).       CL**1
00151                  20  ASC-VEND-INCHG-QUAL           PIC  X(02).       CL**1
00152                  20  ASC-VEND-INCHG-ID             PIC  X(15).       CL**1
00153                  20  ASC-NETWK-ID                  PIC  X(01).       CL**1
TM0320                 20  ASC-NRFC-ID                   PIC  X(01).       CL**3
00154                  20  ASC-NETWORK-NAME              PIC  X(15).       CL**1
00155                  20  ASC-FUNC-CDE                  PIC  X(02).       CL**4
00156                  20  ASC-TRAN-SET-NME              PIC  X(25).       CL**7
00157                  20  ASC-VER-NBR                   PIC  X(12).       CL**2
                       20  ASC-COM-MTHD                  PIC  X(01).            
00158                  20  ASC-VER-CHG-DTE               PIC  X(10).       CL*10
00159                  20  ASC-VER-NME                   PIC  X(01).       CL**2
00160                  20  ASC-SETUP-DTE                 PIC  X(10).       CL**4
00161                  20  ASC-LAST-CHG-DTE              PIC  X(10).       CL**4
00162                  20  ASC-LAST-CHG-UID              PIC  X(08).       CL**1
00163                  20  ASC-ACTV-DTE                  PIC  X(10).       CL**2
00164                  20  ASC-INACTV-DTE                PIC  X(10).       CL**2
00165                  20  ASC-LAST-CALL-DTE             PIC  X(10).       CL**2
00166                  20  ASC-NEXT-CALL-DTE             PIC  X(10).       CL**2
00167                  20  ASC-NOTES-TXT.                                  CL**4
00168                      25  ASC-NOTES-TXT1            PIC  X(40).       CL**5
00169                      25  ASC-NOTES-TXT2            PIC  X(40).       CL**5
00170 /                                                                    CL**1
00171 *----------------------------------------------------------------*   CL**1
00172 *    ABEND PROCESSING COPYBOOK.                                  *   CL**1
00173 *----------------------------------------------------------------*   CL**1
00174      COPY DPWS013.                                                   CL**1
00175 /                                                                    CL**1
00176                                                                      CL**1
00177 *    DB2 AREA FOR FUNCTIONAL GROUPS                                  CL**1
00178                                                                      CL**1
00179      EXEC SQL                                                        CL**1
00180           INCLUDE TTRDTRN                                            CL**1
00181      END-EXEC.                                                       CL**1
00182                                                                      CL**1
00183 *    DB2 AREA FOR VERSION DESCRIPTIONS                               CL**1
00184                                                                      CL**1
00185      EXEC SQL                                                        CL**1
00186           INCLUDE TTRNVER                                            CL**2
00187      END-EXEC.                                                       CL**1
00188                                                                      CL**7
00189 *    DB2 AREA FOR TRAN_SET_CDE DESCRIPTIONS                          CL**7
00190                                                                      CL**2
00191      EXEC SQL                                                        CL**2
00192           INCLUDE TTRNSET                                            CL**7
00193      END-EXEC.                                                       CL**2
00194                                                                      CL**2
00195 *    DB2 AREA FOR COMMUNICATIONS                                     CL**1
00196                                                                      CL**1
00197      EXEC SQL                                                        CL**1
00198           INCLUDE SQLCA                                              CL**1
00199      END-EXEC.                                                       CL**1
00200                                                                      CL**1
00201 /                                                                    CL**1
00202  LINKAGE SECTION.                                                    CL**1
00203                                                                      CL**1
00204  01  DFHCOMMAREA.                                                    CL**1
00205      05  FILLER                         OCCURS  1 TO 4072 TIMES      CL**1
00206                                         DEPENDING ON EIBCALEN.       CL**1
00207          10  FILLER                     PIC X.                       CL**1
00208                                                                      CL**1
00209 /                                                                    CL**1
00210  PROCEDURE DIVISION.                                                 CL**1
00211 *****************************************************************    CL**1
00212 ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **    CL**1
00213 ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **    CL**1
00214 ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **    CL**1
00215 **                                                             **    CL**1
00216 ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **    CL**1
00217 ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **    CL**1
00218 *****************************************************************    CL**1
00219  0000-MAIN-MODULE.                                                   CL**1
00220      INITIALIZE DP030-CICS-API-FIELDS.                               CL**1
00221      MOVE +1                       TO DP030-NUMBER-OF-MAPS.          CL**1
00222      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.             CL**1
00223      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).            CL**1
00224      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                          CL**1
00225      MOVE LENGTH OF ET316AI        TO DP030-MAP-LENGTH (1).          CL**1
00226      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).        CL**1
00227      PERFORM 0001-CALL-CICS-ARCH-API.                                CL**1
00228 *                                                                    CL**1
00229      PERFORM 1000-CONTROL-PROCESSING                                 CL**1
00230 *                                                                    CL**1
00231      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).        CL**1
00232      PERFORM 0001-CALL-CICS-ARCH-API.                                CL**1
00233                                                                      CL**1
00234 /                                                                    CL**1
00235  0001-CALL-CICS-ARCH-API.                                            CL**1
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                         CL**1
00237                                     DFHCOMMAREA                      CL**1
00238                                     DP030-CICS-API-FIELDS            CL**1
00239                                     DP020-STANDARD-COMMAREA          CL**1
00240                                     ET316AI.                         CL**1
00241 *                                                                    CL**1
00242      IF  DP030-RC-CALL-SUCCESSFUL                                    CL**1
00243          CONTINUE                                                    CL**1
00244      ELSE                                                            CL**1
00245          SET DP013-NO-ROLLBACK                                       CL**1
00246              DP013-XCTL-DISPLAY-RESTART                              CL**1
00247              DP013-CICS-ABEND      TO TRUE                           CL**1
00248          MOVE 'BEFORE 0000-MAIN-MODULE'                              CL**1
00249                                    TO DP013-PARAGRAPH                CL**1
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O   CL**1
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)         CL**1
00252          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)         CL**1
00253          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
00254      END-IF.                                                         CL**1
00255                                                                      CL**1
00256 /                                                                    CL**1
00257 *----------------------------------------------------------------*   CL**1
00258 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *   CL**1
00259 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *   CL**1
00260 *----------------------------------------------------------------*   CL**1
00261                                                                      CL**1
00262  1000-CONTROL-PROCESSING.                                            CL**1
00263                                                                      CL**1
00264      EVALUATE TRUE                                                   CL**1
00265          WHEN DP020-NEXT-ACT-INITIAL                                 CL**1
00266               PERFORM 1100-PROCESS-INTER-APPL-COMM                   CL**1
00267               IF  PS-COMMAREA-NOT-VALID                              CL**1
00268                   CONTINUE                                           CL**1
00269               ELSE                                                   CL**1
00270                   PERFORM 4000-BUILD-INITIAL-PANEL                   CL**1
00271               END-IF                                                 CL**1
00272                                                                      CL**1
00273          WHEN DP020-NEXT-ACT-READ-MAP                                CL**1
00274              PERFORM 2000-PROCESS-PANEL                              CL**1
00275                                                                      CL**1
00276          WHEN DP020-NEXT-ACT-RETURN                                  CL**1
00277              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**1
00278                                                                      CL**1
00279          WHEN OTHER                                                  CL**1
00280              SET DP013-LOGIC-ABEND                                   CL**1
00281                  DP013-NO-ROLLBACK TO TRUE                           CL**1
00282              MOVE '1000-CONTROL-PROCESSING'                          CL**1
00283                                    TO DP013-PARAGRAPH                CL**1
00284              MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'      CL**1
00285                                    TO DP013-MESSAGE-TEXT(1)          CL**1
00286              MOVE DP020-NEXT-APPL-ACTIVITY                           CL**1
00287                                    TO DP013-MESSAGE-TEXT(2)          CL**1
00288              PERFORM DP013-0000-PROCESS-ABEND                        CL**1
00289      END-EVALUATE.                                                   CL**1
00290                                                                      CL**1
00291 /                                                                    CL**1
00292 *----------------------------------------------------------------*   CL**1
00293 *    DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE  *   CL**1
00294 *    INTER-APPLICATION FORMAT CODE.                              *   CL**1
00295 *----------------------------------------------------------------*   CL**1
00296  1100-PROCESS-INTER-APPL-COMM.                                       CL**1
00297                                                                      CL**1
00298      SET PS-COMMAREA-VALID            TO TRUE.                       CL**1
00299                                                                      CL**1
00300      INITIALIZE ASC-SPECIFIC-COMMAREA.                               CL**1
00301                                                                      CL**1
00302 *    EVALUATE DP020-INT-APPL-FORMAT-IND                              CL**6
00303 *        WHEN PC-INTER-APPL-LIST                                     CL**6
00304              SET ASC-USE-SELECTION-QUEUE                             CL**1
00305                                  TO TRUE                             CL**1
00306              MOVE ET300-NBR-SELECTED-ITEMS                           CL**1
00307                                   TO ASC-NUMBER-OF-SELECTED-ITEMS    CL**1
00308              MOVE ET300-NUMBER-OF-TS-ITEMS                           CL**1
00309                                   TO ASC-NUMBER-OF-TS-ITEMS          CL**1
00310              MOVE ET300-SEL-QUEUE-NAME                               CL**1
00311                                   TO ASC-SEL-QUEUE-NAME              CL**1
00312              PERFORM 1120-PREPARE-LIST-ENTRY.                        CL**6
00313                                                                      CL**1
00314 *        WHEN OTHER                                                  CL**6
00315 *            MOVE PC-TSYMSG-00148 TO DP020-MSG-NUMBER                CL**6
00316 *            SET PS-ERROR                                            CL**6
00317 *                PS-COMMAREA-NOT-VALID                               CL**6
00318 *                DP020-MSG-FATAL                                     CL**6
00319 *                DP020-NEXT-ACT-APPL-ERROR                           CL**6
00320 *                                TO TRUE                             CL**6
00321 *    END-EVALUATE.                                                   CL**6
00322                                                                      CL**1
00323 /                                                                    CL**1
00324 *----------------------------------------------------------------*   CL**1
00325 * ENTRY IS FROM A LIST.  GET THE PO LIENENUMBER FROM THE TEMP    *   CL**1
00326 * STORAGE QUEUE PASSED FROM THE LIST.                            *   CL**1
00327 *----------------------------------------------------------------*   CL**1
00328  1120-PREPARE-LIST-ENTRY.                                            CL**1
00329      MOVE ET301-MAX-ITEMS        TO ASC-SEL-SUB.                     CL**1
00330      MOVE ZERO                   TO ASC-SEL-ITEM.                    CL**1
00331      PERFORM 2110-GET-NEXT-REC.                                      CL**1
00332                                                                      CL**1
00333 /                                                                    CL**1
00334 *----------------------------------------------------------------*   CL**1
00335 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *   CL**1
00336 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *   CL**1
00337 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *   CL**1
00338 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *   CL**1
00339 * FUNCTION KEYS CAN GET THIS FAR.                                *   CL**1
00340 *----------------------------------------------------------------*   CL**1
00341                                                                      CL**1
00342  2000-PROCESS-PANEL.                                                 CL**1
00343                                                                      CL**1
00344      EVALUATE TRUE                                                   CL**1
00345                                                                      CL**1
00346          WHEN DP020-SRC-AID = DP016-CLEAR                            CL**1
00347              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**1
00348                                                                      CL**1
00349          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                        CL**1
00350              SET DP030-DONT-ERASE                                    CL**1
00351                  DP030-SEND-DATAONLY (PC-APPL-MAP)                   CL**1
00352                                    TO TRUE                           CL**1
00353              PERFORM 4000-BUILD-INITIAL-PANEL                        CL**1
00354                                                                      CL**1
00355          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                CL**1
00356              IF  ASC-USE-SELECTION-QUEUE                             CL**1
00357                  PERFORM 2110-GET-NEXT-REC                           CL**1
00358                  IF PS-LIST-END                                      CL**1
00359                     SET PS-LIST-NOT-END TO TRUE                      CL**1
00360                  ELSE                                                CL**1
00361                     PERFORM 4000-BUILD-INITIAL-PANEL                 CL**1
00362                  END-IF                                              CL**1
00363              ELSE                                                    CL**1
00364 *                --- BOTTOM OF LIST ---                              CL**1
00365                  MOVE PC-TSYMSG-00070                                CL**1
00366                                    TO DP020-MSG-NUMBER               CL**1
00367                  SET DP020-MSG-INFORMATIONAL                         CL**1
00368                      PS-LIST-END   TO TRUE                           CL**1
00369              END-IF                                                  CL**1
00370                                                                      CL**1
00371          WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                CL**1
00372              IF  ASC-USE-SELECTION-QUEUE                             CL**1
00373                  PERFORM 2120-GET-PREV-REC                           CL**1
00374                  IF PS-LIST-END                                      CL**1
00375                     SET PS-LIST-NOT-END TO TRUE                      CL**1
00376                  ELSE                                                CL**1
00377                     PERFORM 4000-BUILD-INITIAL-PANEL                 CL**1
00378                END-IF                                                CL**1
00379              ELSE                                                    CL**1
00380 *                --- TOP OF LIST ---                                 CL**1
00381                  MOVE PC-TSYMSG-00069                                CL**1
00382                                    TO DP020-MSG-NUMBER               CL**1
00383                  SET DP020-MSG-INFORMATIONAL                         CL**1
00384                      PS-LIST-END   TO TRUE                           CL**1
00385              END-IF                                                  CL**1
00386                                                                      CL**1
00387          WHEN OTHER                                                  CL**1
00388              PERFORM 2100-CHECK-FUNCTION-KEY                         CL**1
00389                                                                      CL**1
00390      END-EVALUATE.                                                   CL**1
00391                                                                      CL**1
00392 /                                                                    CL**1
00393 *----------------------------------------------------------------*   CL**1
00394 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED       *   CL**1
00395 * FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED   *   CL**1
00396 * FROM THE CICS ARCHITECTURE API.                                *   CL**1
00397 *----------------------------------------------------------------*   CL**1
00398                                                                      CL**1
00399  2100-CHECK-FUNCTION-KEY.                                            CL**1
00400                                                                      CL**1
00401      EVALUATE TRUE                                                   CL**1
00402          WHEN DP020-SRC-AID = DP016-ENTER                            CL**1
00403              CONTINUE                                                CL**1
00404                                                                      CL**1
00405 *        WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                     CL**5
00406 *            INITIALIZE ET300-INTER-APPL-COMMAREA                    CL**5
00407 *            MOVE ASC-KEY-TP-DKEY     TO ET300-TP-DKEY               CL**5
00408 *            MOVE ASC-ENT-ID          TO ET300-ENT-ID                CL**5
00409 *            MOVE ASC-VENDOR-NAME     TO ET300-VENDOR-NAME           CL**5
00410 *            MOVE ASC-VEND-INCHG-ID   TO ET300-VEND-INCHG-ID         CL**5
00411 *            MOVE ASC-VEND-INCHG-QUAL TO ET300-VEND-INCHG-QUAL       CL**5
00412 *            MOVE ASC-NETWK-ID        TO ET300-NETWK-ID              CL**5
00413 *            MOVE ASC-NETWORK-NAME    TO ET300-NETWORK-NAME          CL**5
00414 *            MOVE ASC-FUNC-CDE        TO ET300-FUNC-CDE              CL**5
00415 *            MOVE ASC-KEY-DIR-IND     TO ET300-DIR-IND               CL**5
00416          WHEN OTHER                                                  CL**1
00417              SET DP013-NO-ROLLBACK                                   CL**1
00418                  DP013-XCTL-DISPLAY-RESTART                          CL**1
00419                  DP013-CICS-ABEND  TO TRUE                           CL**1
00420              MOVE '2000-PROCESS-PANEL'                               CL**1
00421                                    TO DP013-PARAGRAPH                CL**1
00422              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'         CL**1
00423                                    TO DP013-MESSAGE-TEXT (1)         CL**1
00424              PERFORM DP013-0000-PROCESS-ABEND                        CL**1
00425                                                                      CL**1
00426      END-EVALUATE.                                                   CL**1
00427                                                                      CL**1
00428 /                                                                    CL**1
00429  2110-GET-NEXT-REC.                                                  CL**1
00430      ADD +1                        TO ASC-SEL-SUB.                   CL**1
00431      IF  ASC-SEL-SUB > ET301-MAX-ITEMS                               CL**1
00432          ADD +1                    TO ASC-SEL-ITEM                   CL**1
00433          MOVE +1                   TO ASC-SEL-SUB                    CL**1
00434      END-IF.                                                         CL**1
00435      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                   CL**1
00436          PERFORM 9100-READ-SEL-QUEUE                                 CL**1
00437          IF  ET301-ITEM (ASC-SEL-SUB) > ZERO                         CL**1
00438              INITIALIZE ASC-SCREEN-FIELDS                            CL**1
00439              MOVE ET301-TP-DKEY     (ASC-SEL-SUB)                    CL**1
00440                                    TO ASC-KEY-TP-DKEY                CL**1
00441              MOVE ET301-FUNC-CDE    (ASC-SEL-SUB)                    CL**1
00442                                    TO ASC-FUNC-CDE                   CL**1
00443              MOVE ET301-DIR-IND     (ASC-SEL-SUB)                    CL**1
00444                                    TO ASC-KEY-DIR-IND                CL**1
00445              MOVE ET301-TRAN-SET-CDE(ASC-SEL-SUB)                    CL**1
00446                                    TO ASC-KEY-TRAN-SET-CDE           CL**1
00447              MOVE ET301-PROD-TEST-IND(ASC-SEL-SUB)                   CL**1
00448                                    TO ASC-KEY-PROD-TEST-IND          CL**1
00449              MOVE ET301-ENT-ID   (ASC-SEL-SUB)                       CL**1
00450                                    TO ASC-ENT-ID                     CL**1
00451              MOVE ET301-VENDOR-NAME   (ASC-SEL-SUB)                  CL**1
00452                                    TO ASC-VENDOR-NAME                CL**1
00453              MOVE ET301-VEND-INCHG-ID (ASC-SEL-SUB)                  CL**1
00454                                    TO ASC-VEND-INCHG-ID              CL**1
00455              MOVE ET301-VEND-INCHG-QUAL (ASC-SEL-SUB)                CL**1
00456                                    TO ASC-VEND-INCHG-QUAL            CL**1
00457              MOVE ET301-NETWK-ID        (ASC-SEL-SUB)                CL**1
00458                                    TO ASC-NETWK-ID                   CL**1
00459              MOVE ET301-NETWK-NAME      (ASC-SEL-SUB)                CL**1
00460                                    TO ASC-NETWORK-NAME               CL**1
00461              SET ET301-INQ (ASC-SEL-SUB)                             CL**1
00462                                    TO TRUE                           CL**1
00463              PERFORM 9200-REWRITE-LIST-QUEUE                         CL**1
00464          ELSE                                                        CL**1
00465              SUBTRACT +1 FROM ASC-SEL-SUB                            CL**1
00466 *            --- BOTTOM OF LIST ---                                  CL**1
00467              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER               CL**1
00468              SET DP020-MSG-INFORMATIONAL                             CL**1
00469                  PS-LIST-END       TO TRUE                           CL**1
00470          END-IF                                                      CL**1
00471      ELSE                                                            CL**1
00472          SUBTRACT +1 FROM ASC-SEL-ITEM                               CL**1
00473          MOVE ET301-MAX-ITEMS      TO ASC-SEL-SUB                    CL**1
00474          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER               CL**1
00475          SET DP020-MSG-INFORMATIONAL                                 CL**1
00476              PS-LIST-END           TO TRUE                           CL**1
00477      END-IF.                                                         CL**1
00478                                                                      CL**1
00479 /                                                                    CL**1
00480  2120-GET-PREV-REC.                                                  CL**1
00481      SUBTRACT +1 FROM ASC-SEL-SUB.                                   CL**1
00482      IF  ASC-SEL-SUB < +1                                            CL**1
00483          SUBTRACT +1 FROM ASC-SEL-ITEM                               CL**1
00484          MOVE ET301-MAX-ITEMS      TO ASC-SEL-SUB                    CL**1
00485      END-IF.                                                         CL**1
00486 *                                                                    CL**1
00487      IF  ASC-SEL-ITEM > ZERO                                         CL**1
00488          PERFORM 9100-READ-SEL-QUEUE                                 CL**1
00489          INITIALIZE ASC-SCREEN-FIELDS                                CL**1
00490          MOVE ET301-TP-DKEY     (ASC-SEL-SUB)                        CL**1
00491                                TO ASC-KEY-TP-DKEY                    CL**1
00492          MOVE ET301-FUNC-CDE    (ASC-SEL-SUB)                        CL**1
00493                                TO ASC-FUNC-CDE                       CL**1
00494          MOVE ET301-DIR-IND     (ASC-SEL-SUB)                        CL**1
00495                                TO ASC-KEY-DIR-IND                    CL**1
00496          MOVE ET301-TRAN-SET-CDE(ASC-SEL-SUB)                        CL**1
00497                                TO ASC-KEY-TRAN-SET-CDE               CL**1
00498          MOVE ET301-PROD-TEST-IND(ASC-SEL-SUB)                       CL**1
00499                                TO ASC-KEY-PROD-TEST-IND              CL**1
00500          MOVE ET301-ENT-ID   (ASC-SEL-SUB)                           CL**1
00501                                TO ASC-ENT-ID                         CL**1
00502          MOVE ET301-VENDOR-NAME   (ASC-SEL-SUB)                      CL**1
00503                                TO ASC-VENDOR-NAME                    CL**1
00504          MOVE ET301-VEND-INCHG-ID (ASC-SEL-SUB)                      CL**1
00505                                TO ASC-VEND-INCHG-ID                  CL**1
00506          MOVE ET301-VEND-INCHG-QUAL (ASC-SEL-SUB)                    CL**1
00507                                TO ASC-VEND-INCHG-QUAL                CL**1
00508          MOVE ET301-NETWK-ID        (ASC-SEL-SUB)                    CL**1
00509                                TO ASC-NETWK-ID                       CL**1
00510          MOVE ET301-NETWK-NAME      (ASC-SEL-SUB)                    CL**1
00511                                TO ASC-NETWORK-NAME                   CL**1
00512          SET ET301-INQ (ASC-SEL-SUB)                                 CL**1
00513                                TO TRUE                               CL**1
00514          PERFORM 9200-REWRITE-LIST-QUEUE                             CL**1
00515      ELSE                                                            CL**1
00516          MOVE +1                   TO ASC-SEL-ITEM                   CL**1
00517                                       ASC-SEL-SUB                    CL**1
00518 *        --- TOP OF LIST ---                                         CL**1
00519          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER               CL**1
00520          SET DP020-MSG-INFORMATIONAL                                 CL**1
00521              PS-LIST-END           TO TRUE                           CL**1
00522      END-IF.                                                         CL**1
00523                                                                      CL**1
00524 /                                                                    CL**1
00525 *----------------------------------------------------------------*   CL**1
00526 *    VALIDATE THE QUEUE WAS PASSED AND DISPLAY FOR THE FIRST     *   CL**1
00527 *    ENTRY.                                                      *   CL**1
00528 *----------------------------------------------------------------*   CL**1
00529                                                                      CL**1
00530  4000-BUILD-INITIAL-PANEL.                                           CL**1
00531                                                                      CL**1
00532      SET DP030-SET-CURSOR-TRAILER                                    CL**1
00533          PS-NO-ERROR             TO TRUE.                            CL**1
00534                                                                      CL**1
00535      MOVE ASC-KEY-TP-DKEY        TO DK-TP-DKEY                       CL**1
00536      MOVE ASC-KEY-TRAN-SET-CDE   TO DK-TRAN-SET-CDE                  CL**1
00537      MOVE ASC-KEY-PROD-TEST-IND  TO DK-PROD-TEST-IND                 CL**1
00538      MOVE ASC-KEY-DIR-IND        TO DK-DIR-IND                       CL**1
00539                                                                      CL**1
00540      PERFORM 4010-GET-TTRDTRN.                                       CL**1
00541                                                                      CL**3
00542 /                                                                    CL**1
00543 *----------------------------------------------------------------*   CL**1
00544 *  RETRIEVE THE ROW ASSOCIATED W/ THE DK- FIELDS                     CL**1
00545 *----------------------------------------------------------------*   CL**1
00546                                                                      CL**1
00547  4010-GET-TTRDTRN.                                                   CL**1
00548                                                                      CL**1
00549      PERFORM                                                         CL**1
00550          WITH TEST AFTER                                             CL**1
00551          VARYING PV-RETRY-COUNTER                                    CL**1
00552          FROM 1 BY 1                                                 CL**1
00553          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                  CL**1
00554              OR (SQLCODE = +0)                                       CL**9
00555              OR (SQLCODE = +100))                                    CL**9
00556                                                                      CL**1
00557          EXEC SQL                                                    CL**1
00558               SELECT  FUNC_CDE                                       CL**1
00559                      ,VER_NBR                                        CL**1
00560                      ,CHAR(ACTV_DTE, USA)                            CL**1
00561                      ,CHAR(INACTV_DTE, USA)                          CL**1
00562                      ,NOTES_TXT                                      CL**1
00563                      ,CHAR(LAST_CHG_DTE, USA)                        CL**1
00564                      ,LAST_CHG_UID                                   CL**1
00565                      ,CHAR(SETUP_DTE, USA)                           CL**1
00566                      ,CHAR(LAST_CALL_DTE, USA)                       CL**1
00567                      ,CHAR(NEXT_CALL_DTE, USA)                       CL**1
00568                      ,CHAR(VER_CHG_DTE, USA)                         CL*10
                           ,COM_MTHD_CDE                                        
00569                 INTO :TRDTRN-FUNC-CDE                                CL**1
00570                     ,:TRDTRN-VER-NBR                                 CL**1
00571                     ,:TRDTRN-ACTV-DTE                                CL**1
00572                     ,:TRDTRN-INACTV-DTE                              CL**1
00573                     ,:TRDTRN-NOTES-TXT                               CL**1
00574                     ,:TRDTRN-LAST-CHG-DTE                            CL**1
00575                     ,:TRDTRN-LAST-CHG-UID                            CL**1
00576                     ,:TRDTRN-SETUP-DTE                               CL**1
00577                     ,:TRDTRN-LAST-CALL-DTE                           CL**1
00578                     ,:TRDTRN-NEXT-CALL-DTE                           CL**1
00579                     ,:TRDTRN-VER-CHG-DTE                             CL*10
                          ,:TRDTRN-COM-MTHD-CDE                                 
00580                 FROM TTRDTRN                                         CL**1
00581                WHERE TP_DKEY       = :DK-TP-DKEY                     CL**1
00582                  AND TRAN_SET_CDE  = :DK-TRAN-SET-CDE                CL**1
00583                  AND PROD_TEST_IND = :DK-PROD-TEST-IND               CL**1
00584                  AND DIR_IND       = :DK-DIR-IND                     CL**1
00585          END-EXEC                                                    CL**1
00586                                                                      CL**1
00587          EVALUATE TRUE                                               CL**1
00588              WHEN SQLCODE = +0                                       CL**1
00589              WHEN SQLCODE = +100                                     CL**9
00590              WHEN SQLCODE = -904                                     CL**1
00591              WHEN SQLCODE = -913                                     CL**1
00592                   CONTINUE                                           CL**1
00593                                                                      CL**1
00594              WHEN SQLWARN0 NOT = SPACES                              CL**1
00595              WHEN SQLCODE < +0                                       CL**1
00596              WHEN SQLCODE > +0                                       CL**1
00597                   MOVE '4010-GET-TRDTRN'                             CL**1
00598                                   TO DP013-PARAGRAPH                 CL**1
00599                   MOVE 'UNSUCCESSFUL SELECT TRDTRN'                  CL**1
00600                                   TO DP013-MESSAGE-TEXT (1)          CL**1
00601                   MOVE SQLCA      TO DP013-SQLCA                     CL**1
00602                   MOVE 'TRDTRN  ' TO DP013-DB2-TABLE-NAME (1)        CL**1
00603                   SET DP013-DB2-ABEND                                CL**1
00604                       DP013-XCTL-DISPLAY-RESTART                     CL**1
00605                                   TO TRUE                            CL**1
00606                   PERFORM DP013-0000-PROCESS-ABEND                   CL**1
00607          END-EVALUATE                                                CL**1
00608      END-PERFORM.                                                    CL**1
00609                                                                      CL**1
00610      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                           CL**1
00611          MOVE '4010-GET-TRDTRN'   TO DP013-PARAGRAPH                 CL**1
00612          MOVE 'MAX RETRIES EXCEEDED ON SELECT'                       CL**1
00613                                   TO DP013-MESSAGE-TEXT (1)          CL**1
00614          MOVE SQLCA               TO DP013-SQLCA                     CL**1
00615          MOVE 'TTRDTRN '          TO DP013-DB2-TABLE-NAME (1)        CL**1
00616          SET DP013-DB2-ABEND                                         CL**1
00617              DP013-XCTL-DISPLAY-RESTART                              CL**1
00618                                   TO TRUE                            CL**1
00619          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
00620      END-IF.                                                         CL**1
00621                                                                      CL**1
00622      IF SQLCODE = +0                                                 CL**9
00623                                                                      CL**9
00624          PERFORM 4020-GET-TRAN-DESC                                  CL**9
00625          PERFORM 4030-GET-VERSION-NAME                               CL**9
00626                                                                      CL**9
00627          MOVE TRDTRN-FUNC-CDE       TO ASC-FUNC-CDE                  CL**9
00628          MOVE TRDTRN-VER-NBR        TO ASC-VER-NBR                   CL**9
00629          MOVE TRDTRN-ACTV-DTE       TO ASC-ACTV-DTE                  CL**9
00630          MOVE TRDTRN-INACTV-DTE     TO ASC-INACTV-DTE                CL**9
00631          MOVE TRDTRN-NOTES-TXT      TO ASC-NOTES-TXT                 CL**9
00632          MOVE TRDTRN-LAST-CHG-DTE   TO ASC-LAST-CHG-DTE              CL**9
00633          MOVE TRDTRN-LAST-CHG-UID   TO ASC-LAST-CHG-UID              CL**9
00634          MOVE TRDTRN-SETUP-DTE      TO ASC-SETUP-DTE                 CL**9
00635          MOVE TRDTRN-LAST-CALL-DTE  TO ASC-LAST-CALL-DTE             CL**9
00636          MOVE TRDTRN-NEXT-CALL-DTE  TO ASC-NEXT-CALL-DTE             CL**9
00637          MOVE TRDTRN-VER-CHG-DTE    TO ASC-VER-CHG-DTE               CL*10
               MOVE TRDTRN-COM-MTHD-CDE   TO ASC-COM-MTHD                       
00638          MOVE TRNVER-VER-NME        TO ASC-VER-NME                   CL**9
00639          MOVE TRNSET-TRAN-SET-NME   TO ASC-TRAN-SET-NME              CL**9
00640                                                                      CL**9
00641          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                        CL**9
00642                                                                      CL**9
00643      ELSE                                                            CL**9
00644          MOVE PC-TSYMSG-00148       TO DP020-MSG-NUMBER              CL**9
00645          SET PS-ERROR                                                CL**9
00646              DP020-MSG-FATAL                                         CL**9
00647              DP020-NEXT-ACT-APPL-ERROR                               CL**9
00648                                     TO TRUE                          CL**9
00649      END-IF.                                                         CL**9
00650 /                                                                    CL**1
00651 *----------------------------------------------------------------*   CL**1
00652 * SELECT A ROW FROM THE DB2 TPURITM TABLE.                       *   CL**1
00653 *----------------------------------------------------------------*   CL**1
00654  4020-GET-TRAN-DESC.                                                 CL**8
00655                                                                      CL**1
00656      PERFORM                                                         CL**1
00657          WITH TEST AFTER                                             CL**1
00658          VARYING PV-RETRY-COUNTER                                    CL**1
00659          FROM 1 BY 1                                                 CL**1
00660          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                  CL**1
00661              OR (SQLCODE = +0))                                      CL**1
00662                                                                      CL**1
00663          EXEC SQL                                                    CL**1
00664               SELECT   TRAN_SET_NME                                  CL**8
00665                 INTO  :TRNSET-TRAN-SET-NME                           CL**7
00666                 FROM TTRNSET                                         CL**7
00667                WHERE TRAN_SET_CDE =:DK-TRAN-SET-CDE                  CL**8
00668          END-EXEC                                                    CL**1
00669                                                                      CL**1
00670          EVALUATE TRUE                                               CL**1
00671              WHEN SQLCODE = +0                                       CL**1
00672              WHEN SQLCODE = -904                                     CL**1
00673              WHEN SQLCODE = -913                                     CL**1
00674                   CONTINUE                                           CL**1
00675                                                                      CL**1
00676              WHEN SQLWARN0 NOT = SPACES                              CL**1
00677              WHEN SQLCODE < +0                                       CL**1
00678              WHEN SQLCODE > +0                                       CL**1
00679                   MOVE '4020-GET-TRNSET'                             CL**7
00680                                   TO DP013-PARAGRAPH                 CL**1
00681                   MOVE 'UNSUCCESSFUL SELECT'                         CL**1
00682                                   TO DP013-MESSAGE-TEXT (1)          CL**1
00683                   MOVE SQLCA      TO DP013-SQLCA                     CL**1
00684                   MOVE 'TRNSET  ' TO DP013-DB2-TABLE-NAME (1)        CL**8
00685                   SET DP013-DB2-ABEND                                CL**1
00686                       DP013-XCTL-DISPLAY-RESTART                     CL**1
00687                                   TO TRUE                            CL**1
00688                   PERFORM DP013-0000-PROCESS-ABEND                   CL**1
00689          END-EVALUATE                                                CL**1
00690      END-PERFORM.                                                    CL**1
00691                                                                      CL**1
00692      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                           CL**1
00693          MOVE '4020-GET-TTRNSET'  TO DP013-PARAGRAPH                 CL**7
00694          MOVE 'MAX RETRIES EXCEEDED ON SELECT'                       CL**1
00695                                   TO DP013-MESSAGE-TEXT (1)          CL**1
00696          MOVE SQLCA               TO DP013-SQLCA                     CL**1
00697          MOVE 'TTRNSET '          TO DP013-DB2-TABLE-NAME (1)        CL**7
00698          SET DP013-DB2-ABEND                                         CL**1
00699              DP013-XCTL-DISPLAY-RESTART                              CL**1
00700                                   TO TRUE                            CL**1
00701          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
00702      END-IF.                                                         CL**1
00703                                                                      CL**1
00704 /                                                                    CL**2
00705  4030-GET-VERSION-NAME.                                              CL**2
00706                                                                      CL**2
00707      PERFORM                                                         CL**2
00708          WITH TEST AFTER                                             CL**2
00709          VARYING PV-RETRY-COUNTER                                    CL**2
00710          FROM 1 BY 1                                                 CL**2
00711          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                  CL**2
00712              OR (SQLCODE = +0))                                      CL**2
00713                                                                      CL**2
00714          EXEC SQL                                                    CL**2
00715               SELECT   VER_NME                                       CL**2
00716                 INTO  :TRNVER-VER-NME                                CL**2
00717                 FROM TTRNVER                                         CL**2
00718                WHERE TRAN_SET_CDE = :DK-TRAN-SET-CDE                 CL**2
00719                  AND VER_NBR      = :TRDTRN-VER-NBR                  CL**2
00720          END-EXEC                                                    CL**2
00721                                                                      CL**2
00722          EVALUATE TRUE                                               CL**2
00723              WHEN SQLCODE = +0                                       CL**2
00724              WHEN SQLCODE = -904                                     CL**2
00725              WHEN SQLCODE = -913                                     CL**2
00726                   CONTINUE                                           CL**2
00727                                                                      CL**2
00728              WHEN SQLWARN0 NOT = SPACES                              CL**2
00729              WHEN SQLCODE < +0                                       CL**2
00730              WHEN SQLCODE > +0                                       CL**2
00731                   MOVE '4030-GET-TRNVER'                             CL**2
00732                                   TO DP013-PARAGRAPH                 CL**2
00733                   MOVE 'UNSUCCESSFUL SELECT'                         CL**2
00734                                   TO DP013-MESSAGE-TEXT (1)          CL**2
00735                   MOVE SQLCA      TO DP013-SQLCA                     CL**2
00736                   MOVE 'TRDTRN  ' TO DP013-DB2-TABLE-NAME (1)        CL**2
00737                   SET DP013-DB2-ABEND                                CL**2
00738                       DP013-XCTL-DISPLAY-RESTART                     CL**2
00739                                   TO TRUE                            CL**2
00740                   PERFORM DP013-0000-PROCESS-ABEND                   CL**2
00741          END-EVALUATE                                                CL**2
00742      END-PERFORM.                                                    CL**2
00743                                                                      CL**2
00744      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                           CL**2
00745          MOVE '4030-GET-TTRNVER'  TO DP013-PARAGRAPH                 CL**2
00746          MOVE 'MAX RETRIES EXCEEDED ON SELECT'                       CL**2
00747                                   TO DP013-MESSAGE-TEXT (1)          CL**2
00748          MOVE SQLCA               TO DP013-SQLCA                     CL**2
00749          MOVE 'TTRNVER '          TO DP013-DB2-TABLE-NAME (1)        CL**2
00750          SET DP013-DB2-ABEND                                         CL**2
00751              DP013-XCTL-DISPLAY-RESTART                              CL**2
00752                                   TO TRUE                            CL**2
00753          PERFORM DP013-0000-PROCESS-ABEND                            CL**2
00754      END-IF.                                                         CL**2
00755                                                                      CL**2
00756 /                                                                    CL**1
00757 *----------------------------------------------------------------*   CL**1
00758 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *   CL**1
00759 *----------------------------------------------------------------*   CL**1
00760                                                                      CL**1
00761  4400-MOVE-COMMAREA-TO-SCREEN.                                       CL**1
00762                                                                      CL**1
00763      MOVE ASC-VENDOR-NAME                     TO VNDRNMEO            CL**1
00764      MOVE ASC-VEND-INCHG-QUAL                 TO VTPIDQO             CL**1
00765      MOVE ASC-VEND-INCHG-ID                   TO VTPIDO              CL**1
00766      MOVE ASC-NETWK-ID                        TO NTWKCDEO            CL**1
00767      MOVE ASC-NETWORK-NAME                    TO NTWKNMEO            CL**1
00768      MOVE ASC-KEY-TRAN-SET-CDE                TO TSETO               CL**2
00769      MOVE ASC-TRAN-SET-NME                    TO TSETNMEO            CL**7
           MOVE ASC-COM-MTHD                        TO COMMTHO                  
00770      MOVE ASC-KEY-PROD-TEST-IND               TO PRDTSTO             CL**2
00771      MOVE ASC-KEY-DIR-IND                     TO DIRO                CL**2
00772                                                                      CL**1
           IF ASC-COM-MTHD = 'V'                                                
              MOVE 'VAN  ' TO COMNMEO                                           
           ELSE                                                                 
              IF ASC-COM-MTHD = 'F'                                             
                 MOVE 'FTP  ' TO COMNMEO                                        
              ELSE                                                              
                 IF ASC-COM-MTHD = 'E'                                          
                    MOVE 'EMAIL' TO COMNMEO                                     
                 ELSE                                                           
SP0403              IF ASC-COM-MTHD = 'A'                                       
SP0403                 MOVE 'AS2  ' TO COMNMEO                                  
SP0403              ELSE                                                        
                       MOVE SPACES TO COMNMEO.                                  
                                                                                
00773      MOVE ASC-VER-NBR                         TO VERNBRO             CL**2
00774      MOVE ASC-VER-NME                         TO VERNMEO             CL**2
00775      MOVE ASC-VER-CHG-DTE                     TO VERDTEO             CL*10
00776                                                                      CL**2
00777      IF ASC-VER-CHG-DTE = PC-NULL-DTE                                CL*10
00778          MOVE ASC-SETUP-DTE                   TO VERDTEO             CL*10
00779      END-IF                                                          CL*10
00780                                                                      CL*10
00781      MOVE ASC-SETUP-DTE                       TO SETDTEO             CL**2
00782                                                                      CL*16
00783      IF ASC-LAST-CHG-DTE = PC-NULL-DTE                               CL*17
00784          MOVE SPACES                          TO LCHGDTEO            CL*16
00785      ELSE                                                            CL*16
00786          MOVE ASC-LAST-CHG-DTE                TO LCHGDTEO            CL*16
00787      END-IF                                                          CL*16
00788                                                                      CL*16
00789      MOVE ASC-LAST-CHG-UID                    TO LCHGUIDO            CL**2
00790                                                                      CL**2
00791      IF ASC-ACTV-DTE = PC-NULL-DTE                                   CL**2
00792          MOVE SPACES                          TO ACTVDTEO            CL**2
00793      ELSE                                                            CL**2
00794          MOVE ASC-ACTV-DTE                    TO ACTVDTEO            CL**2
00795      END-IF                                                          CL**2
00796                                                                      CL**2
00797      IF ASC-INACTV-DTE = PC-NULL-DTE                                 CL**2
00798          MOVE SPACES                          TO INACTDTO            CL**2
00799      ELSE                                                            CL**2
00800          MOVE ASC-INACTV-DTE                  TO INACTDTO            CL**2
00801      END-IF                                                          CL**2
00802                                                                      CL**2
00803      IF ASC-LAST-CALL-DTE = PC-NULL-DTE                              CL**2
00804          MOVE SPACES                          TO LCALDTEO            CL**2
00805      ELSE                                                            CL**2
00806          MOVE ASC-LAST-CALL-DTE               TO LCALDTEO            CL**2
00807      END-IF                                                          CL**2
00808                                                                      CL**2
00809      IF ASC-NEXT-CALL-DTE = PC-NULL-DTE                              CL**2
00810          MOVE SPACES                          TO NCALDTEO            CL**2
00811      ELSE                                                            CL**2
00812          MOVE ASC-NEXT-CALL-DTE               TO NCALDTEO            CL**2
00813      END-IF                                                          CL**2
00814                                                                      CL**2
00815      MOVE ASC-NOTES-TXT1                      TO NOTE1O              CL**2
00816      MOVE ASC-NOTES-TXT2                      TO NOTE2O.             CL**2
00817                                                                      CL**2
00818 /                                                                    CL**1
00819 *----------------------------------------------------------------*   CL**1
00820 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *   CL**1
00821 *----------------------------------------------------------------*   CL**1
00822                                                                      CL**1
00823  9100-READ-SEL-QUEUE.                                                CL**1
00824      EXEC CICS                                                       CL**1
00825          READQ TS                                                    CL**1
00826              QUEUE (ASC-SEL-QUEUE-NAME)                              CL**1
00827              INTO  (ET301-SELECTED-VNDRS)                            CL**1
00828              ITEM  (ASC-SEL-ITEM)                                    CL**1
00829              RESP  (PV-CICS-RESPONSE)                                CL**1
00830      END-EXEC.                                                       CL**1
00831                                                                      CL**1
00832      IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                      CL**1
00833          CONTINUE                                                    CL**1
00834      ELSE                                                            CL**1
00835         IF  PV-CICS-RESPONSE EQUAL DFHRESP(QIDERR)                   CL**1
00836             MOVE PC-TSYMSG-00057 TO DP020-MSG-NUMBER                 CL**1
00837             SET DP020-MSG-WARNING TO TRUE                            CL**1
00838             SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                    CL**1
00839         ELSE                                                         CL**1
00840          SET DP013-LOGIC-ABEND                                       CL**1
00841              DP013-NO-ROLLBACK     TO TRUE                           CL**1
00842          MOVE '9100-READ-SEL-QUEUE'                                  CL**1
00843                                    TO DP013-PARAGRAPH                CL**1
00844          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'      CL**1
00845                                    TO DP013-MESSAGE-TEXT (1)         CL**1
00846          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
00847         END-IF                                                       CL**1
00848      END-IF.                                                         CL**1
00849                                                                      CL**1
00850 /                                                                    CL**1
00851 *----------------------------------------------------------------*   CL**1
00852 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *   CL**1
00853 *----------------------------------------------------------------*   CL**1
00854  9200-REWRITE-LIST-QUEUE.                                            CL**1
00855                                                                      CL**1
00856      EXEC CICS                                                       CL**1
00857           WRITEQ TS                                                  CL**1
00858               QUEUE  (ASC-SEL-QUEUE-NAME)                            CL**1
00859               FROM (ET301-SELECTED-VNDRS)                            CL**1
00860               ITEM   (ASC-SEL-ITEM)                                  CL**1
00861               RESP   (PV-CICS-RESPONSE)                              CL**1
00862               REWRITE                                                CL**1
00863      END-EXEC.                                                       CL**1
00864                                                                      CL**1
00865      IF  PV-CICS-RESPONSE NOT EQUAL DFHRESP(NORMAL)                  CL**1
00866          SET DP013-CICS-ABEND                                        CL**1
00867              DP013-NO-ROLLBACK     TO TRUE                           CL**1
00868          MOVE '9200-REWRITE-LIST-QUEUE'                              CL**1
00869                                    TO DP013-PARAGRAPH                CL**1
00870          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'      CL**1
00871                                    TO DP013-MESSAGE-TEXT (1)         CL**1
00872          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
00873      END-IF.                                                         CL**1
00874                                                                      CL**1
00875 *----------------------------------------------------------------*   CL**1
00876 *    ABEND PROCESSOR MODULE                                      *   CL**1
00877 *----------------------------------------------------------------*   CL**1
00878                                                                      CL**1
00879      COPY DPPD013.                                                   CL**1
