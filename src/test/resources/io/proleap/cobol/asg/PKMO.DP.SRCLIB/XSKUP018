       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP018.                                         00020045
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/27/2002.                                       00050043
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **       MERCHANDISE HIERARCHY MESSAGE FOR TABLE XST_SUB_CL       00080036
      ******************************************************************00090024
      **THIS IS A CALLED MODULE FOR SUBCLASS MERCH HIER TABLES.  THIS   00100036
      **     PROGRAM WILL PERFORM UPDATES/INSERTS/DELETES FOR THIS      00110036
      **     TABLE.  IT DOES THE FOLLOWING WITH THESE RETURN CODES:     00120036
      **     0000 - END PROGRAM                                         00130024
      **    -0530 - END PROGRAM                                         00140024
      **    -0803 - COMPARE TIMESTAMPS AND UPDATE IF MESSAGE IS NEWER   00150024
      **    -0911 - RETRY INSERT UP TO 5 TIMES AND THEN END PROGRAM     00160024
      ******************************************************************00170000
      **09/27/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00180043
010103**01/01/2003 CHANGED BY:JULIE SMITH TIMESTAMP ADDITION            00190056
      **05/13/2005 CHANGED BY: NANCY MARTIN WR26916                     00191059
      **                       ADDED SUB CLASS ATTR TABLE               00192059
      ******************************************************************00200000
       ENVIRONMENT DIVISION.                                            00210000
       CONFIGURATION SECTION.                                           00220000
       SOURCE-COMPUTER.  IBM-3090.                                      00230000
       OBJECT-COMPUTER.  IBM-3090.                                      00240000
       DATA DIVISION.                                                   00250000
                                                                        00260017
       WORKING-STORAGE SECTION.                                         00270017
      ******************************************************************00280000
      **PROGRAM SWITCHES                                                00290000
      ******************************************************************00300000
       01  PS-PROGRAM-SWITCHES.                                         00310000
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00320020
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00330018
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00340018
      ******************************************************************00350000
      **PROGRAM COUNTERS                                                00360018
      ******************************************************************00370000
       01  PC-PROGRAM-COUNTERS.                                         00380018
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00390018
           05  PC-RETRY-COUNTER-2          PIC 9(01)  VALUE ZEROS.      00400019
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00410024
      ******************************************************************00420018
      **PROGRAM VARIABLES                                               00430018
      ******************************************************************00440018
       01  PV-PROGRAM-VARIABLES.                                        00450018
           05  PV-PARAGRAPH-NAME               PIC X(20)  VALUE SPACES. 00460019
           05  PV-TIME-EVALUATE-INSERT.                                 00470019
               10  PV-TIME-HOUR-IN             PIC  9(02) VALUE ZEROS.  00480019
               10  PV-TIME-MINUTE-IN           PIC  9(02) VALUE ZEROS.  00490019
               10  PV-TIME-SECOND-IN           PIC  9(02) VALUE ZEROS.  00500019
               10  PV-TIME-MICROSEC-IN         PIC  9(06) VALUE ZEROS.  00510019
           05  PV-TIME-EVALUATE-UPDATE.                                 00520019
               10  PV-TIME-HOUR-UP             PIC  9(02) VALUE ZEROS.  00530019
               10  PV-TIME-MINUTE-UP           PIC  9(02) VALUE ZEROS.  00540019
               10  PV-TIME-SECOND-UP           PIC  9(02) VALUE ZEROS.  00550019
               10  PV-TIME-MICROSEC-UP         PIC  9(06) VALUE ZEROS.  00560019
      ******************************************************************00570018
      **TIMESTAMP VARIABLES                                             00580018
      ******************************************************************00590018
       01  SD-TIMESTAMP.                                                00600058
           05  SD-CURR-DTE                     PIC  X(10).              00610018
           05  FILLER                          PIC  X(01) VALUE '-'.    00620058
           05  SD-HOUR                         PIC  9(02).              00630058
           05  FILLER                          PIC  X(01) VALUE '.'.    00640058
           05  SD-MINUTE                       PIC  9(02).              00650058
           05  FILLER                          PIC  X(01) VALUE '.'.    00660058
           05  SD-SECOND                       PIC  9(02).              00670058
           05  FILLER                          PIC  X(01) VALUE '.'.    00680058
           05  SD-MICROSEC                     PIC  9(06).              00690058
      ******************************************************************00700000
      **COPYBOOKS                                                       00710000
      ******************************************************************00720000
      ***DB2 MESSAGE AREA                                               00730000
           COPY SQLCA2.                                                 00740059
      ***MESSAGE COPYBOOK                                               00750018
           COPY HMRD001.                                                00760059
      ******************************************************************00770000
      **TABLE INCLUSIONS                                                00780000
      ******************************************************************00790000
      ***DB2 COMMUNICATIONS AREA                                        00800000
           EXEC SQL                                                     00810000
                INCLUDE SQLCA                                           00820000
           END-EXEC.                                                    00830000
      ***XST_SUB_CL TABLE                                               00840041
           EXEC SQL                                                     00850000
                INCLUDE XST00014                                        00860041
           END-EXEC.                                                    00870000
      ***XST_SUB_CL_ATTR TABLE                                          00871059
           EXEC SQL                                                     00872059
                INCLUDE XST00055                                        00873059
           END-EXEC.                                                    00874059
                                                                        00880000
       LINKAGE SECTION.                                                 00890000
      ******************************************************************00900000
      **LINKAGE VARIABLES                                               00910000
      ******************************************************************00920000
       01  LV-HMRD001-RECORD               PIC X(500).                  00930041
       01  LV-RETURN-CODE                  PIC S9(04).                  00940017
010103 01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00950056
                                                                        00960024
       PROCEDURE DIVISION USING LV-HMRD001-RECORD,                      00970036
                                LV-RETURN-CODE,                         00980056
010103                          LV-CURRENT-TIMESTAMP.                   00990056
      ******************************************************************01000000
       0000-MAINLINE.                                                   01010000
                                                                        01020000
            PERFORM 1000-INITIALIZATION.                                01030000
            PERFORM 2000-PROCESS-MESSAGE                                01040037
                    UNTIL PS-PROCESS-COMPLETE.                          01050018
            PERFORM 9999-WRAPUP.                                        01060000
                                                                        01070000
       EJECT.                                                           01080018
      ***************************************************************** 01090000
      **INITIALIZATION AREA - ALL MOVES TO UPDATE/INSERT                01100024
      ***************************************************************** 01110000
       1000-INITIALIZATION.                                             01120000
                                                                        01130000
           MOVE '1000-INITIALIZATION'      TO PV-PARAGRAPH-NAME.        01140018
                                                                        01150018
           SET PS-PROCESS-CONTINUE         TO TRUE.                     01160020
           INITIALIZE PC-RETRY-COUNTER.                                 01170018
           INITIALIZE SD-TIMESTAMP.                                     01180042
                                                                        01190023
           MOVE LV-HMRD001-RECORD           TO HMRD001-RECORD.          01200045
           MOVE HM001-SCLATTR-DEPT          TO XST00014-DEPT-NBR,       01210059
                                               XST00055-DEPT-NBR.       01211059
           MOVE HM001-SCLATTR-CLASS         TO XST00014-MAJ-CL-NBR,     01220059
                                               XST00055-MAJ-CL-NBR.     01221059
           MOVE HM001-SCLATTR-SUBCLASS      TO XST00014-SUB-CL-NBR,     01230059
                                               XST00055-SUB-CL-NBR.     01231059
           MOVE HM001-SCLATTR-POS-MDSE-DESC TO XST00014-POS-MDSE-DESC.  01240045
           MOVE HM001-SCLATTR-TX-DFLT-IND   TO XST00014-TAX-DFLT-IND.   01250045
           MOVE HM001-SCLATTR-DISC-ELIG-IND TO XST00014-DISC-ELIG-IND.  01260045
                                                                        01270042
010103     MOVE LV-CURRENT-TIMESTAMP        TO XST00014-LAST-UPD-TMST,  01280059
                                               XST00055-LAST-UPD-TMST.  01281061
010103     MOVE LV-CURRENT-TIMESTAMP        TO XST00014-CRE-TMST.       01290056
                                                                        01300057
010103     MOVE HM001-CRE-TMST          TO SD-CURR-DTE.                 01310058
010103     MOVE SD-TIMESTAMP            TO XST00014-SOR-LAST-MSG-TMST.  01320057
           INITIALIZE SD-TIMESTAMP.                                     01330057
                                                                        01340042
       EJECT.                                                           01350038
      ***************************************************************** 01360001
      **MAIN PROGRAM LOGIC - READS ACTION TYPE ON HMRD001 RECORD        01370044
      ***************************************************************** 01380001
       2000-PROCESS-MESSAGE.                                            01390037
                                                                        01400000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01410037
                                                                        01420017
           EVALUATE TRUE                                                01430037
              WHEN HM001-ACTN-TYP-INSERT                                01440037
                   PERFORM 2200-UPDATE-DATA                             01450045
              WHEN HM001-ACTN-TYP-UPDATE                                01460037
                   PERFORM 2200-UPDATE-DATA                             01470037
              WHEN HM001-ACTN-TYP-DELETE                                01480037
                   PERFORM 2300-DELETE-DATA                             01490039
           END-EVALUATE.                                                01500037
                                                                        01510037
       EJECT.                                                           01520037
      ***************************************************************** 01530037
      **INSERT ACTION TYPE                                              01540044
      ***************************************************************** 01550037
       2100-INSERT-DATA.                                                01560037
                                                                        01570037
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01580037
                                                                        01590037
           EXEC SQL                                                     01600018
                INSERT INTO XST_SUB_CL (DEPT_NBR                        01610041
                                      , MAJ_CL_NBR                      01620041
                                      , SUB_CL_NBR                      01630041
                                      , SUB_CL_NM                       01640041
                                      , POS_MDSE_DESC                   01650041
                                      , TAX_DFLT_IND                    01660041
                                      , DISC_ELIG_IND                   01670041
                                      , CRE_TMST                        01680041
                                      , LAST_UPD_TMST                   01690056
010103                                , SOR_LAST_MSG_TMST)              01700056
                VALUES (:XST00014-DEPT-NBR                              01710041
                      , :XST00014-MAJ-CL-NBR                            01720041
                      , :XST00014-SUB-CL-NBR                            01730041
                      , 'NOT AVAILABLE'                                 01740056
                      , :XST00014-POS-MDSE-DESC                         01750041
                      , :XST00014-TAX-DFLT-IND                          01760041
                      , :XST00014-DISC-ELIG-IND                         01770041
                      , :XST00014-CRE-TMST                              01780041
                      , :XST00014-LAST-UPD-TMST                         01790056
010103                , :XST00014-SOR-LAST-MSG-TMST)                    01800056
           END-EXEC.                                                    01810018
                                                                        01820004
           EVALUATE TRUE                                                01830020
      ***SUCCESSFUL PROCESSING                                          01840056
              WHEN SQLCODE = ZEROS                                      01850020
                   SET PS-PROCESS-COMPLETE   TO TRUE                    01860018
                   PERFORM 2125-INSERT-ATTR                             01861059
      ***REFERENTIAL INTEGRITY ISSUES                                   01870056
              WHEN SQLCODE = -530                                       01880020
              WHEN SQLCODE = -532                                       01890056
                   SET PS-PROCESS-COMPLETE   TO TRUE                    01900018
      ***ROW/TABLE LOCK                                                 01910056
              WHEN SQLCODE = -803                                       01920059
      ***ROW/TABLE LOCK                                                 01921059
              WHEN SQLCODE = -913                                       01922059
              WHEN SQLCODE = -911                                       01930028
                   ADD +1                      TO PC-RETRY-COUNTER      01940027
                   IF  PC-RETRY-COUNTER >= PC-RETRY-MAX                 01950031
                       SET PS-PROCESS-COMPLETE TO TRUE                  01960018
                   ELSE                                                 01970018
                       ADD +1                  TO PC-RETRY-COUNTER      01980027
                   END-IF                                               01990018
              WHEN OTHER                                                02000018
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02010018
           END-EVALUATE.                                                02020018
                                                                        02030001
       EJECT.                                                           02040018
      ***************************************************************** 02041059
      **INSERT ATTRIBUTE RECORD - DEFAULT EXCL IND TO 'N'               02042059
      ***************************************************************** 02043059
       2125-INSERT-ATTR.                                                02044059
                                                                        02045059
           MOVE '2125-INSERT-ATTR' TO PV-PARAGRAPH-NAME.                02046059
                                                                        02047059
           EXEC SQL                                                     02048059
                INSERT INTO XST_SUB_CL_ATTR                             02049059
                                 (DEPT_NBR                              02049159
                                , MAJ_CL_NBR                            02049259
                                , SUB_CL_NBR                            02049359
                                , PLU_DWNLD_EXCL_IND                    02049459
                                , LAST_UPD_TMST)                        02049561
                VALUES (:XST00055-DEPT-NBR                              02049659
                      , :XST00055-MAJ-CL-NBR                            02049759
                      , :XST00055-SUB-CL-NBR                            02049859
                      , 'N'                                             02049959
                      , :XST00055-LAST-UPD-TMST)                        02050061
           END-EXEC.                                                    02050159
                                                                        02050259
           EVALUATE TRUE                                                02050359
      ***SUCCESSFUL PROCESSING                                          02050459
              WHEN SQLCODE = ZEROS                                      02050559
      ***ROW EXISTS ON TABLE  (DON'T CARE AS LONG AS ROW EXISTS         02050659
                OR SQLCODE = -803                                       02050759
                   SET PS-PROCESS-COMPLETE   TO TRUE                    02050859
      ***REFERENTIAL INTEGRITY ISSUES                                   02050959
              WHEN SQLCODE = -530                                       02051059
              WHEN SQLCODE = -532                                       02051159
                   SET PS-PROCESS-COMPLETE   TO TRUE                    02051259
      ***ROW/TABLE LOCK                                                 02051359
              WHEN SQLCODE = -913                                       02051459
              WHEN SQLCODE = -911                                       02051559
                   IF  PC-RETRY-COUNTER >= PC-RETRY-MAX                 02051659
                       SET PS-PROCESS-COMPLETE TO TRUE                  02051759
                   ELSE                                                 02051859
                       ADD +1                  TO PC-RETRY-COUNTER      02051959
                   END-IF                                               02052059
              WHEN OTHER                                                02052159
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02052259
           END-EVALUATE.                                                02052359
                                                                        02052459
       EJECT.                                                           02052559
      ******************************************************************02053019
      **UPDATE ACTION TYPE                                              02060044
      ******************************************************************02070019
       2200-UPDATE-DATA.                                                02080019
                                                                        02090019
           MOVE '2200-UPDATE-DATA' TO PV-PARAGRAPH-NAME                 02100019
                                                                        02110019
           EXEC SQL                                                     02120019
              UPDATE XST_SUB_CL                                         02130038
                  SET  POS_MDSE_DESC  = :XST00014-POS-MDSE-DESC         02140046
                     , TAX_DFLT_IND   = :XST00014-TAX-DFLT-IND          02150046
                     , DISC_ELIG_IND  = :XST00014-DISC-ELIG-IND         02160046
                     , LAST_UPD_TMST  = :XST00014-LAST-UPD-TMST         02170046
010103               , SOR_LAST_MSG_TMST  = :XST00014-SOR-LAST-MSG-TMST 02180056
                  WHERE DEPT_NBR      = :XST00014-DEPT-NBR              02190038
                    AND MAJ_CL_NBR    = :XST00014-MAJ-CL-NBR            02200041
                    AND SUB_CL_NBR    = :XST00014-SUB-CL-NBR            02210041
           END-EXEC.                                                    02220019
                                                                        02230019
           EVALUATE TRUE                                                02240020
      ***SUCCESSFUL PROCESSING                                          02250056
              WHEN SQLCODE = ZEROS                                      02260020
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02270019
      ***ROW DOES NOT EXIST                                             02280056
              WHEN SQLCODE = +100                                       02290046
                   PERFORM 2100-INSERT-DATA                             02300046
                           UNTIL PS-PROCESS-COMPLETE                    02310046
      ***ROW/TABLE LOCK                                                 02320056
              WHEN SQLCODE = -911                                       02330020
              WHEN SQLCODE = -913                                       02340053
                   IF  PC-RETRY-COUNTER-2 >= PC-RETRY-MAX               02350035
                       SET PS-PROCESS-COMPLETE TO TRUE                  02360019
                   ELSE                                                 02370019
                       ADD +1                  TO PC-RETRY-COUNTER-2    02380024
                   END-IF                                               02390019
              WHEN OTHER                                                02400019
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02410038
           END-EVALUATE.                                                02420019
                                                                        02430019
       EJECT.                                                           02440019
      ******************************************************************02450039
      **DELETE TYPE ACTION                                              02460044
      **       THERE IS A CASCADE DELETE ON SUB CLASS ATTRIBUTE         02461059
      ******************************************************************02470039
       2300-DELETE-DATA.                                                02480039
                                                                        02490039
           MOVE '2300-DELETE-DATA' TO PV-PARAGRAPH-NAME                 02500039
                                                                        02510039
           EXEC SQL                                                     02520041
                DELETE FROM XST_SUB_CL                                  02530041
                WHERE DEPT_NBR   = :XST00014-DEPT-NBR                   02540041
                  AND MAJ_CL_NBR = :XST00014-MAJ-CL-NBR                 02550041
                  AND SUB_CL_NBR = :XST00014-SUB-CL-NBR                 02560041
           END-EXEC.                                                    02570041
                                                                        02580041
           EVALUATE TRUE                                                02590041
      ***SUCCESSFUL PROCESSING                                          02600056
              WHEN SQLCODE = ZEROS                                      02610041
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02620041
      ***ROW DOES NOT EXIST                                             02630056
              WHEN SQLCODE = +100                                       02640046
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02650046
      ***ROW/TABLE LOCK                                                 02660056
              WHEN SQLCODE = -911                                       02670041
              WHEN SQLCODE = -913                                       02680055
                   IF  PC-RETRY-COUNTER-2 >= PC-RETRY-MAX               02690041
                       SET PS-PROCESS-COMPLETE TO TRUE                  02700041
                   ELSE                                                 02710041
                       ADD +1                  TO PC-RETRY-COUNTER-2    02720041
                   END-IF                                               02730041
              WHEN OTHER                                                02740041
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02750041
           END-EVALUATE.                                                02760041
                                                                        02770041
       EJECT.                                                           02780039
      ******************************************************************02790000
      **PASS SQLCODE TO CALLING AND EXIT PROGRAM                        02800024
      ******************************************************************02810000
       9999-WRAPUP.                                                     02820000
                                                                        02830000
           MOVE '9999-WRAPUP' TO PV-PARAGRAPH-NAME                      02840018
                                                                        02850000
           MOVE SQLCODE       TO LV-RETURN-CODE.                        02860018
           DISPLAY 'XSKUP018 - DEPT NBR: ' XST00014-DEPT-NBR            02870049
                           ' MAJ CLASS: ' XST00014-MAJ-CL-NBR           02880048
                           ' SUB CLASS: ' XST00014-SUB-CL-NBR           02890048
                            ' WITH RETURN-CODE ' LV-RETURN-CODE.        02900048
           EXIT PROGRAM.                                                02910000
                                                                        02920000
       EJECT.                                                           02930018
