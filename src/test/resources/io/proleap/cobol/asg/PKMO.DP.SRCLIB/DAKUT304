      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.      DAKUT304.                                               
       AUTHOR.          BOB MCASEY.                                             
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    06/15/2000                                              
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *    ****** NOTE!! - THIS PROGRAM IS CALLED BY A THE FOLLOWING  *         
      *    ****** PROGRAMS USING COPYBOOK DAWS304                     *         
      *           DAKUT301                                            *         
      *           DAKUT305                                            *         
      *    ****** CHANGES TO ANY OF THESE MODULES WILL REQUIRE        *         
      *    ****** RECOMPILES TO THE PREVIOUSLY NOTED PROGRAMS         *         
      *****************************************************************         
      *                                                               *         
      *    --- GET DB2 INFO FOR ORACLE PROGRAMS                       *         
      *                                                               *         
      *    THIS PROGRAM RETURNS THE VENDOR'S PRIMARY NAME AND DUNNS   *         
      *    NUMBER OR ENTERPRISE ID GIVEN AN ENTERPRISE ID OR DUNNS    *         
      *    NUMBER.  IT WILL ALSO RETURN THE PAYMENT TERM INFORMATION  *         
      *    GIVEN THE PAYMENT TERM ID, OR SELECTED AP CONTROL TABLE    *         
      *    FIELDS DEPENDING ON THE INPUT FUNCTION CODE.  IT WILL      *         
      *    ALSO RETURN THE CONTROL DATES IN M/L.  IF NO ERRORS OCCUR  *         
      *    A ZERO IS RETURNED ON THE RECORD.  IF ERRORS OCCUR, THE    *         
      *    PROGRAM WILL RETURN A MESSAGE FOR EACH AREA AND THE SQL    *         
      *    CODE.                                                      *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * DB2 TABLES:                                                   *         
      *  1. EXTERNAL ENTERPRISE NAME         (TENTNME)                *         
      *  2. EXTERNAL ENTERPRISE              (TEXTENT)                *         
      *  3. AP CONTROL                       (TAPCNTL)                *         
      *  4. M/L DATE CONTROL                 (TMLCNTL)                *         
      *  5. M/L DATE ATTRIBUTES              (TDTATTR)                *         
      *  6. MERCHANDISE AREA TABLE           (TMDSEAR)                *         
      *  7. APPLICATION AVAILABILITY TABLE   (TAPLAVL)                *         
      *  8. RESPONSIBILITY AREA TABLE        (TRSPBAR)                *         
      *---------------------------------------------------------------*         
RB0201*---------------------------------------------------------------*         
RB0201*   CHANGES:      SEE LINES WITH RB0201.                        *         
RB0201*                                                               *         
RB0201*   DATE:         02/19/01                                      *         
RB0201*                                                               *         
RB0201*   DESCRIPTION:  INCLUDE THE FISCAL WEEK FOR THE MONTH INSTEAD *         
RB0201*                 OF THE FISCAL WEEK FOR THE YEAR.              *         
RB0201*                                                               *         
RB0201*   MADE BY: RONNA BUTZ                 IR #:                   *         
RB0201*                                                               *         
SJ0404*   MADE BY: RONNA BUTZ                 IR #:                   *         
SJ0404*                                                               *         
I30021*  WR/PITS#     DATE        DESCRIPTION                                   
I30021*  --------   ----------   ---------------------------------              
I30021*I30021  10-11-2006 J LIN      INCREASE TTRANCDE-TABLE SIZE               
      *---------------------------------------------------------------*         
       EJECT                                                                    
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  FILLER                  PIC X(30)  VALUE                             
               '** BEGINING OF DAKUT304 W/S **'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-OPERCO-NBR           PIC  X(02) VALUE '03'.           00230004
           05  PC-ML                   PIC  X(02) VALUE 'ML'.           00230004
           05  PC-BYR                  PIC  X(03) VALUE 'BYR'.          00230004
           05  PC-DPT                  PIC  X(03) VALUE 'DPT'.          00230004
           05  PC-RETRY-MAX            PIC S9(04) VALUE +3  COMP.               
           05  PC-MAX-TTRNCDE-TABLE    PIC  9(03) VALUE 500.            00230004
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-EFF-DTE              PIC X(10)  VALUE SPACES.                 
           05  PV-CURRENT-PARAGRAPH    PIC X(35)  VALUE SPACES.                 
           05  PV-RETRY-COUNT          PIC S9(04) VALUE ZEROS                   
                                                   COMP.                        
           05  PV-SUB                  PIC S9(04) VALUE ZEROS                   
                                                   COMP.                        
           05  PV-ZERO-COUNT           PIC 9(01)  VALUE ZEROS.                  
           05  PV-TTRNCDE-CSR.                                          00290004
               20  PV-AP-TRN-CDE       PIC X(02).                               
               20  PV-PSTG-TYP-CDE     PIC X(02).                               
               20  PV-TRN-TYP-CDE      PIC X(03).                               
               20  PV-GL-ACCT-NBR      PIC X(06).                               
               20  PV-BTCH-SRC-CDE     PIC X(03).                               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-VENDOR-FOUND-SW              PIC X(01) VALUE 'N'.             
               88 VENDOR-NOT-FOUND                       VALUE 'N'.             
               88 VENDOR-FOUND                           VALUE 'Y'.             
           05  PS-TTRNCDE-TABLE-LOADED-SW      PIC X(01) VALUE 'N'.             
               88  PS-TTRNCDE-TABLE-LOADED               VALUE 'Y'.             
                                                                                
       01  PROGRAM-ABEND-FIELDS.                                                
           05  PA-PROGRAM              PIC X(08) VALUE 'DAKUT304'.              
           05  PA-PARAGRAPH            PIC X(50) VALUE SPACES.                  
           05  PA-OPERATION            PIC X(50) VALUE SPACES.                  
           05  PA-STRUCTURE-1          PIC X(08) VALUE SPACES.                  
           05  PA-STRUCTURE-2          PIC X(08) VALUE SPACES.                  
           05  PA-STATUS               PIC X(02) VALUE SPACES.                  
                                                                                
      *----------------------------------------------------------------*        
      *  BAD SQL CALL DATA AREAS                                       *        
      *----------------------------------------------------------------*        
       01  PROGRAM-MESSAGE-AREA.                                                
           05  PM-BAD-TAPCNTL-CALL        PIC  X(60)  VALUE                     
            'UNSUCCESSFUL SELECT OF THE TAPCNTL TABLE'.                         
           05  PM-BAD-TTRNCDE-OPEN        PIC  X(60)  VALUE                     
            'UNSUCCESSFUL OPEN OF THE TTRNCDE CURSOR'.                          
           05  PM-BAD-TTRNCDE-FETCH       PIC  X(60)  VALUE                     
            'UNSUCCESSFUL FETCH OF THE TTRNCDE CURSOR'.                         
           05  PM-BAD-ENT-ID-CALL         PIC  X(60)  VALUE                     
            'UNSUCCESSFUL ENT ID SELECT ON TENTNME AND TEXTENT TABLES'.         
           05  PM-BAD-PAYTRM-CALL         PIC  X(60)  VALUE                     
            'UNSUCCESSFUL PAYMENT TERMS ID SELECT ON TPAYTRM TABLE'.            
           05  PM-BAD-TMLCNTL-CALL        PIC  X(60)  VALUE                     
            'UNSUCCESSFUL SELECT OF THE TMLCNTL TABLE'.                         
           05  PM-BAD-TDTATTR-CALL         PIC  X(60)  VALUE                    
            'UNSUCCESSFUL SELECT OF THE TDTATTR TABLE'.                         
           05  PM-INV-FUNCTION-CODE-MSG   PIC  X(60)  VALUE                     
            'INVALID FUNCTION CODE PASSED'.                                     
           05  PM-TABLE-EXCEEDED-MSG      PIC  X(60)  VALUE                     
            'TTRNCDE TABLE EXCEEDED 500 ENTRIES'.                               
           05  PM-BAD-TRSPBAR-MSG         PIC  X(60)  VALUE                     
            'UNSUCCESSFUL FETCH ON TRSPBAR BUYER NAME'.                         
           05  PM-BAD-MBS-MSG             PIC  X(60)  VALUE                     
            'UNSUCCESSFUL FETCH ON MBS BUYER NBR'.                              
           05  PM-BAD-CURRENT-MBS-MSG     PIC  X(60)  VALUE                     
            'UNSUCCESSFUL FETCH ON MBS CURRENT BUYER NBR'.                      
                                                                                
       01  BAD-SQL-ERR-MSG.                                                     
           05  BAD-SQL-ERROR-MSG       PIC  X(60)  VALUE SPACES.                
           05  FILLER                  PIC  X(11)  VALUE ' SQLCODE = '.         
           05  BAD-SQL-CODE            PIC  ----9  VALUE ZERO.                  
           05  FILLER                  PIC  X(03)  VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *  EXTERNAL ENTERPRISE NAME TABLE                                *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE TENTNME                                                 
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *  EXTERNAL ENTERPRISE TABLE                                     *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE TEXTENT                                                 
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *  TRANSACTION CODE TABLE                                        *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE TTRNCDE                                                 
           END-EXEC.                                                            
      *----------------------------------------------------------------*        
      *  TRANSACTION CODE TABLE                                        *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE TAPCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *  MERCHANDISE LEDGER PROCESS DATE CONTROL TABLE                 *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *  MERCHANDISE LEDGER DATE CONTROL TABLE                         *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  MERCHANDISE AREA TABLE (TMDSEAR)                                       
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  APPLICATION AVAILABILITY TABLE (TAPLAVL)                               
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TAPLAVL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  RESPONSIBILITY AREA TABLE (TRSPBAR)                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TRSPBAR                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    SQL COMMUNICATIONS AREA DCLGEN                                       
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *                                                                         
      * CURSOR OF TTRNCDE TABLE USED TO LOAD THE TABLE ON DAWS304               
      *                                                                         
           EXEC SQL                                                             
               DECLARE TTRNCDE_CSR CURSOR FOR                                   
                SELECT  DISTINCT AP_TRN_CDE                                     
                               , PSTG_TYP_CDE                                   
                               , TRN_TYP_CDE                                    
                               , GL_ACCT_NBR                                    
                               , BTCH_SRC_CDE                                   
                  FROM  TTRNCDE                                                 
                 ORDER BY AP_TRN_CDE                                            
                 FOR FETCH ONLY                                                 
           END-EXEC.                                                            
      *                                                                         
       01  FILLER                      PIC  X(25)  VALUE                        
           '** END OF DAKUT304 W/S **'.                                         
       EJECT                                                                    
      *                                                                         
       LINKAGE SECTION.                                                         
      *                                                                         
           COPY DAWS304.                                                        
      *                                                                         
       PROCEDURE DIVISION USING DA304-PARAMATERS.                               
                                                                                
       0000-DAKUT304.                                                           
                                                                                
           MOVE '0000-DAKUT304' TO PV-CURRENT-PARAGRAPH.                        
                                                                                
           SET DA304-GOOD-CALL TO TRUE.                                         
                                                                                
           EVALUATE TRUE                                                        
               WHEN DA304-INIT-SELECT-FOR-DAKUT301                              
                   PERFORM 2000-INITIAL-DAKUT301-PROCESS                        
               WHEN DA304-REG-SELECT-FOR-DAKUT301                               
                   PERFORM 2050-NORMAL-DAKUT301-PROCESS                         
               WHEN DA304-REG-SELECT-FOR-DAKUT305                               
                   PERFORM 2060-NORMAL-DAKUT305-PROCESS                         
               WHEN OTHER                                                       
                   MOVE PM-INV-FUNCTION-CODE-MSG TO                             
                        DA304-GENERAL-ERROR-MESSAGE                             
                   SET DA304-BAD-CALL TO TRUE                                   
           END-EVALUATE.                                                        
                                                                                
           GOBACK.                                                              
                                                                                
       EJECT                                                                    
      *------------------------------------------------------------*            
      *   2000-INITIAL-DAKUT301-PROCESS PERFORMS ALL "ONE-TIME"    *            
      *   CALLS NECESSARY FOR DAKUT301.  IF ANY DB2 SELECT DOES    *            
      *   NOT RESULT IN A GOOD RETURN, NO OTHER SELECTS WILL BE    *            
      *   PERFORMED AND CONTROL WILL BE RETURNED BACK TO DAKUT301. *            
      *   CALLED FROM 0000-DAKUT304.                               *            
      *------------------------------------------------------------*            
       2000-INITIAL-DAKUT301-PROCESS.                                           
           PERFORM 2100-TAPCNTL-SELECT.                                         
                                                                                
           PERFORM 2200-OPEN-TTRNCDE-CURSOR.                                    
           PERFORM 2250-PROCESS-TTRNCDE-CURSOR                                  
               UNTIL PS-TTRNCDE-TABLE-LOADED                                    
                  OR DA304-TTRNCDE-BAD-CALL.                                    
                                                                                
           PERFORM 2400-DUN-NAME-NBR-SELECT.                                    
           PERFORM 2500-TMLCNTL-SELECT.                                         
           PERFORM 2600-TDTATTR-SELECT.                                         
                                                                                
      *------------------------------------------------------------*            
      *   2050-NORMAL-DAKUT301-PROCESS CONTROLS THE SELECTS        *            
      *   THAT ARE DONE FOR EVERY INPUT RECORD.                    *            
      *   CALLED FROM 0000-DAKUT304.                               *            
      *------------------------------------------------------------*            
       2050-NORMAL-DAKUT301-PROCESS.                                            
           PERFORM 2400-DUN-NAME-NBR-SELECT.                                    
                                                                                
      *------------------------------------------------------------*            
      *   2060-NORMAL-DAKUT305-PROCESS CONTROLS THE SELECTS        *            
      *   THAT ARE DONE FOR EVERY INPUT RECORD.                    *            
      *   CALLED FROM 0000-DAKUT304.                               *            
      *------------------------------------------------------------*            
       2060-NORMAL-DAKUT305-PROCESS.                                            
           PERFORM 2700-SELECT-MBS-INFO.                                        
                                                                                
           EXEC SQL                                                             
              SET :PV-EFF-DTE  = CURRENT_DATE                                   
           END-EXEC.                                                            
                                                                                
           IF DA304-MBS-BUYER-NBR  = SPACES OR LOW-VALUES                       
               PERFORM 2800-GET-CURRENT-MBS-INFO                                
SJ0404**   ELSE                                                                 
SJ0404*       MOVE DA304-EFF-DTE TO PV-EFF-DTE                                  
           END-IF.                                                              
                                                                                
      *** BUYER NAME INFORMATION....                                            
           PERFORM 2900-GET-BYR-NAME.                                           
      *----------------------------------------------------------------*        
      *    SELECT THE TAPCNTL ROW                                      *        
      *    CALLED FROM 2000-INITIAL-DAKUT301-PROCESS.                  *        
      *----------------------------------------------------------------*        
       2100-TAPCNTL-SELECT.                                                     
                                                                                
           MOVE '2100-TAPCNTL-SELECT' TO PV-CURRENT-PARAGRAPH.                  
                                                                                
           INITIALIZE APCNTL-BTCH-PRC-DTE.                                      
                                                                                
           EXEC SQL                                                             
               SELECT BTCH_PRC_DTE + 1 DAY                                      
               INTO :APCNTL-BTCH-PRC-DTE                                        
               FROM TAPCNTL                                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
                   MOVE APCNTL-PAYT-FLOAT-DAY-QTY TO                            
                        DA304-PAYT-FLOAT-DAY-QTY                                
                   MOVE APCNTL-EXC-FLT-DAY-QTY    TO                            
                        DA304-EXC-FLT-DAY-QTY                                   
                   MOVE APCNTL-RCVG-VARN-DAY-QTY  TO                            
                        DA304-RCVG-VARN-DAY-QTY                                 
                   MOVE SPACES               TO                                 
                        DA304-TAPCNTL-ERROR-MESSAGE                             
                   SET DA304-TAPCNTL-GOOD-CALL TO TRUE                          
               WHEN OTHER                                                       
                   MOVE ZEROES               TO DA304-PAYT-FLOAT-DAY-QTY        
                                                DA304-EXC-FLT-DAY-QTY           
                                                DA304-RCVG-VARN-DAY-QTY         
                   MOVE PM-BAD-TAPCNTL-CALL  TO BAD-SQL-ERROR-MSG               
                   MOVE SQLCODE              TO BAD-SQL-CODE                    
                   MOVE BAD-SQL-ERR-MSG      TO                                 
                        DA304-TAPCNTL-ERROR-MESSAGE                             
                   SET DA304-TAPCNTL-BAD-CALL                                   
                       DA304-BAD-CALL                  TO TRUE                  
                   DISPLAY ' DAKUT304 PARAGRAPH IN ERROR '                      
                           PV-CURRENT-PARAGRAPH                                 
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    SELECT THE TTRNCDE INFORMATION FOR THE TRAN CODE THAT IS    *        
      *    PASSED FROM DAKUT301                                        *        
      *    CALLED FROM 2000-INITIAL-DAKUT301-PROCESS.                  *        
      *----------------------------------------------------------------*        
       2200-OPEN-TTRNCDE-CURSOR.                                                
                                                                                
           MOVE '2200-OPEN-TTRNCDE-CURSOR ' TO PV-CURRENT-PARAGRAPH.            
           EXEC SQL                                                             
               OPEN TTRNCDE_CSR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  MOVE PM-BAD-TTRNCDE-OPEN TO                                   
                       DA304-TTRNCDE-ERROR-MESSAGE                              
                   SET DA304-TTRNCDE-BAD-CALL                                   
                       DA304-BAD-CALL         TO TRUE                           
                   DISPLAY ' DAKUT304 PARAGRAPH IN ERROR '                      
                           PV-CURRENT-PARAGRAPH                                 
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      * INCREMENT SUBSCRIPT AND CONTINUE LOADING INTERNAL TABLE                 
      *****************************************************************         
       2250-PROCESS-TTRNCDE-CURSOR.                                             
      *                                                                         
           MOVE '2250-PROCESS-TTRNCDE-CURSOR' TO PV-CURRENT-PARAGRAPH.          
           EXEC SQL                                                             
                  FETCH TTRNCDE_CSR                                             
                   INTO :PV-TTRNCDE-CSR                                         
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                 ADD 1 TO PV-SUB                                                
                 IF PV-SUB > PC-MAX-TTRNCDE-TABLE                               
                     MOVE PM-TABLE-EXCEEDED-MSG TO                              
                          DA304-TTRNCDE-ERROR-MESSAGE                           
                     SET DA304-TTRNCDE-BAD-CALL                                 
                         DA304-BAD-CALL         TO TRUE                         
                 ELSE                                                           
                     MOVE PV-AP-TRN-CDE   TO DA304-AP-TRN-CDE (PV-SUB)          
                     MOVE PV-PSTG-TYP-CDE TO DA304-PSTG-TYP-CDE (PV-SUB)        
                     MOVE PV-TRN-TYP-CDE  TO DA304-TRN-TYP-CDE (PV-SUB)         
                     MOVE PV-GL-ACCT-NBR  TO DA304-GL-ACCT-NBR (PV-SUB)         
                     MOVE PV-BTCH-SRC-CDE TO DA304-BTCH-SRC-CDE (PV-SUB)        
                 END-IF                                                         
               WHEN +100                                                        
                   IF PV-SUB > 0                                                
                       MOVE SPACES           TO                                 
                            DA304-TTRNCDE-ERROR-MESSAGE                         
                       SET DA304-TTRNCDE-GOOD-CALL                              
                           PS-TTRNCDE-TABLE-LOADED TO TRUE                      
                   ELSE                                                         
                       SET DA304-TTRNCDE-BAD-CALL                               
                           DA304-BAD-CALL       TO TRUE                         
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE PM-BAD-TTRNCDE-FETCH   TO BAD-SQL-ERROR-MSG             
                   MOVE SQLCODE                TO BAD-SQL-CODE                  
                   MOVE BAD-SQL-ERR-MSG        TO                               
                        DA304-TTRNCDE-ERROR-MESSAGE                             
                   SET DA304-TTRNCDE-BAD-CALL                                   
                       DA304-BAD-CALL         TO TRUE                           
                   DISPLAY ' DAKUT304 PARAGRAPH IN ERROR '                      
                           PV-CURRENT-PARAGRAPH                                 
           END-EVALUATE.                                                        
      *                                                                         
      *----------------------------------------------------------------*        
      *    USE THE ENTERPRISE ID TO OBTAIN THE DUN NBR AND VENDOR NAME.*        
      *    CALLED FROM 2000-INITIAL-DAKUT301-PROCESS AND               *        
      *    FROM 2400-DUN-NAME-NBR-SELECT.                              *        
      *----------------------------------------------------------------*        
       2400-DUN-NAME-NBR-SELECT.                                                
                                                                                
           MOVE '2400-DUN-NAME-NBR-SELECT' TO PV-CURRENT-PARAGRAPH.             
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
               UNTIL       PV-RETRY-COUNT > PC-RETRY-MAX OR                     
                       (SQLCODE NOT = -904                                      
                        AND SQLCODE NOT = -913)                                 
                                                                                
               EXEC SQL                                                         
                   SELECT                                                       
                        EN.ENT_NAME_DESC                                        
                      , EE.DUN_NBR                                              
                   INTO                                                         
                        :ENTNME-ENT-NAME-DESC                                   
                      , :EXTENT-DUN-NBR                                         
                   FROM TENTNME EN                                              
                      , TEXTENT EE                                              
                   WHERE EE.ENT_ID             = :DA304-ENT-ID                  
                     AND EN.ENT_ID             = :DA304-ENT-ID                  
                     AND EN.TYPE_CDE           = '01'                           
      *-- COMMENTED THE FOLLOWING CHECK OUT AFTER TALKING TO NANCY              
      *-- EILBES.  IT IS NOT NECESSARY AT THIS POINT 9/27/2000. SHE             
      *-- WILL CONTACT US IF THIS SHOULD CHECK THIS FIELD.                      
      *---           AND EE.STATUS_CDE         = 'A'                            
               END-EXEC                                                         
                                                                                
           END-PERFORM.                                                         
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
                   MOVE EXTENT-DUN-NBR       TO DA304-DUN-NBR                   
                   MOVE ENTNME-ENT-NAME-DESC TO DA304-VENDOR-NAME               
                   MOVE SPACES               TO                                 
                        DA304-VENDOR-ERROR-MESSAGE                              
                   SET DA304-VENDOR-GOOD-CALL TO TRUE                           
               WHEN OTHER                                                       
                   MOVE ZEROES               TO DA304-DUN-NBR                   
                   MOVE SPACES               TO DA304-VENDOR-NAME               
                   MOVE PM-BAD-ENT-ID-CALL   TO BAD-SQL-ERROR-MSG               
                   MOVE SQLCODE              TO BAD-SQL-CODE                    
                   MOVE BAD-SQL-ERR-MSG      TO                                 
                        DA304-VENDOR-ERROR-MESSAGE                              
                   SET DA304-VENDOR-BAD-CALL                                    
                       DA304-BAD-CALL        TO TRUE                            
                   DISPLAY ' DAKUT304 PARAGRAPH IN ERROR '                      
                           PV-CURRENT-PARAGRAPH                                 
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    2500-TMLCNTL-SELECT SELECTS THE MAX POSTING DATES FROM THE  *        
      *    MERCHANDISE LEDGER CONTROL CONTROL TABLE                    *        
      *    2000-INITIAL-DAKUT301-PROCESS.                              *        
      *----------------------------------------------------------------*        
       2500-TMLCNTL-SELECT.                                                     
           MOVE '2500-TMLCNTL-SELECT'  TO PV-CURRENT-PARAGRAPH.                 
                                                                                
           EXEC SQL                                                             
               SELECT MXPSTG_FSCL_WK_NBR                                        
                     ,MXPSTG_FSCL_WK_DTE                                        
                     ,MXPSTG_FSCL_MN_NBR                                        
                     ,MXPSTG_FSCL_MN_DTE                                        
                INTO  :MLCNTL-MXPSTG-FSCL-WK-NBR                                
                     ,:MLCNTL-MXPSTG-FSCL-WK-DTE                                
                     ,:MLCNTL-MXPSTG-FSCL-MN-NBR                                
                     ,:MLCNTL-MXPSTG-FSCL-MN-DTE                                
                FROM   TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE MLCNTL-MXPSTG-FSCL-WK-NBR TO                            
                        DA304-MLCNTL-MX-FSCL-WK-NBR                             
                   MOVE MLCNTL-MXPSTG-FSCL-MN-NBR TO                            
                        DA304-MLCNTL-MX-FSCL-MN-NBR                             
                   MOVE MLCNTL-MXPSTG-FSCL-WK-DTE TO                            
                        DA304-MLCNTL-MX-FSCL-WK-DTE                             
                   MOVE MLCNTL-MXPSTG-FSCL-MN-DTE TO                            
                        DA304-MLCNTL-MX-FSCL-MN-DTE                             
                   MOVE SPACES                    TO                            
                        DA304-MLCNTL-ERROR-MESSAGE                              
                   SET DA304-MLCNTL-GOOD-CALL TO TRUE                           
               WHEN OTHER                                                       
                   MOVE SPACES              TO                                  
                        DA304-MLCNTL-MX-FSCL-MN-NBR                             
                        DA304-MLCNTL-MX-FSCL-WK-NBR                             
                        DA304-MLCNTL-MX-FSCL-MN-DTE                             
                        DA304-MLCNTL-MX-FSCL-WK-DTE                             
                   MOVE PM-BAD-TMLCNTL-CALL TO BAD-SQL-ERROR-MSG                
                   MOVE SQLCODE             TO BAD-SQL-CODE                     
                   MOVE BAD-SQL-ERR-MSG     TO                                  
                        DA304-MLCNTL-ERROR-MESSAGE                              
                   SET DA304-MLCNTL-BAD-CALL                                    
                       DA304-BAD-CALL       TO TRUE                             
                   DISPLAY ' DAKUT304 PARAGRAPH IN ERROR '                      
                       PV-CURRENT-PARAGRAPH                                     
           END-EVALUATE.                                                        
      *----------------------------------------------------------------*        
      *    2600-TDTATTR-SELECT SELECTS THE CURRENT FISCAL YEAR FROM    *        
      *    THE MERCHANDISE LEDGER DATE ATTRIBUTE TABLE                 *        
      *    2000-INITIAL-DAKUT301-PROCESS.                              *        
      *----------------------------------------------------------------*        
       2600-TDTATTR-SELECT.                                                     
           MOVE '2600-TDTATTR-SELECT'  TO PV-CURRENT-PARAGRAPH.                 
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    FSCL_YR_NBR                                                 
                   ,WK_IN_MN_NBR                                                
                INTO                                                            
                    :DTATTR-FSCL-YR-NBR                                         
RB0201             ,:DTATTR-WK-IN-MN-NBR                                        
                FROM                                                            
                    TDTATTR                                                     
                WHERE                                                           
RB0201              KY_DTE = :MLCNTL-MXPSTG-FSCL-WK-DTE                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                  MOVE DTATTR-FSCL-YR-NBR  TO DA304-DTATTR-FSCL-YR-NBR          
RB0201            MOVE DTATTR-WK-IN-MN-NBR TO DA304-DTATTR-WK-IN-MN-NBR         
                  MOVE SPACES              TO                                   
                       DA304-DTATTR-ERROR-MESSAGE                               
                  SET DA304-DTATTR-GOOD-CALL TO TRUE                            
               WHEN OTHER                                                       
                  MOVE SPACES              TO DA304-DTATTR-FSCL-YR-NBR          
RB0201                                        DA304-DTATTR-WK-IN-MN-NBR         
                  MOVE PM-BAD-TDTATTR-CALL TO BAD-SQL-ERROR-MSG                 
                  MOVE SQLCODE             TO BAD-SQL-CODE                      
                  MOVE BAD-SQL-ERR-MSG     TO                                   
                       DA304-DTATTR-ERROR-MESSAGE                               
                  SET DA304-DTATTR-BAD-CALL                                     
                      DA304-BAD-CALL       TO TRUE                              
               DISPLAY ' DAKUT304 PARAGRAPH IN ERROR '                          
                       PV-CURRENT-PARAGRAPH                                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 2700-GET-EFFECTIVE MBS INFORMATION.....                                 
      ******************************************************************        
       2700-SELECT-MBS-INFO.                                                    
           MOVE '2700-SELECT-MBS-INFO' TO PV-CURRENT-PARAGRAPH.                 
                                                                                
           EXEC SQL                                                             
             SELECT BYR_NBR                                                     
             INTO :DA304-MBS-BUYER-NBR                                          
             FROM TMDSEAR                                                       
             WHERE OPERCO_NBR = '03'                                            
               AND MDSELV_CDE = 'DPT'                                           
               AND DEPT_NBR = :DA304-DEPT-NBR                                   
SJ0404***      AND (DAK304-EFF-DTE BETWEEN STRT_DTE AND END_DTE)                
               AND (CURRENT DATE   BETWEEN STRT_DTE AND END_DTE)                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               CONTINUE                                                         
             WHEN  +100                                                         
               CONTINUE                                                         
             WHEN OTHER                                                         
SJ0404       DISPLAY ' SQLCODE ', SQLCODE                                       
               MOVE PM-BAD-MBS-MSG        TO BAD-SQL-ERROR-MSG                  
               MOVE SQLCODE               TO BAD-SQL-CODE                       
               MOVE BAD-SQL-ERR-MSG       TO                                    
                    DA304-MBS-ERROR-MESSAGE                                     
               SET DA304-MBS-BAD-CALL                                           
                   DA304-BAD-CALL          TO TRUE                              
               DISPLAY ' DAKUT304 PARAGRAPH IN ERROR '                          
                       PV-CURRENT-PARAGRAPH                                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * 2800-GET THE LATEST MBS INFORMATION....                                 
      * GET THE MBS INFO FROM THE MOST RECENT ROW FOR THE DEPARTMENT.           
      ******************************************************************        
       2800-GET-CURRENT-MBS-INFO.                                               
           MOVE '2800-GET-LATEST-MBS-INFO' TO PV-CURRENT-PARAGRAPH.             
                                                                                
           EXEC SQL                                                             
             SELECT BYR_NBR                                                     
             INTO :DA304-MBS-BUYER-NBR                                          
             FROM TMDSEAR                                                       
             WHERE OPERCO_NBR = '03'                                            
               AND MDSELV_CDE = 'DPT'                                           
               AND DEPT_NBR = :DA304-DEPT-NBR                                   
               AND END_DTE =                                                    
                        (SELECT MAX(END_DTE)                                    
                         FROM TMDSEAR                                           
                         WHERE OPERCO_NBR = :PC-OPERCO-NBR                      
                           AND MDSELV_CDE = :PC-DPT                             
                           AND DEPT_NBR   = :DA304-DEPT-NBR)                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               SET DA304-MBS-GOOD-CALL     TO TRUE                              
               MOVE SPACES                 TO DA304-MBS-ERROR-MESSAGE           
             WHEN OTHER                                                         
               MOVE PM-BAD-CURRENT-MBS-MSG TO BAD-SQL-ERROR-MSG                 
               MOVE SQLCODE                TO BAD-SQL-CODE                      
               MOVE BAD-SQL-ERR-MSG        TO                                   
                    DA304-MBS-ERROR-MESSAGE                                     
               SET DA304-MBS-BAD-CALL                                           
                   DA304-BAD-CALL          TO TRUE                              
               DISPLAY ' DAKUT304 PARAGRAPH IN ERROR '                          
                       PV-CURRENT-PARAGRAPH                                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 2900-GET THE BUYER NAME.                                                
      ******************************************************************        
       2900-GET-BYR-NAME.                                                       
                                                                                
           MOVE '2900-GET-BYR-NAME' TO PV-CURRENT-PARAGRAPH.                    
                                                                                
           EXEC SQL                                                             
             SELECT DISTINCT B.WORKER_LAST_NAME,                                
                             B.WORKER_FIR_NAME                                  
             INTO :DA304-BUYER-LAST-NAME                                        
                , :DA304-BUYER-FIRST-NAME                                       
             FROM TMDSEAR A,                                                    
                  TRSPBAR B,                                                    
                  TAPLAVL C                                                     
             WHERE A.BYR_NBR     = :DA304-MBS-BUYER-NBR                         
               AND A.DEPT_NBR    = :DA304-DEPT-NBR                              
               AND B.RSPLVL_CDE  = :PC-BYR                                      
               AND A.OPERCO_NBR  = :PC-OPERCO-NBR                               
               AND C.APLSYS_CDE  = :PC-ML                                       
               AND A.MDSEAR_DKEY = B.MDSEAR_DKEY                                
               AND A.MDSEAR_DKEY = C.MDSEAR_DKEY                                
               AND (:PV-EFF-DTE                                                 
                       BETWEEN B.WORKER_STRT_DTE AND B.WORKER_END_DTE)          
               AND (:PV-EFF-DTE                                                 
                       BETWEEN C.MDSEAR_STRT_DTE AND C.MDSEAR_END_DTE)          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               SET DA304-BUYER-GOOD-CALL   TO TRUE                              
               MOVE SPACES                 TO DA304-BUYER-ERROR-MESSAGE         
             WHEN OTHER                                                         
               MOVE PM-BAD-TRSPBAR-MSG     TO BAD-SQL-ERROR-MSG                 
               MOVE SQLCODE                TO BAD-SQL-CODE                      
               MOVE BAD-SQL-ERR-MSG        TO                                   
                    DA304-BUYER-ERROR-MESSAGE                                   
               SET DA304-BUYER-BAD-CALL                                         
                   DA304-BAD-CALL          TO TRUE                              
               DISPLAY ' DAKUT304 PARAGRAPH IN ERROR '                          
                       PV-CURRENT-PARAGRAPH                                     
           END-EVALUATE.                                                        
                                                                                
