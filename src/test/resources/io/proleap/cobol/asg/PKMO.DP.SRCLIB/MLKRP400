000010 IDENTIFICATION DIVISION.                                                 
000020                                                                          
000030 PROGRAM-ID.    MLKRP400.                                                 
000040 AUTHOR.        TESS BESTE.                                               
000050 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000060 DATE-WRITTEN.  02-24-94.                                                 
000070 DATE-COMPILED.                                                           
000080                                                                          
000090******************************************************************        
000100* CREATE A REPORT OF TRANSFERS TO STORE 085 BY READING A FILE OF          
000110* EDITED TRANSFER-FROM TRANSACTIONS WHERE THE TO-STORE = 085,             
000120* AND SUMMING COST AND RETAIL BY FROM-STORE.                              
000130******************************************************************        
000140                                                                 *        
000150                                                                          
000160 ENVIRONMENT DIVISION.                                                    
000170                                                                          
000180 CONFIGURATION SECTION.                                                   
000190                                                                          
000200 SOURCE-COMPUTER.    IBM-3090.                                            
000210 OBJECT-COMPUTER.    IBM-3090.                                            
000220                                                                          
000230                                                                          
000240 INPUT-OUTPUT SECTION.                                                    
000250                                                                          
000260 FILE-CONTROL.                                                            
000270     SELECT INPUT-FILE                                                    
000280         ASSIGN TO INP01.                                                 
000290                                                                          
000300                                                                          
000310                                                                          
000320 DATA DIVISION.                                                           
000330                                                                          
000340 FILE SECTION.                                                            
000350                                                                          
000360 FD  INPUT-FILE                                                           
000370     LABEL RECORDS ARE STANDARD                                           
000380     BLOCK CONTAINS 0 RECORDS                                             
000390     RECORDING  MODE IS F.                                                
000400 01  INPUT-RECORD                PIC X(150).                              
000410                                                                          
000420 WORKING-STORAGE SECTION.                                                 
000430                                                                          
000440******************************************************************        
000450* INPUT RECORD LAYOUT                                                     
000460******************************************************************        
000470                                                                          
000480     COPY MLRD021.                                                        
000490                                                                          
000500 01  PS-PROGRAM-SWITCHES.                                                 
000510     05  PS-END-OF-INPUT-FILE-SW      PIC X         VALUE 'N'.            
000520         88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.            
000530         88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.            
000540                                                                          
000550 01  PV-PROGRAM-VARIABLES.                                                
000560     05  PV-DISPLAY-COST              PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.            
000570     05  PV-DISPLAY-RETAIL            PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.            
000580     05  PV-ACCUM-COST                PIC S9(13)V99 VALUE +0              
000590                                                           COMP-3.        
000600     05  PV-ACCUM-RETAIL              PIC S9(13)V99 VALUE +0              
000610                                                           COMP-3.        
000620     05  PV-ACCUM-FINAL-COST          PIC S9(13)V99 VALUE +0              
000630                                                           COMP-3.        
000640     05  PV-ACCUM-FINAL-RETAIL        PIC S9(13)V99 VALUE +0              
000650                                                           COMP-3.        
000660     05  PV-PREV-STORE-NBR            PIC X(05).                          
000670                                                                          
000680 PROCEDURE DIVISION.                                                      
000690                                                                          
000700 A100-PROGRAM-MAINLINE.                                                   
000710                                                                          
000720     OPEN INPUT INPUT-FILE.                                               
000730                                                                          
000740     READ INPUT-FILE INTO ML021-PURCH-RECORD                              
000750         AT END SET PS-END-OF-INPUT-FILE TO TRUE.                         
000760                                                                          
000770     DISPLAY '                    TRANSFERS TO STORE 085 SINCE '          
000780             'JANUARY 1994'.                                              
000790     DISPLAY ' '.                                                         
000800     DISPLAY '                 STORE        '                             
000810             '       COST               RETAIL'.                          
000820     DISPLAY ' '.                                                         
000830                                                                          
000840     MOVE ML021-PURCH-STORE-NBR TO PV-PREV-STORE-NBR.                     
000850                                                                          
000860     PERFORM UNTIL PS-END-OF-INPUT-FILE                                   
000870         IF ML021-PURCH-STORE-NBR NOT = PV-PREV-STORE-NBR                 
000880             MOVE PV-ACCUM-COST   TO PV-DISPLAY-COST                      
000890             MOVE PV-ACCUM-RETAIL TO PV-DISPLAY-RETAIL                    
000900             DISPLAY  '                  '                                
000910                      PV-PREV-STORE-NBR                                   
000920                      '  '                                                
000930                      PV-DISPLAY-COST                                     
000940                      '  '                                                
000950                      PV-DISPLAY-RETAIL                                   
000960             ADD PV-ACCUM-COST   TO PV-ACCUM-FINAL-COST                   
000970             ADD PV-ACCUM-RETAIL TO PV-ACCUM-FINAL-RETAIL                 
000980             INITIALIZE PV-ACCUM-COST                                     
000990                        PV-ACCUM-RETAIL                                   
001000             MOVE ML021-PURCH-STORE-NBR TO PV-PREV-STORE-NBR              
001010         END-IF                                                           
001020         COMPUTE PV-ACCUM-COST                                            
001030               = PV-ACCUM-COST                                            
001040               + ML021-PURCH-EXTD-NET-COST-AMT                            
001050         COMPUTE PV-ACCUM-RETAIL                                          
001060               = PV-ACCUM-RETAIL                                          
001070               + ML021-PURCH-EXTD-RTL-AMT                                 
001080         READ INPUT-FILE INTO ML021-PURCH-RECORD                          
001090             AT END SET PS-END-OF-INPUT-FILE TO TRUE                      
001100         END-READ                                                         
001110     END-PERFORM.                                                         
001120                                                                          
001130     MOVE PV-ACCUM-COST   TO PV-DISPLAY-COST.                             
001140     MOVE PV-ACCUM-RETAIL TO PV-DISPLAY-RETAIL.                           
001150     DISPLAY  '                  '                                        
001160              PV-PREV-STORE-NBR                                           
001170              '  '                                                        
001180              PV-DISPLAY-COST                                             
001190              '  '                                                        
001200              PV-DISPLAY-RETAIL.                                          
001210     DISPLAY ' '.                                                         
001220     ADD PV-ACCUM-COST          TO PV-ACCUM-FINAL-COST.                   
001230     ADD PV-ACCUM-RETAIL        TO PV-ACCUM-FINAL-RETAIL.                 
001240     MOVE PV-ACCUM-FINAL-COST   TO PV-DISPLAY-COST.                       
001250     MOVE PV-ACCUM-FINAL-RETAIL TO PV-DISPLAY-RETAIL.                     
001260     DISPLAY '                 TOTAL:  '                                  
001270             PV-DISPLAY-COST   '  ' PV-DISPLAY-RETAIL.                    
001280                                                                          
001290     CLOSE INPUT-FILE.                                                    
001300                                                                          
001310     STOP RUN.                                                            
