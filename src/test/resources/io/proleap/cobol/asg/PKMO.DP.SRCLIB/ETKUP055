       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ETKUP055.                                                 
       AUTHOR.        JON FIEDLER.                                              
       DATE-WRITTEN.  03-05-2001.                                               
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
      *****************************************************************         
      * THIS PROGRAM DELETES ROWS FROM THE TABLE TETTXHD WHERE THE              
      * TXT_MSG_TMST IS GREATER THAN 1 YEAR OLD. THIS PROGRAM WILL ALSO         
      * DELETE ROWS IN THE CHILD TABLES TETTXDT AND TETTXDS COINCIDING          
      * WITH THE TXT_MSG_DKEY_ID.                                               
      *****************************************************************         
       FILE-CONTROL.                                                            
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME             PIC X(8)      VALUE                  
                                                            'ETKUP055'.         
           05  PC-MAX-RETRIES              PIC S9(4)     VALUE +3               
                                                         COMP.                  
           05  PC-MAX-TABLE-VALUE          PIC 9         VALUE 6.               
                                                                                
      ******************************************************************        
      * THIS IS A WORKING STORAGE LAYOUT FOR ABEND CODE VALUES.                 
      *     USE THE SET COMMAND TO PROVIDE VALUES TO ABEND-CODE.                
      ******************************************************************        
       01  ABEND-CODE                      PIC S9(04)    VALUE +0               
                                                         COMP SYNC.             
                                                                                
      ******************************************************************        
      * THIS IS WORKING STORAGE FOR THE PROGRAM                                 
      ******************************************************************        
       01 PV-PROGRAM-VARIABLES.                                                 
           05  PV-PARAGRAPH-NAME           PIC X(30)     VALUE SPACES.          
           05  PV-DB2-OPERATION-ATTEMPTED  PIC X(30)     VALUE SPACES.          
           05  PV-RETRY-COUNTER            PIC S9(04)    VALUE +0               
                                                         COMP SYNC.             
           05  PV-TABLE-DELETE-COUNTER      PIC 9(4)     VALUE ZEROS.           
      ******************************************************************        
      *  COMMUNICATION AREA FOR DB2                                             
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 SQL ERROR MESSAGE FORMAT AREA                                      
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * MAIN PROCEDURE                                                          
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
                                                                                
           PERFORM 1000-DELETE.                                                 
           DISPLAY 'TOTAL DELETES = ' PV-TABLE-DELETE-COUNTER.                  
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      * THIS PROCEDURE DELETES A ROW FROM TETTXHD WHERE THE TIMESTAMP           
      * IS GREATER THAN 1 YEAR OLD AND COINCIDING ROWS IN TETTXDT AND           
      * TETTXDS.                                                                
      ******************************************************************        
       1000-DELETE.                                                             
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
0402JF             OR (SQLCODE = +0) OR (SQLCODE = +100))                       
                                                                                
               EXEC SQL                                                         
                DELETE FROM TETTXHD                                             
                WHERE DATE(TXT_MSG_TMST) < CURRENT DATE - 1 YEAR                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       ADD 1 TO PV-TABLE-DELETE-COUNTER                         
0402JF             WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < +0                                            
                   WHEN SQLCODE > +0                                            
                       MOVE '1000-DELETE'                                       
                                           TO PV-PARAGRAPH-NAME                 
                       MOVE 'FATAL ERROR ON TETTXHD TABLE  '                    
                                           TO PV-DB2-OPERATION-ATTEMPTED        
                       PERFORM 9800-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '1000-DELETE'                                               
                                           TO PV-PARAGRAPH-NAME                 
               MOVE 'TIMEOUT ON TETTXHD TABLE '                                 
                           TO PV-DB2-OPERATION-ATTEMPTED                        
               PERFORM 9800-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH IS ACCESSED IF AN INVALID SQL CODE IS RETURNED           
      * FROM AN SQL FUNCTION. IT WILL DISPLAY ABEND TRACKING INFORMATION        
      * AND TERMINATE THE PROGRAM FROM PROCESSING FURTHER.                      
      ******************************************************************        
       9800-SQL-ABEND.                                                          
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
