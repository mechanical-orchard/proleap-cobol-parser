00001 *                                                                 05/18/95
00002 *                                                                 ETKUP004
00003 *                                                                    LV007
00004  IDENTIFICATION DIVISION.                                         ETKUP004
00005  PROGRAM-ID.    ETKUP004.                                            CL**7
00006  AUTHOR.        KIM WRIGHT.                                       ETKUP004
00007  INSTALLATION.  KOHLS DEPARTMENT STORES.                          ETKUP004
00008  DATE-WRITTEN.  01/06/94.                                         ETKUP004
00009  DATE-COMPILED.                                                   ETKUP004
00010 ******************************************************************ETKUP004
00011 *    ETKUP004 -  CLEAN UP UNMATCHED UPC CODE TABLE                ETKUP004
00012 *                                                                 ETKUP004
00013 *    THIS PROGRAM READS IN EVERY RECORD OFF OF THE UNMATCHED UPC  ETKUP004
00014 *    CODE TABLE.  IF THE RECORD IS LOCATED ON THE UPC CROSS-      ETKUP004
00015 *    REFERENCE TABLE  AND SKU TABLES, THEN THE RECORD IS NO LONGERETKUP004
00016 *    UNMATCHED AND IS DELETED FROM THE TABLE.  DISPLAY CLAUSES    ETKUP004
00017 *    SHOW THE UPC CODES DELETED AND A COUNTER IS DISPLAYED AT     ETKUP004
00018 *    THE END.                                                     ETKUP004
00019 ******************************************************************ETKUP004
00020 *                                                                 ETKUP004
00021 *                                                                 ETKUP004
00022  ENVIRONMENT DIVISION.                                            ETKUP004
00023 *                                                                 ETKUP004
00024 *                                                                 ETKUP004
00025  CONFIGURATION SECTION.                                           ETKUP004
00026                                                                   ETKUP004
00027  SOURCE-COMPUTER. IBM-3090.                                       ETKUP004
00028  OBJECT-COMPUTER. IBM-3090.                                       ETKUP004
00029 *                                                                 ETKUP004
00030 *                                                                 ETKUP004
00031  INPUT-OUTPUT SECTION.                                            ETKUP004
00032                                                                   ETKUP004
00033 *                                                                 ETKUP004
00034  DATA DIVISION.                                                   ETKUP004
00035 *                                                                 ETKUP004
00036 *                                                                 ETKUP004
00037  WORKING-STORAGE SECTION.                                         ETKUP004
00038                                                                   ETKUP004
00039 ******************************************************************ETKUP004
00040 *    FIELDS NECESSARY TO CALL THE SQLCA MESSAGE MODULE.           ETKUP004
00041 ******************************************************************ETKUP004
00042                                                                   ETKUP004
00043      COPY DPWS004.                                                ETKUP004
00044 ******************************************************************ETKUP004
00045                                                                   ETKUP004
00046  01  WS-WORKING-STORAGE.                                          ETKUP004
00047      05  WS-UPC-CDE                      PIC S9(15) COMP-3        ETKUP004
00048                                                     VALUE +0.     ETKUP004
00049      05  WS-COUNTER                      PIC S9(03) COMP-3        ETKUP004
00050                                                     VALUE +0.     ETKUP004
00051      05  DISPLAY-VEND-NME                PIC X(35)  VALUE SPACES. ETKUP004
00052                                                                   ETKUP004
00053  01  PV-PROGRAM-VARIABLES.                                        ETKUP004
00054                                                                   ETKUP004
00055      05  PV-PARAGRAPH-NAME                PIC X(30)  VALUE SPACES.ETKUP004
00056      05  PV-DB2-OPERATION-ATTEMPTED       PIC X(30)  VALUE SPACES.ETKUP004
00057                                                                   ETKUP004
00058  01  ABEND-CODE                          PIC S9(04)  VALUE +0     ETKUP004
00059                                                      COMP SYNC.   ETKUP004
00060  EJECT                                                            ETKUP004
00061 ******************************************************************ETKUP004
00062 *    UNMATCHED UPC TABLE                                          ETKUP004
00063 ******************************************************************ETKUP004
00064                                                                   ETKUP004
00065      EXEC SQL                                                     ETKUP004
00066          INCLUDE TUNUPCS                                          ETKUP004
00067      END-EXEC.                                                    ETKUP004
00068                                                                   ETKUP004
00069 ******************************************************************ETKUP004
00070 *    UPC CROSS REFERENCE TABLE                                    ETKUP004
00071 ******************************************************************ETKUP004
00072                                                                   ETKUP004
00073      EXEC SQL                                                     ETKUP004
00074          INCLUDE TUPCSKU                                          ETKUP004
00075      END-EXEC.                                                    ETKUP004
00076                                                                   ETKUP004
00077 ******************************************************************ETKUP004
00078 *    SKU TABLE                                                    ETKUP004
00079 ******************************************************************ETKUP004
00080                                                                   ETKUP004
00081      EXEC SQL                                                     ETKUP004
00082          INCLUDE TSK                                              ETKUP004
00083      END-EXEC.                                                    ETKUP004
00084                                                                   ETKUP004
00085 ******************************************************************ETKUP004
00086 *    ENTITY NAME TABLE                                            ETKUP004
00087 ******************************************************************ETKUP004
00088                                                                   ETKUP004
00089      EXEC SQL                                                     ETKUP004
00090          INCLUDE TENTNME                                          ETKUP004
00091      END-EXEC.                                                    ETKUP004
00092                                                                   ETKUP004
00093 ******************************************************************ETKUP004
00094 *    NETWORK VENDOR TABLE                                         ETKUP004
00095 ******************************************************************ETKUP004
00096                                                                   ETKUP004
00097      EXEC SQL                                                     ETKUP004
00098          INCLUDE TNETVND                                          ETKUP004
00099      END-EXEC.                                                    ETKUP004
00100                                                                   ETKUP004
00101 ******************************************************************ETKUP004
00102 *    SQL COMMUNICATION AREA                                       ETKUP004
00103 ******************************************************************ETKUP004
00104                                                                   ETKUP004
00105      EXEC SQL                                                     ETKUP004
00106          INCLUDE SQLCA                                            ETKUP004
00107      END-EXEC.                                                    ETKUP004
00108                                                                   ETKUP004
00109 ***************************************************************** ETKUP004
00110 *    SELECT ALL RECORDS IN  UNMATCHED DATABASE                    ETKUP004
00111 ***************************************************************** ETKUP004
00112                                                                   ETKUP004
00113       EXEC SQL                                                    ETKUP004
00114          DECLARE TUNUPCS-CURSOR  CURSOR WITH HOLD FOR                CL**6
00115          SELECT  UPC_CDE,                                         ETKUP004
00116                  UPC_CAT_VEND_NME,                                ETKUP004
00117                  UPC_CAT_VEND_ID,                                 ETKUP004
00118                  NETWK_ID                                         ETKUP004
00119          FROM    TUNUPCS                                          ETKUP004
00120          ORDER BY UPC_CDE                                         ETKUP004
00121       END-EXEC.                                                   ETKUP004
00122 *                                                                 ETKUP004
00109 ***************************************************************** ETKUP004
00110 *    SELECT ALL ENTITY ID'S BELONGING TO UNMATCHED UPC VENDOR ID  ETKUP004
00111 ***************************************************************** ETKUP004
00112                                                                   ETKUP004
00113       EXEC SQL                                                    ETKUP004
00114          DECLARE TNETVND-CURSOR  CURSOR FOR                          CL**6
00115          SELECT  ENT_ID                                           ETKUP004
00119          FROM    TNETVND                                          ETKUP004
00120          WHERE   NETWK_ID        = :UNUPCS-NETWK-ID  AND          ETKUP004
00120                  UPC_CAT_VEND_ID = :UNUPCS-UPC-CAT-VEND-ID        ETKUP004
00121       END-EXEC.                                                   ETKUP004
00122 *                                                                 ETKUP004
00123                                                                   ETKUP004
00124      COPY SQLCA2.                                                 ETKUP004
00125  EJECT                                                            ETKUP004
00126 *                                                                 ETKUP004
00127 *                                                                 ETKUP004
00128  LINKAGE SECTION.                                                 ETKUP004
00129                                                                   ETKUP004
00130 *                                                                 ETKUP004
00131 *                                                                 ETKUP004
00132 *                                                                 ETKUP004
00133  PROCEDURE DIVISION.                                              ETKUP004
00134                                                                   ETKUP004
00135  EJECT                                                            ETKUP004
00136  A100-MAIN-MODULE.                                                ETKUP004
00137 ******************************************************************ETKUP004
00138 *    INITIALIZE VARIABLES                                         ETKUP004
00139 *    OPEN CURSOR                                                  ETKUP004
00140 *    PROCESS TUNUPCS                                              ETKUP004
00141 *    CLOSE TUNUPCS CURSOR                                         ETKUP004
00142 *    DISPLAY COUNTER                                              ETKUP004
00143 ******************************************************************ETKUP004
00144                                                                   ETKUP004
00145      MOVE ZEROES TO WS-UPC-CDE.                                   ETKUP004
00146      MOVE ZEROES TO WS-COUNTER.                                   ETKUP004
00147                                                                   ETKUP004
00148      PERFORM B100-OPEN-TUNUPCS-CURSOR.                            ETKUP004
00149                                                                   ETKUP004
00150      PERFORM B200-FETCH-TUNUPCS.                                  ETKUP004
00151                                                                   ETKUP004
00152      PERFORM B300-PROCESS-TUNUPCS                                 ETKUP004
00153             UNTIL SQLCODE = +100.                                 ETKUP004
00154                                                                   ETKUP004
00155      PERFORM B400-CLOSE-TUNUPCS-CURSOR.                           ETKUP004
00156                                                                   ETKUP004
00157      DISPLAY '  '.                                                ETKUP004
00158      DISPLAY 'NUMBER OF UPC CODES DELETED: ' WS-COUNTER.          ETKUP004
00159                                                                   ETKUP004
00160      STOP RUN.                                                    ETKUP004
00161  EJECT                                                            ETKUP004
00162                                                                   ETKUP004
00163  B100-OPEN-TUNUPCS-CURSOR.                                        ETKUP004
00164 ******************************************************************ETKUP004
00165 *    OPEN THE TUNUPCS CURSOR                                      ETKUP004
00166 *                                                                 ETKUP004
00167 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUP004
00168 ******************************************************************ETKUP004
00169                                                                   ETKUP004
00170      EXEC SQL                                                     ETKUP004
00171           OPEN TUNUPCS-CURSOR                                     ETKUP004
00172      END-EXEC.                                                    ETKUP004
00173                                                                   ETKUP004
00174      IF (SQLCODE NOT = +0)  OR                                    ETKUP004
00175         (SQLWARN0 = 'W')                                          ETKUP004
00176                                                                   ETKUP004
00177          MOVE 'OPEN TUNUPCS-CURSOR' TO                            ETKUP004
00178               PV-DB2-OPERATION-ATTEMPTED                          ETKUP004
00179          MOVE 'B100-OPEN-TUNUPCS-CURSOR' TO                       ETKUP004
00180               PV-PARAGRAPH-NAME                                   ETKUP004
00181                                                                   ETKUP004
00182          PERFORM Z999-SQL-ABEND                                   ETKUP004
00183                                                                   ETKUP004
00184      END-IF.                                                      ETKUP004
00185                                                                   ETKUP004
00186  EJECT                                                            ETKUP004
00187                                                                   ETKUP004
00188  B200-FETCH-TUNUPCS.                                              ETKUP004
00189 ******************************************************************ETKUP004
00190 *    FETCH THE UPC CODE FROM THE UNMATCHED UPC CODE TABLE         ETKUP004
00191 *                                                                 ETKUP004
00192 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUP004
00193 ******************************************************************ETKUP004
00194                                                                   ETKUP004
00195      EXEC SQL                                                     ETKUP004
00196         FETCH TUNUPCS-CURSOR                                      ETKUP004
00197         INTO  :UNUPCS-UPC-CDE,                                    ETKUP004
00198               :UNUPCS-UPC-CAT-VEND-NME,                           ETKUP004
00199               :UNUPCS-UPC-CAT-VEND-ID,                            ETKUP004
00200               :UNUPCS-NETWK-ID                                    ETKUP004
00201      END-EXEC.                                                    ETKUP004
00202                                                                   ETKUP004
00203      IF SQLCODE = +100 OR SQLCODE = +0                            ETKUP004
00204         CONTINUE                                                  ETKUP004
00205      ELSE                                                         ETKUP004
00206         MOVE 'FETCH TUNUPCS-CURSOR' TO                            ETKUP004
00207              PV-DB2-OPERATION-ATTEMPTED                           ETKUP004
00208         MOVE 'B200-FETCH-TUNUPCS' TO                              ETKUP004
00209              PV-PARAGRAPH-NAME                                    ETKUP004
00210                                                                   ETKUP004
00211         PERFORM Z999-SQL-ABEND                                    ETKUP004
00212      END-IF.                                                      ETKUP004
00213                                                                   ETKUP004
00214      IF SQLCODE = +000                                            ETKUP004
00215         MOVE UNUPCS-UPC-CDE TO WS-UPC-CDE                         ETKUP004
00216      END-IF.                                                      ETKUP004
00217                                                                   ETKUP004
00218  EJECT                                                            ETKUP004
00219                                                                   ETKUP004
00220  B300-PROCESS-TUNUPCS.                                            ETKUP004
00221 ******************************************************************ETKUP004
00222 *    READ THE CROSS-REFERENCE TABLE USING THE UPC CODE FROM THE   ETKUP004
00223 *    UNMATCHED TABLE.  IF THE RECORD IS FOUND, DELETE THE UPC CODEETKUP004
00224 *    FROM THE UNMATCHED TABLE.                                    ETKUP004
00225 *                                                                 ETKUP004
00226 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUP004
00227 ******************************************************************ETKUP004
00228                                                                   ETKUP004
00229      PERFORM C100-SELECT-SKU.                                     ETKUP004
00230                                                                   ETKUP004
00231      IF SQLCODE = +000                                            ETKUP004
00232                                                                   ETKUP004
00233         MOVE UNUPCS-UPC-CAT-VEND-NME TO DISPLAY-VEND-NME          ETKUP004
00234         IF UNUPCS-UPC-CAT-VEND-NME = SPACES                       ETKUP004
00235             PERFORM C300-SELECT-VEND-NME                          ETKUP004
00236         END-IF                                                    ETKUP004
00237                                                                   ETKUP004
00238         PERFORM C200-DELETE-TUNUPCS                               ETKUP004
00239                                                                      CL**2
00240         PERFORM Z950-COMMIT                                          CL**4
00241                                                                      CL**2
00242         DISPLAY 'UPC CODE DELETED: ' UNUPCS-UPC-CDE               ETKUP004
00243                 '  UPC CAT VEND NAME: ' DISPLAY-VEND-NME          ETKUP004
00244         ADD +1 TO WS-COUNTER                                      ETKUP004
00245      END-IF.                                                      ETKUP004
00246                                                                   ETKUP004
00247      PERFORM B200-FETCH-TUNUPCS.                                  ETKUP004
00248                                                                   ETKUP004
00249  EJECT                                                            ETKUP004
00250                                                                   ETKUP004
00251  B400-CLOSE-TUNUPCS-CURSOR.                                       ETKUP004
00252 ******************************************************************ETKUP004
00253 *    CLOSE THE UNMATCHED TABLE CURSOR                             ETKUP004
00254 *                                                                 ETKUP004
00255 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUP004
00256 ******************************************************************ETKUP004
00257                                                                   ETKUP004
00258      EXEC SQL                                                     ETKUP004
00259           CLOSE TUNUPCS-CURSOR                                    ETKUP004
00260      END-EXEC.                                                    ETKUP004
00261                                                                   ETKUP004
00262      IF (SQLCODE NOT = +0)  OR                                    ETKUP004
00263         (SQLWARN0 = 'W')                                          ETKUP004
00264                                                                   ETKUP004
00265          MOVE 'CLOSE TUNUPCS-CURSOR' TO                           ETKUP004
00266               PV-DB2-OPERATION-ATTEMPTED                          ETKUP004
00267          MOVE 'B400-CLOSE-TUNUPCS-CURSOR' TO                      ETKUP004
00268               PV-PARAGRAPH-NAME                                   ETKUP004
00269                                                                   ETKUP004
00270          PERFORM Z999-SQL-ABEND                                   ETKUP004
00271                                                                   ETKUP004
00272      END-IF.                                                      ETKUP004
00273                                                                   ETKUP004
00274  EJECT                                                            ETKUP004
00275                                                                   ETKUP004
00276  C100-SELECT-SKU.                                                 ETKUP004
00277 ******************************************************************ETKUP004
00278 *    SEE IF A SKU IS SETUP FOR THE UPC CODE ON THE TUPCSKU AND    ETKUP004
00279 *    TSK TABLES.                                                  ETKUP004
00280 *                                                                 ETKUP004
00281 *    PERFORMED BY B300-PROCESS-TUNUPCS.                           ETKUP004
00282 ******************************************************************ETKUP004
00283                                                                   ETKUP004
00284      EXEC SQL                                                     ETKUP004
00285          SELECT UPC_NBR                                           ETKUP004
00286          INTO   :TUPCSKU-UPC-NBR                                  ETKUP004
00287          FROM   TUPCSKU,                                          ETKUP004
00288                 TSK                                               ETKUP004
00289          WHERE  UPC_NBR   = :UNUPCS-UPC-CDE  AND                  ETKUP004
00290                 ITEM_NMBR = ITEM_NBR                              ETKUP004
00291      END-EXEC.                                                    ETKUP004
00292                                                                   ETKUP004
00293       IF SQLCODE = +0 OR SQLCODE = +100                           ETKUP004
00294          CONTINUE                                                 ETKUP004
00295       ELSE                                                        ETKUP004
00296          MOVE 'SELECT FROM TUPCSKU, TSK DB' TO                    ETKUP004
00297               PV-DB2-OPERATION-ATTEMPTED                          ETKUP004
00298          MOVE 'C100-SELECT-SKU'             TO                    ETKUP004
00299               PV-PARAGRAPH-NAME                                   ETKUP004
00300          PERFORM Z999-SQL-ABEND                                   ETKUP004
00301       END-IF.                                                     ETKUP004
00302                                                                   ETKUP004
00303  EJECT                                                            ETKUP004
00304                                                                   ETKUP004
00305  C200-DELETE-TUNUPCS.                                             ETKUP004
00306 ******************************************************************ETKUP004
00307 *    DELETE THE UPC CODE FROM THE UNMATCHED UPC CODE TABLE        ETKUP004
00308 *                                                                 ETKUP004
00309 *    PERFORMED BY B300-PROCESS-TUNUPCS.                           ETKUP004
00310 ******************************************************************ETKUP004
00311                                                                   ETKUP004
00312      EXEC SQL                                                     ETKUP004
00313          DELETE FROM TUNUPCS                                      ETKUP004
00314          WHERE  UPC_CDE = :WS-UPC-CDE                             ETKUP004
00315      END-EXEC.                                                    ETKUP004
00316                                                                   ETKUP004
00317      IF (SQLCODE NOT = +0)  OR                                    ETKUP004
00318         (SQLWARN0 = 'W')                                          ETKUP004
00319                                                                   ETKUP004
00320          MOVE 'DELETE FROM TUNUPCS' TO                            ETKUP004
00321               PV-DB2-OPERATION-ATTEMPTED                          ETKUP004
00322          MOVE 'C200-DELETE-TUNUPCS' TO                            ETKUP004
00323               PV-PARAGRAPH-NAME                                   ETKUP004
00324                                                                   ETKUP004
00325          PERFORM Z999-SQL-ABEND                                   ETKUP004
00326                                                                   ETKUP004
00327      END-IF.                                                      ETKUP004
00328                                                                   ETKUP004
00329  EJECT                                                            ETKUP004
00330  C300-SELECT-VEND-NME.                                            ETKUP004
00331 ***************************************************************** ETKUP004
00332 *    RETRIEVE THE VENDOR ID AND USE IT TO DETERMINE THE NAME.     ETKUP004
00333 *                                                                 ETKUP004
00334 *    PERFORMED BY B300-PROCESS-TUNUPCS                            ETKUP004
00335 ***************************************************************** ETKUP004
00336 *    DISPLAY 'C300-SELECT-VEND-NME'.                              ETKUP004
00337                                                                   ETKUP004
00338      PERFORM D100-SELECT-TNETVND.                                 ETKUP004
00339                                                                   ETKUP004
00355  EJECT                                                            ETKUP004
00356  D100-SELECT-TNETVND.                                             ETKUP004
00357 ***************************************************************** ETKUP004
00358 *    SELECT THE ENTITY ID FROM TNETVND.                           ETKUP004
00359 *                                                                 ETKUP004
00360 *    PERFORMED BY C300-SELECT-VEND-NME                            ETKUP004
00361 ***************************************************************** ETKUP004
00362 *    DISPLAY 'D100-SELECT-TNETVND'.                               ETKUP004
00363                                                                   ETKUP004
00363      PERFORM E100-OPEN-TNETVND-CURSOR.                                    
00363      PERFORM E200-FETCH-TNETVND.                                          
00363                                                                   ETKUP004
00340      IF SQLCODE = +000                                            ETKUP004
00341                                                                   ETKUP004
00342         PERFORM D200-SELECT-TENTNME                               ETKUP004
00343                                                                   ETKUP004
00344         IF SQLCODE = +000                                         ETKUP004
00345            MOVE ENTNME-ENT-NAME-DESC TO DISPLAY-VEND-NME          ETKUP004
00346         ELSE                                                      ETKUP004
00347            MOVE SPACES TO DISPLAY-VEND-NME                        ETKUP004
00348                                                                   ETKUP004
00349      ELSE                                                         ETKUP004
00350                                                                   ETKUP004
00351         MOVE SPACES TO DISPLAY-VEND-NME                           ETKUP004
00352                                                                   ETKUP004
00353      END-IF.                                                      ETKUP004
00383                                                                   ETKUP004
00383      PERFORM E300-CLOSE-TNETVND-CURSOR.                                   
00383                                                                   ETKUP004
00384  EJECT                                                            ETKUP004
00385  D200-SELECT-TENTNME.                                             ETKUP004
00386 ***************************************************************** ETKUP004
00387 *     RETRIEVE THE VENDOR NAME FROM THE TENTNME TABLE             ETKUP004
00388 *                                                                 ETKUP004
00389 *    PERFORMED BY C300-SELECT-VEND-NME                            ETKUP004
00390 ***************************************************************** ETKUP004
00391 *    DISPLAY 'D200-SELECT-TENTNME'.                               ETKUP004
00392                                                                   ETKUP004
00393       EXEC SQL                                                    ETKUP004
00394           SELECT ENT_NAME_DESC                                    ETKUP004
00395           INTO  :ENTNME-ENT-NAME-DESC                             ETKUP004
00396           FROM   TENTNME                                          ETKUP004
00397           WHERE  ENT_ID   = :NETVND-ENT-ID AND                    ETKUP004
00398                  TYPE_CDE = '01'                                  ETKUP004
00399       END-EXEC.                                                   ETKUP004
00400                                                                   ETKUP004
00401       IF SQLCODE = +0 OR SQLCODE = +100                           ETKUP004
00402          CONTINUE                                                 ETKUP004
00403       ELSE                                                        ETKUP004
00404          MOVE 'GET-VENDOR-NAME' TO                                ETKUP004
00405               PV-DB2-OPERATION-ATTEMPTED                          ETKUP004
00406          MOVE 'D200-SELECT-TENTNME'    TO                         ETKUP004
00407               PV-PARAGRAPH-NAME                                   ETKUP004
00408                                                                   ETKUP004
00409          PERFORM Z999-SQL-ABEND                                   ETKUP004
00410       END-IF.                                                     ETKUP004
00411                                                                   ETKUP004
00412  EJECT                                                            ETKUP004
00413                                                                      CL**4
00163  E100-OPEN-TNETVND-CURSOR.                                        ETKUP004
00164 ******************************************************************ETKUP004
00165 *    OPEN THE TNETVND CURSOR                                      ETKUP004
00166 *                                                                 ETKUP004
00167 *    PERFORMED BY D100-SELECT-TNETVND.                            ETKUP004
00168 ******************************************************************ETKUP004
00169                                                                   ETKUP004
00170      EXEC SQL                                                     ETKUP004
00171           OPEN TNETVND-CURSOR                                     ETKUP004
00172      END-EXEC.                                                    ETKUP004
00173                                                                   ETKUP004
00174      IF (SQLCODE NOT = +0)  OR                                    ETKUP004
00175         (SQLWARN0 = 'W')                                          ETKUP004
00176                                                                   ETKUP004
00177          MOVE 'OPEN TNETVND-CURSOR' TO                            ETKUP004
00178               PV-DB2-OPERATION-ATTEMPTED                          ETKUP004
00179          MOVE 'E100-OPEN-TNETVND-CURSOR' TO                       ETKUP004
00180               PV-PARAGRAPH-NAME                                   ETKUP004
00181                                                                   ETKUP004
00182          PERFORM Z999-SQL-ABEND                                   ETKUP004
00183                                                                   ETKUP004
00184      END-IF.                                                      ETKUP004
00185                                                                   ETKUP004
00186  EJECT                                                            ETKUP004
00187                                                                   ETKUP004
00188  E200-FETCH-TNETVND.                                              ETKUP004
00189 ******************************************************************ETKUP004
00190 *    FETCH THE FIRST ENTITY ID FOR THE VENDOR ID FROM TUNUPCS     ETKUP004
00191 *                                                                 ETKUP004
00192 *    PERFORMED BY D100-SELECT-TNETVND.                            ETKUP004
00193 ******************************************************************ETKUP004
00194                                                                   ETKUP004
00195      EXEC SQL                                                     ETKUP004
00196         FETCH TNETVND-CURSOR                                      ETKUP004
00197         INTO  :NETVND-ENT-ID                                      ETKUP004
00201      END-EXEC.                                                    ETKUP004
00202                                                                   ETKUP004
00203      IF SQLCODE = +0 OR SQLCODE = +100                            ETKUP004
00204         CONTINUE                                                  ETKUP004
00205      ELSE                                                         ETKUP004
00206         MOVE 'FETCH TNETVND-CURSOR' TO                            ETKUP004
00207              PV-DB2-OPERATION-ATTEMPTED                           ETKUP004
00208         MOVE 'E200-FETCH-TNETVND' TO                              ETKUP004
00209              PV-PARAGRAPH-NAME                                    ETKUP004
00210                                                                   ETKUP004
00211         PERFORM Z999-SQL-ABEND                                    ETKUP004
00212      END-IF.                                                      ETKUP004
00250                                                                   ETKUP004
00251  E300-CLOSE-TNETVND-CURSOR.                                       ETKUP004
00252 ******************************************************************ETKUP004
00253 *    CLOSE THE NETWORK VENDOR CURSOR                              ETKUP004
00254 *                                                                 ETKUP004
00255 *    PERFORMED BY D100-SELECT-TNETVND.                            ETKUP004
00256 ******************************************************************ETKUP004
00257                                                                   ETKUP004
00258      EXEC SQL                                                     ETKUP004
00259           CLOSE TNETVND-CURSOR                                    ETKUP004
00260      END-EXEC.                                                    ETKUP004
00261                                                                   ETKUP004
00262      IF (SQLCODE NOT = +0)  OR                                    ETKUP004
00263         (SQLWARN0 = 'W')                                          ETKUP004
00264                                                                   ETKUP004
00265          MOVE 'CLOSE TNETVND-CURSOR' TO                           ETKUP004
00266               PV-DB2-OPERATION-ATTEMPTED                          ETKUP004
00267          MOVE 'E300-CLOSE-TNETVND-CURSOR' TO                      ETKUP004
00268               PV-PARAGRAPH-NAME                                   ETKUP004
00269                                                                   ETKUP004
00270          PERFORM Z999-SQL-ABEND                                   ETKUP004
00271                                                                   ETKUP004
00272      END-IF.                                                      ETKUP004
00273                                                                   ETKUP004
00274  EJECT                                                            ETKUP004
00275                                                                   ETKUP004
00414  Z950-COMMIT.                                                        CL**4
00415 ******************************************************************   CL**4
00416 *    COMMIT THE DATABASE UPDATE.                                     CL**4
00417 ******************************************************************   CL**4
00418                                                                      CL**4
00419      EXEC SQL                                                        CL**4
00420         COMMIT                                                       CL**4
00421      END-EXEC.                                                       CL**4
00422 *                                                                    CL**4
00423      IF SQLCODE NOT = +000                                           CL**4
00424          MOVE 'Z950-COMMIT-UPDATE' TO PV-PARAGRAPH-NAME              CL**5
00425          MOVE 'COMMIT' TO PV-DB2-OPERATION-ATTEMPTED                 CL**5
00426          PERFORM Z999-SQL-ABEND                                      CL**4
00427      END-IF.                                                         CL**4
00428                                                                      CL**4
00429  Z999-SQL-ABEND.                                                  ETKUP004
00430 ***************************************************************** ETKUP004
00431 *    ABEND ON AN SQL ERROR OR WARNING                             ETKUP004
00432 ***************************************************************** ETKUP004
00433 *    DISPLAY 'Z999-SQL-ABEND'.                                    ETKUP004
00434                                                                   ETKUP004
00435      DISPLAY '  '.                                                ETKUP004
00436      DISPLAY '** ABEND **'.                                       ETKUP004
00437      DISPLAY 'ETKUP004 - ABEND'.                                  ETKUP004
00438      DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                    ETKUP004
00439      DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.           ETKUP004
00440                                                                   ETKUP004
00441      COPY DPPD004.                                                ETKUP004
00442                                                                   ETKUP004
00443                                                                   ETKUP004
00444      MOVE +4013 TO ABEND-CODE.                                    ETKUP004
00445      CALL 'ILBOABN0' USING ABEND-CODE.                            ETKUP004
00446                                                                   ETKUP004
00447  EJECT                                                            ETKUP004
