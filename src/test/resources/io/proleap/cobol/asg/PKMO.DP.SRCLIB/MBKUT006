00001 *************************                                         03/10/92
00002  IDENTIFICATION DIVISION.                                         MBKUT006
00003 *************************                                            LV006
00004  PROGRAM-ID.    MBKUT006.                                         MBKUT006
00005  AUTHOR.        MIKE FURLICK.                                     MBKUT006
00006  DATE-WRITTEN.  NOVEMBER 25, 1991.                                MBKUT006
00007 *                                                                 MBKUT006
00008 ***************************************************************** MBKUT006
00009 *         CHANGE MERCHANDISE AREA BUYER NUMBER                  * MBKUT006
00010 *                                                               * MBKUT006
00011 *    THIS PROGRAM PROCESSES THE 'MBKUT006' RECORD TYPES FROM    * MBKUT006
00012 *    THE VSAM BATCH CONTROL DATASET.                            * MBKUT006
00013 *                                                               * MBKUT006
00014 *    FOR EACH BATCH CONTROL RECORD, VALIDATE THE RECORD'S       * MBKUT006
00015 *    CONTENTS, ENSURE THE DEPARTMENT EXISTS IN MBS, THEN        * MBKUT006
00016 *    UPDATE TMDSEAR WITH THE NEW BUYER NUMBER.                  * MBKUT006
00017 *                                                               * MBKUT006
00018 *    CHANGED 3/92 BY M. FURLICK TO CHECK THE EFFECTIVE DATE     * MBKUT006
00019 *    ON THE CONTROL RECORD.  IF THE DATE IS BEYOND CURRENT      * MBKUT006
00020 *    CURRENT (STILL IN FUTURE) SKIP THE CONTROL RECORD, BUT     * MBKUT006
00021 *    DON'T DELETE IT.                                           * MBKUT006
00022 *                                                               * MBKUT006
00023 ***************************************************************** MBKUT006
00022 *   06/15/98 DOUG DOUGLAS    STRATAGEM         WR#:6041         * MBKUT006
00022 *            - UPGRADE FOR Y2K                                  * MBKUT006
00022 *            - REPLACE ++INCLUDE'S WITH COPY'S (STANDARDS)      * MBKUT006
00023 ***************************************************************** MBKUT006
00024 *                                                                 MBKUT006
00025  ENVIRONMENT DIVISION.                                            MBKUT006
00026  CONFIGURATION SECTION.                                           MBKUT006
00027  SOURCE-COMPUTER. IBM-3090.                                       MBKUT006
00028  OBJECT-COMPUTER. IBM-3090.                                       MBKUT006
00029 *                                                                 MBKUT006
00030  INPUT-OUTPUT SECTION.                                            MBKUT006
00031  FILE-CONTROL.                                                    MBKUT006
00032 *                                                                 MBKUT006
00033      SELECT BATCH-CONTROL-FILE                                    MBKUT006
00034          ORGANIZATION IS INDEXED                                  MBKUT006
00035          RECORD KEY IS BCR-KEY                                    MBKUT006
00036          FILE STATUS IS PV-VSAM-STATUS                            MBKUT006
00037          ACCESS IS SEQUENTIAL                                     MBKUT006
00038          ASSIGN TO INP01.                                         MBKUT006
00039 *                                                                 MBKUT006
00040  DATA DIVISION.                                                   MBKUT006
00041  FILE SECTION.                                                    MBKUT006
00042 *                                                                 MBKUT006
00043 *                                                                 MBKUT006
00044  FD  BATCH-CONTROL-FILE.                                          MBKUT006
00045 *                                                                 MBKUT006
00046  01  BATCH-CONTROL-RECORD.                                        MBKUT006
00047      05  BCR-KEY               PIC X(30).                         MBKUT006
00048      05  BCR-DATA              PIC X(70).                         MBKUT006
00049 *                                                                 MBKUT006
00050  EJECT                                                            MBKUT006
00051  WORKING-STORAGE SECTION.                                         MBKUT006
00052 *                                                                 MBKUT006
00053  01  PC-PROGRAM-CONSTANTS.                                        MBKUT006
00054      05  PC-MAX-RETRIES            PIC S9(3)  VALUE +3            MBKUT006
00055                                               COMP-3.             MBKUT006
00056      05  PC-DPT                    PIC X(03)  VALUE 'DPT'.        MBKUT006
00060 *                                                                 MBKUT006
00061  01  PS-PROGRAM-SWITCHES.                                         MBKUT006
00062      05  PS-CONTINUE-SW            PIC X      VALUE 'N'.          MBKUT006
00063          88  PS-CONTINUE                      VALUE 'Y'.          MBKUT006
00064          88  PS-DONT-PROCESS                  VALUE 'N'.          MBKUT006
00065 *                                                                 MBKUT006
00066  01  PV-PROGRAM-VARIABLES.                                        MBKUT006
00067      05  PV-DB2-COUNT              PIC S9(9)  VALUE ZERO          MBKUT006
00068                                               COMP-3.             MBKUT006
00069      05  PV-UPDATE-COUNT           PIC S9(9)  VALUE ZERO          MBKUT006
00070                                               COMP-3.             MBKUT006
00071      05  PV-BATCH-CNTL-COUNT       PIC S9(9)  VALUE ZERO          MBKUT006
00072                                               COMP-3.             MBKUT006
00073      05  PV-BATCH-CNTL-SKIPPED     PIC S9(9)  VALUE ZERO          MBKUT006
00074                                               COMP-3.             MBKUT006
00075      05  PV-PARAGRAPH-NAME         PIC X(30)  VALUE SPACE.        MBKUT006
00076      05  PV-DB2-OPER-ATTEMPTED     PIC X(30)  VALUE SPACE.        MBKUT006
00077      05  PV-SQL-SUB                PIC S9(4)  VALUE ZERO          MBKUT006
00078                                               COMP.               MBKUT006
00079      05  PV-VSAM-STATUS            PIC XX     VALUE SPACE.        MBKUT006
00080          88  PV-GOOD-STATUS                   VALUE '00'.         MBKUT006
00081          88  PV-AT-END                        VALUE '10'.         MBKUT006
00082          88  PV-NO-RECORDS                    VALUE '23'.         MBKUT006
00083          88  PV-VERIFIED                      VALUE '97'.         MBKUT006
00084      05  PV-CURRENT-DATE.                                         MBKUT006
00085          10  PV-CD-YY              PIC XX.                        MBKUT006
00086          10  PV-CD-MM              PIC XX.                        MBKUT006
00087          10  PV-CD-DD              PIC XX.                        MBKUT006
00088 *                                                                 MBKUT006
00089  01  PV-DB2-CURRENT-DATE           PIC X(10).                     MBKUT006
00099 *                                                                 MBKUT006
00100  01  ABEND-CODE                    PIC S9(4)  VALUE ZERO          MBKUT006
00101                                               COMP.               MBKUT006
00102      88  ABEND-4001                           VALUE 4001.         MBKUT006
00103      88  ABEND-4002                           VALUE 4002.         MBKUT006
00104      88  ABEND-4003                           VALUE 4003.         MBKUT006
00105      88  ABEND-4004                           VALUE 4004.         MBKUT006
00106      88  ABEND-4005                           VALUE 4005.         MBKUT006
00107      88  ABEND-4006                           VALUE 4006.         MBKUT006
00108      88  ABEND-4013                           VALUE 4013.         MBKUT006
00109      EJECT                                                        MBKUT006
00110 *                                                                 MBKUT006
00111 *    RECORD LAYOUT FOR THE BATCH CONTROL FILE                     MBKUT006
00112 *                                                                 MBKUT006
00113      COPY MBWS018.                                                   CL**6
00114 *                                                                 MBKUT006
00115 *    MDSEAR DCLGEN                                                MBKUT006
00116 *                                                                 MBKUT006
00117      EXEC SQL                                                     MBKUT006
00118          INCLUDE TMDSEAR                                          MBKUT006
00119      END-EXEC.                                                    MBKUT006
00120 *                                                                 MBKUT006
00121 *    SQL COMMUNICATIONS AREA DCLGEN                               MBKUT006
00122 *                                                                 MBKUT006
00123      EXEC SQL                                                     MBKUT006
00124          INCLUDE SQLCA                                            MBKUT006
00125      END-EXEC.                                                    MBKUT006
00126 *                                                                 MBKUT006
00127 *    ERROR MESSAGE AREA FOR DB2                                   MBKUT006
00128 *                                                                 MBKUT006
00129      COPY DPWS004.                                                MBKUT006
00130  EJECT                                                            MBKUT006
00126 ***********************************************                   MBKUT006
00127 *    COPYBOOK FOR STANDARD DATE ROUTINE                           MBKUT006
00128 ***********************************************                   MBKUT006
00129      COPY DPWS500.                                                MBKUT006
00130  EJECT                                                            MBKUT006
00131  PROCEDURE DIVISION.                                              MBKUT006
00132 *                                                                 MBKUT006
00133      PERFORM 1000-MAINLINE.                                       MBKUT006
00134      GOBACK.                                                      MBKUT006
00135  1000-MAINLINE.                                                   MBKUT006
00136 *                                                                 MBKUT006
00137      PERFORM 2000-OPEN-BATCH-CONTROL.                             MBKUT006
00138      IF  PV-GOOD-STATUS                                           MBKUT006
00139          PERFORM 3000-PROCESS-BATCH-CONTROL                       MBKUT006
00140              UNTIL NOT MB018-ASSIGN-BUYER-NUMBER                  MBKUT006
00141          PERFORM 4000-CLOSE-BATCH-CONTROL                         MBKUT006
00142          PERFORM 9000-DISPLAY-TOTALS                              MBKUT006
00143      ELSE                                                         MBKUT006
00144          DISPLAY '*******************************'                MBKUT006
00145          DISPLAY '*                             *'                MBKUT006
00146          DISPLAY '* NO BUYER CHANGE RECORDS FOR *'                MBKUT006
00147          DISPLAY '*                             *'                MBKUT006
00148          DISPLAY '* PROCESSING IN MBKUT006.     *'                MBKUT006
00149          DISPLAY '*                             *'                MBKUT006
00150          DISPLAY '*   ***NORMAL END OF JOB***   *'                MBKUT006
00151          DISPLAY '*******************************'                MBKUT006
00152      END-IF.                                                      MBKUT006
00153 *                                                                 MBKUT006
00154  EJECT                                                            MBKUT006
00155 ***************************************************************** MBKUT006
00156 *                                                               * MBKUT006
00157 *    THIS ROUTINE OPENS THE BATCH CONTROL FILE, ISSUES A        * MBKUT006
00158 *    START TO POSITION IT PROPERLY, AND BUILDS THE DB2          * MBKUT006
00159 *    CURRENT DATE.                                              * MBKUT006
00160 *                                                               * MBKUT006
00161 ***************************************************************** MBKUT006
00162 *                                                                 MBKUT006
00163  2000-OPEN-BATCH-CONTROL.                                         MBKUT006
00164 *                                                                 MBKUT006
00165      OPEN I-O BATCH-CONTROL-FILE.                                 MBKUT006
00166 *                                                                 MBKUT006
00167      IF (PV-GOOD-STATUS                                           MBKUT006
00168      OR  PV-AT-END                                                MBKUT006
00169      OR  PV-VERIFIED)                                             MBKUT006
00170          CONTINUE                                                 MBKUT006
00171      ELSE                                                         MBKUT006
00172          DISPLAY '* * * * * * * * * * * * * * * * *'              MBKUT006
00173          DISPLAY '*    ***MBKUT006 ABEND***       *'              MBKUT006
00174          DISPLAY '*                               *'              MBKUT006
00175          DISPLAY '*   INVALID STATUS ON THE BATCH *'              MBKUT006
00176          DISPLAY '*   CONTROL DATASET - OPEN.     *'              MBKUT006
00177          DISPLAY '*                               *'              MBKUT006
00178          DISPLAY '* STATUS = '                                    MBKUT006
00179                  PV-VSAM-STATUS                                   MBKUT006
00180                  '                  *'                            MBKUT006
00181          DISPLAY '*                               *'              MBKUT006
00182          DISPLAY '* * * * * * * * * * * * * * * * *'              MBKUT006
00183          SET ABEND-4005        TO  TRUE                           MBKUT006
00184          CALL 'ILBOABN0'    USING  ABEND-CODE                     MBKUT006
00185      END-IF.                                                      MBKUT006
00186 *                                                                 MBKUT006
00187      MOVE LOW-VALUES           TO  MB018-BCR-KEY.                 MBKUT006
00188      SET MB018-ASSIGN-BUYER-NUMBER                                MBKUT006
00189                                TO  TRUE.                          MBKUT006
00190      MOVE MB018-BCR-KEY-006 TO  BCR-KEY.                          MBKUT006
00191      START BATCH-CONTROL-FILE                                     MBKUT006
00192            KEY IS GREATER THAN BCR-KEY.                           MBKUT006
00193 *                                                                 MBKUT006
00194      IF  PV-GOOD-STATUS                                           MBKUT006
00195          PERFORM 3100-READ-BATCH-CONTROL                          MBKUT006
00196      ELSE                                                         MBKUT006
00197          IF  PV-NO-RECORDS                                        MBKUT006
00198              CONTINUE                                             MBKUT006
00199          ELSE                                                     MBKUT006
00200              DISPLAY '* * * * * * * * * * * * * * * * *'          MBKUT006
00201              DISPLAY '*    ***MBKUT006 ABEND***       *'          MBKUT006
00202              DISPLAY '*                               *'          MBKUT006
00203              DISPLAY '*   INVALID STATUS ON THE BATCH *'          MBKUT006
00204              DISPLAY '*   CONTROL DATASET - START.    *'          MBKUT006
00205              DISPLAY '*                               *'          MBKUT006
00206              DISPLAY '* STATUS = '                                MBKUT006
00207                      PV-VSAM-STATUS                               MBKUT006
00208                      '                  *'                        MBKUT006
00209              DISPLAY '*                               *'          MBKUT006
00210              DISPLAY '* * * * * * * * * * * * * * * * *'          MBKUT006
00211              SET ABEND-4002        TO  TRUE                       MBKUT006
00212              CALL 'ILBOABN0'    USING  ABEND-CODE                 MBKUT006
00213          END-IF                                                   MBKUT006
00214      END-IF.                                                      MBKUT006
00215 *                                                                 MBKUT006
           INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.                 
00215                                                                   MBKUT006
           SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                              
00215                                                                   MBKUT006
           SET DPG52-LK-DTE-GREG          TO TRUE.                              
00216      ACCEPT PV-CURRENT-DATE FROM DATE.                                    
00217      MOVE PV-CD-MM                  TO  DPG52-GREG-DATE-MO-X.     MBKUT006
00218      MOVE PV-CD-DD                  TO  DPG52-GREG-DATE-DY-X.     MBKUT006
00219      MOVE PV-CD-YY                  TO  DPG52-GREG-DATE-YR-X.     MBKUT006
00215                                                                   MBKUT006
           CALL  DP500-KOHLS-CALENDAR-ROUTINE                                   
                 USING DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.                
00215                                                                   MBKUT006
           IF DPG54-SEVERE-ERROR OR DPG54-WARNING-ERROR                         
00200         DISPLAY '* * * * * * * * * * * * * * * * *'               MBKUT006
00201         DISPLAY '*    ***MBKUT006 ABEND***       *'               MBKUT006
00202         DISPLAY '*                               *'               MBKUT006
00203         DISPLAY '*  INVALID RETURN FROM DPKUT500 *'               MBKUT006
00210         DISPLAY '* * * * * * * * * * * * * * * * *'               MBKUT006
00203         DISPLAY 'ERROR IND.:  '  DPG54-ERROR-IND                  MBKUT006
00203         DISPLAY 'DATE IND.:   '  DPG54-DATE-VALID-IND             MBKUT006
00203         DISPLAY 'ERROR MSG:   '  DPG54-ERROR-MESSAGE              MBKUT006
00211         SET ABEND-4002        TO  TRUE                            MBKUT006
00212         CALL 'ILBOABN0'    USING  ABEND-CODE                      MBKUT006
           ELSE                                                                 
00217         MOVE DPG55-DB2-ISO-DATE-FMT TO PV-DB2-CURRENT-DATE        MBKUT006
00226      END-IF.                                                      MBKUT006
00227 *                                                                 MBKUT006
00228  EJECT                                                            MBKUT006
00229 ****************************************************************  MBKUT006
00230 *                                                              *  MBKUT006
00231 *    PROCESS THE BATCH CONTROL RECORD, VALIDATE THE DATA, IF   *  MBKUT006
00232 *    OK, UPDATE MDSEAR.                                        *  MBKUT006
00233 *                                                              *  MBKUT006
00234 ****************************************************************  MBKUT006
00235 *                                                                 MBKUT006
00236  3000-PROCESS-BATCH-CONTROL.                                      MBKUT006
00237 *                                                                 MBKUT006
00238      DISPLAY '* * * * * * * * * * * * * * * * * * * * * * * * *'     CL**3
00239              ' * * * * * * * * * * * * * * * * * * * * * * * *'.     CL**3
00240      DISPLAY '*'.                                                 MBKUT006
00241      DISPLAY '* ' MB018-BATCH-CNTL-RECORD.                        MBKUT006
00242      DISPLAY '*'.                                                 MBKUT006
00243      ADD 1                     TO  PV-BATCH-CNTL-COUNT.           MBKUT006
00244                                                                   MBKUT006
00245      IF  PV-DB2-CURRENT-DATE LESS MB018-NEW-BUYER-DATE-006        MBKUT006
00246          ADD 1                 TO  PV-BATCH-CNTL-SKIPPED          MBKUT006
00247          DISPLAY '* THIS CONTROL RECORD HAS BEEN BYPASSED;'          CL**5
00248          DISPLAY '* IT WILL BE PROCESSED ON '                        CL**5
00249                  MB018-NEW-BUYER-DATE-006                            CL**5
00250      ELSE                                                         MBKUT006
00251          PERFORM 3200-VALIDATE-DATA                               MBKUT006
00252          IF  PS-CONTINUE                                          MBKUT006
00253              PERFORM 3400-UPDATE-MDSEAR                           MBKUT006
00254          END-IF                                                   MBKUT006
00255          PERFORM 3500-DELETE-BATCH-CONTROL                        MBKUT006
00256      END-IF.                                                      MBKUT006
00257                                                                   MBKUT006
00258      PERFORM 3100-READ-BATCH-CONTROL.                             MBKUT006
00259 *                                                                 MBKUT006
00260  EJECT                                                            MBKUT006
00261 ****************************************************************  MBKUT006
00262 *                                                              *  MBKUT006
00263 *    HERE WE READ THE BATCH CONTROL FILE.                      *  MBKUT006
00264 *                                                              *  MBKUT006
00265 ****************************************************************  MBKUT006
00266 *                                                                 MBKUT006
00267  3100-READ-BATCH-CONTROL.                                         MBKUT006
00268 *                                                                 MBKUT006
00269      READ BATCH-CONTROL-FILE                                      MBKUT006
00270           INTO MB018-BATCH-CNTL-RECORD.                           MBKUT006
00271      IF  PV-GOOD-STATUS                                           MBKUT006
00272      OR  PV-AT-END                                                MBKUT006
00273          CONTINUE                                                 MBKUT006
00274      ELSE                                                         MBKUT006
00275          DISPLAY '* * * * * * * * * * * * * * * * *'              MBKUT006
00276          DISPLAY '*    ***MBKUT006 ABEND***       *'              MBKUT006
00277          DISPLAY '*                               *'              MBKUT006
00278          DISPLAY '*   INVALID STATUS ON THE BATCH *'              MBKUT006
00279          DISPLAY '*   CONTROL DATASET - READ.     *'              MBKUT006
00280          DISPLAY '*                               *'              MBKUT006
00281          DISPLAY '* STATUS = '                                    MBKUT006
00282                  PV-VSAM-STATUS                                   MBKUT006
00283                  '                  *'                            MBKUT006
00284          DISPLAY '*                               *'              MBKUT006
00285          DISPLAY '* * * * * * * * * * * * * * * * *'              MBKUT006
00286          SET ABEND-4002        TO  TRUE                           MBKUT006
00287          CALL 'ILBOABN0'    USING  ABEND-CODE                     MBKUT006
00288      END-IF.                                                      MBKUT006
00289 *                                                                 MBKUT006
00290      IF  PV-AT-END                                                MBKUT006
00291          MOVE LOW-VALUES       TO  MB018-BATCH-CNTL-RECORD        MBKUT006
00292      END-IF.                                                      MBKUT006
00293 *                                                                 MBKUT006
00294  EJECT                                                            MBKUT006
00295 ****************************************************************  MBKUT006
00296 *                                                              *  MBKUT006
00297 *    ENSURE THE DATA, ALBEIT PREVIOUSLY VALIDATED ON-LINE,     *  MBKUT006
00298 *    IS OKAY.                                                  *  MBKUT006
00299 *                                                              *  MBKUT006
00300 ****************************************************************  MBKUT006
00301 *                                                                 MBKUT006
00302  3200-VALIDATE-DATA.                                              MBKUT006
00303 *                                                                 MBKUT006
00304      SET PS-CONTINUE           TO  TRUE.                          MBKUT006
00305 *                                                                 MBKUT006
00306      IF  MB018-OPERATING-CO-006 NOT NUMERIC                       MBKUT006
00307          SET PS-DONT-PROCESS   TO  TRUE                           MBKUT006
00308          DISPLAY '* BATCH CONTROL RECORD ERROR'                   MBKUT006
00309          DISPLAY '* COMPANY NUMBER NOT NUMERIC = '                MBKUT006
00310                  MB018-OPERATING-CO-006 '.'                       MBKUT006
00311          DISPLAY '*'                                              MBKUT006
00312      END-IF.                                                      MBKUT006
00313 *                                                                 MBKUT006
00314      IF  MB018-NEW-BUYER-NUMBER-006 NOT NUMERIC                   MBKUT006
00315          SET PS-DONT-PROCESS   TO  TRUE                           MBKUT006
00316          DISPLAY '* BATCH CONTROL RECORD ERROR'                   MBKUT006
00317          DISPLAY '* NEW BUYER NOT NUMERIC = '                     MBKUT006
00318                  MB018-NEW-BUYER-NUMBER-006 '.'                   MBKUT006
00319          DISPLAY '*'                                              MBKUT006
00320      END-IF.                                                      MBKUT006
00321 *                                                                 MBKUT006
00322 *    NOW, DETERMINE WHETHER ANY ROWS EXIST FOR THE SPECIFIED      MBKUT006
00323 *    DEPARTMENT.                                                  MBKUT006
00324 *                                                                 MBKUT006
00325      PERFORM 3300-DEPARTMENT-COUNT.                               MBKUT006
00326 *                                                                 MBKUT006
00327  EJECT                                                            MBKUT006
00328 ****************************************************************  MBKUT006
00329 *                                                              *  MBKUT006
00330 *    THIS ROUTINE COUNT THE ROWS WITH THE SPECIFIED DEPARTMENT *  MBKUT006
00331 *    NUMBER.                                                   *  MBKUT006
00332 *                                                              *  MBKUT006
00333 ****************************************************************  MBKUT006
00334 *                                                                 MBKUT006
00335  3300-DEPARTMENT-COUNT.                                           MBKUT006
00336 *                                                                 MBKUT006
00337      MOVE ZERO                 TO  PV-DB2-COUNT.                  MBKUT006
00338      PERFORM                                                      MBKUT006
00339          WITH TEST AFTER                                          MBKUT006
00340          VARYING PV-SQL-SUB                                       MBKUT006
00341          FROM 1 BY 1                                              MBKUT006
00342          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       MBKUT006
00343                    OR SQLCODE = 0                                 MBKUT006
00344                    OR SQLCODE = +100)                             MBKUT006
00345 *                                                                 MBKUT006
00346          EXEC SQL                                                 MBKUT006
00347              SELECT                                               MBKUT006
00348                    COUNT(DISTINCT DEPT_NBR)                       MBKUT006
00349               INTO                                                MBKUT006
00350                    :PV-DB2-COUNT                                  MBKUT006
00351               FROM                                                MBKUT006
00352                    TMDSEAR                                        MBKUT006
00353               WHERE                                               MBKUT006
00354                    OPERCO_NBR  = :MB018-OPERATING-CO-006          MBKUT006
00355                AND DEPT_NBR    = :MB018-DEPARTMENT-NUMBER-006     MBKUT006
00356                AND MDSELV_CDE  = :PC-DPT                          MBKUT006
00357          END-EXEC                                                 MBKUT006
00358 *                                                                 MBKUT006
00359          EVALUATE TRUE                                            MBKUT006
00360              WHEN SQLCODE = -904                                  MBKUT006
00361              WHEN SQLCODE = -911                                  MBKUT006
00362              WHEN SQLCODE = +100                                  MBKUT006
00363              WHEN SQLCODE = 0                                     MBKUT006
00364                  CONTINUE                                         MBKUT006
00365              WHEN SQLWARN NOT = SPACES                            MBKUT006
00366              WHEN SQLCODE NOT = ZERO                              MBKUT006
00367                  MOVE '3300-DEPARTMENT-COUNT'                     MBKUT006
00368                                TO  PV-PARAGRAPH-NAME              MBKUT006
00369                  MOVE 'COUNT DEPARTMENTS'                         MBKUT006
00370                                TO  PV-DB2-OPER-ATTEMPTED          MBKUT006
00371                  PERFORM 9900-SQL-ABEND                           MBKUT006
00372          END-EVALUATE                                             MBKUT006
00373      END-PERFORM.                                                 MBKUT006
00374 *                                                                 MBKUT006
00375      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        MBKUT006
00376          MOVE '3300-DEPARTMENT-COUNT'                             MBKUT006
00377                                TO  PV-PARAGRAPH-NAME              MBKUT006
00378          MOVE 'COUNT RETRIES EXCEEDED'                            MBKUT006
00379                                TO  PV-DB2-OPER-ATTEMPTED          MBKUT006
00380          PERFORM 9900-SQL-ABEND                                   MBKUT006
00381      END-IF.                                                      MBKUT006
00382 *                                                                 MBKUT006
00383      IF  PV-DB2-COUNT EQUAL ZERO                                  MBKUT006
00384          SET PS-DONT-PROCESS   TO  TRUE                           MBKUT006
00385          MOVE 4008             TO  RETURN-CODE                    MBKUT006
00386          DISPLAY '* BATCH CONTROL RECORD ERROR'                   MBKUT006
00387          DISPLAY '* NO DEPARTMENTS EXISTS ON MDSEAR. DEPT = '     MBKUT006
00388                  MB018-DEPARTMENT-NUMBER-006 '.'                  MBKUT006
00389          DISPLAY '*'                                              MBKUT006
00390      END-IF.                                                      MBKUT006
00391 *                                                                 MBKUT006
00392  EJECT                                                            MBKUT006
00393 ****************************************************************  MBKUT006
00394 *                                                              *  MBKUT006
00395 *    VALIDATION PASSED, UPDATE MDSEAR.                         *  MBKUT006
00396 *                                                              *  MBKUT006
00397 ****************************************************************  MBKUT006
00398 *                                                                 MBKUT006
00399  3400-UPDATE-MDSEAR.                                              MBKUT006
00400 *                                                                 MBKUT006
00401      PERFORM                                                      MBKUT006
00402          WITH TEST AFTER                                          MBKUT006
00403          VARYING PV-SQL-SUB                                       MBKUT006
00404          FROM 1 BY 1                                              MBKUT006
00405          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       MBKUT006
00406                    OR SQLCODE = 0)                                MBKUT006
00407 *                                                                 MBKUT006
00408          EXEC SQL                                                 MBKUT006
00409             UPDATE                                                MBKUT006
00410                    TMDSEAR                                        MBKUT006
00411             SET                                                   MBKUT006
00412                    BYR_NBR       =  :MB018-NEW-BUYER-NUMBER-006,  MBKUT006
00413                    CHG_DTE       =  :PV-DB2-CURRENT-DATE          MBKUT006
00414             WHERE                                                 MBKUT006
00415                    OPERCO_NBR    =  :MB018-OPERATING-CO-006       MBKUT006
00416              AND   DEPT_NBR      =  :MB018-DEPARTMENT-NUMBER-006  MBKUT006
00417          END-EXEC                                                 MBKUT006
00418 *                                                                 MBKUT006
00419          EVALUATE TRUE                                            MBKUT006
00420              WHEN SQLCODE = -904                                  MBKUT006
00421              WHEN SQLCODE = -911                                  MBKUT006
00422              WHEN SQLCODE = 0                                     MBKUT006
00423                  CONTINUE                                         MBKUT006
00424              WHEN SQLWARN NOT = SPACES                            MBKUT006
00425              WHEN SQLCODE NOT = ZERO                              MBKUT006
00426                  MOVE '3400-UPDATE-MDSEAR'                        MBKUT006
00427                                TO  PV-PARAGRAPH-NAME              MBKUT006
00428                  MOVE 'UPDATE MDSEAR'                             MBKUT006
00429                                TO  PV-DB2-OPER-ATTEMPTED          MBKUT006
00430                  PERFORM 9900-SQL-ABEND                           MBKUT006
00431          END-EVALUATE                                             MBKUT006
00432      END-PERFORM.                                                 MBKUT006
00433 *                                                                 MBKUT006
00434      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        MBKUT006
00435          MOVE '3400-UPDATE-MDSEAR'                                MBKUT006
00436                                TO  PV-PARAGRAPH-NAME              MBKUT006
00437          MOVE 'UPDATE RETRIES EXCEEDED'                           MBKUT006
00438                                TO  PV-DB2-OPER-ATTEMPTED          MBKUT006
00439          PERFORM 9900-SQL-ABEND                                   MBKUT006
00440      END-IF.                                                      MBKUT006
00441 *                                                                 MBKUT006
00442      IF  SQLCODE EQUAL ZERO                                       MBKUT006
00443          ADD SQLERRD(3)        TO  PV-UPDATE-COUNT                MBKUT006
00444      END-IF.                                                      MBKUT006
00445 *                                                                 MBKUT006
00446  EJECT                                                            MBKUT006
00447 ****************************************************************  MBKUT006
00448 *                                                              *  MBKUT006
00449 *    DELETE THE CURRENT BATCH CONTROL RECORD.                  *  MBKUT006
00450 *                                                              *  MBKUT006
00451 ****************************************************************  MBKUT006
00452 *                                                                 MBKUT006
00453  3500-DELETE-BATCH-CONTROL.                                       MBKUT006
00454 *                                                                 MBKUT006
00455      IF  PS-CONTINUE                                              MBKUT006
00456          DISPLAY '* ' SQLERRD(3)                                     CL**3
00457              ' MDSEAR ROWS UPDATED FOR THIS CONTROL RECORD'       MBKUT006
00458          DISPLAY '* '                                                CL**3
00459      END-IF.                                                      MBKUT006
00460 *                                                                 MBKUT006
00461      DELETE BATCH-CONTROL-FILE RECORD.                            MBKUT006
00462 *                                                                 MBKUT006
00463      IF  PV-GOOD-STATUS                                           MBKUT006
00464          CONTINUE                                                 MBKUT006
00465      ELSE                                                         MBKUT006
00466          DISPLAY '* * * * * * * * * * * * * * * * *'              MBKUT006
00467          DISPLAY '*    ***MBKUT006 ABEND***       *'              MBKUT006
00468          DISPLAY '*                               *'              MBKUT006
00469          DISPLAY '*   INVALID STATUS ON THE BATCH *'              MBKUT006
00470          DISPLAY '*   CONTROL DATASET - DELETE    *'              MBKUT006
00471          DISPLAY '*                               *'              MBKUT006
00472          DISPLAY '* STATUS = '                                    MBKUT006
00473                  PV-VSAM-STATUS                                   MBKUT006
00474                  '                  *'                            MBKUT006
00475          DISPLAY '*                               *'              MBKUT006
00476          DISPLAY '* * * * * * * * * * * * * * * * *'              MBKUT006
00477          SET ABEND-4003        TO  TRUE                           MBKUT006
00478          CALL 'ILBOABN0'    USING  ABEND-CODE                     MBKUT006
00479      END-IF.                                                      MBKUT006
00480 *                                                                 MBKUT006
00481  EJECT                                                            MBKUT006
00482 ****************************************************************  MBKUT006
00483 *                                                              *  MBKUT006
00484 *    CLOSE THE BATCH CONTROL FILE.                             *  MBKUT006
00485 *                                                              *  MBKUT006
00486 ****************************************************************  MBKUT006
00487 *                                                                 MBKUT006
00488  4000-CLOSE-BATCH-CONTROL.                                        MBKUT006
00489 *                                                                 MBKUT006
00490      CLOSE BATCH-CONTROL-FILE.                                    MBKUT006
00491 *                                                                 MBKUT006
00492      IF  PV-GOOD-STATUS                                           MBKUT006
00493          CONTINUE                                                 MBKUT006
00494      ELSE                                                         MBKUT006
00495          DISPLAY '* * * * * * * * * * * * * * * * *'              MBKUT006
00496          DISPLAY '*    ***MBKUT006 ABEND***       *'              MBKUT006
00497          DISPLAY '*                               *'              MBKUT006
00498          DISPLAY '*   INVALID STATUS ON THE BATCH *'              MBKUT006
00499          DISPLAY '*   CONTROL DATASET - CLOSE     *'              MBKUT006
00500          DISPLAY '*                               *'              MBKUT006
00501          DISPLAY '* STATUS = '                                    MBKUT006
00502                  PV-VSAM-STATUS                                   MBKUT006
00503                  '                  *'                            MBKUT006
00504          DISPLAY '*                               *'              MBKUT006
00505          DISPLAY '* * * * * * * * * * * * * * * * *'              MBKUT006
00506          SET ABEND-4006        TO  TRUE                           MBKUT006
00507          CALL 'ILBOABN0'    USING  ABEND-CODE                     MBKUT006
00508      END-IF.                                                      MBKUT006
00509 *                                                                 MBKUT006
00510  EJECT                                                            MBKUT006
00511 ****************************************************************  MBKUT006
00512 *                                                              *  MBKUT006
00513 *    DISPLAY THE NUMBER OF RECORDS READ/WRITTEN.               *  MBKUT006
00514 *                                                              *  MBKUT006
00515 ****************************************************************  MBKUT006
00516 *                                                                 MBKUT006
00517  9000-DISPLAY-TOTALS.                                             MBKUT006
00518                                                                      CL**4
00519      DISPLAY '* * * * * * * * * * * * * * * * * * * * * * * * *'     CL**4
00520              ' * * * * * * * * * * * * * * * * * * * * * * * *'.     CL**4
00521                                                                      CL**4
00522      DISPLAY ' '.                                                 MBKUT006
00523      DISPLAY '* * * * * * * * * * * * * * * *'.                      CL**2
00524      DISPLAY '*                             *'.                      CL**2
00525      DISPLAY '*     **MBKUT006 COUNTS**     *'.                      CL**2
00526      DISPLAY '*  '                                                MBKUT006
00527              PV-BATCH-CNTL-COUNT                                  MBKUT006
00528                          ' CTL RECS READ    *'.                      CL**2
00529      DISPLAY '*                             *'.                      CL**2
00530      DISPLAY '*  '                                                   CL**2
00531              PV-BATCH-CNTL-SKIPPED                                MBKUT006
00532                          ' CTL RECS SKIPPED *'.                      CL**2
00533      DISPLAY '*                             *'.                      CL**2
00534      DISPLAY '*  '                                                   CL**2
00535              PV-UPDATE-COUNT                                      MBKUT006
00536                          ' MDSEARS UPDATED  *'.                      CL**2
00537      DISPLAY '*                             *'.                      CL**2
00538      DISPLAY '* * * * * * * * * * * * * * * *'.                      CL**2
00539 *                                                                 MBKUT006
00540  EJECT                                                            MBKUT006
00541 ****************************************************************  MBKUT006
00542 *                                                              *  MBKUT006
00543 *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *  MBKUT006
00544 *                                                              *  MBKUT006
00545 ****************************************************************  MBKUT006
00546 *                                                                 MBKUT006
00547  9900-SQL-ABEND.                                                  MBKUT006
00548 *                                                                 MBKUT006
00549      SET ABEND-4013             TO TRUE.                          MBKUT006
00550      DISPLAY '**  ABEND     **'.                                  MBKUT006
00551      DISPLAY '**  PROGRAM   **  =  MBKUT006'.                     MBKUT006
00552      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            MBKUT006
00553      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        MBKUT006
00554 *                                                                 MBKUT006
00555 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         MBKUT006
00556 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        MBKUT006
00557 *                                                                 MBKUT006
00558      COPY DPPD004.                                                MBKUT006
00559 *                                                                 MBKUT006
00560      CALL 'ILBOABN0'  USING ABEND-CODE.                           MBKUT006
00561 *                                                                 MBKUT006
