       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    HMKUT004.                                                 
       AUTHOR.        JON FIEDLER.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04-06-2001.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      *   HMKUT004 - COMPARE MBS AND RMS MECHANDISE HIERARCHY DATA AND          
      *   CHECK FOR STRUCTURAL DIFFERENCES.                                     
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT RMS-INPUT-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT MBS-INPUT-FILE                                                
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  RMS-INPUT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RMS-INPUT-REC              PIC X(33).                                
                                                                                
                                                                                
                                                                                
       FD  MBS-INPUT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  MBS-INPUT-REC              PIC X(33).                                
                                                                                
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  OUTPUT-RECORD   PIC X(36).                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
           COPY HMRD002.                                                        
                                                                                
           COPY HMRD003.                                                        
                                                                                
           COPY HMRD004.                                                        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-MBS-KEY.                                                      
               10 PV-MBS-HIER-LVL            PIC X(03).                         
               10 PV-MBS-GMA-NBR             PIC X(04).                         
               10 PV-MBS-DMA-NBR             PIC X(04).                         
               10 PV-MBS-DEPT-NBR            PIC X(04).                         
               10 PV-MBS-MAJ-CL-NBR          PIC X(04).                         
               10 PV-MBS-SUB-CL-NBR          PIC X(04).                         
           05  PV-RMS-KEY.                                                      
               10 PV-RMS-HIER-LVL            PIC X(03).                         
               10 PV-RMS-GMA-NBR             PIC X(04).                         
               10 PV-RMS-DMA-NBR             PIC X(04).                         
               10 PV-RMS-DEPT-NBR            PIC X(04).                         
               10 PV-RMS-MAJ-CL-NBR          PIC X(04).                         
               10 PV-RMS-SUB-CL-NBR          PIC X(04).                         
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-ERROR-MESSAGES             PIC 9(13) VALUE ZERO.              
           05  PA-MBS-RECS-READ              PIC 9(13) VALUE ZERO.              
           05  PA-RMS-RECS-READ              PIC 9(13) VALUE ZERO.              
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MORE-RMS-INPUT-RECS-SW       PIC X(01)   VALUE 'Y'.           
               88  PS-MORE-RMS-INPUT-RECS                  VALUE 'Y'.           
               88  PS-NO-MORE-RMS-INPUT-RECS               VALUE 'N'.           
           05  PS-MORE-MBS-INPUT-RECS-SW       PIC X(01)   VALUE 'Y'.           
               88  PS-MORE-MBS-INPUT-RECS                  VALUE 'Y'.           
               88  PS-NO-MORE-MBS-INPUT-RECS               VALUE 'N'.           
           05  PS-RMS-OR-MBS-ERR               PIC X(01)   VALUE '0'.           
               88  PS-RMS-ERROR                            VALUE '1'.           
               88  PS-MBS-ERROR                            VALUE '2'.           
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
                                                                                
           OPEN INPUT  RMS-INPUT-FILE                                           
                       MBS-INPUT-FILE                                           
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM Z100-READ-RMS-INPUT-FILE.                                    
           PERFORM Z200-READ-MBS-INPUT-FILE.                                    
                                                                                
           PERFORM B100-PROCESS-RECS                                            
               UNTIL (PS-NO-MORE-MBS-INPUT-RECS                                 
                 AND PS-NO-MORE-RMS-INPUT-RECS).                                
                                                                                
           CLOSE RMS-INPUT-FILE                                                 
                 MBS-INPUT-FILE                                                 
                 OUTPUT-FILE.                                                   
                                                                                
           DISPLAY '**** HMKUT004 STATISTICS ****'                              
           DISPLAY 'TOTAL ERROR MESSAGES   : ' PA-ERROR-MESSAGES.               
           DISPLAY 'TOTAL MBS RECORDS READ : ' PA-MBS-RECS-READ.                
           DISPLAY 'TOTAL RMS RECORDS READ : ' PA-RMS-RECS-READ.                
           DISPLAY ' '                                                          
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
       B100-PROCESS-RECS.                                                       
      ******************************************************************        
                                                                                
           MOVE HM002-HIER-LVL   TO PV-MBS-HIER-LVL.                            
           MOVE HM002-GMA-NBR    TO PV-MBS-GMA-NBR.                             
           MOVE HM002-DMA-NBR    TO PV-MBS-DMA-NBR.                             
           MOVE HM002-DEPT-NBR   TO PV-MBS-DEPT-NBR.                            
           MOVE HM002-MAJ-CL-NBR TO PV-MBS-MAJ-CL-NBR.                          
           MOVE HM002-SUB-CL-NBR TO PV-MBS-SUB-CL-NBR.                          
                                                                                
                                                                                
           MOVE HM003-HIER-LVL   TO PV-RMS-HIER-LVL.                            
           MOVE HM003-GMA-NBR    TO PV-RMS-GMA-NBR.                             
           MOVE HM003-DMA-NBR    TO PV-RMS-DMA-NBR.                             
           MOVE HM003-DEPT-NBR   TO PV-RMS-DEPT-NBR.                            
           MOVE HM003-MAJ-CL-NBR TO PV-RMS-MAJ-CL-NBR.                          
           MOVE HM003-SUB-CL-NBR TO PV-RMS-SUB-CL-NBR.                          
                                                                                
           IF ((PV-RMS-KEY < PV-MBS-KEY)                                        
              OR PS-NO-MORE-MBS-INPUT-RECS) AND                                 
              PS-MORE-RMS-INPUT-RECS                                            
      *       KEY ON RMS ONLY, ERROR                                            
              MOVE 16 TO HM004-MSG-NBR                                          
              MOVE '1' TO PS-RMS-OR-MBS-ERR                                     
              PERFORM Z000-WRITE-OUTPUT-REC                                     
              PERFORM Z100-READ-RMS-INPUT-FILE                                  
              ELSE                                                              
                IF PV-MBS-KEY = PV-RMS-KEY                                      
      *            KEYS ON BOTH RMS AND MBS, CONTINUE PROCESSING                
                   PERFORM Z100-READ-RMS-INPUT-FILE                             
                   PERFORM Z200-READ-MBS-INPUT-FILE                             
                   ELSE                                                         
                     IF (PV-RMS-KEY > PV-MBS-KEY                                
                         OR PS-NO-MORE-RMS-INPUT-RECS)                          
                         AND PS-MORE-MBS-INPUT-RECS                             
      *                      KEY ON MBS ONLY, ERROR                             
                             MOVE 15 TO HM004-MSG-NBR                           
                             MOVE '2' TO PS-RMS-OR-MBS-ERR                      
                             PERFORM Z000-WRITE-OUTPUT-REC                      
                             PERFORM Z200-READ-MBS-INPUT-FILE                   
                     END-IF                                                     
                END-IF                                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * WRITE OUTPUT RECORD                                                     
      ******************************************************************        
       Z000-WRITE-OUTPUT-REC.                                                   
                                                                                
           IF PS-RMS-ERROR                                                      
              MOVE SPACES TO HM004-SYSTEM                                       
              MOVE HM003-HIER-LVL TO HM004-HIER-LVL                             
              MOVE HM003-GMA-NBR TO HM004-GMA-NBR                               
              MOVE HM003-DMA-NBR TO HM004-DMA-NBR                               
              MOVE HM003-DEPT-NBR TO HM004-DEPT-NBR                             
              MOVE HM003-MAJ-CL-NBR TO HM004-MAJ-CL-NBR                         
              MOVE HM003-SUB-CL-NBR TO HM004-SUB-CL-NBR                         
              MOVE HM003-DKEY TO HM004-DKEY                                     
            ELSE                                                                
              MOVE SPACES TO HM004-SYSTEM                                       
              MOVE HM002-HIER-LVL TO HM004-HIER-LVL                             
              MOVE HM002-GMA-NBR TO HM004-GMA-NBR                               
              MOVE HM002-DMA-NBR TO HM004-DMA-NBR                               
              MOVE HM002-DEPT-NBR TO HM004-DEPT-NBR                             
              MOVE HM002-MAJ-CL-NBR TO HM004-MAJ-CL-NBR                         
              MOVE HM002-SUB-CL-NBR TO HM004-SUB-CL-NBR                         
              MOVE HM002-DKEY TO HM004-DKEY                                     
            END-IF.                                                             
                                                                                
           WRITE OUTPUT-RECORD                                                  
               FROM HM004-MBS-RMS-INTG-REC.                                     
                                                                                
                                                                                
           ADD 1 TO PA-ERROR-MESSAGES.                                          
                                                                                
      ******************************************************************        
       Z100-READ-RMS-INPUT-FILE.                                                
      ******************************************************************        
                                                                                
           INITIALIZE HM003-MBS-RMS-INTG-REC.                                   
                                                                                
           READ RMS-INPUT-FILE INTO HM003-MBS-RMS-INTG-REC                      
               AT END SET PS-NO-MORE-RMS-INPUT-RECS TO TRUE.                    
                                                                                
           ADD 1 TO PA-RMS-RECS-READ.                                           
                                                                                
      ******************************************************************        
       Z200-READ-MBS-INPUT-FILE.                                                
      ******************************************************************        
                                                                                
           INITIALIZE HM002-MBS-RMS-INTG-REC.                                   
                                                                                
           READ MBS-INPUT-FILE INTO HM002-MBS-RMS-INTG-REC                      
               AT END SET PS-NO-MORE-MBS-INPUT-RECS TO TRUE.                    
                                                                                
           ADD 1 TO PA-MBS-RECS-READ.                                           
                                                                                
