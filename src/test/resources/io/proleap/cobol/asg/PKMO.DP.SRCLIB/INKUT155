       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT155                                                  
       AUTHOR.        DORSEY JACKSON                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07-02-2002.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * INKUT155 - TO CREATE PKUNZIP CARD FILE FOR EACH GENERATION              
      *                                                                         
      *     THIS PROGRAM READS CONTROL RECORDS AND CREATES FIVE OUT     00290000
      *     FILES BASED ON INPUT CONTROL FILE.                          00330000
      *                                                                         
      ******************************************************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
25248 * 25248    10-23-2003  UPC CAPABILITIES & STORE NUMBER EXPANSION          
25248 *                      -ROD JANSSEN                                       
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT INP-CONTROL-FILE1                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT INP-CONTROL-FILE2                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT OUT-GEN01-FILE                                                
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT OUT-GEN02-FILE                                                
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT OUT-GEN03-FILE                                                
               ASSIGN TO OUT03.                                                 
                                                                                
           SELECT OUT-GEN04-FILE                                                
               ASSIGN TO OUT04.                                                 
                                                                                
           SELECT OUT-GEN05-FILE                                                
               ASSIGN TO OUT05.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INP-CONTROL-FILE1                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS INP-CONTROL-RECORD1.                                  
                                                                                
       01  INP-CONTROL-RECORD1            PIC X(80).                            
                                                                                
       FD  INP-CONTROL-FILE2                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS INP-CONTROL-RECORD2.                                  
                                                                                
       01  INP-CONTROL-RECORD2            PIC X(80).                            
                                                                                
       FD  OUT-GEN01-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS OUT-GEN01-RECORD.                                     
                                                                                
       01  OUT-GEN01-RECORD               PIC X(80).                            
                                                                                
       FD  OUT-GEN02-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS OUT-GEN01-RECORD.                                     
                                                                                
       01  OUT-GEN02-RECORD               PIC X(80).                            
                                                                                
       FD  OUT-GEN03-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS OUT-GEN03-RECORD.                                     
                                                                                
       01  OUT-GEN03-RECORD               PIC X(80).                            
                                                                                
       FD  OUT-GEN04-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS OUT-GEN04-RECORD.                                     
                                                                                
       01  OUT-GEN04-RECORD               PIC X(80).                            
                                                                                
       FD  OUT-GEN05-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS OUT-GEN05-RECORD.                                     
                                                                                
       01  OUT-GEN05-RECORD               PIC X(80).                            
                                                                                
       EJECT                                                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      *****************************************************************         
      ***************************************************************** 00120000
      *  CONTROL FILE ONE LAYOUT - INPUT.                               00130000
      ***************************************************************** 00140000
                                                                                
       01  CR-CONTROL-RECORD1.                                                  
25248      05  CR1-INSTR                     PIC X(04)   VALUE SPACES.          
           05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  CR1-INJOB                     PIC X(02)   VALUE SPACES.          
25248      05  FILLER                        PIC X(71)   VALUE SPACES.          
                                                                                
       01  CR-CONTROL-RECORD2.                                                  
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  CR2-INCODE                    PIC X(03)   VALUE SPACES.          
           05  FILLER                        PIC X(17)   VALUE SPACES.          
           05  CR2-HLQ-JOB                   PIC X(02)   VALUE SPACES.          
           05  FILLER                        PIC X(03)   VALUE SPACES.          
25248      05  CR2-ARC-STR                   PIC X(04)   VALUE SPACES.          
           05  FILLER REDEFINES CR2-ARC-STR.                                    
25248          10  FILLER                    PIC X(02).                         
               10  CR2-HLQ-GEN               PIC X(02).                         
           05  FILLER                        PIC X(04)   VALUE SPACES.          
           05  CR2-ARC-GEN                   PIC X(02)   VALUE SPACES.          
25248      05  FILLER                        PIC X(43)   VALUE SPACES.          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-RECCNT                     PIC 9(9)   VALUE ZEROES.           
           05  PC-WRITECNT                   PIC 9(9)   VALUE ZEROES.           
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'INKUT155'.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-INPUT-EOF-SW1              PIC X(1)     VALUE 'N'.            
               88  PS-INPUT-EOF1                          VALUE 'Y'.            
           05  PS-INPUT-EOF-SW2              PIC X(1)     VALUE 'N'.            
               88  PS-INPUT-EOF2                          VALUE 'Y'.            
                                                                                
       EJECT                                                                    
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-INKUT155.                                                           
                                                                                
      ****************************************************************          
      *    OPEN ALL FILES.                                                      
      *    PROCESS CONTROL RECORDS                                              
      *    CLOSE ALL FILES.                                                     
      ****************************************************************          
           OPEN INPUT  INP-CONTROL-FILE1                                        
                       INP-CONTROL-FILE2                                        
                OUTPUT OUT-GEN01-FILE                                           
                       OUT-GEN02-FILE                                           
                       OUT-GEN03-FILE                                           
                       OUT-GEN04-FILE                                           
                       OUT-GEN05-FILE .                                         
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS                                                 
               UNTIL PS-INPUT-EOF2.                                             
                                                                                
           CLOSE INP-CONTROL-FILE1                                              
                 INP-CONTROL-FILE2                                              
                 OUT-GEN01-FILE                                                 
                 OUT-GEN02-FILE                                                 
                 OUT-GEN03-FILE                                                 
                 OUT-GEN04-FILE                                                 
                 OUT-GEN05-FILE.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-INITIALIZE.                                                         
      ****************************************************************          
      *    READ FIRST CONTROL RECORD FOR BOTH INPUT FILE.                       
      *    PERFORMED BY 0000-INKUT155.                                          
      ****************************************************************          
                                                                                
           PERFORM 3000-READ-CNTL-FILE1.                                        
           PERFORM 4000-READ-CNTL-FILE2.                                        
                                                                                
       EJECT                                                                    
                                                                                
       2000-PROCESS.                                                            
                                                                                
      ****************************************************************          
      *    CREATE OUTPUT RECORDS                                                
      *    READ THE NEXT CONTROL RECORD                                         
      *                                                                         
      *    PERFORMED BY 0000-INKUT155.                                          
      ****************************************************************          
                                                                                
           IF CR2-INCODE = 'ARC'                                                
               MOVE CR1-INSTR          TO CR2-ARC-STR                           
               MOVE 01                 TO CR2-ARC-GEN                           
               WRITE OUT-GEN01-RECORD  FROM CR-CONTROL-RECORD2                  
               MOVE 02                 TO CR2-ARC-GEN                           
               WRITE OUT-GEN02-RECORD  FROM CR-CONTROL-RECORD2                  
               MOVE 03                 TO CR2-ARC-GEN                           
               WRITE OUT-GEN03-RECORD  FROM CR-CONTROL-RECORD2                  
               MOVE 04                 TO CR2-ARC-GEN                           
               WRITE OUT-GEN04-RECORD  FROM CR-CONTROL-RECORD2                  
               MOVE 05                 TO CR2-ARC-GEN                           
               WRITE OUT-GEN05-RECORD  FROM CR-CONTROL-RECORD2                  
           END-IF.                                                              
                                                                                
           IF CR2-INCODE = 'HLQ'                                                
               MOVE CR1-INJOB          TO CR2-HLQ-JOB                           
               MOVE 01                 TO CR2-HLQ-GEN                           
               WRITE OUT-GEN01-RECORD  FROM CR-CONTROL-RECORD2                  
               MOVE 02                 TO CR2-HLQ-GEN                           
               WRITE OUT-GEN02-RECORD  FROM CR-CONTROL-RECORD2                  
               MOVE 03                 TO CR2-HLQ-GEN                           
               WRITE OUT-GEN03-RECORD  FROM CR-CONTROL-RECORD2                  
               MOVE 04                 TO CR2-HLQ-GEN                           
               WRITE OUT-GEN04-RECORD  FROM CR-CONTROL-RECORD2                  
               MOVE 05                 TO CR2-HLQ-GEN                           
               WRITE OUT-GEN05-RECORD  FROM CR-CONTROL-RECORD2                  
           END-IF.                                                              
                                                                                
           IF CR2-INCODE = 'OUT' OR 'ECH'                                       
               WRITE OUT-GEN01-RECORD  FROM CR-CONTROL-RECORD2                  
               WRITE OUT-GEN02-RECORD  FROM CR-CONTROL-RECORD2                  
               WRITE OUT-GEN03-RECORD  FROM CR-CONTROL-RECORD2                  
               WRITE OUT-GEN04-RECORD  FROM CR-CONTROL-RECORD2                  
               WRITE OUT-GEN05-RECORD  FROM CR-CONTROL-RECORD2                  
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-CNTL-FILE2.                                        
                                                                                
       3000-READ-CNTL-FILE1.                                                    
      ****************************************************************          
      *    READ CONTROL FILE ONE                                                
      *                                                                         
      *    PERFORMED BY 1000-INITIALIZE.                                        
      ****************************************************************          
           READ INP-CONTROL-FILE1 INTO CR-CONTROL-RECORD1                       
               AT END                                                           
                   SET PS-INPUT-EOF1 TO TRUE                                    
                   PERFORM Z300-DISPLAY-CNTL-MESSAGE.                           
                                                                                
       4000-READ-CNTL-FILE2.                                                    
      ****************************************************************          
      *    READ CONTROL FILE ONE                                                
      *                                                                         
      *    PERFORMED BY 1000-INITIALIZE                                         
      *    PERFORMED BY 2000-PROCESS                                            
      ****************************************************************          
           READ INP-CONTROL-FILE2 INTO CR-CONTROL-RECORD2                       
               AT END                                                           
                   SET PS-INPUT-EOF2 TO TRUE.                                   
                                                                                
           EJECT                                                                
                                                                                
       Z300-DISPLAY-CNTL-MESSAGE.                                               
      ****************************************************************          
      *    DISPLAY ERROR MESSAGE                                                
      *    CONTROL RECORD IS MISSING                                            
      *                                                                         
      *    PERFORMED BY 3000-READ-CNTL-DATA                                     
      ****************************************************************          
                                                                                
           DISPLAY 'CONTROL RECORD IS MISSING'.                                 
