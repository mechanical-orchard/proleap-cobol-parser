W31475********************* !!! ATTENTION !!! **************************00001001
W31475* A COPY OF BVPD001 IS CREATED AS BVPD003. ANY CHANGE HERE NEEDES 00002001
W31475* TO REFLECT IN BVPD003 ALSO AND VICE VERSA.                      00003001
      ******************** HISTORY OF CHANGES **************************00010000
      *                                                                *00020000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00030000
      * -------  ----------  ----------------------------------------  *00040000
RONNA *          6/15/2006   REMOVED NON-NUMERIC CHECK ON SKU          *00050000
RONNA *                      A SKU WAS GETTING IN THAT HAD ' 9999999'  *00051000
RONNA *                      INSTEAD OF CONTAINING A ZERO IN THE FIRST *00052000
RONNA *                      DIGIT.                                    *00053000
      ******************************************************************00054000
RYAN  *          7/21/2006   ADDED BV001-PV-PAST-FUT-ITEM-REL-SW TO    *00055000
RYAN  *                      INDICATE IF AN ITEM WAS FOUND BEFORE OR   *00056000
RYAN  *                      AFTER THE SPECIFIED DATE.                 *00057000
      ******************************************************************00058000
RONNA2*         10/05/2006   REMOVE LOGIC FOR SKU, ITEM, UPC, UPC ID,  *00058100
RONNA2*                      AND SKU DEPT LOOKUPS.                      00058200
RONNA2*                      THE PRICING/ATTRIBUTE UTILITY WILL BE     *00058300
RONNA2*                      USED TO GET THE ATTRIBUTES, THEN          *00058400
RONNA2*                      BV UTILTY WILL BE CALLED THROUGH THE       00058500
RONNA2*                      PRICING MODULE                             00058600
      ******************************************************************00058700
      * THIS COPY CODE NEEDS BVWS001 TO FUNCTION.                       00058800
      * ALSO, THE CALLING PROGRAM NEEDS TO HAVE THE FOLLOWING TABLES    00058900
      * TSKXREF                                                         00059000
      * TVNDDPT                                                         00060000
      * TVNDSTL                                                         00070000
      * TUPC                                                            00080000
      * TUPCSKU                                                         00090000
      * TMDARVS                                                         00100000
      * IMT00017 (BRAND VENDOR RELATIONSHIP TABLE)                      00110000
      * MLT00011 (UNKNOWN BRAND VENDOR RELATIONSHIP TABLE)              00120000
      * MLV00000 (VIEW FOR COMBINED KNOWN AND UNKNOWN BRAND VENDOR      00130000
      *           RELATIONSHIP TABLES)                                  00140000
      *                                                                 00141000
      *  HOW TO USE THIS:                                               00142000
      *  ### SEARCHES ###                                               00143000
      *  - THE USER CAN GET BRAND VENDOR RELATIONSHIP(S) THROUGH THE    00144000
      *    FOLLOWING SEARCHES:                                          00145000
      *    -  BRAND (MAY RETURN MULTIPLE RECORDS)                       00146000
      *    -  BRAND/DEPT (MAY RETURN MULTIPLE RECORDS)                  00147000
      *    -  BRAND/VENDOR/DEPT                                         00148000
      *    -  ITEM NUMBER                                               00149000
      *    -  KNOWN DEPT/UNKNOWN BRAND/VENDOR MAY BE KNOWN OR UNKNOWN   00150000
      *    -  RELATIONSHIP ID                                           00160000
      *    -  SKU                                                       00170000
      *    -  SKU/DEPT                                                  00180000
      *    -  UPC ID                                                    00190000
      *    -  UPC NUMBER                                                00200000
      *    -  VENDOR (MAY RETURN MULTIPLE RECORDS)                      00201000
      *    -  VENDOR/DEPT (MAY RETURN MULTIPLE RECORDS)                 00202000
      *                                                                 00203000
      *    ###  SEARCHES RETURNING A SINGLE ROW   ###                   00204000
      *    -    THE FOLLOWING SEARCHES WILL RETURN A SINGLE ROW:        00205000
      *         - BRAND VENDOR DEPARTMENT                               00206000
      *         - RELATIONSHIP ID                                       00207000
      *         - KNOWN DEPT/UNKNOWN BRAND/VEND MAY BE KNOWN OR UNKNOWN 00208000
      *                                                                 00209000
      *    -    THE RESULTS OF THESE SEARCHES WILL BE CONTAINED IN:     00210000
      *         - BV001-PV-ENT-ID-OUT                                   00220000
      *         - BV001-PV-BRAND-OUT                                    00230000
      *         - BV001-PV-DEPT-OUT                                     00240000
      *         - BV001-PV-RLTNSHP-ID-OUT                               00250000
      *                                                                 00260000
      *    ###  SEARCHES RETURNING MULTIPLE ROWS  ###                   00270000
      *    -    THE FOLLOWING SEARCHES CAN RETURN MULTIPLE ROWS:        00280000
      *         - BRAND                                                 00290000
      *         - BRAND DEPT                                            00300000
      *         - VENDOR                                                00310000
      *         - DEPARTMENT VENDOR                                     00320000
      *                                                                 00330000
      *    -    A RECORD IN THE TABLE WILL CONTAIN VENDOR NUMBER,       00340000
      *         BRAND NUMBER, DEPARTMENT NUMBER, AND RELATIONSHIP ID.   00350000
      *                                                                 00360000
      *    -    THE NUMBER OF RECORDS IN THE TABLE WILL BE FOUND IN     00361000
      *         BV001-PV-RT-SUB-OUT.                                    00362000
      *                                                                 00363000
      *    ###  VENDOR DEPARTMENT SEARCH NOTES  ###                     00364000
      *    -    THE VENDOR DEPARTMENT SEARCH CAN FIND PAST OR FUTURE    00365000
      *         RELATIONSHIPS IF BV001-PV-VND-PAST-FUT-IN IS ON.        00366000
      *         IF THAT SWITCH IS ON BV001-PV-PAST-MONTHS MUST BE       00367000
      *         FILLED IN WITH THE NUMBERS OF MONTHS IN THE PAST YOU    00367100
      *         WANT TO LOOK.  IF NOTHING IS FOUND IN THE PAST, THEN    00367200
      *         THE PROCESS LOOKS FOR ANY FUTURE RELATIONSHIPS.         00367300
      *                                                                 00367400
      *    -    IF NO RELATIONSHIPS ARE FOUND IN THE TIMEFRAME YOU ARE  00367500
      *         LOOKING FOR, THIS UTILITY WILL GET THE KNOWN VENDOR/    00367600
      *         UNKNOWN BRAND ROW FOR THE DEPARTMENT.  IF ONE IS NOT    00367700
      *         NOT FOUND IT WILL BE INSERTED AND THEN RETURNED IN:     00367800
      *         - BV001-PV-ENT-ID-OUT                                   00367900
      *         - BV001-PV-BRAND-OUT                                    00368000
      *         - BV001-PV-DEPT-OUT                                     00369000
      *         - BV001-PV-RLTNSHP-ID-OUT                               00369100
      *                                                                 00369200
      *    ###  DATES  ###                                              00369300
      *    -  FOR EACH SEARCH A DATE CAN BE PROVIDED.  IF THE DATE IS   00369400
      *       PROVIDED THE SEARCH WILL LOOK FOR RELATIONSHIPS ACTIVE ON 00369500
      *       THE SPECIFIED DATE. IF NO DATE IS PROVIDED THE DATE WILL  00369600
      *       DEFAULT TO THE CURRENT DATE.                              00369700
      *                                                                 00369800
      *    -  FOR THE ITEM NUMBER, SKU, UPC ID, AND UPC NBR LOOKUPS, IF 00369900
      *       A RELATIONSHIP ROW IS NOT FOUND THAT IS ACTIVE ON THE     00370000
      *       SPECIFIED DATE, A CHECK IS DONE TO FIND THE RELATIONSHIP  00370100
      *       INFORMATION FOR THE SPECIFIED BRAND/DEPT/VENDOR THAT      00370200
      *       MOST RECENTLY EXPIRED.  IF THAT ROW IS NOT FOUND, A       00370300
      *       CHECK IS DONE TO FIND THE FIRST ROW FOR THE SPECIFIED     00370400
      *       BRAND/VENDOR/DEPT THAT IS ACTIVE IN THE FUTURE.  IF       00370500
      *       NO ROW IS FOUND THE RELATIONSHIP INFORMATION FOR THE      00370600
      *       UNKNOWN BRAND/UNKNOWN VENDOR FOR THE DEPARTMENT IS        00370700
      *       RETRIEVED UNLESS THE ENT ID IS PROVIDED, IN WHICH CASE    00370800
      *       THE KNOWN VENDOR/UNKNOWN BRAND ROW IS PROVIDED.           00370900
      *                                                                 00371000
      *    ###  DUMMY SKUS  ###                                         00371100
      *    -  IF A SKU ENDING IN 000 OR 999 IS PROVIDED IT IS PRESUMED  00371200
      *       TO BE A DUMMY SKU.  USERS CAN CHOOSE WHETHER TO FIND THE  00371300
      *       UNKNOWN VENDOR/UNKNOWN BRAND ROW FOR A DEPARTMENT.  IF    00371400
      *       THEY CHOOSE TO AND THE DEPARTMENT FIELD IS POPULATED      00371500
      *       THE UNKNOWN/UNKNOWN ROW WILL BE POPULATED, OTHERWISE AN   00371600
      *       ERROR IS RETURNED.  IF THE SKU IS A DUMMY SKU THEN        00371700
      *       BV001-DUMMY-SKU WILL BE TRUE, REGARDLESS OF WHETHER THE   00371800
      *       USER IS GETTING THE UNKNOWN/UNKNOWN ROW.  IF THE USER     00371900
      *       FINDS A DUMMY SKU BUT IS NOT USING THE UNKNOWN/UNKNOWN    00372000
      *       ROW AN ERROR IS !!NOT!! RETURNED.                         00372100
      *                                                                 00372200
      *    ###  ERROR HANDLING  ###                                     00372300
      *    -    IF FUNCTIONS IN THIS MODULE FIND ERRORS, AN ABEND       00372400
      *         !! WILL NOT !! OCCUR.                                   00372500
      *    -    THE SQL CODE WILL BE IN BV001-PV-SQLCODE-OUT            00372600
      *                                                                 00372700
      *    -    THE ERROR TEXT WILL BE IN BV001-PV-RETURN-MSG-1 & 2     00372800
      *                                                                 00372900
      *    -    THE RETURN CODE FIELD BV001-PV-RETURN-CDE WILL HAVE :   00373000
      *         BV001-PV-NO-ERRORS-FOUND                                00373100
      *         BV001-PV-NO-REL-FOUND (NO RELATIONSHIP FOR SPECIFIED    00373200
      *                               (DATE, OR PAST, OR FUTURE)        00373300
      *         BV001-BAD-INPUT                                         00373400
      *         BV001-ITEM-NOT-FOUND                                    00373500
      *         BV001-REL-ID-NOT-FOUND                                  00373600
      *         BV001-PV-SQL-ERROR                                      00373700
      *         BV001-NO-RLTNSHP-FOR-DTE (PAST OR FUTURE DOES EXIST)    00373800
      *         BV001-PV-INVALID-FUNCTION                               00373900
      *         BV001-PV-OTHER-ERROR                                    00374000
      *         BV001-PV-ZERO-BRAND                                     00374100
      *                                                                 00374200
      *         THE FOLLOWING ERROR TYPE REPRESENT MULTIPLE ERRORS      00374300
      *             BV001-PV-ERROR-FOUND                                00374400
      *                BV001-PV-NO-REL-FOUND                            00374500
      *                BV001-BAD-INPUT                                  00374600
      *                BV001-ITEM-NOT-FOUND                             00374700
      *                BV001-REL-ID-NOT-FOUND                           00374800
      *                BV001-PV-SQL-ERROR                               00374900
      *                BV001-NO-RLTNSHP-FOR-DTE                         00375000
      *                BV001-PV-INVALID-FUNCTION                        00375100
      *                BV001-PV-OTHER-ERROR                             00375200
      *                BV001-PV-ZERO-BRAND                              00375300
      *                                                                 00375400
      *             BV001-FOUND-REL-OR-UNK     (NOT FOR CURSOR USE      00375500
      *               BV001-PV-NO-ERRORS-FOUND  OR STRAIGHT REL LOOKUP) 00375600
      *               BV001-PV-NO-REL-FOUND                             00375700
      *               BV001-ITEM-NOT-FOUND                              00375800
      *               BV001-PV-ZERO-BRAND                               00375900
      *                                                                 00376000
      *             BV001-ITM-OR-REL-NOT-FOUND (NOT FOR CURSOR USE)     00376100
      *               BV001-PV-NO-REL-FOUND                             00376200
      *               BV001-ITEM-NOT-FOUND                              00376300
      *               BV001-PV-ZERO-BRAND                               00376400
      *                                                                 00376500
      *    - BECAUSE OF THE COMPLEXITIES OF WHAT MAY OR MAY NOT         00376600
      *      CONSTITUTE AN ERROR, IT MAY JUST BE BEST TO CODE EACH      00376700
      *      INDIVIDUAL CONDITION AS APPROPRIATE.                       00376800
      *                                                                 00376900
      *                                                                 00377000
RYAN  *    - IN THE EVENT AN DBV RELATIONSHIP IS NOT FOUND FOR THE      00377100
RYAN  *      SPECIFIED DATE, THE UTILITY WILL CHECK FOR A PAST          00377200
RYAN  *      RELATIONSHIP.  IF NO PAST RELATIONSHIP IS FOUND, THE       00377300
RYAN  *      UTILITY WILL CHECK FOR A FUTURE RELATIONSHIP.  IF NO       00377400
RYAN  *      FUTURE RELATIONSHIP IS FOUND, THE UNKNOWN BRAND ROW FOR    00377500
RYAN  *      THE VENDOR/DEPARTMENT WILL BE LOOKED UP.                   00377600
RYAN  *      IF A PAST RELATIONSHIP IS FOUND BV001-PAST-DBV-REL-FOUND   00377700
RYAN  *      IS SET TO TRUE.                                            00377800
RYAN  *      IF A FUTURE RELATIONSHIP IS FOUND BV001-FUT-DBV-REL-FOUND  00377900
RYAN  *      IS SET TO TRUE.                                            00378000
      *                                                                 00378100
      *   ###  ZERO BRAND  ###                                          00378200
      *    - A ZERO BRAND CONDITION CAN OCCUR WHEN LOOKING UP:          00378300
      *        ITEM NUMBER                                              00378400
      *        SKU                                                      00378500
      *        SKU DEPARTMENT                                           00378600
      *        UPC ID                                                   00378700
      *        UPC NUMBER                                               00378800
      *                                                                 00378900
      *    - IF A ZERO BRAND IS FOUND:                                  00379000
      *      -  BV001-PV-ZERO-BRAND IS SET TO TRUE                      00379100
      *      -  IF THE INPUT ENT ID FIELD AND INPUT DEPARTMENT FIELD    00379200
      *         ARE BOTH POPULATED THE UNKNOWN BRAND ROW FOR THAT       00379300
      *         DEPARTMENT AND VENDOR IS FOUND (AFTER VALIDATING THAT   00379400
      *         THE INPUT ENT ID AND DEPARTMENT ARE VALID).             00379500
      *      -  IF THE INPUT ENT ID FIELD IS POPULATED BUT THE INPUT    00379600
      *         DEPARTMENT IS NOT, THEN THE ENT ID IS VALIDATED AND     00379700
      *         THEN THE UNKNOWN BRAND ROW FOR THE INPUT ENT ID AND     00379800
      *         THE DEPARTMENT FOR THE ITEM IS FOUND.                   00379900
      *     -   IF THE INPUT DEPARTMENT FIELD IS POPULATED BUT THE INPUT00380000
      *         ENT ID IS NOT, THEN THE DEPARTMENT IS VALIDATED AND     00380100
      *         THEN THE UNKNOWN BRAND ROW FOR THE INPUT DEPARTMENT AND 00380200
      *         THE ENT ID FOR THE ITEM IS FOUND.                       00380300
      *    ###  UNKNOWN BRAND ###                                       00380400
      *    -  A USER CAN GET THE RELATIONSHIP INFORMATION FOR           00380500
      *       KNOWN DEPARTMENT/UNKNOWN BRAND/KNOWN OR UNKNOWN DEPARTMENT00380600
      *                                                                 00380700
      *    -  IF THE DEPARTMENT IS NOT PROVIDED, AN ERROR IS RETURNED.  00380800
      *                                                                 00380900
      *    -  IF THE ROW DOES NOT ALREADY EXIST FOR THE SPECIFIED       00381000
      *       COMBINATION. THE ROW IS INSERTED ONTO THE RELATIONSHIP    00381100
      *       TABLE.                                                    00381200
      *                                                                 00381300
      *    -  FOR LOOKING UP UNKNOWN VENDOR, THE USER CAN EITHER        00381400
      *       PROVIDE THE UNKNOWN ENT ID, SPACES OR ZEROES FOR THE ENT  00381500
      *       ID.                                                       00381600
      *                                                                 00381700
      *    -  THE FOLLOWING SEARCHES WILL RETURN EITHER AN UNKNOWN      00381800
      *       BRAND/KNOWN VENDOR ROW OR AN UNKNOWN VENDOR/UNKNOWN       00381900
      *       BRAND ROW IF THE DEPARTMENT IS FILLED IN.                 00382000
      *       -  ITEM NUMBER                                            00382100
      *       -  SKU                                                    00382200
      *       -  UPC ID                                                 00382300
      *       -  UPC NUMBER                                             00382400
      *       -  VENDOR/DEPT                                            00382500
      *       -  NOTE:                                                  00382600
      *          IF THE ENT ID IS FILLED IN, THE KNOWN VENDOR/UNKNOWN   00382700
      *          BRAND ROW WILL BE RETRIEVED.  OTHERWISE THE UNKNOWN    00382800
      *          BRAND/UNKNOWN VENDOR ROW WILL BE RETRIEVED.            00382900
      *    #  OTHER NOTES                                               00383000
      *    -  BECAUSE OF DIFFERENCES IN FIELD SIZES YOU MAY WISH TO     00384000
      *       MOVE INPUT VARIABLES TO THEIR -9 VERSIONS INSTEAD OF      00385000
      *       THE X VERSIONS.                                           00386000
      *                                                                 00387000
      ******************************************************************00388000
       BV001-LOOKUP-MAIN.                                               00389000
                                                                        00390000
            INITIALIZE BV001-PV-WORK-AREA.                              00391000
            INITIALIZE BV001-PV-OUTPUT-AREA.                            00391100
            MOVE SPACES TO BV001-PV-RETURN-MSG-1                        00391200
                           BV001-PV-RETURN-MSG-2.                       00391300
            PERFORM BV001-SET-DATE.                                     00391400
            IF BV001-PV-NO-ERRORS-FOUND                                 00391500
               SET BV001-PV-MORE-RECORDS TO TRUE                        00391600
                                                                        00391700
               IF NOT BV001-BAD-INPUT                                   00391800
                  EVALUATE TRUE                                         00391900
                    WHEN BV001-PV-BRND-DEPT-LOOKUP                      00392000
                         PERFORM BV001-LOOKUP-BRND-DPT                  00393000
                    WHEN BV001-PV-BRND-LOOKUP                           00394000
                         PERFORM BV001-LOOKUP-BRND                      00395000
                    WHEN BV001-PV-BRND-VND-DPT-LOOKUP                   00396000
                         PERFORM BV001-LOOKUP-BVD                       00397000
                    WHEN BV001-PV-REL-ID-LOOKUP                         00398000
                         PERFORM BV001-LOOKUP-REL-ID                    00399000
                    WHEN BV001-PV-UNK-BRND-LOOKUP                       00400000
                         PERFORM BV001-LOOKUP-UNKNOWN                   00410000
                    WHEN BV001-PV-VNDR-DEPT-LOOKUP                      00420000
                         PERFORM BV001-LOOKUP-VND-DPT                   00430000
                    WHEN BV001-PV-VNDR-LOOKUP                           00440000
                         PERFORM BV001-LOOKUP-VND                       00450000
                    WHEN OTHER                                          00460000
                         SET BV001-PV-INVALID-FUNCTION TO TRUE          00470000
                         STRING 'INVALID FUNCTION OF ' DELIMITED BY SIZE00480000
                               BV001-PV-LOOKUP-TYPE-IN DELIMITED BY SIZE00490000
                                'IN BV001-LOOKUP-MAIN' DELIMITED BY SIZE00500000
                           INTO BV001-PV-RETURN-MSG-1                   00510000
                  END-EVALUATE                                          00520000
              END-IF                                                    00530000
           END-IF.                                                      00540000
                                                                        00550000
      ******************************************************************00560000
      * PROCESS BRAND VENDOR LOOKUP FOR A BRAND.                        00570000
      ******************************************************************00580000
       BV001-LOOKUP-BRND.                                               00590000
            PERFORM BV001-CHECK-BRAND.                                  00600000
            IF BV001-PV-NO-ERRORS-FOUND                                 00610000
               PERFORM BV001-OPEN-BRND-CSR                              00620000
               IF BV001-PV-NO-ERRORS-FOUND                              00630000
                  PERFORM BV001-FETCH-BRND-CSR                          00640000
                  PERFORM BV001-LOAD-RLTNSHP-TABLE                      00650000
                     UNTIL BV001-PV-NO-MORE-RECORDS OR                  00660000
                           BV001-PV-ERROR-FOUND                         00670000
                  IF BV001-PV-NO-ERRORS-FOUND                           00680000
                     PERFORM BV001-CLOSE-BRND-CSR                       00690000
                  END-IF                                                00700000
               END-IF                                                   00710000
            END-IF.                                                     00720000
      ******************************************************************00730000
      * PROCESS BRAND VENDOR LOOKUP FOR A BRAND DEPARTMENT              00740000
      ******************************************************************00750000
       BV001-LOOKUP-BRND-DPT.                                           00760000
            PERFORM BV001-CHECK-BRAND.                                  00770000
            IF BV001-PV-NO-ERRORS-FOUND                                 00780000
               PERFORM BV001-CHECK-DEPT                                 00790000
               IF BV001-PV-NO-ERRORS-FOUND                              00800000
                  PERFORM BV001-OPEN-BRND-DPT-CSR                       00801000
                  IF BV001-PV-NO-ERRORS-FOUND                           00801100
                     PERFORM BV001-FETCH-BRND-DPT-CSR                   00801200
                     PERFORM BV001-LOAD-RLTNSHP-TABLE                   00801300
                        UNTIL BV001-PV-NO-MORE-RECORDS OR               00801400
                              BV001-PV-ERROR-FOUND                      00801500
                     IF BV001-PV-NO-ERRORS-FOUND                        00801600
                        PERFORM BV001-CLOSE-BRND-DPT-CSR                00801700
                     END-IF                                             00801800
                  END-IF                                                00801900
               END-IF                                                   00802000
            END-IF.                                                     00802100
      ******************************************************************00802200
      * PROCESS BRAND VENDOR LOOKUP FOR A BRAND/VENDOR/DEPT.            00802300
      ******************************************************************00802400
       BV001-LOOKUP-BVD.                                                00802500
            PERFORM BV001-CHECK-ENT-ID.                                 00802600
            IF BV001-PV-NO-ERRORS-FOUND                                 00802700
               PERFORM BV001-CHECK-DEPT                                 00802800
               IF BV001-PV-NO-ERRORS-FOUND                              00802900
                  PERFORM BV001-CHECK-BRAND                             00803000
                  IF BV001-PV-NO-ERRORS-FOUND                           00803100
                     PERFORM BV001-LOOKUP-RLTNSHP                       00803200
                  END-IF                                                00803300
               END-IF                                                   00803400
            END-IF.                                                     00803500
      ******************************************************************00803600
      * GET FUTURE RELATIONSHIP INFORMATION FOR A DEPT/BRAND/VENDOR     00803700
      ******************************************************************00803800
       BV001-LOOKUP-FUT-RLTNSHP.                                        00803900
           EXEC SQL                                                     00804000
              SELECT  DEPT_BRND_VND_ID                                  00804100
                     ,DEPT_NBR                                          00804200
                     ,LBL_SEQ_NBR                                       00804300
                     ,ENT_ID                                            00804400
               INTO :BV001-PV-RLTNSHP-ID-OUT                            00804500
                   ,:BV001-PV-DEPT-OUT                                  00804600
                   ,:BV001-PV-BRAND-OUT                                 00804700
                   ,:BV001-PV-ENT-ID-OUT                                00804800
               FROM IMT_DEPT_BRND_VND                                   00804900
              WHERE ENT_ID        = :BV001-PV-WORK-ENT-ID               00805000
                AND LBL_SEQ_NBR   = :BV001-PV-WORK-BRAND                00805100
                AND DEPT_NBR      = :BV001-PV-WORK-DEPT                 00805200
                AND EFF_BEG_DTE   = (SELECT MIN(EFF_BEG_DTE)            00805300
                                       FROM IMT_DEPT_BRND_VND BV2       00805400
                                      WHERE ENT_ID      =               00805500
                                            :BV001-PV-WORK-ENT-ID       00805600
                                        AND LBL_SEQ_NBR =               00805700
                                            :BV001-PV-WORK-BRAND        00805800
                                        AND DEPT_NBR    =               00805900
                                            :BV001-PV-WORK-DEPT         00806000
                                        AND EFF_BEG_DTE >               00806100
                                            :BV001-PV-WORK-DATE)        00806200
              WITH UR                                                   00806300
           END-EXEC                                                     00806400
                                                                        00806500
           EVALUATE TRUE                                                00806600
              WHEN SQLCODE = 0                                          00806700
RYAN               SET BV001-FUT-DBV-REL-FOUND TO TRUE                  00806800
              WHEN SQLCODE = +100                                       00806900
                   SET BV001-PV-NO-REL-FOUND TO TRUE                    00807000
                   PERFORM BV001-LOOKUP-UNKNOWN                         00807100
              WHEN OTHER                                                00807200
                   SET BV001-PV-SQL-ERROR      TO TRUE                  00807300
                   SET BV001-WORK-ERROR-BV-DPT TO TRUE                  00807400
                   PERFORM BV001-SET-ERROR-MSG2                         00807500
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 00807600
                                   BV001-PV-WORK-SQL-CODE               00807700
                   STRING 'FAIL FUTURE RELATIONSHIP LOOKUP '            00807800
                                                 DELIMITED BY SIZE      00807900
                          'IN BV001-LOOKUP-FUT-RLTNSHP'                 00808000
                                                 DELIMITED BY SIZE      00809000
                     INTO BV001-PV-RETURN-MSG-1                         00809100
           END-EVALUATE.                                                00809200
      ******************************************************************00809300
      * GET PAST RELATIONSHIP INFORMATION FOR A DEPT/BRAND/VENDOR       00809400
      ******************************************************************00809500
       BV001-LOOKUP-PAST-RLTNSHP.                                       00809600
           EXEC SQL                                                     00809700
              SELECT DEPT_BRND_VND_ID                                   00809800
                    ,DEPT_NBR                                           00809900
                    ,LBL_SEQ_NBR                                        00810000
                    ,ENT_ID                                             00811000
                INTO :BV001-PV-RLTNSHP-ID-OUT                           00812000
                    ,:BV001-PV-DEPT-OUT                                 00813000
                    ,:BV001-PV-BRAND-OUT                                00814000
                    ,:BV001-PV-ENT-ID-OUT                               00815000
                FROM IMT_DEPT_BRND_VND                                  00816000
               WHERE ENT_ID        = :BV001-PV-WORK-ENT-ID              00817000
                 AND LBL_SEQ_NBR   = :BV001-PV-WORK-BRAND               00818000
                 AND DEPT_NBR      = :BV001-PV-WORK-DEPT                00818100
                 AND EFF_END_DTE   = (SELECT MAX(EFF_END_DTE)           00818200
                                        FROM IMT_DEPT_BRND_VND BV2      00818300
                                       WHERE ENT_ID      =              00818400
                                             :BV001-PV-WORK-ENT-ID      00818500
                                         AND LBL_SEQ_NBR =              00818600
                                             :BV001-PV-WORK-BRAND       00818700
                                         AND DEPT_NBR    =              00818800
                                             :BV001-PV-WORK-DEPT        00818900
                                         AND EFF_END_DTE <              00819000
                                             :BV001-PV-WORK-DATE)       00819100
              WITH UR                                                   00819200
           END-EXEC                                                     00819300
                                                                        00819400
           EVALUATE TRUE                                                00819500
              WHEN SQLCODE = 0                                          00819600
RYAN               SET BV001-PAST-DBV-REL-FOUND TO TRUE                 00819700
              WHEN SQLCODE = +100                                       00819800
                   PERFORM BV001-LOOKUP-FUT-RLTNSHP                     00819900
              WHEN OTHER                                                00820000
                   SET BV001-PV-SQL-ERROR TO TRUE                       00820100
                   SET BV001-WORK-ERROR-BV-DPT TO TRUE                  00820200
                   PERFORM BV001-SET-ERROR-MSG2                         00820300
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 00820400
                                   BV001-PV-WORK-SQL-CODE               00820500
                   STRING 'FAIL PAST RELATIONSHIP LOOKUP '              00820600
                                                 DELIMITED BY SIZE      00820700
                          'IN BV001-LOOKUP-PAST-RLTNSHP'                00820800
                                                 DELIMITED BY SIZE      00820900
                     INTO BV001-PV-RETURN-MSG-1                         00821000
           END-EVALUATE.                                                00821100
      ******************************************************************00821200
      * GET RELATIONSHIP INFORMATION FOR A DEPT/BRAND/VENDOR            00821300
      ******************************************************************00821400
       BV001-LOOKUP-RLTNSHP.                                            00821500
           EXEC SQL                                                     00821600
              SELECT ML_LO_MDSE_LVL_ID                                  00821700
                    ,DEPT_NBR                                           00821800
                    ,LBL_SEQ_NBR                                        00821900
                    ,ENT_ID                                             00822000
               INTO :BV001-PV-RLTNSHP-ID-OUT                            00822100
                   ,:BV001-PV-DEPT-OUT                                  00822200
                   ,:BV001-PV-BRAND-OUT                                 00822300
                   ,:BV001-PV-ENT-ID-OUT                                00822400
               FROM MLV_BRND_VND                                        00822500
              WHERE ENT_ID        = :BV001-PV-WORK-ENT-ID               00822600
                AND LBL_SEQ_NBR   = :BV001-PV-WORK-BRAND                00822700
                AND EFF_BEG_DTE  <= :BV001-PV-WORK-DATE                 00822800
                AND (EFF_END_DTE IS NULL OR                             00822900
                     EFF_END_DTE >= :BV001-PV-WORK-DATE)                00823000
                AND DEPT_NBR      = :BV001-PV-WORK-DEPT                 00823100
              WITH UR                                                   00823200
           END-EXEC                                                     00823300
                                                                        00823400
           EVALUATE TRUE                                                00823500
              WHEN SQLCODE = 0                                          00823600
                   CONTINUE                                             00823700
              WHEN SQLCODE = +100                                       00823800
                   SET BV001-NO-RLTNSHP-FOR-DTE TO TRUE                 00823900
                   STRING 'RELATIONSHIP WAS NOT FOUND '                 00824000
                                                DELIMITED BY SIZE       00824100
                     INTO BV001-PV-RETURN-MSG-1                         00824200
                   PERFORM BV001-LOOKUP-PAST-RLTNSHP                    00824300
              WHEN OTHER                                                00824400
                   SET BV001-PV-SQL-ERROR TO TRUE                       00824500
                   SET BV001-WORK-ERROR-BV-DPT TO TRUE                  00824600
                   PERFORM BV001-SET-ERROR-MSG2                         00824700
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 00824800
                                   BV001-PV-WORK-SQL-CODE               00824900
                   STRING 'FAIL RELATIONSHIP LOOKUP' DELIMITED BY SIZE  00825000
                          ' IN BV001-LOOKUP-RLTNSHP' DELIMITED BY SIZE  00825100
                     INTO BV001-PV-RETURN-MSG-1                         00825200
           END-EVALUATE.                                                00825300
      ******************************************************************00825400
      * PROCESS BRAND VENDOR LOOKUP FOR AN RELATIONSHIP ID              00825500
      ******************************************************************00825600
       BV001-LOOKUP-REL-ID.                                             00825700
            IF BV001-PV-RLTNSHP-ID-IN = SPACES                          00825800
               OR BV001-PV-RLTNSHP-ID-IN NOT NUMERIC                    00825900
               OR BV001-PV-RLTNSHP-ID-IN = ZEROES                       00826000
               STRING 'BAD RELATIONSHIP ID ' DELIMITED BY SIZE          00826100
                      BV001-PV-RLTNSHP-ID-IN DELIMITED BY SIZE          00826200
                 INTO BV001-PV-RETURN-MSG-1                             00826300
               SET BV001-BAD-INPUT TO TRUE                              00826400
            ELSE                                                        00826500
               MOVE BV001-PV-RLTNSHP-ID-IN                              00826600
                   TO BV001-PV-WORK-REL-ID                              00826700
               EXEC SQL                                                 00826800
                  SELECT ML_LO_MDSE_LVL_ID                              00826900
                        ,DEPT_NBR                                       00827000
                        ,ENT_ID                                         00827100
                        ,LBL_SEQ_NBR                                    00827200
                   INTO :BV001-PV-RLTNSHP-ID-OUT                        00827300
                       ,:BV001-PV-DEPT-OUT                              00827400
                       ,:BV001-PV-ENT-ID-OUT                            00827500
                       ,:BV001-PV-BRAND-OUT                             00827600
                   FROM MLV_BRND_VND                                    00827700
                  WHERE ML_LO_MDSE_LVL_ID = :BV001-PV-WORK-REL-ID       00827800
                  WITH UR                                               00827900
               END-EXEC                                                 00828000
                                                                        00828100
               EVALUATE TRUE                                            00828200
                  WHEN SQLCODE = 0                                      00828300
                       CONTINUE                                         00828400
                  WHEN SQLCODE = +100                                   00828500
                       SET BV001-REL-ID-NOT-FOUND TO TRUE               00828600
                       STRING 'RELATIONSHIP WAS NOT FOUND '             00828700
                                                    DELIMITED BY SIZE   00828800
                         INTO BV001-PV-RETURN-MSG-1                     00828900
                  WHEN OTHER                                            00829000
                       SET BV001-PV-SQL-ERROR TO TRUE                   00829100
                       MOVE SQLCODE TO BV001-PV-SQLCODE-OUT             00829200
                                       BV001-PV-WORK-SQL-CODE           00829300
                       STRING 'FAIL REL ID LOOKUP '                     00829400
                                                     DELIMITED BY SIZE  00829500
                              ' IN BV001-LOOKUP-REL ID'                 00829600
                                                     DELIMITED BY SIZE  00829700
                         INTO BV001-PV-RETURN-MSG-1                     00829800
                       STRING 'REL ID '              DELIMITED BY SIZE  00829900
                              BV001-PV-RLTNSHP-ID-IN DELIMITED BY SIZE  00830000
                              ' SQLCODE = '          DELIMITED BY SIZE  00830100
                              BV001-PV-WORK-SQL-CODE DELIMITED BY SIZE  00830200
                         INTO BV001-PV-RETURN-MSG-2                     00830300
              END-EVALUATE                                              00830400
            END-IF.                                                     00830500
      ******************************************************************00830600
      * PROCESS BRAND VENDOR LOOKUP FOR AN RELATIONSHIP ID              00830700
      ******************************************************************00830800
       BV001-SEQ-INSERT.                                                00830900
            EXEC SQL                                                    00831000
      *         INSERT INTO IMT_BRND_VND_ID                             00831100
      *         VALUES (IDENTITY_VAL_LOCAL())                           00831200
                INSERT INTO IMT_BRND_VND_ID                             00831300
                       ( ROW_SEQ_NBR )                                  00831400
                VALUES ( 0 )                                            00831500
            END-EXEC.                                                   00831600
                                                                        00831700
            EVALUATE TRUE                                               00831800
              WHEN SQLCODE = 0                                          00831900
                   CONTINUE                                             00832000
              WHEN OTHER                                                00832100
                  SET BV001-PV-SQL-ERROR TO TRUE                        00832200
                  MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                  00832300
                                  BV001-PV-WORK-SQL-CODE                00832400
                  STRING 'FAIL SEQUENCE INSERT LOOKUP '                 00832500
                                                DELIMITED BY SIZE       00832600
                          'IN BV001-SEQ-INSERT'                         00832700
                                                DELIMITED BY SIZE       00832800
                    INTO BV001-PV-RETURN-MSG-1                          00832900
                  STRING ' SQLCODE = '          DELIMITED BY SIZE       00833000
                         BV001-PV-WORK-SQL-CODE DELIMITED BY SIZE       00834000
                    INTO BV001-PV-RETURN-MSG-2                          00835000
            END-EVALUATE.                                               00836000
      ******************************************************************00837000
      * GET UNKNOWN BRAND FOR A DEPARTMENT                              00838000
      ******************************************************************00839000
       BV001-LOOKUP-UNKNOWN.                                            00840000
RONNA2*    IF BV001-PV-NO-REL-FOUND OR BV001-PV-ZERO-BRAND              00850000
RONNA2     IF BV001-PV-NO-REL-FOUND                                     00860000
              MOVE BV001-PV-WORK-ENT-ID TO BV001-PV-ENT-ID-OUT          00870000
           ELSE                                                         00880000
              PERFORM BV001-CHECK-DEPT                                  00890000
RONNA2*       IF BV001-PV-NO-ERRORS-FOUND OR BV001-ITEM-NOT-FOUND       00900000
RONNA2        IF BV001-PV-NO-ERRORS-FOUND                               00910000
                 IF BV001-PV-ENT-ID-IN NOT EQUAL SPACES AND             00920000
                    BV001-PV-ENT-ID-IN NOT EQUAL ZERO                   00930000
                    PERFORM BV001-CHECK-ENT-ID                          00940000
                    MOVE BV001-PV-WORK-ENT-ID TO BV001-PV-ENT-ID-OUT    00950000
                 ELSE                                                   00960000
                    SET BV001-PV-ENT-NOT-FOUND TO TRUE                  00970000
                 END-IF                                                 00980000
              END-IF                                                    00990000
           END-IF                                                       01000000
                                                                        01010000
           IF NOT BV001-BAD-INPUT                                       01020000
              SET BV001-PV-BRAND-NOT-FOUND TO TRUE                      01030000
              EXEC SQL                                                  01040000
                 SELECT UNKN_BRND_VND_ID                                01050000
                        ,DEPT_NBR                                       01060000
                  INTO :BV001-PV-RLTNSHP-ID-OUT                         01070000
                      ,:BV001-PV-DEPT-OUT                               01080000
                  FROM MLT_UNKN_BRND_VND                                01090000
                 WHERE ENT_ID         = :BV001-PV-ENT-ID-OUT            01100000
                   AND LBL_SEQ_NBR    = :BV001-PV-BRAND-OUT             01110000
                   AND DEPT_NBR       = :BV001-PV-WORK-DEPT             01120000
                 WITH UR                                                01130000
              END-EXEC                                                  01140000
                                                                        01150000
              EVALUATE TRUE                                             01160000
                WHEN SQLCODE = 0                                        01170000
                     CONTINUE                                           01180000
                WHEN SQLCODE = +100                                     01190000
                       IF BV001-PV-ENT-ID-OUT NOT EQUAL ZERO AND        01200000
                          NOT BV001-PV-ENT-NOT-FOUND                    01210000
                          DISPLAY 'INSERT KNOWN/UNKNOWN'                01220000
                          PERFORM BV001-INSERT-KNWN-UNK                 01230000
                       ELSE                                             01240000
                          DISPLAY 'COULD NOT FIND '                     01250000
                                  ' DEPT: '       BV001-PV-WORK-DEPT    01260000
                                  ' BRAND: '      BV001-PV-BRAND-OUT    01270000
                                  ' ENT ID: '     BV001-PV-ENT-ID-OUT   01280000
                          DISPLAY 'INSERT UNKNOWN/UNKNOWN'              01290000
                          PERFORM BV001-INSERT-UNK-UNK                  01300000
                       END-IF                                           01310000
                WHEN OTHER                                              01320000
                     SET BV001-PV-SQL-ERROR TO TRUE                     01330000
                     MOVE SQLCODE TO BV001-PV-SQLCODE-OUT               01340000
                                     BV001-PV-WORK-SQL-CODE             01350000
                     STRING 'FAIL UNKNOWN/UNKNOWN INSERT '              01360000
                                                   DELIMITED BY SIZE    01370000
                             'IN BV001-LOOKUP-UNKNOWN'                  01380000
                                                   DELIMITED BY SIZE    01390000
                       INTO BV001-PV-RETURN-MSG-1                       01400000
                     STRING 'VENDOR = UNKNOWN ' DELIMITED BY SIZE       01410000
                            ' BRAND = UNKNOWN ' DELIMITED BY SIZE       01420000
                            ' SQLCODE = '         DELIMITED BY SIZE     01430000
                            BV001-PV-WORK-SQL-CODE DELIMITED BY SIZE    01440000
                       INTO BV001-PV-RETURN-MSG-2                       01450000
              END-EVALUATE                                              01460000
           END-IF.                                                      01470000
      ******************************************************************01480000
      * INSERT UNKNOWN/UNKNOWN FOR A DEPARTMENT                         01490000
      ******************************************************************01500000
       BV001-INSERT-UNK-UNK.                                            01510000
           SET BV001-PV-ENT-NOT-FOUND   TO TRUE.                        01520000
           SET BV001-PV-BRAND-NOT-FOUND TO TRUE.                        01530000
           MOVE BV001-PV-WORK-DEPT      TO BV001-PV-DEPT-OUT.           01540000
                                                                        01550000
           PERFORM BV001-INSERT-UNKNOWN.                                01560000
           PERFORM BV001-LOOKUP-UNKNOWN.                                01570000
                                                                        01580000
      ******************************************************************01590000
      * INSERT A ROW ONTO THE UNKNOWN TABLE                             01600000
      ******************************************************************01610000
       BV001-INSERT-UNKNOWN.                                            01620000
           PERFORM BV001-SEQ-INSERT.                                    01630000
RONNA2*    IF BV001-PV-NO-ERRORS-FOUND OR BV001-PV-ITM-OR-REL-NOT-FOUND 01640000
           IF BV001-PV-NO-ERRORS-FOUND OR BV001-PV-NO-REL-FOUND         01650000
                                                                        01660000
              EXEC SQL                                                  01670000
                 INSERT INTO MLT_UNKN_BRND_VND                          01680000
                        ( UNKN_BRND_VND_ID                              01690000
                         ,DEPT_NBR                                      01700000
                         ,LBL_SEQ_NBR                                   01710000
                         ,ENT_ID                                        01720000
                         ,CRE_TMST                                      01730000
                        )                                               01740000
                  VALUES( IDENTITY_VAL_LOCAL ()                         01750000
                        , :BV001-PV-WORK-DEPT                           01760000
                        , :BV001-PV-BRAND-OUT                           01770000
                        , :BV001-PV-ENT-ID-OUT                          01780000
                        , CURRENT TIMESTAMP)                            01790000
              END-EXEC                                                  01800000
                                                                        01810000
              EVALUATE TRUE                                             01820000
                 WHEN SQLCODE = 0                                       01830000
                      SET BV001-UNKNOWN-INS TO TRUE                     01840000
                      DISPLAY 'INSERTED: '                              01850000
                              ' DEPT: '    BV001-PV-WORK-DEPT           01860000
                              ' BRAND: '   BV001-PV-BRAND-OUT           01870000
                              ' ENT ID: '  BV001-PV-ENT-ID-OUT          01880000
                      PERFORM BV001-LOOKUP-UNKNOWN                      01890000
                 WHEN OTHER                                             01900000
                      DISPLAY 'UNKNOWN INSERT FAIL'                     01910000
                      SET BV001-PV-SQL-ERROR TO TRUE                    01920000
                      MOVE SQLCODE TO BV001-PV-SQLCODE-OUT              01930000
                                      BV001-PV-WORK-SQL-CODE            01940000
                      STRING 'FAIL UNKNOWN INSERT '                     01950000
                                                    DELIMITED BY SIZE   01960000
                             'IN BV001-INSERT-UNKNOWN'                  01970000
                                                    DELIMITED BY SIZE   01980000
                        INTO BV001-PV-RETURN-MSG-1                      01990000
                      STRING ' VENDOR = '        DELIMITED BY SIZE      02000000
                             BV001-PV-ENT-ID-IN  DELIMITED BY SIZE      02010000
                             ' DEPT = '          DELIMITED BY SIZE      02020000
                             BV001-PV-DEPT-IN    DELIMITED BY SIZE      02030000
                             ' BRAND = UNKNOWN ' DELIMITED BY SIZE      02040000
                             ' SQLCODE = '       DELIMITED BY SIZE      02050000
                             BV001-PV-WORK-SQL-CODE DELIMITED BY SIZE   02060000
                        INTO BV001-PV-RETURN-MSG-2                      02070000
              END-EVALUATE                                              02080000
           END-IF.                                                      02090000
      ******************************************************************02100000
      * INSERT KNOWN/UNKNOWN FOR A VENDOR/DEPARTMENT                    02110000
      ******************************************************************02120000
       BV001-INSERT-KNWN-UNK.                                           02130000
            SET BV001-PV-BRAND-NOT-FOUND TO TRUE.                       02140000
            MOVE BV001-PV-WORK-DEPT      TO BV001-PV-DEPT-OUT.          02150000
                                                                        02160000
           PERFORM BV001-INSERT-UNKNOWN.                                02170000
           PERFORM BV001-LOOKUP-UNKNOWN.                                02180000
      ******************************************************************02190000
      * PROCESS BRAND VENDOR LOOKUP FOR A VENDOR.                       02200000
      ******************************************************************02210000
       BV001-LOOKUP-VND.                                                02220000
            PERFORM BV001-CHECK-ENT-ID.                                 02230000
            IF BV001-PV-NO-ERRORS-FOUND                                 02240000
               PERFORM BV001-OPEN-VNDR-CSR                              02250000
               IF BV001-PV-NO-ERRORS-FOUND                              02260000
                  PERFORM BV001-FETCH-VNDR-CSR                          02270000
                  PERFORM BV001-LOAD-RLTNSHP-TABLE                      02280000
                     UNTIL BV001-PV-NO-MORE-RECORDS OR                  02290000
                           BV001-PV-ERROR-FOUND                         02300000
                  IF BV001-PV-NO-ERRORS-FOUND                           02310000
                     PERFORM BV001-CLOSE-VNDR-CSR                       02320000
                  END-IF                                                02330000
               END-IF                                                   02340000
            END-IF.                                                     02350000
      ******************************************************************02360000
      * PROCESS BRAND VENDOR LOOKUP FOR A VENDOR DEPARTMENT             02370000
      ******************************************************************02380000
       BV001-LOOKUP-VND-DPT.                                            02390000
            SET BV001-PV-VNDR-DEPT-CSR-ORIG TO TRUE.                    02400000
            PERFORM BV001-CHECK-ENT-ID.                                 02410000
            IF BV001-PV-NO-ERRORS-FOUND                                 02420000
               PERFORM BV001-CHECK-DEPT                                 02430000
               IF BV001-PV-NO-ERRORS-FOUND                              02440000
                  PERFORM BV001-OPEN-VNDR-DPT-CSR                       02450000
                  IF BV001-PV-NO-ERRORS-FOUND                           02460000
                     PERFORM BV001-FETCH-VNDR-DPT-CSR                   02470000
                     PERFORM BV001-LOAD-RLTNSHP-TABLE                   02480000
                        UNTIL BV001-PV-NO-MORE-RECORDS OR               02490000
                              BV001-PV-ERROR-FOUND                      02500000
                     IF BV001-PV-NO-ERRORS-FOUND                        02510000
                        PERFORM BV001-CLOSE-VNDR-DPT-CSR                02520000
                     END-IF                                             02530000
                     IF BV001-PV-RT-SUB-OUT EQUAL ZERO AND              02540000
                        BV001-VND-LOOK-PAST-FUT AND                     02550000
                        BV001-PV-NO-ERRORS-FOUND                        02560000
                        IF BV001-PV-PAST-MONTHS > 0                     02570000
                           PERFORM BV001-VD-PAST-FUTURE                 02580000
                        ELSE                                            02590000
                           SET BV001-BAD-INPUT TO TRUE                  02600000
                           STRING 'TO GET PAST/FUT FOR VENDOR/DEPT '    02610000
                           DELIMITED BY SIZE                            02620000
                                'THE NUMBERS OF MONTHS INTO THE'        02630000
                           DELIMITED BY SIZE                            02640000
                                'PAST MUST BE POPULATED'                02650000
                           DELIMITED BY SIZE                            02660000
                           INTO BV001-PV-RETURN-MSG-1                   02670000
                        END-IF                                          02680000
                     END-IF                                             02690000
                     IF BV001-PV-RT-SUB-OUT EQUAL ZERO AND              02700000
                        BV001-USE-KNOWN-UNKNOWN AND                     02710000
                        BV001-PV-NO-ERRORS-FOUND                        02720000
                        PERFORM BV001-LOOKUP-UNKNOWN                    02730000
                     END-IF                                             02740000
                  END-IF                                                02750000
               END-IF                                                   02760000
            END-IF.                                                     02770000
      ******************************************************************02780000
      * GET PAST AND FUTURE VENDOR DEPARTMENT INFORMATION.              02790000
      ******************************************************************02800000
       BV001-VD-PAST-FUTURE.                                            02810000
           SET BV001-PV-VNDR-DEPT-CSR-PAST TO TRUE.                     02820000
           SET BV001-PV-MORE-RECORDS       TO TRUE.                     02830000
           PERFORM BV001-OPEN-VD-PAST-CSR.                              02840000
           IF BV001-PV-NO-ERRORS-FOUND                                  02850000
              PERFORM BV001-FETCH-VD-PAST-CSR                           02860000
              PERFORM BV001-LOAD-RLTNSHP-TABLE                          02870000
                 UNTIL BV001-PV-NO-MORE-RECORDS OR                      02880000
                       BV001-PV-ERROR-FOUND                             02890000
              IF BV001-PV-NO-ERRORS-FOUND                               02900000
                 PERFORM BV001-CLOSE-VD-PAST-CSR                        02910000
              END-IF                                                    02920000
              IF BV001-PV-RT-SUB-OUT EQUAL ZERO                         02930000
                 SET BV001-PV-VNDR-DEPT-CSR-FUT TO TRUE                 02940000
                 SET BV001-PV-MORE-RECORDS      TO TRUE                 02950000
                 PERFORM BV001-OPEN-VD-FUT-CSR                          02960000
                 IF BV001-PV-NO-ERRORS-FOUND                            02970000
                    PERFORM BV001-FETCH-VD-FUT-CSR                      02980000
                    PERFORM BV001-LOAD-RLTNSHP-TABLE                    02990000
                       UNTIL BV001-PV-NO-MORE-RECORDS OR                03000000
                             BV001-PV-ERROR-FOUND                       03010000
                    IF BV001-PV-NO-ERRORS-FOUND                         03020000
                       PERFORM BV001-CLOSE-VD-FUT-CSR                   03030000
                    END-IF                                              03040000
                    IF BV001-PV-RT-SUB-OUT EQUAL ZERO                   03050000
                       SET BV001-PV-BRAND-NOT-FOUND TO TRUE             03060000
                       PERFORM BV001-LOOKUP-UNKNOWN                     03070000
                    END-IF                                              03080000
                 END-IF                                                 03090000
              END-IF                                                    03100000
           END-IF.                                                      03110000
      ******************************************************************03120000
      * OPEN THE BRAND CURSOR                                           03130000
      ******************************************************************03140000
       BV001-OPEN-BRND-CSR.                                             03150000
           EXEC SQL                                                     03160000
              OPEN BV_BRND_CSR                                          03170000
           END-EXEC                                                     03180000
                                                                        03190000
           EVALUATE TRUE                                                03200000
              WHEN SQLCODE = 0                                          03210000
                   CONTINUE                                             03220000
              WHEN OTHER                                                03230000
                   SET BV001-PV-SQL-ERROR TO TRUE                       03240000
                   SET BV001-WORK-ERROR-JUST-BRND TO TRUE               03250000
                   PERFORM BV001-SET-ERROR-MSG2                         03260000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 03270000
                                   BV001-PV-WORK-SQL-CODE               03280000
                   STRING 'FAIL BRAND CURSOR OPEN '                     03290000
                                                 DELIMITED BY SIZE      03300000
                          'IN BV001-OPEN-BRND-CSR'                      03310000
                                                 DELIMITED BY SIZE      03320000
                     INTO BV001-PV-RETURN-MSG-1                         03330000
           END-EVALUATE.                                                03340000
      ******************************************************************03350000
      * OPEN THE BRAND DEPT CURSOR                                      03360000
      ******************************************************************03370000
       BV001-OPEN-BRND-DPT-CSR.                                         03380000
           EXEC SQL                                                     03390000
              OPEN BV_BRND_DEPT_CSR                                     03400000
           END-EXEC                                                     03410000
                                                                        03420000
           EVALUATE TRUE                                                03430000
              WHEN SQLCODE = 0                                          03440000
                   CONTINUE                                             03450000
              WHEN OTHER                                                03460000
                   SET BV001-PV-SQL-ERROR TO TRUE                       03470000
                   SET BV001-WORK-ERROR-BRND-DPT TO TRUE                03480000
                   PERFORM BV001-SET-ERROR-MSG2                         03490000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 03500000
                                   BV001-PV-WORK-SQL-CODE               03510000
                   STRING 'FAIL BRAND DEPT CURSOR OPEN '                03520000
                                                 DELIMITED BY SIZE      03530000
                          'IN BV001-OPEN-BRND-DPT-CSR'                  03540000
                                                 DELIMITED BY SIZE      03550000
                     INTO BV001-PV-RETURN-MSG-1                         03560000
           END-EVALUATE.                                                03570000
      ******************************************************************03580000
      * OPEN THE VENDOR CURSOR                                          03590000
      ******************************************************************03600000
       BV001-OPEN-VNDR-CSR.                                             03610000
           EXEC SQL                                                     03620000
              OPEN BV_VNDR_CSR                                          03630000
           END-EXEC                                                     03640000
                                                                        03650000
           EVALUATE TRUE                                                03660000
              WHEN SQLCODE = 0                                          03670000
                   CONTINUE                                             03680000
              WHEN OTHER                                                03690000
                   SET BV001-PV-SQL-ERROR TO TRUE                       03700000
                   SET BV001-WORK-ERROR-JUST-VNDR TO TRUE               03710000
                   PERFORM BV001-SET-ERROR-MSG2                         03720000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 03730000
                                   BV001-PV-WORK-SQL-CODE               03740000
                   STRING 'FAIL VENDOR CURSOR OPEN '                    03750000
                                                 DELIMITED BY SIZE      03760000
                          'IN BV001-OPEN-VNDR-CSR'                      03770000
                                                 DELIMITED BY SIZE      03780000
                     INTO BV001-PV-RETURN-MSG-1                         03790000
           END-EVALUATE.                                                03800000
      ******************************************************************03810000
      * OPEN THE VENDOR DEPT CURSOR                                     03820000
      ******************************************************************03830000
       BV001-OPEN-VNDR-DPT-CSR.                                         03840000
           EXEC SQL                                                     03850000
              OPEN BV_VNDR_DEPT_CSR                                     03860000
           END-EXEC                                                     03870000
                                                                        03880000
           EVALUATE TRUE                                                03890000
              WHEN SQLCODE = 0                                          03900000
                   CONTINUE                                             03910000
              WHEN OTHER                                                03920000
                   SET BV001-PV-SQL-ERROR TO TRUE                       03930000
                   SET BV001-WORK-ERROR-VNDR-DPT TO TRUE                03940000
                   PERFORM BV001-SET-ERROR-MSG2                         03950000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 03960000
                                   BV001-PV-WORK-SQL-CODE               03970000
                   STRING 'FAIL VENDOR DEPT CURSOR OPEN '               03980000
                                                 DELIMITED BY SIZE      03990000
                          'IN BV001-OPEN-VNDR-DPT-CSR'                  04000000
                                                 DELIMITED BY SIZE      04010000
                     INTO BV001-PV-RETURN-MSG-1                         04020000
           END-EVALUATE.                                                04030000
      ******************************************************************04040000
      * OPEN THE VENDOR DEPT PAST CURSOR                                04050000
      ******************************************************************04060000
       BV001-OPEN-VD-PAST-CSR.                                          04070000
           EXEC SQL                                                     04080000
              OPEN BV_VND_DPT_PST_CSR                                   04090000
           END-EXEC                                                     04100000
                                                                        04110000
           EVALUATE TRUE                                                04120000
              WHEN SQLCODE = 0                                          04130000
                   CONTINUE                                             04140000
              WHEN OTHER                                                04150000
                   SET BV001-PV-SQL-ERROR TO TRUE                       04160000
                   SET BV001-WORK-ERROR-VNDR-DPT TO TRUE                04170000
                   PERFORM BV001-SET-ERROR-MSG2                         04180000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 04190000
                                   BV001-PV-WORK-SQL-CODE               04200000
                   STRING 'FAIL VENDOR DEPT PAST CURSOR OPEN '          04210000
                                                 DELIMITED BY SIZE      04220000
                          'IN BV001-OPEN-VD-PAST-CSR'                   04230000
                                                 DELIMITED BY SIZE      04240000
                     INTO BV001-PV-RETURN-MSG-1                         04250000
           END-EVALUATE.                                                04260000
      ******************************************************************04270000
      * OPEN THE VENDOR DEPT FUTURE CURSOR                              04280000
      ******************************************************************04290000
       BV001-OPEN-VD-FUT-CSR.                                           04300000
           EXEC SQL                                                     04310000
              OPEN BV_VND_DPT_FUT_CSR                                   04320000
           END-EXEC                                                     04330000
                                                                        04340000
           EVALUATE TRUE                                                04350000
              WHEN SQLCODE = 0                                          04360000
                   CONTINUE                                             04370000
              WHEN OTHER                                                04380000
                   SET BV001-PV-SQL-ERROR TO TRUE                       04390000
                   SET BV001-WORK-ERROR-VNDR-DPT TO TRUE                04400000
                   PERFORM BV001-SET-ERROR-MSG2                         04410000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 04420000
                                   BV001-PV-WORK-SQL-CODE               04430000
                   STRING 'FAIL VENDOR DEPT FUTURE CURSOR OPEN '        04440000
                                                 DELIMITED BY SIZE      04450000
                          'IN BV001-OPEN-VD-FUT-CSR'                    04460000
                                                 DELIMITED BY SIZE      04470000
                     INTO BV001-PV-RETURN-MSG-1                         04480000
           END-EVALUATE.                                                04490000
      ******************************************************************04500000
      * FETCH A RECORD FOR THE BRAND CURSOR                             04510000
      ******************************************************************04520000
       BV001-FETCH-BRND-CSR.                                            04530000
           EXEC SQL                                                     04540000
              FETCH BV_BRND_CSR                                         04550000
               INTO :BV001-PV-WORK-RLTNSHP-ID                           04560000
                   ,:BV001-PV-WORK-DEPT                                 04570000
                   ,:BV001-PV-WORK-ENT-ID                               04580000
           END-EXEC                                                     04590000
                                                                        04600000
           EVALUATE TRUE                                                04610000
              WHEN SQLCODE = 0                                          04620000
                   CONTINUE                                             04630000
              WHEN SQLCODE = +100                                       04640000
                   SET BV001-PV-NO-MORE-RECORDS TO TRUE                 04650000
              WHEN OTHER                                                04660000
                   SET BV001-PV-SQL-ERROR TO TRUE                       04670000
                   SET BV001-WORK-ERROR-JUST-BRND TO TRUE               04680000
                   PERFORM BV001-SET-ERROR-MSG2                         04690000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 04700000
                                   BV001-PV-WORK-SQL-CODE               04710000
                   STRING 'FAIL BRAND CURSOR FETCH '                    04720000
                                                 DELIMITED BY SIZE      04730000
                          'IN BV001-FETCH-BRND-CSR'                     04740000
                                                 DELIMITED BY SIZE      04750000
                     INTO BV001-PV-RETURN-MSG-1                         04760000
           END-EVALUATE.                                                04770000
      ******************************************************************04780000
      * FETCH A RECORD FOR THE BRAND DEPARTMENT CURSOR                  04790000
      ******************************************************************04800000
       BV001-FETCH-BRND-DPT-CSR.                                        04810000
           EXEC SQL                                                     04820000
              FETCH BV_BRND_DEPT_CSR                                    04830000
               INTO :BV001-PV-WORK-RLTNSHP-ID                           04840000
                   ,:BV001-PV-WORK-ENT-ID                               04850000
           END-EXEC                                                     04860000
                                                                        04870000
           EVALUATE TRUE                                                04880000
              WHEN SQLCODE = 0                                          04890000
                   CONTINUE                                             04900000
              WHEN SQLCODE = +100                                       04910000
                   SET BV001-PV-NO-MORE-RECORDS TO TRUE                 04920000
              WHEN OTHER                                                04930000
                   SET BV001-PV-SQL-ERROR TO TRUE                       04940000
                   SET BV001-WORK-ERROR-BRND-DPT TO TRUE                04950000
                   PERFORM BV001-SET-ERROR-MSG2                         04960000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 04970000
                                   BV001-PV-WORK-SQL-CODE               04980000
                   STRING 'FAIL BRAND DEPT CURSOR FETCH '               04990000
                                                 DELIMITED BY SIZE      05000000
                          'IN BV001-FETCH-BRND-DPT-CSR'                 05010000
                                                 DELIMITED BY SIZE      05020000
                     INTO BV001-PV-RETURN-MSG-1                         05030000
           END-EVALUATE.                                                05040000
      ******************************************************************05050000
      * FETCH A RECORD FOR THE VENDOR CURSOR                            05060000
      ******************************************************************05070000
       BV001-FETCH-VNDR-CSR.                                            05080000
           EXEC SQL                                                     05090000
              FETCH BV_VNDR_CSR                                         05100000
               INTO :BV001-PV-WORK-RLTNSHP-ID                           05110000
                   ,:BV001-PV-WORK-DEPT                                 05120000
                   ,:BV001-PV-WORK-BRAND                                05130000
           END-EXEC                                                     05140000
                                                                        05150000
           EVALUATE TRUE                                                05160000
              WHEN SQLCODE = 0                                          05170000
                   CONTINUE                                             05180000
              WHEN SQLCODE = +100                                       05190000
                   SET BV001-PV-NO-MORE-RECORDS TO TRUE                 05200000
              WHEN OTHER                                                05210000
                   SET BV001-PV-SQL-ERROR TO TRUE                       05220000
                   SET BV001-WORK-ERROR-JUST-VNDR TO TRUE               05230000
                   PERFORM BV001-SET-ERROR-MSG2                         05240000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 05250000
                                   BV001-PV-WORK-SQL-CODE               05260000
                   STRING 'FAIL VENDOR CURSOR FETCH' DELIMITED BY SIZE  05270000
                          ' IN BV001-FETCH-VNDR-CSR' DELIMITED BY SIZE  05280000
                     INTO BV001-PV-RETURN-MSG-1                         05290000
           END-EVALUATE.                                                05300000
      ******************************************************************05310000
      * FETCH A RECORD FOR THE VENDOR DEPARTMENT CURSOR                 05320000
      ******************************************************************05330000
       BV001-FETCH-VNDR-DPT-CSR.                                        05340000
           EXEC SQL                                                     05350000
              FETCH BV_VNDR_DEPT_CSR                                    05360000
               INTO :BV001-PV-WORK-RLTNSHP-ID                           05370000
                   ,:BV001-PV-WORK-BRAND                                05380000
           END-EXEC                                                     05390000
                                                                        05400000
           EVALUATE TRUE                                                05410000
              WHEN SQLCODE = 0                                          05420000
                   CONTINUE                                             05430000
              WHEN SQLCODE = +100                                       05440000
                   SET BV001-PV-NO-MORE-RECORDS TO TRUE                 05450000
              WHEN OTHER                                                05460000
                   SET BV001-PV-SQL-ERROR TO TRUE                       05470000
                   SET BV001-WORK-ERROR-VNDR-DPT TO TRUE                05480000
                   PERFORM BV001-SET-ERROR-MSG2                         05490000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 05500000
                                   BV001-PV-WORK-SQL-CODE               05510000
                   STRING 'FAIL VENDOR DEPT CURSOR FETCH '              05520000
                                                 DELIMITED BY SIZE      05530000
                          'IN BV001-FETCH-VNDR-DPT-CSR'                 05540000
                                                 DELIMITED BY SIZE      05550000
                     INTO BV001-PV-RETURN-MSG-1                         05560000
           END-EVALUATE.                                                05570000
      ******************************************************************05580000
      * FETCH A RECORD FOR THE VENDOR DEPARTMENT PAST CURSOR            05590000
      ******************************************************************05600000
       BV001-FETCH-VD-PAST-CSR.                                         05610000
           EXEC SQL                                                     05620000
              FETCH BV_VND_DPT_PST_CSR                                  05630000
               INTO :BV001-PV-WORK-RLTNSHP-ID                           05640000
                   ,:BV001-PV-WORK-BRAND                                05650000
           END-EXEC                                                     05660000
                                                                        05670000
           EVALUATE TRUE                                                05680000
              WHEN SQLCODE = 0                                          05690000
                   CONTINUE                                             05700000
              WHEN SQLCODE = +100                                       05710000
                   SET BV001-PV-NO-MORE-RECORDS TO TRUE                 05720000
              WHEN OTHER                                                05730000
                   SET BV001-PV-SQL-ERROR TO TRUE                       05740000
                   SET BV001-WORK-ERROR-VNDR-DPT TO TRUE                05750000
                   PERFORM BV001-SET-ERROR-MSG2                         05760000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 05770000
                                   BV001-PV-WORK-SQL-CODE               05780000
                   STRING 'FAIL VENDOR DEPT PAST CURSOR FETCH '         05790000
                                                 DELIMITED BY SIZE      05800000
                          'IN BV001-FETCH-VNDR-DPT-CSR'                 05810000
                                                 DELIMITED BY SIZE      05820000
                     INTO BV001-PV-RETURN-MSG-1                         05830000
           END-EVALUATE.                                                05840000
      ******************************************************************05850000
      * FETCH A RECORD FOR THE VENDOR DEPARTMENT FUTURE CURSOR          05860000
      ******************************************************************05870000
       BV001-FETCH-VD-FUT-CSR.                                          05880000
           EXEC SQL                                                     05890000
              FETCH BV_VND_DPT_FUT_CSR                                  05900000
               INTO :BV001-PV-WORK-RLTNSHP-ID                           05910000
                   ,:BV001-PV-WORK-BRAND                                05920000
           END-EXEC                                                     05930000
                                                                        05940000
           EVALUATE TRUE                                                05950000
              WHEN SQLCODE = 0                                          05960000
                   CONTINUE                                             05970000
              WHEN SQLCODE = +100                                       05980000
                   SET BV001-PV-NO-MORE-RECORDS TO TRUE                 05990000
              WHEN OTHER                                                06000000
                   SET BV001-PV-SQL-ERROR TO TRUE                       06010000
                   SET BV001-WORK-ERROR-VNDR-DPT TO TRUE                06020000
                   PERFORM BV001-SET-ERROR-MSG2                         06030000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 06040000
                                   BV001-PV-WORK-SQL-CODE               06050000
                   STRING 'FAIL VENDOR DEPT FUTURE CURSOR FETCH '       06060000
                                                 DELIMITED BY SIZE      06070000
                          'IN BV001-FETCH-VNDR-DPT-CSR'                 06080000
                                                 DELIMITED BY SIZE      06090000
                     INTO BV001-PV-RETURN-MSG-1                         06100000
           END-EVALUATE.                                                06110000
      ******************************************************************06120000
      * CLOSE THE BRAND CURSOR                                          06130000
      ******************************************************************06140000
       BV001-CLOSE-BRND-CSR.                                            06150000
           EXEC SQL                                                     06160000
              CLOSE BV_BRND_CSR                                         06170000
           END-EXEC                                                     06180000
                                                                        06190000
           EVALUATE TRUE                                                06200000
              WHEN SQLCODE = 0                                          06210000
                   CONTINUE                                             06220000
              WHEN OTHER                                                06230000
                   SET BV001-PV-SQL-ERROR TO TRUE                       06240000
                   SET BV001-WORK-ERROR-JUST-BRND TO TRUE               06250000
                   PERFORM BV001-SET-ERROR-MSG2                         06260000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 06270000
                                   BV001-PV-WORK-SQL-CODE               06280000
                   STRING 'FAIL BRAND CURSOR CLOSE '                    06290000
                                                 DELIMITED BY SIZE      06300000
                          'IN BV001-CLOSE-BRND-CSR'                     06310000
                                                 DELIMITED BY SIZE      06320000
                     INTO BV001-PV-RETURN-MSG-1                         06330000
           END-EVALUATE.                                                06340000
      ******************************************************************06350000
      * CLOSE THE BRAND DEPT CURSOR                                     06360000
      ******************************************************************06370000
       BV001-CLOSE-BRND-DPT-CSR.                                        06380000
           EXEC SQL                                                     06390000
              CLOSE BV_BRND_DEPT_CSR                                    06400000
           END-EXEC                                                     06410000
                                                                        06420000
           EVALUATE TRUE                                                06430000
              WHEN SQLCODE = 0                                          06440000
                   CONTINUE                                             06450000
              WHEN OTHER                                                06460000
                   SET BV001-PV-SQL-ERROR TO TRUE                       06470000
                   SET BV001-WORK-ERROR-BRND-DPT TO TRUE                06480000
                   PERFORM BV001-SET-ERROR-MSG2                         06490000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 06500000
                                   BV001-PV-WORK-SQL-CODE               06510000
                   STRING 'FAIL BRAND DEPT CURSOR CLOSE '               06520000
                                                 DELIMITED BY SIZE      06530000
                          'IN BV001-CLOSE-BRND-DPT-CSR'                 06540000
                                                 DELIMITED BY SIZE      06550000
                     INTO BV001-PV-RETURN-MSG-1                         06560000
           END-EVALUATE.                                                06570000
      ******************************************************************06580000
      * CLOSE THE VENDOR CURSOR                                         06590000
      ******************************************************************06600000
       BV001-CLOSE-VNDR-CSR.                                            06610000
           EXEC SQL                                                     06620000
              CLOSE BV_VNDR_CSR                                         06630000
           END-EXEC                                                     06640000
                                                                        06650000
           EVALUATE TRUE                                                06660000
              WHEN SQLCODE = 0                                          06670000
                   CONTINUE                                             06680000
              WHEN OTHER                                                06690000
                   SET BV001-PV-SQL-ERROR TO TRUE                       06700000
                   SET BV001-WORK-ERROR-JUST-VNDR TO TRUE               06710000
                   PERFORM BV001-SET-ERROR-MSG2                         06720000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 06730000
                                   BV001-PV-WORK-SQL-CODE               06740000
                   STRING 'FAIL VENDOR CURSOR CLOSE' DELIMITED BY SIZE  06750000
                          ' IN BV001-CLOSE-VNDR-CSR' DELIMITED BY SIZE  06760000
                     INTO BV001-PV-RETURN-MSG-1                         06770000
           END-EVALUATE.                                                06780000
      ******************************************************************06790000
      * CLOSE THE VENDOR DEPT CURSOR                                    06800000
      ******************************************************************06810000
       BV001-CLOSE-VNDR-DPT-CSR.                                        06820000
           EXEC SQL                                                     06830000
              CLOSE BV_VNDR_DEPT_CSR                                    06840000
           END-EXEC                                                     06850000
                                                                        06860000
           EVALUATE TRUE                                                06870000
              WHEN SQLCODE = 0                                          06880000
                   CONTINUE                                             06890000
              WHEN OTHER                                                06900000
                   SET BV001-PV-SQL-ERROR TO TRUE                       06910000
                   SET BV001-WORK-ERROR-VNDR-DPT TO TRUE                06920000
                   PERFORM BV001-SET-ERROR-MSG2                         06930000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 06940000
                                   BV001-PV-WORK-SQL-CODE               06950000
                   STRING 'FAIL VENDOR DEPT CURSOR CLOSE '              06960000
                                                 DELIMITED BY SIZE      06970000
                          'IN BV001-CLOSE-VNDR-DPT-CSR'                 06980000
                                                 DELIMITED BY SIZE      06990000
                     INTO BV001-PV-RETURN-MSG-1                         07000000
           END-EVALUATE.                                                07010000
      ******************************************************************07020000
      * CLOSE THE VENDOR DEPT PAST CURSOR                               07030000
      ******************************************************************07040000
       BV001-CLOSE-VD-PAST-CSR.                                         07050000
           EXEC SQL                                                     07060000
              CLOSE BV_VND_DPT_PST_CSR                                  07070000
           END-EXEC                                                     07080000
                                                                        07090000
           EVALUATE TRUE                                                07100000
              WHEN SQLCODE = 0                                          07110000
                   CONTINUE                                             07120000
              WHEN OTHER                                                07130000
                   SET BV001-PV-SQL-ERROR TO TRUE                       07140000
                   SET BV001-WORK-ERROR-VNDR-DPT TO TRUE                07150000
                   PERFORM BV001-SET-ERROR-MSG2                         07160000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 07170000
                                   BV001-PV-WORK-SQL-CODE               07180000
                   STRING 'FAIL VENDOR DEPT PAST CURSOR CLOSE '         07190000
                                                 DELIMITED BY SIZE      07200000
                          'IN BV001-CLOSE-VD-PAST-CSR'                  07210000
                                                 DELIMITED BY SIZE      07220000
                     INTO BV001-PV-RETURN-MSG-1                         07230000
           END-EVALUATE.                                                07240000
      ******************************************************************07250000
      * CLOSE THE VENDOR DEPT FUTURE CURSOR                             07260000
      ******************************************************************07270000
       BV001-CLOSE-VD-FUT-CSR.                                          07280000
           EXEC SQL                                                     07290000
              CLOSE BV_VND_DPT_FUT_CSR                                  07300000
           END-EXEC                                                     07310000
                                                                        07320000
           EVALUATE TRUE                                                07330000
              WHEN SQLCODE = 0                                          07340000
                   CONTINUE                                             07350000
              WHEN OTHER                                                07360000
                   SET BV001-PV-SQL-ERROR TO TRUE                       07370000
                   SET BV001-WORK-ERROR-VNDR-DPT TO TRUE                07380000
                   PERFORM BV001-SET-ERROR-MSG2                         07390000
                   MOVE SQLCODE TO BV001-PV-SQLCODE-OUT                 07400000
                                   BV001-PV-WORK-SQL-CODE               07410000
                   STRING 'FAIL VENDOR DEPT FUTURE CURSOR CLOSE '       07420000
                                                 DELIMITED BY SIZE      07430000
                          'IN BV001-CLOSE-VD-FUT-CSR'                   07440000
                                                 DELIMITED BY SIZE      07450000
                     INTO BV001-PV-RETURN-MSG-1                         07460000
           END-EVALUATE.                                                07470000
      ******************************************************************07480000
      *    LOAD THE OUTPUT TABLE WITH ALL RELATIONSHIPS OF THE SPECIFIED07490000
      *    TYPE.                                                        07500000
      ******************************************************************07510000
       BV001-LOAD-RLTNSHP-TABLE.                                        07520000
           ADD 1 TO BV001-PV-RT-SUB-OUT.                                07530000
                                                                        07540000
           MOVE BV001-PV-WORK-RLTNSHP-ID TO                             07550000
                    BV001-RT-RLTNSHP-ID-OUT(BV001-PV-RT-SUB-OUT).       07560000
                                                                        07570000
           MOVE BV001-PV-WORK-ENT-ID TO                                 07580000
                    BV001-RT-ENT-ID-OUT(BV001-PV-RT-SUB-OUT).           07590000
                                                                        07600000
           MOVE BV001-PV-WORK-BRAND TO                                  07610000
                    BV001-RT-BRND-OUT(BV001-PV-RT-SUB-OUT).             07620000
                                                                        07630000
           MOVE BV001-PV-WORK-DEPT        TO                            07640000
                    BV001-RT-DEPT-OUT(BV001-PV-RT-SUB-OUT).             07650000
                                                                        07660000
           EVALUATE TRUE                                                07670000
                 WHEN BV001-PV-VNDR-DEPT-LOOKUP                         07680000
                      EVALUATE TRUE                                     07690000
                         WHEN BV001-PV-VNDR-DEPT-CSR-ORIG               07700000
                              PERFORM BV001-FETCH-VNDR-DPT-CSR          07710000
                         WHEN BV001-PV-VNDR-DEPT-CSR-FUT                07720000
                              PERFORM BV001-FETCH-VD-FUT-CSR            07730000
                         WHEN BV001-PV-VNDR-DEPT-CSR-PAST               07740000
                              PERFORM BV001-FETCH-VD-PAST-CSR           07750000
                         WHEN OTHER                                     07760000
                              STRING 'INVALID VENDOR DEPT CURSOR TYPE ' 07770000
                              DELIMITED BY SIZE                         07780000
                                     'IN BV001-LOAD-RLTNSHP-TABLE'      07790000
                              DELIMITED BY SIZE                         07800000
                              INTO BV001-PV-RETURN-MSG-1                07810000
                              SET BV001-PV-OTHER-ERROR TO TRUE          07820000
                      END-EVALUATE                                      07830000
                 WHEN BV001-PV-VNDR-LOOKUP                              07840000
                      PERFORM BV001-FETCH-VNDR-CSR                      07850000
                 WHEN BV001-PV-BRND-DEPT-LOOKUP                         07860000
                      PERFORM BV001-FETCH-BRND-DPT-CSR                  07870000
                 WHEN BV001-PV-BRND-LOOKUP                              07880000
                      PERFORM BV001-FETCH-BRND-CSR                      07890000
           END-EVALUATE.                                                07900000
      ******************************************************************07910000
      * CHECK THAT THE ENT ID MEETS INITIAL REQUIREMENTS                07920000
      ******************************************************************07930000
       BV001-CHECK-ENT-ID.                                              07940000
            IF BV001-PV-ENT-ID-IN = SPACES                              07950000
                OR BV001-PV-ENT-ID-IN NOT NUMERIC                       07960000
                OR BV001-PV-ENT-ID-IN = ZEROES                          07970000
                STRING 'BAD VENDOR '      DELIMITED BY SIZE             07980000
                       BV001-PV-ENT-ID-IN DELIMITED BY SIZE             07990000
                  INTO BV001-PV-RETURN-MSG-1                            08000000
                SET BV001-BAD-INPUT TO TRUE                             08010000
            ELSE                                                        08020000
                MOVE BV001-PV-ENT-ID-IN-9 TO BV001-PV-WORK-ENT-ID       08030000
            END-IF.                                                     08040000
      ******************************************************************08050000
      * CHECK THAT THE DEPARTMENT MEETS INITIAL REQUIREMENTS            08060000
      ******************************************************************08070000
       BV001-CHECK-DEPT.                                                08080000
            IF BV001-PV-DEPT-IN = SPACES                                08090000
      *         OR BV001-PV-DEPT-IN NOT NUMERIC                         08100000
                OR BV001-PV-DEPT-IN = ZEROES                            08110000
                STRING 'BAD DEPT '      DELIMITED BY SIZE               08120000
                       BV001-PV-DEPT-IN DELIMITED BY SIZE               08130000
                  INTO BV001-PV-RETURN-MSG-1                            08140000
                SET BV001-BAD-INPUT TO TRUE                             08150000
            ELSE                                                        08160000
                MOVE BV001-PV-DEPT-IN-9 TO BV001-PV-WORK-DEPT           08170000
            END-IF.                                                     08180000
      ******************************************************************08190000
      * CHECK THAT THE BRAND MEETS INITIAL REQUIREMENTS                 08200000
      ******************************************************************08210000
       BV001-CHECK-BRAND.                                               08220000
            IF BV001-PV-BRAND-IN = SPACES                               08230000
                OR BV001-PV-BRAND-IN NOT NUMERIC                        08240000
                OR BV001-PV-BRAND-IN = ZEROES                           08250000
                STRING 'BAD BRAND '      DELIMITED BY SIZE              08260000
                       BV001-PV-BRAND-IN DELIMITED BY SIZE              08270000
                  INTO BV001-PV-RETURN-MSG-1                            08280000
                SET BV001-BAD-INPUT TO TRUE                             08290000
            ELSE                                                        08300000
                MOVE BV001-PV-BRAND-IN-9 TO BV001-PV-WORK-BRAND         08310000
            END-IF.                                                     08320000
      ******************************************************************08330000
      * CHECK THAT THE DATE FIELD MEETS INITIAL REQUIREMENTS            08340000
      * IF NO DATE IS PROVIDED, SET TO CURRENT DATE.                    08350000
      ******************************************************************08360000
       BV001-SET-DATE.                                                  08370000
            IF BV001-PV-EFF-DTE-IN = SPACES OR                          08380000
               BV001-PV-EFF-DTE-IN = ZEROES                             08390000
038845        EXEC SQL                                                  08400000
038846           SELECT CURRENT DATE                                    08410000
038847           INTO :BV001-PV-WORK-DATE                               08420000
038848           FROM SYSIBM.SYSDUMMY1                                  08430000
038849         END-EXEC                                                 08440000
               EVALUATE TRUE                                            08450000
                  WHEN SQLCODE = 0                                      08460000
                       CONTINUE                                         08470000
                  WHEN OTHER                                            08480000
                       SET BV001-PV-SQL-ERROR TO TRUE                   08490000
                       MOVE SQLCODE TO BV001-PV-SQLCODE-OUT             08500000
                                       BV001-PV-WORK-SQL-CODE           08510000
                       STRING 'FAIL CURRENT DATE FETCH SQLCODE = '      08520000
                                                     DELIMITED BY SIZE  08530000
                              BV001-PV-WORK-SQL-CODE DELIMITED BY SIZE  08540000
                         INTO BV001-PV-RETURN-MSG-1                     08550000
               END-EVALUATE                                             08560000
            ELSE                                                        08570000
               IF BV001-PV-EFF-DTE-IN-YR NOT NUMERIC OR                 08580000
                  BV001-PV-EFF-DTE-IN-MN NOT NUMERIC OR                 08590000
                  BV001-PV-EFF-DTE-IN-DY NOT NUMERIC                    08600000
                  STRING 'BAD DATE '         DELIMITED BY SIZE          08610000
                         BV001-PV-EFF-DTE-IN DELIMITED BY SIZE          08620000
                    INTO BV001-PV-RETURN-MSG-1                          08630000
                  SET BV001-BAD-INPUT TO TRUE                           08640000
               ELSE                                                     08650000
                  MOVE BV001-PV-EFF-DTE-IN TO BV001-PV-WORK-DATE        08660000
            END-IF.                                                     08670000
      ******************************************************************08680000
      * POPULATE THE SECOND LINE OF THE ERROR MESSAGE APPROPRIATELY     08690000
      ******************************************************************08700000
       BV001-SET-ERROR-MSG2.                                            08710000
            EVALUATE TRUE                                               08720000
               WHEN BV001-WORK-ERROR-JUST-BRND                          08730000
                    STRING 'BRAND = '             DELIMITED BY SIZE     08740000
                           BV001-PV-BRAND-IN      DELIMITED BY SIZE     08750000
                           ' DATE = '             DELIMITED BY SIZE     08760000
                           BV001-PV-WORK-DATE     DELIMITED BY SIZE     08770000
                           ' SQLCODE = '          DELIMITED BY SIZE     08780000
                           BV001-PV-WORK-SQL-CODE DELIMITED BY SIZE     08790000
                      INTO BV001-PV-RETURN-MSG-2                        08800000
               WHEN BV001-WORK-ERROR-BRND-DPT                           08810000
                    STRING 'BRAND = '             DELIMITED BY SIZE     08820000
                           BV001-PV-BRAND-IN      DELIMITED BY SIZE     08830000
                           ' DATE = '             DELIMITED BY SIZE     08840000
                           BV001-PV-WORK-DATE     DELIMITED BY SIZE     08850000
                           ' DEPT = '             DELIMITED BY SIZE     08860000
                           BV001-PV-DEPT-IN       DELIMITED BY SIZE     08870000
                           ' SQLCODE = '          DELIMITED BY SIZE     08880000
                           BV001-PV-WORK-SQL-CODE DELIMITED BY SIZE     08890000
                      INTO BV001-PV-RETURN-MSG-2                        08900000
               WHEN BV001-WORK-ERROR-JUST-VNDR                          08910000
                    STRING ' VENDOR = '           DELIMITED BY SIZE     08920000
                           BV001-PV-ENT-ID-IN     DELIMITED BY SIZE     08930000
                           ' DATE = '             DELIMITED BY SIZE     08940000
                           BV001-PV-WORK-DATE     DELIMITED BY SIZE     08950000
                           ' SQLCODE = '          DELIMITED BY SIZE     08960000
                           BV001-PV-WORK-SQL-CODE DELIMITED BY SIZE     08970000
                      INTO BV001-PV-RETURN-MSG-2                        08980000
               WHEN BV001-WORK-ERROR-VNDR-DPT                           08990000
                    STRING ' VENDOR = '           DELIMITED BY SIZE     09000000
                           BV001-PV-ENT-ID-IN     DELIMITED BY SIZE     09010000
                           ' DATE = '             DELIMITED BY SIZE     09020000
                           BV001-PV-WORK-DATE     DELIMITED BY SIZE     09030000
                           ' DEPT = '             DELIMITED BY SIZE     09040000
                           BV001-PV-DEPT-IN       DELIMITED BY SIZE     09050000
                           ' SQLCODE = '          DELIMITED BY SIZE     09060000
                           BV001-PV-WORK-SQL-CODE DELIMITED BY SIZE     09070000
                      INTO BV001-PV-RETURN-MSG-2                        09080000
               WHEN BV001-WORK-ERROR-BV-DPT                             09090000
                    STRING 'VENDOR = '            DELIMITED BY SIZE     09100000
                           BV001-PV-ENT-ID-IN     DELIMITED BY SIZE     09110000
                           ' BRAND = '            DELIMITED BY SIZE     09120000
                           BV001-PV-BRAND-IN      DELIMITED BY SIZE     09130000
                           ' DATE = '             DELIMITED BY SIZE     09140000
                           BV001-PV-WORK-DATE     DELIMITED BY SIZE     09150000
                           ' DEPT = '             DELIMITED BY SIZE     09160000
                           BV001-PV-DEPT-IN       DELIMITED BY SIZE     09170000
                           ' SQLCODE = '          DELIMITED BY SIZE     09180000
                           BV001-PV-WORK-SQL-CODE DELIMITED BY SIZE     09190000
                      INTO BV001-PV-RETURN-MSG-2                        09200000
            END-EVALUATE.                                               09210000
