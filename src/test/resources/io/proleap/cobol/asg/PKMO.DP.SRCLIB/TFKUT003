       IDENTIFICATION DIVISION.                                         00010005
       PROGRAM-ID.    TFKUT003.                                         00020005
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030005
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040005
       DATE-WRITTEN.  07/29/2005.                                       00050005
       DATE-COMPILED.                                                   00060005
      ******************************************************************00070005
      **THIS PROGRAM WILL APPLY THE INVENTORY RULES USED TO DETERMINE   00080005
      **     IF A STORE INVOLVED IN A TRANSFER IS BEFORE INVENTORY,     00090005
      **     AFTER INVENTORY OR NORMAL.                                 00100005
      ******************************************************************00110005
      **DATE:07/29/2005 MADE BY:JULIE TEWES INITIAL PROGRAM WRITE       00120005
      ******************************************************************00130005
      **DATE:02/10/2006 MADE BY:JULIE TEWES FIX TO CORRECT ISSUE WHERE  00140005
      **                INVENTORY DATE NOT BEING LOOKED UP AND TO ADD   00150005
      **                INVPAR-ACTL-LDG-CTOF-TMST LOGIC                 00160005
      ******************************************************************00170005
      **DATE:05/24/2006 MADE BY:JULIE TEWES INCLUDE INVPAR-ACTL-LDG-CTOF00180005
      **                -TMST IN THE RECEIVE SIDE PROCESSING            00190005
      ******************************************************************00200005
      **DATE:06/27/2006 MADE BY:JULIE TEWES FIX TO BEFORE INVENTORY IND-00210005
      **                CATOR                                           00220005
      ******************************************************************00230005
      **DATE:03/05/2007 MADE BY:M.MORRISON  EXCLUDE ESTIMATED INVENTORY*00240005
      **                WHEN RETRIEVING STORE MAX INVENTORY DATE       *00250005
      ******************************************************************00260005
      * 01/14/09 - J KNOKE          -  WR NO. WR33304                   00270005
      *      HILO MODIFICATIONS  -  TINVPAR                             00280005
      ******************************************************************00280105
062509**DATE:06/25/2009 MADE BY:D. WELLER                               00280205
062509**    TREAT THE 'IA' ADJUSTMENT REASON CODE THE SAME AS THE 'CM'  00280305
062509**    ADJUSTMENT REASON CODE.                                     00280405
      ***************************************************************** 00280505
      * 03/28/2012 CHANGED BY:  DEANNA BRANTNER   PR#3403               00280606
      *            DESCRIPTION: COMPILE ONLY CHANGES TO TFRD054 COPYBK  00280706
      *                         FOR LARGE RETAIL PROJECT.               00280807
      ******************************************************************00280907
       ENVIRONMENT DIVISION.                                            00281007
       CONFIGURATION SECTION.                                           00281107
       SOURCE-COMPUTER.    IBM-3090.                                    00281207
       OBJECT-COMPUTER.    IBM-3090.                                    00281307
       INPUT-OUTPUT SECTION.                                            00281407
       FILE-CONTROL.                                                    00282005
                                                                        00283005
           SELECT INPUT-FILE                                            00284005
                  ASSIGN TO INP01.                                      00285005
           SELECT ML-OUTPUT-FILE                                        00286005
                  ASSIGN TO OUT01.                                      00287005
           SELECT FS-OUTPUT-FILE                                        00288005
                  ASSIGN TO OUT02.                                      00289005
                                                                        00290005
       DATA DIVISION.                                                   00300005
       FILE SECTION.                                                    00310005
                                                                        00320005
       FD  INPUT-FILE                                                   00330005
           RECORDING MODE IS F                                          00340005
           LABEL RECORDS OMITTED                                        00350005
           BLOCK CONTAINS 0 RECORDS                                     00360005
           DATA RECORD IS INPUT-RECORD.                                 00370005
       01  INPUT-RECORD                        PIC X(117).              00380005
                                                                        00390005
       FD  ML-OUTPUT-FILE                                               00400005
           RECORDING MODE IS F                                          00410005
           LABEL RECORDS OMITTED                                        00420005
           BLOCK CONTAINS 0 RECORDS                                     00430005
           DATA RECORD IS ML-OUTPUT-RECORD.                             00440005
       01  ML-OUTPUT-RECORD                     PIC X(120).             00450005
                                                                        00460005
       FD  FS-OUTPUT-FILE                                               00470005
           RECORDING MODE IS F                                          00480005
           LABEL RECORDS OMITTED                                        00490005
           BLOCK CONTAINS 0 RECORDS                                     00500005
           DATA RECORD IS FS-OUTPUT-RECORD.                             00510005
       01  FS-OUTPUT-RECORD                     PIC X(200).             00520005
                                                                        00530005
       WORKING-STORAGE SECTION.                                         00540005
      ******************************************************************00550005
      **PROGRAM SWITCHES                                                00560005
      ******************************************************************00570005
       01  PS-SWITCH-AREAS.                                             00580005
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)  VALUE 'N'.    00590005
               88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.    00600005
               88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.    00610005
           05  PS-FROM-OR-TO-STORE-IND         PIC X(01)  VALUE ' '.    00620005
               88  PS-FROM-STORE                          VALUE 'F'.    00630005
               88  PS-TO-STORE                            VALUE 'T'.    00640005
           05  PS-SPECIAL-HANDLING-TO          PIC X(01)  VALUE 'N'.    00650005
      ***SPECIAL HANDLING IS FOR WHEN ML HAS STARTED THEIR CUTOFF PERIOD00660005
      ***        BY ENTERING A PLANNED INVENTORY DATE                   00670005
               88  PS-SPECIAL-HANDLING-TO-Y               VALUE 'Y'.    00680005
               88  PS-SPECIAL-HANDLING-TO-N               VALUE 'N'.    00690005
           05  PS-SPECIAL-HANDLING-FROM PIC X(01)         VALUE 'N'.    00700005
               88  PS-SPECIAL-HANDLING-FROM-Y             VALUE 'Y'.    00710005
               88  PS-SPECIAL-HANDLING-FROM-N             VALUE 'N'.    00720005
      ******************************************************************00730005
      **DATE TIME FIELDS                                                00740005
      ******************************************************************00750005
       01 DF-DATE-TIME-FIELDS.                                          00760005
          05 DF-ACCEPT-DATE                    PIC X(06) VALUE SPACES.  00770005
          05 DF-ACCEPT-TIME                    PIC X(08) VALUE SPACES.  00780005
      ******************************************************************00790005
      **PROGRAM VARIABLES                                               00800005
      ******************************************************************00810005
       01 DV-DATABASE-VARIABLES.                                        00820005
          05 DV-DURING-INVENTORY-IND           PIC X(16) VALUE SPACES.  00830005
          05 DV-INVENTORY-SNAP-COUNT           PIC S9(07) COMP          00840005
                                                         VALUE ZEROS.   00850005
          05 DV-CREATE-DATE                    PIC X(10) VALUE SPACES.  00860005
          05 DV-RECEIVE-DATE                   PIC X(10) VALUE SPACES.  00870005
          05 DV-INV-STATUS-IND                 PIC X(02) VALUE SPACES.  00880005
      ******************************************************************00890005
      **PROGRAM COUNTERS                                                00900005
      ******************************************************************00910005
       01  PC-PROGRAM-COUNTERS.                                         00920005
           05  PC-INPUT-RECORDS-READ           PIC 9(08) VALUE ZEROS.   00930005
           05  PC-ML-RECORDS-WRITTEN           PIC 9(08) VALUE ZEROS.   00940005
           05  PC-FS-RECORDS-WRITTEN           PIC 9(08) VALUE ZEROS.   00950005
      ******************************************************************00960005
      **PROGRAM CONSTANTS                                               00970005
      ******************************************************************00980005
       01  PC-PROGRAM-CONTSTANTS.                                       00990005
           05  PC-PROGRAM-NAME                 PIC X(08)                01000005
                                               VALUE 'TFKUT003'.        01010005
           05  PC-MAX-DATE                     PIC X(10)                01020005
                                               VALUE '9999-09-09'.      01030005
           05  PC-MAX-TMST                     PIC X(26)                01040005
                                  VALUE '9999-12-31-23.59.59.999999'.   01050005
                                                                        01060005
      ****************************************************************  01070005
      **ABEND INFORMATION                                               01080005
      ****************************************************************  01090005
       01  AC-ABEND-FIELDS.                                             01100005
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    01110005
                                                           COMP SYNC.   01120005
               88  AC-TABLE-ERROR                        VALUE +4004.   01130005
               88  AC-DB2-ERROR                          VALUE +4013.   01140005
       01  PV-MISC-ABEND-FIELDS.                                        01150005
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   01160005
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   01170005
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   01180005
           05  PV-DB2-OPERATION                PIC X(30) VALUE SPACE.   01190005
      ******************************************************************01200005
      **COPYBOOKS                                                       01210005
      ******************************************************************01220005
      ****INPUT RECORD LAYOUT                                           01230005
           COPY TFRD056.                                                01240005
      ****FS RECORD LAYOUT                                              01250005
           COPY FSRD035.                                                01260005
      ****ML RECORD LAYOUT                                              01270005
           COPY TFRD054.                                                01280005
      ****ABEND ROUTINE INFORMATION                                     01290005
           COPY DPWS004.                                                01300005
      ****KOHLS CALENDAR ROUTINE                                        01310005
           COPY DPWS500.                                                01320005
      ****ABEND ROUTINE INFORMATION                                     01330005
           COPY SQLCA2.                                                 01340005
      ******************************************************************01350005
      **TABLE INCLUSIONS                                                01360005
      ******************************************************************01370005
      ***INVENTORY PARMS TABLE                                          01380005
           EXEC SQL                                                     01390005
                INCLUDE TINVPAR                                         01400005
           END-EXEC.                                                    01410005
      ***TRANSFER INVENTORY SNAPSHOT TABLE                              01420005
           EXEC SQL                                                     01430005
                INCLUDE TFT00000                                        01440005
           END-EXEC.                                                    01450005
      *****DB2 TABLE INCLUSION                                          01460005
           EXEC SQL                                                     01470005
                INCLUDE SQLCA                                           01480005
           END-EXEC.                                                    01490005
                                                                        01500005
       PROCEDURE DIVISION.                                              01510005
      ******************************************************************01520005
       000-MAINLINE-PROCESSING.                                         01530005
                                                                        01540005
           PERFORM 1000-INITIALIZATION.                                 01550005
           PERFORM 2000-PROCESS-RECORDS                                 01560005
                   UNTIL PS-END-OF-INPUT-FILE.                          01570005
           PERFORM 9999-WRAPUP.                                         01580005
           GOBACK.                                                      01590005
                                                                        01600005
       EJECT.                                                           01610005
      ******************************************************************01620005
      **INITIALIZATION ROUTINE                                          01630005
      ******************************************************************01640005
       1000-INITIALIZATION.                                             01650005
                                                                        01660005
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01670005
                                                                        01680005
           OPEN INPUT  INPUT-FILE                                       01690005
                OUTPUT ML-OUTPUT-FILE                                   01700005
                       FS-OUTPUT-FILE.                                  01710005
                                                                        01720005
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01730005
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01740005
                                                                        01750005
           PERFORM 9999-READ-INPUT-RECORD.                              01760005
                                                                        01770005
       EJECT.                                                           01780005
      ******************************************************************01790005
      **PROCESS INPUT RECORD TO CHECK ROWS                              01800005
      ******************************************************************01810005
       2000-PROCESS-RECORDS.                                            01820005
                                                                        01830005
           MOVE '2000-PROCESS-RECORDS'  TO PV-PARAGRAPH-NAME.           01840005
                                                                        01850005
           INITIALIZE FS035-ADJUSTMENT-RECORD.                          01860005
           INITIALIZE TFRD054-ML-DAILY-TRANSFER-FILE.                   01870005
                                                                        01880005
           PERFORM 3000-CREATE-ML-RECORD.                               01890005
                                                                        01900005
           IF  TFRD056-ADJ-RSN-CDE = 'CM'                               01910005
               OR TFRD056-ADJ-RSN-CDE = 'IA'                            01920005
      ***WHEN A TRANSFER IS CREATED ALWAYS SEND UNITS TO FASHION        01930005
      ***     FOR TO AND FROM STORES                                    01940005
               PERFORM 4000-CREATE-TO-STR-FS-RECORD                     01950005
               PERFORM 4000-CREATE-FROM-STR-FS-RECORD                   01960005
           ELSE                                                         01970005
      ***ALL OTHER TRANSFER ADJUSTMENT REASONS FOLLOW INVENTORY RULES   01980005
      ***     WHEN SENDING TO FASHION                                   01990005
               IF   TFRD054-FROM-LOC-INV-NORMAL                         02000005
                 OR PS-SPECIAL-HANDLING-FROM-Y                          02010005
                    PERFORM 4000-CREATE-FROM-STR-FS-RECORD              02020005
               END-IF                                                   02030005
               IF   TFRD054-TO-LOC-INV-NORMAL                           02040005
                 OR PS-SPECIAL-HANDLING-TO-Y                            02050005
                    PERFORM 4000-CREATE-TO-STR-FS-RECORD                02060005
               END-IF                                                   02070005
           END-IF.                                                      02080005
                                                                        02090005
           PERFORM 9999-READ-INPUT-RECORD.                              02100005
                                                                        02110005
       EJECT.                                                           02120005
      ******************************************************************02130005
      **CREATE ML RECORD TFRD054                                        02140005
      ******************************************************************02150005
       3000-CREATE-ML-RECORD.                                           02160005
                                                                        02170005
           MOVE '3000-CREATE-ML-RECORD'            TO PV-PARAGRAPH-NAME.02180005
                                                                        02190005
           IF  TFRD056-ADJ-RSN-CDE = 'CM'                               02200005
               OR TFRD056-ADJ-RSN-CDE = 'IA'                            02210005
      ****TRANSFER HAS BEEN CREATED                                     02220005
               PERFORM 3100-PERFORM-CREATE-RULES                        02230005
               SET TFRD054-INVENTORY-SNAP-NO       TO TRUE              02240005
           ELSE                                                         02250005
      ****TRANSFER HAS BEEN RECEIVED                                    02260005
               EVALUATE TRUE                                            02270005
                  WHEN TFRD056-XFER-CRTE-DATE = TFRD056-XFER-RCPT-DATE  02280005
      ****IF TRANSFER IS CREATED AND RECEIVED ON THE SAME DAY IT MUST   02290005
      ****   BE SENT TO ML/FS THE SAME WAY                              02300005
                       PERFORM 3100-PERFORM-CREATE-RULES                02310005
                       SET TFRD054-INVENTORY-SNAP-NO                    02320005
                                                    TO TRUE             02330005
                       SET PS-SPECIAL-HANDLING-TO-Y TO TRUE             02340005
                       SET PS-SPECIAL-HANDLING-FROM-Y                   02350005
                                                    TO TRUE             02360005
                  WHEN OTHER                                            02370005
                       PERFORM 3200-PERFORM-RECEIVE-RULES               02380005
      ****IF FBD IS AN ACTUAL DATE DO NOT CHECK IF TRANSFER AS BEEN     02390005
      ****    PASSED IN THE SNAPSHOT PROCESS AS IT DOESN'T MATTER       02400005
                       MOVE TFRD056-TO-STR-NBR      TO INVPAR-LOC-NBR   02410005
                       PERFORM 3225-RETRIEVE-TINVPAR                    02420005
                       IF  INVPAR-ACTL-FIN-BK-DTE = PC-MAX-DATE         02430005
                           PERFORM 3300-INVENTORY-SNAP-CHECK            02440005
                       ELSE                                             02450005
                           SET TFRD054-INVENTORY-SNAP-NO                02460005
                                                    TO TRUE             02470005
                       END-IF                                           02480005
               END-EVALUATE                                             02490005
           END-IF.                                                      02500005
                                                                        02510005
           PERFORM 3400-MOVE-ML-DATA.                                   02520005
           PERFORM 9999-WRITE-ML-RECORD.                                02530005
                                                                        02540005
       EJECT.                                                           02550005
      ******************************************************************02560005
      **PERFORM ML/FS CREATE TRANSFER INVENTORY RULES                   02570005
      ******************************************************************02580005
       3100-PERFORM-CREATE-RULES.                                       02590005
                                                                        02600005
           MOVE '3100-PERFORM-CREATE-RULES'    TO PV-PARAGRAPH-NAME.    02610005
                                                                        02620005
      ****CHECK FROM STORE AGAINST INVENTORY RULES                      02630005
           MOVE TFRD056-FRM-PSTG-STR-NBR       TO INVPAR-LOC-NBR.       02640005
           MOVE TFRD056-XFER-CRTE-DATE         TO DV-CREATE-DATE.       02650005
           SET PS-FROM-STORE                   TO TRUE.                 02660005
           PERFORM 3110-CHECK-STORE-INV.                                02670005
           PERFORM 3120-INTERPRET-STATUS.                               02680005
                                                                        02690005
      ****CHECK TO STORE AGAINST INVENTORY RULES                        02700005
           MOVE TFRD056-TO-PSTG-STR-NBR        TO INVPAR-LOC-NBR.       02710005
           MOVE TFRD056-XFER-CRTE-DATE         TO DV-CREATE-DATE.       02720005
           SET PS-TO-STORE                     TO TRUE.                 02730005
           PERFORM 3110-CHECK-STORE-INV.                                02740005
           PERFORM 3120-INTERPRET-STATUS.                               02750005
                                                                        02760005
       EJECT.                                                           02770005
      ******************************************************************02780005
      **CHECK STORE CREATE DATE AGAINST INVENTORY CYCLE (INV DATE & FBD)02790005
      ******************************************************************02800005
       3110-CHECK-STORE-INV.                                            02810005
                                                                        02820005
           MOVE '3110-CHECK-STORE-INV'    TO PV-PARAGRAPH-NAME.         02830005
                                                                        02840005
           EXEC SQL                                                     02850005
               SELECT '01'                                              02860005
               INTO   :DV-INV-STATUS-IND                                02870005
               FROM   TINVPAR                                           02880005
               WHERE  LOC_NBR          =  :INVPAR-LOC-NBR               02890005
                 AND  ACTL_INV_DTE     =  :PC-MAX-DATE                  02900005
                 AND  ACTL_FIN_BK_DTE  =  :PC-MAX-DATE                  02910005
                 AND  DATE(ACTL_LDG_CTOF_TMST)                          02920005
                                       <= :DV-CREATE-DATE               02930005
                 AND  LOC_INV_STAT_CDE =  'IN'                          02940005
                 AND NOT                                                02950005
      ****THESE INDICATE ESTIMATED INVENTORY AND SHOULD NOT BE USED     02960005
                      (SCAN_INV_IND  = 'Y'                              02970005
                        AND  SCHD_PHY_CNT_CDE = 'EI')                   02980005
               UNION                                                    02990005
               SELECT '02'                                              03000005
               INTO   :DV-INV-STATUS-IND                                03010005
               FROM   TINVPAR                                           03020005
               WHERE  LOC_NBR          =  :INVPAR-LOC-NBR               03030005
                 AND  ACTL_INV_DTE     =  :DV-CREATE-DATE               03040005
                 AND  ACTL_FIN_BK_DTE  =  :PC-MAX-DATE                  03050005
                 AND  DATE(ACTL_LDG_CTOF_TMST)                          03060005
                                       <= :DV-CREATE-DATE               03070005
                 AND  LOC_INV_STAT_CDE =  'IN'                          03080005
                 AND NOT                                                03090005
      ****THESE INDICATE ESTIMATED INVENTORY AND SHOULD NOT BE USED     03100005
                      (SCAN_INV_IND  = 'Y'                              03110005
                        AND  SCHD_PHY_CNT_CDE = 'EI')                   03120005
               UNION                                                    03130005
               SELECT '03'                                              03140005
               INTO   :DV-INV-STATUS-IND                                03150005
               FROM   TINVPAR                                           03160005
               WHERE  LOC_NBR          =  :INVPAR-LOC-NBR               03170005
                 AND  ACTL_INV_DTE     <  :DV-CREATE-DATE               03180005
                 AND  ACTL_FIN_BK_DTE  =  :PC-MAX-DATE                  03190005
                 AND  DATE(ACTL_LDG_CTOF_TMST)                          03200005
                                       <= :DV-CREATE-DATE               03210005
                 AND  LOC_INV_STAT_CDE =  'IN'                          03220005
                 AND NOT                                                03230005
      ****THESE INDICATE ESTIMATED INVENTORY AND SHOULD NOT BE USED     03240005
                      (SCAN_INV_IND  = 'Y'                              03250005
                        AND  SCHD_PHY_CNT_CDE = 'EI')                   03260005
                WITH UR                                                 03270005
           END-EXEC.                                                    03280005
                                                                        03290005
           EVALUATE TRUE                                                03300005
              WHEN SQLCODE = +0                                         03310005
                  CONTINUE                                              03320005
              WHEN SQLCODE = +100                                       03330005
                   MOVE '04' TO DV-INV-STATUS-IND                       03340005
              WHEN OTHER                                                03350005
                   DISPLAY 'SELECT FROM TINVPAR....'                    03360005
                   DISPLAY 'LOC-NBR...' INVPAR-LOC-NBR                  03370005
                   DISPLAY 'XFER CREATE DATE..' TFRD056-XFER-CRTE-DATE  03380005
                   PERFORM 9999-DISPLAY-STATISTICS                      03390005
                   PERFORM 9999-SQL-ERROR                               03400005
           END-EVALUATE.                                                03410005
                                                                        03420005
       EJECT.                                                           03430005
      ******************************************************************03440005
      **LOGIC TO DETERMINE INVENTORY INDICATOR                          03450005
      ******************************************************************03460005
       3120-INTERPRET-STATUS.                                           03470005
                                                                        03480005
           MOVE '3120-INTERPRET-STATUS'        TO PV-PARAGRAPH-NAME.    03490005
                                                                        03500005
           EVALUATE TRUE                                                03510005
              WHEN  DV-INV-STATUS-IND = '01'                            03520005
              WHEN  DV-INV-STATUS-IND = '02'                            03530005
                   IF  PS-FROM-STORE                                    03540005
                       SET TFRD054-FROM-LOC-INV-BEFORE TO TRUE          03550005
                   ELSE                                                 03560005
                       SET TFRD054-TO-LOC-INV-BEFORE   TO TRUE          03570005
                   END-IF                                               03580005
              WHEN  DV-INV-STATUS-IND = '03'                            03590005
                   IF  PS-FROM-STORE                                    03600005
                       SET TFRD054-FROM-LOC-INV-AFTER  TO TRUE          03610005
                   ELSE                                                 03620005
                       SET TFRD054-TO-LOC-INV-AFTER    TO TRUE          03630005
                   END-IF                                               03640005
              WHEN  DV-INV-STATUS-IND = '04'                            03650005
                   IF  PS-FROM-STORE                                    03660005
                       SET TFRD054-FROM-LOC-INV-NORMAL TO TRUE          03670005
                   ELSE                                                 03680005
                       SET TFRD054-TO-LOC-INV-NORMAL   TO TRUE          03690005
                   END-IF                                               03700005
           END-EVALUATE.                                                03710005
                                                                        03720005
       EJECT.                                                           03730005
      ******************************************************************03740005
      **PERFORM ML/FS RECEIVE TRANSFER INVENTORY RULES                  03750005
      ******************************************************************03760005
       3200-PERFORM-RECEIVE-RULES.                                      03770005
                                                                        03780005
           MOVE '3200-PERFORM-RECEIVE-RULES'   TO PV-PARAGRAPH-NAME.    03790005
                                                                        03800005
           MOVE TFRD056-XFER-CRTE-DATE         TO DV-CREATE-DATE.       03810005
           MOVE TFRD056-XFER-RCPT-DATE         TO DV-RECEIVE-DATE.      03820005
                                                                        03830005
      ****FROM STORE INVENTORY LOGIC                                    03840005
           MOVE TFRD056-FRM-PSTG-STR-NBR             TO INVPAR-LOC-NBR  03850005
           PERFORM 3225-RETRIEVE-TINVPAR                                03860005
           IF   (INVPAR-ACTL-INV-DTE = PC-MAX-DATE                      03870005
             AND INVPAR-ACTL-LDG-CTOF-TMST NOT EQUAL PC-MAX-TMST)       03880005
             OR (DV-RECEIVE-DATE = INVPAR-ACTL-INV-DTE                  03890005
             AND INVPAR-ACTL-FIN-BK-DTE = PC-MAX-DATE)                  03900005
                 SET TFRD054-FROM-LOC-INV-BEFORE     TO TRUE            03910005
                 SET PS-SPECIAL-HANDLING-FROM-Y      TO TRUE            03920005
           ELSE                                                         03930005
                 MOVE TFRD056-XFER-CRTE-DATE         TO DV-CREATE-DATE  03940005
                 MOVE TFRD056-XFER-RCPT-DATE         TO DV-RECEIVE-DATE 03950005
                 SET PS-FROM-STORE                   TO TRUE            03960005
                 PERFORM 3210-RETRIEVE-INV-STATUS                       03970005
                 PERFORM 3220-INTERPRET-STATUS                          03980005
                 SET PS-SPECIAL-HANDLING-FROM-N      TO TRUE            03990005
           END-IF.                                                      04000005
                                                                        04010005
      ****TO STORE INVENTORY LOGIC                                      04020005
           MOVE TFRD056-TO-PSTG-STR-NBR              TO INVPAR-LOC-NBR. 04030005
           PERFORM 3225-RETRIEVE-TINVPAR                                04040005
           IF   (INVPAR-ACTL-INV-DTE = PC-MAX-DATE                      04050005
             AND INVPAR-ACTL-LDG-CTOF-TMST NOT EQUAL PC-MAX-TMST)       04060005
             OR (DV-RECEIVE-DATE = INVPAR-ACTL-INV-DTE                  04070005
             AND INVPAR-ACTL-FIN-BK-DTE = PC-MAX-DATE)                  04080005
                 SET TFRD054-TO-LOC-INV-BEFORE       TO TRUE            04090005
                 SET PS-SPECIAL-HANDLING-TO-Y TO TRUE                   04100005
           ELSE                                                         04110005
                 MOVE TFRD056-XFER-CRTE-DATE         TO DV-CREATE-DATE  04120005
                 MOVE TFRD056-XFER-RCPT-DATE         TO DV-RECEIVE-DATE 04130005
                 SET PS-TO-STORE                     TO TRUE            04140005
                 PERFORM 3210-RETRIEVE-INV-STATUS                       04150005
                 PERFORM 3220-INTERPRET-STATUS                          04160005
                 SET PS-SPECIAL-HANDLING-TO-N        TO TRUE            04170005
           END-IF.                                                      04180005
                                                                        04190005
       EJECT.                                                           04200005
      ******************************************************************04210005
      **RETREIVE WHAT PROCESSING SCENARIO IS IN EFFECT FOR              04220005
      ******************************************************************04230005
       3210-RETRIEVE-INV-STATUS.                                        04240005
                                                                        04250005
           MOVE '3210-RETRIEVE-INV-STATUS' TO PV-PARAGRAPH-NAME.        04260005
                                                                        04270005
           EXEC SQL                                                     04280005
                SELECT '01'                                             04290005
                INTO :DV-INV-STATUS-IND                                 04300005
                FROM TABLE (SELECT MAX(ACTL_INV_DTE) AS INVDTE          04310005
                                     ,LOC_NBR          AS LOC           04320005
                              FROM TINVPAR                              04330005
                              GROUP BY  LOC_NBR )      A                04340005
                      ,TINVPAR   B                                      04350005
                WHERE  A.INVDTE           = B.ACTL_INV_DTE              04360005
                  AND  A.LOC              = B.LOC_NBR                   04370005
                  AND  B.ACTL_INV_DTE    >= :DV-CREATE-DATE             04380005
                  AND  B.ACTL_FIN_BK_DTE <= :DV-RECEIVE-DATE            04390005
                  AND  B.ACTL_INV_DTE    <  :DV-RECEIVE-DATE            04400005
                  AND  B.ACTL_INV_DTE    <> :PC-MAX-DATE                04410005
                  AND  B.LOC_NBR         =  :INVPAR-LOC-NBR             04420005
                  AND  B.LOC_INV_STAT_CDE  =  'IN'                      04430005
      ****THESE INDICATE ESTIMATED INVENTORY AND SHOULD NOT BE USED     04440005
                  AND NOT (B.SCAN_INV_IND  = 'Y'                        04450005
                           AND B.SCHD_PHY_CNT_CDE = 'EI')               04460005
                 UNION                                                  04470005
                SELECT '02'                                             04480005
                INTO :DV-INV-STATUS-IND                                 04490005
                FROM TABLE (SELECT MAX(ACTL_INV_DTE) AS INVDTE          04500005
                                  ,LOC_NBR          AS LOC              04510005
                            FROM TINVPAR                                04520005
                            GROUP BY  LOC_NBR )      A                  04530005
                    ,TINVPAR B                                          04540005
                WHERE  A.INVDTE           = B.ACTL_INV_DTE              04550005
                  AND  A.LOC              = B.LOC_NBR                   04560005
                  AND  B.ACTL_INV_DTE    >= :DV-CREATE-DATE             04570005
                  AND  B.ACTL_FIN_BK_DTE >  :DV-RECEIVE-DATE            04580005
                  AND  B.ACTL_INV_DTE    <  :DV-RECEIVE-DATE            04590005
                  AND  B.ACTL_INV_DTE    <> :PC-MAX-DATE                04600005
                  AND  B.LOC_NBR         =  :INVPAR-LOC-NBR             04610005
                  AND  B.LOC_INV_STAT_CDE  =  'IN'                      04620005
      ****THESE INDICATE ESTIMATED INVENTORY AND SHOULD NOT BE USED     04630005
                  AND NOT (B.SCAN_INV_IND  = 'Y'                        04640005
                           AND B.SCHD_PHY_CNT_CDE = 'EI')               04650005
                 UNION                                                  04660005
                SELECT '03'                                             04670005
                INTO :DV-INV-STATUS-IND                                 04680005
                FROM TABLE (SELECT MAX(ACTL_INV_DTE) AS INVDTE          04690005
                                  ,LOC_NBR          AS LOC              04700005
                            FROM TINVPAR                                04710005
                            GROUP BY  LOC_NBR )      A                  04720005
                    ,TINVPAR    B                                       04730005
                WHERE  A.INVDTE           = B.ACTL_INV_DTE              04740005
                  AND  A.LOC              = B.LOC_NBR                   04750005
                  AND  B.ACTL_INV_DTE    >= :DV-CREATE-DATE             04760005
                  AND  B.ACTL_FIN_BK_DTE >= :DV-RECEIVE-DATE            04770005
                  AND  B.ACTL_INV_DTE    >= :DV-RECEIVE-DATE            04780005
                  AND  B.ACTL_INV_DTE    <> :PC-MAX-DATE                04790005
                  AND  B.LOC_NBR         =  :INVPAR-LOC-NBR             04800005
                  AND  B.LOC_INV_STAT_CDE  =  'IN'                      04810005
      ****THESE INDICATE ESTIMATED INVENTORY AND SHOULD NOT BE USED     04820005
                  AND NOT (B.SCAN_INV_IND  = 'Y'                        04830005
                           AND  B.SCHD_PHY_CNT_CDE = 'EI')              04840005
                 UNION                                                  04850005
                SELECT '04'                                             04860005
                INTO :DV-INV-STATUS-IND                                 04870005
                FROM TABLE (SELECT MAX(ACTL_INV_DTE) AS INVDTE          04880005
                                     ,LOC_NBR          AS LOC           04890005
                              FROM TINVPAR                              04900005
                              GROUP BY  LOC_NBR )      A                04910005
                      ,TINVPAR   B                                      04920005
                WHERE  A.INVDTE           = B.ACTL_INV_DTE              04930005
                  AND  A.LOC              = B.LOC_NBR                   04940005
                  AND  B.ACTL_INV_DTE     < :DV-CREATE-DATE             04950005
                  AND  B.ACTL_FIN_BK_DTE  > :DV-RECEIVE-DATE            04960005
                  AND  B.ACTL_INV_DTE     > :DV-RECEIVE-DATE            04970005
                  AND  B.ACTL_INV_DTE    <> :PC-MAX-DATE                04980005
                  AND  B.LOC_NBR          = :INVPAR-LOC-NBR             04990005
                  AND  B.LOC_INV_STAT_CDE  =  'IN'                      04991005
      ****THESE INDICATE ESTIMATED INVENTORY AND SHOULD NOT BE USED     04992005
                  AND NOT (B.SCAN_INV_IND  = 'Y'                        04993005
                           AND B.SCHD_PHY_CNT_CDE = 'EI')               04994005
                 UNION                                                  04995005
                SELECT '05'                                             04996005
                INTO :DV-INV-STATUS-IND                                 04997005
                FROM TABLE (SELECT MAX(ACTL_INV_DTE) AS INVDTE          04998005
                                  ,LOC_NBR          AS LOC              04999005
                            FROM TINVPAR                                05000005
                            GROUP BY  LOC_NBR )      A                  05010005
                    ,TINVPAR   B                                        05020005
                WHERE  A.INVDTE           = B.ACTL_INV_DTE              05030005
                  AND  A.LOC              = B.LOC_NBR                   05040005
                  AND  B.ACTL_INV_DTE     < :DV-CREATE-DATE             05050005
                  AND  B.ACTL_FIN_BK_DTE >= :DV-RECEIVE-DATE            05060005
                  AND  B.ACTL_INV_DTE     < :DV-RECEIVE-DATE            05070005
                  AND  B.ACTL_INV_DTE    <> :PC-MAX-DATE                05080005
                  AND  B.LOC_NBR          = :INVPAR-LOC-NBR             05090005
                  AND  B.LOC_INV_STAT_CDE  =  'IN'                      05100005
      ****THESE INDICATE ESTIMATED INVENTORY AND SHOULD NOT BE USED     05110005
                  AND NOT (B.SCAN_INV_IND  = 'Y'                        05120005
                           AND B.SCHD_PHY_CNT_CDE = 'EI')               05130005
               WITH UR                                                  05140005
           END-EXEC.                                                    05150005
                                                                        05160005
           EVALUATE TRUE                                                05170005
              WHEN SQLCODE = +0                                         05180005
                   CONTINUE                                             05190005
              WHEN SQLCODE = +100                                       05200005
                   MOVE '06'           TO DV-INV-STATUS-IND             05210005
              WHEN OTHER                                                05220005
                   DISPLAY 'SELECT INVENTORY DATES....'                 05230005
                   DISPLAY 'LOC-NBR...' INVPAR-LOC-NBR                  05240005
                   DISPLAY 'CREATE DATE..' DV-CREATE-DATE               05250005
                   DISPLAY 'RECEIVE DATE..' DV-RECEIVE-DATE             05260005
                   PERFORM 9999-DISPLAY-STATISTICS                      05270005
                   PERFORM 9999-SQL-ERROR                               05280005
           END-EVALUATE.                                                05290005
                                                                        05300005
       EJECT.                                                           05310005
      ******************************************************************05320005
      **LOGIC TO DETERMINE INVENTORY INDICATOR                          05330005
      ******************************************************************05340005
       3220-INTERPRET-STATUS.                                           05350005
                                                                        05360005
           MOVE '3220-INTERPRET-STATUS'        TO PV-PARAGRAPH-NAME.    05370005
                                                                        05380005
           EVALUATE TRUE                                                05390005
              WHEN  DV-INV-STATUS-IND = '01'                            05400005
      ****INDICATES THE CREATE DATE IS ON OR BEFORE AN INVENTORY DATE   05410005
      ****          AND THE RECEIVE DATE IS AFTER THE FBD               05420005
                   IF  PS-FROM-STORE                                    05430005
                       SET TFRD054-FROM-LOC-INV-AFTER                   05440005
                                               TO TRUE                  05450005
                   ELSE                                                 05460005
                       SET TFRD054-TO-LOC-INV-AFTER                     05470005
                                               TO TRUE                  05480005
                   END-IF                                               05490005
              WHEN  DV-INV-STATUS-IND = '02'                            05500005
      ****INDICATES THE CREATE DATE IS ON OR BEFORE AN INVENTORY DATE   05510005
      ****          AND THE RECEIVE DATE IS ON OR BEFORE THE FBD        05520005
                   IF  PS-FROM-STORE                                    05530005
                       SET TFRD054-FROM-LOC-INV-BEFORE                  05540005
                                               TO TRUE                  05550005
                   ELSE                                                 05560005
                       SET TFRD054-TO-LOC-INV-BEFORE                    05570005
                                               TO TRUE                  05580005
                   END-IF                                               05590005
              WHEN  DV-INV-STATUS-IND = '03'                            05600005
      ****INDICATES THE CREATE DATE IS ON OR BEFORE AN INVENTORY DATE   05610005
      ****          AND THE RECEIVE DATE IS ON OR BEFORE THE FBD        05620005
              WHEN  DV-INV-STATUS-IND = '04'                            05630005
      ****INDICATES THE CREATE DATE IS AFTER AN INVENTORY DATE          05640005
      ****          AND THE RECEIVE DATE IS AFTER THE FBD               05650005
              WHEN  DV-INV-STATUS-IND = '05'                            05660005
      ****INDICATES THE CREATE DATE IS AFTER AN INVENTORY DATE          05670005
      ****          AND THE RECEIVE DATE IS BEFORE OR ON THE FBD        05680005
              WHEN  DV-INV-STATUS-IND = '06'                            05690005
      ****INDICATES STORE DOES NOT EXIST IN TABLE TINVPAR               05700005
                   IF  PS-FROM-STORE                                    05710005
                       SET TFRD054-FROM-LOC-INV-NORMAL                  05720005
                                               TO TRUE                  05730005
                   ELSE                                                 05740005
                       SET TFRD054-TO-LOC-INV-NORMAL                    05750005
                                               TO TRUE                  05760005
                   END-IF                                               05770005
           END-EVALUATE.                                                05780005
                                                                        05790005
       EJECT.                                                           05800005
      ******************************************************************05810005
      **RETRIEVE STORE MAX INVENTORY DATE                               05820005
      ******************************************************************05830005
       3225-RETRIEVE-TINVPAR.                                           05840005
                                                                        05850005
           MOVE '3225-RETRIEVE-TINVPAR'   TO PV-PARAGRAPH-NAME.         05860005
                                                                        05870005
           EXEC SQL                                                     05880005
               SELECT A.ACTL_INV_DTE                                    05890005
                     ,A.ACTL_FIN_BK_DTE                                 05900005
                     ,A.ACTL_LDG_CTOF_TMST                              05910005
               INTO  :INVPAR-ACTL-INV-DTE                               05920005
                    ,:INVPAR-ACTL-FIN-BK-DTE                            05930005
                    ,:INVPAR-ACTL-LDG-CTOF-TMST                         05940005
               FROM TINVPAR                   A                         05950005
                     ,TABLE (SELECT MAX(ACTL_INV_DTE) AS INV_DTE        05960005
                                   ,LOC_NBR           AS LOC_NBR        05970005
                          FROM TINVPAR                                  05980005
                             WHERE  LOC_NBR = :INVPAR-LOC-NBR           05990005
                               AND  LOC_INV_STAT_CDE  =  'IN'           06000005
                             GROUP BY LOC_NBR)        B                 06010005
               WHERE  A.ACTL_INV_DTE      =  B.INV_DTE                  06020005
                 AND  A.LOC_NBR           =  B.LOC_NBR                  06030005
                 AND  A.LOC_INV_STAT_CDE  =  'IN'                       06040005
      ****THESE INDICATE ESTIMATED INVENTORY AND SHOULD NOT BE USED     06050005
                 AND NOT (A.SCAN_INV_IND     = 'Y'                      06060005
                          AND  A.SCHD_PHY_CNT_CDE = 'EI')               06070005
               WITH UR                                                  06080005
           END-EXEC.                                                    06090005
                                                                        06100005
           EVALUATE TRUE                                                06110005
              WHEN SQLCODE = +0                                         06120005
                   CONTINUE                                             06130005
              WHEN SQLCODE = +100                                       06140005
                   MOVE SPACES       TO INVPAR-ACTL-INV-DTE             06150005
              WHEN OTHER                                                06160005
                   DISPLAY 'SELECT FROM TINVPAR....'                    06170005
                   DISPLAY 'LOC-NBR...' INVPAR-LOC-NBR                  06180005
                   PERFORM 9999-DISPLAY-STATISTICS                      06190005
                   PERFORM 9999-SQL-ERROR                               06200005
           END-EVALUATE.                                                06210005
                                                                        06220005
       EJECT.                                                           06230005
      ******************************************************************06240005
      **CHECK STORE INVENTORY DATES TO DETERMINE INVENTORY INDICATOR    06250005
      ******************************************************************06260005
       3300-INVENTORY-SNAP-CHECK.                                       06270005
                                                                        06280005
           MOVE '3300-INVENTORY-SNAP-CHECK' TO PV-PARAGRAPH-NAME.       06290005
                                                                        06300005
           MOVE TFRD056-XFER-DKEY-ID        TO TFT00000-XFER-DKEY-ID.   06310005
                                                                        06320005
           EXEC SQL                                                     06330005
                SELECT 1                                                06340005
                INTO   :DV-INVENTORY-SNAP-COUNT                         06350005
                FROM   TFT_PHY_INV_ITM                                  06360005
                WHERE  XFER_DKEY_ID = :TFT00000-XFER-DKEY-ID            06370005
                WITH UR                                                 06380005
           END-EXEC.                                                    06390005
                                                                        06400005
           EVALUATE TRUE                                                06410005
              WHEN SQLCODE = +0                                         06420005
              WHEN SQLCODE = -811                                       06430005
                   SET TFRD054-INVENTORY-SNAP-YES                       06440005
                                            TO TRUE                     06450005
              WHEN SQLCODE = +100                                       06460005
                   SET TFRD054-INVENTORY-SNAP-NO                        06470005
                                            TO TRUE                     06480005
              WHEN OTHER                                                06490005
                   DISPLAY 'SELECT FROM TINVPAR....'                    06500005
                   DISPLAY 'LOC-NBR...' INVPAR-LOC-NBR                  06510005
                   DISPLAY 'XFER CREATE DATE..' TFRD056-XFER-CRTE-DATE  06520005
                   DISPLAY 'XFER DKEY ID..' TFT00000-XFER-DKEY-ID       06530005
                   PERFORM 9999-DISPLAY-STATISTICS                      06540005
                   PERFORM 9999-SQL-ERROR                               06550005
           END-EVALUATE.                                                06560005
                                                                        06570005
       EJECT.                                                           06580005
      ******************************************************************06590005
      **MOVE REST OF DATA TO ML LAYOUT                                  06600005
      ******************************************************************06610005
       3400-MOVE-ML-DATA.                                               06620005
                                                                        06630005
           MOVE '3400-MOVE-ML-DATA'        TO PV-PARAGRAPH-NAME.        06640005
                                                                        06650005
      ****DOCUMENT INFORMATION                                          06660005
           MOVE TFRD056-FRM-PSTG-STR-NBR   TO TFRD054-FROM-LOC-NBR.     06670005
           MOVE TFRD056-TO-PSTG-STR-NBR    TO TFRD054-TO-LOC-NBR.       06680005
           MOVE TFRD056-XFER-RCPT-DATE     TO TFRD054-RECEIVE-DATE.     06690005
           MOVE TFRD056-XFER-CRTE-DATE     TO TFRD054-CREATE-DATE.      06700005
           MOVE TFRD056-JULIAN-DATE        TO TFRD054-BATCH-NBR.        06710005
           MOVE TFRD056-TYP-1ST-LNE-DESC   TO TFRD054-TYP-1ST-LNE-DESC. 06720005
           MOVE TFRD056-TYP-2ND-LNE-DESC   TO TFRD054-TYP-2ND-LNE-DESC. 06730005
           MOVE TFRD056-ADJ-RSN-CDE        TO TFRD054-ADJ-RSN-CDE.      06740005
           MOVE TFRD056-TO-STR-NBR         TO TFRD054-ORIG-TO-LOC-NBR.  06750005
      ****DOCUMENT NBR                                                  06760005
           MOVE TFRD056-XFER-SEQ-NBR       TO TFRD054-DOC-SEQ-NBR.      06770005
           MOVE TFRD056-FR-STR-NBR         TO TFRD054-DOC-FROM-LOC-NBR. 06780005
      ****ITEM INFORMATION                                              06790005
           MOVE TFRD056-XFER-LNE-NBR       TO TFRD054-LNE-NBR.          06800005
           MOVE TFRD056-SKU-NBR            TO TFRD054-SKU-NBR.          06810005
           MOVE TFRD056-ITM-RTL-PRIC-AMT   TO TFRD054-ITEM-PRICE.       06820005
           MOVE TFRD056-ITM-QTY            TO TFRD054-ITEM-QTY.         06830005
           MOVE TFRD056-DEPT               TO TFRD054-DEPT.             06840005
           MOVE TFRD056-SUBCLASS           TO TFRD054-SUBCLASS.         06850005
           MOVE TFRD056-EXTENDED-RETAIL    TO TFRD054-EXTENDED-RETAIL.  06860005
                                                                        06870005
       EJECT.                                                           06880005
      ******************************************************************06890005
      **CREATE ML RECORD TFRD054                                        06900005
      ******************************************************************06910005
       4000-CREATE-FROM-STR-FS-RECORD.                                  06920005
                                                                        06930005
           MOVE '4000-CREATE-FROM-STR-FS-RECORD'                        06940005
                                         TO PV-PARAGRAPH-NAME.          06950005
                                                                        06960005
      ****FROM STORE RECORD TO FASHION SALES                            06970005
           MOVE TFRD056-ITM-NBR          TO FS035-ITM-NBR.              06980005
           MOVE TFRD056-FRM-PSTG-STR-NBR TO FS035-LOC-NBR.              06990005
           MOVE TFRD056-ITM-QTY          TO FS035-ADJ-QTY.              07000005
           MOVE TFRD056-JULIAN-DATE      TO FS035-BTCH-NBR.             07010005
           MOVE PC-PROGRAM-NAME          TO FS035-ADJ-SRC-PGM-NM.       07020005
           SET  FS035-ACTN-NEG           TO TRUE.                       07030005
           SET  FS035-RCPT-ADJ           TO TRUE.                       07040005
           PERFORM 9999-WRITE-FS-RECORD.                                07050005
                                                                        07060005
       EJECT.                                                           07070005
      ******************************************************************07080005
      **CREATE ML RECORD TFRD054                                        07090005
      ******************************************************************07100005
       4000-CREATE-TO-STR-FS-RECORD.                                    07110005
                                                                        07120005
           MOVE '4000-CREATE-TO-STR-FS-RECORD'                          07130005
                                         TO PV-PARAGRAPH-NAME.          07140005
                                                                        07150005
      ****TO STORE RECORD TO FASHION SALES                              07160005
           MOVE TFRD056-ITM-NBR          TO FS035-ITM-NBR.              07170005
           MOVE TFRD056-TO-PSTG-STR-NBR  TO FS035-LOC-NBR.              07180005
           MOVE TFRD056-ITM-QTY          TO FS035-ADJ-QTY.              07190005
           MOVE TFRD056-JULIAN-DATE      TO FS035-BTCH-NBR.             07200005
           MOVE PC-PROGRAM-NAME          TO FS035-ADJ-SRC-PGM-NM.       07210005
           SET  FS035-ACTN-PLS           TO TRUE.                       07220005
           SET  FS035-RCPT-ADJ           TO TRUE.                       07230005
           PERFORM 9999-WRITE-FS-RECORD.                                07240005
                                                                        07250005
       EJECT.                                                           07260005
      ******************************************************************07270005
      **READ INPUT RECORD                                               07280005
      ******************************************************************07290005
       9999-READ-INPUT-RECORD.                                          07300005
                                                                        07310005
           READ INPUT-FILE INTO TFRD056-TRANSFER-TO-PROCESS             07320005
                AT END                                                  07330005
                SET PS-END-OF-INPUT-FILE    TO TRUE                     07340005
           END-READ.                                                    07350005
                                                                        07360005
           IF  PS-NOT-END-OF-INPUT-FILE                                 07370005
               ADD +1                      TO PC-INPUT-RECORDS-READ     07380005
           END-IF.                                                      07390005
                                                                        07400005
       EJECT.                                                           07410005
      ****************************************************************  07420005
      **WRITE ML RECORD                                                 07430005
      ****************************************************************  07440005
       9999-WRITE-ML-RECORD.                                            07450005
                                                                        07460005
           WRITE ML-OUTPUT-RECORD FROM TFRD054-ML-DAILY-TRANSFER-FILE.  07470005
                                                                        07480005
           ADD +1              TO PC-ML-RECORDS-WRITTEN.                07490005
                                                                        07500005
       EJECT.                                                           07510005
      ****************************************************************  07520005
      **WRITE FS RECORD                                                 07530005
      ****************************************************************  07540005
       9999-WRITE-FS-RECORD.                                            07550005
                                                                        07560005
           WRITE FS-OUTPUT-RECORD FROM FS035-ADJUSTMENT-RECORD.         07570005
                                                                        07580005
           ADD +1              TO PC-FS-RECORDS-WRITTEN.                07590005
                                                                        07600005
       EJECT.                                                           07610005
      ******************************************************************07620005
      **CLOSE INPUT FILES.                                              07630005
      ******************************************************************07640005
       9999-WRAPUP.                                                     07650005
                                                                        07660005
           CLOSE INPUT-FILE                                             07670005
                 ML-OUTPUT-FILE                                         07680005
                 FS-OUTPUT-FILE.                                        07690005
                                                                        07700005
           PERFORM 9999-DISPLAY-STATISTICS.                             07710005
                                                                        07720005
       EJECT.                                                           07730005
      ******************************************************************07740005
      **DISPLAY PROGRAM STATS                                           07750005
      ******************************************************************07760005
       9999-DISPLAY-STATISTICS.                                         07770005
                                                                        07780005
           DISPLAY '*************************************************'. 07790005
           DISPLAY '* PROGRAM...........: TFKUT003'.                    07800005
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             07810005
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             07820005
           DISPLAY '* INPUT RECORDS READ: ' PC-INPUT-RECORDS-READ.      07830005
           DISPLAY '* ML RECORDS WRITTEN: ' PC-ML-RECORDS-WRITTEN.      07840005
           DISPLAY '* FS RECORDS WRITTEN: ' PC-FS-RECORDS-WRITTEN.      07850005
           DISPLAY '*************************************************'. 07860005
                                                                        07870005
       EJECT.                                                           07880005
      ******************************************************************07890005
      * ABEND ROUTINE FOR BAD SQL CALLS                                *07900005
      ******************************************************************07910005
       9999-SQL-ERROR.                                                  07920005
                                                                        07930005
           DISPLAY '** ABEND     **'.                                   07940005
           DISPLAY '** PROGRAM   ** = TFKUT003'.                        07950005
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07960005
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               07970005
      ***************************************************************** 07980005
      *    COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED  07990005
      *    TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.   08000005
      ***************************************************************** 08010005
           COPY DPPD004.                                                08020005
           SET AC-DB2-ERROR TO TRUE.                                    08030005
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         08040005
                                                                        08050005
       EJECT.                                                           08060005
