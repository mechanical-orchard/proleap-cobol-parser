       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP046.                                         00020049
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  10/30/2002.                                       00050049
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070041
      **       VSM ITEM MESSAGE - NUMBER 041 - SKU ADD2                 00080049
      ******************************************************************00090041
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO UPDATE THE FOLLOWING   00100056
      ***     ATTRIBUTES ON XST_SKU:                                    00110056
      ***     1. TX_PRD_CDE_DESC                                        00120056
      ***     2. NRF_CLOR_DESC                                          00130056
      ***THE UPDATE GENERALLY FOLLOWS THE SKU ADD MESSAGE (#40) AS THE  00140056
      ***    SECOND PART OF A SKU ADD - DUE TO VSM SCREEN LAYOUTS.      00150056
      ******************************************************************00160056
      **10/30/2002 CHANGED BY:JULIE SMITH   INITIAL PROGRAM WRITE       00170049
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00180054
      ******************************************************************00190041
       ENVIRONMENT DIVISION.                                            00200000
       CONFIGURATION SECTION.                                           00210000
       SOURCE-COMPUTER.  IBM-3090.                                      00220000
       OBJECT-COMPUTER.  IBM-3090.                                      00230000
       DATA DIVISION.                                                   00240000
                                                                        00250017
       WORKING-STORAGE SECTION.                                         00260017
      ******************************************************************00270000
      **PROGRAM SWITCHES                                                00280000
      ******************************************************************00290000
       01  PS-PROGRAM-SWITCHES.                                         00300000
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00310020
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00320018
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00330018
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00340051
               88  PS-ACTION-REVERSE                  VALUE 01.         00350051
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00360051
               88  PS-ACTION-NOTHING                  VALUE 02.         00370051
      ******************************************************************00380000
      **PROGRAM COUNTERS                                                00390018
      ******************************************************************00400000
       01  PC-PROGRAM-COUNTERS.                                         00410018
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00420018
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00430024
      ******************************************************************00440049
      **PROGRAM CALCULATIONS                                            00450049
      ******************************************************************00460049
       01  PC-PROGRAM-CALCULATIONS.                                     00470049
           05  PC-10-DIGIT                  PIC 9(10)  VALUE ZEROS.     00480049
           05  PC-18-DIGIT                  PIC S9(18) COMP VALUE ZEROS.00490049
           05  PC-09-DIGIT                  PIC S9(09) COMP VALUE ZEROS.00500049
      ******************************************************************00510018
      **PROGRAM VARIABLES                                               00520018
      ******************************************************************00530018
       01  PV-PROGRAM-VARIABLES.                                        00540018
           05  PV-PARAGRAPH-NAME               PIC X(20)  VALUE SPACES. 00550019
      ******************************************************************00560000
      **COPYBOOKS                                                       00570000
      ******************************************************************00580000
      ***DB2 MESSAGE AREA                                               00590000
           COPY SQLCA2.                                                 00600057
      ***MESSAGE COPYBOOK                                               00610018
           COPY IMRD008.                                                00620057
      ***DATE ROUTINE COPYBOOK                                          00630019
           COPY DPWS500.                                                00640057
      ******************************************************************00650000
      **TABLE INCLUSIONS                                                00660000
      ******************************************************************00670000
      ***DB2 COMMUNICATIONS AREA                                        00680000
           EXEC SQL                                                     00690000
                INCLUDE SQLCA                                           00700000
           END-EXEC.                                                    00710000
      ***XST_SKU TABLE                                                  00720040
           EXEC SQL                                                     00730000
                INCLUDE XST00010                                        00740040
           END-EXEC.                                                    00750000
                                                                        00760000
       LINKAGE SECTION.                                                 00770000
      ******************************************************************00780000
      **LINKAGE VARIABLES                                               00790000
      ******************************************************************00800000
       01  LV-IMRD008-RECORD               PIC X(450).                  00810018
       01  LV-RETURN-CODE                  PIC S9(04).                  00820017
       01  LV-ACTION-IND                   PIC 9(02).                   00830051
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00840051
                                                                        00850024
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00860019
                                LV-RETURN-CODE,                         00870051
                                LV-ACTION-IND,                          00880051
                                LV-CURRENT-TIMESTAMP.                   00890051
      ******************************************************************00900000
       0000-MAINLINE.                                                   00910000
                                                                        00920000
            PERFORM 1000-INITIALIZATION.                                00930000
            PERFORM 2000-PROCESS-MESSAGE                                00940040
                    UNTIL PS-PROCESS-COMPLETE.                          00950018
            PERFORM 9999-WRAPUP.                                        00960000
                                                                        00970000
       EJECT.                                                           00980018
      ***************************************************************** 00990000
      **INITIALIZATION AREA - ALL MOVES TO UPDATE/INSERT                01000024
      ***************************************************************** 01010000
       1000-INITIALIZATION.                                             01020000
                                                                        01030000
           MOVE '1000-INITIALIZATION'      TO PV-PARAGRAPH-NAME.        01040018
                                                                        01050018
           SET PS-PROCESS-CONTINUE         TO TRUE.                     01060020
           INITIALIZE PC-RETRY-COUNTER.                                 01070018
                                                                        01080049
           MOVE LV-IMRD008-RECORD          TO IMRD008-RECORD.           01090023
                                                                        01100019
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01110049
      *     COMP FIELDS.                                                01120049
           MOVE IM008-ITEM-KEY-INTR-UPC-ID TO PC-10-DIGIT.              01130049
           MOVE PC-10-DIGIT                TO PC-18-DIGIT.              01140049
           MOVE PC-18-DIGIT                TO PC-09-DIGIT.              01150049
           MOVE PC-09-DIGIT                TO XST00010-INTR-UPC-ID.     01160049
                                                                        01170049
           MOVE IM008-SKU-ADD2-TX-PRD-CDE-DESC                          01180049
                                           TO XST00010-TX-PRD-CDE-DESC. 01190049
           MOVE IM008-SKU-ADD2-NRF-CLOR-DESC TO XST00010-NRF-CLOR-DESC. 01200049
121402     MOVE LV-CURRENT-TIMESTAMP       TO XST00010-LAST-UPD-TMST.   01210054
121402     MOVE IM008-CRE-TMST          TO XST00010-SOR-LAST-MSG-TMST.  01220054
                                                                        01230043
       EJECT.                                                           01240040
      ***************************************************************** 01250040
      **MAIN PROGRAM LOGIC - TRIES TO INSERT MESSAGE SENT               01260040
      ***************************************************************** 01270040
       2000-PROCESS-MESSAGE.                                            01280040
                                                                        01290040
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01300040
                                                                        01310040
           EXEC SQL                                                     01320040
              UPDATE XST_SKU                                            01330049
                  SET   TX_PRD_CDE_DESC  = :XST00010-TX-PRD-CDE-DESC    01340049
                      , NRF_CLOR_DESC    = :XST00010-NRF-CLOR-DESC      01350049
                      , LAST_UPD_TMST    = :XST00010-LAST-UPD-TMST      01360054
121402                , SOR_LAST_MSG_TMST = :XST00010-SOR-LAST-MSG-TMST 01370054
                  WHERE INTR_UPC_ID      = :XST00010-INTR-UPC-ID        01380049
           END-EXEC.                                                    01390040
                                                                        01400040
           EVALUATE TRUE                                                01410040
      ***SUCCESSFUL PROCESSING                                          01420056
              WHEN SQLCODE = ZEROS                                      01430040
                   SET PS-PROCESS-COMPLETE   TO TRUE                    01440049
      ***ROW/TABLE LOCK                                                 01450056
              WHEN SQLCODE = -911                                       01460040
              WHEN SQLCODE = -913                                       01470047
                   ADD +1                      TO PC-RETRY-COUNTER      01480040
                   IF  PC-RETRY-COUNTER >= PC-RETRY-MAX                 01490040
                       SET PS-PROCESS-COMPLETE TO TRUE                  01500040
                   ELSE                                                 01510040
                       ADD +1                  TO PC-RETRY-COUNTER      01520040
                   END-IF                                               01530040
              WHEN OTHER                                                01540040
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01550040
           END-EVALUATE.                                                01560040
                                                                        01570040
       EJECT.                                                           01580040
      ******************************************************************01590040
      **PASS SQLCODE TO CALLING AND EXIT PROGRAM                        01600040
      ******************************************************************01610040
       9999-WRAPUP.                                                     01620040
                                                                        01630040
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME.             01640053
                                                                        01650040
           MOVE SQLCODE               TO LV-RETURN-CODE.                01660053
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01670051
                                                                        01680051
           EXIT PROGRAM.                                                01690040
                                                                        01700040
       EJECT.                                                           01710040
