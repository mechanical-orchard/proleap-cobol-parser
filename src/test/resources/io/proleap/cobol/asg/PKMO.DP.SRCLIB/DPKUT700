CBL NODYNAM,LIB,OBJECT,RENT,RES,APOST                                   CKT00010
      *                                                               * CKT00020
      * this also exists in tkms.install.mqseries                     * CKT00020
      *                                                               * CKT00020
      *                                                               * CKT00020
      * ------------------------------------------------------------- * CKT00030
       IDENTIFICATION DIVISION.                                         CKT00040
      * ------------------------------------------------------------- * CKT00050
       PROGRAM-ID. dpkut700.                                            CKT00060
      *REMARKS                                                          CKT00070
      *                                                                 CKT00080
      * Author: Wayne M. Schutz WSCHUTZ@VNET.IBM.COM                    CKT00090
      * Release: 1.0, September 2nd, 1994.                              CKT00100
      *                                                                 CKT00110
      *                                                                 CKT00120
      * This program is used in conjunction with CKTIBAT2.  Its         CKT00130
      * function is to cause CKTIBAT2 to end by passing it a            CKT00140
      * "quit" message.  It sends a message of type REPORT to the       CKT00150
      * initiation queue, with a feebback code of MQFB-QUIT.            CKT00160
      *                                                                 CKT00170
      * //   JOB Card                                                   CKT00180
      * //dpkut700 EXEC PGM=dpkut700,PARM=',BATCH.INITQ'                CKT00190
      * //STEPLIB  DD  DSN=++user loadlib++,DISP=SHR                    CKT00200
      * //         DD  DSN=++mqm hlq ++.SCSQAUTH,DISP=SHR               CKT00210
      * //SYSPRINT DD  SYSOUT=*,                                        CKT00220
      * //             DCB=(DSORG=PS,RECFM=VBA,LRECL=133,BLKSIZE=137)   CKT00230
      * //                                                              CKT00240
      *                                                                 CKT00250
      * The DD SYSPRINT is required as progress messages are            CKT00260
      * written to it during execution.                                 CKT00270
      *                                                                 CKT00280
      *  The parm specified the queue manager and initation queue       CKT00290
      *  to send to QUIT message to.  The queue manager name is         CKT00300
      *  optional.                                                      CKT00310
      * ------------------------------------------------------------- * CKT00320
      *                                                                 CKT00330
       ENVIRONMENT DIVISION.                                            CKT00340
      * ------------------------------------------------------------- * CKT00350
       INPUT-OUTPUT SECTION.                                            CKT00360
       FILE-CONTROL.                                                    CKT00370
           SELECT SYSPRINT ASSIGN TO UT-S-SYSPRINT.                     CKT00380
      * ------------------------------------------------------------- * CKT00390
       DATA DIVISION.                                                   CKT00400
      * ------------------------------------------------------------- * CKT00410
       FILE SECTION.                                                    CKT00420
       FD  SYSPRINT                                                     CKT00430
           BLOCK CONTAINS 0 RECORDS                                     CKT00440
           RECORDING MODE IS F.                                         CKT00450
       01  PRINT-REC.                                                   CKT00460
           05  CARRIAGE-CONTROL        PIC X.                           CKT00470
           05  PRINT-DATA              PIC X(132).                      CKT00480
      * ------------------------------------------------------------- * CKT00490
       WORKING-STORAGE SECTION.                                         CKT00500
      * ------------------------------------------------------------- * CKT00510
      *                                                                 CKT00520
      *    W00 - General work fields                                    CKT00530
      *                                                                 CKT00540
       01  W00-DATE.                                                    CKT00550
           05  W00-YY                  PIC 99.                          CKT00560
           05  W00-MM                  PIC 99.                          CKT00570
           05  W00-DD                  PIC 99.                          CKT00580
       01  W00-TIME.                                                    CKT00590
           05  W00-HOURS               PIC 99.                          CKT00600
           05  W00-MINUTES             PIC 99.                          CKT00610
           05  W00-SECONDS             PIC 99.                          CKT00620
           05  W00-HUNDRETHS           PIC 99.                          CKT00630
       01  W00-PRINT-DATA              PIC X(132).                      CKT00640
       01  W00-RETURN-CODE             PIC S9(04) BINARY  VALUE ZERO.   CKT00650
       01  W00-INDEX                   PIC S9(04) BINARY  VALUE ZERO.   CKT00660
      *                                                                 CKT00670
      *    W01 - Lines of the print report                              CKT00680
      *                                                                 CKT00690
       01  W01-HEADER-1.                                                CKT00700
           05  FILLER                  PIC X(10) VALUE SPACES.          CKT00710
           05  W01-MM                  PIC 99.                          CKT00720
           05  FILLER                  PIC X     VALUE '/'.             CKT00730
           05  W01-DD                  PIC 99.                          CKT00740
           05  FILLER                  PIC X     VALUE '/'.             CKT00750
           05  W01-YY                  PIC 99.                          CKT00760
           05  FILLER                  PIC X(8)  VALUE SPACES.          CKT00770
           05  FILLER                  PIC X(106) VALUE                 CKT00780
                                             'BATCH TRIGGER TERMINATOR'.CKT00790
       01  W01-HEADER-2.                                                CKT00800
           05  FILLER                  PIC X(5)  VALUE SPACES.          CKT00810
           05  FILLER                  PIC X(29) VALUE                  CKT00820
                                       '        Queue Manager Name : '. CKT00830
           05  W01-MQM-NAME            PIC X(48) VALUE SPACES.          CKT00840
           05  FILLER                  PIC X(50) VALUE SPACES.          CKT00850
       01  W01-HEADER-3.                                                CKT00860
           05  FILLER                  PIC X(17) VALUE SPACES.          CKT00870
           05  FILLER                  PIC X(17) VALUE                  CKT00880
                                                    ' Trigger Queue : '.CKT00890
           05  W01-QUEUE-NAME          PIC X(48) VALUE SPACES.          CKT00900
           05  FILLER                  PIC X(50) VALUE SPACES.          CKT00910
       01  W01-HEADER-4.                                                CKT00920
           05  W01-HOURS               PIC X(2).                        CKT00930
           05  FILLER                  PIC X VALUE ':'.                 CKT00940
           05  W01-MINUTES             PIC X(2).                        CKT00950
           05  FILLER                  PIC X VALUE ':'.                 CKT00960
           05  W01-SECONDS             PIC X(2).                        CKT00970
           05  FILLER                  PIC X(3) VALUE SPACES.           CKT00980
           05  W01-HEADER-4-DATA       PIC X(80) VALUE SPACES.          CKT00990
      *                                                                 CKT01000
      *    W02 - Data fields derived from the PARM field                CKT01010
      *                                                                 CKT01020
       01  W02-MQM                     PIC X(48) VALUE SPACES.          CKT01030
       01  W02-OBJECT                  PIC X(48) VALUE SPACES.          CKT01040
      *                                                                 CKT01050
      *    W03 - MQM API fields                                         CKT01060
      *                                                                 CKT01070
       01  W03-BUFFER-LENGTH           PIC S9(9) BINARY  VALUE 0.       CKT01080
       01  W03-HCONN                   PIC S9(9) BINARY.                CKT01090
       01  W03-OPTIONS                 PIC S9(9) BINARY.                CKT01100
       01  W03-HOBJ                    PIC S9(9) BINARY.                CKT01110
       01  W03-DATA-LENGTH             PIC S9(9) BINARY.                CKT01120
       01  W03-COMPCODE                PIC S9(9) BINARY.                CKT01130
       01  W03-REASON                  PIC S9(9) BINARY.                CKT01140
       01  W03-MESSAGE-DATA            PIC S9.                          CKT01150
       01  W04-MESSAGE-1.                                               CKT01160
           05  FILLER              PIC X(10)  VALUE SPACES.             CKT01170
           05  FILLER              PIC X(122) VALUE                     CKT01180
              '********** NO DATA PASSED TO PROGRAM. PROGRAM REQUIRES A CKT01190
      -       'QUEUE MANAGER NAME AND A QUEUE NAME. **********'.        CKT01200
       01  W04-MESSAGE-2.                                               CKT01210
           05  FILLER              PIC X(25)  VALUE SPACES.             CKT01220
           05  FILLER              PIC X(107) VALUE                     CKT01230
              '********** NO QUEUE MANAGER NAME PASSED TO PROGRAM - DEFACKT01240
      -       'ULT USED *****'.                                         CKT01250
       01  W04-MESSAGE-3.                                               CKT01260
           05  FILLER              PIC X(38) VALUE SPACES.              CKT01270
           05  FILLER              PIC X(94) VALUE                      CKT01280
              '********** NO QUEUE NAME PASSED TO PROGRAM. **********'. CKT01290
       01  W04-MESSAGE-4.                                               CKT01300
           05  FILLER              PIC X(13) VALUE SPACES.              CKT01310
           05  FILLER              PIC X(32) VALUE                      CKT01320
                   '********** AN ERROR OCCURRED IN '.                  CKT01330
           05  W04-MSG4-TYPE       PIC X(10).                           CKT01340
           05  FILLER              PIC X(20) VALUE                      CKT01350
                   '. COMPLETION CODE = '.                              CKT01360
           05  W04-MSG4-COMPCODE   PIC Z(8)9.                           CKT01370
           05  FILLER              PIC X(15) VALUE ' REASON CODE ='.    CKT01380
           05  W04-MSG4-REASON     PIC Z(8)9.                           CKT01390
           05  FILLER              PIC X(24) VALUE ' **********'.       CKT01400
      *                                                                 CKT01410
      *    The following copy files define API control blocks.          CKT01420
      *                                                                 CKT01430
       01  W05-MQM-OBJECT-DESCRIPTOR.                                   CKT01440
           COPY CMQODV.                                                 CKT01450
       01  W05-MQM-MESSAGE-DESCRIPTOR.                                  CKT01460
           COPY CMQMDV.                                                 CKT01470
       01  W05-MQM-PUT-MESSAGE-OPTIONS.                                 CKT01480
           COPY CMQPMOV.                                                CKT01490
      *                                                                 CKT01500
      *    Copy file of constants (for filling in the control blocks)   CKT01510
      *    and return codes (for testing the result of a call)          CKT01520
      *                                                                 CKT01530
       01  W05-MQM-CONSTANTS.                                           CKT01540
           COPY CMQV.                                                   CKT01550
      *                                                                 CKT01560
      *    W06 - Return values                                          CKT01570
      *                                                                 CKT01580
       01  W06-CSQ4-OK             PIC S9(4) VALUE 0.                   CKT01590
       01  W06-CSQ4-WARNING        PIC S9(4) VALUE 4.                   CKT01600
       01  W06-CSQ4-ERROR          PIC S9(4) VALUE 8.                   CKT01610
      *                                                                 CKT01620
      *                                                                 CKT01630
      * ------------------------------------------------------------- * CKT01640
       LINKAGE SECTION.                                                 CKT01650
      * ------------------------------------------------------------- * CKT01660
       01  PARMDATA.                                                    CKT01670
           05  PARM-LEN                PIC S9(03) BINARY.               CKT01680
           05  PARM-STRING             PIC X(100).                      CKT01690
      *                                                                 CKT01700
           EJECT                                                        CKT01710
      * ------------------------------------------------------------- * CKT01720
       PROCEDURE DIVISION USING PARMDATA.                               CKT01730
      * ------------------------------------------------------------- * CKT01740
      * ------------------------------------------------------------- * CKT01750
       A-MAIN SECTION.                                                  CKT01760
      * ------------------------------------------------------------- * CKT01770
      *                                                               * CKT01780
      * This section receives the names of the queue manager and the  * CKT01790
      * queue from the PARM statement in the JCL. It opens the queue, * CKT01800
      * reads all the messages, and prints them                       * CKT01810
      *                                                               * CKT01820
      * This section uses the MQGET call with the BROWSE option to    * CKT01830
      * ensure that the data is not removed from the queue            * CKT01840
      *                                                               * CKT01850
      * ------------------------------------------------------------- * CKT01860
      *                                                                 CKT01870
      *    Open the print file, initialize the fields for the           CKT01880
      *    header date and the page number, and print the first         CKT01890
      *    line of the header                                           CKT01900
      *                                                                 CKT01910
           OPEN OUTPUT SYSPRINT.                                        CKT01920
      *                                                                 CKT01930
           ACCEPT W00-DATE FROM DATE.                                   CKT01940
           MOVE W00-MM TO W01-MM.                                       CKT01950
           MOVE W00-DD TO W01-DD.                                       CKT01960
           MOVE W00-YY TO W01-YY.                                       CKT01970
      *                                                                 CKT01980
           MOVE W01-HEADER-1 TO PRINT-DATA.                             CKT01990
           WRITE PRINT-REC AFTER ADVANCING PAGE.                        CKT02000
      *                                                                 CKT02010
      *    If no data was passed, create a message, print it, and       CKT02020
      *    exit                                                         CKT02030
      *                                                                 CKT02040
           IF PARM-LEN = 0 THEN                                         CKT02050
              MOVE W04-MESSAGE-1 TO PRINT-DATA                          CKT02060
              WRITE PRINT-REC                                           CKT02070
              MOVE W06-CSQ4-WARNING TO W00-RETURN-CODE                  CKT02080
              GO TO A-MAIN-END                                          CKT02090
           END-IF.                                                      CKT02100
      *                                                                 CKT02110
      *    Separate into the relevant fields any data passed in the     CKT02120
      *    PARM statement                                               CKT02130
      *                                                                 CKT02140
           UNSTRING PARM-STRING DELIMITED BY ALL ','                    CKT02150
                                   INTO W02-MQM                         CKT02160
                                        W02-OBJECT.                     CKT02170
      *                                                                 CKT02180
      *    Move the data (spaces if nothing is entered) into the        CKT02190
      *    relevant print fields                                        CKT02200
      *                                                                 CKT02210
           MOVE W02-MQM    TO W01-MQM-NAME.                             CKT02220
           MOVE W02-OBJECT TO W01-QUEUE-NAME.                           CKT02230
      *                                                                 CKT02240
      *    Print a message if the queue manager name is missing, the    CKT02250
      *    default queue manager will be used                           CKT02260
      *                                                                 CKT02270
           IF W02-MQM = SPACES OR W02-MQM = LOW-VALUES THEN             CKT02280
              MOVE W04-MESSAGE-2 TO PRINT-DATA                          CKT02290
              WRITE PRINT-REC                                           CKT02300
           END-IF.                                                      CKT02310
      *                                                                 CKT02320
      *    Print a message if the queue name is missing and exit from   CKT02330
      *    program                                                      CKT02340
      *                                                                 CKT02350
           IF W02-OBJECT = SPACES OR W02-OBJECT = LOW-VALUES THEN       CKT02360
              MOVE W04-MESSAGE-3 TO PRINT-DATA                          CKT02370
              WRITE PRINT-REC                                           CKT02380
              MOVE W06-CSQ4-WARNING TO W00-RETURN-CODE                  CKT02390
              GO TO A-MAIN-END                                          CKT02400
           END-IF.                                                      CKT02410
      *                                                                 CKT02420
      *    Print the remaining header lines                             CKT02430
      *                                                                 CKT02440
           MOVE W01-HEADER-2 TO PRINT-DATA.                             CKT02450
           WRITE PRINT-REC AFTER ADVANCING 2.                           CKT02460
      *                                                                 CKT02470
           MOVE W01-HEADER-3 TO PRINT-DATA.                             CKT02480
           WRITE PRINT-REC AFTER ADVANCING 1.                           CKT02490
      *                                                                 CKT02500
      *    Connect to the specified queue manager.                      CKT02510
      *                                                                 CKT02520
           CALL 'MQCONN' USING W02-MQM                                  CKT02530
                               W03-HCONN                                CKT02540
                               W03-COMPCODE                             CKT02550
                               W03-REASON.                              CKT02560
      *                                                                 CKT02570
      *    Test the output of the connect call.  If the call failed,    CKT02580
      *    print an error message showing the completion code and       CKT02590
      *    reason code                                                  CKT02600
      *                                                                 CKT02610
           IF (W03-COMPCODE NOT = MQCC-OK) THEN                         CKT02620
              MOVE 'CONNECT'     TO W04-MSG4-TYPE                       CKT02630
              MOVE W03-COMPCODE  TO W04-MSG4-COMPCODE                   CKT02640
              MOVE W03-REASON    TO W04-MSG4-REASON                     CKT02650
              MOVE W04-MESSAGE-4 TO PRINT-DATA                          CKT02660
              WRITE PRINT-REC                                           CKT02670
              MOVE W06-CSQ4-ERROR TO W00-RETURN-CODE                    CKT02680
              GO TO A-MAIN-END                                          CKT02690
           END-IF.                                                      CKT02700
      *                                                                 CKT02710
      *    Initialize the object descriptor (MQOD) control block.       CKT02720
      *    (The copy file initializes all the other fields)             CKT02730
      *                                                                 CKT02740
            MOVE W02-OBJECT        TO MQOD-OBJECTNAME.                  CKT02750
      *                                                                 CKT02760
      *    Initialize the working storage fields required to open       CKT02770
      *    the queue                                                    CKT02780
      *                                                                 CKT02790
      *      W03-OPTIONS IS SET TO OPEN THE QUEUE FOR INPUT             CKT02800
      *      W03-HOBJ    is set by the MQOPEN call and is used by the   CKT02810
      *                  MQGET and MQCLOSE calls                        CKT02820
      *                                                                 CKT02830
           MOVE MQOO-INPUT-AS-Q-DEF TO W03-OPTIONS.                     CKT02840
           MOVE MQPMO-NO-SYNCPOINT TO MQPMO-OPTIONS.                    CKT02850
           MOVE MQMT-REPORT TO MQMD-MSGTYPE.                            CKT02860
           MOVE MQFB-QUIT TO MQMD-FEEDBACK.                             CKT02870
      *                                                                 CKT02880
      *    PUT THE REPORT MESSAGE ON THE QUEUE                          CKT02890
      *                                                                 CKT02900
           CALL 'MQPUT1' USING W03-HCONN                                CKT02910
                               MQOD                                     CKT02920
                               MQMD                                     CKT02930
                               MQPMO                                    CKT02940
                               W03-BUFFER-LENGTH                        CKT02950
                               W03-MESSAGE-DATA                         CKT02960
                               W03-COMPCODE                             CKT02970
                               W03-REASON.                              CKT02980
      *                                                                 CKT02990
      *    TEST THE OUTPUT OF THE PUT1 CALL.  IF THE CALL FAILED, PRINT CKT03000
      *    an error message showing the completion code and reason code CKT03010
      *                                                                 CKT03020
           IF (W03-COMPCODE NOT = MQCC-OK) THEN                         CKT03030
              MOVE 'PUT1'        TO W04-MSG4-TYPE                       CKT03040
              MOVE W03-COMPCODE  TO W04-MSG4-COMPCODE                   CKT03050
              MOVE W03-REASON    TO W04-MSG4-REASON                     CKT03060
              MOVE W04-MESSAGE-4 TO PRINT-DATA                          CKT03070
              WRITE PRINT-REC                                           CKT03080
              MOVE W06-CSQ4-ERROR TO W00-RETURN-CODE                    CKT03090
           END-IF.                                                      CKT03100
      *                                                                 CKT03110
      *                                                                 CKT03120
      * Disconnect from the queue manager                               CKT03130
      *                                                                 CKT03140
           CALL 'MQDISC' USING W03-HCONN                                CKT03150
                               W03-COMPCODE                             CKT03160
                               W03-REASON.                              CKT03170
      *                                                                 CKT03180
      *    Test the output of the disconnect call.  If the call failed, CKT03190
      *    print an error message showing the completion code and       CKT03200
      *    reason code                                                  CKT03210
      *                                                                 CKT03220
           IF (W03-COMPCODE NOT = MQCC-OK) THEN                         CKT03230
              MOVE 'DISCONNECT'  TO W04-MSG4-TYPE                       CKT03240
              MOVE W03-COMPCODE  TO W04-MSG4-COMPCODE                   CKT03250
              MOVE W03-REASON    TO W04-MSG4-REASON                     CKT03260
              MOVE W04-MESSAGE-4 TO PRINT-DATA                          CKT03270
              MOVE W06-CSQ4-ERROR TO W00-RETURN-CODE                    CKT03280
              WRITE PRINT-REC                                           CKT03290
           END-IF.                                                      CKT03300
      *                                                                 CKT03310
       A-MAIN-END.                                                      CKT03320
      *                                                                 CKT03330
      *    Set the return code                                          CKT03340
      *                                                                 CKT03350
           MOVE W00-RETURN-CODE to RETURN-CODE.                         CKT03360
      *                                                                 CKT03370
      *    Close the print file and stop                                CKT03380
      *                                                                 CKT03390
           CLOSE SYSPRINT.                                              CKT03400
           STOP RUN.                                                    CKT03410
      *                                                                 CKT03420
