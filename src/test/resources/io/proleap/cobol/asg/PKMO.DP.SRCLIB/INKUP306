       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.     INKUP306.                                        00020000
       INSTALLATION.   KOHLS DEPARTMENT STORES.                         00030000
       DATE-WRITTEN.   12/15/2006.                                      00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *PURPOSE:  THE MODULE TAKES IN THE PACK AND HOLD FILE             00080005
      *          CREATED NIGHTLY FOR STORES TAKING A                    00090005
      *          PHYSICAL INVENTORY.                                    00100000
      *          TRANSACTIONS ARE FORMATTED TO REFLECT PACK AND HOLD,   00110005
      *          LOOKING UP THE PRICE THOUGH PRICE MODULE AND           00120000
      *          EXTD-RTL-AMT IS CALCULATED, AND THE RECORD IS WRITTEN  00130000
      *          TO THE SHEET INVENTORY TABLE - TINENTY.                00140000
      *          (SEE THE FLOW SECTION FOR MORE DETAILS)                00150000
      *          INVALID SKU RECORDS ARE DROPPED TO EXTRACT FILE FOR    00160000
      *          LATER PROCESSING AGAIN.                                00170000
      *                                                                 00180000
      *CHG ID/                                                          00190000
      *WR/PROJ DATE       PGMR        DESCRIPTION                       00200000
      *------- ---------- ----------- ----------------------------------00210000
      *W28545  12-15-2006 JILL LIN    THE NEW E-COMMERCE PARTIAL ORDER  00220000
      *                               PROCESS (CREATION)                00230000
      ******************************************************************00240000
      *W33304  06-19-2008 P JAEGER    HILO CHANGES                      00241000
      ******************************************************************00243000
RONNA *RONNA   06-10-2020 R MCDONALD  THIS WAS UN-ARCHIVED AND          00244004
RONNA *                               IS NOW BEING USED FOR PACK & HOLD 00244104
RONNA *                               TRANSACTIONS THAT GO INTO SHEET   00244204
RONNA *                               INVENTORY                         00244304
RONNA *                               ALL REFERENCES TO ECOM PARTIAL    00244404
RONNA *                               SHIP HAVE BEEN CHANGED TO PACK    00244504
RONNA *                               & HOLD                            00244604
      ******************************************************************00245004
                                                                        00250000
       ENVIRONMENT DIVISION.                                            00260000
       CONFIGURATION SECTION.                                           00270000
       INPUT-OUTPUT SECTION.                                            00280000
       FILE-CONTROL.                                                    00290000
                                                                        00300000
RONNA      SELECT PACK-HOLD-FILE                                        00310004
               ASSIGN TO INP01.                                         00320000
                                                                        00330000
           SELECT CC-CONTROL-FILE                                       00340000
               ASSIGN TO INP02.                                         00350000
                                                                        00360000
           SELECT ERROR-EXTRACT-FILE                                    00370000
               ASSIGN TO OUT01.                                         00380000
                                                                        00390000
       DATA DIVISION.                                                   00400000
       FILE SECTION.                                                    00410000
                                                                        00420000
RONNA  FD  PACK-HOLD-FILE                                               00430004
           RECORDING MODE IS F                                          00440000
           LABEL RECORDS ARE STANDARD                                   00450000
           BLOCK CONTAINS 0 RECORDS.                                    00460000
RONNA  01  PACK-HOLD-RECORD           PIC X(58).                        00470004
                                                                        00480000
       FD  CC-CONTROL-FILE                                              00490000
           RECORDING MODE IS F                                          00500000
           LABEL RECORDS ARE STANDARD                                   00510000
           BLOCK CONTAINS 0 RECORDS.                                    00520000
       01  CC-CONTROL-RECORD          PIC X(80).                        00530000
                                                                        00540000
       FD  ERROR-EXTRACT-FILE                                           00550000
           RECORDING MODE IS F                                          00560000
           LABEL RECORDS ARE STANDARD                                   00570000
           BLOCK CONTAINS 0 RECORDS.                                    00580000
       01  ERROR-EXTRACT-RECORD       PIC X(58).                        00590000
                                                                        00600000
      ******************************************************************00610000
       WORKING-STORAGE SECTION.                                         00620000
      ******************************************************************00630000
                                                                        00640000
       01  PA-PROGRAM-ACCUMULATORS.                                     00650000
           05  PA-INSERT-COUNT         PIC 9(09)            VALUE ZEROS.00660000
           05  PA-UPDATE-COUNT         PIC 9(09)            VALUE ZEROS.00670000
           05  PA-NOT-VALID-SKU        PIC 9(09)            VALUE ZEROS.00680000
           05  PA-EXTRACT-RECS-READ    PIC 9(09)            VALUE ZEROS.00690000
           05  PA-DFF-EXTD-RTL-AMT     PIC S9(15)V99 COMP-3 VALUE ZEROS.00700000
           05  PA-RECORDS-READ         PIC S9(15)    COMP-3 VALUE ZEROS.00710000
           05  PA-DUPLICATE-REC        PIC S9(15)    COMP-3 VALUE ZEROS.00720000
           05  PA-NO-UNIT-COUNT        PIC S9(15)    COMP-3 VALUE ZEROS.00730000
           05  PA-NO-PRICE-COUNT       PIC S9(15)    COMP-3 VALUE ZEROS.00740000
           05  PA-RECORDS-ACCEPTED     PIC S9(15)    COMP-3 VALUE ZEROS.00750000
           05  PA-RETRY-COUNT          PIC S9(04)    COMP   VALUE ZEROS.00760000
           05  PA-RECORDS-ACCPTD-STR   PIC S9(15)    COMP-3 VALUE ZEROS.00761006
                                                                        00770000
       01  PC-PROGRAM-CONSTANTS-AREA.                                   00780000
           05  PC-PROGRAM-NAME         PIC X(08)       VALUE 'INKUP306'.00790000
           05  PC-JOB-NAME             PIC X(08)       VALUE 'INK359  '.00800000
           05  PC-OPERCO-NBR           PIC X(02)       VALUE '03'.      00810000
           05  PC-APLSYS-CDE           PIC X(02)       VALUE 'IN'.      00820000
           05  PC-MDSELV-CDE           PIC X(03)       VALUE 'SCL'.     00830000
           05  PC-MAX-RETRIES          PIC 9(03)       VALUE 3.         00840000
                                                                        00850000
       01  PV-PROGRAM-VARIABLES-AREA.                                   00860000
           05  PV-EXTD-AMOUNT          PIC S9(09)V99 COMP-3 VALUE ZEROS.00870000
           05  PV-OPER-ATTEMPTED       PIC X(25)           VALUE SPACES.00880000
           05  PV-UNT-RTL-AMT-HOLD     PIC S9(15)V99 COMP-3 VALUE ZEROS.00890000
           05  PV-UPC-STAT-CHG-DTE     PIC X(10)           VALUE SPACES.00900000
           05  PV-UNT-RTL-CHG-DTE      PIC X(10)           VALUE SPACES.00910000
           05  PV-STR-NBR-HOLD         PIC 9(04)           VALUE ZEROES.00911006
           05  PV-REC-INSERTED-DISP    PIC 9(09)           VALUE ZEROES.00912028
                                                                        00920000
       01  PS-SWITCHES-AREA.                                            00930000
RONNA      05  PS-END-OF-PACK-HOLD     PIC X                VALUE 'N'.  00940004
RONNA          88  END-OF-PACK-HOLD                         VALUE 'Y'.  00950004
                                                                        00960000
       01  CONTROL-DATE-RECORD.                                         00970000
           05  CC-DATE.                                                 00980000
               10  CC-DATE-YYYY        PIC X(04)           VALUE SPACES.00990000
               10  CC-DASH-1           PIC X               VALUE '-'.   01000000
               10  CC-DATE-MM          PIC X(02)           VALUE SPACES.01010000
               10  CC-DASH-2           PIC X               VALUE '-'.   01020000
               10  CC-DATE-DD          PIC X(02)           VALUE SPACES.01030000
           05  CC-FILLER               PIC X(70)           VALUE SPACES.01040000
                                                                        01050000
       01  CC-DATE-MM-DD.                                               01060000
           05  CC-MONTH                PIC X(02)           VALUE SPACES.01070000
           05  CC-DAY                  PIC X(02)           VALUE SPACES.01080000
                                                                        01090000
       01  CC-UNEDIT-DATE.                                              01100000
           05  CC-UNEDIT-DATE-YYYY     PIC X(04)           VALUE SPACES.01110000
           05  CC-UNEDIT-DATE-MM       PIC X(02)           VALUE SPACES.01120000
           05  CC-UNEDIT-DATE-DD       PIC X(02)           VALUE SPACES.01130000
                                                                        01140000
       01  ABEND-CODE                  PIC S9(04)          VALUE ZEROES 01150000
                               COMP SYNC.                               01160000
           88  ABEND-4004                                  VALUE +4004. 01170000
           88  ABEND-4013                                  VALUE +4013. 01180000
                                                                        01190000
      ******************************************************************01200000
      * COPYBOOK - INPUT RECORD LAYOUT FOR FILES RECEIVED FROM          01210000
      *            E-COMMERCE                                           01220000
      ******************************************************************01230000
           COPY INWS018.                                                01240000
                                                                        01250000
      ******************************************************************01260000
      * COPYBOOK -                                                      01270000
      *    DPWS004 CONTAINS THE FIELDS NECESSARY TO CALL THE SQLCA      01280000
      * MESSAGE MODULE 'DSNTIAR' AND DISPLAY THE FORMATTED RESULTS.     01290000
      ******************************************************************01300000
           COPY DPWS004.                                                01310000
                                                                        01320000
      ******************************************************************01330000
      * SQL ABEND COMMON MODULE                                         01340000
      ******************************************************************01350000
           EXEC SQL                                                     01360000
               INCLUDE SQLCA                                            01370000
           END-EXEC.                                                    01380000
                                                                        01390000
      ******************************************************************01400000
      *    TINENTY - SHEET INVENTORY ENTRY TABLE.                       01410000
      ******************************************************************01420000
           EXEC SQL                                                     01430000
               INCLUDE TINENTY                                          01440000
           END-EXEC.                                                    01450000
                                                                        01460000
      ******************************************************************01470000
      *    TINVPAR - INVENTORY PARAMATERS TABLE                         01480000
      ******************************************************************01490000
           EXEC SQL                                                     01500000
               INCLUDE TINVPAR                                          01510000
           END-EXEC.                                                    01520000
                                                                        01530000
      ******************************************************************01540000
      *    TINCNTL - INVENTORY CONTROL MASTER TABLE                     01550000
      ******************************************************************01560000
           EXEC SQL                                                     01570000
               INCLUDE TINCNTL                                          01580000
           END-EXEC.                                                    01590000
                                                                        01600000
      ******************************************************************01610000
      * COPYBOOK - PRICING MODULE                                       01620000
      ******************************************************************01630000
           COPY INWS400.                                                01640000
                                                                        01650000
      ******************************************************************01660000
       PROCEDURE DIVISION.                                              01670000
      ******************************************************************01680000
       0000-MAINLINE-DRIVER-SECTION.                                    01690000
                                                                        01700000
           PERFORM 1000-INITIALIZATION.                                 01710000
                                                                        01720000
           IF CC-DATE = INVPAR-PLND-INV-DTE                             01730000
               AND IN018-SCAN-DTE = CC-DATE-MM-DD                       01740000
RONNA          PERFORM 2000-PROCESS-PACK-HOLD                           01750004
RONNA              UNTIL END-OF-PACK-HOLD                               01760004
           ELSE                                                         01770000
               DISPLAY '***PARAGRAPH - 0000-MAINLINE-DRIVER-SECTION'    01780000
               DISPLAY 'CC DTE  : ' CC-DATE                             01790000
               DISPLAY 'SCAN DTE: ' IN018-SCAN-DTE                      01800000
               DISPLAY 'PLND DTE: ' INVPAR-PLND-INV-DTE                 01810000
               IF END-OF-PACK-HOLD                                      01811016
                  DISPLAY 'EMPTY FILE '                                 01812016
                  DISPLAY 'NO PACK HOLD DATA TO PROCESS'                01812116
               ELSE                                                     01813016
                  DISPLAY 'CHECK STORE ACTIVE INVENTORY'                01813119
                  PERFORM 4100-BUILD-ERROR-EXTRACT                      01814016
                  MOVE +0 TO PV-STR-NBR-HOLD                            01814122
                  PERFORM 8100-READ-PACK-HOLD                           01814221
                  PERFORM 2000-PROCESS-PACK-HOLD                        01815020
                      UNTIL END-OF-PACK-HOLD                            01816020
      **          PERFORM 9990-ABEND                                    01820017
               END-IF                                                   01830018
           END-IF.                                                      01831016
                                                                        01840000
           PERFORM 9000-END-OF-JOB-TASKS.                               01850000
                                                                        01860000
       9999-EXIT-PROGRAM.                                               01870000
           STOP RUN.                                                    01880000
                                                                        01890000
      ******************************************************************01900000
      * 1000-INITIALIZATION.                                            01910000
      ******************************************************************01920000
       1000-INITIALIZATION.                                             01930000
                                                                        01940000
RONNA      OPEN  INPUT PACK-HOLD-FILE                                   01950004
                 INPUT CC-CONTROL-FILE                                  01960000
                 OUTPUT ERROR-EXTRACT-FILE.                             01970000
                                                                        01980000
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                          01990000
           MOVE +600000 TO INENTY-SHEET-NBR.                            02000000
           MOVE +1      TO INENTY-LNE-NBR.                              02010000
           PERFORM 8000-READ-CC-CONTROL.                                02020000
RONNA      PERFORM 8100-READ-PACK-HOLD.                                 02030004
           MOVE IN018-STORE-NUMBER TO INENTY-STR-NBR                    02040000
                                      INVPAR-LOC-NBR                    02050006
                                      PV-STR-NBR-HOLD.                  02051006
           IF NOT END-OF-PACK-HOLD                                      02052013
              PERFORM 8300-READ-TINVPAR                                 02060013
           END-IF.                                                      02070013
      ******************************************************************02080000
RONNA * 2000-PROCESS-PACK-HOLD.                                         02090004
      ******************************************************************02100000
RONNA  2000-PROCESS-PACK-HOLD.                                          02110004
           ADD +1     TO PA-EXTRACT-RECS-READ.                          02120000
           IF IN018-STORE-NUMBER  = PV-STR-NBR-HOLD                     02130006
              PERFORM 2100-PROCESS-PACK-HOLD-STR                        02140006
           ELSE                                                         02150006
              PERFORM 2200-PCK-HLD-STORE-CHANGE                         02161006
           END-IF.                                                      02170006
                                                                        02171006
           PERFORM 8100-READ-PACK-HOLD.                                 02172009
                                                                        02181006
      ******************************************************************02240106
       2100-PROCESS-PACK-HOLD-STR.                                      02241006
                                                                        02241407
           PERFORM 3000-GATHER-RTL-INFORMATION.                         02242006
                                                                        02243006
           IF INENTY-LNE-NBR = +25                                      02244006
               MOVE +1 TO INENTY-LNE-NBR                                02245006
               ADD +1  TO INENTY-SHEET-NBR                              02246006
           ELSE                                                         02247006
               ADD +1  TO INENTY-LNE-NBR                                02248006
           END-IF.                                                      02249006
                                                                        02249106
      ******************************************************************02249406
       2200-PCK-HLD-STORE-CHANGE.                                       02249506
      ******************************************************************02249606
                                                                        02249811
           IF PA-RECORDS-ACCPTD-STR > ZEROES                            02249906
               PERFORM 8700-UPDATE-TINVPAR                              02250006
               PERFORM 8900-COMMIT-DB2                                  02250128
               MOVE +0   TO PA-RECORDS-ACCPTD-STR                       02250206
           END-IF.                                                      02250406
                                                                        02250506
           MOVE +600000 TO INENTY-SHEET-NBR.                            02250612
           MOVE +1      TO INENTY-LNE-NBR.                              02250712
                                                                        02250812
           MOVE IN018-STORE-NUMBER TO INENTY-STR-NBR                    02250906
                                      INVPAR-LOC-NBR                    02251006
                                      PV-STR-NBR-HOLD.                  02251106
           PERFORM 8300-READ-TINVPAR.                                   02251206
                                                                        02251306
           IF CC-DATE = INVPAR-PLND-INV-DTE                             02251406
               AND IN018-SCAN-DTE = CC-DATE-MM-DD                       02251506
                PERFORM 2100-PROCESS-PACK-HOLD-STR                      02251606
           ELSE                                                         02251706
               DISPLAY '***PARAGRAPH - 2200-PCK-HLD-STORE-CHANGE  '     02251806
               DISPLAY 'CC DTE  : ' CC-DATE                             02251906
               DISPLAY 'SCAN DTE: ' IN018-SCAN-DTE                      02252006
               DISPLAY 'PLND DTE: ' INVPAR-PLND-INV-DTE                 02252106
               PERFORM 4100-BUILD-ERROR-EXTRACT                         02252225
               MOVE +0           TO PV-STR-NBR-HOLD                     02252311
      *****    PERFORM 9990-ABEND                                       02252425
           END-IF.                                                      02252511
      ******************************************************************02253000
      * 3000-GATHER-RTL-INFORMATION.                                    02260000
      *     USE THE SKU FROM DIRECT SHIP FILE TO LOOK UP PRICE AND      02270000
      *     UPDATE TINENTY.                                             02280000
      ******************************************************************02290000
       3000-GATHER-RTL-INFORMATION.                                     02300000
                                                                        02310000
           INITIALIZE IN400-PV-INPUT-AREA.                              02320000
           SET IN400-PV-SKU-LOOKUP  TO TRUE.                            02330000
           MOVE IN018-STORE-NUMBER  TO IN400-CONVERT-LOC.               02340000
           MOVE IN400-CONVERT-LOC   TO IN400-PV-LOC-IN.                 02350000
           MOVE INVPAR-PLND-INV-DTE TO IN400-PV-EFF-DTE-IN.             02360000
           MOVE IN018-SKU           TO IN400-PV-SKU-IN.                 02370000
           PERFORM IN400-LOOKUP-RETAIL.                                 02380000
                                                                        02390000
      ***  INVALID KEY CONDITION..                                      02400000
           IF IN400-PV-INVALID-KEY-INPUT                                02410000
               DISPLAY 'INVALID - ', IN018-SKU,                         02420000
                       ' LOC - ', IN018-STORE-NUMBER,                   02430000
                       ' PRC RTN - ', IN400-PV-RETURN-CDE,              02440000
                       ' ATT RTN - ', IN400-PV-ATT-RTNCDE,              02450000
                       ' PRC SQL - ', IN400-PV-PRC-SQLCDE,              02460000
                       ' ATT SQL - ', IN400-PV-ATT-SQLCDE,              02470000
                       '  ', IN400-PV-RETURN-RSN                        02480000
               DISPLAY IN400-PV-PARAGRAPH                               02490000
               PERFORM 4100-BUILD-ERROR-EXTRACT                         02500000
               ADD +1 TO PA-NOT-VALID-SKU                               02510000
               ADD -1 TO INENTY-LNE-NBR                                 02520000
           ELSE                                                         02530000
               IF IN400-PS-RTL-FOUND                                    02540000
                   IF IN018-QUANTITY GREATER ZERO                       02550000
                       PERFORM 4000-BUILD-SHEET-TINENTY                 02560000
                   ELSE                                                 02570000
                       PERFORM 4100-BUILD-ERROR-EXTRACT                 02580000
                       ADD +1 TO PA-NO-UNIT-COUNT                       02590000
                   END-IF                                               02600000
               ELSE                                                     02610000
                   PERFORM 4100-BUILD-ERROR-EXTRACT                     02620000
                   ADD +1 TO PA-NO-PRICE-COUNT                          02630000
               END-IF                                                   02640000
           END-IF.                                                      02650000
                                                                        02660000
      ******************************************************************02670000
      * 4000-BUILD-SHEET-TINENTY.                                       02680000
      ******************************************************************02690000
       4000-BUILD-SHEET-TINENTY.                                        02700000
                                                                        02710000
      *** FORMAT THE TRANSFER FOR ENTRY INTO THE SHEET TABLE...         02720000
           IF IN400-PS-RENUMBER-SKU                                     02730000
               MOVE IN400-PRC-SKU-OUT  TO INENTY-SKU-NBR                02740000
               MOVE IN400-PRC-UPC-OUT  TO INENTY-UPC-NBR                02750000
           ELSE                                                         02760000
               MOVE IN018-SKU          TO INENTY-SKU-NBR                02770000
               MOVE IN018-UPC-CODE     TO INENTY-UPC-NBR                02780000
           END-IF.                                                      02790000
                                                                        02800000
           MOVE IN018-QUANTITY         TO INENTY-INV-QTY.               02810000
           MOVE IN400-PRC-UNIT-RTL-OUT TO INENTY-UNT-RTL-AMT.           02820000
      *    MOVE PV-UNT-RTL-AMT-HOLD TO INENTY-UNT-RTL-AMT.              02830000
           COMPUTE PV-EXTD-AMOUNT = INENTY-UNT-RTL-AMT                  02840000
                                  * INENTY-INV-QTY.                     02850000
           MOVE PV-EXTD-AMOUNT         TO INENTY-EXTD-RTL-AMT.          02860000
           MOVE PC-PROGRAM-NAME        TO INENTY-LAST-UPD-BY-USR-ID.    02870000
                                                                        02880000
           PERFORM 8200-INSERT-INTO-TINENTY.                            02890000
                                                                        02900000
      ******************************************************************02910000
      * 4100-BUILD-ERROR-EXTRACT.                                       02920000
      ******************************************************************02930000
       4100-BUILD-ERROR-EXTRACT.                                        02940000
RONNA      WRITE ERROR-EXTRACT-RECORD FROM PACK-HOLD-RECORD.            02950004
                                                                        02960000
      ******************************************************************02970000
      * 8000-READ-CC-CONTROL.                                           02980000
      *     READ THE INTRANSIT FILE PASSED FROM THE RECEIVING TEAM.     02990000
      ******************************************************************03000000
       8000-READ-CC-CONTROL.                                            03010000
           READ CC-CONTROL-FILE INTO CONTROL-DATE-RECORD.               03020000
           IF CC-DATE = SPACES                                          03030000
               ACCEPT CC-UNEDIT-DATE FROM DATE YYYYMMDD                 03040000
               MOVE CC-UNEDIT-DATE-MM TO CC-MONTH                       03050000
               MOVE CC-UNEDIT-DATE-DD TO CC-DAY                         03060000
           ELSE                                                         03070000
               MOVE CC-DATE-MM TO CC-MONTH                              03080000
               MOVE CC-DATE-DD TO CC-DAY                                03090000
           END-IF.                                                      03100000
                                                                        03110000
      ******************************************************************03120000
RONNA * 8100-READ-PACK-HOLD.                                            03130004
      *     READ THE INTRANSIT FILE PASSED FROM THE RECEIVING TEAM.     03140000
      ******************************************************************03150000
RONNA  8100-READ-PACK-HOLD.                                             03160004
RONNA      READ PACK-HOLD-FILE                                          03170004
               INTO IN018-INVENTORY-COUNT-RECORD                        03180000
                   AT END                                               03190000
RONNA                  SET END-OF-PACK-HOLD TO TRUE.                    03200004
RONNA      IF NOT END-OF-PACK-HOLD                                      03210004
               ADD 1 TO PA-RECORDS-READ                                 03220000
           END-IF.                                                      03230000
                                                                        03240000
      ******************************************************************03250000
      * 8200-INSERT-INTO-TINENTY.                                       03260000
      ******************************************************************03270000
       8200-INSERT-INTO-TINENTY.                                        03280000
           PERFORM WITH TEST AFTER                                      03290000
              VARYING PA-RETRY-COUNT FROM 1 BY 1                        03300000
               UNTIL (PA-RETRY-COUNT > PC-MAX-RETRIES)                  03310000
                   OR (SQLCODE = 0)                                     03320000
                   OR (SQLCODE = +100)                                  03330000
                   OR (SQLCODE = -803)                                  03340000
                                                                        03350000
             EXEC SQL                                                   03360000
               INSERT INTO TINENTY                                      03370000
                    (STR_NBR                                            03380000
                  ,  SHEET_TYP_CDE                                      03390000
                  ,  SHEET_NBR                                          03400000
                  ,  LNE_NBR                                            03410000
                  ,  SKU_NBR                                            03420000
                  ,  UPC_NBR                                            03430000
                  ,  INV_QTY                                            03440000
                  ,  UNT_RTL_AMT                                        03450000
                  ,  EXTD_RTL_AMT                                       03460000
                  ,  LAST_UPD_TMST                                      03470000
                  ,  LAST_UPD_BY_USR_ID)                                03480000
               VALUES                                                   03490000
                   (:INENTY-STR-NBR                                     03500000
                  , 'M'                                                 03510000
                  , :INENTY-SHEET-NBR                                   03520000
                  , :INENTY-LNE-NBR                                     03530000
                  , :INENTY-SKU-NBR                                     03540000
                  , :INENTY-UPC-NBR                                     03550000
                  , :INENTY-INV-QTY                                     03560000
                  , :INENTY-UNT-RTL-AMT                                 03570000
                  , :INENTY-EXTD-RTL-AMT                                03580000
                  , CURRENT TIMESTAMP                                   03590000
                  , :INENTY-LAST-UPD-BY-USR-ID)                         03600000
             END-EXEC                                                   03610000
                                                                        03620000
             EVALUATE TRUE                                              03630000
               WHEN SQLCODE = ZERO                                      03640000
                   ADD 1            TO PA-RECORDS-ACCEPTED              03650000
                                       PA-RECORDS-ACCPTD-STR            03651006
                   ADD 1            TO PA-INSERT-COUNT                  03660000
               WHEN SQLCODE = -803                                      03670000
                   ADD 1            TO PA-DUPLICATE-REC                 03680000
               WHEN SQLCODE = -904                                      03690000
               WHEN SQLCODE = -911                                      03700000
                   CONTINUE                                             03710000
               WHEN OTHER                                               03720000
                       DISPLAY '***PARAGRAPH - 8200-INSERT-INTO ***'    03730000
                       DISPLAY ' STORE  : ' INENTY-STR-NBR              03740000
                       DISPLAY ' SKU    : ' INENTY-SKU-NBR              03750000
                       DISPLAY 'SHEET   : ' INENTY-SHEET-NBR            03760000
                       DISPLAY 'LINE    : ' INENTY-LNE-NBR              03770000
                       PERFORM 9900-SQL-ERROR                           03780000
               END-EVALUATE                                             03790000
           END-PERFORM.                                                 03800000
                                                                        03810000
           IF  PA-RETRY-COUNT > PC-MAX-RETRIES                          03820000
               DISPLAY '*** MAX RETRIES - 8200-INSERT-INTO ***'         03830000
               DISPLAY 'STORE : ' IN018-STORE-NUMBER                    03840000
               DISPLAY 'SKU   : ' IN018-SKU                             03850000
               DISPLAY 'SHEET : ' INENTY-SHEET-NBR                      03860000
               DISPLAY 'LINE  : ' INENTY-LNE-NBR                        03870000
               PERFORM 9900-SQL-ERROR                                   03880000
           END-IF.                                                      03890000
                                                                        03900000
      ******************************************************************03910000
      * 8300-READ-TINVPAR.                                              03920000
      ******************************************************************03930000
       8300-READ-TINVPAR.                                               03940000
      ***  DISPLAY '8300-READ-TINVPAR'                                  03950027
           PERFORM                                                      03960000
               WITH TEST AFTER                                          03970000
               VARYING PA-RETRY-COUNT FROM 1 BY 1                       03980000
               UNTIL (PA-RETRY-COUNT > PC-MAX-RETRIES)                  03990000
                   OR (SQLCODE = 0)                                     04000000
                   OR (SQLCODE = +100)                                  04010000
                                                                        04020000
               EXEC SQL                                                 04030000
                   SELECT PLND_INV_DTE                                  04040003
                         ,A.INV_ID                                      04050000
                   INTO :INVPAR-PLND-INV-DTE                            04060000
                       ,:INVPAR-INV-ID                                  04070000
                   FROM TINVPAR A,                                      04080000
                        TINCNTL B                                       04090000
                   WHERE B.ACTV_IND = 'Y'                               04100000
                     AND A.INV_ID = B.INV_ID                            04110000
                     AND LOC_NBR = :INVPAR-LOC-NBR                      04120003
W33304               AND A.LOC_INV_STAT_CDE = 'IN'                      04121001
                     AND ACTL_FIN_BK_DTE = '9999-09-09'                 04130000
                   WITH UR                                              04140000
               END-EXEC                                                 04150000
                                                                        04160000
               EVALUATE TRUE                                            04170000
               WHEN SQLCODE = 0                                         04180000
                   CONTINUE                                             04190000
               WHEN SQLCODE = +100                                      04191013
                   DISPLAY '** PARAGRAPH - 8300-READ-TINVPAR **'        04192013
                   DISPLAY '** ERROR - STORE NOT IN AN INVENTORY'       04193013
                           ' STATUS **'                                 04194013
                   DISPLAY ' STORE  :     ' IN018-STORE-NUMBER          04195013
                   PERFORM 4100-BUILD-ERROR-EXTRACT                     04195115
      ***          PERFORM 9900-SQL-ERROR                               04196023
               WHEN SQLCODE = -913                                      04200000
               WHEN SQLCODE = -904                                      04210000
               WHEN SQLCODE = -911                                      04220000
                   DISPLAY '** PARAGRAPH - 8300-READ-TINVPAR **'        04221013
                   DISPLAY '** ERROR - RESOURCE NOT AVAILABLE   '       04222013
                           ' STATUS **'                                 04223013
                   DISPLAY ' STORE  :     ' IN018-STORE-NUMBER          04224013
                   PERFORM 9900-SQL-ERROR                               04225013
               WHEN SQLWARN NOT = SPACE                                 04290000
               WHEN SQLCODE NOT = ZERO                                  04300000
                   DISPLAY '** PARAGRAPH - 8300-READ-TINVPAR **'        04310000
                   DISPLAY ' STORE  :     ' IN018-STORE-NUMBER          04320000
                   PERFORM 9900-SQL-ERROR                               04330000
               END-EVALUATE                                             04340000
           END-PERFORM.                                                 04350000
                                                                        04360000
           IF  PA-RETRY-COUNT > PC-MAX-RETRIES                          04370000
               DISPLAY '** MAX RETRIES - 8300-READ-TINVPAR **'          04380000
               DISPLAY ' STORE  :     ' IN018-STORE-NUMBER              04390000
               PERFORM 9900-SQL-ERROR                                   04400000
           END-IF.                                                      04410000
                                                                        04420000
      ***************************************************************** 04430000
      * 8700-UPDATE-TINVPAR.                                            04440000
      *    UPDATE THE INVENTORY PARAMETER DATABASE FOR EACH STORE       04450000
      * FOR TRANSFER SHEETS IF WE HAVE WRITTEN TRANSFER SHEETS TO       04460000
      * TINENTY.....                                                    04470000
      ***************************************************************** 04480000
       8700-UPDATE-TINVPAR.                                             04490000
           MOVE 'Y' TO INVPAR-SHEET-INV-REQR-IND.                       04500000
                                                                        04510000
           PERFORM                                                      04520000
               WITH TEST AFTER                                          04530000
               VARYING PA-RETRY-COUNT                                   04540000
               FROM 1 BY 1                                              04550000
               UNTIL (PA-RETRY-COUNT > PC-MAX-RETRIES                   04560000
                   OR SQLCODE = 0 OR +100)                              04570000
                                                                        04580000
               EXEC SQL                                                 04590000
                   UPDATE TINVPAR                                       04600000
                   SET SHEET_INV_REQR_IND                               04610000
                       = :INVPAR-SHEET-INV-REQR-IND                     04620000
                   WHERE LOC_NBR = :INVPAR-LOC-NBR                      04630000
                     AND INV_ID = :INVPAR-INV-ID                        04640000
                     AND ACTL_FIN_BK_DTE = '9999-09-09'                 04650000
               END-EXEC                                                 04660000
               EVALUATE TRUE                                            04670000
                   WHEN SQLCODE = -904                                  04680000
                   WHEN SQLCODE = -914                                  04690000
                   WHEN SQLCODE = 0                                     04700000
                   WHEN SQLCODE = +100                                  04710000
                       CONTINUE                                         04720000
                   WHEN SQLWARN NOT = SPACES                            04730000
                   WHEN SQLCODE NOT = ZERO                              04740000
                       DISPLAY                                          04750000
                           '***PARAGRAPH - 8700-UPDATE-TINVPAR ***'     04760000
                       DISPLAY '*** MAX RETRIES - 8700-UPDATE',         04770000
                           '-TINVPAR ***'                               04780000
                       DISPLAY ' SKU  : ' IN018-SKU                     04790000
                               ' STORE: ' IN018-STORE-NUMBER            04800000
                               ' INVID: ' INVPAR-INV-ID                 04810000
                       PERFORM 9900-SQL-ERROR                           04820000
               END-EVALUATE                                             04830000
           END-PERFORM.                                                 04840000
           IF PA-RETRY-COUNT > PC-MAX-RETRIES                           04850000
               DISPLAY '*** MAX RETRIES - 8700-UPDATE-TINVPAR ***'      04860000
               DISPLAY ' SKU  : ' IN018-SKU                             04870000
                       ' STORE: ' IN018-STORE-NUMBER                    04880000
                       ' INVID: ' INVPAR-INV-ID                         04890000
               PERFORM 9900-SQL-ERROR                                   04900000
           END-IF.                                                      04910000
                                                                        04920000
      ******************************************************************04921027
      * DB2 COMMIT                                                      04922027
      ******************************************************************04923027
       8900-COMMIT-DB2.                                                 04924027
           MOVE  PA-RECORDS-ACCPTD-STR TO PV-REC-INSERTED-DISP          04924128
                                                                        04925027
           EXEC SQL                                                     04926027
              COMMIT                                                    04927027
           END-EXEC.                                                    04928027
                                                                        04929027
           EVALUATE SQLCODE                                             04929127
               WHEN ZERO                                                04929227
                   DISPLAY 'COMMIT  ' PV-STR-NBR-HOLD                   04929328
                   DISPLAY 'RECORDS ' PV-REC-INSERTED-DISP              04929428
               WHEN OTHER                                               04929528
                   DISPLAY '** PARAGRAPH - 8900-COMMIT-DB2   **'        04929628
                   DISPLAY '** ERROR - RESOURCE NOT AVAILABLE   '       04929728
                   DISPLAY ' STORE  :     ' PV-STR-NBR-HOLD             04929828
                   DISPLAY 'INKUP306 ABENDING ON COMMIT'                04929928
                   PERFORM 9900-SQL-ERROR                               04930028
           END-EVALUATE.                                                04930427
      /*****************************************************************04931000
      * 9000-END-OF-JOB-TASKS.                                          04940000
      ******************************************************************04950000
       9000-END-OF-JOB-TASKS.                                           04960000
                                                                        04970000
           IF PA-RECORDS-ACCEPTED > ZEROES                              04980000
               PERFORM 8700-UPDATE-TINVPAR                              04990000
           END-IF.                                                      05000000
           DISPLAY '***********************'.                           05010000
           DISPLAY '* ' PC-PROGRAM-NAME ' SUMMARY    *'.                05020000
           DISPLAY '***********************'.                           05030000
           DISPLAY ' '.                                                 05040000
           DISPLAY ' RECORDS READ:     ' PA-RECORDS-READ.               05050000
           DISPLAY ' NOT VALID SKU:    ' PA-NOT-VALID-SKU.              05060000
           DISPLAY ' NO UNIT COUNT:    ' PA-NO-UNIT-COUNT.              05070000
           DISPLAY ' NO PRICE COUNT:   ' PA-NO-PRICE-COUNT.             05080000
           DISPLAY ' RECORDS ACCEPTED: ' PA-RECORDS-ACCEPTED.           05090000
           DISPLAY ' '.                                                 05100000
           DISPLAY '***********************'.                           05110000
           DISPLAY '*** TABLE COMMIT STATISTICS      ***'.              05120000
           DISPLAY '                                             '.     05130000
           DISPLAY 'INPUT EXTRACT RECORDS READ ' PA-EXTRACT-RECS-READ.  05140000
           DISPLAY '  ROWS INSERTED  ', PA-INSERT-COUNT.                05150000
           DISPLAY '  DUPLICATE(S)   ', PA-DUPLICATE-REC.               05160000
           DISPLAY '***********************'.                           05170000
                                                                        05180000
RONNA      CLOSE PACK-HOLD-FILE                                         05190004
                 CC-CONTROL-FILE                                        05200000
                 ERROR-EXTRACT-FILE.                                    05210000
                                                                        05220000
      ******************************************************************05230000
      * 9900-SQL-ERROR.                                                 05240000
      ******************************************************************05250000
       9900-SQL-ERROR.                                                  05260000
                                                                        05270000
           SET ABEND-4013            TO TRUE.                           05280000
           DISPLAY '**  ABEND     **'.                                  05290000
           DISPLAY '**  PROGRAM   **  =  ' PC-PROGRAM-NAME              05300000
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED            05310000
                                                                        05320000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         05330000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        05340000
                                                                        05350000
           COPY DPPD004.                                                05360000
                                                                        05370000
           PERFORM 9990-ABEND.                                          05380000
                                                                        05390000
       9990-ABEND.                                                      05400000
                                                                        05410000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              05420000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           05430000
                                                                        05440000
      ******************************************************************05450000
      *  PRICING MODULE.                                                05460000
      ******************************************************************05470000
           COPY INPD400.                                                05480000
                                                                        05490000
