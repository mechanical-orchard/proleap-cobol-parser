00001 *                                                                 02/05/92
00002 *                                                                 MBKCS107
00003 *                                                                    LV174
00004  IDENTIFICATION DIVISION.                                         MBKCS107
00005                                                                   MBKCS107
00006  PROGRAM-ID.    MBKCS107.                                            CL110
00007  AUTHOR.        BOB LOOS - STRATAGEM.                                CL*28
00008  INSTALLATION.  KOHLS DEPARTMENT STORES.                          MBKCS107
00009  DATE-WRITTEN.  07-19-91.                                            CL102
00010  DATE-COMPILED.                                                   MBKCS107
00011                                                                   MBKCS107
00012 ***************************************************************** MBKCS107
00013 *    M107 - MERCHANDISE AREA SUB-CLASS SELECTION                     CL110
00014 *                                                                    CL**3
00015 *    THIS PROGRAM ALLOWS THE USER TO CHOOSE THE TYPE OF              CL*23
00016 *    CRITERIA THAT HE OR SHE WANTS TO USE TO GENERATE THE MERCH-     CL*30
00017 *    ANDISE AREA SUB-CLASS LIST. THE CRITERIA IS SELECTED BY         CL102
00018 *    ENTERING THE APPROPRIATE VALUES ON THE FILTER                   CL102
00019 ***************************************************************** MBKCS107
262978* CHANGED 10/21/2021    BY JIM HUNTER      WR: CHG0262978                 
262978* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
262978* MAKING THE CALL DYNAMIC VS STATIC.                                      
262978******************************************************************        
00020                                                                   MBKCS107
00021 *                                                                 MBKCS107
00022 *                                                                 MBKCS107
00023 *                                                                 MBKCS107
00024  ENVIRONMENT DIVISION.                                            MBKCS107
00025 *                                                                 MBKCS107
00026 *                                                                 MBKCS107
00027 *                                                                 MBKCS107
00028  DATA DIVISION.                                                   MBKCS107
00029 *                                                                 MBKCS107
00030 *                                                                 MBKCS107
00031  WORKING-STORAGE SECTION.                                         MBKCS107
00032                                                                      CL**5
00033  01  PC-PROGRAM-CONSTANTS.                                           CL**5
00034      05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.MBKCS107
00035      05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072  MBKCS107
00036                                                      COMP SYNC.   MBKCS107
00037      05  PC-CURRENT-MAP-NAME            PIC X(08)    VALUE        MBKCS107
00038      'MB107A  '.                                                     CL110
00039      05  PC-CURRENT-MAPSET-NAME         PIC X(08)    VALUE        MBKCS107
00040      'MBKM107 '.                                                     CL110
00041      05  PC-CURRENT-PROGRAM-NAME        PIC X(08)    VALUE        MBKCS107
00042      'MBKCS107'.                                                     CL110
00043      05  PC-ERASE-EOF-COMP              PIC S9(04)   VALUE +128   MBKCS107
00044                                                      COMP SYNC.   MBKCS107
00045      05  FILLER REDEFINES PC-ERASE-EOF-COMP.                      MBKCS107
00046          10  FILLER                     PIC X(01).                MBKCS107
00047          10  PC-ERASE-EOF               PIC X(01).                MBKCS107
00048      05  PC-MBLSUBCL                    PIC X(08)    VALUE           CL102
00049      'MBLSUBCL'.                                                     CL102
00050      05  PC-ZERO-VALUE                  PIC X(01)    VALUE '0'.      CL*66
00051      05  PC-OPER-COMPANY                PIC X(02)    VALUE '03'.     CL*66
00052      05  PC-TSQ-LENGTH                  PIC S9(04)   VALUE 2007      CL167
00053                                                      COMP SYNC.      CL167
00054      05  PC-MAX-RETRIES                 PIC 9(04)    VALUE 3.        CL156
00055      05  PC-TSYMSG-NUMBERS.                                          CL107
00056          10 PC-TSYMSG-00005             PIC 9(05)    VALUE 00005.    CL107
00057          10 PC-TSYMSG-00007             PIC 9(05)    VALUE 00007.    CL142
00058          10 PC-TSYMSG-00010             PIC 9(05)    VALUE 00010.    CL123
00059          10 PC-TSYMSG-00032             PIC 9(05)    VALUE 00032.    CL132
00060          10 PC-TSYMSG-00045             PIC 9(05)    VALUE 00045.    CL107
00061          10 PC-TSYMSG-00078             PIC 9(05)    VALUE 00078.    CL129
00062          10 PC-TSYMSG-00079             PIC 9(05)    VALUE 00079.    CL129
00063          10 PC-TSYMSG-00082             PIC 9(05)    VALUE 00082.    CL126
00064          10 PC-TSYMSG-00137             PIC 9(05)    VALUE 00137.    CL155
00065                                                                   MBKCS107
00066  01  PM-PROGRAM-MESSAGES.                                         MBKCS107
00067      05  PM-PROGRAM-MESSAGE-1           PIC X(78)    VALUE           CL**3
00068      'INVALID NEXT APPLICATION ACTIVITY RECEIVED'.                   CL**3
00069                                                                      CL**5
00070  01  PS-PROGRAM-SWITCHES.                                         MBKCS107
00071      05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.   MBKCS107
00072          88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.   MBKCS107
00073          88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.   MBKCS107
00074          88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.   MBKCS107
00075          88  PS-SEND-DATA-ONLY                       VALUE '4'.   MBKCS107
00076          88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.   MBKCS107
00077          88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.   MBKCS107
00078      05  PS-ROW-FOUND-SW                PIC X(01)    VALUE 'N'.      CL154
00079          88  PS-ROW-FOUND                            VALUE 'Y'.      CL154
00080          88  PS-ROW-NOT-FOUND                        VALUE 'N'.      CL154
00081      05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.   MBKCS107
00082          88  PS-ERROR                                VALUE 'Y'.   MBKCS107
00083          88  PS-NO-ERROR                             VALUE 'N'.   MBKCS107
00084      05  PS-SPECIF-TRAN-SW              PIC X(01)    VALUE 'N'.      CL109
00085          88  PS-SPECIF-TRAN                          VALUE 'Y'.      CL109
00086          88  PS-NO-SPECIF-TRAN                       VALUE 'N'.      CL109
00087      05  PS-SPECIF-DEPT-SW              PIC X(01)    VALUE 'N'.      CL109
00088          88  PS-SPECIF-DPT                           VALUE 'Y'.      CL111
00089          88  PS-NO-SPECIF-DPT                        VALUE 'N'.      CL112
00090      05  PS-SPECIF-MCL-SW               PIC X(01)    VALUE 'N'.      CL109
00091          88  PS-SPECIF-MCL                           VALUE 'Y'.      CL109
00092          88  PS-NO-SPECIF-MCL                        VALUE 'N'.      CL109
00093      05  PS-SPECIF-SCL-SW               PIC X(01)    VALUE 'N'.      CL109
00094          88  PS-SPECIF-SCL                           VALUE 'Y'.      CL109
00095          88  PS-NO-SPECIF-SCL                        VALUE 'N'.      CL109
00096                                                                   MBKCS107
00097  01  PV-PROGRAM-VARIABLES.                                        MBKCS107
00098      05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0     MBKCS107
00099                                                      COMP SYNC.   MBKCS107
00100      05  PV-NAV-MNEMONIC                PIC X(08)    VALUE SPACES.   CL**5
00101      05  PV-RETRY-COUNTER               PIC 9(04)    VALUE 0.        CL155
00102      05  PV-ITEM-NBR                    PIC S9(04)   VALUE 0         CL170
00103                                                      COMP SYNC.      CL170
00104                                                                      CL*88
00105  EJECT                                                            MBKCS107
00106 ***************************************************************** MBKCS107
00107 *    STANDARD COMMAREA.                                           MBKCS107
00108 ***************************************************************** MBKCS107
00109                                                                   MBKCS107
00110      COPY DPWS020.                                                   CL152
00111      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                MBKCS107
00112                                                                   MBKCS107
00113 ***************************************************************** MBKCS107
00114 *    INTER-APPLICATION COMMAREA.                                  MBKCS107
00115 ***************************************************************** MBKCS107
00116                                                                   MBKCS107
00117          10  INTER-APPL-COMM-AREA       PIC X(200).               MBKCS107
00118                                                                      CL*32
00119      COPY MBWS005.                                                   CL174
00120                                                                   MBKCS107
00121 ***************************************************************** MBKCS107
00122 *    SPECIFIC COMMMAREA FOR MBKCS006.                                CL102
00123 ***************************************************************** MBKCS107
00124                                                                   MBKCS107
00125      COPY MBWS006.                                                   CL174
00126                                                                   MBKCS107
00127 *****************************************************************    CL141
00128 *    NUMERIC EDIT COPYBOOK                                           CL141
00129 *****************************************************************    CL141
00130                                                                      CL141
00131      COPY DPWS010I.                                                  CL141
00132                                                                      CL141
00133 ***************************************************************** MBKCS107
00134 *    ABEND PROCESSING COPYBOOK.                                   MBKCS107
00135 ***************************************************************** MBKCS107
00136                                                                   MBKCS107
00137      COPY DPWS013.                                                   CL174
00138                                                                   MBKCS107
00139 ***************************************************************** MBKCS107
00140 *    ATTRIBUTE SETTINGS COPYBOOK.                                 MBKCS107
00141 ***************************************************************** MBKCS107
00142                                                                   MBKCS107
00143      COPY DPWS015.                                                MBKCS107
00144                                                                   MBKCS107
00145 ***************************************************************** MBKCS107
00146 *    CURRENT DATE AND TIME COPYBOOK.                              MBKCS107
00147 ***************************************************************** MBKCS107
00148                                                                   MBKCS107
00149      COPY DPWS017.                                                   CL*14
00150                                                                   MBKCS107
00151 *****************************************************************    CL166
00152 *    TEMPORARY STORAGE QUEUE LAYOUT.                                 CL166
00153 *****************************************************************    CL166
00154                                                                      CL166
00155  01 TSQ-TEMP-STG-QUEUE.                                              CL166
00156      COPY MBWS003.                                                   CL174
00157                                                                      CL166
00158 ***************************************************************** MBKCS107
00159 *    TOGGLE FUNCTION KEY LINES COPYBOOK.                          MBKCS107
00160 ***************************************************************** MBKCS107
00161                                                                   MBKCS107
00162      COPY DPWS009.                                                   CL*14
                                                                        MBKCS101
      *****************************************************************         
      *    SYSTEM MESSAGE FIELDS                                                
      *****************************************************************         
                                                                                
           COPY DPWS006.                                                        
                                                                                
00164 ***************************************************************** MBKCS107
00165 *    FUNCTION KEYS COPYBOOK.                                      MBKCS107
00166 ***************************************************************** MBKCS107
00167                                                                   MBKCS107
00168      COPY DPWS016.                                                   CL*14
00169                                                                   MBKCS107
00170 ***************************************************************** MBKCS107
00171 *    DPKUT045 COPYBOOK.                                           MBKCS107
00172 ***************************************************************** MBKCS107
00173                                                                   MBKCS107
00174      COPY DPWS045.                                                   CL*14
00175                                                                      CL*75
00176      COPY DPRD507I.                                                  CL*76
00177  EJECT                                                               CL*50
00178                                                                   MBKCS107
00179 ***************************************************************** MBKCS107
00180 *    STANDARD ATTENTION IDENTIFIER LIST                           MBKCS107
00181 ***************************************************************** MBKCS107
00182                                                                   MBKCS107
00183      COPY DFHAID.                                                 MBKCS107
00184                                                                   MBKCS107
00185 ***************************************************************** MBKCS107
00186 *    STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST        MBKCS107
00187 ***************************************************************** MBKCS107
00188                                                                   MBKCS107
00189      COPY DFHBMSCA.                                               MBKCS107
00190                                                                   MBKCS107
00191 *****************************************************************    CL146
00192 *    DB2 COMMUNICATION AREA                                          CL146
00193 *****************************************************************    CL146
00194      EXEC SQL                                                        CL146
00195          INCLUDE SQLCA                                               CL146
00196      END-EXEC.                                                       CL146
00197                                                                      CL146
00198 *****************************************************************    CL146
00199 *    MERCHANDISE AREA TABLE                                          CL146
00200 *****************************************************************    CL146
00201      EXEC SQL                                                        CL146
00202          INCLUDE TMDSEAR                                             CL146
00203      END-EXEC.                                                       CL146
00204                                                                      CL146
00205 ***************************************************************** MBKCS107
00206 *    MAP LAYOUT                                                   MBKCS107
00207 ***************************************************************** MBKCS107
00208                                                                   MBKCS107
00209      COPY MBKM107.                                                   CL110
00210                                                                      CL**6
00211  01  MD-MAP-DATA-AREA REDEFINES MB107AO.                             CL110
00212      05  FILLER                         PIC X(425).                  CL143
00213  EJECT                                                            MBKCS107
00214  LINKAGE SECTION.                                                 MBKCS107
00215                                                                   MBKCS107
00216  01  DFHCOMMAREA.                                                    CL*27
00217      05  FILLER                         OCCURS  1 TO 4072 TIMES      CL*27
00218                                         DEPENDING ON EIBCALEN.       CL*27
00219          10  FILLER                     PIC X.                       CL*27
00220                                                                      CL*27
00221 *                                                                 MBKCS107
00222 *                                                                 MBKCS107
00223 *                                                                 MBKCS107
00224  PROCEDURE DIVISION.                                              MBKCS107
00225                                                                   MBKCS107
00226  0000-MAIN-MODULE.                                                   CL**8
00227                                                                   MBKCS107
00228 ******************************************************************MBKCS107
00229 *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.           MBKCS107
00230 ******************************************************************MBKCS107
00231                                                                   MBKCS107
00232      PERFORM DP015-0000-CHECK-COMMAREA.                           MBKCS107
00233                                                                   MBKCS107
00234 ******************************************************************   CL**8
00235 *    DP020-SRC-RETURN-W-TRAN-ID WILL BE TRUE IF THIS PROGRAM WAS     CL**8
00236 *    SENT A STANDARD COMMAREA. IF NOT SET TRUE (MEANING OTHER        CL**8
00237 *    THAN A STANDARD COMMAREA WAS SENT TO THIS PROGRAM, THIS         CL**8
00238 *    PROGRAM WILL RETURN TO CISS.                                    CL**8
00239 ******************************************************************   CL**8
00240      IF DP020-SRC-RETURN-W-TRAN-ID                                MBKCS107
00241          PERFORM 1000-CONTROL-PROCESSING                             CL*11
00242      END-IF.                                                      MBKCS107
00243                                                                   MBKCS107
00244      PERFORM DP003-0000-RETURN-TO-CICS.                           MBKCS107
00245  EJECT                                                            MBKCS107
00246  1000-CONTROL-PROCESSING.                                            CL**8
00247                                                                   MBKCS107
00248 ******************************************************************MBKCS107
00249 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT    MBKCS107
00250 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                    MBKCS107
00251 *                                                                 MBKCS107
00252 *    PERFORMED BY 0000-MAIN-MODULE.                                  CL**8
00253 ******************************************************************MBKCS107
00254                                                                      CL**3
00255      EVALUATE TRUE                                                MBKCS107
00256                                                                   MBKCS107
00257          WHEN DP020-NEXT-ACT-INITIAL                              MBKCS107
00258              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                    CL**3
00259                                                                   MBKCS107
00260              PERFORM 1100-INITIALIZE-COMMAREA                        CL**8
00261              PERFORM 5100-SAVE-INITIAL-INTERAPPL                     CL*41
00262              PERFORM 2000-BUILD-INITIAL-PANEL                        CL*37
00263                                                                   MBKCS107
00264          WHEN DP020-NEXT-ACT-READ-MAP                             MBKCS107
00265              PERFORM 3000-PROCESS-PANEL                              CL*10
00266                                                                      CL**3
00267          WHEN DP020-NEXT-ACT-RETURN                                  CL**3
00268              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                    CL**3
00269              PERFORM 5100-UPDATE-INTER-APPL                          CL*34
00270              PERFORM 4000-DISPLAY-PANEL                              CL*20
00271                                                                      CL**3
00272          WHEN DP020-NEXT-ACT-ERROR                                MBKCS107
00273                                                                      CL*21
00274              IF DP020-NAV-MNEMONIC = SPACES                          CL*15
00275                  MOVE SPACES              TO ANAVCMDO                CL*34
00276                  MOVE DP015-GREEN         TO ANAVCMDC                CL*34
00277                  MOVE DP015-UNDERLINE     TO ANAVCMDH                CL*34
00278              ELSE                                                    CL*15
00279                  MOVE DP015-RED           TO ANAVCMDC                CL*34
00280                  MOVE DP015-REVERSE       TO ANAVCMDH                CL*34
00281              END-IF                                                  CL*16
00282                                                                      CL**4
00283              SET PS-ERROR                 TO TRUE                    CL*38
00284              SET PS-SEND-DATA-ONLY-FRSET  TO TRUE                    CL*38
00285                                                                      CL*38
00286              MOVE -1                      TO ANAVCMDL                CL**4
00287                                                                      CL115
00288              MOVE LOW-VALUES TO DP020-INTER-APPL-DATA-AREA           CL115
00289              PERFORM 3300-EDIT-DATA-IN-COMMAREA                      CL115
00290                                                                      CL**4
00291              PERFORM 4000-DISPLAY-PANEL                              CL*20
00292                                                                      CL**4
00293          WHEN OTHER                                               MBKCS107
00294              SET DP013-LOGIC-ABEND        TO TRUE                    CL*34
00295              SET DP013-NO-ROLLBACK        TO TRUE                    CL*34
00296                                                                   MBKCS107
00297              MOVE 'B100-CONTROL-PROCESSING' TO                    MBKCS107
00298                   DP013-PARAGRAPH                                 MBKCS107
00299                                                                      CL**4
00300              MOVE PM-PROGRAM-MESSAGE-1      TO                       CL**3
00301                   DP013-MESSAGE-TEXT(1)                           MBKCS107
00302                                                                   MBKCS107
00303              PERFORM DP013-0000-PROCESS-ABEND                     MBKCS107
00304                                                                   MBKCS107
00305      END-EVALUATE.                                                MBKCS107
00306  EJECT                                                            MBKCS107
00307  1100-INITIALIZE-COMMAREA.                                           CL*20
00308                                                                      CL*20
00309 ******************************************************************   CL*20
00310 *    INITIALIZE THE INTER APPLICATION COMMAREA AND THE               CL*20
00311 *    APPLICATION SPECIFIC COMMAREA.                                  CL*20
00312 *                                                                    CL*20
00313 *    PERFORMED BY 1000-CONTROL-PROCESSING.                           CL*20
00314 *    PERFORMED BY D200-CHECK-FUNCTION-KEY.                           CL*20
00315 ******************************************************************   CL*20
00316                                                                      CL*20
00317      MOVE LOW-VALUES            TO DP020-INTER-APPL-DATA-AREA.       CL*57
00318      MOVE PC-OPER-COMPANY       TO MB006-CO-NUMBER-X.                CL104
00319      MOVE SPACES                TO MB006-DEPT-NUMBER-X               CL104
00320                                    MB006-MCLASS-NUMBER-X             CL104
00321                                    MB006-SCLASS-NUMBER-X.            CL143
00322                                                                      CL*20
00323  EJECT                                                               CL*20
00324                                                                      CL137
00325  2000-BUILD-INITIAL-PANEL.                                           CL*37
00326                                                                   MBKCS107
00327 ******************************************************************MBKCS107
00328 *    PERFORMED ON THE FIRST PASS IF THIS PROGRAM                     CL*38
00329 *                                                                 MBKCS107
00330 *    PERFORMED BY B100-CONTROL-PROCESSING.                        MBKCS107
00331 ******************************************************************MBKCS107
00332                                                                      CL*38
00333      MOVE 'N'             TO MB006-HISTORY-FLAG.                     CL137
00334                                                                      CL137
00335      PERFORM 4000-DISPLAY-PANEL.                                     CL*38
00336                                                                      CL137
00337  EJECT                                                               CL*37
00338  3000-PROCESS-PANEL.                                                 CL*37
00339                                                                      CL*37
00340 ******************************************************************   CL*37
00341 *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED     CL*37
00342 *    ON WHAT FUNCTION KEY WAS SELECTED.                              CL*37
00343 *                                                                    CL*37
00344 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL*37
00345 ******************************************************************   CL*37
00346                                                                      CL*37
00347      SET PS-SEND-DATA-ONLY-FRSET TO TRUE                          MBKCS107
00348                                                                   MBKCS107
00349      PERFORM DP007-0000-GET-PANEL.                                   CL**5
00350                                                                      CL**5
00351      IF (DP020-SRC-AID NOT = DP016-PF5)  AND                         CL**5
00352         (DP020-SRC-AID NOT = DP016-CLEAR)                            CL**5
00353          PERFORM 3200-MOVE-SCREEN-TO-COMMAREA                        CL*10
00354      END-IF.                                                         CL**5
00355                                                                      CL**5
00356      PERFORM 3100-CHECK-LOCAL-FKEYS.                                 CL*10
00357  EJECT                                                            MBKCS107
00358  3100-CHECK-LOCAL-FKEYS.                                             CL*20
00359                                                                      CL*10
00360 ******************************************************************   CL*10
00361 *    PERFORM THE APPROPRIATE PARAGRAPH BASED ON WHICH FUNCTION       CL*10
00362 *    KEY THE USER HIT.                                               CL*10
00363 *                                                                    CL*10
00364 *    PERFORMED BY 3000-PROCESS-PANEL.                                CL*10
00365 ******************************************************************   CL*10
00366                                                                      CL*10
00367      EVALUATE TRUE                                                   CL*10
00368                                                                      CL*10
00369          WHEN DP020-FK-GO-TO-TRAN(DP020-SRC-AID) OR                  CL*98
00370               DP020-FK-RETURN(DP020-SRC-AID)                         CL*99
00371               PERFORM 5000-SETUP-INTER-APPL                          CL*60
00372               PERFORM DP015-1000-START-NAVIGATOR                     CL*60
00373                                                                      CL*60
00374          WHEN  DP020-FK-EDIT-GOTO(DP020-SRC-AID)                     CL*98
00375                PERFORM 3300-EDIT-DATA-IN-COMMAREA                    CL*92
00376                IF PS-NO-ERROR                                        CL*92
00377                    IF ((DP020-SRC-AID = DP016-PF15) OR               CL153
00378                        (DP020-SRC-AID = DP016-PF14))                 CL153
00379                        PERFORM 3310-INQUIRY-UPDATE-EDITS             CL153
00380                    END-IF                                            CL153
00381                END-IF                                                CL153
00382                IF PS-NO-ERROR                                        CL153
00383                    PERFORM 5000-SETUP-INTER-APPL                     CL*92
00384                    PERFORM DP015-1000-START-NAVIGATOR                CL*92
00385                ELSE                                                  CL*92
00386                    PERFORM 4100-MOVE-COMMAREA-TO-SCREEN              CL100
00387                    PERFORM 4000-DISPLAY-PANEL                        CL*92
00388                END-IF                                                CL*92
00389                                                                      CL*32
00390          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                        CL*32
00391              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                    CL*35
00392              INITIALIZE ANAVKEYO ANAVCMDO                            CL122
00393              PERFORM 1100-INITIALIZE-COMMAREA                        CL*32
00394              MOVE SPACES TO DP020-NAV-KEY-DATA                       CL115
00395              PERFORM 2000-BUILD-INITIAL-PANEL                        CL*58
00396                                                                      CL*32
00397          WHEN DP020-SRC-AID = DP016-ENTER                            CL*10
00398              IF (DP020-NAV-MNEMONIC = SPACES)     OR                 CL*10
00399                 (DP020-NAV-MNEMONIC = LOW-VALUES)                    CL*10
00400                  PERFORM 3300-EDIT-DATA-IN-COMMAREA                  CL*81
00401                  PERFORM 4100-MOVE-COMMAREA-TO-SCREEN                CL*83
00402                  PERFORM 4000-DISPLAY-PANEL                          CL*52
00403              ELSE                                                    CL*10
00404                  MOVE LOW-VALUES TO DP020-INTER-APPL-DATA-AREA       CL165
00405                  PERFORM DP015-1000-START-NAVIGATOR                  CL*10
00406              END-IF                                                  CL*10
00407                                                                      CL*10
00408          WHEN DP020-SRC-AID = DP016-PA2                              CL*33
00409              SET PS-SEND-DATA-ONLY TO TRUE                           CL*33
00410              MOVE -1               TO ANAVCMDL                       CL*33
00411              PERFORM DP017-0000-GET-CURR-DATE-TIME                   CL*79
00412              PERFORM DP009-0000-LOAD-FK-LINES                        CL*79
00413              PERFORM DP008-0000-WRITE-PANEL                          CL*79
00414                                                                      CL*33
00415          WHEN DP020-SRC-AID = DP016-CLEAR                            CL*33
00416              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                    CL*35
00417              PERFORM 3300-EDIT-DATA-IN-COMMAREA                      CL*33
00418              PERFORM 4000-DISPLAY-PANEL                              CL*33
00419                                                                      CL*33
00420          WHEN OTHER                                                  CL*33
00421              PERFORM 3300-EDIT-DATA-IN-COMMAREA                      CL*37
00422              MOVE PC-TSYMSG-00045 TO DP020-MSG-NUMBER                CL107
00423              SET DP020-MSG-FATAL  TO TRUE                            CL107
00424              PERFORM 4000-DISPLAY-PANEL                              CL*37
00425                                                                      CL*10
00426      END-EVALUATE.                                                   CL*10
00427  EJECT                                                               CL*10
00428  3200-MOVE-SCREEN-TO-COMMAREA.                                       CL*20
00429                                                                   MBKCS107
00430 ******************************************************************MBKCS107
00431 *    MOVE THE FIELDS FROM THE MAP TO THE COMMAREA IF ANYTHING        CL*20
00432 *    WAS ENTERED IN THAT FIELD.                                      CL*20
00433 *                                                                    CL*20
00434 *    PERFORMED BY 3000-PROCESS-PANEL.                                CL*11
00435 ******************************************************************MBKCS107
00436                                                                   MBKCS107
00437      IF ADEPTL > 0                                                   CL*33
00438         MOVE ADEPTI                  TO MB006-DEPT-NUMBER-X          CL105
00439      ELSE                                                            CL*30
00440         IF ADEPTA = PC-ERASE-EOF                                     CL*30
00441            MOVE SPACES               TO MB006-DEPT-NUMBER-X          CL105
00442         END-IF                                                       CL*30
00443      END-IF.                                                         CL*33
00444                                                                      CL*33
00445      IF AMCLASSL > 0                                                 CL*33
00446         MOVE AMCLASSI                TO MB006-MCLASS-NUMBER-X        CL105
00447      ELSE                                                            CL*30
00448         IF AMCLASSA = PC-ERASE-EOF                                   CL*30
00449            MOVE SPACES               TO MB006-MCLASS-NUMBER-X        CL105
00450         END-IF                                                       CL*30
00451      END-IF.                                                         CL*33
00452                                                                      CL*33
00453      IF ASCLASSL > 0                                                 CL*33
00454         MOVE ASCLASSI                TO MB006-SCLASS-NUMBER-X        CL105
00455      ELSE                                                            CL*30
00456         IF ASCLASSA = PC-ERASE-EOF                                   CL*30
00457            MOVE SPACES               TO MB006-SCLASS-NUMBER-X        CL105
00458         END-IF                                                       CL*30
00459      END-IF.                                                         CL*33
00460                                                                      CL139
00461      IF AHISTRYL > 0                                                 CL137
00462         MOVE AHISTRYI                TO MB006-HISTORY-FLAG           CL138
00463      ELSE                                                            CL137
00464         IF AHISTRYA = PC-ERASE-EOF                                   CL137
00465            MOVE SPACES               TO MB006-HISTORY-FLAG           CL137
00466         END-IF                                                       CL137
00467      END-IF.                                                         CL137
00468                                                                      CL*33
00469  EJECT                                                            MBKCS107
00470                                                                      CL136
00471  3300-EDIT-DATA-IN-COMMAREA.                                         CL*10
00472                                                                      CL**5
00473 ******************************************************************   CL**5
00474 *    EDIT THE DEPARTMENT, MAJOR CLASS AND SUBCLASS.                  CL102
00475 *    IN ORDER TO BE VALID, ANY AND OR ALL OF THE ABOVE MUST          CL**3
00476 *    CONTAIN NUMERIC VALUES, AN '*', OR BE SPACES.                   CL*38
00477 *                                                                    CL*20
00478 *    PERFORMED BY 1000-CONTROL-PROCESSING.                           CL*10
00479 *    PERFORMED BY 3000-PROCESS-PANEL.                                CL*10
00480 *    PERFORMED BY 3100-CHECK-LOCAL-FKEYS.                            CL*10
00481 ******************************************************************   CL**5
00482                                                                      CL**5
00483      IF NOT DP020-NEXT-ACT-ERROR                                     CL*59
00484         SET PS-NO-ERROR TO TRUE                                      CL*62
00485         MOVE DP015-GREEN     TO ANAVKEYC                             CL121
00486                                 ANAVCMDC                             CL121
00487         MOVE DP015-UNDERLINE TO ANAVKEYH                             CL121
00488                                 ANAVCMDH                             CL121
00489      END-IF.                                                         CL*59
00490                                                                      CL160
00491      PERFORM 3350-RESET-ATTRIBUTES.                                  CL161
00492                                                                      CL161
00493      IF (MB006-HISTORY-FLAG NOT = 'N') AND                           CL160
00494         (MB006-HISTORY-FLAG NOT = 'Y')                               CL160
00495         MOVE DP015-RED          TO AHISTRYC                          CL160
00496         MOVE DP015-REVERSE      TO AHISTRYH                          CL160
00497         MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                  CL160
00498         MOVE -1                 TO AHISTRYL                          CL160
00499         SET DP020-MSG-FATAL                                          CL160
00500             PS-ERROR            TO TRUE                              CL160
00501      END-IF.                                                         CL160
00502                                                                      CL*26
00503                                                                      CL139
00504      PERFORM 3400-EDIT-DEPARTMENT.                                   CL140
00505                                                                      CL133
00506      PERFORM 3410-EDIT-MAJOR-CLASS.                                  CL140
00507                                                                      CL*26
00508      PERFORM 3420-EDIT-SUB-CLASS.                                    CL140
00509                                                                      CL*26
00510                                                                      CL**5
00511                                                                      CL*70
00512      IF (DP020-NAV-MNEMONIC = SPACES OR                              CL119
00513          DP020-NAV-MNEMONIC = LOW-VALUES) AND                        CL119
00514         (DP020-NAV-KEY-DATA > SPACES)                                CL119
00515          MOVE DP015-RED       TO ANAVKEYC                            CL116
00516          MOVE DP015-REVERSE   TO ANAVKEYH                            CL116
00517          IF PS-NO-ERROR                                              CL116
00518              MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER                CL116
00519              MOVE -1              TO ANAVKEYL                        CL116
00520              SET  DP020-MSG-FATAL                                    CL119
00521                   PS-ERROR         TO TRUE                           CL119
00522          END-IF                                                      CL117
00523      END-IF.                                                         CL116
00524  EJECT                                                               CL**5
00525                                                                      CL145
00526  3310-INQUIRY-UPDATE-EDITS.                                          CL145
00527                                                                      CL145
00528 ******************************************************************   CL145
00529 *    THIS PARAGRAPH PERFORMS VALIDATIONS FOR THE INQUIRY AND         CL145
00530 *    UPDATE FUNCTIONS.                                               CL145
00531 *    1) VERIFY THAT NO '*' ARE IN ANY FIELDS.                        CL145
00532 *    2) VERIFY THAT LEVEL SPECIFIED EXISTS.                          CL145
00533 ******************************************************************   CL145
00534                                                                      CL145
00535      IF MB006-HISTORY-FLAG NOT = 'N'                                 CL157
00536         MOVE DP015-RED          TO AHISTRYC                          CL145
00537         MOVE DP015-REVERSE      TO AHISTRYH                          CL145
00538         MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                  CL145
00539         MOVE -1                 TO AHISTRYL                          CL145
00540         SET DP020-MSG-FATAL                                          CL145
00541             PS-ERROR            TO TRUE                              CL145
00542      END-IF.                                                         CL145
00543                                                                      CL145
00544      PERFORM 3700-HILIGHT-ERRORS.                                    CL153
00545                                                                      CL145
00546      IF PS-NO-ERROR                                                  CL145
00547                                                                      CL145
00548          PERFORM 3340-VALIDATE-SUB-CLASS                             CL146
00549                                                                      CL148
00550          IF PS-ROW-NOT-FOUND                                         CL148
00551               MOVE DP015-RED          TO ADEPTC ASCLASSC AMCLASSC    CL159
00552               MOVE DP015-REVERSE      TO ADEPTH ASCLASSH AMCLASSH    CL159
00553               MOVE PC-TSYMSG-00079    TO DP020-MSG-NUMBER            CL159
00554               MOVE -1                 TO ADEPTL                      CL159
00555               SET DP020-MSG-FATAL                                    CL148
00556                   PS-ERROR            TO TRUE                        CL148
00557          ELSE                                                        CL162
00558              MOVE MDSEAR-GMA-NBR      TO MB006-GMA-NUMBER-X          CL162
00559              MOVE MDSEAR-DMA-NBR      TO MB006-DMA-NUMBER-X          CL162
00560          END-IF                                                      CL148
00561                                                                      CL145
00562      END-IF.                                                         CL145
00563                                                                      CL139
00564  3340-VALIDATE-SUB-CLASS.                                            CL146
00565 ******************************************************************   CL146
00566 *                                                                    CL146
00567 *                                                                    CL146
00568 *                                                                    CL146
00569 *                                                                    CL146
00570 ******************************************************************   CL146
00571                                                                      CL146
00572      PERFORM WITH TEST AFTER                                         CL146
00573          VARYING PV-RETRY-COUNTER                                    CL146
00574          FROM 1 BY 1                                                 CL146
00575          UNTIL (( PV-RETRY-COUNTER > PC-MAX-RETRIES)                 CL146
00576                  OR (SQLCODE = 0)                                    CL146
00577                  OR (SQLCODE = +100))                                CL146
00578                                                                      CL146
00579                                                                      CL146
00580          EXEC SQL                                                    CL146
00581              SELECT GMA_NBR,                                         CL146
00582                     DMA_NBR,                                         CL146
00583                     DEPT_NBR,                                        CL146
00584                     MAJ_CL_NBR,                                      CL146
00585                     SUB_CL_NBR                                       CL146
00586              INTO :MDSEAR-GMA-NBR,                                   CL146
00587                   :MDSEAR-DMA-NBR,                                   CL146
00588                   :MDSEAR-DEPT-NBR,                                  CL146
00589                   :MDSEAR-MAJ-CL-NBR,                                CL146
00590                   :MDSEAR-SUB-CL-NBR                                 CL146
00591              FROM TMDSEAR                                            CL149
00592              WHERE ((SUB_CL_NBR = :MB006-SCLASS-NUMBER-X) AND        CL151
00593                     (MAJ_CL_NBR = :MB006-MCLASS-NUMBER-X) AND        CL151
00594                     (DEPT_NBR   = :MB006-DEPT-NUMBER-X) AND          CL151
00595                     (END_DTE > DATE(CURRENT TIMESTAMP)))             CL151
00596          END-EXEC                                                    CL146
00597                                                                      CL146
00598          EVALUATE TRUE                                               CL146
00599              WHEN SQLCODE = ZERO                                     CL146
00600                  SET PS-ROW-FOUND TO TRUE                            CL146
00601              WHEN SQLCODE = -904                                     CL146
00602              WHEN SQLCODE = -913                                     CL146
00603                   CONTINUE                                           CL146
00604              WHEN SQLCODE = +100                                     CL146
00605                  SET PS-ROW-NOT-FOUND TO TRUE                        CL146
00606              WHEN SQLWARN0 NOT = SPACES                              CL146
00607              WHEN SQLCODE < 0                                        CL146
00608              WHEN SQLCODE > 0                                        CL146
00609                  MOVE '3340-VALIDATE-SUB-CLASS'                      CL147
00610                                        TO DP013-PARAGRAPH            CL147
00611                  MOVE 'SELECT SUBCLASS INFORMATION' TO               CL146
00612                                              DP013-MESSAGE-TEXT(1)   CL146
00613                  STRING 'DPT ' MB006-DEPT-NUMBER-X                   CL147
00614                         ' MCL ' MB006-MCLASS-NUMBER-X                CL147
00615                         ' SCL ' MB006-SCLASS-NUMBER-X                CL147
00616                         DELIMITED BY SIZE                            CL146
00617                         INTO DP013-MESSAGE-TEXT(2)                   CL146
00618                  MOVE SQLCA TO DP013-SQLCA                           CL146
00619                  MOVE 'TMDSEAR' TO DP013-DB2-TABLE-NAME(1)           CL146
00620                  SET DP013-DB2-ABEND TO TRUE                         CL146
00621                  SET DP013-NO-ROLLBACK TO TRUE                       CL146
00622                  SET DP013-XCTL-DISPLAY-RESTART TO TRUE              CL146
00623                  PERFORM DP013-0000-PROCESS-ABEND                    CL146
00624          END-EVALUATE                                                CL146
00625      END-PERFORM.                                                    CL146
00626                                                                      CL146
00627                                                                      CL146
00628      IF PV-RETRY-COUNTER > PC-MAX-RETRIES                            CL146
00629          MOVE '3340-VALIDATE-SUBCLASS' TO DP013-PARAGRAPH            CL147
00630          MOVE 'SELECT SUBCLASS INFORMATION' TO                       CL146
00631                                      DP013-MESSAGE-TEXT(1)           CL146
00632          MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                    CL146
00633                                      DP013-MESSAGE-TEXT(2)           CL146
00634          STRING 'DPT ' MB006-DEPT-NUMBER-X                           CL147
00635                 ' MCL ' MB006-MCLASS-NUMBER-X                        CL147
00636                 ' SCL ' MB006-SCLASS-NUMBER-X                        CL147
00637                 DELIMITED BY SIZE                                    CL146
00638                 INTO DP013-MESSAGE-TEXT(3)                           CL146
00639          MOVE SQLCA TO DP013-SQLCA                                   CL146
00640          MOVE 'TMDSEAR' TO DP013-DB2-TABLE-NAME(1)                   CL146
00641          SET DP013-DB2-ABEND                                         CL146
00642              DP013-NO-ROLLBACK                                       CL146
00643              DP013-XCTL-DISPLAY-RESTART TO TRUE                      CL146
00644          PERFORM DP013-0000-PROCESS-ABEND                            CL146
00645      END-IF.                                                         CL146
00646                                                                      CL146
00647                                                                      CL146
00648                                                                      CL146
00649  EJECT                                                               CL146
00650  3350-RESET-ATTRIBUTES.                                              CL142
00651 ******************************************************************   CL139
00652 *                                                                    CL139
00653 *                                                                    CL139
00654 ******************************************************************   CL139
00655      MOVE DP015-GREEN     TO ADEPTC                                  CL139
00656                              AMCLASSC                                CL139
00657                              ASCLASSC                                CL139
00658                              AHISTRYC.                               CL139
00659                                                                      CL139
00660      MOVE DP015-UNDERLINE TO ADEPTH                                  CL139
00661                              AMCLASSH                                CL139
00662                              ASCLASSH                                CL139
00663                              AHISTRYH.                               CL139
00664                                                                      CL133
00665  3400-EDIT-DEPARTMENT.                                               CL133
00666 ******************************************************************   CL135
00667 *                                                                    CL135
00668 *                                                                    CL135
00669 ******************************************************************   CL135
00670                                                                      CL133
00671      IF MB006-DEPT-NUMBER-X = SPACES                                 CL133
00672         MOVE DP015-RED               TO ADEPTC                       CL158
00673         MOVE DP015-REVERSE           TO ADEPTH                       CL158
00674         IF PS-NO-ERROR                                               CL158
00675            MOVE PC-TSYMSG-00007      TO DP020-MSG-NUMBER             CL158
00676            MOVE -1                   TO ADEPTL                       CL158
00677            SET DP020-MSG-FATAL       TO TRUE                         CL158
00678            SET PS-ERROR              TO TRUE                         CL158
00679         END-IF                                                       CL158
00680      ELSE                                                            CL133
00681         IF MB006-ALL-DEPARTMENTS                                     CL133
00682            MOVE '***'                 TO MB006-DEPT-NUMBER-X         CL133
00683         ELSE                                                         CL133
00684            MOVE MB006-DEPT-NUMBER-X   TO DP010I-UNEDITED-FIELD       CL134
00685            MOVE +3                    TO DP010I-MAXIMUM-DIGITS       CL134
00686            SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                   CL134
262978           CALL DP010I-NUMERIC-EDIT-ROUTINE                               
262978                USING DP010I-NUMERIC-EDIT-AREA                            
00688            IF DP010I-ERROR-DETECTED                                  CL134
00689               MOVE DP015-RED          TO ADEPTC                      CL133
00690               MOVE DP015-REVERSE      TO ADEPTH                      CL133
00691               IF PS-NO-ERROR                                         CL133
00692                  MOVE PC-TSYMSG-00005      TO DP020-MSG-NUMBER       CL133
00693                  MOVE -1                   TO ADEPTL                 CL133
00694                  SET DP020-MSG-FATAL       TO TRUE                   CL133
00695                  SET PS-ERROR              TO TRUE                   CL133
00696               END-IF                                                 CL133
00697            ELSE                                                      CL133
00698               MOVE DP010I-NUMERIC-FIELD    TO MB006-DEPT-NUMBER      CL142
00699               SET PS-SPECIF-DPT            TO TRUE                   CL133
00700               IF MB006-DEPT-NUMBER-X = '000'                         CL133
00701                   MOVE DP015-RED    TO ADEPTC                        CL133
00702                   MOVE DP015-REVERSE TO ADEPTH                       CL133
00703                   IF PS-NO-ERROR                                     CL133
00704                      MOVE PC-TSYMSG-00010 TO DP020-MSG-NUMBER        CL133
00705                      MOVE -1             TO ADEPTL                   CL133
00706                      SET DP020-MSG-FATAL TO TRUE                     CL133
00707                      SET PS-ERROR        TO TRUE                     CL133
00708                   END-IF                                             CL133
00709               END-IF                                                 CL133
00710            END-IF                                                    CL133
00711         END-IF                                                       CL133
00712      END-IF.                                                         CL133
00713      EJECT                                                           CL133
00714                                                                      CL133
00715  3410-EDIT-MAJOR-CLASS.                                              CL133
00716 ******************************************************************   CL135
00717 *                                                                    CL135
00718 *                                                                    CL135
00719 ******************************************************************   CL135
00720                                                                      CL133
00721      IF MB006-MCLASS-NUMBER-X = SPACES                               CL133
00722         MOVE DP015-RED           TO AMCLASSC                         CL173
00723         MOVE DP015-REVERSE       TO AMCLASSH                         CL173
00724         IF PS-NO-ERROR                                               CL173
00725            MOVE PC-TSYMSG-00007  TO DP020-MSG-NUMBER                 CL173
00726            MOVE -1               TO AMCLASSL                         CL173
00727            SET DP020-MSG-FATAL   TO TRUE                             CL173
00728            SET PS-ERROR          TO TRUE                             CL173
00729         END-IF                                                       CL173
00730      ELSE                                                            CL173
00731         IF MB006-ALL-MCLASSES                                        CL133
00732            MOVE '**'                       TO                        CL133
00733                 MB006-MCLASS-NUMBER-X                                CL133
00734         ELSE                                                         CL133
00735            MOVE MB006-MCLASS-NUMBER-X   TO DP010I-UNEDITED-FIELD     CL134
00736            MOVE +2                      TO DP010I-MAXIMUM-DIGITS     CL134
00737            SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                   CL134
262978           CALL DP010I-NUMERIC-EDIT-ROUTINE                               
262978                USING DP010I-NUMERIC-EDIT-AREA                            
00739            IF DP010I-ERROR-DETECTED                                  CL134
00740               MOVE DP015-RED               TO AMCLASSC               CL133
00741               MOVE DP015-REVERSE           TO AMCLASSH               CL133
00742               IF PS-NO-ERROR                                         CL133
00743                  MOVE PC-TSYMSG-00005      TO DP020-MSG-NUMBER       CL133
00744                  MOVE -1                   TO AMCLASSL               CL133
00745                  SET DP020-MSG-FATAL       TO TRUE                   CL133
00746                  SET PS-ERROR              TO TRUE                   CL133
00747               END-IF                                                 CL133
00748            ELSE                                                      CL133
00749               MOVE DP010I-NUMERIC-FIELD    TO                        CL134
00750                    MB006-MCLASS-NUMBER                               CL134
00751               SET PS-SPECIF-MCL            TO TRUE                   CL133
00752               IF MB006-MCLASS-NUMBER-X = '00'                        CL133
00753                   MOVE DP015-RED           TO AMCLASSC               CL133
00754                   MOVE DP015-REVERSE       TO AMCLASSH               CL133
00755                   IF PS-NO-ERROR                                     CL133
00756                      MOVE PC-TSYMSG-00010  TO DP020-MSG-NUMBER       CL133
00757                      MOVE -1               TO AMCLASSL               CL133
00758                      SET DP020-MSG-FATAL   TO TRUE                   CL133
00759                      SET PS-ERROR          TO TRUE                   CL133
00760                   END-IF                                             CL133
00761               ELSE                                                   CL133
00762                   IF MB006-ALL-DEPARTMENTS                           CL133
00763                       MOVE DP015-RED      TO ADEPTC                  CL133
00764                       MOVE DP015-REVERSE  TO ADEPTH                  CL133
00765                       IF PS-NO-ERROR                                 CL133
00766                          MOVE PC-TSYMSG-00005  TO                    CL133
00767                               DP020-MSG-NUMBER                       CL133
00768                          MOVE -1               TO ADEPTL             CL133
00769                          SET DP020-MSG-FATAL   TO TRUE               CL133
00770                          SET PS-ERROR          TO TRUE               CL133
00771                       END-IF                                         CL133
00772                   END-IF                                             CL133
00773               END-IF                                                 CL133
00774            END-IF                                                    CL133
00775         END-IF                                                       CL133
00776      END-IF.                                                         CL133
00777      EJECT                                                           CL133
00778                                                                      CL133
00779  3420-EDIT-SUB-CLASS.                                                CL133
00780 ******************************************************************   CL135
00781 *                                                                    CL135
00782 *                                                                    CL135
00783 ******************************************************************   CL135
00784                                                                      CL133
00785      IF MB006-SCLASS-NUMBER-X = SPACES                               CL133
00786         MOVE DP015-RED           TO ASCLASSC                         CL173
00787         MOVE DP015-REVERSE       TO ASCLASSH                         CL173
00788         IF PS-NO-ERROR                                               CL173
00789            MOVE PC-TSYMSG-00007  TO DP020-MSG-NUMBER                 CL173
00790            MOVE -1               TO ASCLASSL                         CL173
00791            SET DP020-MSG-FATAL   TO TRUE                             CL173
00792            SET PS-ERROR          TO TRUE                             CL173
00793         END-IF                                                       CL173
00794      ELSE                                                            CL133
00795         IF MB006-ALL-SCLASSES                                        CL133
00796            MOVE '***'                      TO                        CL133
00797                 MB006-SCLASS-NUMBER-X                                CL133
00798         ELSE                                                         CL133
00799            MOVE MB006-SCLASS-NUMBER-X   TO DP010I-UNEDITED-FIELD     CL134
00800            MOVE +3                      TO DP010I-MAXIMUM-DIGITS     CL134
00801            SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                   CL134
262978           CALL DP010I-NUMERIC-EDIT-ROUTINE                               
262978                USING DP010I-NUMERIC-EDIT-AREA                            
00803            IF DP010I-ERROR-DETECTED                                  CL134
00804               MOVE DP015-RED               TO ASCLASSC               CL133
00805               MOVE DP015-REVERSE           TO ASCLASSH               CL133
00806               IF PS-NO-ERROR                                         CL133
00807                  MOVE PC-TSYMSG-00005      TO DP020-MSG-NUMBER       CL133
00808                  MOVE -1                   TO ASCLASSL               CL133
00809                  SET DP020-MSG-FATAL       TO TRUE                   CL133
00810                  SET PS-ERROR              TO TRUE                   CL133
00811               END-IF                                                 CL133
00812            ELSE                                                      CL133
00813               MOVE DP010I-NUMERIC-FIELD    TO                        CL134
00814                    MB006-SCLASS-NUMBER-R                             CL142
00815               SET PS-SPECIF-SCL            TO TRUE                   CL133
00816               IF MB006-SCLASS-NUMBER-X = '000'                       CL133
00817                   MOVE DP015-RED           TO ASCLASSC               CL133
00818                   MOVE DP015-REVERSE       TO ASCLASSH               CL133
00819                   IF PS-NO-ERROR                                     CL133
00820                      MOVE PC-TSYMSG-00010  TO DP020-MSG-NUMBER       CL133
00821                      MOVE -1               TO ASCLASSL               CL133
00822                      SET DP020-MSG-FATAL   TO TRUE                   CL133
00823                      SET PS-ERROR          TO TRUE                   CL133
00824                   END-IF                                             CL133
00825               ELSE                                                   CL133
00826                   IF MB006-ALL-DEPARTMENTS                           CL133
00827                       MOVE DP015-RED      TO ADEPTC                  CL133
00828                       MOVE DP015-REVERSE  TO ADEPTH                  CL133
00829                       IF PS-NO-ERROR                                 CL133
00830                          MOVE PC-TSYMSG-00005  TO                    CL133
00831                               DP020-MSG-NUMBER                       CL133
00832                          MOVE -1               TO ADEPTL             CL133
00833                          SET DP020-MSG-FATAL   TO TRUE               CL133
00834                          SET PS-ERROR          TO TRUE               CL133
00835                       END-IF                                         CL133
00836                   ELSE                                               CL133
00837                       IF MB006-ALL-MCLASSES                          CL133
00838                           MOVE DP015-RED           TO AMCLASSC       CL133
00839                           MOVE DP015-REVERSE       TO AMCLASSH       CL133
00840                           IF PS-NO-ERROR                             CL133
00841                               MOVE PC-TSYMSG-00005  TO               CL133
00842                                    DP020-MSG-NUMBER                  CL133
00843                               MOVE -1               TO AMCLASSL      CL133
00844                               SET DP020-MSG-FATAL   TO TRUE          CL133
00845                               SET PS-ERROR          TO TRUE          CL133
00846                           END-IF                                     CL133
00847                       END-IF                                         CL133
00848                   END-IF                                             CL133
00849               END-IF                                                 CL133
00850            END-IF                                                    CL133
00851         END-IF                                                       CL133
00852      END-IF.                                                         CL133
00853      EJECT                                                           CL133
00854                                                                      CL133
00855                                                                      CL133
00856  3700-HILIGHT-ERRORS.                                                CL109
00857                                                                      CL115
00858 ******************************************************************   CL115
00859 *    EDIT EACH SCREEN FIELD FOR A SPECIFIC VALUE.                    CL117
00860 *    IF PF14 OR PF15, EACH FIELD MUST CONTAIN A VALUE.               CL117
00861 *                                                                    CL115
00862 *    PERFORMED BY 3300-EDIT-DATA-IN-COMMAREA                         CL115
00863 ******************************************************************   CL115
00864                                                                      CL115
00865                                                                      CL109
00866      IF MB006-ALL-SCLASSES                                           CL145
00867          MOVE DP015-RED          TO ASCLASSC                         CL145
00868          MOVE DP015-REVERSE      TO ASCLASSH                         CL145
00869          MOVE -1                 TO ASCLASSL                         CL145
00870          MOVE PC-TSYMSG-00137    TO DP020-MSG-NUMBER                 CL146
00871          SET DP020-MSG-FATAL                                         CL146
00872              PS-ERROR            TO TRUE                             CL146
00873      END-IF.                                                         CL146
00874                                                                      CL145
00875                                                                      CL109
00876      IF MB006-ALL-MCLASSES                                           CL146
00877          MOVE DP015-RED          TO AMCLASSC                         CL146
00878          MOVE DP015-REVERSE      TO AMCLASSH                         CL146
00879          MOVE -1                 TO AMCLASSL                         CL146
00880          MOVE PC-TSYMSG-00137    TO DP020-MSG-NUMBER                 CL146
00881          SET DP020-MSG-FATAL                                         CL146
00882              PS-ERROR            TO TRUE                             CL146
00883      END-IF.                                                         CL146
00884                                                                      CL146
00885      IF MB006-ALL-DEPARTMENTS                                        CL163
00886          MOVE DP015-RED          TO ADEPTC                           CL163
00887          MOVE DP015-REVERSE      TO ADEPTH                           CL163
00888          MOVE -1                 TO ADEPTL                           CL163
00889          MOVE PC-TSYMSG-00137    TO DP020-MSG-NUMBER                 CL146
00890          SET DP020-MSG-FATAL                                         CL146
00891              PS-ERROR            TO TRUE                             CL146
00892      END-IF.                                                         CL146
00893                                                                      CL109
00894  EJECT                                                               CL109
00895  4000-DISPLAY-PANEL.                                                 CL*20
00896                                                                      CL**3
00897 ******************************************************************   CL**3
00898 *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.          CL**3
00899 *                                                                    CL**3
00900 *    PERFORMED BY 1000-CONTROL-PROCESSING.                           CL*10
00901 *    PERFORMED BY 3000-PROCESS-PANEL.                                CL*10
00902 *    PERFORMED BY 3100-CHECK-LOCAL-FKEYS.                            CL*10
00903 ******************************************************************   CL**3
00904                                                                      CL**3
00905      PERFORM DP017-0000-GET-CURR-DATE-TIME.                          CL**3
00906      MOVE DP017-EDITED-TIME-HMS                                      CL171
00907                                TO  ATIMEO.                           CL171
00908                                                                      CL**3
00909      IF PS-SEND-MAP-N-DATA-FRSET                                     CL**3
00910          PERFORM DP002-0000-FORMAT-TITLE-LINES                       CL**3
00911          PERFORM 4100-MOVE-COMMAREA-TO-SCREEN                        CL*82
00912      END-IF.                                                         CL**3
00913                                                                      CL**4
00914      PERFORM DP005-0000-FORMAT-MSG-LINES.                            CL**4
00915                                                                      CL**3
00916      IF (DP020-SRC-AID = DP016-PA2) OR                               CL**4
00917         (PS-SEND-MAP-N-DATA-FRSET)                                   CL**4
00918          PERFORM DP009-0000-LOAD-FK-LINES                            CL**3
00919      END-IF.                                                         CL**3
00920                                                                      CL**3
00921      IF PS-NO-ERROR                                                  CL**3
00922          MOVE -1 TO ADEPTL                                           CL106
00923      END-IF                                                          CL**3
00924                                                                      CL**3
00925      PERFORM DP008-0000-WRITE-PANEL.                                 CL**3
00926                                                                      CL**3
00927  EJECT                                                               CL**3
00928  4100-MOVE-COMMAREA-TO-SCREEN.                                       CL*20
00929                                                                      CL**4
00930 ******************************************************************   CL**4
00931 *    MOVE THE FIELDS FROM THE COMMAREA TO THE SCREEN.                CL**4
00932 *                                                                    CL**4
00933 *    PERFORMED BY 4000-DISPLAY-PANEL.                                CL*20
00934 ******************************************************************   CL**4
00935                                                                      CL**4
00936      MOVE MB006-CO-NUMBER-X     TO ACOO.                             CL105
00937      MOVE MB006-DEPT-NUMBER-X   TO ADEPTO.                           CL105
00938      MOVE MB006-MCLASS-NUMBER-X TO AMCLASSO.                         CL105
00939      MOVE MB006-SCLASS-NUMBER-X TO ASCLASSO.                         CL105
00940      MOVE MB006-HISTORY-FLAG    TO AHISTRYO.                         CL137
00941                                                                      CL*87
00942      IF PS-NO-ERROR                                                  CL*95
00943          MOVE DP015-GREEN       TO ADEPTC                            CL102
00944                                    AMCLASSC                          CL*87
00945                                    ASCLASSC                          CL*95
00946                                                                      CL*87
00947          MOVE DP015-UNDERLINE   TO ADEPTH                            CL102
00948                                    AMCLASSH                          CL*87
00949                                    ASCLASSH                          CL*95
00950                                                                      CL*87
00951      END-IF.                                                         CL*95
00952  EJECT                                                               CL**4
00953                                                                      CL125
00954  5000-SETUP-INTER-APPL.                                              CL*31
00955                                                                      CL*31
00956 ******************************************************************   CL*31
00957 *    SETUP THE INTER APPLICATION COMMAREA WITH DATA FROM THE         CL*31
00958 *    SPECIFIC COMMAREA AND SET THE INTER APPLICATION FORMAT CODE.    CL*31
00959 *                                                                    CL*31
00960 *    PERFORMED BY 3100-CHECK-LOCAL-FKEYS                             CL*31
00961 ******************************************************************   CL*31
00962                                                                      CL*31
00963      INITIALIZE TSQ-TEMP-STG-QUEUE.                                  CL166
00964      MOVE EIBTRMID                  TO MB006-TERM-ID.                CL167
00965      MOVE EIBTASKN                  TO MB006-TASK-ID.                CL167
00966      SET MB003-IDX                  TO 1.                            CL166
00967      MOVE 100                       TO MB003-MAX-ITEMS.              CL166
00968      SET MB003-UPDATE               TO TRUE.                         CL166
00969      MOVE 1                         TO MB003-ITEM (1)                CL166
00970                                        MB003-SELECTED-ITEMS          CL166
00971                                        PV-ITEM-NBR.                  CL166
00972      MOVE MB006-CO-NUMBER-X         TO MB005-CO-NUMBER.              CL166
00973      MOVE MB006-GMA-NUMBER-X        TO MB005-GMA-NUMBER              CL166
00974                                        MB003-GMA-NUMBER(1).          CL166
00975      MOVE MB006-DMA-NUMBER-X        TO MB005-DMA-NUMBER              CL166
00976                                        MB003-DMA-NUMBER(1).          CL166
00977      MOVE MB006-DEPT-NUMBER-X       TO MB005-DEPT-NUMBER             CL166
00978                                        MB003-DEPT-NUMBER(1).         CL166
00979      MOVE MB006-MCLASS-NUMBER-X     TO MB005-MCLASS-NUMBER           CL166
00980                                        MB003-MCLASS-NUMBER(1).       CL166
00981      MOVE MB006-SCLASS-NUMBER-X     TO MB005-SCLASS-NUMBER           CL166
00982                                        MB003-SCLASS-NUMBER(1).       CL166
00983      MOVE 'SCL'                     TO MB003-MDSELV-NUMBER(1).       CL166
00984      MOVE MB006-HISTORY-FLAG        TO MB005-HISTORY-INDICATOR.      CL166
00985      PERFORM 5010-WRITE-TSQ.                                         CL166
00986      MOVE MB006-TEMP-QUEUE-NAME     TO MB005-LIST-QUEUE-NAME.        CL166
00987      MOVE PC-TSQ-LENGTH             TO MB005-LIST-QUEUE-LENGTH.      CL166
00988                                                                      CL*31
00989      SET DP020-MB-SUB-CLASS-SELECT TO TRUE.                          CL115
00990  EJECT                                                               CL*31
00991  5010-WRITE-TSQ.                                                     CL168
00992 ******************************************************************   CL168
00993 *                                                                    CL168
00994 *                                                                    CL168
00995 ******************************************************************   CL168
00996                                                                      CL168
00997      EXEC CICS                                                       CL168
00998          WRITEQ TS                                                   CL168
00999          QUEUE(MB006-TEMP-QUEUE-NAME)                                CL169
01000          FROM(TSQ-TEMP-STG-QUEUE)                                    CL168
01001          LENGTH(PC-TSQ-LENGTH)                                       CL168
01002          ITEM(PV-ITEM-NBR)                                           CL168
01003          RESP(PV-CICS-RESPONSE)                                      CL168
01004      END-EXEC.                                                       CL168
01005      IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                       CL168
01006         SET DP013-LOGIC-ABEND TO TRUE                                CL168
01007         SET DP013-NO-ROLLBACK TO TRUE                                CL168
01008         MOVE '5010-WRITE-TSQ' TO DP013-PARAGRAPH                     CL168
01009         MOVE 'RESPONSE CODE = ' TO DP013-MESSAGE-TEXT(1)             CL168
01010         MOVE PV-CICS-RESPONSE TO DP013-MESSAGE-TEXT(2)               CL168
01011         PERFORM DP013-0000-PROCESS-ABEND                             CL168
01012      END-IF.                                                         CL168
01013  5100-SAVE-INITIAL-INTERAPPL.                                        CL*41
01014                                                                      CL*41
01015 ******************************************************************   CL*41
01016 *    SAVE THE INTER-APPLICATION COMMAREA ON THE FIRST CYCLE          CL*41
01017 *    OF THIS TRANSACTION.                                            CL*41
01018 *                                                                    CL*41
01019 *    PERFORMED BY 1000-CONTROL-PROCESSING                            CL*41
01020 ******************************************************************   CL*41
01021                                                                      CL*41
01022      MOVE LOW-VALUES TO INTER-APPL-COMM-AREA.                        CL*41
01023                                                                      CL*41
01024  EJECT                                                               CL*41
01025                                                                      CL168
01026                                                                      CL168
01027                                                                      CL168
01028  5100-UPDATE-INTER-APPL.                                             CL*36
01029                                                                      CL*34
01030 ******************************************************************   CL*34
01031 *    WHEN RETURNING FROM ANOTHER TRANSACTION, SAVE THE FIELDS        CL*34
01032 *    PASSED IN THE INTER APPLICATION COMMAREA, ONLY IF THE           CL*34
01033 *    INTER APPLICATION FORMAT CODE IS VALID FOR THIS TRANSACTION.    CL*34
01034 *                                                                    CL*34
01035 *    PERFORMED BY 1000-CONTROL-PROCESSING                            CL*34
01036 ******************************************************************   CL*34
01037                                                                      CL*34
01038      IF DP020-MB-SUB-CLASS-LIST                                      CL118
01039         MOVE MB005-CO-NUMBER       TO MB006-CO-NUMBER-X              CL102
01040         MOVE MB005-DEPT-NUMBER     TO MB006-DEPT-NUMBER-X            CL102
01041         MOVE MB005-MCLASS-NUMBER   TO MB006-MCLASS-NUMBER-X          CL102
01042         MOVE MB005-SCLASS-NUMBER   TO MB006-SCLASS-NUMBER-X          CL102
01043      END-IF.                                                         CL*34
01044                                                                      CL*34
01045      INITIALIZE MB005-INTER-APPL-COMMAREA.                           CL102
01046  EJECT                                                               CL*34
01047 ******************************************************************MBKCS107
01048 *    FILL IN SCREEN HEADER MODULE                                 MBKCS107
01049 ******************************************************************MBKCS107
01050                                                                   MBKCS107
01051      COPY DPPD002.                                                   CL*14
01052  EJECT                                                            MBKCS107
01053 ******************************************************************MBKCS107
01054 *    RETURN TO CICS MODULE                                        MBKCS107
01055 ******************************************************************MBKCS107
01056                                                                   MBKCS107
01057      COPY DPPD003.                                                   CL*14
01058  EJECT                                                            MBKCS107
01059 ******************************************************************MBKCS107
01060 *    FORMAT MESSAGE LINES MODULE                                  MBKCS107
01061 ******************************************************************MBKCS107
01062                                                                   MBKCS107
01063      COPY DPPD005.                                                   CL111
01064  EJECT                                                            MBKCS107
01065 ******************************************************************MBKCS107
01066 *    GET PANEL MODULE                                             MBKCS107
01067 ******************************************************************MBKCS107
01068                                                                   MBKCS107
01069      COPY DPPD007.                                                   CL*14
01070  EJECT                                                            MBKCS107
01071 ******************************************************************MBKCS107
01072 *    WRITE PANEL MODULE                                           MBKCS107
01073 ******************************************************************MBKCS107
01074                                                                   MBKCS107
01075      COPY DPPD008.                                                   CL*14
01076  EJECT                                                            MBKCS107
01077 ******************************************************************MBKCS107
01078 *    TOGGLE THE FUNCTION KEY LINE MODULE                          MBKCS107
01079 ******************************************************************MBKCS107
01080                                                                   MBKCS107
01081      COPY DPPD009.                                                   CL*14
01082  EJECT                                                            MBKCS107
01083 ******************************************************************MBKCS107
01084 *    ABEND PROCESSOR MODULE                                       MBKCS107
01085 ******************************************************************MBKCS107
01086                                                                   MBKCS107
01087      COPY DPPD013.                                                   CL*14
01088  EJECT                                                            MBKCS107
01089 ******************************************************************MBKCS107
01090 *    CHECK COMMUNICATION AREA MODULE                              MBKCS107
01091 ******************************************************************MBKCS107
01092                                                                   MBKCS107
01093      COPY DPPD015.                                                   CL*14
01094  EJECT                                                            MBKCS107
01095 ******************************************************************MBKCS107
01096 *    UPDATE TIME AND DATE MODULE                                  MBKCS107
01097 ******************************************************************MBKCS107
01098                                                                   MBKCS107
01099      COPY DPPD017.                                                   CL*14
01100  EJECT                                                            MBKCS107
