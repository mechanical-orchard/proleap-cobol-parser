       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MRKUT160.                                                 
       AUTHOR.        JIM ARENDT.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02/2019.                                                  
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************00080000
      * MRKUT160 - MDS SESSIONS PURGE EXTRACT                           00090002
      ******************************************************************00100000
      * THIS PROGRAM WILL OPEN A CURSOR TO RETRIEVE ALL OF THE ECOM     00110001
      * LOCATIONS.                                                      00110001
      ******************************************************************00100000
                                                                                
      ******************************************************************00290094
       ENVIRONMENT DIVISION.                                            00280094
      ******************************************************************00290094
       CONFIGURATION SECTION.                                           00310094
       SOURCE-COMPUTER.    IBM-3090.                                    00320094
       OBJECT-COMPUTER.    IBM-3090.                                    00330094
                                                                        00340094
       INPUT-OUTPUT SECTION.                                            00350094
       FILE-CONTROL.                                                    00360094
                                                                        00420094
           SELECT LOC-OUTPUT-FILE                                       00430094
               ASSIGN TO OUT01.                                         00440094
                                                                        00470094
      ******************************************************************00480094
       DATA DIVISION.                                                   00490094
      ******************************************************************00500094
       FILE SECTION.                                                    00520094
                                                                        00530094
       FD  LOC-OUTPUT-FILE                                              00640094
           RECORDING MODE IS F                                          00650094
           BLOCK CONTAINS 0 RECORDS.                                    00660094
       01  LOC-OUTPUT-RECORD               PIC  X(05).                  00670094
                                                                        00680094
      ******************************************************************00500094
       WORKING-STORAGE SECTION.                                         00740094
                                                                        01110094
       01  PC-PROGRAM-CONSTANTS.                                        01420094
           05  PC-PROGRAM-NAME             PIC  X(08)   VALUE           01430094
                                                        'MRKUT160'.     01440094
           05  PC-MAX-RETRIES              PIC S9(04)   COMP SYNC               
                                                        VALUE +3.               
                                                                        01570094
      ******************************************************************00500094
       01  PA-PROGRAM-ACCUMULATORS.                                     01120094
           05  PA-RECORDS-READ             PIC  9(09)   VALUE ZERO.     01190094
           05  PA-WRITE-COUNT              PIC  9(09)   VALUE ZERO.     01190094
                                                                        01410094
      ******************************************************************00500094
       01  PS-PROGRAM-SWITCHES.                                         01580094
           05  PS-END-OF-CSR-SW            PIC  X(01)   VALUE 'N'.      01590094
               88  PS-END-OF-CSR                        VALUE 'Y'.      01600094
                                                                        01760094
      ******************************************************************00500094
       01  PV-OUTPUT-RECORD.                                            01770094
           05  PV-OUT-LOC-NBR              PIC  9(05)   VALUE ZEROES.           
                                                                        01760094
      ******************************************************************00500094
       01  PV-PROGRAM-VARIABLES.                                        01770094
           05  PV-SQL-SUB                  PIC  9(03)   VALUE ZERO.             
           05  PV-PARA-NAME                PIC  X(35)   VALUE SPACES.   02030094
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(35)   VALUE SPACES.   02030094
           05  PV-SQLCODE                  PIC S9(09)   VALUE +0.               
           05  PV-SQLCODE-DISPLAY          PIC  +(08)9  VALUE ZEROES.   00830000
                                                                        01930094
      ******************************************************************00500094
       01  ABEND-CODE                      PIC S9(04)   VALUE ZERO      00900094
                                                        COMP SYNC.      00910094
           88  AC-ABEND-4013                   VALUE +4013.                     
      *        SQL ERROR                                                        
                                                                        00920094
           COPY SQLCA2.                                                 02110094
                                                                        02670099
      ******************************************************************02730094
      *  XMT_LOC DCLGEN                                                 02672099
      ******************************************************************02730094
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************02730094
           EXEC SQL                                                     02690094
                INCLUDE SQLCA                                           02700094
           END-EXEC.                                                    02710094
                                                                        02720094
      ******************************************************************02730094
      *   CURSOR TO RETRIEVE ALL ECOM LOCATIONS                                 
      ******************************************************************02730094
           EXEC SQL                                                             
               DECLARE MAIN-CSR CURSOR FOR                                      
                  SELECT DIGITS(LOC_NBR)                                        
                    FROM XMT_LOC                                                
                   WHERE E_BUS_IND   = 'Y'                                      
                     AND LOC_TYP_CDE = 'DS'                                     
                     AND LOC_NBR     <> 873                                     
                   ORDER BY                                                     
                         LOC_NBR                                                
                    WITH UR                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************02730094
      *  DB2 ERROR MESSAGES FORMAT AREA.                                02740094
      ******************************************************************02750094
           COPY DPWS004.                                                02760094
                                                                        02770094
      ******************************************************************02800094
       PROCEDURE DIVISION.                                              02810094
      ******************************************************************02820094
      ******************************************************************02860094
      * MAIN PROCESSING ROUTINE                                         02870094
      ******************************************************************02880094
       0000-MAINLINE.                                                   02850094
                                                                        02890094
           PERFORM 1000-HOUSEKEEPING.                                   02900094
                                                                        02910094
           PERFORM 2000-PROCESS-MAIN-CURSOR                             02920094
               UNTIL PS-END-OF-CSR.                                             
                                                                        02940094
           PERFORM 3000-END-OF-JOB.                                     02950094
                                                                                
           GOBACK.                                                      02960094
                                                                        02980094
      ******************************************************************03710094
      *  OPEN OUTPUT FILE AND CURSOR                                    03720094
      ******************************************************************03730094
       1000-HOUSEKEEPING.                                               02990094
           MOVE '*1000-HOUSEKEEPING.' TO PV-PARA-NAME.                  03010094
                                                                        03020094
           OPEN OUTPUT LOC-OUTPUT-FILE.                                 03040094
                                                                        03070094
           PERFORM 8000-OPEN-MAIN-CSR.                                  03680094
                                                                        03070094
           PERFORM 8100-FETCH-MAIN-CSR.                                         
                                                                        03680094
      ******************************************************************07160094
      * PROCESS MAIN CURSOR                                             07170094
      ******************************************************************07180094
       2000-PROCESS-MAIN-CURSOR.                                        03910094
           MOVE '*2000-PROCESS-MAIN-CURSOR'    TO PV-PARA-NAME.         03920094
                                                                                
           MOVE XMT00001-LOC-NBR        TO PV-OUT-LOC-NBR.                      
                                                                                
           PERFORM 2500-WRITE-LOC-OUTPUT-FILE.                                  
                                                                                
           PERFORM 8100-FETCH-MAIN-CSR.                                         
                                                                                
      ******************************************************************07320094
      * WRITE OUTPUT RECORD                                             07330094
      ******************************************************************07340094
       2500-WRITE-LOC-OUTPUT-FILE.                                              
                                                                                
           WRITE LOC-OUTPUT-RECORD FROM PV-OUTPUT-RECORD.               07370094
                                                                                
           ADD +1 TO PA-WRITE-COUNT.                                    07380094
                                                                                
      ******************************************************************03710094
      *  CLOSE FILE AND DISPLAY COUNTS                                  03720094
      ******************************************************************03730094
       3000-END-OF-JOB.                                                 03690094
           MOVE '*3000-END-OF-JOB.' TO PV-PARA-NAME.                    03010094
                                                                        03740094
           PERFORM 8200-CLOSE-MAIN-CSR.                                         
                                                                        03790094
           CLOSE LOC-OUTPUT-FILE.                                       03750094
                                                                        03790094
           DISPLAY ' '.                                                         
           DISPLAY '*******************************'.                           
           DISPLAY '    MRKUT160 - PROGRAM STATS'.                              
           DISPLAY 'CURSOR ROWS READ: ' PA-RECORDS-READ.                        
           DISPLAY 'RECORDS WRITTEN : ' PA-WRITE-COUNT.                         
           DISPLAY '*******************************'.                           
           DISPLAY ' '.                                                         
                                                                        03900094
      ******************************************************************07320094
      * OPEN MAIN CURSOR                                                07330094
      ******************************************************************07340094
       8000-OPEN-MAIN-CSR.                                                      
           MOVE '8000-OPEN-MAIN-CSR.    ' TO PV-PARA-NAME.                      
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
               EXEC SQL                                                         
                   OPEN MAIN-CSR                                                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = -904                                          
                        CONTINUE                                                
                                                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '8000-OPEN-MAIN-CSR.'                              
                                          TO PV-PARA-NAME                       
                        MOVE 'OPEN OPERATION'                                   
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                        PERFORM 9980-SQL-ERROR                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '8000-OPEN-MAIN-CSR'                                        
                                      TO  PV-PARA-NAME                          
               MOVE 'OPEN CURSOR RETRIES EXCEEDED'                              
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9980-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************07320094
      *  FETCH MAIN CURSOR                                                      
      ******************************************************************07320094
       8100-FETCH-MAIN-CSR.                                                     
           MOVE '8100-FETCH-MAIN-CSR.' TO PV-PARA-NAME.                         
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
               EXEC SQL                                                         
                   FETCH MAIN-CSR                                               
                    INTO :XMT00001-LOC-NBR                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                        ADD 1 TO PA-RECORDS-READ                                
                                                                                
                   WHEN SQLCODE = +100                                          
                        SET PS-END-OF-CSR TO TRUE                               
                                                                                
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                                                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '8100-FETCH-MAIN-CSR'                              
                                      TO  PV-PARA-NAME                          
                        MOVE 'FETCH MAIN CURSOR'                                
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
                        PERFORM 9980-SQL-ERROR                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '8100-FETCH-MAIN-CSR'                                       
                                      TO  PV-PARA-NAME                          
               MOVE 'FETCH MAIN CURSOR RETRIES EXCEEDED'                        
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9980-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************07320094
      * CLOSE MAIN CURSOR                                               07330094
      ******************************************************************07340094
       8200-CLOSE-MAIN-CSR.                                                     
           MOVE '8200-CLOSE-MAIN-CSR.' TO PV-PARA-NAME.                         
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  CLOSE MAIN-CSR                                                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                                                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '8200-CLOSE-MAIN-CSR'                              
                                      TO  PV-PARA-NAME                          
                        MOVE 'CLOSE MAIN CURSOR'                                
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
                        PERFORM 9980-SQL-ERROR                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '8200-CLOSE-MAIN-CSR'                                       
                                      TO  PV-PARA-NAME                          
               MOVE 'CLOSE MAIN CURSOR RETRIES EXCEEDED'                        
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9980-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************07510094
      * ABEND ROUTINE FOR BAD SQL CALLS                                 07520094
      ******************************************************************07530094
       9980-SQL-ERROR.                                                  07500094
                                                                        07500094
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
           DISPLAY '** ABEND **          = SQL ERROR'.                  07540094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07550094
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              07560094
           DISPLAY '** OPERATION **      = ' PV-DB2-OPERATION-ATTEMPTED.        
           DISPLAY '** SQL CODE **       = ' PV-SQLCODE-DISPLAY.                
           DISPLAY 'CURSOR ROWS READ     = ' PA-RECORDS-READ.                   
           DISPLAY 'RECORDS WRITTEN      = ' PA-WRITE-COUNT.                    
                                                                        07570094
      ******************************************************************07580094
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    07590094
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         07600094
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     07610094
      * FORMAT.                                                         07620094
      ******************************************************************07630094
           COPY DPPD004.                                                07640094
                                                                        07650094
           SET AC-ABEND-4013 TO TRUE.                                   07700094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07710094
                                                                        07720094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                       07750094
      ******************************************************************07760094
       9999-ABEND.                                                      07730094
                                                                        07730094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              07780094
           DISPLAY 'CURSOR ROWS READ     = ' PA-RECORDS-READ.                   
           DISPLAY 'RECORDS WRITTEN      = ' PA-WRITE-COUNT.                    
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
