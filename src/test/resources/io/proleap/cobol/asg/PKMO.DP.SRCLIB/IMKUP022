       IDENTIFICATION DIVISION.                                         00010067
                                                                        00020067
       PROGRAM-ID.    IMKUP022.                                         00030067
       AUTHOR.        DAVE METZGER.                                     00040067
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050067
       DATE-WRITTEN.  09/11/2001.                                       00060067
       DATE-COMPILED.                                                   00070067
                                                                        00080067
      ******************************************************************00090067
      *                                                                 00100067
      * STORED PROCEDURE FOR TUPCPLS INSERT TRIGGER FOR CHANGE IN       00110067
      * SKU RETAIL, STATUS, OR LANDED COST.                             00120067
      *                                                                 00130067
      *  ***   SKU STORE RETAIL/STATUS CHANGE EVENT    ***              00140067
      *                                                                 00150067
      * THIS STORED PROCEDURE PROGRAM WILL BE USED TO FORMAT            00160067
      * ITEM MAINTENANCE SKU RETAIL / STATUS CHANGE TRANSACTION         00170067
      * RECORDS AND INSERT THEM IN THE ITEM EVENT MESSAGE BRIDGE        00180067
      * DB2 TABLE.  IT WILL ALSO CREATE SKU STORE RETAIL/STATUS CHANGE  00190067
      * EVENTS AND LANDED COST CHANGE EVENTS.                           00191067
      * THIS PROGRAM WILL NOT HANDLE STORE SPECIFIC INSERTS TO          00200067
      * TUPCPLS MADE BY PERM PRICE UPDATES JOBS (FOR ACTIVE, CLEARANCE, 00201067
      * AND CONTINUED CLEARANCE PRICE CHANGES).                         00201167
      *                                                                 00201267
      ************************************************************      00201367
      * MAINT LOG.                                                      00202067
      *                                                                 00202167
      * 01/22/02  D. METZGER  CHANGED TO ONLY CREATE RETAIL/STATUS      00202267
      *                       CHANGE MSGS WHEN NO SALES AND/OR          00202367
      *                       RECEIPTS TO PREVENT MSGS FROM PERM.       00202467
      *                                                                 00203067
      * 01/24/02  P. KEBER    FILL IM008-ITEM-KEY-UPC-NBR FOR ALL       00204067
      *                       TRANSACTIONS.                             00207067
      *                                                                 00208067
      * 10/07/02  P. KEBER    CHANGE ID: WR23974                        00209067
      *   ADDED LOGIC TO SET IM008-RTL-CHG-SKU-STAT-DESC-N AND          00209367
      *   IM008-RTL-CHG-SKU-STAT-DESC-CI ON THE SKU RETAIL/STATUS CHANGE00209467
      *   MESSAGE.  SKU-STAT-DESC WILL BE OBTAINED FROM TUPCSTA.        00209567
      *                                                                 00209667
      * 12/17/02  P. KEBER    CHANGE ID: WR24405                        00209767
      *   ADDED LOGIC TO PROCESS PERM SKU RETAIL OR STATUS CHANGES WHEN 00209867
      *   NEW-STR-NBR = 0. FILL NEW IM008-RTL-CHG-MEITGP-NBR, IM008-RTL-00209967
      *   CHG-UNT-RTL-CHG-DT-N, AND IM008-RTL-CHG-RGN-PRIC-IND-SUP      00210067
      *   FIELDS.  ALSO ADD LOGIC TO PROCESS PERM SKU STORE RETAIL OR   00210167
      *   STATUS CHANGES. THE OLD TUPCPLS SPECIFIC STORE ROW IS         00210267
      *   RETRIEVED TO GET OLD VALUES.  A NEW BUSINESS EVENT CODE 080   00210367
      *   RECORD IS CREATED FOR SKU STORE RETAIL OR STATUS CHANGES.     00210567
      *   ADD EFFECTIVE BEGIN TIMESTAMP TO THE LANDED COST CHANGE MSG.  00210667
      *                                                                 00210767
      * 01/05/04  P. KEBER    CHANGE ID: P000621332                     00210867
      *   IGNORE HISTORY ROWS FOR PEKUP040 STORE SPECIFIC INSERTS FOR   00210967
      *   RETAIL CHANGES.                                               00211067
      *                                                                 00211167
      * 11/10/04  P. KEBER    CHANGE ID: WR27472                        00211267
      *   CHANGES TO REMOVE THE OBSOLETE LOGIC FOR PROCESSING PEKUP040  00211367
      *   CHANGES.                                                      00211467
      *                                                                 00211567
      * 05/10/05  P. KEBER    CHANGE ID: P000761430                     00211667
      *   PREVENT THE CREATION OF A 075 ITEM MESSAGE WHEN UPDATING      00211767
      *   FUTURE PERM CREATED STORE ZERO TUPCPLS ROWS.                  00211867
      *                                                                 00211967
      * 09/15/05  P. KEBER    CHANGE ID: WR29434                        00212067
      *   POPULATE NEW 075 AND 080 BUSINESS EVENT FIELDS WITH THE NEW   00212167
      *   TUPCPLS MARKDOWN CODE.                                        00212267
      *                                                                 00212370
      * 06/12/07  P. KEBER    CHANGE ID: WR30315                        00212470
      *   ADD DISPLAY OF KEY INFORMATION IN ERROR PROCESSING.           00212570
      *                                                                 00212671
      * 12/27/07  P. KEBER    CHANGE ID: INC000000204172                00212771
      *   FILL IM008-LAND-COST-EFF-BEG-TMST WITH THE REAL COST CHANGE   00212871
      *   TIMESTAMP INSTEAD OF THE FUTURE ROW TIMESTAMP.                00212971
      ************************************************************      00213067
                                                                        00220067
       ENVIRONMENT DIVISION.                                            00230067
                                                                        00240067
       CONFIGURATION SECTION.                                           00250067
                                                                        00260067
       SOURCE-COMPUTER.        IBM-3090.                                00270067
       OBJECT-COMPUTER.        IBM-3090.                                00280067
                                                                        00290067
       INPUT-OUTPUT SECTION.                                            00300067
                                                                        00310067
       FILE-CONTROL.                                                    00320067
                                                                        00330067
       DATA DIVISION.                                                   00340067
                                                                        00350067
       FILE SECTION.                                                    00360067
                                                                        00370067
       WORKING-STORAGE SECTION.                                         00380067
                                                                        00390067
      ***************************************************************** 00400067
      *  LEGACY ITEM MAINTENANCE INFO COPYBOOK                          00410067
      *  FOR ALL BUSINESS EVENTS                                        00420067
      ***************************************************************** 00430067
           COPY IMRD008.                                                00440067
                                                                        00450067
      ******************************************************************00460067
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        00470067
      ******************************************************************00480067
           COPY DPWS004.                                                00490067
                                                                        00500067
       01  PC-PROGRAM-CONSTANTS.                                        00510067
           05  PC-MAX-RETRIES          PIC S9(04)     VALUE +2          00511067
                                                           COMP SYNC.   00512067
           05  PC-PGM-NAME             PIC  X(08)     VALUE 'IMKUP022'. 00550067
           05  PC-ORIGIN-CNTRY         PIC  X(03)     VALUE 'US '.      00571067
           05  PC-FUTURE-BEG-TIME      PIC  X(15)     VALUE             00572071
                 '00.00.00.000000'.                                     00580071
                                                                        00581071
       01  PS-PROGRAM-SWITCHES.                                         00590067
           05  PS-SQL-ERROR-SW         PIC X       VALUE 'N'.           00600067
               88  PS-SQL-ERROR                    VALUE 'Y'.           00610067
               88  PS-NO-SQL-ERROR                 VALUE 'N'.           00620067
           05  PS-OLD-ROW-FOUND-SW     PIC X       VALUE 'N'.           00630067
               88  PS-OLD-ROW-FOUND                VALUE 'Y'.           00640067
               88  PS-OLD-ROW-NOT-FOUND            VALUE 'N'.           00650067
           05  PS-SALES-RECEIPTS-EXIST-SW                               00660067
                                       PIC X       VALUE 'N'.           00670067
               88  PS-SALES-RECEIPTS-EXIST         VALUE 'Y'.           00680067
               88  PS-SALES-RECEIPTS-DONT-EXIST    VALUE 'N'.           00690067
           05  PS-STATUS-CHANGED-SW    PIC X       VALUE 'N'.           00700067
               88  PS-STATUS-CHANGED               VALUE 'Y'.           00710067
               88  PS-STATUS-NOT-CHANGED           VALUE 'N'.           00720067
                                                                        00730067
       01  PV-PROGRAM-VARIABLES.                                        00740067
           05  PV-UPC-ID-DOUBLE        PIC S9(10)  VALUE ZERO           00741070
                                                   COMP SYNC.           00742070
           05  PV-RETRY-COUNT          PIC S9(04)  VALUE ZERO           00770067
                                                   COMP SYNC.           00780067
           05  PV-UPC-COMP-10          PIC S9(10)  COMP                 00800067
                                                   VALUE ZERO.          00810067
           05  PV-SALES-RCPTS-EXIST    PIC S9(04)  COMP-3.              00811067
           05  PV-TRIGGER-TYPE         PIC X       VALUE SPACE.         00820067
               88  PV-INSERT-TRIGGER               VALUE 'I'.           00830067
               88  PV-UPDATE-TRIGGER               VALUE 'U'.           00840067
           05  PV-PARAGRAPH-NAME       PIC  X(30)  VALUE SPACE.         00841070
           05  PV-DB2-OPERATION        PIC  X(30)  VALUE SPACE.         00842070
                                                                        00850067
           05  PV-ITEM-UPC-NBR         PIC  9(15)  VALUE ZERO.          00851067
           05  PV-ITEM-UPC-RENUM REDEFINES PV-ITEM-UPC-NBR.             00852067
               10  FILLER              PIC  9(06).                      00853067
               10  PV-ITEM-UPC-SKU-NUM PIC  9(08).                      00854067
               10  PV-ITEM-UPC-CHKDGT  PIC  9(01).                      00855067
           05  PV-DB2-KEY-INFO         PIC  X(50)  VALUE SPACES.        00855170
           05  PV-EFF-END-TMST-PLUS1   PIC  X(26)  VALUE SPACES.        00855271
           05  PV-NEW-EFF-BEG-TMST     PIC  X(26)  VALUE SPACES.        00855371
           05  PV-NEW-EFF-BEG-REDEF REDEFINES PV-NEW-EFF-BEG-TMST.      00855471
               10  PV-NEW-EFF-BEG-DATE         PIC X(10).               00855571
               10  FILLER                      PIC X(01).               00855671
               10  PV-NEW-EFF-BEG-TIME         PIC X(15).               00855771
                                                                        00856067
           05  PV-TMITGPL-KEY-INFO.                                     00856170
               10  FILLER                      PIC X(12)   VALUE        00856270
                   'MEITGP_NBR: '.                                      00856370
               10  PV-TMITGPL-KEY-MEITGP-NBR   PIC Z(09)   VALUE ZERO.  00856470
                                                                        00856570
           05  PV-TUPC-KEY-INFO.                                        00856670
               10  FILLER                      PIC X(08)   VALUE        00856770
                   'UPC_ID: '.                                          00856870
               10  PV-TUPC-KEY-UPC-ID          PIC Z(10)   VALUE ZERO.  00856970
                                                                        00857070
           05  PV-TUPCPLS-KEY-INFO.                                     00857170
               10  FILLER                      PIC X(08)   VALUE        00858070
                   'UPC_ID: '.                                          00859070
               10  PV-TUPCPLS-KEY-UPC-ID       PIC Z(10)   VALUE ZERO.  00859170
                                                                        00859270
           05  PV-TUPCSTA-KEY-INFO.                                     00859370
               10  FILLER                      PIC X(14)   VALUE        00859470
                   'UPC_STAT_CDE: '.                                    00859570
               10  PV-TUPCSTA-KEY-UPC-STAT-CDE PIC X(02)   VALUE ZERO.  00859670
                                                                        00859770
      ************************************************************      00860067
      *  ITEM EVENT MESSAGE BRIDGE TABLE                                00870067
      ************************************************************      00880067
           EXEC SQL                                                     00890067
               INCLUDE IMT00001                                         00900067
           END-EXEC.                                                    00910067
                                                                        00920067
      ******************************************************************00930067
      *  VENDOR STYLE TABLE                                             00940067
      ******************************************************************00950067
           EXEC SQL                                                     00960067
               INCLUDE TVNDSTL                                          00970067
           END-EXEC.                                                    00980067
                                                                        00990067
      ************************************************************      01000067
      *  VENDOR STYLE UPC TABLE                                         01010067
      ************************************************************      01020067
           EXEC SQL                                                     01030067
               INCLUDE TUPC                                             01040067
           END-EXEC.                                                    01050067
                                                                        01060067
      ************************************************************      01070067
      *  SKU STORE ATTRIBUTE TABLE                                      01080067
      ************************************************************      01090067
           EXEC SQL                                                     01100067
               INCLUDE TUPCPLS                                          01110067
           END-EXEC.                                                    01120067
                                                                        01130067
      ************************************************************      01131067
      *  SKU STATUS TABLE                                               01132067
      ************************************************************      01133067
           EXEC SQL                                                     01134067
               INCLUDE TUPCSTA                                          01135067
           END-EXEC.                                                    01136067
                                                                        01137067
      ************************************************************      01140067
      *  EXTERNAL ENTITY TABLE                                          01150067
      ************************************************************      01160067
           EXEC SQL                                                     01170067
               INCLUDE TEXTENT                                          01180067
           END-EXEC.                                                    01190067
                                                                        01200067
      ******************************************************************01210067
      *  MERCHANDISE AREA VENDOR STYLE                                  01220067
      ******************************************************************01230067
           EXEC SQL                                                     01240067
               INCLUDE TMDARVS                                          01250067
           END-EXEC.                                                    01260067
      *                                                                 01270067
      ******************************************************************01420067
      *  MERCHANDISE ITEM GROUP PRICE AND QTY TABLE                     01430067
      ******************************************************************01440067
           EXEC SQL                                                     01450067
               INCLUDE TMITGPL                                          01460067
           END-EXEC.                                                    01470067
      *                                                                 01480067
      ******************************************************************01490067
      *    DB2 COMMUNICATION AREA                                       01500067
      ******************************************************************01510067
           EXEC SQL                                                     01520067
               INCLUDE SQLCA                                            01530067
           END-EXEC.                                                    01540067
                                                                        01550067
      ******************************************************************01560067
      * LINKAGE SECTION FOR VALUES PASSED FROM THE DB2 TRIGGER ON       01570067
      * AN INSERT INTO TUPCPLS (RETAIL, STATUS, OR MEIT GP NBR CHANGE)  01580067
      ******************************************************************01590067
       LINKAGE SECTION.                                                 01600067
       01  LS-TRIGGER-TYPE-CDE          PIC   X(01).                    01610067
       01  LS-NEW-UPC-ID                PIC  S9(09)      COMP.          01620067
       01  LS-NEW-UPC-STAT-CDE          PIC   X(02).                    01630067
       01  LS-NEW-ITM-NBR               PIC  S9(15)V     COMP-3.        01640067
       01  LS-NEW-UNT-RTL-AMT           PIC  S9(07)V9(2) COMP-3.        01650067
       01  LS-NEW-MEITGP-NBR            PIC  S9(09)      COMP.          01660067
       01  LS-NEW-UNT-COST-AMT          PIC  S9(07)V9(3) COMP-3.        01670067
       01  LS-NEW-STR-NBR               PIC  S9(04)      COMP.          01671067
       01  LS-NEW-UPC-STAT-CHG-DTE      PIC   X(10).                    01672067
       01  LS-NEW-UNT-RTL-CHG-DTE       PIC   X(10).                    01673067
       01  LS-NEW-EFF-BEG-TMST          PIC   X(26).                    01674067
       01  LS-NEW-CRE-BY-USR-ID         PIC   X(08).                    01675067
       01  LS-NEW-MKDN-CDE              PIC   X(01).                    01676067
       01  LS-INDARRAY.                                                 01677067
           05 LS-INDVAR-TRIGGER-TYPE-CDE                                01677167
                                        PIC S9(4) COMP.                 01677267
           05 LS-INDVAR-UPC-ID          PIC S9(4) COMP.                 01678067
           05 LS-INDVAR-UPC-STAT-CDE    PIC S9(4) COMP.                 01679067
           05 LS-INDVAR-ITM-NBR         PIC S9(4) COMP.                 01679167
           05 LS-INDVAR-UNT-RTL-AMT     PIC S9(4) COMP.                 01679267
           05 LS-INDVAR-MEITGP-NBR      PIC S9(4) COMP.                 01679367
           05 LS-INDVAR-UNT-COST-AMT    PIC S9(4) COMP.                 01679467
           05 LS-INDVAR-STR-NBR         PIC S9(4) COMP.                 01679567
           05 LS-INDVAR-UPC-STAT-CHG-DTE                                01679667
                                        PIC S9(4) COMP.                 01679767
           05 LS-INDVAR-UNT-RTL-CHG-DTE PIC S9(4) COMP.                 01679867
           05 LS-INDVAR-EFF-BEG-TMST    PIC S9(4) COMP.                 01679967
           05 LS-INDVAR-CRE-BY-USR-ID   PIC S9(4) COMP.                 01680067
           05 LS-INDVAR-MKDN-CDE        PIC S9(4) COMP.                 01680167
                                                                        01681067
       PROCEDURE DIVISION USING  LS-TRIGGER-TYPE-CDE                    01690067
                               , LS-NEW-UPC-ID                          01700067
                               , LS-NEW-UPC-STAT-CDE                    01710067
                               , LS-NEW-ITM-NBR                         01720067
                               , LS-NEW-UNT-RTL-AMT                     01730067
                               , LS-NEW-MEITGP-NBR                      01740067
                               , LS-NEW-UNT-COST-AMT                    01750067
                               , LS-NEW-STR-NBR                         01751067
                               , LS-NEW-UPC-STAT-CHG-DTE                01752067
                               , LS-NEW-UNT-RTL-CHG-DTE                 01753067
                               , LS-NEW-EFF-BEG-TMST                    01754067
                               , LS-NEW-CRE-BY-USR-ID                   01755067
                               , LS-NEW-MKDN-CDE                        01756067
                               , LS-INDARRAY.                           01757067
                                                                        01760067
      *-----------------------------------------------------------------01770067
      * BUILD DATA FOR THE STATUS CODE AND/OR RETAIL AMOUNT CHANGE.     01780067
      *-----------------------------------------------------------------01790067
       1000-MAIN-MODULE.                                                01800067
                                                                        01810067
           PERFORM 8000-GET-OLD-TUPCPLS-STR-INFO.                       01820067
                                                                        01830067
      *    IF THE OLD SKU IS NOT FOUND FOR A STORE ZERO RECORD, THE     01831067
      *    INSERT IS FOR A NEW SKU SO NO PROCESSING WILL TAKE PLACE.    01831167
           IF PS-NO-SQL-ERROR AND                                       01833067
              ((PS-OLD-ROW-FOUND AND LS-NEW-STR-NBR = 0) OR             01840067
               LS-NEW-STR-NBR > 0)                                      01850067
               PERFORM 2000-PROCESS-TUPCPLS                             01860067
           END-IF.                                                      01870067
                                                                        01880067
           GOBACK.                                                      01890067
                                                                        01900067
      *-----------------------------------------------------------------01910067
      * DETERMINE TYPE OF INSERT TAKING PLACE - EITHER A RETAIL/STATUS  01920067
      * CHANGE AND/OR A LANDED COST CHANGE                              01930067
      *-----------------------------------------------------------------01970067
       2000-PROCESS-TUPCPLS.                                            01980067
                                                                        01990067
           MOVE LS-TRIGGER-TYPE-CDE    TO PV-TRIGGER-TYPE.              02000067
                                                                        02010067
           IF PV-INSERT-TRIGGER                                         02020067
              IF UPCPLS-UNT-RTL-AMT  NOT = LS-NEW-UNT-RTL-AMT  OR       02030067
                 UPCPLS-UPC-STAT-CDE NOT = LS-NEW-UPC-STAT-CDE OR       02040067
                 UPCPLS-MEITGP-NBR   NOT = LS-NEW-MEITGP-NBR            02050067
      *          *** PROCESS RETAIL/STATUS CHANGE MSG                   02060067
                 PERFORM 2100-PROCESS-RTL-STAT-CHG                      02070067
              END-IF                                                    02080067
                                                                        02090067
              IF UPCPLS-UNT-COST-AMT NOT = LS-NEW-UNT-COST-AMT          02100067
                AND LS-NEW-STR-NBR = ZERO                               02101067
      *          *** PROCESS COST CHANGE MSG                            02110067
                 PERFORM 2200-PROCESS-COST-CHG                          02120067
              END-IF                                                    02130067
           ELSE                                                         02140067
              IF PV-UPDATE-TRIGGER                                      02150067
                 IF UPCPLS-UNT-RTL-AMT NOT = LS-NEW-UNT-RTL-AMT         02160067
      *             *** PROCESS RETAIL CHANGE MSG IF NOT A PERM ROW     02170067
                    IF NOT (LS-NEW-CRE-BY-USR-ID = 'IMKUP039'           02181067
                            OR LS-NEW-CRE-BY-USR-ID = 'IMKUP041')       02181167
                       PERFORM 2100-PROCESS-RTL-STAT-CHG                02183067
                    END-IF                                              02184067
                 END-IF                                                 02190067
                                                                        02200067
                 IF UPCPLS-UNT-COST-AMT NOT = LS-NEW-UNT-COST-AMT       02210067
                   AND LS-NEW-STR-NBR = ZERO                            02211067
      *             *** PROCESS COST CHANGE MSG                         02220067
                    PERFORM 2200-PROCESS-COST-CHG                       02230067
                 END-IF                                                 02240067
              ELSE                                                      02250067
                 DISPLAY ' '                                            02260067
                 DISPLAY '  ========================================'   02270067
                 DISPLAY '  =      BAD PARM PASSED INTO IMKUP022   ='   02280067
                 DISPLAY '  ========================================'   02290067
                 DISPLAY '  * INVALID VALUE PASSED FROM TRIGGER      '  02300067
                 DISPLAY '  * TRIGGER TYPE MUST BE EITHER U - UPDATE'   02310067
                 DISPLAY '  *                          OR I - INSERT'   02320067
              END-IF                                                    02330067
           END-IF.                                                      02340067
                                                                        02350067
      *-----------------------------------------------------------------02360067
      * PROCESS TUPCPLS RETAIL/STATUS CHANGE (TUPCPLS INSERT TRIGGER)   02370067
      *-----------------------------------------------------------------02380067
       2100-PROCESS-RTL-STAT-CHG.                                       02390067
                                                                        02400067
           IF LS-NEW-STR-NBR = ZERO                                     02430067
      *        SET THE BUSINESS EVENT FOR THE SKU RETAIL/STATUS CHANGE  02440067
      *        (IM008-ITEM-ACTION-TYPE = 075)                           02450067
               SET IM008-BUS-EVENT-RTL-STATUS-CHG TO TRUE               02460067
               PERFORM 3000-BUILD-RETAIL-STATUS-CHG                     02470067
           ELSE                                                         02471067
      *        SET THE BUSINESS EVENT FOR THE SKU STORE RTL/STATUS CHG  02472067
      *        (IM008-ITEM-ACTION-TYPE = 080)                           02473067
               SET IM008-BUS-EVENT-STR-RTL-ST-CHG TO TRUE               02474067
               PERFORM 3500-BUILD-SKU-ST-RTL-ST-CHG                     02475067
           END-IF.                                                      02480067
                                                                        02490067
      *-----------------------------------------------------------------02500067
      * PROCESS LANDED COST (TUPCPLS UPDATE OR INSERT TRIGGER)          02510067
      *-----------------------------------------------------------------02520067
       2200-PROCESS-COST-CHG.                                           02530067
                                                                        02540067
      *    SET THE BUSINESS EVENT FOR THE SKU LANDED COST CHANGE        02550067
      *    (IM008-ITEM-ACTION-TYPE = 070)                               02560067
           SET IM008-BUS-EVENT-SKU-COST-CHG TO TRUE.                    02570067
                                                                        02580067
           PERFORM 5000-BUILD-MESSAGE-KEY.                              02596067
                                                                        02597067
           IF PS-NO-SQL-ERROR                                           02598067
               INITIALIZE  DCLIMT00001                                  02599067
               PERFORM 4000-BUILD-COST-DATA                             02599167
               PERFORM 5100-MOVE-DATA-TO-EVENT-TBL                      02599267
               PERFORM 8900-INSERT-IMT-EVNT-MSG                         02599367
           END-IF.                                                      02599467
                                                                        02599567
                                                                        02600067
      *-----------------------------------------------------------------02610067
      * PROCESS RETAIL AMOUNT AND/OR STATUS CODE CHANGE.                02620067
      *-----------------------------------------------------------------02630067
       3000-BUILD-RETAIL-STATUS-CHG.                                    02640067
                                                                        02650067
           PERFORM 5000-BUILD-MESSAGE-KEY.                              02660067
                                                                        02670067
           IF PS-NO-SQL-ERROR AND                                       02680067
               LS-NEW-MEITGP-NBR NOT = 0                                02690067
               MOVE LS-NEW-MEITGP-NBR   TO MITGPL-MEITGP-NBR            02700067
               PERFORM 8300-GET-MITGPL                                  02710067
           ELSE                                                         02720067
               MOVE ZEROS               TO MITGPL-MITGPL-QTY            02730067
                                           MITGPL-GP-AMT                02740067
           END-IF.                                                      02750067
                                                                        02760067
           IF PS-NO-SQL-ERROR                                           02761067
               PERFORM 8500-GET-TUPCSTA-STAT-CDE-DESC                   02764067
           END-IF.                                                      02768067
                                                                        02769067
           IF PS-NO-SQL-ERROR                                           02770067
               INITIALIZE  DCLIMT00001                                  02780067
               PERFORM 3100-BUILD-RETAIL-STATUS-DATA                    02790067
               PERFORM 5100-MOVE-DATA-TO-EVENT-TBL                      02800067
               PERFORM 8900-INSERT-IMT-EVNT-MSG                         02810067
           END-IF.                                                      02820067
                                                                        02830067
      *-----------------------------------------------------------------02840067
      * BUILD THE TUPCPLS RETAIL STATUS FOR IM008 LAYOUT                02850067
      *-----------------------------------------------------------------02860067
       3100-BUILD-RETAIL-STATUS-DATA.                                   02870067
                                                                        02880067
           INITIALIZE IM008-SKU-RTL-STATUS-CHG-DATA.                    02890067
                                                                        02900067
      ***  MOVE RETAIL AND SET CHANGE INDICATOR                         02910067
           MOVE LS-NEW-UNT-RTL-AMT   TO IM008-RTL-CHG-UNT-RTL-AMT-N.    02921067
           IF UPCPLS-UNT-RTL-AMT  = LS-NEW-UNT-RTL-AMT                  02930067
               SET IM008-RTL-CHG-UNT-RTL-AMT-NOC TO TRUE                02940067
           ELSE                                                         02950067
               SET IM008-RTL-CHG-UNT-RTL-AMT-CHG TO TRUE                02960067
           END-IF.                                                      02970067
                                                                        02970167
           MOVE LS-NEW-UNT-RTL-CHG-DTE                                  02971067
                                     TO IM008-RTL-CHG-UNT-RTL-CHG-DTE.  02972067
                                                                        02980067
      ***  MOVE MEITGP NUMBERS AND SET CHANGE INDICATOR                 02990067
           MOVE UPCPLS-MEITGP-NBR    TO IM008-RTL-CHG-MEITGP-NBR.       02991067
           MOVE LS-NEW-MEITGP-NBR    TO IM008-RTL-CHG-MEITGP-NBR-N.     03000067
           IF UPCPLS-MEITGP-NBR = LS-NEW-MEITGP-NBR                     03010067
               SET IM008-RTL-CHG-MEITGP-NBR-NOC TO TRUE                 03020067
           ELSE                                                         03030067
               SET IM008-RTL-CHG-MEITGP-NBR-CHG TO TRUE                 03040067
           END-IF.                                                      03050067
                                                                        03060067
           MOVE MITGPL-MITGPL-QTY    TO IM008-RTL-CHG-GP-QTY-N          03070067
           MOVE MITGPL-GP-AMT        TO IM008-RTL-CHG-GP-RTL-AMT-N      03080067
           MOVE LS-NEW-UPC-STAT-CDE  TO IM008-RTL-CHG-UPC-STAT-CDE-N.   03090067
           MOVE UPCPLS-UPC-STAT-CDE  TO IM008-RTL-CHG-UPC-STAT-CDE.     03100067
           MOVE LS-NEW-UPC-STAT-CHG-DTE                                 03100167
                                     TO IM008-RTL-CHG-UPC-STAT-CHG-DTE. 03100267
                                                                        03101067
      ***  FILL NEW UPC STATUS DESCRIPTION AND SET CHANGE INDICATOR     03102067
           MOVE UPCSTA-UPC-STAT-DESC TO IM008-RTL-CHG-SKU-STAT-DESC-N.  03103067
           IF UPCPLS-UPC-STAT-CDE = LS-NEW-UPC-STAT-CDE                 03104067
               SET IM008-RTL-CHG-SKU-STAT-DSC-NOC TO TRUE               03104167
           ELSE                                                         03106067
               SET IM008-RTL-CHG-SKU-STAT-DSC-CHG TO TRUE               03106167
           END-IF.                                                      03108067
                                                                        03110067
           MOVE UPC-REGN-PRIC-IND    TO IM008-RTL-CHG-RGN-PRIC-IND-SUP. 03111067
           MOVE LS-NEW-UNT-COST-AMT  TO IM008-RTL-CHG-UNT-COST-AMT-SUP. 03111167
           MOVE LS-NEW-EFF-BEG-TMST  TO IM008-RTL-CHG-EFF-BEG-TMST.     03111467
           MOVE LS-NEW-CRE-BY-USR-ID TO IM008-RTL-CHG-CRE-BY-USR-ID.    03111567
      ***  CHECK IF MARKDOWN CODE IS NULL                               03111669
           IF LS-INDVAR-MKDN-CDE = -1                                   03112168
               MOVE SPACE            TO IM008-RTL-CHG-MKDN-CDE          03112268
           ELSE                                                         03112368
               MOVE LS-NEW-MKDN-CDE  TO IM008-RTL-CHG-MKDN-CDE          03112468
           END-IF.                                                      03112568
                                                                        03113067
      ***  DETERMINE LENGTH OF MESSAGE FOR SKU RETAIL/STATUS CHANGE     03120067
      ***  ENTIRE LENGTH OF RECORD MINUS THE RTL/STAT CHG FILLER        03130067
           COMPUTE IMT00001-MSG-TXT-LEN =                               03140067
               LENGTH OF IMRD008-RECORD -                               03150067
               LENGTH OF IM008-RTL-CHG-FILLER.                          03160067
                                                                        03170067
      *-----------------------------------------------------------------03180067
      * PROCESS SKU STORE RETAIL AMOUNT AND/OR STATUS CODE CHANGE.      03190067
      *-----------------------------------------------------------------03200067
       3500-BUILD-SKU-ST-RTL-ST-CHG.                                    03210067
                                                                        03220067
           PERFORM 5000-BUILD-MESSAGE-KEY.                              03230067
                                                                        03240067
           IF PS-NO-SQL-ERROR                                           03340067
               PERFORM 8500-GET-TUPCSTA-STAT-CDE-DESC                   03350067
           END-IF.                                                      03351067
                                                                        03352067
           IF PS-NO-SQL-ERROR                                           03353067
               INITIALIZE  DCLIMT00001                                  03354067
               PERFORM 3600-BUILD-STR-RTL-STATUS-DATA                   03355067
               PERFORM 5100-MOVE-DATA-TO-EVENT-TBL                      03356067
               PERFORM 8900-INSERT-IMT-EVNT-MSG                         03357067
           END-IF.                                                      03358067
                                                                        03359067
      *-----------------------------------------------------------------03359167
      * BUILD THE SKU STORE RETAIL STATUS FOR IM008 LAYOUT              03359267
      *-----------------------------------------------------------------03359467
       3600-BUILD-STR-RTL-STATUS-DATA.                                  03359567
                                                                        03359667
           INITIALIZE IM008-SKU-STR-RTL-ST-CHG-DATA.                    03359767
                                                                        03359867
      ***  MOVE RETAIL AND SET CHANGE INDICATOR                         03359967
           MOVE LS-NEW-UNT-RTL-AMT   TO IM008-STR-RTL-UNT-RTL-AMT-N.    03360067
           IF PS-OLD-ROW-NOT-FOUND                                      03360167
               SET IM008-STR-RTL-UNT-RTL-AMT-CHG TO TRUE                03360267
           ELSE                                                         03360367
               IF UPCPLS-UNT-RTL-AMT = LS-NEW-UNT-RTL-AMT               03360767
                   SET IM008-STR-RTL-UNT-RTL-AMT-NOC TO TRUE            03360867
               ELSE                                                     03360967
                   SET IM008-STR-RTL-UNT-RTL-AMT-CHG TO TRUE            03361067
               END-IF                                                   03361167
           END-IF.                                                      03361267
                                                                        03361367
           MOVE LS-NEW-UNT-RTL-CHG-DTE                                  03361467
                                     TO IM008-STR-RTL-UNT-RTL-CHG-DTE.  03361567
                                                                        03362067
           MOVE LS-NEW-UPC-STAT-CDE  TO IM008-STR-RTL-UPC-STAT-CDE-N.   03362167
           IF PS-OLD-ROW-NOT-FOUND                                      03362267
               MOVE SPACES           TO IM008-STR-RTL-UPC-STAT-CDE      03362367
           ELSE                                                         03362467
               MOVE UPCPLS-UPC-STAT-CDE                                 03362567
                                     TO IM008-STR-RTL-UPC-STAT-CDE      03362667
           END-IF.                                                      03362767
           MOVE LS-NEW-UPC-STAT-CHG-DTE                                 03362867
                                     TO IM008-STR-RTL-UPC-STAT-CHG-DTE. 03362967
                                                                        03363067
      ***  FILL NEW UPC STATUS DESCRIPTION AND SET CHANGE INDICATOR     03363167
           MOVE UPCSTA-UPC-STAT-DESC TO IM008-STR-RTL-SKU-STAT-DESC-N.  03363267
           IF IM008-STR-RTL-UPC-STAT-CDE-N = IM008-STR-RTL-UPC-STAT-CDE 03363367
               SET IM008-STR-RTL-SKU-STAT-DSC-NOC TO TRUE               03363467
           ELSE                                                         03363567
               SET IM008-STR-RTL-SKU-STAT-DSC-CHG TO TRUE               03363667
           END-IF.                                                      03363767
                                                                        03363867
           MOVE LS-NEW-UNT-COST-AMT  TO IM008-STR-RTL-UNT-COST-AMT-SUP. 03364067
           MOVE LS-NEW-EFF-BEG-TMST  TO IM008-STR-RTL-EFF-BEG-TMST.     03364167
           MOVE 1                    TO IM008-STR-RTL-STORE-COUNT.      03364267
           MOVE LOW-VALUES           TO IM008-STR-RTL-STORE-TABLE.      03364367
           MOVE LS-NEW-STR-NBR       TO IM008-STR-RTL-STORE(1).         03364467
                                                                        03364567
      *    CHECK IF MARKDOWN CODE IS NULL                               03364667
           IF LS-INDVAR-MKDN-CDE = -1                                   03364967
               MOVE SPACE            TO IM008-STR-RTL-MKDN-CDE          03365067
           ELSE                                                         03365167
               MOVE LS-NEW-MKDN-CDE  TO IM008-STR-RTL-MKDN-CDE          03365267
           END-IF.                                                      03365367
                                                                        03365467
      ***  DETERMINE LENGTH OF MESSAGE FOR SKU RETAIL/STATUS CHANGE     03365567
      ***  ENTIRE LENGTH OF RECORD MINUS THE RTL/STAT CHG FILLER        03365667
           COMPUTE IMT00001-MSG-TXT-LEN =                               03365767
               LENGTH OF IMRD008-RECORD -                               03365867
               LENGTH OF IM008-STR-RTL-FILLER.                          03365967
                                                                        03366067
      *-----------------------------------------------------------------03367067
      * BUILD THE TUPCPLS COST CHANGE FOR IM008 LAYOUT                  03370067
      *-----------------------------------------------------------------03380067
       4000-BUILD-COST-DATA.                                            03390067
                                                                        03400067
           INITIALIZE IM008-SKU-LANDED-COST-CHG-DATA.                   03410067
           MOVE LS-NEW-UNT-COST-AMT  TO IM008-LAND-COST-LAND-COST-N.    03420067
           MOVE UPC-UNT-COST-1ST-AMT TO IM008-LAND-COST-1ST-COST-SUP.   03430067
           IF PV-UPDATE-TRIGGER                                         03430171
               MOVE LS-NEW-EFF-BEG-TMST TO PV-NEW-EFF-BEG-TMST          03431071
               IF PV-NEW-EFF-BEG-TIME = PC-FUTURE-BEG-TIME              03431171
                   PERFORM 8100-GET-BEG-TIMESTAMP                       03431271
                   IF PV-EFF-END-TMST-PLUS1 < LS-NEW-EFF-BEG-TMST       03431371
      *                USE THE REAL COST CHANGE TIMESTAMP INSTEAD OF    03431471
      *                THE FUTURE ROW TIMESTAMP                         03431571
                       MOVE PV-EFF-END-TMST-PLUS1                       03431671
                                        TO IM008-LAND-COST-EFF-BEG-TMST 03431771
                   ELSE                                                 03431871
                       MOVE LS-NEW-EFF-BEG-TMST                         03431971
                                        TO IM008-LAND-COST-EFF-BEG-TMST 03432071
                   END-IF                                               03432171
               ELSE                                                     03432271
                   MOVE LS-NEW-EFF-BEG-TMST                             03432371
                                        TO IM008-LAND-COST-EFF-BEG-TMST 03432471
               END-IF                                                   03432571
           ELSE                                                         03432671
               MOVE LS-NEW-EFF-BEG-TMST TO IM008-LAND-COST-EFF-BEG-TMST 03432771
           END-IF.                                                      03433071
                                                                        03440067
      ***  DETERMINE LENGTH OF MESSAGE FOR LANDED COST CHANGE           03450067
      ***  ENTIRE LENGTH OF RECORD MINUS THE LAND COST FILLER           03460067
           COMPUTE IMT00001-MSG-TXT-LEN =                               03470067
               LENGTH OF IMRD008-RECORD -                               03480067
               LENGTH OF IM008-LAND-COST-FILLER.                        03490067
                                                                        03500067
      *-----------------------------------------------------------------03510067
      * BUILD THE BUSINESS EVENT KEY DATA FOR IM008                     03520067
      *-----------------------------------------------------------------03530067
       5000-BUILD-MESSAGE-KEY.                                          03540067
                                                                        03550067
           MOVE LS-NEW-UPC-ID            TO UPC-UPC-ID.                 03560067
           PERFORM 8400-GET-ALL-KEY-INFO.                               03580067
                                                                        03590067
           IF PS-NO-SQL-ERROR                                           03600067
               MOVE SPACES               TO IM008-CRE-TMST              03610067
               INITIALIZE IM008-ITEM-KEY-DATA                           03620067
               MOVE VNDSTL-VS-ID         TO IM008-ITEM-KEY-VS-ID        03630067
               MOVE VNDSTL-ENT-ID        TO IM008-ITEM-KEY-ENT-ID       03640067
               MOVE EXTENT-DUN-NBR       TO IM008-ITEM-KEY-DUN-NBR      03650067
               MOVE VNDSTL-VS-NBR        TO IM008-ITEM-KEY-VS-NBR       03660067
               MOVE MDARVS-SUB-CL-MDSEAR-ID                             03670067
                                         TO IM008-ITEM-KEY-SUBCL-MDSE-ID03680067
               MOVE MDARVS-DEPT-NBR      TO IM008-ITEM-KEY-DEPT         03690067
               MOVE MDARVS-MAJ-CL-NBR    TO IM008-ITEM-KEY-CLASS        03700067
               MOVE MDARVS-SUB-CL-NBR    TO IM008-ITEM-KEY-SUBCL        03710067
               MOVE MDARVS-RMS-STYL-ID   TO IM008-ITEM-KEY-RMS-STYL-ID  03720067
               MOVE UPC-UPC-NBR          TO PV-ITEM-UPC-NBR             03721067
               MOVE PV-ITEM-UPC-SKU-NUM  TO IM008-ITEM-KEY-SKU          03730067
                                                                        03740067
               MOVE UPC-INTR-UPC-ID      TO PV-UPC-COMP-10              03750067
               MOVE PV-UPC-COMP-10       TO IM008-ITEM-KEY-INTR-UPC-ID  03760067
               MOVE LS-NEW-UPC-ID        TO PV-UPC-COMP-10              03780067
               MOVE PV-UPC-COMP-10       TO IM008-ITEM-KEY-UPC-ID       03790067
                                                                        03800067
               MOVE UPC-UPC-NBR          TO IM008-ITEM-KEY-UPC-NBR      03830067
                                                                        03870067
               MOVE PC-ORIGIN-CNTRY      TO IM008-ITEM-KEY-ORIGIN-CNTRY 03890067
           END-IF.                                                      03900067
                                                                        03910067
      *-----------------------------------------------------------      03920067
      * MOVE THE STYLE RECLASS MSG TO THE ITEM EVENT                    03930067
      * MSG BRIDGE TABLE                                                03940067
      *-----------------------------------------------------------      03950067
       5100-MOVE-DATA-TO-EVENT-TBL.                                     03960067
                                                                        03970067
           MOVE MDARVS-RMS-STYL-ID      TO IMT00001-RMS-STYL-ID.        03980067
           MOVE IM008-BUSINESS-EVENT-ID TO IMT00001-BUS-EVNT-TYP-CDE.   03990067
           MOVE IMRD008-RECORD          TO IMT00001-MSG-TXT-TEXT.       04000067
                                                                        04010067
      *-----------------------------------------------------------------04020067
      * GET THE OLD TUPCPLS RETAIL, STATUS CODE, GROUP NBR AND COST.    04030067
      *-----------------------------------------------------------------04040067
       8000-GET-OLD-TUPCPLS-STR-INFO.                                   04050067
                                                                        04060067
           INITIALIZE PV-RETRY-COUNT.                                   04070067
                                                                        04080067
           PERFORM WITH TEST AFTER                                      04090067
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       04100067
                 UNTIL SQLCODE = +0                                     04110067
                    OR SQLCODE = +100                                   04120067
                    OR PS-SQL-ERROR                                     04130067
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  04140067
               EXEC SQL                                                 04150067
                   SELECT  EFF_END_TMST                                 04190071
                          ,UNT_COST_AMT                                 04190171
                          ,UNT_RTL_AMT                                  04190271
                          ,MEITGP_NBR                                   04191067
                          ,UPC_STAT_CDE                                 04200067
                     INTO :UPCPLS-EFF-END-TMST                          04240071
                         ,:UPCPLS-UNT-COST-AMT                          04240171
                         ,:UPCPLS-UNT-RTL-AMT                           04240271
                         ,:UPCPLS-MEITGP-NBR                            04241067
                         ,:UPCPLS-UPC-STAT-CDE                          04250067
                     FROM  TUPCPLS                                      04260067
                    WHERE  UPC_ID       = :LS-NEW-UPC-ID                04270067
                      AND  OPERCO_NBR   = '03'                          04280067
                      AND  STR_NBR      = :LS-NEW-STR-NBR               04290067
                      AND  EFF_END_TMST =                               04300067
                           (SELECT MAX(EFF_END_TMST)                    04310067
                            FROM TUPCPLS                                04320067
                            WHERE  UPC_ID       = :LS-NEW-UPC-ID        04330067
                              AND  OPERCO_NBR   = '03'                  04340067
                              AND  STR_NBR      = :LS-NEW-STR-NBR)      04350067
                     WITH UR                                            04360067
               END-EXEC                                                 04370067
                                                                        04380067
               EVALUATE TRUE                                            04390067
                   WHEN SQLCODE = ZERO                                  04400067
                       SET PS-OLD-ROW-FOUND         TO TRUE             04410067
                   WHEN SQLCODE = +100                                  04420067
                       SET PS-OLD-ROW-NOT-FOUND TO TRUE                 04430067
                       IF LS-NEW-STR-NBR > 0                            04431067
                           MOVE SPACES TO UPCPLS-UPC-STAT-CDE           04432067
                           MOVE ZEROES TO UPCPLS-UNT-COST-AMT           04432167
                           MOVE ZEROES TO UPCPLS-UNT-RTL-AMT            04432267
                           MOVE ZEROES TO UPCPLS-MEITGP-NBR             04432367
                       END-IF                                           04433067
                   WHEN SQLCODE = -904                                  04440067
                   WHEN SQLCODE = -913                                  04450067
                       CONTINUE                                         04460067
                   WHEN OTHER                                           04470067
                       MOVE '8000-GET-OLD-TUPCPLS-STR-INFO'             04480067
                                                   TO PV-PARAGRAPH-NAME 04490067
                       MOVE 'SELECT FROM TUPCPLS'  TO PV-DB2-OPERATION  04500067
                       MOVE LS-NEW-UPC-ID       TO PV-UPC-ID-DOUBLE     04501070
                       MOVE PV-UPC-ID-DOUBLE    TO PV-TUPCPLS-KEY-UPC-ID04503070
                       MOVE PV-TUPCPLS-KEY-INFO TO PV-DB2-KEY-INFO      04505070
                       PERFORM 9998-SQL-ERROR                           04510067
               END-EVALUATE                                             04520067
           END-PERFORM.                                                 04530067
                                                                        04540067
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           04550067
               MOVE '8000-GET-OLD-TUPCPLS-STR-INFO'                     04560067
                                                 TO PV-PARAGRAPH-NAME   04570067
               MOVE 'MAX RETRIES EXCEEDED'       TO PV-DB2-OPERATION    04580067
               MOVE LS-NEW-UPC-ID       TO PV-UPC-ID-DOUBLE             04581070
               MOVE PV-UPC-ID-DOUBLE    TO PV-TUPCPLS-KEY-UPC-ID        04582070
               MOVE PV-TUPCPLS-KEY-INFO TO PV-DB2-KEY-INFO              04583070
               PERFORM 9998-SQL-ERROR                                   04590067
           END-IF.                                                      04600067
                                                                        04610067
      *-----------------------------------------------------------------05480067
      * DB2 CALL TO GET THE BEGIN TIMESTAMP FROM AN END TIMESTAMP.      05500071
      *-----------------------------------------------------------------05510067
       8100-GET-BEG-TIMESTAMP.                                          05520071
                                                                        05530067
           PERFORM WITH TEST AFTER                                      05540067
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       05550067
                 UNTIL SQLCODE = +0                                     05560067
                    OR PS-SQL-ERROR                                     05570067
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  05580067
               EXEC SQL                                                 05590067
                   SELECT  TIMESTAMP(:UPCPLS-EFF-END-TMST) +            05600071
                           1 MICROSECOND                                05610071
                     INTO :PV-EFF-END-TMST-PLUS1                        05620071
                     FROM  SYSIBM.SYSDUMMY1                             05640071
               END-EXEC                                                 05680067
                                                                        05690067
               EVALUATE TRUE                                            05700067
                   WHEN SQLCODE = ZERO                                  05710067
                   WHEN SQLCODE = -904                                  05720067
                   WHEN SQLCODE = -913                                  05730067
                       CONTINUE                                         05740067
                   WHEN OTHER                                           05750067
                       MOVE '8100-GET-BEG-TIMESTAMP'                    05760071
                                                   TO PV-PARAGRAPH-NAME 05761071
                       MOVE 'SELECT TIMESTAMP'     TO PV-DB2-OPERATION  05770071
                       MOVE SPACES                 TO PV-DB2-KEY-INFO   05771071
                       PERFORM 9998-SQL-ERROR                           05780067
               END-EVALUATE                                             05790067
           END-PERFORM.                                                 05800067
                                                                        05810067
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          05820067
               MOVE '8100-GET-BEG-TIMESTAMP'   TO PV-PARAGRAPH-NAME     05830071
               MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-OPERATION      05840067
               MOVE SPACES                     TO PV-DB2-KEY-INFO       05843071
               PERFORM 9998-SQL-ERROR                                   05850067
           END-IF.                                                      05860067
                                                                        05870067
      *-----------------------------------------------------------------05871071
      * *** ONLY EXECUTED WHEN MEITGP-NBR > 0 ***                       05872071
      * DB2 CALL TO GET THE GROUP RETAIL AMT AND QTY                    05873071
      *-----------------------------------------------------------------05874071
       8300-GET-MITGPL.                                                 05875071
                                                                        05876071
           PERFORM WITH TEST AFTER                                      05877071
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       05878071
                 UNTIL SQLCODE = +0                                     05879071
                    OR PS-SQL-ERROR                                     05879171
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  05879271
               EXEC SQL                                                 05879371
                   SELECT  MITGPL_QTY                                   05879471
                          ,GP_AMT                                       05879571
                     INTO :MITGPL-MITGPL-QTY                            05879671
                         ,:MITGPL-GP-AMT                                05879771
                     FROM  TMITGPL                                      05879871
                    WHERE  OPERCO_NBR = '03'                            05879971
                      AND  MEITGP_NBR = :LS-NEW-MEITGP-NBR              05880071
                      AND  ADLOC_ID   = '   '                           05880171
               END-EXEC                                                 05880271
                                                                        05880371
               EVALUATE TRUE                                            05880471
                   WHEN SQLCODE = ZERO                                  05880571
                   WHEN SQLCODE = -904                                  05880671
                   WHEN SQLCODE = -913                                  05880771
                       CONTINUE                                         05880871
                   WHEN OTHER                                           05880971
                       MOVE '8300-GET-MITGPL'      TO PV-PARAGRAPH-NAME 05881071
                       MOVE 'SELECT FROM TMITGPL'  TO PV-DB2-OPERATION  05881171
                       MOVE LS-NEW-MEITGP-NBR                           05881271
                             TO PV-TMITGPL-KEY-MEITGP-NBR               05881371
                       MOVE PV-TMITGPL-KEY-INFO    TO PV-DB2-KEY-INFO   05881471
                       PERFORM 9998-SQL-ERROR                           05881571
               END-EVALUATE                                             05881671
           END-PERFORM.                                                 05881771
                                                                        05881871
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          05881971
               MOVE '8300-GET-MITGPL'          TO PV-PARAGRAPH-NAME     05882071
               MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-OPERATION      05882171
               MOVE LS-NEW-MEITGP-NBR      TO PV-TMITGPL-KEY-MEITGP-NBR 05882271
               MOVE PV-TMITGPL-KEY-INFO    TO PV-DB2-KEY-INFO           05882371
               PERFORM 9998-SQL-ERROR                                   05882471
           END-IF.                                                      05882571
                                                                        05882671
      *-----------------------------------------------------------------05883071
      * DB2 CALL JOINING IN ALL TABLES TO GET THE KEY INFORMATION       05890067
      *-----------------------------------------------------------------05900067
       8400-GET-ALL-KEY-INFO.                                           05910067
                                                                        05920067
           PERFORM WITH TEST AFTER                                      05930067
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       05940067
                 UNTIL SQLCODE = +0                                     05950067
                    OR PS-SQL-ERROR                                     05960067
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  05970067
               EXEC SQL                                                 05980067
                   SELECT VNDSTL.VS_ID                                  05990067
                         ,VNDSTL.ENT_ID                                 06000067
                         ,EXTENT.DUN_NBR                                06010067
                         ,VNDSTL.VS_NBR                                 06020067
                         ,MDARVS.SUB_CL_MDSEAR_ID                       06030067
                         ,MDARVS.DEPT_NBR                               06040067
                         ,MDARVS.MAJ_CL_NBR                             06050067
                         ,MDARVS.SUB_CL_NBR                             06060067
                         ,MDARVS.RMS_STYL_ID                            06070067
                         ,UPC.UPC_ID                                    06090067
                         ,UPC.INTR_UPC_ID                               06100067
                         ,UPC.UPC_NBR                                   06110067
                         ,UPC.UNT_COST_1ST_AMT                          06120067
                         ,UPC.REGN_PRIC_IND                             06121067
                    INTO :VNDSTL-VS-ID                                  06130067
                        ,:VNDSTL-ENT-ID                                 06140067
                        ,:EXTENT-DUN-NBR                                06150067
                        ,:VNDSTL-VS-NBR                                 06160067
                        ,:MDARVS-SUB-CL-MDSEAR-ID                       06170067
                        ,:MDARVS-DEPT-NBR                               06171067
                        ,:MDARVS-MAJ-CL-NBR                             06172067
                        ,:MDARVS-SUB-CL-NBR                             06173067
                        ,:MDARVS-RMS-STYL-ID                            06210067
                        ,:UPC-UPC-ID                                    06230067
                        ,:UPC-INTR-UPC-ID                               06240067
                        ,:UPC-UPC-NBR                                   06250067
                        ,:UPC-UNT-COST-1ST-AMT                          06260067
                        ,:UPC-REGN-PRIC-IND                             06261067
                   FROM   TUPC    UPC                                   06270067
                         ,TMDARVS MDARVS                                06280067
                         ,TVNDSTL VNDSTL                                06290067
                         ,TEXTENT EXTENT                                06310067
                   WHERE UPC.UPC_ID      = :LS-NEW-UPC-ID               06320067
                     AND UPC.UPC_TYP_CDE = 'I'                          06330067
                     AND MDARVS.SUB_CL_MDSEAR_ID                        06340067
                                         = UPC.SUB_CL_MDSEAR_ID         06341067
                     AND MDARVS.VS_ID    = UPC.VS_ID                    06350067
                     AND VNDSTL.VS_ID    = UPC.VS_ID                    06360067
                     AND EXTENT.ENT_ID   = VNDSTL.ENT_ID                06390067
                                                                        06401067
               END-EXEC                                                 06410067
                                                                        06420067
               EVALUATE TRUE                                            06430067
                   WHEN SQLCODE = ZERO                                  06440067
                   WHEN SQLCODE = -904                                  06450067
                   WHEN SQLCODE = -913                                  06460067
                       CONTINUE                                         06470067
                   WHEN OTHER                                           06480067
                       MOVE '8400-GET-ALL-KEY-INFO' TO PV-PARAGRAPH-NAME06490067
                       MOVE 'SELECT FROM ALL TBLS'  TO PV-DB2-OPERATION 06500067
                       MOVE LS-NEW-UPC-ID      TO PV-UPC-ID-DOUBLE      06501070
                       MOVE PV-UPC-ID-DOUBLE   TO PV-TUPC-KEY-UPC-ID    06502070
                       MOVE PV-TUPC-KEY-INFO   TO PV-DB2-KEY-INFO       06503070
                       PERFORM 9998-SQL-ERROR                           06510067
               END-EVALUATE                                             06520067
           END-PERFORM.                                                 06530067
                                                                        06540067
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          06550067
               MOVE '8400-GET-ALL-KEY-INFO'    TO PV-PARAGRAPH-NAME     06560067
               MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-OPERATION      06570067
               MOVE LS-NEW-UPC-ID       TO PV-UPC-ID-DOUBLE             06571070
               MOVE PV-UPC-ID-DOUBLE    TO PV-TUPC-KEY-UPC-ID           06572070
               MOVE PV-TUPC-KEY-INFO TO PV-DB2-KEY-INFO                 06573070
               PERFORM 9998-SQL-ERROR                                   06580067
           END-IF.                                                      06590067
                                                                        06600067
      *-----------------------------------------------------------------06601067
      * GET THE NEW STATUS CODE DESCRIPTION FROM TUPCSTA                06602067
      *-----------------------------------------------------------------06603067
       8500-GET-TUPCSTA-STAT-CDE-DESC.                                  06604067
                                                                        06605067
           INITIALIZE PV-RETRY-COUNT.                                   06606067
                                                                        06607067
           PERFORM WITH TEST AFTER                                      06608067
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       06609067
                 UNTIL SQLCODE = +0                                     06609167
                    OR PS-SQL-ERROR                                     06609267
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  06609367
               EXEC SQL                                                 06609467
                   SELECT  UPC_STAT_DESC                                06609567
                     INTO :UPCSTA-UPC-STAT-DESC                         06609667
                     FROM  TUPCSTA                                      06609767
                    WHERE  UPC_STAT_CDE = :LS-NEW-UPC-STAT-CDE          06609867
                     WITH UR                                            06609967
               END-EXEC                                                 06610167
                                                                        06610267
               EVALUATE TRUE                                            06610367
                   WHEN SQLCODE = ZERO                                  06610467
                   WHEN SQLCODE = -904                                  06610567
                   WHEN SQLCODE = -913                                  06610667
                       CONTINUE                                         06610767
                   WHEN OTHER                                           06610867
                       MOVE '8500-GET-TUPCSTA-STAT-CDE-DESC'            06610967
                                                   TO PV-PARAGRAPH-NAME 06611067
                       MOVE 'SELECT FROM TUPCSTA'  TO PV-DB2-OPERATION  06611167
                       MOVE LS-NEW-UPC-STAT-CDE                         06611270
                             TO PV-TUPCSTA-KEY-UPC-STAT-CDE             06611370
                       MOVE PV-TUPCSTA-KEY-INFO    TO PV-DB2-KEY-INFO   06611470
                       PERFORM 9998-SQL-ERROR                           06611567
               END-EVALUATE                                             06611667
           END-PERFORM.                                                 06611767
                                                                        06611867
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          06611967
               MOVE '8500-GET-TUPCSTA-STAT-CDE-DESC'                    06612067
                                                 TO PV-PARAGRAPH-NAME   06612167
               MOVE 'MAX RETRIES EXCEEDED'       TO PV-DB2-OPERATION    06612267
               MOVE LS-NEW-UPC-STAT-CDE  TO PV-TUPCSTA-KEY-UPC-STAT-CDE 06612470
               MOVE PV-TUPCSTA-KEY-INFO  TO PV-DB2-KEY-INFO             06612570
               PERFORM 9998-SQL-ERROR                                   06612667
           END-IF.                                                      06612767
                                                                        06612867
      *-----------------------------------------------------------      06613067
      *  INSERT A BUSINESS EVENT RECORD TO THE ITEM EVENT MESSAGE       06620067
      *  BRIDGE DB2 TABLE.                                              06630067
      *-----------------------------------------------------------      06640067
       8900-INSERT-IMT-EVNT-MSG.                                        06650067
                                                                        06660067
           PERFORM WITH TEST AFTER                                      06670067
                 UNTIL SQLCODE = +0                                     06680067
                    OR PS-SQL-ERROR                                     06690067
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  06700067
           EXEC SQL                                                     06710067
                INSERT INTO IMT_EVNT_MSG_BRG                            06720067
                       ( CRE_TMST                                       06730067
                       ,RMS_STYL_ID                                     06740067
                       ,BUS_EVNT_TYP_CDE                                06750067
                       ,MSG_TXT )                                       06760067
                VALUES                                                  06770067
                      ( CURRENT TIMESTAMP                               06780067
                      ,:IMT00001-RMS-STYL-ID                            06790067
                      ,:IMT00001-BUS-EVNT-TYP-CDE                       06800067
                      ,:IMT00001-MSG-TXT)                               06810067
           END-EXEC                                                     06820067
           EVALUATE TRUE                                                06830067
               WHEN SQLCODE = 0                                         06840067
                    CONTINUE                                            06850067
               WHEN SQLCODE = -911                                      06860067
               WHEN SQLCODE = -913                                      06870067
                    ADD +1 TO PV-RETRY-COUNT                            06880067
               WHEN SQLWARN0 NOT = SPACES                               06890067
               WHEN SQLCODE < ZERO                                      06900067
               WHEN SQLCODE > ZERO                                      06910067
                    MOVE '8900-INSERT-IMT-EVNT-MSG'                     06920067
                                   TO PV-PARAGRAPH-NAME                 06930067
                    MOVE 'INSERT INTO IMT_EVNT_MSG_BRG'                 06940067
                                   TO PV-DB2-OPERATION                  06950067
                    PERFORM 9998-SQL-ERROR                              06960067
               END-EVALUATE                                             06970067
           END-PERFORM.                                                 06980067
                                                                        06990067
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          07000067
               MOVE '8900-INSERT-IMT-EVNT-MSG'                          07010067
                                      TO PV-PARAGRAPH-NAME              07020067
               MOVE 'INSERT INTO IMT_EVNT_MSG_BRG'                      07030067
                                      TO PV-DB2-OPERATION               07040067
               PERFORM 9998-SQL-ERROR                                   07050067
           END-IF.                                                      07060067
                                                                        07070067
      *-----------------------------------------------------------------07080067
      * PROCESS DB2 ERROR AND FORCE ROLLBACK                            07090067
      *-----------------------------------------------------------------07100067
       9998-SQL-ERROR.                                                  07110067
                                                                        07120067
           SET PS-SQL-ERROR TO TRUE.                                    07130067
                                                                        07140067
           DISPLAY '** '                                                07150067
                   PC-PGM-NAME                                          07160067
                   ' ABEND IN PARAGRAPH '                               07170067
                   PV-PARAGRAPH-NAME.                                   07180067
           DISPLAY '** '                                                07190067
                   PC-PGM-NAME                                          07200067
                   ' OPERATION = ' PV-DB2-OPERATION.                    07210067
           DISPLAY '** IMKUP022 KEY INFO: ' PV-DB2-KEY-INFO.            07211070
                                                                        07220067
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT     07230067
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.               07240067
           COPY DPPD004.                                                07250067
      *                                                                 07260067
           EXEC SQL                                                     07270067
               ROLLBACK                                                 07280067
           END-EXEC.                                                    07290067
