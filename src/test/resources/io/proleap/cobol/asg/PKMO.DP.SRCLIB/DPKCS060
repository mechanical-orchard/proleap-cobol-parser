000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.                DPKCS060.                                     
000600 AUTHOR.                    LARRY SCHLEICHER - STRATAGEM.                 
000700 INSTALLATION.              KOHLS DEPARTMENT STORES.                      
000800 DATE-WRITTEN.              04-09-97.                                     
000900 DATE-COMPILED.                                                           
001000******************************************************************        
001100*  CICS PROGRAM                                                  *        
001200*                                                                *        
001300*      THIS PROGRAM IS THE PRINTER ID VALIDATION ROUTINE.        *        
001400*      IT WILL VALIDATE IF A REPORT CAN BE PRINTED ON THE        *        
001500*      REQUESTED PRINTER.                                        *        
001100*  CHANGES:                                                      *        
001200*  02/03/2022  REMOVE USE OF TABLE TRPTPGM     CHG0266303        *        
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000******************************************************************        
002100 DATA DIVISION.                                                           
002200******************************************************************        
002300 WORKING-STORAGE SECTION.                                                 
002400 01  PAN-VALET PIC X(28) VALUE 'DPKCS060 START OF W/S'.                   
002500 01  PV-PROGRAM-VARIABLES.                                                
002600     05  PV-CICS-RESPONSE             PIC S9(9)  VALUE +0                 
002700                                                 COMP SYNC.               
002800     05  PV-PRIM-IND                  PIC  X(1).                          
002900 01  PV-CONSTANTS.                                                        
003000     05  PC-A                         PIC  X(1)  VALUE 'A'.               
003100     05  PC-Y                         PIC  X(1)  VALUE 'Y'.               
003200     05  PC-N                         PIC  X(1)  VALUE 'N'.               
003300 01  DN-DB2-NULL-INDICATORS.                                              
003400     05  DN-PRTR-CLUSTER-NBR          PIC S9(04) COMP VALUE +0.           
003500     05  DN-IP-ADDR                   PIC S9(04) COMP VALUE +0.           
003600     05  DN-CLUSTR-CDE                PIC S9(04) COMP VALUE +0.           
003700     05  DN-DAT-XFER-ID               PIC S9(04) COMP VALUE +0.           
003710     05  DN-XMIT-INTFC-CDE            PIC S9(04) COMP VALUE +0.           
003800 01  DK-DB2-KEYS.                                                         
003900     05  DK-OPER-UNT-ID                 PIC S9(04)  VALUE ZERO            
004000                                                    COMP.                 
004100     05  DK-FCLTY-ID                    PIC S9(04)  VALUE ZERO            
004200                                                    COMP.                 
004300*                                                                         
004400*  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA.                       
004500*                                                                         
004600     COPY DPWS013.                                                        
004700                                                                          
004800     COPY DPWS072.                                                        
004900                                                                          
005000*---------------------------------------------------------------*         
005100*    DB2 AREA FOR PRINTER ID TABLE                              *         
005200*---------------------------------------------------------------*         
005300     EXEC SQL                                                             
005400          INCLUDE TPRTRID                                                 
005500     END-EXEC.                                                            
005600                                                                          
005700*---------------------------------------------------------------*         
005800*    DB2 AREA FOR PRINTER STOCK TABLE                           *         
005900*---------------------------------------------------------------*         
006000     EXEC SQL                                                             
006100          INCLUDE TRPTSTK                                                 
006200     END-EXEC.                                                            
006300                                                                          
006400*---------------------------------------------------------------*         
006500*    DB2 AREA FOR PRINTER TRACK TABLE                           *         
006600*---------------------------------------------------------------*         
006700     EXEC SQL                                                             
006800          INCLUDE TPRTRTK                                                 
006900     END-EXEC.                                                            
007000                                                                          
007100*---------------------------------------------------------------*         
007200*    DB2 AREA FOR PRINTER TYPE TABLE                            *         
007300*---------------------------------------------------------------*         
007400     EXEC SQL                                                             
007500          INCLUDE TPRTRTY                                                 
007600     END-EXEC.                                                            
007700                                                                          
007800*---------------------------------------------------------------*         
007900*    DB2 AREA FOR PRINTER CLUSTER TABLE                         *         
008000*---------------------------------------------------------------*         
008100     EXEC SQL                                                             
008200          INCLUDE TPRTRCL                                                 
008300     END-EXEC.                                                            
008400                                                                          
009200*---------------------------------------------------------------*         
009300*  COMMUNICATION AREAS FOR DB2                                            
009400*---------------------------------------------------------------*         
009500     EXEC SQL                                                             
009600         INCLUDE SQLCA                                                    
009700     END-EXEC.                                                            
009800                                                                          
009900******************************************************************        
010000 LINKAGE SECTION.                                                         
010100******************************************************************        
010200 01  DFHCOMMAREA.                                                         
010300     05  FILLER                       OCCURS 1 TO 600 TIMES               
010400                                      DEPENDING ON EIBCALEN.              
010500         10  FILLER                   PIC X.                              
010600                                                                          
010700******************************************************************        
010800 PROCEDURE DIVISION.                                                      
010900 0000-MAINLINE.                                                           
011000                                                                          
011100     MOVE DFHCOMMAREA           TO DP072-COMM-AREA.                       
011200                                                                          
011300     MOVE DP072-ORIGIN-OPER-UNT-ID   TO DK-OPER-UNT-ID.                   
011400     MOVE DP072-ORIGIN-FCLTY-ID      TO DK-FCLTY-ID.                      
011500     INITIALIZE DP072-RETURN-DATA.                                        
011600     IF DP072-VALIDATE                                                    
011700         PERFORM 1000-VALIDATE-PRINTER                                    
011800     ELSE                                                                 
011900         PERFORM 2000-SELECT-PRINTER                                      
012000     END-IF.                                                              
012100     IF SQLCODE = +0  OR                                                  
012200        SQLCODE = -811                                                    
012300         MOVE RPTSTK-STK-TYP-CDE        TO DP072-PRTR-STK-TYP-CDE         
012400         MOVE RPTSTK-PRIM-STK-IND       TO                                
012500                                    DP072-PRTR-PRIM-STOCK-IND             
012600         MOVE PRTRTK-PRTR-OPER-UNT-ID   TO DP072-PRTR-OPER-UNT-ID         
012700         MOVE PRTRTK-PRTR-FCLTY-ID      TO DP072-PRTR-FCLTY-ID            
012800         MOVE PRTRTK-PRTR-TRK-NBR       TO DP072-PRTR-TRACK-NBR           
012900         MOVE PRTRTK-PRTR-TRK-STAT-CDE  TO                                
013000                                    DP072-PRTR-PRTR-TRK-STAT-CDE          
013100         MOVE PRTRID-PRTR-PHY-STAT-CDE  TO DP072-PRTR-STATUS-CDE          
013200         MOVE PRTRID-PRTR-LOC-DESC      TO DP072-PRTR-LOC-DESC            
013300         MOVE PRTRID-PRTR-CLUSTR-NBR    TO DP072-PRTR-CLUSTER-NBR         
013310         MOVE PRTRID-PRTR-TYP-CDE       TO DP072-PRTR-TYP-CDE             
013400         MOVE PRTRTY-PAPR-TRK-QTY       TO DP072-PRTR-TRACK-QTY           
013500         MOVE PRTRTY-AUTO-CTTR-IND      TO                                
013600                                    DP072-PRTR-AUTO-CUTTER-IND            
013700         MOVE PRTRTY-PRT-BRCDE-IND      TO                                
013800                                    DP072-PRTR-PRTR-BRCDE-IND             
013900         MOVE PRTRTY-PRTR-CNTL-IND      TO                                
014000                                    DP072-PRTR-PRTR-CNTL-IND              
014100         IF DN-PRTR-CLUSTER-NBR = -1                                      
014200             MOVE SPACES        TO  DP072-PRTR-IP-ADDRESS                 
014300                                    DP072-PRTR-CLUSTER-OS-RLSE-CDE      E 
014400                                    DP072-PRTR-DATA-XFER-MODE-ID          
014410                                    DP072-XMIT-INTFC-CDE                  
014500         ELSE                                                             
014600             MOVE PRTRCL-IP-ADDR        TO DP072-PRTR-IP-ADDRESS          
014700             MOVE PRTRCL-CLUSTR-OS-RLSE-CDE TO                            
014800                                    DP072-PRTR-CLUSTER-OS-RLSE-CDE      E 
014900             MOVE PRTRCL-DAT-XFER-MODE-ID   TO                            
015000                                    DP072-PRTR-DATA-XFER-MODE-ID          
015010             MOVE PRTRCL-XMIT-INTFC-CDE     TO                            
015020                                    DP072-XMIT-INTFC-CDE                  
015100         END-IF                                                           
015200     END-IF.                                                              
015300     MOVE DP072-COMM-AREA       TO DFHCOMMAREA.                           
015400     EXEC CICS RETURN END-EXEC.                                           
015500     GOBACK.                                                              
015600 1000-VALIDATE-PRINTER.                                                   
015700     MOVE PC-N TO DP072-PRTR-ALT-OPER-FCLTY.                              
015800     PERFORM 1100-SELECT-PRINTER.                                         
015900     IF SQLCODE = +100                                                    
016000         IF DP072-ORIGIN-OPER-UNT-ID NOT = DP072-OPER-UNT-ID OR           
016100            DP072-ORIGIN-FCLTY-ID NOT = DP072-FCLTY-ID                    
016200             MOVE DP072-OPER-UNT-ID   TO DK-OPER-UNT-ID                   
016300             MOVE DP072-FCLTY-ID      TO DK-FCLTY-ID                      
016400             PERFORM 1100-SELECT-PRINTER                                  
016500             IF SQLCODE = +100                                            
016600                 CONTINUE                                                 
016700             ELSE                                                         
016800                 MOVE PC-Y TO DP072-PRTR-ALT-OPER-FCLTY                   
016900             END-IF                                                       
017000         END-IF                                                           
017100     END-IF.                                                              
017200*----------------------------------------------------------------*        
017300*    GET PRINTER TABLE INFO                                      *        
017400*----------------------------------------------------------------*        
017500 1100-SELECT-PRINTER.                                                     
017600                                                                          
017700     EXEC SQL                                                             
017800         SELECT TEMP.STK_TYP_CDE,                                         
017900                TEMP.PRIM_STK_IND,                                        
018000                TEMP.PRTR_OPER_UNT_ID,                                    
018100                TEMP.PRTR_FCLTY_ID,                                       
018200                TEMP.PRTR_TRK_NBR,                                        
018300                TEMP.PRTR_TRK_STAT_CDE,                                   
018400                TEMP.PRTR_CLUSTR_NBR,                                     
018500                TEMP.PRTR_LOC_DESC,                                       
018600                TEMP.PRTR_PHY_STAT_CDE,                                   
018610                TEMP.PRTR_TYP_CDE,                                        
018700                TEMP.PAPR_TRK_QTY,                                        
018800                TEMP.AUTO_CTTR_IND,                                       
018900                TEMP.PRT_BRCDE_IND,                                       
019000                TEMP.PRTR_CNTL_IND,                                       
019100                E.IP_ADDR,                                                
019200                E.CLUSTR_OS_RLSE_CDE,                                     
019300                E.DAT_XFER_MODE_ID,                                       
019310                E.XMIT_INTFC_CDE                                          
019400          INTO  :RPTSTK-STK-TYP-CDE                                       
019500              , :RPTSTK-PRIM-STK-IND                                      
019600              , :PRTRTK-PRTR-OPER-UNT-ID                                  
019700              , :PRTRTK-PRTR-FCLTY-ID                                     
019800              , :PRTRTK-PRTR-TRK-NBR                                      
019900              , :PRTRTK-PRTR-TRK-STAT-CDE                                 
020000              , :PRTRID-PRTR-CLUSTR-NBR    :DN-PRTR-CLUSTER-NBR           
020100              , :PRTRID-PRTR-LOC-DESC                                     
020200              , :PRTRID-PRTR-PHY-STAT-CDE                                 
020210              , :PRTRID-PRTR-TYP-CDE                                      
020300              , :PRTRTY-PAPR-TRK-QTY                                      
020400              , :PRTRTY-AUTO-CTTR-IND                                     
020500              , :PRTRTY-PRT-BRCDE-IND                                     
020600              , :PRTRTY-PRTR-CNTL-IND                                     
020700              , :PRTRCL-IP-ADDR            :DN-IP-ADDR                    
020800              , :PRTRCL-CLUSTR-OS-RLSE-CDE :DN-CLUSTR-CDE                 
020900              , :PRTRCL-DAT-XFER-MODE-ID   :DN-DAT-XFER-ID                
020910              , :PRTRCL-XMIT-INTFC-CDE     :DN-XMIT-INTFC-CDE             
021000            FROM                                                          
021100        (SELECT A.STK_TYP_CDE,                                            
021200                A.PRIM_STK_IND,                                           
021300                B.PRTR_OPER_UNT_ID,                                       
021400                B.PRTR_FCLTY_ID,                                          
021500                B.PRTR_TRK_NBR,                                           
021600                B.PRTR_TRK_STAT_CDE,                                      
021700                C.PRTR_CLUSTR_NBR,                                        
021800                C.PRTR_LOC_DESC,                                          
021900                C.PRTR_PHY_STAT_CDE,                                      
021910                C.PRTR_TYP_CDE,                                           
022000                D.PAPR_TRK_QTY,                                           
022100                D.AUTO_CTTR_IND,                                          
022200                D.PRT_BRCDE_IND,                                          
022300                D.PRTR_CNTL_IND                                           
022400           FROM TRPTSTK A,                                                
022500                TPRTRTK B,                                                
022600                TPRTRID C,                                                
022700                TPRTRTY D                                                 
022800          WHERE A.RPTG_PGM_NM      = :DP072-RPT-PGM-NME                   
022900            AND A.RPTG_PGM_TYP_ID  = :DP072-RPT-PGM-TYP-ID                
023000            AND A.STK_TYP_CDE      = B.STK_TYP_CDE                        
023100            AND B.PRTR_ID          = :DP072-PRINTER-ID                    
023200            AND B.PRTR_OPER_UNT_ID = :DK-OPER-UNT-ID                      
023300            AND B.PRTR_FCLTY_ID    = :DK-FCLTY-ID                         
023400            AND B.PRTR_TRK_STAT_CDE = :PC-A                               
023500            AND B.PRTR_ID          = C.PRTR_ID                            
023600            AND B.PRTR_OPER_UNT_ID = C.PRTR_OPER_UNT_ID                   
023700            AND B.PRTR_FCLTY_ID    = C.PRTR_FCLTY_ID                      
023800            AND C.PRTR_TYP_CDE     = D.PRTR_TYP_CDE )  AS TEMP            
023900      LEFT   JOIN  TPRTRCL E                                              
024000             ON                                                           
024100                TEMP.PRTR_CLUSTR_NBR  = E.PRTR_CLUSTR_NBR    AND          
024200                TEMP.PRTR_OPER_UNT_ID = E.CLUSTR_OPER_UNT_ID AND          
024300                TEMP.PRTR_FCLTY_ID    = E.CLUSTR_FCLTY_ID                 
024400     END-EXEC.                                                            
024500     EVALUATE TRUE                                                        
024600         WHEN SQLCODE = ZERO                                              
024700         WHEN SQLCODE = -811                                              
024800         WHEN SQLCODE = +100                                              
024900              CONTINUE                                                    
025000         WHEN SQLWARN0 NOT = SPACES                                       
025100         WHEN SQLCODE  NOT = ZERO                                         
025200              MOVE '1100-SELECT-PRINTER'                                  
025300                             TO DP013-PARAGRAPH                           
025400              MOVE 'UNSUCCESSFUL SELECT OF PRINTER INFO'                  
025500                             TO DP013-MESSAGE-TEXT (1)                    
025600              MOVE SQLCA     TO DP013-SQLCA                               
025700              MOVE 'TRPTSTK' TO DP013-DB2-TABLE-NAME (1)                  
025800              MOVE 'TPRTRTK' TO DP013-DB2-TABLE-NAME (2)                  
025900              MOVE 'TPRTRID' TO DP013-DB2-TABLE-NAME (3)                  
026000              MOVE 'TPRTRTY' TO DP013-DB2-TABLE-NAME (4)                  
026100              SET DP013-DB2-ABEND                                         
026200                  DP013-XCTL-DISPLAY-RESTART                              
026300                             TO TRUE                                      
026400              PERFORM 9999-ABEND                                          
026500     END-EVALUATE.                                                        
026600                                                                          
026700 2000-SELECT-PRINTER.                                                     
026800     MOVE PC-N TO DP072-PRTR-ALT-OPER-FCLTY.                              
026900     MOVE PC-Y TO PV-PRIM-IND.                                            
027000     PERFORM 2100-SELECT-PRINTER.                                         
027100     IF SQLCODE = +100                                                    
027200         MOVE PC-N TO PV-PRIM-IND                                         
027300         PERFORM 2100-SELECT-PRINTER                                      
027400         IF SQLCODE = +100                                                
027500             MOVE PC-Y TO PV-PRIM-IND                                     
027600             IF DP072-ORIGIN-OPER-UNT-ID NOT = DP072-OPER-UNT-ID          
027700                OR DP072-ORIGIN-FCLTY-ID NOT = DP072-FCLTY-ID             
027800                 MOVE DP072-OPER-UNT-ID   TO DK-OPER-UNT-ID               
027900                 MOVE DP072-FCLTY-ID      TO DK-FCLTY-ID                  
028000                 PERFORM 2100-SELECT-PRINTER                              
028100                 IF SQLCODE = +100                                        
028200                     MOVE PC-N TO PV-PRIM-IND                             
028300                     PERFORM 2100-SELECT-PRINTER                          
028400                     IF SQLCODE = +100                                    
028500                         CONTINUE                                         
028600                     ELSE                                                 
028700                         MOVE PC-Y TO                                     
028800                                   DP072-PRTR-ALT-OPER-FCLTY              
028900                     END-IF                                               
029000                 ELSE                                                     
029100                     MOVE PC-Y TO DP072-PRTR-ALT-OPER-FCLTY               
029200                 END-IF                                                   
029300             END-IF                                                       
029400         END-IF                                                           
029500     END-IF.                                                              
029600                                                                          
029700*----------------------------------------------------------------*        
029800*    GET PRINTER TABLE INFO                                      *        
029900*----------------------------------------------------------------*        
030000 2100-SELECT-PRINTER.                                                     
030100                                                                          
030200     EXEC SQL                                                             
030300         SELECT TEMP.STK_TYP_CDE,                                         
030400                TEMP.PRIM_STK_IND,                                        
030500                TEMP.PRTR_OPER_UNT_ID,                                    
030600                TEMP.PRTR_FCLTY_ID,                                       
030700                TEMP.PRTR_TRK_NBR,                                        
030800                TEMP.PRTR_TRK_STAT_CDE,                                   
030900                TEMP.PRTR_CLUSTR_NBR,                                     
031000                TEMP.PRTR_LOC_DESC,                                       
031100                TEMP.PRTR_PHY_STAT_CDE,                                   
031110                TEMP.PRTR_TYP_CDE,                                        
031200                TEMP.PAPR_TRK_QTY,                                        
031300                TEMP.AUTO_CTTR_IND,                                       
031400                TEMP.PRT_BRCDE_IND,                                       
031500                TEMP.PRTR_CNTL_IND,                                       
031600                E.IP_ADDR,                                                
031700                E.CLUSTR_OS_RLSE_CDE,                                     
031800                E.DAT_XFER_MODE_ID,                                       
031810                E.XMIT_INTFC_CDE                                          
031900          INTO  :RPTSTK-STK-TYP-CDE                                       
032000              , :RPTSTK-PRIM-STK-IND                                      
032100              , :PRTRTK-PRTR-OPER-UNT-ID                                  
032200              , :PRTRTK-PRTR-FCLTY-ID                                     
032300              , :PRTRTK-PRTR-TRK-NBR                                      
032400              , :PRTRTK-PRTR-TRK-STAT-CDE                                 
032500              , :PRTRID-PRTR-CLUSTR-NBR    :DN-PRTR-CLUSTER-NBR           
032600              , :PRTRID-PRTR-LOC-DESC                                     
032700              , :PRTRID-PRTR-PHY-STAT-CDE                                 
032710              , :PRTRID-PRTR-TYP-CDE                                      
032800              , :PRTRTY-PAPR-TRK-QTY                                      
032900              , :PRTRTY-AUTO-CTTR-IND                                     
033000              , :PRTRTY-PRT-BRCDE-IND                                     
033100              , :PRTRTY-PRTR-CNTL-IND                                     
033200              , :PRTRCL-IP-ADDR            :DN-IP-ADDR                    
033300              , :PRTRCL-CLUSTR-OS-RLSE-CDE :DN-CLUSTR-CDE                 
033400              , :PRTRCL-DAT-XFER-MODE-ID   :DN-DAT-XFER-ID                
033410              , :PRTRCL-XMIT-INTFC-CDE     :DN-XMIT-INTFC-CDE             
033500            FROM                                                          
033600        (SELECT A.STK_TYP_CDE,                                            
033700                A.PRIM_STK_IND,                                           
033800                B.PRTR_OPER_UNT_ID,                                       
033900                B.PRTR_FCLTY_ID,                                          
034000                B.PRTR_TRK_NBR,                                           
034100                B.PRTR_TRK_STAT_CDE,                                      
034200                C.PRTR_CLUSTR_NBR,                                        
034300                C.PRTR_LOC_DESC,                                          
034400                C.PRTR_PHY_STAT_CDE,                                      
034410                C.PRTR_TYP_CDE,                                           
034500                D.PAPR_TRK_QTY,                                           
034600                D.AUTO_CTTR_IND,                                          
034700                D.PRT_BRCDE_IND,                                          
034800                D.PRTR_CNTL_IND                                           
034900           FROM TRPTSTK A,                                                
035000                TPRTRTK B,                                                
035100                TPRTRID C,                                                
035200                TPRTRTY D                                                 
035300          WHERE A.RPTG_PGM_NM      = :DP072-RPT-PGM-NME                   
035400            AND A.RPTG_PGM_TYP_ID  = :DP072-RPT-PGM-TYP-ID                
035500            AND A.PRIM_STK_IND     = :PV-PRIM-IND                         
035600            AND A.STK_TYP_CDE      = B.STK_TYP_CDE                        
035700            AND B.PRTR_ID          = :DP072-PRINTER-ID                    
035800            AND B.PRTR_OPER_UNT_ID = :DK-OPER-UNT-ID                      
035900            AND B.PRTR_FCLTY_ID    = :DK-FCLTY-ID                         
036000            AND B.PRTR_TRK_STAT_CDE = :PC-A                               
036100            AND B.PRTR_ID          = C.PRTR_ID                            
036200            AND B.PRTR_OPER_UNT_ID = C.PRTR_OPER_UNT_ID                   
036300            AND B.PRTR_FCLTY_ID    = C.PRTR_FCLTY_ID                      
036400            AND C.PRTR_TYP_CDE     = D.PRTR_TYP_CDE )  AS TEMP            
036500      LEFT   JOIN  TPRTRCL E                                              
036600             ON                                                           
036700                TEMP.PRTR_CLUSTR_NBR  = E.PRTR_CLUSTR_NBR    AND          
036800                TEMP.PRTR_OPER_UNT_ID = E.CLUSTR_OPER_UNT_ID AND          
036900                TEMP.PRTR_FCLTY_ID    = E.CLUSTR_FCLTY_ID                 
037000     END-EXEC.                                                            
037100     EVALUATE TRUE                                                        
037200         WHEN SQLCODE = ZERO                                              
037300         WHEN SQLCODE = -811                                              
037400         WHEN SQLCODE = +100                                              
037500              CONTINUE                                                    
037600         WHEN SQLWARN0 NOT = SPACES                                       
037700         WHEN SQLCODE  NOT = ZERO                                         
037800              MOVE '1100-SELECT-PRINTER'                                  
037900                             TO DP013-PARAGRAPH                           
038000              MOVE 'UNSUCCESSFUL SELECT OF PRINTER INFO'                  
038100                             TO DP013-MESSAGE-TEXT (1)                    
038200              MOVE SQLCA     TO DP013-SQLCA                               
038300              MOVE 'TRPTSTK' TO DP013-DB2-TABLE-NAME (1)                  
038400              MOVE 'TPRTRTK' TO DP013-DB2-TABLE-NAME (2)                  
038500              MOVE 'TPRTRID' TO DP013-DB2-TABLE-NAME (3)                  
038600              MOVE 'TPRTRTY' TO DP013-DB2-TABLE-NAME (4)                  
038700              SET DP013-DB2-ABEND                                         
038800                  DP013-XCTL-DISPLAY-RESTART                              
038900                             TO TRUE                                      
039000              PERFORM 9999-ABEND                                          
039100     END-EVALUATE.                                                        
039200                                                                          
039300 9999-ABEND.                                                              
039400                                                                          
039500     MOVE 'DPKCS060'             TO DP013-PROGRAM.                        
039600                                                                          
039700     SET DP013-LINK-RETURN       TO TRUE.                                 
039800     SET DP013-NO-ROLLBACK       TO TRUE.                                 
039900                                                                          
040000     INITIALIZE                     DP013-SRC-TASK-NUMBER                 
040100                                    DP013-STACK.                          
040200                                                                          
040300     MOVE DFHEIBLK               TO DP013-DFHEIBLK.                       
040400                                                                          
040500     EXEC CICS                                                            
040600         LINK PROGRAM  (DP013-NAME)                                       
040700              COMMAREA (DP013-PARAMETERS)                                 
040800              LENGTH   (DP013-LENGTH)                                     
040900     END-EXEC.                                                            
041000                                                                          
041100     EXEC CICS                                                            
041200         RETURN                                                           
041300     END-EXEC.                                                            
041400                                                                          
