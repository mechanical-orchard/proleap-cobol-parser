      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
                                                                                
       PROGRAM-ID.             SVKUT096.                                        
       AUTHOR.                 JAMES ALT.                                       
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            DECEMBER 2011.                                   
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      * THIS PROGRAM READS THE POS TRANSACTION FILE AND COMBINES ALL  *         
      * OF THE E-COMMERCE TRANSACTIONS INTO ONE BY CARD NUMBER.       *         
      *****************************************************************         
BOPUS *  10/22/2014  INCREASED THE SIZE OF THE INPUT FILE TO HOLD     *         
BOPUS *              THE OMNI CHNL-FFLMT-CDE. 'AFS' IS 'SFS' ORDERS,  *         
BOPUS *              BUT THIS WAS ADDED SO WE COULD SORT AND ROLLUP   *         
BOPUS *              CORRECTLY IN THE FUTURE WHEN WE ADD THE CODE     *         
BOPUS *              TO NOT HAVE 'BPS' IN THE ROLLUP.  RIGHT NOW WE   *         
BOPUS *              NEED 'BPS' IN THE ROLLUP BECAUSE THE REAUTH IS   *         
BOPUS *              NOT BEING DONE. THIS WILL BE CORRECTED IN JAN    *         
BOPUS *              2015.                                            *         
BOPUS *  MADE BY: BARB SCHULTZ                     CHG0094005         *         
      *****************************************************************         
BPS2  *  02/17/2015  ADDED THE CODE TO NOT HAVE 'BPS' IN THE ROLLUP   *         
BPS2  *              LOGIC.                                           *         
BPS2  *  MADE BY: BARB SCHULTZ                     CHG0098856         *         
      *****************************************************************         
P10237*  06/17/2015  COSA DATA SECURITY PROJECT.                      *         
P10237*              CHANGES TO ACCOMMODATE THE ENCRYPTED DL# IN INPUT*         
P10237*              FILE AND TO REMOVE THE CARD NBR DISPLAY AS THESE *         
P10237*              DISPLAYS ARE FOR TESTING PURPOSE AND TO REMOVE   *         
P10237*              THE CREATION OF UPDT-OUT-FILE FILE.              *         
P10237*  MADE BY: COGNIZANT                        CHG0104429         *         
      *****************************************************************         
BOSS  *  01/22/2018  ADDED THE BOSS CODE EVERYWHERE THERE WAS THE     *         
BOSS  *              BPS CODE.                                        *         
BOSS  *  MADE BY: BARB SCHULTZ                     CHG0188487         *         
      *****************************************************************         
P12431*  08/27/2018  MODIFIED THE PROGRAM TO HANDLE NEWLY ADDED       *         
P12431*              ORD_NBR FIELD IN THE SVT TABLES.                 *         
P12431*  MADE BY: COGNIZANT                        CHG0208643         *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
                                                                                
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT COSA-IN-FILE                                                  
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT COSA-OUT-FILE                                                 
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  COSA-IN-FILE                                                         
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
P12431 01  COSA-IN-RECORD  PIC X(179).                                          
                                                                                
       FD  COSA-OUT-FILE                                                        
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
P12431 01  COSA-OUT-RECORD PIC X(179).                                          
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
       01  FILLER                            PIC X(50)  VALUE                   
           ' *** WORKING STORAGE STARTS HERE FOR SVKUT096 *** '.                
                                                                                
      ******************************************************************        
      * PROGRAM SWITCHES                                               *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-COSA-FILE-SW        PIC X(01)  VALUE 'N'.              
               88  PS-END-OF-COSA-FILE                  VALUE 'Y'.              
           05  PS-FIRST-READ-SW              PIC X(01)  VALUE 'Y'.              
               88  PS-FIRST-READ-YES                    VALUE 'Y'.              
               88  PS-FIRST-READ-NO                     VALUE 'N'.              
           05  PS-FIRST-TRXN-SW              PIC X(01)  VALUE 'Y'.              
               88  PS-FIRST-TRXN-YES                    VALUE 'Y'.              
               88  PS-FIRST-TRXN-NO                     VALUE 'N'.              
                                                                                
      ******************************************************************        
      * PROGRAM CONSTANTS                                              *        
      ******************************************************************        
       01  PC-CONSTANTS.                                                        
           05  PC-PROGRAM-NAME             PIC X(08)     VALUE                  
                                                         'SVKUT096'.            
                                                                                
      ******************************************************************        
      * PROGRAM ACCUMULATORS                                           *        
      ******************************************************************        
       01  PA-ACCUMULATORS.                                                     
           05  PA-COSA-FILE-READS      PIC S9(07)       COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-COSA-FILE-WRITES     PIC S9(07)       COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-COSA-FILE-COMBINED   PIC S9(07)       COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-CMBND-AMT            PIC S9(07)V9(02) COMP-3                  
                                                        VALUE ZEROES.           
                                                                                
      ******************************************************************        
      * PROGRAM VARIABLES                                              *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DISPLAY-ASTERISKS    PIC  X(80) VALUE ALL '*'.                
           05  PV-DISPLAY-HEADING.                                              
               10 FILLER               PIC  X(30) VALUE ALL '*'.                
               10 FILLER               PIC  X(20) VALUE                         
                                       '  SVKUT096 COUNTS'.                     
               10 FILLER               PIC  X(30) VALUE ALL '*'.                
           05  PV-DISPLAY-COUNT        PIC  Z,ZZZ,ZZ9.                          
           05  PV-DISPLAY-AMOUNT       PIC  Z,ZZZ,ZZ9.99-.                      
           05  PV-PARAGRAPH-NAME.                                               
               10  PV-PARAGRAPH-NBR    PIC  X(04).                              
               10  FILLER              PIC  X(26).                              
           05  PV-OPERATION-MSG-1      PIC  X(40).                              
           05  PV-OPERATION-MSG-2      PIC  X(40).                              
BOPUS      05  PV-OMNI-CHNL-FFLMT      PIC  X(03) VALUE SPACES.                 
           05  PV-STR-NBR              PIC S9(04) COMP.                         
P12431     05  PV-SPACE-COUNT          PIC S9(04) VALUE +0 COMP SYNC.           
P12431     05  PV-ZERO-COUNT           PIC S9(04) VALUE +0 COMP SYNC.           
P12431     05  PV-ORD-NBR-LEN          PIC S9(04) VALUE +0 COMP SYNC.           
                                                                                
       01  AC-ABEND-CODE               PIC S9(04) VALUE ZERO                    
                                                  COMP SYNC.                    
           88  AC-DB2-ERROR                       VALUE +4013.                  
                                                                                
      ******************************************************************        
      * KMC WORKING STORAGE CONSTANTS                                  *        
      ******************************************************************        
           COPY SVWS004.                                                        
                                                                                
      ******************************************************************        
      * SAVE TRANSACTION FILE LAYOUT (REPLACE PREFIX SV021 WITH PV001) *        
      ******************************************************************        
           COPY SVRD021 REPLACING SV021 BY PV001.                               
                                                                                
      ******************************************************************        
      * TRANSACTION FILE LAYOUT                                        *        
      ******************************************************************        
           COPY SVRD021.                                                        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * MAINLINE PROCESSING                                            *        
      ******************************************************************        
       0000-MAINLINE-PARAGRAPH.                                                 
                                                                                
           PERFORM 1000-INITIALIZE-PROGRAM.                                     
                                                                                
           PERFORM 2000-PROCESS-TRANSACTION-DATA                                
             UNTIL PS-END-OF-COSA-FILE.                                         
                                                                                
           PERFORM 2300-WRITE-COSA-OUT-FILE.                                    
                                                                                
           PERFORM 3000-END-PROGRAM.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      * INITIALIZATION ROUTINES                                        *        
      *  -  OPEN FILES                                                 *        
      *  -  READ FIRST RECORD ON TRANSACTION FILE                      *        
      *  -  CHECK FOR EMPTY INPUT FILE                                 *        
      *  -  SAVE FIRST RECORD                                          *        
      ******************************************************************        
       1000-INITIALIZE-PROGRAM.                                                 
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY PV-DISPLAY-ASTERISKS.                                        
           DISPLAY PV-DISPLAY-HEADING.                                          
                                                                                
           OPEN INPUT  COSA-IN-FILE                                             
                OUTPUT COSA-OUT-FILE.                                           
                                                                                
           INITIALIZE PV001-POS-ACTIVITY-RECORD.                                
                                                                                
           PERFORM 2500-READ-COSA-IN-FILE.                                      
                                                                                
           IF PS-END-OF-COSA-FILE                                               
               DISPLAY '* WARNING:  TRANSACTION FILE WAS EMPTY'                 
               DISPLAY '* NO TRANSACTIONS WERE UPDATED'                         
           ELSE                                                                 
               PERFORM 2400-SAVE-COSA-RECORD                                    
               SET PS-FIRST-READ-YES    TO TRUE                                 
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * COMBINE ALL TRANSACTIONS WITH THE SAME CARD AND ORDER NUMBERS  *        
      * INTO A SINGLE TRANSACTIONS WITH THE AUTH AMT FIELDS ROLLED UP  *        
      ******************************************************************        
       2000-PROCESS-TRANSACTION-DATA.                                           
      * IF THE CURRENT RECORD MATCHES THE PREVIOUS RECORD, ROLL UP THE          
      * THE AUTH AMT FIELDS                                                     
P12431                                                                          
P12431     INITIALIZE PV-SPACE-COUNT.                                           
P12431     INSPECT FUNCTION REVERSE(SV021-ORD-NBR)                              
P12431         TALLYING PV-SPACE-COUNT FOR LEADING SPACES.                      
P12431     INITIALIZE PV-ZERO-COUNT.                                            
P12431     INSPECT SV021-ORD-NBR                                                
P12431       TALLYING PV-ZERO-COUNT FOR LEADING ZEROES.                         
P12431     COMPUTE PV-ORD-NBR-LEN = LENGTH OF SV021-ORD-NBR                     
P12431                           - (PV-ZERO-COUNT + PV-SPACE-COUNT).            
P12431                                                                          
           IF PS-FIRST-READ-NO                                                  
               IF  SV021-CRD-NBR > 0                                            
P12431         AND PV-ORD-NBR-LEN > 0                                           
                   IF  SV021-CRD-NBR = PV001-CRD-NBR                            
                   AND SV021-ORD-NBR = PV001-ORD-NBR                            
BPS2               AND SV021-OMNI-CHNL-FFLMT NOT = 'BPS'                        
BOSS               AND SV021-OMNI-CHNL-FFLMT NOT = 'BOS'                        
BPS2               AND PV001-OMNI-CHNL-FFLMT NOT = 'BPS'                        
BOSS               AND PV001-OMNI-CHNL-FFLMT NOT = 'BOS'                        
                       PERFORM 2100-ROLL-UP-AUTH-AMTS                           
                   ELSE                                                         
                       PERFORM 2300-WRITE-COSA-OUT-FILE                         
                       PERFORM 2400-SAVE-COSA-RECORD                            
                   END-IF                                                       
               ELSE                                                             
                   PERFORM 2300-WRITE-COSA-OUT-FILE                             
                   PERFORM 2400-SAVE-COSA-RECORD                                
               END-IF                                                           
           ELSE                                                                 
               SET PS-FIRST-READ-NO     TO TRUE                                 
           END-IF.                                                              
                                                                                
           PERFORM 2500-READ-COSA-IN-FILE.                                      
                                                                                
      ******************************************************************        
      * ROLL UP THE AUTH AMT FIELDS                                    *        
      ******************************************************************        
       2100-ROLL-UP-AUTH-AMTS.                                                  
                                                                                
           IF PS-FIRST-TRXN-YES                                                 
               SET PS-FIRST-TRXN-NO     TO TRUE                                 
               PERFORM 2110-SETUP-UPDT-OUT-FILE                                 
           END-IF.                                                              
                                                                                
           ADD +1                       TO PA-COSA-FILE-COMBINED.               
                                                                                
           PERFORM 2120-SETUP-UPDT-OUT-FILE.                                    
                                                                                
      ******************************************************************        
      * WRITE OUT UPDATE RECORD FROM SAVED RECORD                      *        
      ******************************************************************        
       2110-SETUP-UPDT-OUT-FILE.                                                
                                                                                
           MOVE PV001-STR-NBR           TO PV-STR-NBR.                          
           MOVE PV001-ORIG-AUTH-AMT     TO PV-DISPLAY-AMOUNT.                   
BOPUS      MOVE PV001-OMNI-CHNL-FFLMT   TO PV-OMNI-CHNL-FFLMT.                  
                                                                                
BOPUS * I WANT OMNI-CHNL-FFLMT-CDE WRITTEN TO OUTPUT RECORD                     
BOPUS *    IF SV021-OMNI-CHNL-FFLMT = 'AFS' OR 'BPS'                            
BOSS       IF SV021-OMNI-CHNL-FFLMT = 'AFS' OR 'BPS' OR 'BOS'                   
BOPUS         MOVE SV021-OMNI-CHNL-FFLMT   TO PV001-OMNI-CHNL-FFLMT             
BOPUS      END-IF.                                                              
                                                                                
P10237*    DISPLAY 'CARD NBR : ', PV-CRD-NBR-12.                                
P10237*    DISPLAY 'STORE NBR: ', PV-STR-NBR.                                   
P10237*    DISPLAY 'ORIG AMT : ', PV-DISPLAY-AMOUNT.                            
P10237*    DISPLAY 'OMNI CHNL: ', PV-OMNI-CHNL-FFLMT.                           
P10237*    DISPLAY 'OMNI CHNL21: ', SV021-OMNI-CHNL-FFLMT.                      
                                                                                
           MOVE ZEROES                  TO PA-CMBND-AMT.                        
                                                                                
           ADD PV001-ORIG-AUTH-AMT      TO PA-CMBND-AMT.                        
                                                                                
      ******************************************************************        
      * WRITE OUT UPDATE RECORD FROM CURRENT RECORD                    *        
      ******************************************************************        
       2120-SETUP-UPDT-OUT-FILE.                                                
                                                                                
           MOVE SV021-STR-NBR           TO PV-STR-NBR.                          
           MOVE SV021-ORIG-AUTH-AMT     TO PV-DISPLAY-AMOUNT.                   
                                                                                
           ADD SV021-APP-AUTH-AMT       TO PV001-APP-AUTH-AMT.                  
           ADD SV021-ORIG-AUTH-AMT      TO PV001-ORIG-AUTH-AMT.                 
           ADD SV021-ORIG-AUTH-AMT      TO PA-CMBND-AMT.                        
                                                                                
           MOVE PA-CMBND-AMT            TO PV-DISPLAY-AMOUNT.                   
                                                                                
      ******************************************************************        
      * WRITE OUT TRANSACTION RECORD                                   *        
      ******************************************************************        
       2300-WRITE-COSA-OUT-FILE.                                                
                                                                                
           WRITE COSA-OUT-RECORD FROM PV001-POS-ACTIVITY-RECORD.                
                                                                                
           ADD +1                       TO PA-COSA-FILE-WRITES.                 
                                                                                
      ******************************************************************        
      * SAVE CURRENT TRANSACTION RECORD                                *        
      ******************************************************************        
       2400-SAVE-COSA-RECORD.                                                   
                                                                                
           MOVE SV021-POS-ACTIVITY-RECORD                                       
                                        TO PV001-POS-ACTIVITY-RECORD.           
           MOVE 0                       TO PA-CMBND-AMT.                        
                                                                                
           SET PS-FIRST-TRXN-YES        TO TRUE.                                
                                                                                
      ******************************************************************        
      * READ IN TRANSACTION RECORD                                     *        
      ******************************************************************        
       2500-READ-COSA-IN-FILE.                                                  
                                                                                
           INITIALIZE SV021-POS-ACTIVITY-RECORD.                                
                                                                                
           READ COSA-IN-FILE INTO SV021-POS-ACTIVITY-RECORD                     
              AT END                                                            
                 SET PS-END-OF-COSA-FILE                                        
                                        TO TRUE                                 
           END-READ.                                                            
                                                                                
           IF PS-END-OF-COSA-FILE                                               
              CONTINUE                                                          
           ELSE                                                                 
              ADD +1                    TO PA-COSA-FILE-READS                   
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * END OF PROGRAM ROUTINES                                        *        
      *  -  DISPLAY COUNTS                                             *        
      *  -  CLOSE FILES                                                *        
      ******************************************************************        
       3000-END-PROGRAM.                                                        
                                                                                
           MOVE PA-COSA-FILE-READS      TO PV-DISPLAY-COUNT.                    
                                                                                
           DISPLAY '* TOTAL COSA RECORDS IN  ' PV-DISPLAY-COUNT.                
                                                                                
           MOVE PA-COSA-FILE-COMBINED   TO PV-DISPLAY-COUNT.                    
                                                                                
           DISPLAY '* TOTAL RECORDS COMBINED ' PV-DISPLAY-COUNT.                
                                                                                
           MOVE PA-COSA-FILE-WRITES     TO PV-DISPLAY-COUNT.                    
                                                                                
           DISPLAY '* TOTAL COSA RECORDS OUT ' PV-DISPLAY-COUNT.                
                                                                                
           DISPLAY PV-DISPLAY-ASTERISKS.                                        
           DISPLAY ' '.                                                         
                                                                                
           CLOSE COSA-IN-FILE                                                   
                 COSA-OUT-FILE.                                                 
                                                                                
      ******************************************************************        
      *  ABEND PROGRAM WHEN A NON SQL-RELATED ERROR OCCURS.                     
      ******************************************************************        
       9999-ABEND-PROGRAM.                                                      
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.               
                                                                                
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                                 
