       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.     MLKUT463.                                                
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/12/2004.                                              
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:                                                                 
      *  THE MODULE WILL TAKE AN INPUT FILE IN THE KDU1806 LAYOUT AND           
      *  PRODUCES ANOTHER FILE FOR REVERSAL OF OUR MERCHANDISE LEDGER           
      *                                                                         
      * CHANGE HISTORY:                                                         
      * CHG ID/                                                                 
      * WR/PROJ DATE       PGMR             DESCRIPTION OF CHANGES              
      * ------- ---------- -------------    ------------------                  
      * ??????  05-12-2004 V. LEE YANG      CONVERT FROM DYL280                 
      *                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT LEDGER-INPUT-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT LEDGER-OUTPUT-FILE                                            
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  LEDGER-INPUT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  LEDGER-INPUT-RECORD      PIC X(065).                                 
                                                                                
       FD  LEDGER-OUTPUT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  LEDGER-OUTPUT-RECORD     PIC X(065).                                 
                                                                                
      /***************************************************************          
       WORKING-STORAGE SECTION.                                                 
      ****************************************************************          
                                                                                
      *01  D040-RECORD.                                                         
           COPY PSWS010.                                                        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW           PIC X(01)       VALUE 'N'.           
               88  PS-NOT-END-OF-FILE                      VALUE 'N'.           
               88  PS-END-OF-FILE                          VALUE 'Y'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RECORD-COUNT             PIC S9(15)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-WRITTEN-RECORDS          PIC S9(15)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-COUNT-DISP               PIC ZZ,ZZZ,ZZZ,ZZZ,ZZ9-              
                                                           VALUE ZEROES.        
           05  PV-RETAIL                   PIC S9(15)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RETAIL-DISP              PIC $ZZ,ZZZ,ZZZ,ZZZ,ZZ9.99-          
                                                           VALUE ZEROES.        
                                                                                
      /***************************************************************          
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS-LOOP                                       
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM 9000-EOJ-TASKS.                                              
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /***************************************************************          
      *1000-INITIALIZATION.                                                     
      ****************************************************************          
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN  INPUT LEDGER-INPUT-FILE                                        
                OUTPUT LEDGER-OUTPUT-FILE.                                      
                                                                                
           SET PS-NOT-END-OF-FILE   TO TRUE.                                    
           MOVE ZEROES              TO PV-RECORD-COUNT                          
                                       PV-WRITTEN-RECORDS                       
                                       PV-RETAIL.                               
                                                                                
           PERFORM 8000-READ-INPUT-FILE.                                        
                                                                                
      /***************************************************************          
      *2000-MAIN-PROCESS-LOOP.                                                  
      ****************************************************************          
       2000-MAIN-PROCESS-LOOP.                                                  
                                                                                
           ADD 1                    TO PV-RECORD-COUNT.                         
                                                                                
           COMPUTE D040-SALE-AMT = -1 * D040-SALE-AMT                           
                                                                                
           COMPUTE PV-RETAIL     = PV-RETAIL                                    
                                 + D040-SALE-AMT * D040-SALE-UNITS.             
                                                                                
      ***  CREATE THE REVERSAL....                                              
           WRITE LEDGER-OUTPUT-RECORD FROM D040-RECORD.                         
           ADD 1                TO PV-WRITTEN-RECORDS.                          
                                                                                
      ***  READ NEXT RECORD....                                                 
           PERFORM 8000-READ-INPUT-FILE.                                        
                                                                                
      ******************************************************************        
      *8000-READ-INPUT-FILE.                                                    
      * GET THE MBS INFO FROM THE MOST RECENT ROW FOR THE DEPARTMENT.           
      ******************************************************************        
       8000-READ-INPUT-FILE.                                                    
                                                                                
           READ LEDGER-INPUT-FILE NEXT                                          
               INTO D040-RECORD                                                 
                   AT END  MOVE 'Y'     TO PS-END-OF-FILE-SW.                   
                                                                                
      ******************************************************************        
      *9000-EOJ-TASKS.                                                          
      ******************************************************************        
       9000-EOJ-TASKS.                                                          
                                                                                
           DISPLAY '*********************************'.                         
           DISPLAY '**  CONTROL SUMMARY - MLKUT463  **'.                        
           MOVE PV-RECORD-COUNT     TO PV-COUNT-DISP.                           
           DISPLAY '* REVERSAL RECS READS   = ' PV-COUNT-DISP.                  
           MOVE PV-WRITTEN-RECORDS  TO PV-COUNT-DISP.                           
           DISPLAY '*               WRITTEN = ' PV-COUNT-DISP.                  
           MOVE PV-RETAIL           TO PV-RETAIL-DISP.                          
           DISPLAY '*         RETAIL AMOUNT = ' PV-RETAIL-DISP.                 
           DISPLAY '*********************************'.                         
                                                                                
           CLOSE LEDGER-INPUT-FILE                                              
                 LEDGER-OUTPUT-FILE.                                            
                                                                                
