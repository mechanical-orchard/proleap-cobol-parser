       IDENTIFICATION DIVISION.                                         00010093
       PROGRAM-ID.           PXKUT125.                                  00020075
       AUTHOR.               COGNIZANT.                                 00030068
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00040001
       DATE-WRITTEN          05/17/2013.                                00050041
       DATE-COMPILED.                                                   00060001
                                                                        00070001
      ******************************************************************00080001
      *   PXKUT125 - POSTPONE PRICE CHANGE EVENTS IN ORACLE             00090075
      *                                                                 00100001
      *   THIS PROGRAM SEQUENTIALLY READS ACTIVE EVENTS FROM PXT_EVNT   00110070
      *   TABLE AND XST_PRCHG TABLE TO FIND OUT THE POSTPONED EVENTS    00120081
      *   FOR A PARTICULAR PRCHG_ID AND PRCHG_EFF_DTE.                  00130081
      ******************** HISTORY OF CHANGES **************************00150001
      * CHG ID/                                                        *00160001
      * WR/PROJ          DATE        DESCRIPTION OF CHANGES            *00170040
      * ---------------  ----------  --------------------------------  *00180040
      *                              ORIGINAL CODE                      00181073
      ******************************************************************00190001
                                                                        00200001
       ENVIRONMENT DIVISION.                                            00210001
                                                                        00220001
       CONFIGURATION SECTION.                                           00230001
                                                                        00240001
       SOURCE-COMPUTER. IBM-3090.                                       00250001
       OBJECT-COMPUTER. IBM-3090.                                       00260001
                                                                        00270001
       INPUT-OUTPUT SECTION.                                            00280001
       FILE-CONTROL.                                                    00290001
           SELECT INPUT-FILE                                            00300041
               ASSIGN TO INP01.                                         00310001
                                                                        00320001
           SELECT OUTPUT-FILE                                           00330041
               ASSIGN TO OUT01.                                         00340041
                                                                        00350001
       DATA DIVISION.                                                   00420001
                                                                        00430001
       FILE SECTION.                                                    00440001
       FD  INPUT-FILE                                                   00450041
           LABEL RECORDS ARE STANDARD                                   00460005
           RECORDING MODE IS F                                          00470005
           BLOCK CONTAINS 0 RECORDS.                                    00480005
       01  INPUT-RECORD                  PIC X(80).                     00490089
                                                                        00500001
       FD  OUTPUT-FILE                                                  00510041
           LABEL RECORDS ARE STANDARD                                   00520001
           RECORDING MODE IS F                                          00530011
           BLOCK CONTAINS 0 RECORDS.                                    00540001
       01  OUTPUT-RECORD                 PIC X(80).                     00550089
                                                                        00560001
       WORKING-STORAGE SECTION.                                         00690001
                                                                        00700001
      ***************************************************************** 00710001
      *  INPUT RECORD LAYOUT                                            00720040
      ***************************************************************** 00730001
       01  PL-ORACLE-RECORD.                                            00740089
           05 PL-IN-EVNT-DTE             PIC  X(10)  VALUE SPACES.      00750489
           05 FILLER                     PIC  X(01)  VALUE SPACES.      00750589
           05 PL-IN-EVNT-CTG-CDE         PIC  X(03)  VALUE SPACES.      00751089
           05 FILLER                     PIC  X(01)  VALUE SPACES.      00751189
           05 PL-IN-PRCHG-TYP-CDE        PIC  X(03)  VALUE SPACES.      00751289
           05 FILLER                     PIC  X(62)  VALUE SPACES.      00751389
                                                                        00751441
      ***************************************************************** 00752044
      *  OUTPUT RECORD LAYOUT                                           00753044
      ******************************************************************00754044
       01  PL-OUTPUT-RECORD.                                            00755089
           05 FILLER                     PIC  X(02)  VALUE '..'.        00755189
           05 FILLER                     PIC  X(01)  VALUE 'C'.         00755289
           05 PL-OUT-PRCHG-TYP-CDE       PIC  X(03)  VALUE SPACES.      00756089
           05 PL-OUT-EVNT-CTG-CDE        PIC  X(03)  VALUE SPACES.      00757089
           05 PL-OUT-PRCHG-EFF-DTE       PIC  X(10)  VALUE SPACES.      00758089
           05 FILLER                     PIC  X(20)  VALUE              00758289
                                         '.. 00000.. 00000000 '.        00758389
           05 FILLER                     PIC  X(41)  VALUE              00758489
                     '.........................................'.       00759086
                                                                        00760070
      ***************************************************************** 01020003
      *  PROGRAM'S ACCUMULATORS                                       * 01030003
      ***************************************************************** 01040003
       01  PA-PROGRAM-ACCUMULATORS.                                     01050003
           05  PA-RECORDS-READ           PIC 9(10)   VALUE ZEROES.      01103089
                                                                        01130001
      ***************************************************************** 01140001
      *  PROGRAM'S CONSTANTS                                          * 01150001
      ***************************************************************** 01160001
       01  PC-PROGRAM-CONSTANTS.                                        01170001
           05  PC-MAX-DEADLOCKS          PIC S9(04)  VALUE +3.          01180089
           05  PC-PROGRAM-NAME           PIC X(08)   VALUE              01190089
               'PXKUT125'.                                              01200075
                                                                        01230001
      ***************************************************************** 01240001
      *  PROGRAM'S SWITCHES                                           * 01250001
      ***************************************************************** 01260001
       01  PS-PROGRAM-SWITCHES.                                         01270001
           05  PS-END-OF-INP-FILE-SW     PIC X(01)   VALUE 'N'.         01280089
               88  PS-END-OF-INP-FILE                VALUE 'Y'.         01290089
               88  PS-NOT-END-OF-INP-FILE            VALUE 'N'.         01300089
           05  PS-ROW-FOUND-SW           PIC X(01)   VALUE 'N'.         01334089
               88  PS-ROW-FOUND                      VALUE 'Y'.         01335089
               88  PS-ROW-NOT-FOUND                  VALUE 'N'.         01336089
                                                                        01337045
      ***************************************************************** 01338045
      *  PROGRAM'S COUNTERS                                             01339045
      ***************************************************************** 01340045
       01  PC-PROGRAM-COUNTERS.                                         01350045
           05  PC-OUTPUT-RECORDS-WRITTEN PIC 9(06)   VALUE ZEROS.       01360089
                                                                        01370045
      ***************************************************************** 01700001
      *  PROGRAM'S VARIABLES                                          * 01710001
      ***************************************************************** 01720001
       01  PV-PROGRAM-VARIABLES.                                        01730001
           05  PV-PRCHG-EFF-DTE          PIC  X(10)  VALUE SPACES.      01730189
           05  PV-PARAGRAPH-NAME         PIC  X(40)  VALUE SPACES.      01750089
           05  PV-ACTIVE-ROW-COUNT       PIC S9(09)  COMP-3 VALUE +0.   01750189
           05  PV-EVNT-CDE               PIC  X(03)  VALUE SPACES.      01751089
           05  PV-EVNT-CTG-CDE           PIC  X(03)  VALUE SPACES.      01752089
           05  PV-PRCHG-TYP-CDE          PIC  X(03)  VALUE SPACES.      01753089
           05  PV-EVNT-DTE               PIC  X(10)  VALUE SPACES.      01801189
                                                                        01821724
      ***************************************************************** 01823001
      *  ABEND CODE AREA                                              * 01830001
      ***************************************************************** 01840001
       01  ABEND-CODE                    PIC S9(04)  VALUE +0  COMP.    01850089
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.     01950064
                                                                        01980001
      ***************************************************************** 02041041
      *  ABEND ROUTINE INFORMATION                                      02042041
      ***************************************************************** 02043041
           COPY DPWS900.                                                02044049
                                                                        02045041
      ***************************************************************** 02046041
      *  ABEND ROUTINE INFORMATION                                      02047041
      ***************************************************************** 02048041
           COPY DPWS004.                                                02049049
                                                                        02049141
      ************************************************************      02049241
      * DB2 MESSAGE AREA                                                02049341
      ************************************************************      02049441
           COPY SQLCA2.                                                 02049541
                                                                        02049641
      ************************************************************      02049741
      * DB2 COMMUNICATIONS AREA                                         02049841
      ************************************************************      02049941
           EXEC SQL                                                     02050049
             INCLUDE SQLCA                                              02050149
           END-EXEC.                                                    02050249
                                                                        02050341
      ************************************************************      02050441
      * DB2 TABLE XST_PRCHG (XST00063)                                  02050541
      ************************************************************      02050641
           EXEC SQL                                                     02050741
             INCLUDE XST00063                                           02050841
           END-EXEC.                                                    02050941
                                                                        02051041
                                                                        02052066
      ************************************************************      02052166
      * DB2 TABLE XST_STR_GP_PRCHG (XST00064)                           02052270
      ************************************************************      02052366
           EXEC SQL                                                     02052466
             INCLUDE XST00064                                           02052566
           END-EXEC.                                                    02052666
                                                                        02052766
       PROCEDURE DIVISION.                                              02570001
                                                                        02580001
      ******************************************************************02590001
      *    MAINLINE LOGIC                                              *02600001
      ******************************************************************02610001
                                                                        02620001
       0000-MAINLINE.                                                   02630001
                                                                        02640001
           PERFORM 1000-INITIALIZATION.                                 02650001
                                                                        02660001
           PERFORM 2000-PROCESS-INPUT                                   02670001
               UNTIL PS-END-OF-INP-FILE.                                02680040
                                                                        02690001
           PERFORM 9000-EOJ-LOGIC.                                      02700089
                                                                        02710001
           GOBACK.                                                      02720001
                                                                        02730090
      ******************************************************************02740091
      *  INITIALIZE THE VARIABLES AND OPEN THE INPUT OUTPUT FILES       02750091
      ******************************************************************02760091
       1000-INITIALIZATION.                                             02770001
                                                                        02780001
           OPEN INPUT  INPUT-FILE.                                      02790042
           OPEN OUTPUT OUTPUT-FILE.                                     02800044
                                                                        03150001
           PERFORM 7000-READ-INPUT-FILE.                                03160090
                                                                        03161070
           IF PS-END-OF-INP-FILE                                        03170070
               DISPLAY ' *** NOTHING TO PROCESS ***'                    03180070
               DISPLAY ' '                                              03190070
           END-IF.                                                      03200070
                                                                        03210070
      ******************************************************************03780001
      *    2000-PROCESS-INPUT                                          *03790001
      *      THIS IS PERFORMED ONLY VIA THE 0000-MAINLINE              *03800001
      *      PURPOSE: READ AND PROCESS THE FILES EXTRACTED IN JOBS     *03810023
      *               PXK001                                           *03811090
      ******************************************************************03830001
       2000-PROCESS-INPUT.                                              03840001
                                                                        03850001
           MOVE ZEROES                     TO PV-ACTIVE-ROW-COUNT.      03891090
           MOVE PL-IN-EVNT-CTG-CDE         TO PV-EVNT-CTG-CDE.          03892090
           MOVE PL-IN-EVNT-DTE             TO PV-EVNT-DTE.              03893090
           MOVE PL-IN-PRCHG-TYP-CDE        TO PV-PRCHG-TYP-CDE.         03894090
           PERFORM 8000-PROCESS-UNIQUE-MARKS.                           03900090
           IF PV-ACTIVE-ROW-COUNT = 0                                   03901090
              INITIALIZE PL-OUTPUT-RECORD                               03901190
              MOVE PV-EVNT-CTG-CDE         TO PL-OUT-EVNT-CTG-CDE       03901290
              MOVE PV-PRCHG-TYP-CDE        TO PL-OUT-PRCHG-TYP-CDE      03901390
              MOVE PV-EVNT-DTE             TO PL-OUT-PRCHG-EFF-DTE      03901490
              PERFORM 7100-WRITE-OUTPUT                                 03902091
           END-IF.                                                      03903090
                                                                        03904090
           PERFORM 7000-READ-INPUT-FILE.                                03910090
                                                                        03920090
      ******************************************************************03990001
      *  READ INPUT FILE                                                04000057
      ******************************************************************04010001
       7000-READ-INPUT-FILE.                                            04020090
                                                                        04030001
           READ INPUT-FILE INTO PL-ORACLE-RECORD                        04040089
             AT END                                                     04050089
                SET PS-END-OF-INP-FILE     TO TRUE.                     04051090
                                                                        04060001
           IF PS-NOT-END-OF-INP-FILE                                    04061090
               ADD 1 TO PA-RECORDS-READ                                 04062090
           END-IF.                                                      04063090
                                                                        04064090
      ******************************************************************04065091
      *  WRITES OUTPUT FILE WHEN THE COUNT OF RECORD IS ZERO            04066091
      ******************************************************************04067091
       7100-WRITE-OUTPUT.                                               04068091
                                                                        04069091
           WRITE OUTPUT-RECORD             FROM PL-OUTPUT-RECORD.       04069191
           ADD 1                           TO PC-OUTPUT-RECORDS-WRITTEN.04069291
                                                                        04069391
      ******************************************************************04070090
      *    8000-PROCESS-UNIQUE-MARKS                                   *04080090
      *      DB2 QUERY TO EXTRACT ACTIVE EVENTS FROM XST_STR_GP_PRCHG, *04090092
      *      XST_PRCHG TABLE TO FIND OUT THE POSTPONED EVENTS FOR A    *04100090
      *      PARTICULAR PRCHG_ID AND PRCHG_EFF_DTE.                    *04110090
      ******************************************************************04120090
       8000-PROCESS-UNIQUE-MARKS.                                       04400090
                                                                        04410001
           MOVE '8000-PROCESS-UNIQUE-MARKS'                             04420090
                                           TO PV-PARAGRAPH-NAME.        04420190
                                                                        04421455
           EXEC SQL                                                     04422055
              SELECT COUNT(*)                                           04423055
                INTO :PV-ACTIVE-ROW-COUNT                               04423155
                FROM XST_STR_GP_PRCHG STRGP                             04424067
                WHERE  PRCHG_EFF_DTE = :PV-EVNT-DTE                     04441055
                   AND LAST_ACTN_CDE <> 'D'                             04442055
                   AND EXISTS                                           04490155
                   (SELECT 1                                            04490258
                      FROM XST_PRCHG PRCHG                              04490367
                     WHERE PRCHG.PRCHG_ID = STRGP.PRCHG_ID              04490455
                       AND PRCHG.EVNT_CTG_CDE = :PV-EVNT-CTG-CDE        04490555
                       AND PRCHG.PRCHG_TYP_CDE  = :PV-PRCHG-TYP-CDE     04490655
                       AND PRCHG.PRCHG_ST_CDE = 'APR'                   04500055
                       AND PRCHG.LAST_ACTN_CDE <> 'D')                  04501055
               WITH UR                                                  04510055
           END-EXEC.                                                    04511055
                                                                        04512055
           EVALUATE TRUE                                                04513055
               WHEN SQLCODE = ZERO                                      04517055
                   SET PS-ROW-FOUND        TO TRUE                      04518090
               WHEN OTHER                                               04519163
                   DISPLAY '** PL-IN-EVNT-DTE: '      PV-EVNT-DTE       04519290
                   DISPLAY '** PL-IN-EVNT-CTG-CDE: '  PV-EVNT-CTG-CDE   04519390
                   DISPLAY '** PL-IN-PRCHG-TYP-CDE: ' PV-PRCHG-TYP-CDE  04519489
                   PERFORM 9990-SQL-ERROR                               04519556
           END-EVALUATE.                                                04519660
                                                                        04644045
      ******************************************************************05690001
      *    9000-EOJ-LOGIC.                                              05700089
      *      THIS IS PERFORMED AT END OF INPUT FILE.                    05710001
      *      PURPOSE: CLOSE INPUT FILE,OUTPUT FILE                      05720057
      *      DISPLAY RECORD COUNTS.                                     05730001
      ******************************************************************05740001
       9000-EOJ-LOGIC.                                                  05750089
                                                                        05750733
           CLOSE INPUT-FILE.                                            05780040
           CLOSE OUTPUT-FILE.                                           05790045
                                                                        05810001
           DISPLAY ' '.                                                 05820001
           DISPLAY 'RECORDS READ       ' PA-RECORDS-READ.               05830026
           DISPLAY 'RECORDS WRITTEN    ' PC-OUTPUT-RECORDS-WRITTEN.     05840091
           DISPLAY '**------ END ' PC-PROGRAM-NAME ' ------**'.         05850026
                                                                        05910001
      ******************************************************************05920001
      * DISPLAYS ERRORS ENCOUNTERED ON SQL REQUEST                      05930001
      ******************************************************************05940001
       9990-SQL-ERROR.                                                  05950001
                                                                        05960001
           SET ABEND-4013-DB2-ERROR         TO TRUE.                    05970044
           DISPLAY '**  ABEND          **'.                             05980044
           DISPLAY '**  PROGRAM        **  = ' PC-PROGRAM-NAME.         05990061
           DISPLAY '**  PARAGRAPH      **  = ' PV-PARAGRAPH-NAME.       06000044
                                                                        06020044
           COPY DPPD004.                                                06060056
                                                                        06061044
           CALL 'ILBOABN0'  USING ABEND-CODE.                           06080062
                                                                        06240005
