       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    INKUT292.                                         00020000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030000
       DATE-WRITTEN.  12-06-2001.                                       00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *     INKUT292 - CREATE A SORT CARD FOR SPLITTING UP THE          00080000
      *                STORE DEPARTMENT RANKING FILE FOR THE EXCEL      00090000
      *                FTP                                              00100000
      *                                                                 00110000
      *CHANGE HISTORY:                                                  00120000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00130000
      * -------  ----------  ----------------------------------------   00140000
      *          12-06-2001  (CREATION...)  -RONNA BUTZ                 00150000
W26600*                                                                 00151000
W26600* W26600   04-  -2004  STORE EXPANSION                            00153000
W26600*                      M JOHNSON                                  00154000
W26600*                                                                 00155000
      ******************************************************************00160000
                                                                        00170000
       ENVIRONMENT DIVISION.                                            00180000
       CONFIGURATION SECTION.                                           00190000
       SOURCE-COMPUTER.        IBM-3090.                                00200000
       OBJECT-COMPUTER.        IBM-3090.                                00210000
       INPUT-OUTPUT SECTION.                                            00220000
       FILE-CONTROL.                                                    00230000
                                                                        00240000
           SELECT DEPT-STR-RANKING-FILE-IN                              00250000
               ASSIGN TO INP01.                                         00260000
                                                                        00270000
           SELECT SORT-CARD-FILE-OUT                                    00280000
               ASSIGN TO OUT01.                                         00290000
                                                                        00300000
       DATA DIVISION.                                                   00310000
       FILE SECTION.                                                    00320000
                                                                        00330000
       FD  DEPT-STR-RANKING-FILE-IN                                     00340000
           LABEL RECORDS ARE OMITTED                                    00350000
           RECORDING MODE IS F                                          00360000
           BLOCK CONTAINS 0 RECORDS.                                    00370000
W26600 01  DEPT-STR-RANKING-REC-IN PIC X(145).                          00380000
                                                                        00390000
       FD  SORT-CARD-FILE-OUT                                           00400000
           LABEL RECORDS ARE OMITTED                                    00410000
           RECORDING MODE IS F                                          00420000
           BLOCK CONTAINS 0 RECORDS.                                    00430000
       01  SORT-CARD-REC-OUT       PIC X(80).                           00440000
                                                                        00450000
      ******************************************************************00460000
       WORKING-STORAGE SECTION.                                         00470000
      ******************************************************************00480000
                                                                        00490000
       01  PC-PROGRAM-CONSTANTS.                                        00500000
           05  PC-PROGRAM-NAME         PIC X(20)  VALUE 'INKUT292'.     00510000
W26600     05  PC-STORE-MAX            PIC 9(05)  VALUE 9999.           00520000
           05  PC-GE                   PIC X(02)  VALUE 'GE'.           00530000
           05  PC-LE                   PIC X(02)  VALUE 'LE'.           00540000
           05  PC-NE                   PIC X(02)  VALUE 'NE'.           00550000
           05  PC-EQ                   PIC X(02)  VALUE 'EQ'.           00560000
           05  PC-AND                  PIC X(06)  VALUE ''',AND,'.      00570000
           05  PC-OR                   PIC X(06)  VALUE ''',OR, '.      00580000
           05  PC-CLOSE-PARENTH        PIC X(06)  VALUE ''')    '.      00590000
           05  PC-ONE                  PIC X(01)  VALUE '1'.            00600000
           05  PC-TWO                  PIC X(01)  VALUE '2'.            00610000
           05  PC-THREE                PIC X(01)  VALUE '3'.            00620000
           05  PC-FOUR                 PIC X(01)  VALUE '4'.            00630000
           05  PC-1                    PIC 9(01)  VALUE 1.              00640000
           05  PC-2                    PIC 9(01)  VALUE 2.              00650000
           05  PC-3                    PIC 9(01)  VALUE 3.              00660000
           05  PC-INCLUDE-LINE              PIC X(14) VALUE             00670000
               '     INCLUDE=('.                                        00680000
           05  PC-INCLUDE-LINE-BLANK        PIC X(14) VALUE             00690000
               '              '.                                        00700000
                                                                        00710000
       01  PV-PROGRAM-VARIABLES.                                        00720000
W26600     05  PV-SAVE-STORE           PIC X(05)  VALUE SPACES.         00730000
           05  PV-STORE-COUNT          PIC 9(05)  VALUE ZEROS.          00740000
W26600     05  PV-STORE-NBR            PIC 9(05)  VALUE ZEROS.          00750000
           05  PV-STORE-SECTION        PIC 9(03)  VALUE ZEROS.          00760000
           05  PV-STORE-QUOTIENT       PIC 9(03)  VALUE ZEROS.          00770000
           05  PV-STORE-REMAINDER      PIC 9(02)  VALUE ZEROS.          00780000
           05  PV-RECS-READ            PIC 9(15)  VALUE ZEROS.          00790000
                                                                        00800000
       01  SS-SUBSCRIPTS.                                               00810000
           05  SS-STORE                PIC S9(04) COMP SYNC             00820000
                                                  VALUE ZERO.           00830000
       01  PS-PROGRAM-SWITCHES.                                         00840000
           05  PS-DEPT-STR-RANKING     PIC  X(01) VALUE 'N'.            00850000
               88  PS-EOF-DEPT-STR-RANKING        VALUE 'Y'.            00860000
               88  PS-NOT-EOF-DEPT-STR-RANKING    VALUE 'N'.            00870000
                                                                        00880000
       01  ST-STORE-TABLE.                                              00890000
           05  ST-STORE OCCURS 9999.                                    00900000
               10  ST-STORE-NBR        PIC X(05)  VALUE SPACES.         00910000
                                                                        00920000
      /**************************************************************** 00930000
      * INPUT FILE LAYOUT FOR THE STORE PART OF THE FILE                00940000
      ***************************************************************** 00950000
       01  IN-FILE-REC.                                                 00960000
           05  IN-FILLER1              PIC X(25)  VALUE SPACES.         00970000
W26600     05  IN-STORE                PIC X(05)  VALUE SPACES.         00980000
W26600     05  IN-FILLER2              PIC X(115) VALUE SPACES.         00990000
                                                                        01000000
      /**************************************************************** 01010000
      * DETAIL RECORDS FOR THE CONTROL CARD.                            01020000
      ***************************************************************** 01030000
       01  DL-DETAIL-LINE1.                                             01040000
           05  FILLER                       PIC X(40) VALUE             01050000
W26600         '  SORT FIELDS=(26,5,AQ,A,124,16,ASL,D)  '.              01060000
           05  FILLER                       PIC X(40) VALUE SPACES.     01070000
                                                                        01080000
       01  DL-DETAIL-LINE2.                                             01090000
           05  FILLER                       PIC X(40) VALUE             01100000
               '  ALTSEQ CODE=(40FA)                    '.              01110000
           05  FILLER                       PIC X(40) VALUE SPACES.     01120000
                                                                        01130000
       01  DF-DETAIL-FILE-NUMBER.                                       01140000
           05  FILLER                       PIC X(02) VALUE SPACES.     01150000
           05  FILLER                       PIC X(14) VALUE             01160000
               ' OUTFIL FILES='.                                        01170000
           05  DF-FILE-NBR                  PIC X(01) VALUE SPACES.     01180000
           05  FILLER                       PIC X(23) VALUE             01190000
                               ',                      '.               01200000
           05  FILLER                       PIC X(40) VALUE SPACES.     01210000
                                                                        01220000
       01  DI-DETAIL-INCLUDE.                                           01230000
           05  FILLER                       PIC X(02) VALUE SPACES.     01240000
           05  DI-INCLUDE                   PIC X(14) VALUE SPACES.     01250000
           05  FILLER                       PIC X(08) VALUE             01260000
W26600                       '26,5,CH,'.                                01270000
           05  DI-COND                      PIC X(02) VALUE SPACES.     01280000
           05  FILLER                       PIC X(03) VALUE             01290000
                                        ',C'''.                         01300000
           05  DI-STORE-BOUNDARY            PIC X(05) VALUE SPACES.     01310000
           05  DI-AND-OR-COND               PIC X(06) VALUE SPACES.     01320000
           05  FILLER                       PIC X(02) VALUE SPACES.     01330000
           05  FILLER                       PIC X(38) VALUE SPACES.     01340000
                                                                        01350000
       01  DL-DETAIL-LINE-END.                                          01360000
           05  FILLER                       PIC X(40) VALUE             01370000
               '      END                               '.              01380000
           05  FILLER                       PIC X(40) VALUE SPACES.     01390000
                                                                        01400000
       01  ABEND-CODE                       PIC S9(04) COMP             01410000
                                                       VALUE ZERO.      01420000
                                                                        01430000
      /*****************************************************************01440000
      ******************************************************************01450000
       PROCEDURE DIVISION.                                              01460000
      ******************************************************************01470000
                                                                        01480000
       0100-MAINLINE-INKUT292.                                          01490000
                                                                        01500000
           PERFORM 1000-INITIALIZATION.                                 01510000
                                                                        01520000
           PERFORM 2000-STORE-RANKING-RECS                              01530000
               UNTIL PS-EOF-DEPT-STR-RANKING.                           01540000
                                                                        01550000
           IF PV-STORE-COUNT > 0                                        01560000
               PERFORM 3000-BUILD-SORT-CARD                             01570000
           END-IF.                                                      01580000
                                                                        01590000
           PERFORM 8000-EOJ.                                            01600000
                                                                        01610000
       9999-EXIT-PROGRAM.                                               01620000
           STOP RUN.                                                    01630000
                                                                        01640000
      /*****************************************************************01650000
      * 1000-INITIALIZATION.                                            01660000
      ******************************************************************01670000
       1000-INITIALIZATION.                                             01680000
                                                                        01690000
           DISPLAY '******************************************'.        01700000
           DISPLAY '*  START OF INKUT292                     *'.        01710000
           DISPLAY '******************************************'.        01720000
                                                                        01730000
           OPEN  INPUT DEPT-STR-RANKING-FILE-IN                         01740000
                OUTPUT SORT-CARD-FILE-OUT.                              01750000
                                                                        01760000
           PERFORM 4000-READ-STR-RANKING-FILE.                          01770000
           IF PS-EOF-DEPT-STR-RANKING                                   01780000
               DISPLAY '*************************************'          01790000
               DISPLAY '**  PROGRAM = INKUT292'                         01800000
               DISPLAY '**  PARAGRAPH = 1000-INITIALIZTION'             01810000
               DISPLAY '**'                                             01820000
               DISPLAY '**  NO DATA IN THE STORE RANKING FILE'          01830000
               DISPLAY '**  INP01'                                      01840000
               DISPLAY '*************************************'          01850000
           END-IF.                                                      01860000
                                                                        01870000
                                                                        01880000
      /*****************************************************************01890000
      * 2000-STORE-RANKING-RECS.                                        01900000
      ******************************************************************01910000
       2000-STORE-RANKING-RECS.                                         01920000
                                                                        01930000
           IF IN-STORE NOT = SPACES AND                                 01940000
              IN-STORE NOT = PV-SAVE-STORE                              01950000
               ADD +1                  TO SS-STORE                      01960000
                                          PV-STORE-COUNT                01970000
               MOVE IN-STORE           TO PV-SAVE-STORE                 01980000
                                          ST-STORE-NBR(SS-STORE)        01990000
               IF SS-STORE > PC-STORE-MAX                               02050000
                   DISPLAY '** ABEND **'                                02060000
                   DISPLAY '** PROGRAM INKUT292 **'                     02070000
                   DISPLAY '** PARAGRAPH 1200-USE-CONTROL-CARD-DTE'     02080000
                   DISPLAY '** STORE TABLE OUT OF BOUNDS           '    02090000
                   MOVE +4004 TO ABEND-CODE                             02100000
                   PERFORM 9999-ABEND                                   02110000
               END-IF                                                   02120000
                                                                        02130000
           END-IF.                                                      02140000
                                                                        02150000
           PERFORM 4000-READ-STR-RANKING-FILE.                          02160000
                                                                        02170000
      /*****************************************************************02180000
      * 3000-BUILD-SORT-CARD.                                           02190000
      ******************************************************************02200000
       3000-BUILD-SORT-CARD.                                            02210000
                                                                        02220000
      *    WRITE LINE ONE OF SORT CARD                                  02230000
           WRITE SORT-CARD-REC-OUT                                      02240000
               FROM DL-DETAIL-LINE1.                                    02250000
                                                                        02260000
      *    WRITE LINE TWO OF SORT CARD                                  02270000
           WRITE SORT-CARD-REC-OUT                                      02280000
               FROM DL-DETAIL-LINE2.                                    02290000
                                                                        02300000
      *    WRITE LINE THREE OF THE SORT CARD                            02310000
           MOVE PC-ONE             TO DF-FILE-NBR.                      02320000
           WRITE SORT-CARD-REC-OUT                                      02330000
               FROM DF-DETAIL-FILE-NUMBER.                              02340000
                                                                        02350000
      *    WRITE LINE FOUR OF THE SORT CARD                             02360000
      *                                                                 02370000
      *    BECAUSE THE INPUT FILE NEEDS TO BE DIVIDED AS EQUALLY AS     02380000
      *    POSSIBLE INTO FOUR FILES, DIVIDE THE NUMBER OF STORES BY     02390000
      *    FOUR.  USE THE QUOTIENT TO DETERMINE THE MINIMUM NUMBER OF   02400000
      *    STORES PER OUTPUT.  USE THE REMAINDER TO DETERMINE WHICH     02410000
      *    FILES WILL GET AN EXTRA STORE.                               02420000
      *                                                                 02430000
           IF PV-STORE-COUNT > 0                                        02440000
               DIVIDE 4 INTO PV-STORE-COUNT                             02450000
                   GIVING    PV-STORE-QUOTIENT                          02460000
                   REMAINDER PV-STORE-REMAINDER                         02470000
                   ON SIZE ERROR                                        02480000
                   DISPLAY '************************************'       02490000
                   DISPLAY '*'                                          02500000
                   DISPLAY '* PV-STORE-QUOTIENT ' PV-STORE-QUOTIENT     02510000
                   DISPLAY '* PV-STORE-REMAINDER ' PV-STORE-REMAINDER   02520000
                   DISPLAY '* CHANGING REMAINDER TO ZERO'               02530000
                   DISPLAY '*'                                          02540000
                   DISPLAY '************************************'       02550000
                   MOVE 0 TO PV-STORE-REMAINDER                         02560000
               END-DIVIDE                                               02570000
                                                                        02580000
           MOVE PC-INCLUDE-LINE    TO DI-INCLUDE.                       02590000
           MOVE PC-LE              TO DI-COND.                          02600000
           ADD  PV-STORE-QUOTIENT  TO PV-STORE-SECTION.                 02610000
           IF PV-STORE-REMAINDER   >= PC-1                              02620000
               ADD +1              TO PV-STORE-SECTION.                 02630000
           MOVE PV-STORE-SECTION   TO SS-STORE.                         02640000
           MOVE ST-STORE-NBR(SS-STORE)                                  02650000
                                   TO DI-STORE-BOUNDARY                 02660000
                                      PV-STORE-NBR.                     02670000
           MOVE PC-AND             TO DI-AND-OR-COND.                   02680000
           WRITE SORT-CARD-REC-OUT                                      02690000
               FROM DI-DETAIL-INCLUDE.                                  02700000
                                                                        02710000
      *    WRITE LINE FIVE OF THE SORT CARD                             02720000
           MOVE PC-INCLUDE-LINE-BLANK                                   02730000
                                   TO DI-INCLUDE.                       02740000
           MOVE PC-NE              TO DI-COND.                          02750000
           MOVE SPACES             TO DI-STORE-BOUNDARY.                02760000
           MOVE PC-CLOSE-PARENTH   TO DI-AND-OR-COND.                   02770000
           WRITE SORT-CARD-REC-OUT                                      02780000
               FROM DI-DETAIL-INCLUDE.                                  02790000
                                                                        02800000
      *    WRITE LINE SIX OF THE SORT CARD                              02810000
           MOVE PC-TWO             TO DF-FILE-NBR.                      02820000
           WRITE SORT-CARD-REC-OUT                                      02830000
               FROM DF-DETAIL-FILE-NUMBER.                              02840000
                                                                        02850000
      *    WRITE LINE SEVEN OF THE SORT CARD                            02860000
           MOVE PC-INCLUDE-LINE    TO DI-INCLUDE.                       02870000
           MOVE PC-GE              TO DI-COND.                          02880000
           ADD  +1                 TO PV-STORE-NBR.                     02890000
           MOVE PV-STORE-NBR       TO DI-STORE-BOUNDARY.                02900000
           MOVE PC-AND             TO DI-AND-OR-COND.                   02910000
           WRITE SORT-CARD-REC-OUT                                      02920000
               FROM DI-DETAIL-INCLUDE.                                  02930000
                                                                        02940000
      *    WRITE LINE EIGHT OF THE SORT CARD                            02950000
           MOVE PC-INCLUDE-LINE-BLANK                                   02960000
                                   TO DI-INCLUDE.                       02970000
           MOVE PC-LE              TO DI-COND.                          02980000
           ADD  PV-STORE-QUOTIENT  TO PV-STORE-SECTION.                 02990000
           IF PV-STORE-REMAINDER   >= PC-2                              03000000
               ADD +1              TO PV-STORE-SECTION.                 03010000
           MOVE PV-STORE-SECTION   TO SS-STORE.                         03020000
           MOVE ST-STORE-NBR(SS-STORE)                                  03030000
                                   TO DI-STORE-BOUNDARY                 03040000
                                      PV-STORE-NBR.                     03050000
           MOVE PC-AND             TO DI-AND-OR-COND.                   03060000
           WRITE SORT-CARD-REC-OUT                                      03070000
               FROM DI-DETAIL-INCLUDE.                                  03080000
                                                                        03090000
      *    WRITE LINE NINE  OF THE SORT CARD                            03100000
           MOVE PC-INCLUDE-LINE-BLANK                                   03110000
                                   TO DI-INCLUDE.                       03120000
           MOVE PC-NE              TO DI-COND.                          03130000
           MOVE SPACES             TO DI-STORE-BOUNDARY                 03140000
           MOVE PC-CLOSE-PARENTH   TO DI-AND-OR-COND.                   03150000
           WRITE SORT-CARD-REC-OUT                                      03160000
               FROM DI-DETAIL-INCLUDE.                                  03170000
                                                                        03180000
      *    WRITE LINE TEN   OF THE SORT CARD                            03190000
           MOVE PC-THREE           TO DF-FILE-NBR.                      03200000
           WRITE SORT-CARD-REC-OUT                                      03210000
               FROM DF-DETAIL-FILE-NUMBER.                              03220000
                                                                        03230000
      *    WRITE LINE ELEVEN OF THE SORT CARD                           03240000
           MOVE PC-INCLUDE-LINE    TO DI-INCLUDE.                       03250000
           MOVE PC-GE              TO DI-COND.                          03260000
           ADD  +1                 TO PV-STORE-NBR.                     03270000
           MOVE PV-STORE-NBR       TO DI-STORE-BOUNDARY.                03280000
           MOVE PC-AND             TO DI-AND-OR-COND.                   03290000
           WRITE SORT-CARD-REC-OUT                                      03300000
               FROM DI-DETAIL-INCLUDE.                                  03310000
                                                                        03320000
      *    WRITE LINE TWELVE OF THE SORT CARD                           03330000
           MOVE PC-INCLUDE-LINE-BLANK                                   03340000
                                   TO DI-INCLUDE.                       03350000
           MOVE PC-LE              TO DI-COND.                          03360000
           ADD  PV-STORE-QUOTIENT  TO PV-STORE-SECTION.                 03370000
           IF PV-STORE-REMAINDER   = PC-3                               03380000
               ADD +1              TO PV-STORE-SECTION.                 03390000
           MOVE PV-STORE-SECTION   TO SS-STORE.                         03400000
           MOVE ST-STORE-NBR(SS-STORE)                                  03410000
                                   TO DI-STORE-BOUNDARY                 03420000
                                      PV-STORE-NBR.                     03430000
           MOVE PC-AND             TO DI-AND-OR-COND.                   03440000
           WRITE SORT-CARD-REC-OUT                                      03450000
               FROM DI-DETAIL-INCLUDE.                                  03460000
                                                                        03470000
      *    WRITE LINE THIRTEEN OF THE SORT CARD                         03480000
           MOVE PC-INCLUDE-LINE-BLANK                                   03490000
                                   TO DI-INCLUDE.                       03500000
           MOVE PC-NE              TO DI-COND.                          03510000
           MOVE SPACES             TO DI-STORE-BOUNDARY.                03520000
           MOVE PC-CLOSE-PARENTH   TO DI-AND-OR-COND.                   03530000
           WRITE SORT-CARD-REC-OUT                                      03540000
               FROM DI-DETAIL-INCLUDE.                                  03550000
                                                                        03560000
      *    WRITE LINE FOURTEEN OF THE SORT CARD                         03570000
           MOVE PC-FOUR            TO DF-FILE-NBR.                      03580000
           WRITE SORT-CARD-REC-OUT                                      03590000
               FROM DF-DETAIL-FILE-NUMBER.                              03600000
                                                                        03610000
      *    WRITE LINE FIFTEEN OF THE SORT CARD                          03620000
           MOVE PC-INCLUDE-LINE    TO DI-INCLUDE.                       03630000
           MOVE PC-GE              TO DI-COND.                          03640000
           ADD  +1                 TO PV-STORE-NBR.                     03650000
           MOVE PV-STORE-NBR       TO DI-STORE-BOUNDARY.                03660000
           MOVE PC-OR              TO DI-AND-OR-COND.                   03670000
           WRITE SORT-CARD-REC-OUT                                      03680000
               FROM DI-DETAIL-INCLUDE.                                  03690000
                                                                        03700000
      *    WRITE LINE SIXTEEN OF THE SORT CARD                          03710000
           MOVE PC-INCLUDE-LINE-BLANK                                   03720000
                                   TO DI-INCLUDE.                       03730000
           MOVE PC-EQ              TO DI-COND.                          03740000
           MOVE SPACES             TO DI-STORE-BOUNDARY.                03750000
           MOVE PC-CLOSE-PARENTH   TO DI-AND-OR-COND.                   03760000
           WRITE SORT-CARD-REC-OUT                                      03770000
               FROM DI-DETAIL-INCLUDE.                                  03780000
                                                                        03790000
      *    WRITE LINE SEVENTEEN OF THE SORT CARD                        03800000
           WRITE SORT-CARD-REC-OUT                                      03810000
               FROM DL-DETAIL-LINE-END.                                 03820000
                                                                        03830000
      /**************************************************************** 03840000
      * 4000-READ-STR-RANKING-FILE.                                     03850000
      ***************************************************************** 03860000
       4000-READ-STR-RANKING-FILE.                                      03870000
                                                                        03880000
           READ DEPT-STR-RANKING-FILE-IN INTO IN-FILE-REC               03890000
               AT END MOVE 'Y'     TO PS-DEPT-STR-RANKING               03900000
               NOT AT END ADD +1   TO PV-RECS-READ                      03910000
           END-READ.                                                    03920000
                                                                        03930000
      /*****************************************************************03940000
      * 8000-EOJ.  FINAL PROCESSING FOR THE PROGRAM.                    03950000
      ******************************************************************03960000
       8000-EOJ.                                                        03970000
                                                                        03980000
           DISPLAY ' '.                                                 03990000
           DISPLAY '*** RECORD COUNT SUMMARY'.                          04000000
           DISPLAY 'INVENTORY EXTRACT RECORDS READ  = '                 04010000
                 PV-RECS-READ.                                          04020000
           DISPLAY 'TOTAL NUMBER OF UNIQUE STORES   = '                 04030000
                 PV-STORE-COUNT.                                        04040000
                                                                        04050000
           DISPLAY '******************************************'.        04060000
           DISPLAY '*  END OF INKUT292                       *'.        04070000
           DISPLAY '******************************************'.        04080000
                                                                        04090000
           CLOSE DEPT-STR-RANKING-FILE-IN                               04100000
                 SORT-CARD-FILE-OUT.                                    04110000
                                                                        04120000
                                                                        04130000
       9999-ABEND.                                                      04140000
                                                                        04150000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04160000
                                                                        04170000
