       ID  DIVISION.                                                    11/29/94
00002  PROGRAM-ID.         INKUT104.                                    INKUT104
00003  AUTHOR.             TROY BIRK.                                      LV007
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                     INKUT104
00005  DATE-WRITTEN.       08/93.                                       INKUT104
00006  DATE-COMPILED.                                                   INKUT104
00007                                                                   INKUT104
00008 ******************************************************************INKUT104
00009 *                 PROGRAM DESCRIPTION                                CL**6
00010 ******************************************************************   CL**6
00011 *  THIS PROGRAM WILL STRIP ALL DUPLICATE SHEETS OFF THE INPUT     INKUT104
00012 *  FILE, IF ANY EXIST. IT WILL ALSO CREATE THE FOLLOWING REPORT:  INKUT104
00013 *                                                                 INKUT104
00014 *  1).  SHEET / LINE ERROR REPORT.                                INKUT104
00015 *       THIS REPORT WILL SHOW ANY SHEETS THAT ARE DUPLICATES      INKUT104
00016 *       (AND THEREFORE HAVE BEEN STRIPPED OFF THE INPUT FILE),    INKUT104
00017 *       ANY MISSING SHEETS IN KEYING STORES, AND ANY LINE         INKUT104
00018 *       NUMBER > 25 IN KEYING STORES.  IF NONE OF THE ABOVE       INKUT104
00019 *       ERROR CONDITIONS ARE ENCOUNTERED, THIS REPORT WILL        INKUT104
00020 *       NOT BE PRODUCED.                                          INKUT104
00021 ******************************************************************   CL**6
00022 *                     CHANGE LOG                                     CL**6
00023 ******************************************************************   CL**6
00024 * 08/94   MARC BLAZICH                                               CL**6
00025 * CHANGED PROGRAM TO USE IN010-SCAN-LINE-NBR INSTEAD OF              CL**6
00026 * IN010-LINE-NBR DO DETERMINE HOW MANY LINES PER SHEET.              CL**6
00027 * ALSO CHANGED PROGRAM TO ASSIGN IN010-LINE-NBR BY INCREMENTING      CL**6
00028 * LINE EVERY TIME NEW RECORD IS READ W/O CHANGING STORE OR SHEET     CL**6
00024 * 02/99   MARIA KLAMIK                                               CL**6
00018 * EXPAND COPYBOOK INWS010 TO ACCOMMODATE NEW SCAN DATE AND           CL**3
      * TIMESTAMP FIELDS.                                                       
00029 ******************************************************************INKUT104
W21732* 11/02   IGSI                                                            
W21732* DEPT/CLASS/SEQ ARE REMOVED AND 8 DIGIT SKU IS ADDED.                    
C21732* 03/31   SCOTT JACKSON                                                   
C21732* MOVED ASSIGNMENT OF LINE NUMBER TO PREVIOUS PROGRAM (INKUT112)          
W21732******************************************************************INKUT104
25248 * 25248    10-06-2003  UPC CAPABILITIES & STORE NUMBER EXPANSION          
25248 *                      -ROD JANSSEN                                       
W26600*                                                                         
W26600******************************************************************INKUT104
W26600* W26600   06-  -2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
W26600*                                                                         
W26600******************************************************************INKUT102
W26006* W26006   02-24-2005  REMOVE REFERENCES TO OBSOLETE STORE TABLE.         
W26006*                      ERIN SEARL                                         
W30692*W30692* 06-23-2006 ROD JANSSEN RTV SNAPSHOT ISSUE AND VOLUME             
W26006******************************************************************INKUT102
SR3403* SR3403   02-04-2012  LARGE RETAIL AMOUNT EXPANSION                      
SR3403*                      CHANDAN RACHH                                      
SR3403******************************************************************INKUT102
00030                                                                   INKUT104
00031  ENVIRONMENT DIVISION.                                            INKUT104
00032 *                                                                 INKUT104
00033  CONFIGURATION SECTION.                                           INKUT104
00034 *                                                                 INKUT104
00035  SOURCE-COMPUTER.        IBM-3090.                                INKUT104
00036  OBJECT-COMPUTER.        IBM-3090.                                INKUT104
00037 *                                                                 INKUT104
00038  INPUT-OUTPUT SECTION.                                            INKUT104
00039  FILE-CONTROL.                                                    INKUT104
00040      SELECT  PHYSICAL-INVENTORY-FILE                              INKUT104
00041          ASSIGN TO INP01.                                         INKUT104
00042      SELECT  SHEET-LINE-ERROR-REPORT                              INKUT104
00043          ASSIGN TO RPT01.                                         INKUT104
00044      SELECT  UPDATED-INVENTORY-FILE                               INKUT104
00045          ASSIGN TO OUT01.                                         INKUT104
00046                                                                   INKUT104
00047  DATA DIVISION.                                                   INKUT104
00048  FILE SECTION.                                                    INKUT104
00049  FD  PHYSICAL-INVENTORY-FILE                                      INKUT104
00050      LABEL RECORDS ARE STANDARD                                   INKUT104
00051      RECORDING MODE IS F                                          INKUT104
00052      BLOCK CONTAINS 0 RECORDS.                                    INKUT104
W21732 01  PHYSICAL-INVENTORY-RECORD  PIC X(100).                          CL**2
00054                                                                   INKUT104
00055  FD  SHEET-LINE-ERROR-REPORT                                      INKUT104
00056      LABEL RECORDS ARE STANDARD                                   INKUT104
00057      RECORDING MODE IS F                                          INKUT104
00058      BLOCK CONTAINS 0 RECORDS.                                    INKUT104
00059  01  SHEET-LINE-ERROR-RECORD    PIC X(132).                       INKUT104
00060                                                                   INKUT104
00061  FD  UPDATED-INVENTORY-FILE                                       INKUT104
00062      LABEL RECORDS ARE STANDARD                                   INKUT104
00063      RECORDING MODE IS F                                          INKUT104
00064      BLOCK CONTAINS 0 RECORDS.                                    INKUT104
W21732 01  UPDATED-INVENTORY-RECORD   PIC X(100).                          CL**2
00066                                                                   INKUT104
00067  WORKING-STORAGE SECTION.                                         INKUT104
00068 *                                                                 INKUT104
00069  01  PC-PRINT-CONTROL.                                            INKUT104
00070      05  PC-LINES-PER-PAGE       PIC S9(03)  VALUE +57  COMP-3.   INKUT104
00071      05  PC-ADVANCE              PIC S9(03)  VALUE ZERO COMP-3.   INKUT104
00072      05  PC-PAGE-COUNT           PIC S9(05)  VALUE ZERO COMP-3.   INKUT104
00073      05  PC-LINE-COUNT           PIC S9(03)  VALUE +100 COMP-3.   INKUT104
00074 *                                                                 INKUT104
00075  01  PC-PROGRAM-CONSTANTS.                                        INKUT104
00076      05  PC-LINE-25              PIC 9(02)    VALUE 25.           INKUT104
00077      05  PC-MAX-RETRIES          PIC S9(03)   VALUE +03           INKUT104
00078                                               COMP-3.             INKUT104
00079                                                                   INKUT104
00080 *                                                                 INKUT104
00081  01  MSG-MESSAGES.                                                INKUT104
00082      05  MSG-MISSING-SHEET           PIC X(31)             VALUE  INKUT104
00083      'SHEET NUMBER IS MISSING        '.                           INKUT104
00084      05  MSG-MISSING-RANGE           PIC X(31)             VALUE  INKUT104
00085      'RANGE OF MISSING SHEETS        '.                           INKUT104
00086      05  MSG-LINE-GREATER-THAN-MAX   PIC X(31)             VALUE  INKUT104
00087      'LINE NUMBER IS GREATER THAN 25'.                            INKUT104
00088      05  MSG-DUPLICATE-SHEET-LINE    PIC X(31)             VALUE  INKUT104
00089      'DUPLICATE SHEET AND LINE NUMBER'.                           INKUT104
00090 *                                                                 INKUT104
00091 *                                                                 INKUT104
00092  01  PV-PROGRAM-VARIABLES.                                        INKUT104
00093      05  PV-ERROR-COUNT       PIC S9(03)  VALUE ZERO   COMP-3.    INKUT104
C21732*    05  PV-LINE-NUMBER       PIC  9(06)  VALUE ZERO.                CL**4
00095      05  PV-TODAYS-DATE.                                          INKUT104
00096          10  PV-YY               PIC 9(02)     VALUE ZERO.        INKUT104
00097          10  PV-MM               PIC 9(02)     VALUE ZERO.        INKUT104
00098          10  PV-DD               PIC 9(02)     VALUE ZERO.        INKUT104
00099      05  PV-TODAYS-TIME.                                          INKUT104
00100          10  PV-HR               PIC 9(02)     VALUE ZERO.        INKUT104
00101          10  PV-MIN              PIC 9(02)     VALUE ZERO.        INKUT104
00102          10  PV-SEC              PIC 9(02)     VALUE ZERO.        INKUT104
00103          10  FILLER              PIC 9(02)     VALUE ZERO.        INKUT104
00104      05  PV-SQL-SUB              PIC 9(03)     VALUE ZERO.        INKUT104
00105      05  PV-PARAGRAPH-NAME       PIC X(30)     VALUE SPACES.      INKUT104
00106      05  PV-DB2-OPER-ATTEMPTED   PIC X(30)     VALUE SPACES.      INKUT104
C21732*    05  PV-TEMP-LINE            PIC 9(06)     VALUE ZERO.        INKUT104
00108      05  PV-INCR-VALUE           PIC 9(06)     VALUE ZERO.        INKUT104
00109      05  PV-TEMP-AREA            PIC 9(06)     VALUE ZERO.        INKUT104
00110      05  PV-TEMP-AREA2           PIC 9(06)     VALUE ZERO.        INKUT104
00111      05  PV-EXT-RETAIL           PIC S9(09)V99 VALUE ZERO         INKUT104
00112                                                COMP-3.            INKUT104
25248      05 PV-EDIT-LOC-NBR          PIC S9(04) COMP VALUE ZEROS.             
00113                                                                   INKUT104
00115  01  SA-SAVE-AREAS.                                               INKUT104
W26600     05  SA-STORE-NUMBER         PIC X(04)    VALUE ZERO.         INKUT104
25248      05  SA-STORE-NUMBER-NUM REDEFINES                                    
W26600         SA-STORE-NUMBER         PIC 9(04).                               
00117      05  SA-AREA-NUMBER          PIC 9(06)    VALUE ZERO.         INKUT104
00118      05  SA-SCAN-LINE-NUMBER     PIC 9(06)    VALUE ZERO.            CL**5
00119 *                                                                 INKUT104
00120  01  H1-HEADING-LINE.                                             INKUT104
00121      05  FILLER          PIC X(33)        VALUE SPACES.           INKUT104
00122      05  FILLER          PIC X(36)        VALUE                   INKUT104
00123      '  SHEET/LINE ERROR REPORT FOR STORE '.                      INKUT104
W26600     05  H1-STORE        PIC X(04)        VALUE SPACES.           INKUT104
00125      05  FILLER          PIC X(01)        VALUE SPACES.           INKUT104
00126      05  H1-STORE-NAME   PIC X(25)         VALUE SPACES.          INKUT104
00127      05  FILLER          PIC X(33)        VALUE SPACES.           INKUT104
00128 *                                                                 INKUT104
00129  01  H2-HEADING-LINE.                                             INKUT104
00130      05  FILLER                  PIC X(01)    VALUE SPACES.       INKUT104
00131      05  H2-HEADING-COULUNM1     PIC X(06)    VALUE SPACES.       INKUT104
00132      05  FILLER                  PIC X(10)    VALUE SPACES.       INKUT104
00133      05  FILLER                  PIC X(06)    VALUE 'LINE  '.     INKUT104
00134      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
W21732     05  FILLER                  PIC X(10)    VALUE '  SKU     '. INKUT104
00136      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
00137      05  FILLER                  PIC X(09)    VALUE '   PRICE '.  INKUT104
00138      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
00139      05  FILLER                  PIC X(04)    VALUE ' QTY'.       INKUT104
00140      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
00141      05  FILLER                  PIC X(14)    VALUE               INKUT104
00142                                               '  EXT. RETAIL '.   INKUT104
00143      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
00144      05  FILLER                  PIC X(13)    VALUE               INKUT104
00145                                               '  UPC        '.    INKUT104
00146      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
00147      05  FILLER                  PIC X(31)    VALUE 'ERROR'.      INKUT104
00148 *                                                                 INKUT104
00149  01  DL1-DETAIL-LINE.                                             INKUT104
00150      05  FILLER                  PIC X(01)    VALUE SPACES.       INKUT104
00151      05  DL1-SHEET-NUMBER        PIC X(06)    VALUE SPACES.       INKUT104
00152      05  DL1-DASH1               PIC X(01)    VALUE SPACES.       INKUT104
00153      05  DL1-SHEET-END           PIC X(06)    VALUE SPACES.       INKUT104
00154      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
00155      05  DL1-LINE-NUMBER         PIC X(06)    VALUE SPACES.       INKUT104
00156      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
W21732     05  DL1-SKU                 PIC X(08)    VALUE SPACES.               
SR3403     05  FILLER                  PIC X(04)    VALUE SPACES.       INKUT104
SR3403     05  DL1-PRICE               PIC ZZ,ZZ9.99-.                  INKUT104
SR3403     05  DL1-PRICE-RED REDEFINES DL1-PRICE PIC X(10).             INKUT104
00165      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
00166      05  DL1-QUANTITY            PIC ZZZ9.                        INKUT104
00167      05  DL1-QUANT-RED REDEFINES DL1-QUANTITY PIC X(04).          INKUT104
00168      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
00169      05  DL1-EXT-RETAIL          PIC ZZ,ZZZ,ZZ9.99-.              INKUT104
00170      05  DL1-XRTL-RED REDEFINES DL1-EXT-RETAIL PIC X(14).         INKUT104
00171      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
00172      05  DL1-UPC                 PIC X(13)    VALUE SPACES.       INKUT104
00173      05  FILLER                  PIC X(03)    VALUE SPACES.       INKUT104
00174      05  DL1-ERROR-MSG           PIC X(31)    VALUE SPACES.       INKUT104
00175 *                                                                 INKUT104
00176  01  DL2-DETAIL-LINE.                                             INKUT104
00177      05  FILLER          PIC X(35)        VALUE SPACES.           INKUT104
00178      05  FILLER          PIC X(38)        VALUE                   INKUT104
00179          '**** TOTAL NUMBER OF ERRORS FOR STORE '.                INKUT104
W26600     05  D2-STORE        PIC X(04)        VALUE ZERO.             INKUT104
00181      05  FILLER          PIC X(03)        VALUE ' = '.            INKUT104
00182      05  D2-TOTAL-ERRS   PIC ZZZZZZZ9     VALUE ZERO.             INKUT104
00183      05  FILLER          PIC X(5)         VALUE ' ****'.          INKUT104
00184      05  FILLER          PIC X(5)         VALUE SPACES.           INKUT104
00185      05  FILLER          PIC X(35)        VALUE SPACES.           INKUT104
00186 *                                                                 INKUT104
00187 *                                                                 INKUT104
00188  01  PS-PROGRAM-SWITCHES.                                         INKUT104
00189      05  PS-END-OF-INVENTORY     PIC X        VALUE 'N'.          INKUT104
00190          88  PS-EOF-INVENTORY                 VALUE 'Y'.          INKUT104
00191 *                                                                 INKUT104
00192 ******************************************************************INKUT104
00193 *    INPUT FILE RECORD LAYOUT                                     INKUT104
00194 ******************************************************************INKUT104
00195      COPY INWS010.                                                   CL**7
00196 *                                                                 INKUT104
00197 ******************************************************************INKUT104
00198 *    KOHLS STANDARD REPORT HEADER - 132 CHARACTERS                INKUT104
00199 ******************************************************************INKUT104
00200      COPY DPWS132O.                                               INKUT104
00201 *                                                                 INKUT104
00202 ******************************************************************INKUT104
00203 *      SQL ABEND ROUTINE WORKING STORAGE                          INKUT104
00204 ******************************************************************INKUT104
00205 *                                                                 INKUT104
00206      COPY DPWS004.                                                INKUT104
00207                                                                   INKUT104
25248 *****************************************************************         
  |   * XMT_LOC DCLGEN                                                          
  |   *****************************************************************         
  |        EXEC SQL                                                             
  |           INCLUDE XMT00001                                                  
  |        END-EXEC.                                                            
25248                                                                           
  |   *****************************************************************         
00218      EXEC SQL                                                     INKUT104
00219          INCLUDE SQLCA                                            INKUT104
00220      END-EXEC.                                                    INKUT104
00221                                                                   INKUT104
  |   *****************************************************************         
00222      COPY SQLCA2.                                                 INKUT104
00223                                                                   INKUT104
00224  01  ABEND-CODE               PIC S9(04)  VALUE ZERO              INKUT104
00225                                           COMP SYNC.              INKUT104
00226      EJECT                                                        INKUT104
00227  LINKAGE SECTION.                                                 INKUT104
00228 *                                                                 INKUT104
00229  PROCEDURE DIVISION.                                              INKUT104
00230 *                                                                 INKUT104
00231 ******************************************************************INKUT104
00232 *                                                                 INKUT104
00233 ******************************************************************INKUT104
00234  A100-MAIN-MODULE.                                                INKUT104
00235                                                                   INKUT104
00236      PERFORM B100-OPEN-FILES.                                     INKUT104
00237      PERFORM B200-INITIALIZE.                                     INKUT104
00238                                                                   INKUT104
00239      PERFORM Z100-READ-INVENTORY-FILE.                            INKUT104
00240                                                                   INKUT104
00241      PERFORM B400-PROCESS-RECS   UNTIL                            INKUT104
00242          PS-EOF-INVENTORY.                                        INKUT104
00243                                                                   INKUT104
00244      IF PV-ERROR-COUNT > 0                                        INKUT104
00245          PERFORM C200-WRITE-TOTAL-MSG                             INKUT104
00246      END-IF.                                                      INKUT104
00247                                                                   INKUT104
00248      PERFORM B500-CLOSE-FILES.                                    INKUT104
00249                                                                   INKUT104
00250      STOP RUN.                                                    INKUT104
00251                                                                   INKUT104
00252 ******************************************************************INKUT104
00253 *                                                                 INKUT104
00254 ******************************************************************INKUT104
00255  B100-OPEN-FILES.                                                 INKUT104
00256 *                                                                 INKUT104
00257      OPEN INPUT  PHYSICAL-INVENTORY-FILE                          INKUT104
00258           OUTPUT SHEET-LINE-ERROR-REPORT                          INKUT104
00259                  UPDATED-INVENTORY-FILE.                          INKUT104
00260 *                                                                 INKUT104
00261 ******************************************************************INKUT104
00262 * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING.          INKUT104
00263 ******************************************************************INKUT104
00264  B200-INITIALIZE.                                                 INKUT104
00265                                                                   INKUT104
00266      ACCEPT PV-TODAYS-DATE FROM DATE.                             INKUT104
00267      ACCEPT PV-TODAYS-TIME FROM TIME.                             INKUT104
00268      MOVE PV-MM           TO DP132O-RUN-MONTH.                    INKUT104
00269      MOVE PV-DD           TO DP132O-RUN-DAY.                      INKUT104
00270      MOVE PV-YY           TO DP132O-RUN-YEAR.                     INKUT104
00271      MOVE PV-HR           TO DP132O-RUN-HOUR.                     INKUT104
00272      MOVE PV-MIN          TO DP132O-RUN-MINUTE.                   INKUT104
00273      MOVE 'INKUT104'      TO DP132O-PROGRAM-NAME.                 INKUT104
00274      MOVE 1               TO DP132O-REPORT-NUMBER.                INKUT104
00275 *                                                                 INKUT104
00276 ******************************************************************INKUT104
00277 *                                                                 INKUT104
00278 ******************************************************************INKUT104
00279  B400-PROCESS-RECS.                                               INKUT104
00280                                                                      CL**2
00281      IF IN010-STORE-NUMBER NOT = SA-STORE-NUMBER                  INKUT104
00282          IF PV-ERROR-COUNT > 0                                    INKUT104
00283              PERFORM C200-WRITE-TOTAL-MSG                         INKUT104
00284          END-IF                                                   INKUT104
00285          MOVE IN010-STORE-NUMBER TO SA-STORE-NUMBER               INKUT104
00286                                     H1-STORE                      INKUT104
00287                                                                   INKUT104
00288          INITIALIZE PV-ERROR-COUNT                                INKUT104
00289          PERFORM Z200-READ-STORE-TABLE                            INKUT104
25248          MOVE XMT00001-LOC-NM    TO H1-STORE-NAME                 INKUT104
00291                                                                   INKUT104
00292          IF IN010-SCANNING-STORE                                  INKUT104
00293              MOVE 'AREA  ' TO H2-HEADING-COULUNM1                 INKUT104
00294          ELSE                                                     INKUT104
00295              MOVE 'SHEET ' TO H2-HEADING-COULUNM1                 INKUT104
00296          END-IF                                                   INKUT104
00297          INITIALIZE  SA-AREA-NUMBER                                  CL**2
00298                      SA-SCAN-LINE-NUMBER                             CL**5
00299          MOVE +100 TO PC-LINE-COUNT                               INKUT104
00300      END-IF.                                                      INKUT104
00301                                                                   INKUT104
C21372** MOVE THIS LOGIC TO PREVIOUS PROGRAM (INKUT112 - WHICH WILL        CL**4
C21372** REMOVE INVALID SKU RECORDS). THIS MOVE WILL KEEP SCAN-LINE AND    CL**4
C21372** LINE-NUMBER IN SYNCH                                              CL**4
C21372**                                                                   CL**4
00302 **   PV-LINE-NUMBER IS MOVED TO IN010-LINE-NUMBER IN THE Z500        CL**4
00303 **   PARAGRAPH.  IT IS INCREMENTED BY 1 FOR EACH RECORD W/IN         CL**4
00304 **   A STORE/AREA.  IT IS RESET UPON A CHANGE IN STORE OR AREA.      CL**4
00305                                                                      CL**4
C21732*    IF IN010-STORE-NUMBER NOT = SA-STORE-NUMBER OR                  CL**2
C21732*       IN010-AREA-NUMBER  NOT = SA-AREA-NUMBER                      CL**2
C21732*        MOVE  0   TO PV-LINE-NUMBER                                 CL**4
C21732*    END-IF                                                          CL**2
C21732*                                                                    CL**2
C21732*    ADD 1                  TO PV-LINE-NUMBER                        CL**4
00312                                                                      CL**4
00313      IF NOT IN010-SCANNING-STORE                                  INKUT104
00314          IF (IN010-SCAN-LINE-NUMBER = SA-SCAN-LINE-NUMBER AND        CL**5
00315              IN010-AREA-NUMBER      = SA-AREA-NUMBER)                CL**5
00316              INITIALIZE DL1-DETAIL-LINE                              CL**5
00317              MOVE IN010-AREA-NUMBER TO DL1-SHEET-NUMBER              CL**5
00318              MOVE IN010-SCAN-LINE-NUMBER                             CL**5
00319                                     TO DL1-LINE-NUMBER               CL**5
W21732             MOVE IN010-SKU         TO DL1-SKU                       CL**5
00323              MOVE IN010-PRICE       TO DL1-PRICE                     CL**5
00324                                                                      CL**5
00325              MOVE IN010-UPC-CODE    TO DL1-UPC                       CL**5
00326              MOVE IN010-QUANTITY    TO DL1-QUANTITY                  CL**5
00329              COMPUTE PV-EXT-RETAIL = IN010-QUANTITY *                CL**5
00330                                      IN010-PRICE                     CL**5
00331              MOVE PV-EXT-RETAIL TO DL1-EXT-RETAIL                    CL**5
00332              MOVE MSG-DUPLICATE-SHEET-LINE TO DL1-ERROR-MSG          CL**5
00333              PERFORM C100-WRITE-ERROR-MSG                            CL**5
00334          END-IF                                                      CL**5
00335                                                                      CL**5
00336          IF (IN010-AREA-NUMBER NOT = SA-AREA-NUMBER) AND             CL**5
00337             (SA-AREA-NUMBER NOT = 0)                              INKUT104
00338              COMPUTE PV-TEMP-AREA = IN010-AREA-NUMBER - 1         INKUT104
00339              IF (SA-AREA-NUMBER NOT = PV-TEMP-AREA)               INKUT104
00340                  INITIALIZE DL1-DETAIL-LINE                       INKUT104
00341                  MOVE SPACES TO DL1-PRICE-RED                     INKUT104
00342                                 DL1-QUANT-RED                     INKUT104
00343                                 DL1-XRTL-RED                      INKUT104
00344                  COMPUTE PV-TEMP-AREA2 = SA-AREA-NUMBER + 1       INKUT104
00345                  IF PV-TEMP-AREA2 = PV-TEMP-AREA                  INKUT104
00346                      MOVE MSG-MISSING-SHEET TO DL1-ERROR-MSG      INKUT104
00347                      MOVE PV-TEMP-AREA2 TO DL1-SHEET-NUMBER       INKUT104
00348                  ELSE                                             INKUT104
00349                      MOVE MSG-MISSING-RANGE TO DL1-ERROR-MSG      INKUT104
00350                      MOVE PV-TEMP-AREA2 TO DL1-SHEET-NUMBER       INKUT104
00351                      MOVE PV-TEMP-AREA  TO DL1-SHEET-END          INKUT104
00352                      MOVE '-'           TO DL1-DASH1              INKUT104
00353                  END-IF                                           INKUT104
00354                  PERFORM C100-WRITE-ERROR-MSG                     INKUT104
00355              END-IF                                               INKUT104
00356          END-IF                                                   INKUT104
00357                                                                   INKUT104
W30692*        IF IN010-SCAN-LINE-NUMBER > PC-LINE-25                      CL**5
W30692*            INITIALIZE DL1-DETAIL-LINE                           INKUT104
W30692*            MOVE IN010-AREA-NUMBER TO DL1-SHEET-NUMBER           INKUT104
W30692*            MOVE IN010-SCAN-LINE-NUMBER                             CL**5
W30692*                                   TO DL1-LINE-NUMBER               CL**5
W30692*            MOVE IN010-SKU         TO DL1-SKU                    INKUT104
W30692*            MOVE IN010-PRICE       TO DL1-PRICE                  INKUT104
W30692*            MOVE IN010-UPC-CODE    TO DL1-UPC                    INKUT104
W30692*            MOVE IN010-QUANTITY    TO DL1-QUANTITY               INKUT104
W30692*            COMPUTE PV-EXT-RETAIL = IN010-QUANTITY *             INKUT104
W30692*                                    IN010-PRICE                  INKUT104
W30692*            MOVE PV-EXT-RETAIL TO DL1-EXT-RETAIL                 INKUT104
W30692*            MOVE MSG-LINE-GREATER-THAN-MAX TO DL1-ERROR-MSG      INKUT104
W30692*            PERFORM C100-WRITE-ERROR-MSG                         INKUT104
W30692*        END-IF                                                   INKUT104
00377      END-IF.                                                      INKUT104
00378                                                                   INKUT104
00379      PERFORM Z500-WRITE-EDITED-RECORD.                            INKUT104
00380                                                                   INKUT104
00381      MOVE IN010-AREA-NUMBER      TO SA-AREA-NUMBER.                  CL**5
00382      MOVE IN010-SCAN-LINE-NUMBER TO SA-SCAN-LINE-NUMBER.             CL**5
00383                                                                      CL**2
00384      PERFORM Z100-READ-INVENTORY-FILE.                            INKUT104
00385 *                                                                 INKUT104
00386 ******************************************************************INKUT104
00387 *  CLOSE ALL FILES USED BY THIS PROGRAM.                          INKUT104
00388 ******************************************************************INKUT104
00389  B500-CLOSE-FILES.                                                INKUT104
00390 *                                                                 INKUT104
00391      CLOSE       PHYSICAL-INVENTORY-FILE                          INKUT104
00392                  SHEET-LINE-ERROR-REPORT                          INKUT104
00393                  UPDATED-INVENTORY-FILE.                          INKUT104
00394 *                                                                 INKUT104
00395 ******************************************************************INKUT104
00396 *                                                                 INKUT104
00397 ******************************************************************INKUT104
00398  C100-WRITE-ERROR-MSG.                                            INKUT104
00399                                                                   INKUT104
00400      ADD 1 TO PV-ERROR-COUNT.                                     INKUT104
00401      IF PC-LINE-COUNT > PC-LINES-PER-PAGE                         INKUT104
00402          PERFORM D100-PRINT-HEADINGS                              INKUT104
00403      END-IF.                                                      INKUT104
00404      MOVE 2 TO PC-ADVANCE.                                        INKUT104
00405      MOVE DL1-DETAIL-LINE TO SHEET-LINE-ERROR-RECORD.             INKUT104
00406      PERFORM Z600-WRITE-ERROR-REPORT-LINE.                        INKUT104
00407                                                                   INKUT104
00408 *                                                                 INKUT104
00409 ******************************************************************INKUT104
00410 *                                                                 INKUT104
00411 ******************************************************************INKUT104
00412  C200-WRITE-TOTAL-MSG.                                            INKUT104
00413                                                                   INKUT104
00414      IF PC-LINE-COUNT > PC-LINES-PER-PAGE                         INKUT104
00415          PERFORM D100-PRINT-HEADINGS                              INKUT104
00416      END-IF.                                                      INKUT104
00417      MOVE 2 TO PC-ADVANCE.                                        INKUT104
00418      MOVE SA-STORE-NUMBER TO D2-STORE.                            INKUT104
00419      MOVE PV-ERROR-COUNT TO D2-TOTAL-ERRS.                        INKUT104
00420      MOVE DL2-DETAIL-LINE TO SHEET-LINE-ERROR-RECORD.             INKUT104
00421      PERFORM Z600-WRITE-ERROR-REPORT-LINE.                        INKUT104
00422                                                                   INKUT104
00423 *                                                                 INKUT104
00424 ******************************************************************INKUT104
00425 *    PRINT THE REPORT HEADINGS.                                   INKUT104
00426 ******************************************************************INKUT104
00427  D100-PRINT-HEADINGS.                                             INKUT104
00428 *                                                                 INKUT104
00429      MOVE 1 TO PC-LINE-COUNT.                                     INKUT104
00430      ADD 1  TO PC-PAGE-COUNT.                                     INKUT104
00431      MOVE PC-PAGE-COUNT    TO DP132O-PAGE-NUMBER.                 INKUT104
00432      WRITE SHEET-LINE-ERROR-RECORD                                INKUT104
00433            FROM DP132O-STANDRD-HEADER-132-COLS                    INKUT104
00434            AFTER ADVANCING PAGE.                                  INKUT104
00435      MOVE 2 TO PC-ADVANCE.                                        INKUT104
00436      MOVE H1-HEADING-LINE TO SHEET-LINE-ERROR-RECORD.             INKUT104
00437      PERFORM Z600-WRITE-ERROR-REPORT-LINE.                        INKUT104
00438      MOVE H2-HEADING-LINE TO SHEET-LINE-ERROR-RECORD.             INKUT104
00439      PERFORM Z600-WRITE-ERROR-REPORT-LINE.                        INKUT104
00440 *                                                                 INKUT104
00441 ******************************************************************INKUT104
00442 * Z100-READ-INVENTORY-FILE  READS UNEDITTED PHYSICAL COUNTS FILE  INKUT104
00443 ******************************************************************INKUT104
00444  Z100-READ-INVENTORY-FILE.                                        INKUT104
00445                                                                   INKUT104
00446      READ PHYSICAL-INVENTORY-FILE                                 INKUT104
00447          INTO IN010-INVENTORY-COUNT-RECORD                        INKUT104
00448              AT END                                               INKUT104
00449                  MOVE 'Y' TO PS-END-OF-INVENTORY.                 INKUT104
00450 *                                                                 INKUT104
00451 *                                                                 INKUT104
00452 ****************************************************************  INKUT104
00453 *  STORE TABLE IS READ FOR STORE NUMBER VERIFICATION.             INKUT104
00454 ****************************************************************  INKUT104
00455  Z200-READ-STORE-TABLE.                                           INKUT104
00456                                                                   INKUT104
00457      PERFORM                                                      INKUT104
00458          WITH TEST AFTER                                          INKUT104
00459          VARYING PV-SQL-SUB                                       INKUT104
00460          FROM 1 BY 1                                              INKUT104
00461          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES) OR                   INKUT104
00462                (SQLCODE = 0)                                      INKUT104
00463                                                                   INKUT104
25248          MOVE SA-STORE-NUMBER-NUM TO PV-EDIT-LOC-NBR                      
  |            MOVE PV-EDIT-LOC-NBR     TO XMT00001-LOC-NBR                     
  |                                                                             
  |            EXEC SQL                                                         
  |               SELECT LOC_NM                                                 
  |               INTO :XMT00001-LOC-NM                                         
  |                FROM XMT_LOC                                                 
  |               WHERE LOC_NBR = :XMT00001-LOC-NBR                             
  |            END-EXEC                                                         
25248                                                                           
00472          EVALUATE TRUE                                            INKUT104
00473              WHEN SQLCODE = -904                                  INKUT104
00474              WHEN SQLCODE = -911                                  INKUT104
00475              WHEN SQLCODE = ZERO                                  INKUT104
00476                  CONTINUE                                         INKUT104
00477              WHEN SQLWARN0 NOT = SPACES                           INKUT104
00478              WHEN SQLCODE < 0                                     INKUT104
00479              WHEN SQLCODE > 0                                     INKUT104
00480                   MOVE 'Z200-READ-STORE-TABLE'                    INKUT104
00481                         TO PV-PARAGRAPH-NAME                      INKUT104
00482                   MOVE 'READ ON STORE TABLE'                      INKUT104
00483                         TO PV-DB2-OPER-ATTEMPTED                  INKUT104
00484                   PERFORM Z900-SQL-ABEND                          INKUT104
00485          END-EVALUATE                                             INKUT104
00486      END-PERFORM.                                                 INKUT104
00487                                                                   INKUT104
00488      IF PV-SQL-SUB > PC-MAX-RETRIES                               INKUT104
00489           MOVE 'Z200-READ-STORE-TABLE'  TO PV-PARAGRAPH-NAME      INKUT104
00490           MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPER-ATTEMPTED    INKUT104
00491           PERFORM Z900-SQL-ABEND                                  INKUT104
00492      END-IF.                                                      INKUT104
00493                                                                   INKUT104
00494 ******************************************************************INKUT104
00495 *                                                                 INKUT104
00496 ******************************************************************INKUT104
00497  Z500-WRITE-EDITED-RECORD.                                        INKUT104
00498                                                                   INKUT104
C21732*    MOVE PV-LINE-NUMBER TO IN010-LINE-NUMBER.                       CL**4
00500                                                                      CL**4
00501      WRITE UPDATED-INVENTORY-RECORD                               INKUT104
00502          FROM IN010-INVENTORY-COUNT-RECORD.                       INKUT104
00503                                                                   INKUT104
00504 ******************************************************************INKUT104
00505 *                                                                 INKUT104
00506 ******************************************************************INKUT104
00507  Z600-WRITE-ERROR-REPORT-LINE.                                    INKUT104
00508                                                                   INKUT104
00509      WRITE SHEET-LINE-ERROR-RECORD                                INKUT104
00510          AFTER ADVANCING PC-ADVANCE.                              INKUT104
00511      ADD PC-ADVANCE TO PC-LINE-COUNT.                             INKUT104
00512 *                                                                 INKUT104
00513 ******************************************************************INKUT104
00514 *  STANDARD DB2 ERROR ABEND ROUTINE                               INKUT104
00515 *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             INKUT104
00516 ******************************************************************INKUT104
00517  Z900-SQL-ABEND.                                                  INKUT104
00518                                                                   INKUT104
00519      MOVE +4013                 TO ABEND-CODE.                    INKUT104
00520      DISPLAY '**  ABEND     **'.                                  INKUT104
00521      DISPLAY '**  PROGRAM   **  =  INKUT104'.                     INKUT104
00522      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            INKUT104
00523      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        INKUT104
00524                                                                   INKUT104
00525 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         INKUT104
00526 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        INKUT104
00527                                                                   INKUT104
00528      COPY DPPD004.                                                INKUT104
00529                                                                   INKUT104
00530      CALL 'ILBOABN0'  USING ABEND-CODE.                           INKUT104
