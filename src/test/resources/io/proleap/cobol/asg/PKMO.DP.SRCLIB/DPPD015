00001 *                                                                 09/12/90
00002 *      THIS PROCEDURE DIVISION COPY BOOK IS USED TO CHECK FOR     DPPD015 
00003 *  A COMMAREA UPON THE ENTRANCE INTO A TRANSACTION WRITTEN           LV012
00004 *  UNDER THE NEW CICS ARCHITECTURE.  THE FOLLOWING DATA AREA      DPPD015 
00005 *  MUST BE DEFINED IN EACH PROGRAM USING THIS ROUTINE:            DPPD015 
00006 *                                                                 DPPD015 
00007 *    01  PC-PROGRAM-CONSTANTS.                                    DPPD015 
00008 *        05  PC-NAVIGATOR-TRAN-ID                                    CL**2
00009 *                                PIC X(4)     VALUE 'Z008'.          CL**2
00010 *                                                                 DPPD015 
00011 *       BEFORE PERFORMING THE PARAGRAPH, THE FOLLOWING SWITCHES   DPPD015 
00012 *  MUST BE SET, TO TAILOR ANY ABENDS TO THE NEEDS OF THE          DPPD015 
00013 *  APPLICATION:                                                   DPPD015 
00014 *                                                                 DPPD015 
00015 *            DP013-RESTART-TERMINAL-IND                           DPPD015 
00016 *            DP013-ROLLBACK-IND                                   DPPD015 
00017 *            DP013-DISPLAY-PANEL-IND                              DPPD015 
00018 *            DP013-TRANSFER-OF-CONTROL-TYPE                       DPPD015 
00019 *                                                                 DPPD015 
00020                                                                   DPPD015 
00021  DP015-0000-CHECK-COMMAREA.                                       DPPD015 
00022      IF EIBCALEN = ZERO                                           DPPD015 
00023          EXEC CICS                                                DPPD015 
00024              RETRIEVE                                             DPPD015 
00025                  INTO   (DP020-STANDARD-COMMAREA)                    CL**3
00026                  LENGTH (PC-MAXIMUM-COMMAREA-LENGTH)                 CL*12
00027                  RESP   (PV-CICS-RESPONSE)                           CL**3
00028          END-EXEC                                                 DPPD015 
00029          EVALUATE PV-CICS-RESPONSE  ALSO  TRUE                    DPPD015 
00030              WHEN DFHRESP(ENDDATA)  ALSO  ANY                        CL**6
00031                  INITIALIZE DP020-STANDARD-COMMAREA                  CL**3
00032                  SET DP020-BAD-COMMAREA TO TRUE                      CL*12
00033                  MOVE PC-MAXIMUM-COMMAREA-LENGTH TO                  CL*12
00034                            DP020-COMMAREA-LENGTH                     CL*12
00035                  PERFORM DP015-1000-START-NAVIGATOR               DPPD015 
00036              WHEN DFHRESP(NORMAL)   ALSO                          DPPD015 
00037                                     (NOT DP020-GOOD-COMMAREA)        CL**4
00038                  INITIALIZE DP020-STANDARD-COMMAREA                  CL**3
00039                  SET DP020-BAD-COMMAREA TO TRUE                      CL*12
00040                  MOVE PC-MAXIMUM-COMMAREA-LENGTH TO                  CL*12
00041                            DP020-COMMAREA-LENGTH                     CL*12
00042                  PERFORM DP015-1000-START-NAVIGATOR               DPPD015 
00043              WHEN DFHRESP(NORMAL)   ALSO                          DPPD015 
00044                                     (DP020-GOOD-COMMAREA)            CL**4
00045                  SET DP020-SRC-RETURN-W-TRAN-ID TO TRUE           DPPD015 
00046              WHEN OTHER                                           DPPD015 
00047                  SET DP013-CICS-ABEND TO TRUE                     DPPD015 
00048                  MOVE 'DP015-0000-CHECK-COMMAREA' TO              DPPD015 
00049                            DP013-PARAGRAPH                        DPPD015 
00050                  MOVE 'ATTEMPTING TO RECEIVE THE STANDARD COMMAREADPPD015 
00051 -                          ' ' TO DP013-MESSAGE-TEXT (1)             CL**7
00052                  PERFORM DP013-0000-PROCESS-ABEND                 DPPD015 
00053          END-EVALUATE                                             DPPD015 
00054      ELSE                                                         DPPD015 
00055          MOVE DFHCOMMAREA TO DP020-STANDARD-COMMAREA                 CL**3
00056          IF (NOT DP020-GOOD-COMMAREA)                             DPPD015 
00057              INITIALIZE DP020-STANDARD-COMMAREA                      CL**3
00058              SET DP020-BAD-COMMAREA TO TRUE                          CL*12
00059              MOVE PC-MAXIMUM-COMMAREA-LENGTH TO                      CL*12
00060                        DP020-COMMAREA-LENGTH                         CL*12
00061              PERFORM DP015-1000-START-NAVIGATOR                   DPPD015 
00062          ELSE                                                     DPPD015 
00063              SET DP020-SRC-RETURN-W-TRAN-ID TO TRUE               DPPD015 
00064          END-IF                                                   DPPD015 
00065      END-IF.                                                      DPPD015 
00066 *DP015-0000-EXIT.                                                 DPPD015 
00067                                                                   DPPD015 
00068  DP015-1000-START-NAVIGATOR.                                      DPPD015 
00069      SET DP020-SRC-RETURN-W-O-TRAN-ID TO TRUE.                       CL*10
00070                                                                      CL*11
00071      EXEC CICS                                                    DPPD015 
00072          START                                                    DPPD015 
00073              TRANSID (PC-NAVIGATOR-TRAN-ID)                          CL**8
00074              FROM    (DP020-STANDARD-COMMAREA)                       CL**8
00075              LENGTH  (DP020-COMMAREA-LENGTH)                         CL*12
00076              TERMID  (EIBTRMID)                                      CL**8
00077              RESP    (PV-CICS-RESPONSE)                              CL**8
00078      END-EXEC.                                                    DPPD015 
00079                                                                   DPPD015 
00080      IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    DPPD015 
00081          SET DP013-CICS-ABEND TO TRUE                             DPPD015 
00082          MOVE 'DP015-1000-START-NAVIGATOR' TO DP013-PARAGRAPH     DPPD015 
00083          STRING 'ATTEMPTING TO START THE NAVIGATOR ('             DPPD015 
00084                                                DELIMITED BY SIZE  DPPD015 
00085                 PC-NAVIGATOR-TRAN-ID           DELIMITED BY SIZE     CL**2
00086                 ')'                            DELIMITED BY SIZE  DPPD015 
00087              INTO DP013-MESSAGE-TEXT (1)                          DPPD015 
00088          PERFORM DP013-0000-PROCESS-ABEND                         DPPD015 
00089      END-IF.                                                      DPPD015 
00090 *DP015-1000-EXIT.                                                 DPPD015 
