      ******************** HISTORY OF CHANGES **************************00581099
      *                                                                *00581199
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00581299
      * -------  ----------  ----------------------------------------  *00581399
      *                                                                *        
      ******************************************************************        
      * THIS COPY CODE NEEDS MLWS501 TO FUNCTION.                               
      *                                                                         
      *  HOW TO USE THIS:                                                       
      *      - PASS IN ENT-ID.  IF THE ENT-ID CONTAINS VALID NUMERIC            
      *        NON-ZERO, THIS PROCESS WILL RETURN THAT VALUE.                   
      *      - SET THE APPROPRIATE PV-LOOKUP-TYPE INDICATOR, USE THE            
      *        SWITCH (88 LEVEL) SKU, UPCNBR, UPCID, ITM, OR INHOUSE.           
      *        IF NOTHING IS SET HERE, IT WILL SEARCH UNTIL IT FINDS            
      *        SKU, UPC-NBR, UPC-ID, ITM-NBR, OR DEPT/INHOUSE THAT              
      *        THE PROCESS CAN USE.                                             
      *      - FILL IN THE VALUE FOR SKU, UPC-NBR, ETC DEPENDING ON             
      *        THE SETTING OF THE SWITCH.                                       
      *        INHOUSE NUMBER LOOKUP REQUIRES DEPT AND INHOUSE NBR.             
      *        ALL OTHER LOOKUP REQUIRES EFFECTIVE DATE AND ITEM                
      *        LEVEL INFORMATION SUCH AS SKU, ITM-NBR, UPC-NBR, ETC.            
      *      - IDEALLY, YOU SHOULD KNOW WHAT ITEM LEVEL INFORMATION             
      *        YOU HAVE AND SET THE PV-LOOKUP-TYPE AS APPROPRIATE.              
      *      - IF THE LOOKUP AT THE SPECIFIED LEVEL OR THE FIRST                
      *        ASSUMPTION LEVEL FAIL, THIS PROCESS WILL TRY FOR                 
      *        INHOUSE LOOKUP, IF VALID DATA IS AVAILABLE FOR LOOKUP.           
      *                                                                         
      *      - ENTRY PROCEDURE: ML501-LOOKUP-VENDOR                             
      *                                                                         
      ******************************************************************        
                                                                                
      ******************************************************************        
      * START LOOKING UP VENDOR INFORMATION                                     
      ******************************************************************        
       ML501-LOOKUP-VENDOR.                                                     
                                                                                
           SET ML501-PV-FOUND-AS-REQUESTED  TO TRUE.                            
           MOVE SPACES                      TO ML501-PV-RETURN-RSN              
           MOVE ZEROES                      TO SQLCODE.                         
                                                                                
           IF ML501-PV-ENT-ID-IN = SPACES                                       
               OR ML501-PV-ENT-ID-IN NOT NUMERIC                                
               OR ML501-PV-ENT-ID-IN = ZEROES                                   
               PERFORM ML501-CHECK-OPTION                                       
26006          IF ML501-PV-ENT-ID-OUT IS EQUAL TO ZEROES                        
26006              SET ML501-PV-ENT-NOT-FOUND TO TRUE                           
26006          END-IF                                                           
           ELSE                                                                 
               SET ML501-PV-ENT-ID-GIVEN    TO TRUE                             
               MOVE ML501-PV-ENT-ID-IN      TO ML501-PV-ENT-ID-OUT              
           END-IF.                                                              
                                                                                
           MOVE SPACES                      TO ML501-PV-ENT-ID-IN               
                                               ML501-PV-INHOUSE-NBR-IN          
                                               ML501-PV-DEPT-IN                 
                                               ML501-PV-SKU-IN                  
                                               ML501-PV-UPC-NBR-IN              
                                               ML501-PV-UPC-ID-IN               
                                               ML501-PV-ITM-NBR-IN.             
                                                                                
      ******************************************************************        
      * VALIDATE/CHECK OPTION FROM INPUT PARAMETERS                             
      ******************************************************************        
       ML501-CHECK-OPTION.                                                      
                                                                                
           EVALUATE TRUE                                                        
               WHEN ML501-PV-SKU-LOOKUP                                         
                   IF ML501-PV-SKU-IN = SPACES                                  
                       OR ML501-PV-SKU-IN NOT NUMERIC                           
                       OR ML501-PV-SKU-IN = ZEROES                              
                       OR ML501-PV-DUMMY-SKU                                    
                       IF NOT ML501-PV-DUMMY-SKU                                
                           DISPLAY ' *** BAD SKU ' ML501-PV-SKU-IN              
                                   ', ATTEMPT DEFAULT LOOKUP'                   
                       END-IF                                                   
                       PERFORM ML501-LOOKUP-DEFAULT                             
                   ELSE                                                         
                       MOVE ML501-PV-SKU-IN-9 TO ML501-PV-WORK-SKU              
                       PERFORM ML501-LOOKUP-BY-SKU                              
                   END-IF                                                       
               WHEN ML501-PV-UPC-NBR-LOOKUP                                     
                   IF ML501-PV-UPC-NBR-IN = SPACES                              
                       OR ML501-PV-UPC-NBR-IN NOT NUMERIC                       
                       OR ML501-PV-UPC-NBR-IN = ZEROES                          
                       DISPLAY ' *** BAD UPC-NBR ' ML501-PV-UPC-NBR-IN          
                               ', ATTEMPT DEFAULT LOOKUP'                       
                       PERFORM ML501-LOOKUP-DEFAULT                             
                   ELSE                                                         
                       MOVE ML501-PV-UPC-NBR-IN-9                               
                           TO ML501-PV-WORK-UPC-NBR                             
                       PERFORM ML501-LOOKUP-BY-UPC-NBR                          
                   END-IF                                                       
               WHEN ML501-PV-UPC-ID-LOOKUP                                      
                   IF ML501-PV-UPC-ID-IN = SPACES                               
                       OR ML501-PV-UPC-ID-IN NOT NUMERIC                        
                       OR ML501-PV-UPC-ID-IN = ZEROES                           
                       DISPLAY ' *** BAD UPC-ID ' ML501-PV-UPC-ID-IN            
                               ', ATTEMPT DEFAULT LOOKUP'                       
                       PERFORM ML501-LOOKUP-DEFAULT                             
                   ELSE                                                         
                       MOVE ML501-PV-UPC-ID-IN-9 TO ML501-PV-WORK-UPC-ID        
                       PERFORM ML501-LOOKUP-BY-UPC-ID                           
                   END-IF                                                       
               WHEN ML501-PV-ITM-NBR-LOOKUP                                     
                   IF ML501-PV-ITM-NBR-IN = SPACES                              
                       OR ML501-PV-ITM-NBR-IN NOT NUMERIC                       
                       OR ML501-PV-ITM-NBR-IN = ZEROES                          
                       DISPLAY ' *** BAD ITM-NBR ' ML501-PV-UPC-NBR-IN          
                               ', ATTEMPT DEFAULT LOOKUP'                       
                       PERFORM ML501-LOOKUP-DEFAULT                             
                   ELSE                                                         
                       MOVE ML501-PV-ITM-NBR-IN-9                               
                           TO ML501-PV-WORK-ITM-NBR                             
                       PERFORM ML501-LOOKUP-BY-ITM-NBR                          
                   END-IF                                                       
               WHEN ML501-PV-INHOUSE-LOOKUP                                     
                   IF (ML501-PV-INHOUSE-NBR-IN = SPACES OR                      
                       ML501-PV-INHOUSE-NBR-IN NOT NUMERIC OR                   
                       ML501-PV-INHOUSE-NBR-IN = ZEROES)                        
                       OR                                                       
                       (ML501-PV-DEPT-IN = SPACES OR                            
                        ML501-PV-DEPT-IN NOT NUMERIC OR                         
                        ML501-PV-DEPT-IN = ZEROES)                              
                       DISPLAY ' *** BAD DEPT ' ML501-PV-DEPT-IN                
                               ' OR IN-HOUSE ' ML501-PV-INHOUSE-NBR-IN          
26006                  MOVE ZEROES            TO ML501-PV-ENT-ID-OUT            
26006                  SET ML501-PV-NOT-FOUND TO TRUE                           
                   ELSE                                                         
                       PERFORM ML501-LOOKUP-BY-INHOUSE                          
                   END-IF                                                       
               WHEN OTHER                                                       
                   PERFORM ML501-LOOKUP-DEFAULT                                 
           END-EVALUATE.                                                        
                                                                                
      * ATTEMPT LOOKUP USING INHOUSE NUMBER, IF IT IS NOT ALREADY TRIED         
           IF (ML501-PV-NOT-FOUND OR ML501-PV-SQL-ERROR)                        
               AND NOT ML501-PV-INHOUSE-LOOKUP                                  
               IF (ML501-PV-INHOUSE-NBR-IN = SPACES OR                          
                   ML501-PV-INHOUSE-NBR-IN NOT NUMERIC OR                       
                   ML501-PV-INHOUSE-NBR-IN = ZEROES)                            
                   OR                                                           
                   (ML501-PV-DEPT-IN = SPACES OR                                
                    ML501-PV-DEPT-IN NOT NUMERIC OR                             
                    ML501-PV-DEPT-IN = ZEROES)                                  
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE ML501-PV-INHOUSE-NBR-IN-9                               
                       TO ML501-PV-WORK-INHOUSE                                 
                   MOVE ML501-PV-DEPT-IN-9    TO ML501-PV-WORK-DEPT-9           
                   PERFORM ML501-LOOKUP-BY-INHOUSE                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * DEFAULT VENDOR INFORMATION LOOKUP                                       
      * - THIS ROUTINE WILL CHECK FOR A NON-ZERO/NON-SPACE AND VALID            
      *   NUMERIC ENT-ID IN THE INPUT PARAMETER.  RETURN THE INPUT              
      *   ENT-ID AS GIVEN.                                                      
      * - CHECK SKU, UPC-NBR, UPC-ID, THEN ITM-NBR IN THE GIVEN ORDER           
      *   AND USE THE FIRST NON-SPACE/NON-ZERO AND VALID NUMERIC AS A           
      *   BASIS FOR THE LOOKUP.                                                 
      * - LAST ATTEMPT WILL USE THE INHOUSE NUMBER ALONG WITH DEPT              
      *   TO FIND THE ENT-ID, IF INHOUSE NUMBER AND DEPT ARE NON-SPACE/         
      *   NON-ZERO AND VALID NUMERIC.                                           
      * - FAILURE FROM THESE ATTEMPTS WILL RESULT IN A NOT FOUND                
      *   CONDITION BEING RETURNED.                                             
      ******************************************************************        
       ML501-LOOKUP-DEFAULT.                                                    
                                                                                
           SET ML501-PV-FOUND-WITH-ASSUMPTION TO TRUE.                          
                                                                                
           IF ML501-PV-SKU-IN = SPACES                                          
               OR ML501-PV-SKU-IN NOT NUMERIC                                   
               OR ML501-PV-SKU-IN = ZEROES                                      
               OR ML501-PV-DUMMY-SKU                                            
               IF ML501-PV-UPC-NBR-IN = SPACES                                  
                   OR ML501-PV-UPC-NBR-IN NOT NUMERIC                           
                   OR ML501-PV-UPC-NBR-IN = ZEROES                              
                   IF ML501-PV-UPC-ID-IN = SPACES                               
                       OR ML501-PV-UPC-ID-IN NOT NUMERIC                        
                       OR ML501-PV-UPC-ID-IN = ZEROES                           
                       IF ML501-PV-ITM-NBR-IN = SPACES                          
                           OR ML501-PV-ITM-NBR-IN NOT NUMERIC                   
                           OR ML501-PV-ITM-NBR-IN = ZEROES                      
                           IF (ML501-PV-INHOUSE-NBR-IN = SPACES OR              
                               ML501-PV-INHOUSE-NBR-IN NOT NUMERIC OR           
                               ML501-PV-INHOUSE-NBR-IN = ZEROES)                
                               OR                                               
                               (ML501-PV-DEPT-IN = SPACES OR                    
                                ML501-PV-DEPT-IN NOT NUMERIC OR                 
                                ML501-PV-DEPT-IN = ZEROES)                      
                               SET ML501-PV-NO-LOOKUP-TYPE    TO TRUE           
                           ELSE                                                 
                               SET ML501-PV-TRY-INHOUSE-LOOKUP TO TRUE          
                               SET ML501-PV-INHOUSE-LOOKUP    TO TRUE           
                           END-IF                                               
                       ELSE                                                     
                           SET ML501-PV-TRY-ITM-NBR-LOOKUP TO TRUE              
                           SET ML501-PV-ITM-NBR-LOOKUP    TO TRUE               
                       END-IF                                                   
                   ELSE                                                         
                       SET ML501-PV-TRY-UPC-ID-LOOKUP TO TRUE                   
                       SET ML501-PV-UPC-ID-LOOKUP     TO TRUE                   
                   END-IF                                                       
               ELSE                                                             
                   SET ML501-PV-TRY-UPC-NBR-LOOKUP TO TRUE                      
                   SET ML501-PV-UPC-NBR-LOOKUP    TO TRUE                       
               END-IF                                                           
           ELSE                                                                 
               SET ML501-PV-TRY-SKU-LOOKUP    TO TRUE                           
               SET ML501-PV-SKU-LOOKUP        TO TRUE                           
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
               WHEN ML501-PV-SKU-LOOKUP                                         
                   MOVE ML501-PV-SKU-IN-9     TO ML501-PV-WORK-SKU              
                   PERFORM ML501-LOOKUP-BY-SKU                                  
               WHEN ML501-PV-UPC-NBR-LOOKUP                                     
                   MOVE ML501-PV-UPC-NBR-IN-9 TO ML501-PV-WORK-UPC-NBR          
                   PERFORM ML501-LOOKUP-BY-UPC-NBR                              
               WHEN ML501-PV-UPC-ID-LOOKUP                                      
                   MOVE ML501-PV-UPC-ID-IN-9  TO ML501-PV-WORK-UPC-ID           
                   PERFORM ML501-LOOKUP-BY-UPC-ID                               
               WHEN ML501-PV-ITM-NBR-LOOKUP                                     
                   MOVE ML501-PV-ITM-NBR-IN-9 TO ML501-PV-WORK-ITM-NBR          
                   PERFORM ML501-LOOKUP-BY-ITM-NBR                              
               WHEN ML501-PV-INHOUSE-LOOKUP                                     
                   MOVE ML501-PV-INHOUSE-NBR-IN-9                               
                       TO ML501-PV-WORK-INHOUSE                                 
                   MOVE ML501-PV-DEPT-IN-9    TO ML501-PV-WORK-DEPT-9           
                   PERFORM ML501-LOOKUP-BY-INHOUSE                              
               WHEN OTHER                                                       
                   MOVE ZEROES                TO ML501-PV-ENT-ID-OUT            
                   SET ML501-PV-NOT-FOUND     TO TRUE                           
                   SET ML501-PV-FAIL-ASSUMPTION-RULE TO TRUE                    
           END-EVALUATE.                                                        
                                                                                
      * ATTEMPT LOOKUP USING INHOUSE NUMBER, IF IT IS NOT ALREADY TRIED         
           IF (ML501-PV-NOT-FOUND OR ML501-PV-SQL-ERROR)                        
               AND NOT ML501-PV-INHOUSE-LOOKUP                                  
               AND NOT ML501-PV-FAIL-ASSUMPTION-RULE                            
               IF (ML501-PV-INHOUSE-NBR-IN = SPACES OR                          
                   ML501-PV-INHOUSE-NBR-IN NOT NUMERIC OR                       
                   ML501-PV-INHOUSE-NBR-IN = ZEROES)                            
                   OR                                                           
                   (ML501-PV-DEPT-IN = SPACES OR                                
                    ML501-PV-DEPT-IN NOT NUMERIC OR                             
                    ML501-PV-DEPT-IN = ZEROES)                                  
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE ML501-PV-INHOUSE-NBR-IN-9                               
                       TO ML501-PV-WORK-INHOUSE                                 
                   MOVE ML501-PV-DEPT-IN-9    TO ML501-PV-WORK-DEPT-9           
                   PERFORM ML501-LOOKUP-BY-INHOUSE                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * VENDOR LOOKUP BY SKU                                                    
      ******************************************************************        
       ML501-LOOKUP-BY-SKU.                                                     
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                      C.ENT_ID                                                  
                 INTO                                                           
                      :ML501-PV-ENT-ID-OUT                                      
                 FROM                                                           
                      TSKXREF    A                                              
                     ,TUPC       B                                              
                     ,TVNDSTL    C                                              
                WHERE                                                           
                      A.SKU         = :ML501-PV-WORK-SKU                        
                  AND A.SKU_STAT_CDE NOT IN ('98', '00')                        
                  AND A.INTR_UPC_ID     = B.UPC_ID                              
                  AND B.VS_ID           = C.VS_ID                               
                  AND :ML501-PV-EFF-DTE-IN                                      
                          BETWEEN B.EFF_BEG_DTE                                 
                          AND COALESCE(B.EFF_END_DTE, '9999-12-31')             
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   MOVE ZEROES            TO ML501-PV-ENT-ID-OUT                
                   SET ML501-PV-NOT-FOUND TO TRUE                               
               WHEN OTHER                                                       
                   MOVE ZEROES            TO ML501-PV-ENT-ID-OUT                
                   SET ML501-PV-SQL-ERROR TO TRUE                               
                   DISPLAY ' *** FAIL VENDOR LOOKUP'                            
                           '     PARAGRAPH: ML501-LOOKUP-BY-SKU'                
                   DISPLAY '     SKU = ' ML501-PV-WORK-SKU                      
                           ' EFF DATE = ' ML501-PV-EFF-DTE-IN                   
                           ' SQLCODE = ' SQLCODE                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * VENDOR LOOKUP BY UPC-NBR                                                
      ******************************************************************        
       ML501-LOOKUP-BY-UPC-NBR.                                                 
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                      C.ENT_ID                                                  
                 INTO                                                           
                      :ML501-PV-ENT-ID-OUT                                      
                 FROM                                                           
                      TUPC       B                                              
                     ,TVNDSTL    C                                              
                WHERE                                                           
                      B.UPC_NBR         = :ML501-PV-WORK-UPC-NBR                
                  AND B.VS_ID           = C.VS_ID                               
                  AND :ML501-PV-EFF-DTE-IN                                      
                          BETWEEN B.EFF_BEG_DTE                                 
                          AND COALESCE(B.EFF_END_DTE, '9999-12-31')             
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   MOVE ZEROES            TO ML501-PV-ENT-ID-OUT                
                   SET ML501-PV-NOT-FOUND TO TRUE                               
               WHEN OTHER                                                       
                   MOVE ZEROES            TO ML501-PV-ENT-ID-OUT                
                   SET ML501-PV-SQL-ERROR TO TRUE                               
                   DISPLAY ' *** FAIL VENDOR LOOKUP'                            
                           '     PARAGRAPH: ML501-LOOKUP-BY-UPC-NBR'            
                   DISPLAY '     UPC-NBR = ' ML501-PV-WORK-UPC-NBR              
                           ' EFF DATE = ' ML501-PV-EFF-DTE-IN                   
                           ' SQLCODE = ' SQLCODE                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * VENDOR LOOKUP BY UPC-ID                                                 
      ******************************************************************        
       ML501-LOOKUP-BY-UPC-ID.                                                  
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                      C.ENT_ID                                                  
                 INTO                                                           
                      :ML501-PV-ENT-ID-OUT                                      
                 FROM                                                           
                      TUPC       B                                              
                     ,TVNDSTL    C                                              
                WHERE                                                           
                      B.UPC_ID          = :ML501-PV-WORK-UPC-ID                 
                  AND B.VS_ID           = C.VS_ID                               
                  AND :ML501-PV-EFF-DTE-IN                                      
                          BETWEEN B.EFF_BEG_DTE                                 
                          AND COALESCE(B.EFF_END_DTE, '9999-12-31')             
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   MOVE ZEROES            TO ML501-PV-ENT-ID-OUT                
                   SET ML501-PV-NOT-FOUND TO TRUE                               
               WHEN OTHER                                                       
                   MOVE ZEROES            TO ML501-PV-ENT-ID-OUT                
                   SET ML501-PV-SQL-ERROR TO TRUE                               
                   DISPLAY ' *** FAIL VENDOR LOOKUP'                            
                           '     PARAGRAPH: ML501-LOOKUP-BY-UPC-ID'             
                   DISPLAY '     UPC-ID = ' ML501-PV-WORK-UPC-ID                
                           ' EFF DATE = ' ML501-PV-EFF-DTE-IN                   
                           ' SQLCODE = ' SQLCODE                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * VENDOR LOOKUP BY ITEM NUMBER                                            
      ******************************************************************        
       ML501-LOOKUP-BY-ITM-NBR.                                                 
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                      C.ENT_ID                                                  
                 INTO                                                           
                      :ML501-PV-ENT-ID-OUT                                      
                 FROM                                                           
                      TSKXREF    A                                              
                     ,TUPC       B                                              
                     ,TVNDSTL    C                                              
                WHERE                                                           
                      A.ITM_NBR     = :ML501-PV-WORK-ITM-NBR                    
                  AND A.SKU_STAT_CDE NOT IN ('98', '00')                        
                  AND A.INTR_UPC_ID     = B.UPC_ID                              
                  AND B.VS_ID           = C.VS_ID                               
                  AND :ML501-PV-EFF-DTE-IN                                      
                          BETWEEN B.EFF_BEG_DTE                                 
                          AND COALESCE(B.EFF_END_DTE, '9999-12-31')             
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   MOVE ZEROES            TO ML501-PV-ENT-ID-OUT                
                   SET ML501-PV-NOT-FOUND TO TRUE                               
               WHEN OTHER                                                       
                   MOVE ZEROES            TO ML501-PV-ENT-ID-OUT                
                   SET ML501-PV-SQL-ERROR TO TRUE                               
                   DISPLAY ' *** FAIL VENDOR LOOKUP'                            
                           '     PARAGRAPH: ML501-LOOKUP-BY-ITM-NBR'            
                   DISPLAY '     ITEM NUMBER = ' ML501-PV-WORK-ITM-NBR          
                           ' EFF DATE = ' ML501-PV-EFF-DTE-IN                   
                           ' SQLCODE = ' SQLCODE                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * VENDOR LOOKUP BY INHOUSE NUMBER                                         
      ******************************************************************        
       ML501-LOOKUP-BY-INHOUSE.                                                 
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                      ENT_ID                                                    
                 INTO                                                           
                      :ML501-PV-ENT-ID-OUT                                      
                 FROM                                                           
                      TVNDDPT                                                   
                WHERE                                                           
                      DEPT_NBR    = :ML501-PV-WORK-DEPT-3X                      
                  AND INHOUSE_NBR = :ML501-PV-WORK-INHOUSE                      
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   MOVE ZEROES            TO ML501-PV-ENT-ID-OUT                
                   SET ML501-PV-NOT-FOUND TO TRUE                               
               WHEN OTHER                                                       
                   MOVE ZEROES            TO ML501-PV-ENT-ID-OUT                
                   SET ML501-PV-SQL-ERROR TO TRUE                               
                   DISPLAY ' *** FAIL VENDOR LOOKUP'                            
                           '     PARAGRAPH: ML501-LOOKUP-BY-INHOUSE'            
                   DISPLAY '     INHOUSE NBR = ' ML501-PV-WORK-INHOUSE          
                           ' DEPT = ' ML501-PV-WORK-DEPT-3X                     
                           ' SQLCODE = ' SQLCODE                                
           END-EVALUATE.                                                        
                                                                                
