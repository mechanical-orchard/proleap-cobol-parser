       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      SFKUT072.                                       00020001
       AUTHOR.          JIM HUNTER.                                     00030001
       DATE-WRITTEN.    MARCH, 2012.                                    00040001
                                                                        00050000
      *-----------------------------------------------------------      00060000
      *   MLS FLOOR PAD AND BUSINESS GROUP FILE FORMATTER               00070001
      *-----------------------------------------------------------      00080000
      *   THIS PROGRAM WILL FORMAT THE FLOOR PAD, BUSINESS GROUP        00090001
      *   AND FLOOR PAD / BUSINESS GROUP ASSOCIATION FILES SENT         00100001
      *   TO THE STORES AS PART OF THE MLS PROCESS.                     00110001
      *   EACH FILE WILL HAVE IT'S OWN HEADER, BUT, ALL 3 TYPES         00120001
      *   OF RECORDS WILL BE WRITTEN TO THE SAME OUTPUT FILE.           00130001
      *   THE MIDTIER SERVER WILL DISTRIBUTE THEM TO THE STORES.        00140001
      *   ALL 3 FILES WILL BE 'FULL' FILES AND GO TO ALL STORES.        00150003
      *-----------------------------------------------------------      00160000
      * HISTORY LOG:                                                    00170000
      *-----------------------------------------------------------      00180000
      * DATE:        JUNE, 2012                                         00190013
      * WR/IR#:      WR41115                                            00200013
      * PROGRAMMER:  JIM HUNTER                                         00210013
      * DESCRIPTION: INITIAL PROGRAM MOVE                               00220013
      *                                                                 00230000
      *----------------------------------------------------------       00240000
       ENVIRONMENT DIVISION.                                            00250000
      *----------------------------------------------------------       00260000
       CONFIGURATION SECTION.                                           00270000
       SOURCE-COMPUTER.        IBM-3090.                                00280000
       OBJECT-COMPUTER.        IBM-3090.                                00290000
                                                                        00300000
       INPUT-OUTPUT SECTION.                                            00310000
                                                                        00320000
       FILE-CONTROL.                                                    00330000
                                                                        00340000
           SELECT FLOOR-PAD-FILE                                        00350001
                  ASSIGN TO INP01.                                      00360000
           SELECT BUSINESS-GROUP-FILE                                   00370001
                  ASSIGN TO INP02.                                      00380000
           SELECT FLRPAD-BUSGRP-ASSOC-FILE                              00390001
                  ASSIGN TO INP03.                                      00400001
           SELECT FORMATTED-FLRPAD-BUSGRP-FILE                          00410003
                  ASSIGN TO OUT01.                                      00420001
                                                                        00430000
       DATA DIVISION.                                                   00440000
       FILE SECTION.                                                    00450000
                                                                        00460000
       FD  FLOOR-PAD-FILE                                               00470001
           RECORDING MODE F                                             00480000
           BLOCK CONTAINS 0 RECORDS                                     00490000
           LABEL RECORDS ARE OMITTED                                    00500000
           DATA RECORD IS FLOOR-PAD-RECORD.                             00510001
       01  FLOOR-PAD-RECORD                PIC X(120).                  00520001
                                                                        00530000
       FD  BUSINESS-GROUP-FILE                                          00540001
           RECORDING MODE F                                             00550000
           BLOCK CONTAINS 0 RECORDS                                     00560000
           LABEL RECORDS ARE OMITTED                                    00570000
           DATA RECORD IS BUSINESS-GROUP-RECORD.                        00580001
       01  BUSINESS-GROUP-RECORD           PIC X(80).                   00590001
                                                                        00600000
       FD  FLRPAD-BUSGRP-ASSOC-FILE                                     00610001
           RECORDING MODE F                                             00620001
           BLOCK CONTAINS 0 RECORDS                                     00630001
           LABEL RECORDS ARE OMITTED                                    00640001
           DATA RECORD IS FLRPAD-BUSGRP-ASSOC-RECORD.                   00650001
       01  FLRPAD-BUSGRP-ASSOC-RECORD          PIC X(80).               00660011
                                                                        00670001
       FD  FORMATTED-FLRPAD-BUSGRP-FILE                                 00680003
           RECORDING MODE IS V                                          00690000
           RECORD IS VARYING IN SIZE                                    00700000
           FROM 1 TO 120 CHARACTERS                                     00710006
           DEPENDING ON PV-BYTE-COUNT                                   00720000
           LABEL RECORDS ARE OMITTED                                    00730000
           DATA RECORD IS FORMATTED-FLRPAD-BUSGRP-RECORD.               00740003
       01  FORMATTED-FLRPAD-BUSGRP-RECORD.                              00750003
           05  FRMTED-FLRPAD-BUSGRP-REC-FIELD PIC X(01)                 00760003
               OCCURS 120 TIMES                                         00770006
                    INDEXED BY  OUT-LEN.                                00780001
                                                                        00790000
       WORKING-STORAGE SECTION.                                         00800000
      *-----------------------------------------------------------      00810000
      * PS- PROGRAM SWITCHES                                            00820000
      *-----------------------------------------------------------      00830000
       01  PS-PROGRAM-SWITCHES.                                         00840000
           05  PS-END-OF-BUS-GRP-SW        PIC X(01) VALUE 'N'.         00850002
               88  PS-END-OF-BUS-GRP                 VALUE 'Y'.         00860002
               88  PS-NOT-END-OF-BUS-GRP             VALUE 'N'.         00870002
           05  PS-END-OF-FLR-PAD-SW        PIC X(01) VALUE 'N'.         00880002
               88  PS-END-OF-FLR-PAD                 VALUE 'Y'.         00890002
               88  PS-NOT-END-OF-FLR-PAD             VALUE 'N'.         00900002
           05  PS-END-OF-FLRPAD-BUSGRP-SW  PIC X(01) VALUE 'N'.         00910005
               88  PS-END-OF-FLRPAD-BUSGRP           VALUE 'Y'.         00920005
               88  PS-NOT-END-OF-FLRPAD-BUSGRP       VALUE 'N'.         00930005
           05  PS-MLS-HDR-NOT-WRITTEN-SW   PIC X(01) VALUE 'N'.         00940002
               88  PS-MLS-HDR-WRITTEN                VALUE 'N'.         00950002
               88  PS-MLS-HDR-NOT-WRITTEN            VALUE 'Y'.         00960002
                                                                        00970000
      *-----------------------------------------------------------      00980000
      * PROGRAM CONSTANTS                                               00990000
      *-----------------------------------------------------------      01000000
       01  PC-PROGRAM-CONSTANTS.                                        01010000
           05  PC-CURRENT-PROGRAM-NAME  PIC X(08) VALUE 'SFKUT072'.     01020001
           05  PC-CSV-DELIMITER             PIC X(01) VALUE '|'.        01030000
           05  PC-UNDER-SCORE               PIC X(01) VALUE '_'.        01040000
           05  PC-ONE-SPACE                 PIC X(01) VALUE ' '.        01050000
           05  PC-ONE-ZERO                  PIC X(01) VALUE '0'.        01060000
           05  PC-STR-GRP-ID-5000           PIC X(05) VALUE '05000'.    01070000
           05  PC-STR-GRP-ID-1000           PIC X(05) VALUE '01000'.    01080000
           05  PC-FIVE-ZERO                 PIC X(05) VALUE '00000'.    01090000
           05  PC-HDR-IND                   PIC X(03) VALUE 'HDR'.      01100000
           05  PC-FILE-APPLY-TYPE-FUL       PIC X(01) VALUE 'N'.        01110000
           05  PC-FILE-APPLY-TYPE-CHG       PIC X(01) VALUE 'C'.        01120000
                                                                        01130000
      *-----------------------------------------------------------      01140000
      * PV- PROGRAM VARIABLES                                           01150000
      *-----------------------------------------------------------      01160000
       01  PV-PROGRAM-VARAIBLES.                                        01170000
           05  PV-RECORD-ACTION             PIC X      VALUE SPACE.     01180002
           05  PV-PREV-DEST-LOC-NBR         PIC S9(4)  VALUE ZERO.      01190002
           05  PV-CURRENT-DATE              PIC X(10)  VALUE SPACES.    01200002
           05  PV-FORMAT-CORR-ID.                                       01210000
               10  PV-FORMAT-MQ-CORR-ID     PIC X(03).                  01220002
               10  PV-NULL-VALUE            PIC X(21).                  01230002
           05  PV-TALLY                     PIC S9(03) COMP-3 VALUE +0. 01240003
           05  PV-PIPE-COUNT                PIC 9(04)  VALUE ZERO.      01250002
           05  PV-BYTE-COUNT                PIC 9(04)  VALUE ZERO.      01260002
           05  PV-RECORD-COUNT              PIC 9(09)  VALUE ZERO.      01270002
           05  PV-SPACE                     PIC X(01).                  01280002
           05  PV-DISPLAY-INDEX             PIC S9(04) COMP.            01290000
           05  PV-STR-GRP-TYP-CDE           PIC 9(05)  VALUE ZERO.      01300002
           05  PV-STR-GRP-ID-GRP.                                       01310000
               10  PV-FIRST-BYTES           PIC 9(05)  VALUE ZERO.      01320002
               10  PV-STR-GRP-ID            PIC 9(05)  VALUE ZERO.      01330002
                                                                        01340000
           05  PV-VARIABLE-OUTPUT-FIELD.                                01350000
               10  PV-BIT-VARIABLE          PIC X(01)                   01360000
                                                VALUE SPACES            01370000
                                                OCCURS 20 TIMES         01380000
                                                INDEXED BY              01390000
                                                PV-BIT-IDX              01400000
                                                PV-EOF-IDX.             01410000
                                                                        01420000
           05  PV-VARIABLE-CHAR-FIELD.                                  01430000
               10  PV-BIT-CHR-VARIABLE      PIC X(01)                   01440000
                                                VALUE SPACES            01450000
                                                OCCURS 260 TIMES        01460000
                                                INDEXED BY              01470000
                                                PV-CHR-BIT-IDX          01480000
                                                PV-CHR-EOF-IDX.         01490000
                                                                        01500000
           05  PV-SAVE-PREV-LOC-NBR         PIC X(05) VALUE SPACES.     01510000
           05  PV-RUN-TIME.                                             01520000
               15  R-HR                     PIC 99.                     01530002
               15  R-MIN                    PIC 99.                     01540002
               15  R-SEC                    PIC 99.                     01550002
               15  R-TH                     PIC 99.                     01560002
                                                                        01570000
           05  PV-DATE-IN.                                              01580000
               15 DATE-IN-CC                PIC XX.                     01590002
               15 DATE-IN-YY                PIC XX.                     01600002
               15 DATE-IN-MM                PIC XX.                     01610002
               15 DATE-IN-DD                PIC XX.                     01620002
           05  PV-TIMESTAMP                 PIC X(26) VALUE SPACES.     01630000
           05  PV-CURRENT-TMST REDEFINES    PV-TIMESTAMP.               01640000
               10  PV-CURR-YYYY             PIC 9(04).                  01650000
               10  FILLER                   PIC X(01).                  01660000
               10  PV-CURR-MO               PIC 9(02).                  01670000
               10  FILLER                   PIC X(01).                  01680000
               10  PV-CURR-DD               PIC 9(02).                  01690000
               10  FILLER                   PIC X(01).                  01700000
               10  PV-CURR-HH               PIC 9(02).                  01710000
               10  FILLER                   PIC X(01).                  01720000
               10  PV-CURR-MM               PIC 9(02).                  01730000
               10  FILLER                   PIC X(01).                  01740000
               10  PV-CURR-SS               PIC 9(02).                  01750000
               10  FILLER                   PIC X(01).                  01760000
               10  PV-CURR-SSSSSS           PIC 9(06).                  01770000
                                                                        01780000
           05  PV-HEADER-TMST.                                          01790000
               10  PV-HEADER-YYYY           PIC 9(04).                  01800000
               10  PV-HEADER-YYYY-RED REDEFINES PV-HEADER-YYYY.         01810000
                15  PV-HEADER-CC            PIC 9(02).                  01820000
                15  PV-HEADER-YY            PIC 9(02).                  01830000
               10  PV-HEADER-MO             PIC 9(02)  VALUE ZERO.      01840000
               10  PV-HEADER-DD             PIC 9(02)  VALUE ZERO.      01850000
               10  PV-HEADER-HH             PIC 9(02)  VALUE ZERO.      01860000
               10  PV-HEADER-MM             PIC 9(02)  VALUE ZERO.      01870000
               10  PV-HEADER-SS             PIC 9(02)  VALUE ZERO.      01880000
           05  PV-QUOTIENT                  PIC 9(04)  VALUE ZERO.      01890000
           05  PV-RMDR-4                    PIC 9(04)  VALUE ZERO.      01900000
           05  PV-RMDR-100                  PIC 9(04)  VALUE ZERO.      01910000
      *----------------------------------------------------------       01920000
      * COPYBOOK  EPRD217 LAYOUT (HEADER LAYOUT)                        01930000
      *----------------------------------------------------------       01940000
           COPY EPRD217.                                                01950000
                                                                        01960000
      *----------------------------------------------------------       01970000
      * FLOOR PAD INPUT RECORD CREATED BY SQLPLUS                       01980003
      *----------------------------------------------------------       01990000
        01 PV-FLOOR-PAD-IN-REC.                                         02000003
           05 PV-IN-FLR-PAD-FILLER1           PIC X     VALUE SPACES.   02010003
           05 PV-IN-FLR-PAD-ID                PIC 9(10) VALUE ZEROES.   02020003
           05 PV-IN-FLR-PAD-FILLER2           PIC X     VALUE SPACES.   02030003
           05 PV-IN-FLR-PAD-CDE               PIC XX    VALUE SPACES.   02040003
           05 PV-IN-FLR-PAD-FILLER3           PIC X     VALUE SPACES.   02050003
           05 PV-IN-FLR-PAD-DESC              PIC X(40) VALUE SPACES.   02060003
           05 PV-IN-FLR-PAD-FILLER4           PIC X     VALUE SPACES.   02070003
           05 PV-IN-FLR-PAD-AREA-TYP-CDE      PIC X     VALUE SPACES.   02080006
           05 PV-IN-FLR-PAD-FILLER5           PIC X     VALUE SPACES.   02090003
           05 PV-IN-FLR-PAD-EFF-BEG-DTE       PIC X(10) VALUE SPACES.   02100003
           05 PV-IN-FLR-PAD-FILLER6           PIC X     VALUE SPACES.   02110003
           05 PV-IN-FLR-PAD-EFF-END-DTE       PIC X(10) VALUE SPACES.   02120003
           05 PV-IN-FLR-PAD-FILLER7           PIC X     VALUE SPACES.   02130003
           05 PV-IN-FLR-PAD-LAST-UPDT-TMST    PIC X(19) VALUE SPACES.   02140003
           05 PV-IN-FLR-PAD-FILLER8           PIC X     VALUE SPACES.   02150003
           05 PV-IN-FLR-PAD-LAST-UPDT-USR-ID  PIC X(8)  VALUE SPACES.   02160008
           05 PV-IN-FLR-PAD-FILLER9           PIC X(12) VALUE SPACES.   02170003
                                                                        02180003
      *----------------------------------------------------------       02190003
      * BUSINESS GROUP RECORD CREATED BY SQLPLUS                        02200003
      *----------------------------------------------------------       02210003
        01 PV-BUSINESS-GROUP-IN-REC.                                    02220003
           05 PV-IN-BUS-GRP-FILLER1           PIC X     VALUE SPACES.   02230003
           05 PV-IN-BUS-GRP-NBR               PIC 9(3)  VALUE ZEROES.   02240003
           05 PV-IN-BUS-GRP-FILLER2           PIC XX    VALUE SPACES.   02250003
           05 PV-IN-BUS-GRP-AREA-ID           PIC 99    VALUE ZEROES.   02260003
           05 PV-IN-BUS-GRP-FILLER3           PIC X     VALUE SPACES.   02270003
           05 PV-IN-BUS-GRP-DESC              PIC X(25) VALUE SPACES.   02280003
           05 PV-IN-BUS-GRP-FILLER4           PIC X     VALUE SPACES.   02290003
           05 PV-IN-BUS-GRP-SHRT-DESC         PIC X(8)  VALUE SPACES.   02300003
           05 PV-IN-BUS-GRP-FILLER5           PIC X(37) VALUE SPACES.   02310003
                                                                        02320003
      *----------------------------------------------------------       02330003
      * FLOOR PAD / BUSINESS GROUP RECORD CREATED BY SQLPLUS            02340005
      *----------------------------------------------------------       02350003
        01 PV-FLR-PAD-BUS-GRP-IN-REC.                                   02360006
           05 PV-IN-FP-BG-FILLER1             PIC X     VALUE SPACES.   02370005
           05 PV-IN-FP-BG-BUS-GRP-NBR         PIC 9(3)  VALUE ZEROES.   02380005
           05 PV-IN-FP-BG-FILLER2             PIC X     VALUE SPACES.   02390012
           05 PV-IN-FP-BG-EFF-BEG-DTE         PIC X(10) VALUE SPACES.   02400005
           05 PV-IN-FP-BG-FILLER3             PIC X     VALUE SPACES.   02410005
           05 PV-IN-FP-BG-EFF-END-DTE         PIC X(10) VALUE SPACES.   02420005
           05 PV-IN-FP-BG-FILLER4             PIC XX    VALUE SPACES.   02430005
           05 PV-IN-FP-BG-FLR-PAD-ID          PIC 9(10) VALUE ZEROES.   02440005
           05 PV-IN-FP-BG-FILLER5             PIC X(42) VALUE SPACES.   02450012
                                                                        02460003
                                                                        02470000
      *----------------------------------------------------------       02480000
      * DATE ROUTINE COPY MEMBERS                                       02490000
      *----------------------------------------------------------       02500000
           COPY DPWS500.                                                02510000
                                                                        02520000
      *-----------------------------------------------------------      02530000
      * PROGRAM COUNTERS                                                02540000
      *-----------------------------------------------------------      02550000
       01  PC-PROGRAM-COUNTERS.                                         02560000
           05  PC-FLOOR-PAD-IN-RECS         PIC 9(09) VALUE ZEROS.      02570002
           05  PC-BUS-GROUP-IN-RECS         PIC 9(09) VALUE ZEROS.      02580002
           05  PC-FLR-PAD-BUS-GRP-IN-RECS   PIC 9(09) VALUE ZEROS.      02590002
           05  PC-RECS-WRITTEN-TO-OUT-FILE  PIC 9(09) VALUE ZEROS.      02600002
                                                                        02610000
      *-----------------------------------------------------------      02620000
      * ERROR HANDLING                                                  02630000
      *-----------------------------------------------------------      02640000
       01  PV-MISC-ABEND-FIELDS.                                        02650000
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      02660000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      02670000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      02680000
           05  PV-COMPLETION-CODE           PIC S9(09) BINARY.          02690000
           05  PV-REASON                    PIC S9(09) BINARY.          02700000
           05  PV-REASON-TEXT               PIC X(030) VALUE SPACES.    02710000
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 02720000
           88  ABEND-4003-CTRL-CARD-ERROR        VALUE +4003.           02730000
           88  ABEND-4020-MQ-ERROR               VALUE +4020.           02740000
                                                                        02750000
      *----------------------------------------------------------       02760000
       PROCEDURE DIVISION.                                              02770000
      *----------------------------------------------------------       02780000
       0000-MAINLINE.                                                   02790000
                                                                        02800000
           PERFORM 1000-INITIALIZATION.                                 02810000
                                                                        02820000
           PERFORM 2000-PROCESS-RECORDS.                                02830000
                                                                        02840000
           PERFORM 9000-WRAPUP.                                         02850000
                                                                        02860000
           GOBACK.                                                      02870000
                                                                        02880000
      *----------------------------------------------------------       02890000
      * 1000-INITIALIZATION.                                            02900000
      *     INITIALIZATION AREA -                                       02910000
      *               INITIALIZES PROGRAM COUNTERS                      02920000
      *----------------------------------------------------------       02930000
       1000-INITIALIZATION.                                             02940000
                                                                        02950000
                                                                        02960000
           INITIALIZE PC-PROGRAM-COUNTERS.                              02970000
           SET PS-NOT-END-OF-BUS-GRP        TO TRUE.                    02980004
           SET PS-NOT-END-OF-FLR-PAD        TO TRUE.                    02990004
           SET PS-NOT-END-OF-FLRPAD-BUSGRP  TO TRUE.                    03000005
      *** SET UP TIMESTAMP    INFORMATION FOR HEADER                    03010000
                                                                        03020000
           ACCEPT PV-RUN-TIME FROM TIME.                                03030000
           ACCEPT PV-DATE-IN  FROM DATE YYYYMMDD.                       03040000
           MOVE DATE-IN-CC           TO PV-HEADER-CC.                   03050000
                                                                        03060000
           MOVE DATE-IN-YY           TO PV-HEADER-YY.                   03070000
                                                                        03080000
           MOVE DATE-IN-MM           TO PV-HEADER-MO.                   03090000
                                                                        03100000
           MOVE DATE-IN-DD           TO PV-HEADER-DD.                   03110000
                                                                        03120000
           MOVE R-HR                 TO PV-HEADER-HH.                   03130000
                                                                        03140000
           MOVE R-MIN                TO PV-HEADER-MM.                   03150000
                                                                        03160000
           MOVE R-SEC                TO PV-HEADER-SS.                   03170000
           DISPLAY 'TMST= ' PV-HEADER-TMST ' - ' PV-DATE-IN.            03180000
                                                                        03190000
           OPEN INPUT  FLOOR-PAD-FILE                                   03200004
                       BUSINESS-GROUP-FILE                              03210004
                       FLRPAD-BUSGRP-ASSOC-FILE                         03220004
                OUTPUT FORMATTED-FLRPAD-BUSGRP-FILE.                    03230004
                                                                        03240000
                                                                        03250000
      *----------------------------------------------------------       03260000
      * 2000-PROCESS-RECORDS - READ MESSAGES FROM 3 INPUT FILES.        03270004
      *----------------------------------------------------------       03280000
       2000-PROCESS-RECORDS.                                            03290000
                                                                        03300000
      *** PROCESS FLOOR PAD RECORDS FROM INPUT FILE                     03310004
           DISPLAY 'FLOOR PAD PROCESS'.                                 03320004
                                                                        03330000
           PERFORM 3000-READ-FLOOR-PAD-FILE.                            03340004
                                                                        03350000
           PERFORM UNTIL PS-END-OF-FLR-PAD                              03360005
               IF PC-FLOOR-PAD-IN-RECS = 1                              03370005
                   PERFORM 3100-FRMT-FLOOR-PAD-HDR-REC                  03380005
               END-IF                                                   03390005
               PERFORM 3200-FRMT-FLOOR-PAD-RECS                         03400006
               PERFORM 3000-READ-FLOOR-PAD-FILE                         03410005
           END-PERFORM.                                                 03420000
                                                                        03430000
      *** PROCESS BUSINESS GROUP RECORDS FROM INPUT FILE                03440005
           DISPLAY 'BUSINESS GROUP PROCESS'.                            03450005
                                                                        03460005
           PERFORM 4000-READ-BUSINESS-GROUP-FILE.                       03470005
                                                                        03480005
           PERFORM UNTIL PS-END-OF-BUS-GRP                              03490005
               IF PC-BUS-GROUP-IN-RECS = 1                              03500010
                   PERFORM 4100-FRMT-BUSINESS-GRP-HDR-REC               03510005
               END-IF                                                   03520005
               PERFORM 4200-FRMT-BUSINESS-GROUP-RECS                    03530009
               PERFORM 4000-READ-BUSINESS-GROUP-FILE                    03540009
           END-PERFORM.                                                 03550005
                                                                        03560005
      *** PROCESS FLRPAD BUSGRP ASSOCIATION RECORDS FROM INPUT FILE     03570005
           DISPLAY 'FLOOR PAD / BUSINESS GROUP ASSOCIATION PROCESS'.    03580005
                                                                        03590005
           PERFORM 5000-READ-FLRPAD-BUSGRP-FILE.                        03600009
                                                                        03610005
           PERFORM UNTIL PS-END-OF-FLRPAD-BUSGRP                        03620005
               IF PC-FLR-PAD-BUS-GRP-IN-RECS = 1                        03630009
                   PERFORM 5100-FRMT-FLRPAD-BUSGP-HDR-REC               03640005
               END-IF                                                   03650005
               PERFORM 5200-FRMT-FLRPAD-BUSGRP-RECS                     03660009
               PERFORM 5000-READ-FLRPAD-BUSGRP-FILE                     03670009
           END-PERFORM.                                                 03680005
                                                                        03690005
                                                                        03700004
      *----------------------------------------------------------       03710004
      * 3000-READ-FLOOR-PAD-FILE.                                       03720004
      *----------------------------------------------------------       03730004
       3000-READ-FLOOR-PAD-FILE.                                        03740004
                                                                        03750004
           READ FLOOR-PAD-FILE INTO PV-FLOOR-PAD-IN-REC                 03760005
               AT END                                                   03770004
                   SET PS-END-OF-FLR-PAD TO TRUE.                       03780005
                                                                        03790005
           IF PS-NOT-END-OF-FLR-PAD                                     03800005
             ADD +1 TO PC-FLOOR-PAD-IN-RECS.                            03810005
                                                                        03820004
                                                                        03830006
      *-------------------------------------------------------------    03840006
      * 3100-FRMT-FLOOR-PAD-HDR-REC.                                    03850006
      *     THIS PARAGRAPH WILL FORMAT AND ADD A HEADER RECORD          03860006
      *     FOR THE FLOOR PAD RECORDS.                                  03870006
      *-------------------------------------------------------------    03880006
       3100-FRMT-FLOOR-PAD-HDR-REC.                                     03890006
                                                                        03900006
           MOVE PC-HDR-IND             TO EP217-HDR-INDICATOR.          03910006
           MOVE PC-STR-GRP-ID-1000     TO EP217-STR-GRP-TYP.            03920006
           MOVE ZEROES                 TO EP217-STR-GRP-EXCP-IND.       03930006
           MOVE 1                      TO EP217-STR-GRP-ID-STR-LOC.     03940006
           MOVE ZEROES                 TO EP217-EXCO-STR-NBR.           03950006
           MOVE PV-DATE-IN             TO EP217-FILE-DIST-DATE.         03960006
           SET EP217-FILE-MLS-FLRPAD-BUSGRP TO TRUE.                    03970006
           SET EP217-SFL-FLR-PAD            TO TRUE.                    03980006
           SET EP217-FILE-NAME-FUL          TO TRUE.                    03990006
           MOVE ZEROES                 TO EP217-STORE-NBR.              04000006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-1.           04010006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-2.           04020006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-3.           04030006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-4.           04040006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-5.           04050006
           MOVE PV-HEADER-TMST         TO EP217-CRT-TIMESTAMP.          04060006
           MOVE PC-FILE-APPLY-TYPE-FUL TO EP217-FILE-TYPE-APPLY.        04070006
                                                                        04080006
           PERFORM 7000-WRITE-HEADER-REC.                               04090006
                                                                        04100006
                                                                        04110006
      *-------------------------------------------------------------    04120006
      * 3200-FRMT-FLOOR-PAD-RECS.                                       04130006
      *     THIS PARAGRAPH FORMATS THE DATA RECORD FOR THE FLOOR        04140006
      *     PAD TABLE.                                                  04150006
      *     THE FOLLOWING FIELDS ARE RETRIEVED FROM THE ORACLE          04160006
      *     TABLE, BUT ARE NOT NEEDED AT THE STORES AT THIS TIME:       04170006
      *     LAST-UPDT-TMST                                              04180006
      *     LAST-UPDT-USR-ID                                            04190006
      *-------------------------------------------------------------    04200006
       3200-FRMT-FLOOR-PAD-RECS.                                        04210006
                                                                        04220006
      **-- INITIALIZE VARIABLES TO WRITE DETAIL RECORD                  04230006
                                                                        04240006
           SET OUT-LEN TO +1.                                           04250006
           MOVE 0 TO PV-BYTE-COUNT.                                     04260006
           INITIALIZE FORMATTED-FLRPAD-BUSGRP-RECORD.                   04270006
                                                                        04280006
      **-- FORMAT RECORD ACTION                                         04290006
                                                                        04300006
           MOVE 'A'                  TO PV-RECORD-ACTION.               04310006
           PERFORM 6100-MOVE-DELIMITER.                                 04320006
           PERFORM 6000-MOVE-RECORD-ACTION.                             04330006
           PERFORM 6100-MOVE-DELIMITER.                                 04340006
                                                                        04350006
      **-- FORMAT FLR-PAD-ID    NUMERIC(10)                             04360006
           INITIALIZE PV-VARIABLE-OUTPUT-FIELD.                         04370006
           EVALUATE TRUE                                                04380006
               WHEN PV-IN-FLR-PAD-ID = 0                                04390006
                  PERFORM 6500-MOVE-ZERO                                04400006
               WHEN OTHER                                               04410006
                  INITIALIZE PV-TALLY                                   04420006
                  INSPECT PV-IN-FLR-PAD-ID     TALLYING PV-TALLY        04430006
                      FOR LEADING ZERO                                  04440006
                  SET PV-BIT-IDX TO PV-TALLY                            04450006
                  SET PV-BIT-IDX UP BY +1                               04460006
                  MOVE PV-IN-FLR-PAD-ID   TO PV-VARIABLE-OUTPUT-FIELD   04470006
                  PERFORM 6300-MOVE-NUMERIC-DATA VARYING PV-EOF-IDX     04480006
                     FROM PV-BIT-IDX BY +1                              04490006
                     UNTIL PV-EOF-IDX > 10                              04500006
           END-EVALUATE.                                                04510006
                                                                        04520006
           PERFORM 6100-MOVE-DELIMITER.                                 04530006
                                                                        04540006
      **-- FORMAT FLR-PAD-CDE   VARCHAR(2)                              04550006
                                                                        04560006
           EVALUATE TRUE                                                04570007
             WHEN PV-IN-FLR-PAD-CDE = SPACE                             04580007
               PERFORM 6200-MOVE-SPACE                                  04590007
             WHEN OTHER                                                 04600007
               MOVE PV-IN-FLR-PAD-CDE TO PV-VARIABLE-CHAR-FIELD         04610007
               PERFORM VARYING PV-CHR-EOF-IDX                           04620007
                  FROM 2 BY -1                                          04630007
                  UNTIL PV-BIT-CHR-VARIABLE(PV-CHR-EOF-IDX)             04640007
                                                  NOT = SPACE           04650007
                      MOVE ' '  TO PV-SPACE                             04660007
               END-PERFORM                                              04670007
               PERFORM 6400-MOVE-CHAR-DATA VARYING PV-CHR-BIT-IDX       04680007
               FROM 1 BY 1                                              04690007
                UNTIL PV-CHR-BIT-IDX > PV-CHR-EOF-IDX                   04700007
           END-EVALUATE.                                                04710007
                                                                        04720007
           PERFORM 6100-MOVE-DELIMITER.                                 04730007
                                                                        04740006
      **-- FORMAT FLR-PAD-DESC  VARCHAR(40)                             04750006
                                                                        04760007
           EVALUATE TRUE                                                04770007
             WHEN PV-IN-FLR-PAD-DESC = SPACE                            04780007
               PERFORM 6200-MOVE-SPACE                                  04790007
             WHEN OTHER                                                 04800007
               MOVE PV-IN-FLR-PAD-DESC TO PV-VARIABLE-CHAR-FIELD        04810009
               PERFORM VARYING PV-CHR-EOF-IDX                           04820007
                  FROM 40 BY -1                                         04830007
                  UNTIL PV-BIT-CHR-VARIABLE(PV-CHR-EOF-IDX)             04840007
                                                  NOT = SPACE           04850007
                      MOVE ' '  TO PV-SPACE                             04860007
               END-PERFORM                                              04870007
               PERFORM 6400-MOVE-CHAR-DATA VARYING PV-CHR-BIT-IDX       04880007
               FROM 1 BY 1                                              04890007
                UNTIL PV-CHR-BIT-IDX > PV-CHR-EOF-IDX                   04900007
           END-EVALUATE.                                                04910007
                                                                        04920007
           PERFORM 6100-MOVE-DELIMITER.                                 04930007
                                                                        04940007
      **-- FORMAT FLR-PAD-AREA-TYP-CDE   CHAR(1)                        04950006
           MOVE PV-IN-FLR-PAD-AREA-TYP-CDE                              04960007
             TO FRMTED-FLRPAD-BUSGRP-REC-FIELD(OUT-LEN).                04970007
           SET OUT-LEN  UP BY +1.                                       04980007
           ADD 1        TO PV-BYTE-COUNT.                               04990007
                                                                        05000007
           PERFORM 6100-MOVE-DELIMITER.                                 05010007
                                                                        05020007
      **-- FORMAT FLR-PAD-EFF-BEG-DTE    DATE                           05030006
                                                                        05040007
           EVALUATE TRUE                                                05050007
             WHEN PV-IN-FLR-PAD-EFF-BEG-DTE = SPACE                     05060007
               PERFORM 6200-MOVE-SPACE                                  05070007
             WHEN OTHER                                                 05080007
               MOVE PV-IN-FLR-PAD-EFF-BEG-DTE TO PV-VARIABLE-CHAR-FIELD 05090007
               PERFORM VARYING PV-CHR-EOF-IDX                           05100007
                   FROM 10 BY -1                                        05110007
                   UNTIL PV-BIT-CHR-VARIABLE(PV-CHR-EOF-IDX)            05120007
                                                  NOT = SPACE           05130007
                      MOVE ' '  TO PV-SPACE                             05140007
               END-PERFORM                                              05150007
                                                                        05160007
               PERFORM 6400-MOVE-CHAR-DATA VARYING PV-CHR-BIT-IDX       05170007
                   FROM 1 BY 1                                          05180007
                   UNTIL PV-CHR-BIT-IDX > PV-CHR-EOF-IDX                05190007
           END-EVALUATE.                                                05200007
                                                                        05210007
           PERFORM 6100-MOVE-DELIMITER.                                 05220007
                                                                        05230007
      **-- FORMAT FLR-PAD-EFF-END-DTE    DATE                           05240006
                                                                        05250007
           EVALUATE TRUE                                                05260007
             WHEN PV-IN-FLR-PAD-EFF-END-DTE = SPACE                     05270007
               PERFORM 6200-MOVE-SPACE                                  05280007
             WHEN OTHER                                                 05290007
               MOVE PV-IN-FLR-PAD-EFF-END-DTE TO PV-VARIABLE-CHAR-FIELD 05300007
               PERFORM VARYING PV-CHR-EOF-IDX                           05310007
                   FROM 10 BY -1                                        05320007
                   UNTIL PV-BIT-CHR-VARIABLE(PV-CHR-EOF-IDX)            05330007
                                                  NOT = SPACE           05340007
                      MOVE ' '  TO PV-SPACE                             05350007
               END-PERFORM                                              05360007
                                                                        05370007
               PERFORM 6400-MOVE-CHAR-DATA VARYING PV-CHR-BIT-IDX       05380007
                   FROM 1 BY 1                                          05390007
                   UNTIL PV-CHR-BIT-IDX > PV-CHR-EOF-IDX                05400007
           END-EVALUATE.                                                05410007
                                                                        05420007
           PERFORM 6100-MOVE-DELIMITER.                                 05430007
                                                                        05440007
           PERFORM 7100-WRITE-FRMTD-FLRPAD-BUSGRP.                      05450006
                                                                        05460006
                                                                        05470006
                                                                        05480006
      *----------------------------------------------------------       05490006
      * 4000-READ-BUSINESS-GROUP-FILE.                                  05500006
      *----------------------------------------------------------       05510006
       4000-READ-BUSINESS-GROUP-FILE.                                   05520009
                                                                        05530006
           READ BUSINESS-GROUP-FILE INTO PV-BUSINESS-GROUP-IN-REC       05540006
               AT END                                                   05550006
                   SET PS-END-OF-BUS-GRP TO TRUE.                       05560006
                                                                        05570006
           IF PS-NOT-END-OF-BUS-GRP                                     05580006
             ADD +1 TO PC-BUS-GROUP-IN-RECS.                            05590006
                                                                        05600006
                                                                        05610006
      *-------------------------------------------------------------    05620006
      * 4100-FRMT-BUSINESS-GRP-HDR-REC.                                 05630006
      *     THIS PARAGRAPH WILL FORMAT AND ADD A HEADER RECORD          05640006
      *     FOR THE BUSINESS GROUP RECORDS.                             05650006
      *-------------------------------------------------------------    05660006
       4100-FRMT-BUSINESS-GRP-HDR-REC.                                  05670006
                                                                        05680006
           MOVE PC-HDR-IND             TO EP217-HDR-INDICATOR.          05690006
           MOVE PC-STR-GRP-ID-1000     TO EP217-STR-GRP-TYP.            05700006
           MOVE ZEROES                 TO EP217-STR-GRP-EXCP-IND.       05710006
           MOVE 1                      TO EP217-STR-GRP-ID-STR-LOC.     05720006
           MOVE ZEROES                 TO EP217-EXCO-STR-NBR.           05730006
           MOVE PV-DATE-IN             TO EP217-FILE-DIST-DATE.         05740006
           SET EP217-FILE-MLS-FLRPAD-BUSGRP TO TRUE.                    05750006
           SET EP217-SFL-BUS-GRP            TO TRUE.                    05760006
           SET EP217-FILE-NAME-FUL          TO TRUE.                    05770006
           MOVE ZEROES                 TO EP217-STORE-NBR.              05780006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-1.           05790006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-2.           05800006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-3.           05810006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-4.           05820006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-5.           05830006
           MOVE PV-HEADER-TMST         TO EP217-CRT-TIMESTAMP.          05840006
           MOVE PC-FILE-APPLY-TYPE-FUL TO EP217-FILE-TYPE-APPLY.        05850006
                                                                        05860006
           PERFORM 7000-WRITE-HEADER-REC.                               05870006
                                                                        05880006
      *-------------------------------------------------------------    05890006
      * 4200-FRMT-BUSINESS-GROUP-RECS.                                  05900009
      *     THIS PARAGRAPH FORMATS THE DATA RECORD FOR THE BUSINESS     05910006
      *     GROUP TABLE.  IT WAS COPIED FROM XSKUT500 WHICH SENDS       05920006
      *     THE DB2 VERSION OF THE TABLE TO THE STORES ALONG WITH       05930006
      *     THE SIGN DATA.                                              05940006
      *     THE FOLLOWING FIELDS ARE RETRIEVED FROM THE ORACLE          05950006
      *     TABLE, BUT ARE NOT NEEDED AT THE STORES AT THIS TIME:       05960006
      *     BUS-AREA-ID                                                 05970006
      *     BUS-GP-SHRT-DESC                                            05980006
      *-------------------------------------------------------------    05990006
       4200-FRMT-BUSINESS-GROUP-RECS.                                   06000009
                                                                        06010006
      **-- INITIALIZE VARIABLES TO WRITE DETAIL RECORD                  06020006
                                                                        06030006
           SET OUT-LEN TO +1.                                           06040006
           MOVE 0 TO PV-BYTE-COUNT.                                     06050006
           INITIALIZE FORMATTED-FLRPAD-BUSGRP-RECORD.                   06060006
                                                                        06070006
      **-- FORMAT RECORD ACTION                                         06080006
                                                                        06090006
           MOVE 'A'                  TO PV-RECORD-ACTION.               06100006
           PERFORM 6100-MOVE-DELIMITER.                                 06110006
           PERFORM 6000-MOVE-RECORD-ACTION.                             06120006
           PERFORM 6100-MOVE-DELIMITER.                                 06130006
                                                                        06140006
      **-- FORMAT BUS-GP-NBR - NUMERIC(3)                               06150006
                                                                        06160006
           INITIALIZE PV-VARIABLE-OUTPUT-FIELD.                         06170006
           EVALUATE TRUE                                                06180006
               WHEN PV-IN-BUS-GRP-NBR = 0                               06190009
                  PERFORM 6500-MOVE-ZERO                                06200006
               WHEN OTHER                                               06210006
                  INITIALIZE PV-TALLY                                   06220006
                  INSPECT PV-IN-BUS-GRP-NBR     TALLYING PV-TALLY       06230009
                      FOR LEADING ZERO                                  06240006
                  SET PV-BIT-IDX TO PV-TALLY                            06250006
                  SET PV-BIT-IDX UP BY +1                               06260006
                  MOVE PV-IN-BUS-GRP-NBR   TO PV-VARIABLE-OUTPUT-FIELD  06270009
                  PERFORM 6300-MOVE-NUMERIC-DATA VARYING PV-EOF-IDX     06280006
                     FROM PV-BIT-IDX BY +1                              06290006
                     UNTIL PV-EOF-IDX > 3                               06300006
           END-EVALUATE.                                                06310006
                                                                        06320006
           PERFORM 6100-MOVE-DELIMITER.                                 06330006
                                                                        06340006
      **-- FORMAT BUS-GP-DESC - VARCHAR(25)                             06350006
           EVALUATE TRUE                                                06360006
             WHEN PV-IN-BUS-GRP-DESC = SPACE                            06370009
               PERFORM 6200-MOVE-SPACE                                  06380006
             WHEN OTHER                                                 06390006
               MOVE PV-IN-BUS-GRP-DESC TO PV-VARIABLE-CHAR-FIELD        06400009
               PERFORM VARYING PV-CHR-EOF-IDX                           06410006
                  FROM 25 BY -1                                         06420006
                  UNTIL PV-BIT-CHR-VARIABLE(PV-CHR-EOF-IDX)             06430006
                                                  NOT = SPACE           06440006
                      MOVE ' '  TO PV-SPACE                             06450006
               END-PERFORM                                              06460006
               PERFORM 6400-MOVE-CHAR-DATA VARYING PV-CHR-BIT-IDX       06470006
               FROM 1 BY 1                                              06480006
                UNTIL PV-CHR-BIT-IDX > PV-CHR-EOF-IDX                   06490006
           END-EVALUATE.                                                06500006
                                                                        06510006
           PERFORM 6100-MOVE-DELIMITER.                                 06520006
                                                                        06530006
           PERFORM 7100-WRITE-FRMTD-FLRPAD-BUSGRP.                      06540006
                                                                        06550006
                                                                        06560006
      *----------------------------------------------------------       06570006
      * 5000-READ-FLRPAD-BUSGRP-FILE.                                   06580009
      *----------------------------------------------------------       06590006
       5000-READ-FLRPAD-BUSGRP-FILE.                                    06600009
                                                                        06610006
           READ FLRPAD-BUSGRP-ASSOC-FILE                                06620009
               INTO PV-FLR-PAD-BUS-GRP-IN-REC                           06630006
               AT END                                                   06640006
                   SET PS-END-OF-FLRPAD-BUSGRP TO TRUE.                 06650006
                                                                        06660006
           IF PS-NOT-END-OF-FLRPAD-BUSGRP                               06670006
             ADD +1 TO PC-FLR-PAD-BUS-GRP-IN-RECS.                      06680006
                                                                        06690006
                                                                        06700006
      *-------------------------------------------------------------    06710006
      * 5100-FRMT-FLRPAD-BUSGP-HDR-REC.                                 06720006
      *     THIS PARAGRAPH WILL FORMAT AND ADD A HEADER RECORD          06730006
      *     FOR THE FLOOR PAD BUSINESS GROUP ASSOCIATION RECORDS.       06740006
      *-------------------------------------------------------------    06750006
       5100-FRMT-FLRPAD-BUSGP-HDR-REC.                                  06760006
                                                                        06770006
           MOVE PC-HDR-IND             TO EP217-HDR-INDICATOR.          06780006
           MOVE PC-STR-GRP-ID-1000     TO EP217-STR-GRP-TYP.            06790006
           MOVE ZEROES                 TO EP217-STR-GRP-EXCP-IND.       06800006
           MOVE 1                      TO EP217-STR-GRP-ID-STR-LOC.     06810006
           MOVE ZEROES                 TO EP217-EXCO-STR-NBR.           06820006
           MOVE PV-DATE-IN             TO EP217-FILE-DIST-DATE.         06830006
           SET EP217-FILE-MLS-FLRPAD-BUSGRP TO TRUE.                    06840006
           SET EP217-SFL-FLR-PAD-BUS-GRP    TO TRUE.                    06850006
           SET EP217-FILE-NAME-FUL          TO TRUE.                    06860006
           MOVE ZEROES                 TO EP217-STORE-NBR.              06870006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-1.           06880006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-2.           06890006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-3.           06900006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-4.           06910006
           MOVE PC-UNDER-SCORE         TO EP217-UNDERSCORE-5.           06920006
           MOVE PV-HEADER-TMST         TO EP217-CRT-TIMESTAMP.          06930006
           MOVE PC-FILE-APPLY-TYPE-FUL TO EP217-FILE-TYPE-APPLY.        06940006
                                                                        06950006
           PERFORM 7000-WRITE-HEADER-REC.                               06960006
                                                                        06970006
                                                                        06980006
      *-------------------------------------------------------------    06990007
      * 5200-FRMT-FLRPAD-BUSGRP-RECS.                                   07000009
      *     THIS PARAGRAPH FORMATS THE DATA RECORD FOR THE FLOOR        07010007
      *     PAD / BUSINESS GROUP ASSOCIATION TABLE.                     07020007
      *     ALL FIELDS ARE RETRIEVED FROM THE ORACLE TABLE AND          07030007
      *     SENT TO THE STORES.                                         07040007
      *-------------------------------------------------------------    07050007
       5200-FRMT-FLRPAD-BUSGRP-RECS.                                    07060009
                                                                        07070007
      **-- INITIALIZE VARIABLES TO WRITE DETAIL RECORD                  07080007
                                                                        07090007
           SET OUT-LEN TO +1.                                           07100007
           MOVE 0 TO PV-BYTE-COUNT.                                     07110007
           INITIALIZE FORMATTED-FLRPAD-BUSGRP-RECORD.                   07120007
                                                                        07130007
      **-- FORMAT RECORD ACTION                                         07140007
                                                                        07150007
           MOVE 'A'                  TO PV-RECORD-ACTION.               07160007
           PERFORM 6100-MOVE-DELIMITER.                                 07170007
           PERFORM 6000-MOVE-RECORD-ACTION.                             07180007
           PERFORM 6100-MOVE-DELIMITER.                                 07190007
                                                                        07200007
      **-- FORMAT BUS-GP-NBR - NUMERIC(3)                               07210007
                                                                        07220007
           INITIALIZE PV-VARIABLE-OUTPUT-FIELD.                         07230007
           EVALUATE TRUE                                                07240007
               WHEN PV-IN-FP-BG-BUS-GRP-NBR = 0                         07250007
                  PERFORM 6500-MOVE-ZERO                                07260007
               WHEN OTHER                                               07270007
                  INITIALIZE PV-TALLY                                   07280007
                  INSPECT PV-IN-FP-BG-BUS-GRP-NBR  TALLYING PV-TALLY    07290007
                      FOR LEADING ZERO                                  07300007
                  SET PV-BIT-IDX TO PV-TALLY                            07310007
                  SET PV-BIT-IDX UP BY +1                               07320007
                  MOVE PV-IN-FP-BG-BUS-GRP-NBR                          07330007
                                          TO PV-VARIABLE-OUTPUT-FIELD   07340007
                  PERFORM 6300-MOVE-NUMERIC-DATA VARYING PV-EOF-IDX     07350007
                     FROM PV-BIT-IDX BY +1                              07360007
                     UNTIL PV-EOF-IDX > 3                               07370007
           END-EVALUATE.                                                07380007
                                                                        07390007
           PERFORM 6100-MOVE-DELIMITER.                                 07400007
                                                                        07410007
      **-- FORMAT FLR-PAD-BUS-GRP-EFF-BEG-DTE    DATE                   07420007
                                                                        07430007
           EVALUATE TRUE                                                07440007
             WHEN PV-IN-FP-BG-EFF-BEG-DTE = SPACE                       07450007
               PERFORM 6200-MOVE-SPACE                                  07460007
             WHEN OTHER                                                 07470007
               MOVE PV-IN-FP-BG-EFF-BEG-DTE  TO PV-VARIABLE-CHAR-FIELD  07480007
               PERFORM VARYING PV-CHR-EOF-IDX                           07490007
                   FROM 10 BY -1                                        07500007
                   UNTIL PV-BIT-CHR-VARIABLE(PV-CHR-EOF-IDX)            07510007
                                                  NOT = SPACE           07520007
                      MOVE ' '  TO PV-SPACE                             07530007
               END-PERFORM                                              07540007
                                                                        07550007
               PERFORM 6400-MOVE-CHAR-DATA VARYING PV-CHR-BIT-IDX       07560007
                   FROM 1 BY 1                                          07570007
                   UNTIL PV-CHR-BIT-IDX > PV-CHR-EOF-IDX                07580007
           END-EVALUATE.                                                07590007
                                                                        07600007
           PERFORM 6100-MOVE-DELIMITER.                                 07610007
                                                                        07620007
      **-- FORMAT FLR-PAD-BUS-GRP-EFF-END-DTE    DATE                   07630007
                                                                        07640007
           EVALUATE TRUE                                                07650007
             WHEN PV-IN-FP-BG-EFF-END-DTE  = SPACE                      07660012
               PERFORM 6200-MOVE-SPACE                                  07670007
             WHEN OTHER                                                 07680007
               MOVE PV-IN-FP-BG-EFF-END-DTE  TO PV-VARIABLE-CHAR-FIELD  07690012
               PERFORM VARYING PV-CHR-EOF-IDX                           07700007
                   FROM 10 BY -1                                        07710007
                   UNTIL PV-BIT-CHR-VARIABLE(PV-CHR-EOF-IDX)            07720007
                                                  NOT = SPACE           07730007
                      MOVE ' '  TO PV-SPACE                             07740007
               END-PERFORM                                              07750007
                                                                        07760007
               PERFORM 6400-MOVE-CHAR-DATA VARYING PV-CHR-BIT-IDX       07770007
                   FROM 1 BY 1                                          07780007
                   UNTIL PV-CHR-BIT-IDX > PV-CHR-EOF-IDX                07790007
           END-EVALUATE.                                                07800007
                                                                        07810007
           PERFORM 6100-MOVE-DELIMITER.                                 07820007
                                                                        07830007
      **-- FORMAT FLR-PAD-ID    NUMERIC(10)                             07840007
                                                                        07850007
           INITIALIZE PV-VARIABLE-OUTPUT-FIELD.                         07860007
           EVALUATE TRUE                                                07870007
               WHEN PV-IN-FP-BG-FLR-PAD-ID = 0                          07880007
                  PERFORM 6500-MOVE-ZERO                                07890007
               WHEN OTHER                                               07900007
                  INITIALIZE PV-TALLY                                   07910007
                  INSPECT PV-IN-FP-BG-FLR-PAD-ID TALLYING PV-TALLY      07920007
                      FOR LEADING ZERO                                  07930007
                  SET PV-BIT-IDX TO PV-TALLY                            07940007
                  SET PV-BIT-IDX UP BY +1                               07950007
                  MOVE PV-IN-FP-BG-FLR-PAD-ID                           07960012
                                          TO PV-VARIABLE-OUTPUT-FIELD   07970012
                  PERFORM 6300-MOVE-NUMERIC-DATA VARYING PV-EOF-IDX     07980007
                     FROM PV-BIT-IDX BY +1                              07990007
                     UNTIL PV-EOF-IDX > 10                              08000007
           END-EVALUATE.                                                08010007
                                                                        08020007
           PERFORM 6100-MOVE-DELIMITER.                                 08030007
                                                                        08040007
           PERFORM 7100-WRITE-FRMTD-FLRPAD-BUSGRP.                      08050007
                                                                        08060007
                                                                        08070006
      *-----------------------------------------------------------------08080006
      * 6000-MOVE-RECORD-ACTION                                         08090006
      *  THIS PARAGRAPH WILL MOVE ACTION CDE TO THE OUTPUT FILE VARIABLE08100006
      *----------------------------------------------------------------*08110006
       6000-MOVE-RECORD-ACTION.                                         08120006
                                                                        08130006
           MOVE PV-RECORD-ACTION                                        08140006
             TO FRMTED-FLRPAD-BUSGRP-REC-FIELD(OUT-LEN).                08150006
           SET OUT-LEN  UP BY +1.                                       08160007
           ADD 1        TO PV-BYTE-COUNT.                               08170007
                                                                        08180006
      *-----------------------------------------------------------------08190000
      * 6100-MOVE-DELIMITER                                             08200006
      *  THIS PARAGRAPH WILL MOVE '|' TO THE OUTPUT FILE VARIABLE       08210000
      *----------------------------------------------------------------*08220000
       6100-MOVE-DELIMITER.                                             08230006
                                                                        08240000
           MOVE PC-CSV-DELIMITER                                        08250000
             TO FRMTED-FLRPAD-BUSGRP-REC-FIELD(OUT-LEN).                08260003
           SET OUT-LEN                                                  08270003
             UP BY +1.                                                  08280000
           ADD 1                                                        08290000
             TO PV-BYTE-COUNT.                                          08300000
                                                                        08310000
                                                                        08320000
      *-----------------------------------------------------------------08330009
      * 6200-MOVE-SPACE                                                 08340009
      *  THIS PARAGRAPH WILL MOVE ' ' TO THE OUTPUT FILE VARIABLE       08350009
      *----------------------------------------------------------------*08360009
       6200-MOVE-SPACE.                                                 08370006
                                                                        08380006
           MOVE PC-ONE-SPACE                                            08390006
             TO FRMTED-FLRPAD-BUSGRP-REC-FIELD(OUT-LEN).                08400006
           SET OUT-LEN                                                  08410006
             UP BY +1.                                                  08420006
           ADD 1                                                        08430006
             TO PV-BYTE-COUNT.                                          08440006
                                                                        08450000
                                                                        08460000
      *-----------------------------------------------------------------08470000
      * 6300-MOVE-NUMERIC-DATA                                          08480006
      *  THIS PARAGRAPH WILL NUMERIC DATA TO THE OUTPUT FILE VARIABLE   08490000
      *----------------------------------------------------------------*08500000
       6300-MOVE-NUMERIC-DATA.                                          08510006
                                                                        08520000
           MOVE PV-BIT-VARIABLE(PV-EOF-IDX)                             08530000
             TO FRMTED-FLRPAD-BUSGRP-REC-FIELD(OUT-LEN).                08540003
           SET OUT-LEN                                                  08550003
             UP BY +1.                                                  08560000
           ADD 1                                                        08570000
             TO PV-BYTE-COUNT.                                          08580000
                                                                        08590000
                                                                        08600000
      *-----------------------------------------------------------------08610000
      * 6400-MOVE-CHAR-DATA                                             08620006
      *  THIS PARAGRAPH WILL MOVE ' ' TO THE OUTPUT FILE VARIABLE       08630000
      *----------------------------------------------------------------*08640000
       6400-MOVE-CHAR-DATA.                                             08650006
                                                                        08660000
           MOVE PV-BIT-CHR-VARIABLE(PV-CHR-BIT-IDX)                     08670000
             TO FRMTED-FLRPAD-BUSGRP-REC-FIELD(OUT-LEN).                08680003
           SET OUT-LEN                                                  08690003
             UP BY +1.                                                  08700000
           ADD 1                                                        08710000
             TO PV-BYTE-COUNT.                                          08720000
                                                                        08730000
      *-----------------------------------------------------------------08740000
      * 6500-MOVE-ZERO                                                  08750006
      *  THIS PARAGRAPH WILL MOVE '0' TO THE OUTPUT FILE VARIABLE       08760000
      *----------------------------------------------------------------*08770000
       6500-MOVE-ZERO.                                                  08780006
                                                                        08790000
           MOVE PC-ONE-ZERO                                             08800000
             TO FRMTED-FLRPAD-BUSGRP-REC-FIELD(OUT-LEN).                08810003
           SET OUT-LEN                                                  08820003
             UP BY +1.                                                  08830000
           ADD 1                                                        08840000
             TO PV-BYTE-COUNT.                                          08850000
                                                                        08860006
      *-------------------------------------------------------------    08870006
      * 7000-WRITE-HEADER-REC.                                          08880006
      *     FORMAT AND WRITE THE HEADER RECORD.                         08890006
      *-------------------------------------------------------------    08900006
       7000-WRITE-HEADER-REC.                                           08910006
                                                                        08920006
           PERFORM 7050-INCR-HEADER-TIMESTAMP.                          08930006
                                                                        08940006
           MOVE 64                  TO PV-BYTE-COUNT.                   08950006
           MOVE EP217-HEADER-RECORD TO FORMATTED-FLRPAD-BUSGRP-RECORD.  08960006
           PERFORM 7100-WRITE-FRMTD-FLRPAD-BUSGRP.                      08970006
      *    WRITE FORMATTED-FLRPAD-BUSGRP-RECORD                         08980009
      *      FROM EP217-HEADER-RECORD.                                  08990009
                                                                        09000006
                                                                        09010006
      *-------------------------------------------------------------    09020006
      * 7050-INCR-HEADER-TIMESTAMP.                                     09030006
      *     THIS PARAGRAPH WILL INCREMENT THE TIMESTAMP BY 1 SECOND     09040006
      *     THIS WOULD ENSURE THE HEADER RECORDS TO BE 1 SECOND APART   09050006
      *     WHICH IS MID-TIER REQUIREMENT.                              09060006
      *-------------------------------------------------------------    09070006
       7050-INCR-HEADER-TIMESTAMP.                                      09080006
                                                                        09090006
           ADD 1                                                        09100006
             TO PV-HEADER-SS.                                           09110006
           IF PV-HEADER-SS GREATER 59                                   09120006
               MOVE ZERO                                                09130006
                 TO PV-HEADER-SS                                        09140006
               ADD 1                                                    09150006
                 TO PV-HEADER-MM                                        09160006
           END-IF.                                                      09170006
                                                                        09180006
           IF PV-HEADER-MM GREATER 59                                   09190006
               MOVE ZERO                                                09200006
                 TO PV-HEADER-MM                                        09210006
               ADD 1                                                    09220006
                 TO PV-HEADER-HH                                        09230006
           END-IF.                                                      09240006
                                                                        09250006
           IF PV-HEADER-HH GREATER 23                                   09260006
               MOVE ZERO                                                09270006
                 TO PV-HEADER-HH                                        09280006
               ADD 1                                                    09290006
                 TO PV-HEADER-DD                                        09300006
           END-IF.                                                      09310006
                                                                        09320006
           IF PV-HEADER-MO = 01                                         09330006
             OR PV-HEADER-MO = 03                                       09340006
               OR PV-HEADER-MO = 05                                     09350006
                 OR PV-HEADER-MO = 07                                   09360006
                   OR PV-HEADER-MO = 08                                 09370006
                     OR PV-HEADER-MO = 10                               09380006
                       OR PV-HEADER-MO = 12                             09390006
               IF PV-HEADER-DD GREATER 31                               09400006
                   MOVE 1                                               09410006
                     TO PV-HEADER-DD                                    09420006
                   ADD 1                                                09430006
                     TO PV-HEADER-MO                                    09440006
               END-IF                                                   09450006
           ELSE                                                         09460006
               IF PV-HEADER-MO = 04                                     09470006
                 OR PV-HEADER-MO = 06                                   09480006
                   OR PV-HEADER-MO = 09                                 09490006
                     OR PV-HEADER-MO = 11                               09500006
                   IF PV-HEADER-DD GREATER 30                           09510006
                       MOVE 1                                           09520006
                         TO PV-HEADER-DD                                09530006
                       ADD 1                                            09540006
                         TO PV-HEADER-MO                                09550006
                   END-IF                                               09560006
               ELSE                                                     09570006
                   IF PV-HEADER-MO = 02                                 09580006
                       DIVIDE PV-HEADER-YYYY BY 100                     09590006
                         GIVING PV-QUOTIENT                             09600006
                           REMAINDER PV-RMDR-100                        09610006
                       DIVIDE PV-HEADER-YYYY BY 4                       09620006
                         GIVING PV-QUOTIENT                             09630006
                           REMAINDER PV-RMDR-4                          09640006
                       IF PV-RMDR-100 = 0                               09650006
                           IF PV-HEADER-DD GREATER 28                   09660006
                               MOVE 1                                   09670006
                                 TO PV-HEADER-DD                        09680006
                               ADD 1                                    09690006
                                 TO PV-HEADER-MO                        09700006
                           END-IF                                       09710006
                       ELSE                                             09720006
                           IF PV-RMDR-4 = 0                             09730006
                               IF PV-HEADER-DD GREATER 29               09740006
                                   MOVE 1                               09750006
                                     TO PV-HEADER-DD                    09760006
                                   ADD 1                                09770006
                                     TO PV-HEADER-MO                    09780006
                               END-IF                                   09790006
                           ELSE                                         09800006
                               IF PV-HEADER-DD GREATER 28               09810006
                                   MOVE 1                               09820006
                                     TO PV-HEADER-DD                    09830006
                                   ADD 1                                09840006
                                     TO PV-HEADER-MO                    09850006
                               END-IF                                   09860006
                           END-IF                                       09870006
                       END-IF                                           09880006
                   END-IF                                               09890006
               END-IF                                                   09900006
           END-IF.                                                      09910006
                                                                        09920006
           IF PV-HEADER-MO GREATER 12                                   09930006
               MOVE 1                                                   09940006
                 TO PV-HEADER-MO                                        09950006
               ADD 1                                                    09960006
                 TO PV-HEADER-YYYY                                      09970006
           END-IF.                                                      09980006
                                                                        09990006
                                                                        10000006
      *----------------------------------------------------------       10010006
      * 7100-WRITE-FRMTD-FLRPAD-BUSGRP.                                 10020006
      *----------------------------------------------------------       10030006
       7100-WRITE-FRMTD-FLRPAD-BUSGRP.                                  10040006
                                                                        10050006
           WRITE FORMATTED-FLRPAD-BUSGRP-RECORD.                        10060006
                                                                        10070006
           ADD +1 TO PC-RECS-WRITTEN-TO-OUT-FILE.                       10080006
                                                                        10090006
                                                                        10100006
                                                                        10110000
      *----------------------------------------------------------       10120000
      * 9000-WRAPUP.                                                    10130000
      *     END OF PROGRAM ROUTINE.                                     10140000
      *     CLOSES ALL FILES, AND                                       10150000
      *     DISPLAYS ALL DATA.                                          10160000
      *----------------------------------------------------------       10170000
       9000-WRAPUP.                                                     10180000
                                                                        10190000
           CLOSE FLOOR-PAD-FILE                                         10200004
                 BUSINESS-GROUP-FILE                                    10210004
                 FLRPAD-BUSGRP-ASSOC-FILE                               10220004
                 FORMATTED-FLRPAD-BUSGRP-FILE.                          10230004
                                                                        10240000
           DISPLAY ' '.                                                 10250000
           DISPLAY '*********************************************'.     10260000
           DISPLAY 'PROGRAM NAME: ' PC-CURRENT-PROGRAM-NAME.            10270000
           DISPLAY '*********************************************'.     10280000
           DISPLAY 'FLOOR PLAN RECORDS READ:                 '          10290012
                           PC-FLOOR-PAD-IN-RECS.                        10300002
           DISPLAY '*********************************************'.     10310000
           DISPLAY 'BUSINESS GROUP RECORDS READ:             '          10320012
                           PC-BUS-GROUP-IN-RECS.                        10330002
           DISPLAY '*********************************************'.     10340012
           DISPLAY 'FLOOR PAD / BUSINESS GROUP RECORDS READ: '          10350012
                           PC-FLR-PAD-BUS-GRP-IN-RECS.                  10360012
           DISPLAY '*********************************************'.     10370000
           DISPLAY 'RECORDS WRITTEN TO FORMATTED FILE:       '          10380012
                           PC-RECS-WRITTEN-TO-OUT-FILE.                 10390002
           DISPLAY '*********************************************'.     10400000
