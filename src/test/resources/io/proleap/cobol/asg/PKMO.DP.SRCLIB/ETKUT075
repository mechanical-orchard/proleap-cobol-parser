       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.         ETKUT075.                                            
       AUTHOR.             SUE PARI.                                            
       DATE-WRITTEN.       11/18/98.                                            
       DATE-COMPILED.                                                           
                                                                                
************************************************************************        
******* ANY NEW OUTBOUND DOCUMENTS WILL NEED CORRESPONDING      ********        
******* CHANGES IN 1400-GS-CONTROL-NBR.                         ********        
************************************************************************        
************************************************************************        
******* THIS PROGRAM WILL UPDATE THE DB2 TABLES WITH THE LAST   ********        
******* USED CONTROL NUMBERS THAT WERE PROCESSED IN UNIX.       ********        
************************************************************************        
JF0502*                                                                         
JF0502*   DATE 05/02/2002                                                       
JF0502*     ADDED LOGIC FOR 816 PROCESSING.                                     
JF0502*   MADE BY: JON FIEDLER             WR/IR #: 22778                       
      ******************************************************************ETKUT004
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 120 CHARACTERS                                       
           DATA RECORD IS INPUT-RECORD.                                         
                                                                                
       01  INPUT-RECORD                    PIC X(120).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INPUT-RECORD.                                                     
           05  FILLER                      PIC X(48).                           
           05  IN-ISA-CNTL-NBR             PIC 9(8).                            
           05  FILLER                      PIC X(3).                            
           05  IN-GS-CNTL-NBR              PIC 9(8).                            
           05  FILLER                      PIC X(2).                            
           05  IN-ENT-ID                   PIC X(10).                           
           05  FILLER                      PIC X(1).                            
           05  IN-TRAN-TYPE                PIC X(3).                            
           05  FILLER                      PIC X(2).                            
           05  IN-PARTNER-ID               PIC X(15).                           
           05  FILLER                      PIC X(2).                            
           05  IN-PARTNER-ID-QUAL          PIC X(2).                            
           05  FILLER                      PIC X(16).                           
                                                                                
       01  WS-PROGRAM-VARIABLES.                                                
                                                                                
           05  WS-ENT-ID                   PIC X(10).                           
           05  FILLER REDEFINES WS-ENT-ID.                                      
               10  WS-TEST-ENT-ID          PIC 9(9).                            
               10  WS-TEST-IND             PIC X(1).                            
                                                                                
           05  WS-FUNC-CDE                 PIC X(2).                            
           05  WS-DIR-IND                  PIC X(1).                            
                                                                                
           05  WS-VENDOR-ENT-ID            PIC S9(9) COMP.                      
           05  WS-LAST-SEND-CNTL-NBR       PIC S9(9) COMP-3.                    
           05  WS-LAST-CNTL-NBR            PIC S9(9) COMP-3.                    
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FILE-SW              PIC X(1)  VALUE 'N'.                 
               88  END-OF-FILE                       VALUE 'Y'.                 
           05  TP-DKEY-FOUND-SW            PIC X(1)  VALUE 'N'.                 
               88  TP-DKEY-NOTFOUND                  VALUE 'N'.                 
               88  TP-DKEY-FOUND                     VALUE 'Y'.                 
                                                                                
                                                                                
       01  PV-PARAGRAPH-NAME         PIC X(30).                                 
       01  PV-DB2-OPER-ATTEMPTED     PIC X(30).                                 
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
           COPY DPWS004.                                                        
                                                                                
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDFNC                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT INPUT-FILE.                                               
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-PROCESS-INPUT-FILE                                      
              THRU 1000-PROCESS-INPUT-FILE-EXIT                                 
                 UNTIL END-OF-FILE.                                             
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-INPUT-FILE.                                                 
                                                                                
           MOVE IN-ENT-ID TO WS-ENT-ID.                                         
           IF WS-TEST-IND = 'T'                                                 
              MOVE WS-TEST-ENT-ID TO WS-VENDOR-ENT-ID                           
           ELSE                                                                 
              MOVE WS-ENT-ID TO WS-VENDOR-ENT-ID.                               
                                                                                
           SET TP-DKEY-FOUND TO TRUE.                                           
                                                                                
           PERFORM 1200-GET-TP-DKEY                                             
              THRU 1200-GET-TP-DKEY-EXIT.                                       
                                                                                
           IF TP-DKEY-NOTFOUND                                                  
              NEXT SENTENCE                                                     
           ELSE                                                                 
              PERFORM 1300-ISA-CONTROL-NBR                                      
                 THRU 1300-ISA-CONTROL-NBR-EXIT                                 
              PERFORM 1400-GS-CONTROL-NBR                                       
                 THRU 1400-GS-CONTROL-NBR-EXIT.                                 
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-INPUT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
                                                                                
       1100-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE INTO WS-INPUT-RECORD                                 
              AT END                                                            
                 SET END-OF-FILE TO TRUE.                                       
                                                                                
       1100-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       1200-GET-TP-DKEY.                                                        
                                                                                
           EXEC SQL                                                             
              SELECT TP_DKEY                                                    
              INTO   :TRDPRT-TP-DKEY                                            
              FROM   TTRDPRT                                                    
              WHERE  ENT_ID             = :WS-VENDOR-ENT-ID                     
                AND  VEND_INCHG_ID      = :IN-PARTNER-ID                        
                AND  VEND_INCHG_ID_DESC = :IN-PARTNER-ID-QUAL                   
                AND  INACTV_DTE         = '9999-09-09'                          
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZERO                                                    
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 DISPLAY 'ENT ID --> ' WS-VENDOR-ENT-ID                         
                 DISPLAY 'PARTNER ID --> ' IN-PARTNER-ID                        
                 DISPLAY '******** NOT FOUND ********'                          
                 DISPLAY '                           '                          
                 SET  TP-DKEY-NOTFOUND TO TRUE                                  
              ELSE                                                              
                 MOVE '1200-GET-TP-DKEY'    TO PV-PARAGRAPH-NAME                
                 MOVE 'SELECT TRDPRT TABLE' TO PV-DB2-OPER-ATTEMPTED            
                 DISPLAY 'ENT ID --> ' WS-VENDOR-ENT-ID                         
                 DISPLAY 'PARTNER ID --> ' IN-PARTNER-ID                        
                 PERFORM 1500-SQL-ERROR.                                        
                                                                                
       1200-GET-TP-DKEY-EXIT.                                                   
           EXIT.                                                                
                                                                                
                                                                                
       1300-ISA-CONTROL-NBR.                                                    
                                                                                
           MOVE IN-ISA-CNTL-NBR TO WS-LAST-SEND-CNTL-NBR.                       
                                                                                
           EXEC SQL                                                             
              UPDATE TTRDPRT                                                    
              SET    LAST_SEND_CNTL_NBR = :WS-LAST-SEND-CNTL-NBR                
              WHERE  ENT_ID             = :WS-VENDOR-ENT-ID                     
                AND  VEND_INCHG_ID      = :IN-PARTNER-ID                        
                AND  VEND_INCHG_ID_DESC = :IN-PARTNER-ID-QUAL                   
                AND  INACTV_DTE         = '9999-09-09'                          
                AND  LAST_SEND_CNTL_NBR < :WS-LAST-SEND-CNTL-NBR                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZERO                                                    
              DISPLAY 'ISA CNTL NBR FOR VENDOR ' IN-PARTNER-ID                  
                      ' CHANGED TO ' WS-LAST-SEND-CNTL-NBR                      
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 MOVE '1300-ISA-CONTROL-NBR' TO PV-PARAGRAPH-NAME               
                 MOVE 'UPDATE TRDPRT TABLE'  TO PV-DB2-OPER-ATTEMPTED           
                 DISPLAY 'ENT ID --> ' WS-VENDOR-ENT-ID                         
                 DISPLAY 'PARTNER ID --> ' IN-PARTNER-ID                        
                 PERFORM 1500-SQL-ERROR.                                        
                                                                                
       1300-ISA-CONTROL-NBR-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       1400-GS-CONTROL-NBR.                                                     
                                                                                
           MOVE IN-GS-CNTL-NBR TO WS-LAST-CNTL-NBR.                             
                                                                                
           IF IN-TRAN-TYPE = '850'                                              
              MOVE 'PO' TO WS-FUNC-CDE                                          
              MOVE 'S'  TO WS-DIR-IND.                                          
SP0509     IF IN-TRAN-TYPE = '860'                                              
SP0509        MOVE 'PC' TO WS-FUNC-CDE                                          
SP0509        MOVE 'S'  TO WS-DIR-IND.                                          
           IF IN-TRAN-TYPE = '852'                                              
              MOVE 'PD' TO WS-FUNC-CDE                                          
              MOVE 'S'  TO WS-DIR-IND.                                          
           IF IN-TRAN-TYPE = '864'                                              
              MOVE 'TX' TO WS-FUNC-CDE                                          
              MOVE 'S'  TO WS-DIR-IND.                                          
           IF IN-TRAN-TYPE = '820'                                              
              MOVE 'RA' TO WS-FUNC-CDE                                          
              MOVE 'S'  TO WS-DIR-IND.                                          
JF0502     IF IN-TRAN-TYPE = '816'                                              
JF0502        MOVE 'OR' TO WS-FUNC-CDE                                          
JF0502        MOVE 'S'  TO WS-DIR-IND.                                          
                                                                                
      ****************************************************************          
      *                                                                         
      *  THE 840 DOCUMENT IS USED AS A DUMMY FOR TRADING PARTNER                
      *  ID'S WHICH DO NOT HAVE AN OUTBOUND DOCUMENT LOADED                     
      *  ON UNIX.  WE NEED TO LOAD THE DUMMY TO ACCOUNT FOR                     
      *  OUTBOUND FA CONTROL NUMBERS.                                           
      *                                                                         
      ****************************************************************          
                                                                                
           IF IN-TRAN-TYPE = '840'                                              
              MOVE 'RQ' TO WS-FUNC-CDE                                          
              MOVE 'S'  TO WS-DIR-IND.                                          
                                                                                
           EXEC SQL                                                             
              UPDATE TTRDFNC                                                    
              SET    LAST_CNTL_NBR = :WS-LAST-CNTL-NBR                          
              WHERE  TP_DKEY       = :TRDPRT-TP-DKEY                            
                AND  FUNC_CDE      = :WS-FUNC-CDE                               
                AND  DIR_IND       = :WS-DIR-IND                                
                AND  LAST_CNTL_NBR < :WS-LAST-CNTL-NBR                          
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZERO                                                    
              DISPLAY ' GS CNTL NBR FOR VENDOR ' IN-PARTNER-ID                  
                      ' CHANGED TO ' WS-LAST-CNTL-NBR                           
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 MOVE '1400-GS-CONTROL-NBR' TO PV-PARAGRAPH-NAME                
                 MOVE 'UPDATE TRDFNC TABLE' TO PV-DB2-OPER-ATTEMPTED            
                 DISPLAY 'TP DKEY --> ' TRDPRT-TP-DKEY                          
                 DISPLAY 'FUNC CDE --> ' WS-FUNC-CDE                            
                 PERFORM 1500-SQL-ERROR.                                        
                                                                                
       1400-GS-CONTROL-NBR-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       1500-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
