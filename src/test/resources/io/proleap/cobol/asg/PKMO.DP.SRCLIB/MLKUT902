       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT902.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/19/2004.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  COMMON EXTRACT OF ML'S WEEKLY SUMMARY TABLE FOR OTHER DOMAINS *        
      *                                                                *        
      *  THIS PROGRAM WILL EXTRACT DATA FROM ML'S WEEKLY SUMMARY       *        
      *  TABLE FOR OTHER DOMAINS TO LOAD INTO THEIR VERSION OF THE     *        
      *  TABLE.  THIS PROGRAM IS SIMILAR TO MLKUT901, BUT IT WILL      *        
      *  SUM UP ALL THE DATA COLUMNS TO THE WEEKLY, DEPT, MAJ-CLASS,   *        
      *  AND VENDOR.  IT WILL ALSO CALCULATE THE ENDING INVENTORY COST *        
      *  USING THE ENDING INVENTORY RETAIL FROM DETAIL TABLE AND       *        
      *  THE CUMULATIVE MARKUP PERCENT FROM TVAMCL TABLE.              *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
W23843* WR23843  2005-06-23  FIX EIC CARRY OVER FROM PREVIOUS CALC     *        
      *                      WHEN NO CUM% FOUND IN TVAMCL.  -VL YANG   *        
      * W28582   2006-01-17  CLARK SCHWENN- CHANGES ARE FOR VENDOR/    *        
      *                      BRAND PROJECT.   ADDED LOGIC - JOIN TO    *        
      *                      NOW PICK UP THE ENT ID FROM THE DEPT      *        
      *                      BRAND VENDOR RELATIONSHIP TABLE.          *        
32094 * WR32094  2007-08-06  MISSING EIC IN THE VERSION 2 DATA FEED TO          
32094 *                      THE DATA WAREHOUSE             -R JANSSEN          
      * W28582   2007-08-20  JILL LIN - PULL NEW WKLY_VND TABLE FOR    *        
      *                      CREATING ML902 FILE FOR VA EHANCEMENT.    *        
      * W33539   2007-11-02  JILL LIN - ENHANCE THE PERFORMANCE BY     *        
      *                      REMOVE WKLY AND PARTN TABLES.             *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
W33539     SELECT OPTIONAL MLT-WKLY-SUM-VND-FILE                                
               ASSIGN TO INP01.                                                 
                                                                                
W33539     SELECT MLT-WKLY-SUM-EXTRACT                                          
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
W33539 FD  MLT-WKLY-SUM-VND-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
W33539 01  MLT-WKLY-SUM-VND-REC            PIC X(360).                          
                                                                                
       FD  MLT-WKLY-SUM-EXTRACT                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 360 CHARACTERS                                       
           DATA RECORD IS COMMON-EXTRACT-REC.                                   
       01  COMMON-EXTRACT-REC              PIC X(360).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  COMMON EXTRACT DATA LAYOUT                                             
      ******************************************************************        
      *01  ML902-WKLY-SUM-COMMON-EXTRACT.                                       
           COPY MLRD902.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
W33539     05  PS-END-OF-WKLYVND-FILE      PIC X           VALUE 'N'.           
W33539         88  NOT-END-OF-WKLYVND-FILE                 VALUE 'N'.           
W33539         88  END-OF-WKLYVND-FILE                     VALUE 'Y'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-DEPT-NBR-9               PIC 9(03)       VALUE ZEROES.        
           05  PV-DEPT-NBR REDEFINES PV-DEPT-NBR-9                              
                                           PIC X(03).                           
           05  PV-MAJ-CL-NBR-9             PIC 9(02)       VALUE ZEROES.        
           05  PV-MAJ-CL-NBR REDEFINES PV-MAJ-CL-NBR-9                          
                                           PIC X(02).                           
W33539     05  PV-RECORD-EXTRACTED         PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4003                                  VALUE +4003.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TVAMCL - MONTHLY VENDOR ANALYSIS MAJ-CLASS TABLE           *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TVAMCL                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TDTATTR - ML'S DATE ATTRIBUTE TABLE                        *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
W33539     PERFORM Z500-READ-WKLYVND-FILE.                                      
W33539     MOVE ZEROES TO PV-RECORD-EXTRACTED.                                  
W33539     PERFORM B200-PROCESS-WKLYVND-FILE                                    
W33539         UNTIL END-OF-WKLYVND-FILE.                                       
                                                                                
           PERFORM B400-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
W33539     OPEN INPUT  MLT-WKLY-SUM-VND-FILE                                    
W33539          OUTPUT MLT-WKLY-SUM-EXTRACT.                                    
                                                                                
      ******************************************************************        
W33539*    PROCESS WKLY VND FLAT FILE                                           
      ******************************************************************        
W33539 B200-PROCESS-WKLYVND-FILE.                                               
W33539     MOVE ZEROES TO ML902-SUB-CL-NBR                                      
W33539                    ML902-LOC-NBR.                                        
W33539     PERFORM Z300-CALC-END-INV-COST.                                      
           PERFORM Z400-WRITE-EXTRACT.                                          
W33539     PERFORM Z500-READ-WKLYVND-FILE.                                      
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B400-FINALIZE.                                                           
                                                                                
W33539     DISPLAY ' EXTRACTED ' PV-RECORD-EXTRACTED.                           
                                                                                
W33539     CLOSE MLT-WKLY-SUM-VND-FILE                                          
W33539           MLT-WKLY-SUM-EXTRACT.                                          
                                                                                
      ******************************************************************        
      *    CALCULATE ENDING INVENTORY COST USING CMU% FROM TVAMCL               
      ******************************************************************        
       Z300-CALC-END-INV-COST.                                                  
                                                                                
           MOVE ML902-DEPT-NBR           TO PV-DEPT-NBR-9.                      
           MOVE PV-DEPT-NBR              TO VAMCL-DEPT-NBR.                     
                                                                                
           MOVE ML902-MAJ-CL-NBR         TO PV-MAJ-CL-NBR-9.                    
           MOVE PV-MAJ-CL-NBR            TO VAMCL-MAJ-CL-NBR.                   
                                                                                
           MOVE ML902-ENT-ID             TO VAMCL-ENT-ID.                       
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                       A.CUM_MKUP_PCT                                           
                  INTO                                                          
                       :VAMCL-CUM-MKUP-PCT                                      
                  FROM                                                          
                       TVAMCL     A                                             
                      ,TDTATTR    B                                             
                  WHERE                                                         
                       B.KY_DTE          = :ML902-FSCL-WK-END-DTE               
                   AND B.FSCL_MN_NBR     = A.FSCL_MN_NBR                        
                   AND B.FSCL_MN_END_DTE = A.FSCL_MN_END_DTE                    
                   AND A.DEPT_NBR        = :VAMCL-DEPT-NBR                      
                   AND A.MAJ_CL_NBR      = :VAMCL-MAJ-CL-NBR                    
                   AND A.ENT_ID          = :VAMCL-ENT-ID                        
                  WITH UR                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   COMPUTE ML902-END-INV-COST-AMT ROUNDED                       
                           = ML902-END-INV-RTL-AMT                              
                               * (100 - VAMCL-CUM-MKUP-PCT) / 100               
               WHEN +100                                                        
W23843             MOVE ML902-END-INV-RTL-AMT                                   
W23843                 TO ML902-END-INV-COST-AMT                                
                   IF ML902-END-INV-RTL-AMT = ZEROES                            
                       CONTINUE                                                 
                   ELSE                                                         
                       DISPLAY '   CANNOT FIND CMU% FOR'                        
                               ' FISCAL WEEK: ' ML902-FSCL-WK-END-DTE           
                               ' DEPT: ' VAMCL-DEPT-NBR                         
                               ' MAJ-CLASS: ' VAMCL-MAJ-CL-NBR                  
                               ' ENT-ID: ' VAMCL-ENT-ID                         
                       DISPLAY '    DEFAULTING EIC = EIR (CMU% = 0): '          
                               ML902-END-INV-COST-AMT                           
                   END-IF                                                       
               WHEN OTHER                                                       
                   DISPLAY '*** FISCAL WEEK: ' ML902-FSCL-WK-END-DTE            
                           ' DEPT: ' VAMCL-DEPT-NBR                             
                           ' MAJ-CLASS: ' VAMCL-MAJ-CL-NBR                      
                           ' ENT-ID: ' VAMCL-ENT-ID                             
                   MOVE 'GET CMU% FROM TVAMCL' TO PV-OPER-ATTEMPTED             
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    WRITE OUT COMMON EXTRACT                                             
      ******************************************************************        
       Z400-WRITE-EXTRACT.                                                      
                                                                                
           ADD +1 TO PV-RECORD-EXTRACTED.                                       
           WRITE COMMON-EXTRACT-REC FROM ML902-WKLY-SUM-COMMON-EXTRACT.         
                                                                                
      ******************************************************************        
      *    READ CONTROL CARD                                                    
      ******************************************************************        
W33539 Z500-READ-WKLYVND-FILE.                                                  
                                                                                
W33539     READ MLT-WKLY-SUM-VND-FILE                                           
W33539         INTO ML902-WKLY-SUM-COMMON-EXTRACT                               
W33539         AT END SET END-OF-WKLYVND-FILE TO TRUE.                          
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MLKUT902'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                           
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
