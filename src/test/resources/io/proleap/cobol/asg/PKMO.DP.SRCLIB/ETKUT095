       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              ETKUT095.                                       
       AUTHOR.                  SUE PARI.                                       
       DATE-WRITTEN.            09/10/99.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM CHANGES THE ENT-ID TO A MASTER ENT-ID FOR ALL     *        
      * TRADING PARTNER IDS WITH MULTIPLE ENT-IDS.                     *        
************************************************************************        
JF0305*  DATE  02/17/2005                                                       
JF0305*  CHANGE MADE:                                                           
JF0305*   UPDATED PROGRAM FOR STORE EXPANSION TO FIVE DIGITS.                   
JF0305*  MADE BY: JON FIEDLER             WR# : WR26913                         
************************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT PD-INPUT-FILE                                                 
              ASSIGN TO INP01.                                                  
                                                                                
           SELECT PD-OUTPUT-FILE                                                
              ASSIGN TO OUT01.                                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PD-INPUT-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
JF0305     RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
           COPY ETRD019.                                                        
                                                                                
       FD  PD-OUTPUT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
JF0305     RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS OUTPUT-EDI-EXTRACT-RECORD.                            
                                                                                
JF0305 01  OUTPUT-PRODUCT-ACTIVITY-RECORD      PIC X(150).                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PD-FUNC-CDE                      PIC X(02) VALUE 'PD'.            
       01  PC-852-SET-CDE                      PIC X(03) VALUE '852'.           
                                                                                
       01  WS-ENT-ID-N                         PIC 9(10).                       
       01  WS-ENT-ID                           PIC S9(09) COMP.                 
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FILE-SW                  PIC X(01)   VALUE 'N'.           
               88  END-OF-FILE                             VALUE 'Y'.           
                                                                                
       01  PV-PARAGRAPH-NAME                   PIC X(30).                       
       01  PV-DB2-OPER-ATTEMPTED               PIC X(30).                       
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
           COPY DPWS004.                                                        
                                                                                
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDFNC                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDMLT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    PARTNER CURSOR                                                       
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE PARTNER_CSR CURSOR FOR                                     
               SELECT A.VEND_INCHG_ID,                                          
                      A.VEND_INCHG_ID_DESC,                                     
                      A.ENT_ID                                                  
               FROM   TTRDPRT A,                                                
                      TTRDFNC B,                                                
                      TTRDTRN C                                                 
               WHERE  A.ENT_ID       = :WS-ENT-ID AND                           
                      A.TP_DKEY      = B.TP_DKEY AND                            
                      A.TP_DKEY      = C.TP_DKEY AND                            
                      A.INACTV_DTE   = '9999-09-09' AND                         
                      B.FUNC_CDE     = :PC-PD-FUNC-CDE AND                      
                      C.INACTV_DTE   = '9999-09-09' AND                         
                      C.TRAN_SET_CDE = :PC-852-SET-CDE                          
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  PD-INPUT-FILE                                            
                OUTPUT PD-OUTPUT-FILE.                                          
                                                                                
           PERFORM 1300-READ-INPUT-FILE                                         
              THRU 1300-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-FILE.                                             
                                                                                
           CLOSE PD-INPUT-FILE                                                  
                 PD-OUTPUT-FILE.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-FILE.                                                       
                                                                                
           IF ET019-HEADER-RECORD                                               
               PERFORM 1100-DETERMINE-ENT-ID                                    
                  THRU 1100-DETERMINE-ENT-ID-EXIT.                              
                                                                                
           PERFORM 1200-WRITE-OUTREC                                            
              THRU 1200-WRITE-OUTREC-EXIT.                                      
                                                                                
           PERFORM 1300-READ-INPUT-FILE                                         
              THRU 1300-READ-INPUT-FILE-EXIT.                                   
                                                                                
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
       1100-DETERMINE-ENT-ID.                                                   
                                                                                
           MOVE ET019-ENT-ID TO WS-ENT-ID-N.                                    
           MOVE WS-ENT-ID-N  TO WS-ENT-ID.                                      
                                                                                
           PERFORM 1400-OPEN-PRT-CURSOR THRU 1400-EXIT.                         
           PERFORM 1500-FETCH-PARTNER THRU 1500-EXIT.                           
           PERFORM 1600-CLOSE-PRT-CURSOR THRU 1600-EXIT.                        
                                                                                
      ***************************************************************           
      * THE CODE BELOW CHECKS FOR TRADING PARTNER ID'S WHICH HAVE               
      * BEEN SET UP FOR MORE THAN ONE ENT-ID AND MOVES THE ENT-ID OF            
      * THE PARENT TO THE FILE.                                                 
      ***************************************************************           
                                                                                
           MOVE TRDPRT-VEND-INCHG-ID TO TRDMLT-VND-INCHG-ID.                    
           MOVE TRDPRT-VEND-INCHG-ID-DESC TO TRDMLT-VND-INCHG-ID-DESC.          
                                                                                
           EXEC SQL                                                             
             SELECT PRNT_ENT_ID                                                 
               INTO :TRDMLT-PRNT-ENT-ID                                         
               FROM TTRDMLT                                                     
              WHERE VND_INCHG_ID = :TRDMLT-VND-INCHG-ID                         
                AND VND_INCHG_ID_DESC = :TRDMLT-VND-INCHG-ID-DESC               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +000                                              
                   MOVE TRDMLT-PRNT-ENT-ID TO WS-ENT-ID-N                       
                   MOVE WS-ENT-ID-N TO ET019-ENT-ID                             
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '1100-DETERMINE-ENT-ID' TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT TTRDMLT' TO PV-DB2-OPER-ATTEMPTED               
                   PERFORM 1700-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       1100-DETERMINE-ENT-ID-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       1200-WRITE-OUTREC.                                                       
                                                                                
           MOVE ET019-PRODUCT-ACTIVITY-RECORD TO                                
              OUTPUT-PRODUCT-ACTIVITY-RECORD.                                   
           WRITE OUTPUT-PRODUCT-ACTIVITY-RECORD.                                
                                                                                
       1200-WRITE-OUTREC-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
       1300-READ-INPUT-FILE.                                                    
                                                                                
           READ PD-INPUT-FILE                                                   
              AT END                                                            
                 SET END-OF-FILE TO TRUE.                                       
                                                                                
       1300-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    OPEN PARTNER CURSOR                                                  
      *****************************************************************         
       1400-OPEN-PRT-CURSOR.                                                    
                                                                                
           MOVE '1400-'              TO PV-PARAGRAPH-NAME.                      
           MOVE 'OPEN PARTNER CSR'   TO PV-DB2-OPER-ATTEMPTED.                  
                                                                                
           EXEC SQL                                                             
              OPEN PARTNER_CSR                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 1700-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       1400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    FETCH PARTNER                                                        
      *****************************************************************         
       1500-FETCH-PARTNER.                                                      
                                                                                
           MOVE '1500-'            TO PV-PARAGRAPH-NAME.                        
           MOVE 'FETCH PARTNER'    TO PV-DB2-OPER-ATTEMPTED.                    
                                                                                
           EXEC SQL                                                             
             FETCH PARTNER_CSR                                                  
             INTO  :TRDPRT-VEND-INCHG-ID,                                       
                   :TRDPRT-VEND-INCHG-ID-DESC,                                  
                   :TRDPRT-ENT-ID                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY 'BAD WS-ENT-ID  = ' WS-ENT-ID                        
                   PERFORM 1700-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    CLOSE PARTNER CURSOR                                                 
      *****************************************************************         
       1600-CLOSE-PRT-CURSOR.                                                   
                                                                                
           MOVE '1600-'            TO PV-PARAGRAPH-NAME.                        
           MOVE 'CLOSE PARTNR CSR' TO PV-DB2-OPER-ATTEMPTED.                    
                                                                                
           EXEC SQL                                                             
              CLOSE PARTNER_CSR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 1700-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       1600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1700-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
