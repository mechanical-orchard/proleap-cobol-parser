       IDENTIFICATION DIVISION.                                         00010002
       PROGRAM-ID.      XSKUP096.                                       00020002
       AUTHOR.          VAMSI PASUPULETI.                               00030002
       DATE-WRITTEN.    JUNE 2017.                                      00040002
      *--------------------------------------------------------------*  00050002
      * DESCRIPTION:                                                 *  00060002
      *               PURGE PROCESS FOR PRICE CHANGE DATA WITHIN     *  00070002
      *      THE XS DOMAIN.  DELETE DATE IS PROVIDED FROM THE CONTROL*  00080002
      *      CARD VALUES.                                            *  00090002
      *      --- PURGE DATE IS (JOB RUN DATE - 30 DAYS) ----------   *  00100002
      *                                                              *  00110002
      *      STR-GP-PRCHG-CSR CURSOR FETCHES DISTINCT PRICE CHANGE ID*  00120002
      *      AND FOR EACH ID, A DELETE COMMAND IS ISSUED FOR         *  00130002
      *      XST_PRCHG TABLE. XST_STR_GP_PRCHG AND XST_PRCHG_MDSE    *  00140002
      *      TABLES ARE CHILD TO XST_PRCHG TABLE. HENCE ALL THE DATA *  00150002
      *      FOR EXTRACTED PRICE CHANGE ID WILL BE PURGED.           *  00160002
      *                                                              *  00170002
      *  ----IN ADDITION TO REGULAR PURGE, PRICE CHANGE ID THAT HAS  *  00180002
      *      NO CHILD ROWS IN PARENT TABLE(XST_PRCHG) WILL GET PURGED*  00190002
      *                                                              *  00200002
      * INPUTS:                                                      *  00210002
      *      PRCHG-MDSE-CSR CURSOR                                   *  00220002
      *      STR-GP-PRCHG-CSR CURSOR                                 *  00230002
      *      PRCHG-CSR CURSOR                                        *  00240002
      *      OLD-PRCHG-CSR CURSOR                                    *  00250002
      * OUTPUTS:                                                     *  00260002
      *      NONE                                                    *  00270002
      *--------------------------------------------------------------*  00280002
      *--------------------------------------------------------------*  00290002
      * HISTORY LOG:                                                 *  00300002
      *--------------------------------------------------------------*  00310002
      * DATE:        21/06/2017                                      *  00320002
      * WR/IR#:                                                      *  00330002
      * PROGRAMMER:  VAMSI PASUPULETI                                *  00340002
      * DESCRIPTION: PURGE PRCHG, STR_GP_PRCHG & PRCHG_MDSE DATA     *  00350002
      *              USING THE CONTROL CARD WHICH IS 30 DAYS OLDER   *  00360002
      *              THAN THE JOB RUN DATE                           *  00370002
      *--------------------------------------------------------------*  00380002
      * DATE:                                                        *  00390002
      * WR/IR#:                                                      *  00400002
      * PROGRAMMER:                                                  *  00410002
      * DESCRIPTION:                                                 *  00420002
      *--------------------------------------------------------------*  00430002
       ENVIRONMENT DIVISION.                                            00440002
       CONFIGURATION SECTION.                                           00450002
       SOURCE-COMPUTER.        IBM-3090.                                00460002
       OBJECT-COMPUTER.        IBM-3090.                                00470002
       INPUT-OUTPUT SECTION.                                            00480002
       FILE-CONTROL.                                                    00490002
                                                                        00500002
           SELECT CONTROL-CARD-PURGE-DATE                               00510002
                  ASSIGN TO INP01.                                      00520000
                                                                        00530002
       DATA DIVISION.                                                   00540002
       FILE SECTION.                                                    00550002
                                                                        00560002
       FD  CONTROL-CARD-PURGE-DATE                                      00570002
           RECORDING MODE IS F                                          00580002
           BLOCK CONTAINS 0 RECORDS                                     00590002
           LABEL RECORDS ARE OMITTED                                    00600002
           DATA RECORD IS CONTROL-CARD-PURGE-REC.                       00610000
       01  CONTROL-CARD-PURGE-REC          PIC X(080).                  00620000
                                                                        00630002
      *--------------------------------------------------------------*  00640002
       WORKING-STORAGE SECTION.                                         00650002
      *--------------------------------------------------------------*  00660002
       01  PS-PROGRAM-SWITCHES.                                         00670002
           05  PS-EOF-CC-PURGE-SW           PIC X(01)  VALUE 'N'.       00680000
               88  PS-EOF-CC-PURGE                     VALUE 'Y'.       00690000
               88  PS-NOT-EOF-CC-PURGE                 VALUE 'N'.       00700000
           05  PS-EOF-PRCHG-MDSE-CSR-SW     PIC X(01)  VALUE 'N'.       00710000
               88  PS-EOF-PRCHG-MDSE-CSR               VALUE 'Y'.       00720000
               88  PS-NOT-EOF-PRCHG-MDSE-CSR           VALUE 'N'.       00730000
           05  PS-EOF-STR-GP-CSR-SW         PIC X(01)  VALUE 'N'.       00740000
               88  PS-EOF-STR-GP-CSR                   VALUE 'Y'.       00750000
               88  PS-NOT-EOF-STR-GP-CSR               VALUE 'N'.       00760000
           05  PS-EOF-PRCHG-CSR-SW          PIC X(01)  VALUE 'N'.       00770000
               88  PS-EOF-PRCHG-CSR                    VALUE 'Y'.       00780000
               88  PS-NOT-EOF-PRCHG-CSR                VALUE 'N'.       00790000
           05  PS-EOF-OLD-PRCHG-SW          PIC X(01)  VALUE 'N'.       00800000
               88  PS-EOF-OLD-PRCHG-CSR                VALUE 'Y'.       00810000
               88  PS-NOT-EOF-OLD-PRCHG-CSR            VALUE 'N'.       00820000
           05  PS-DEFAULT-CKPT-FEQ-SW       PIC  X(01) VALUE 'Y'.       00830000
               88  PS-DEFAULT-CKPT-FEQ-Y               VALUE 'Y'.       00840000
               88  PS-DEFAULT-CKPT-FEQ-N               VALUE 'N'.       00850000
                                                                        00860002
      *--------------------------------------------------------------*  00870002
      * PROGRAM CONSTANTS                                               00880002
      *--------------------------------------------------------------*  00890002
       01  PC-PROGRAM-CONSTANTS.                                        00900002
           05  PC-JOB-NAME             PIC  X(06)  VALUE 'XSK125'.      00910002
           05  PC-PGM-NAME             PIC  X(08)  VALUE 'XSKUP096'.    00920002
           05  PC-MAX-RETRIES          PIC S9(03)  VALUE +3 COMP-3.     00930002
           05  PC-MAX-DEADLOCKS        PIC S9(03)  VALUE +3 COMP-3.     00940002
           05  PC-COMMIT-CNT           PIC S9(09)  VALUE 01.            00950002
           05  PC-CONTROL-CARDS.                                        00960000
               10  PC-CC-PURGE-DATE       PIC X(10).                    00970000
               10  FILLER REDEFINES PC-CC-PURGE-DATE.                   00980000
                   15  PC-CC-CCYY         PIC X(04).                    00990000
                   15  FILLER             PIC X(01).                    01000000
                   15  PC-CC-MM           PIC X(02).                    01010000
                   15  FILLER             PIC X(01).                    01020000
                   15  PC-CC-DD           PIC X(02).                    01030000
                                                                        01040002
      *--------------------------------------------------------------*  01050002
      * PV- PROGRAM VARIABLES                                           01060002
      *--------------------------------------------------------------*  01070002
       01  PV-PROGRAM-VARAIBLES.                                        01080002
           05  PV-STRGP-FETCH-COUNT       PIC 9(09)   VALUE ZEROS.      01100002
           05  PV-PRCMD-FETCH-COUNT       PIC 9(09)   VALUE ZEROS.      01110002
           05  PV-OLD-FETCH-COUNT         PIC 9(09)   VALUE ZEROS.      01120006
           05  PV-DEL-MDSE-CNT            PIC 9(09)   VALUE ZEROS.      01130002
           05  PV-DEL-STRGP-CNT           PIC 9(09)   VALUE ZEROS.      01140002
           05  PV-DEL-OLD-PRCHG-CNT       PIC 9(09)   VALUE ZEROS.      01150006
           05  PV-COMMITED-CNT            PIC 9(09)   VALUE ZEROS.      01170000
           05  PV-COMMIT-CNT              PIC 9(09)   VALUE ZEROS.      01180000
           05  PV-RETRY-CNT               PIC S9(04)  VALUE ZEROS.      01190000
       01  PV-ABEND-FIELDS.                                             01200000
           05  PV-ABEND-CODE              PIC S9(4)   VALUE ZERO.       01210000
           05  PV-PARAGRAPH-NAME          PIC X(30)   VALUE SPACE.      01220000
       01  PV-MISC-FIELDS.                                              01230000
           05  PV-SQL-SUB                 PIC S9(09)  VALUE 0 COMP.     01240002
           05  PV-RETRY                   PIC 9999    VALUE 0 COMP.     01250002
           05  PV-COMMIT-TMST             PIC X(26)   VALUE SPACES.     01260002
           05  PV-CURRENT-TMST            PIC X(26)   VALUE SPACES.     01270002
           05  PV-PRCHG-ID                PIC X(10)  VALUE SPACES.      01280000
                                                                        01290002
      *--------------------------------------------------------------*  01300002
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         01310002
      *--------------------------------------------------------------*  01320002
           COPY DPWS004.                                                01330002
                                                                        01340002
      *--------------------------------------------------------------*  01350002
      * DATE ROUTINE COPY MEMBERS                                       01360002
      *--------------------------------------------------------------*  01370002
           COPY DPWS500.                                                01380002
                                                                        01390002
      *--------------------------------------------------------------*  01400002
      * DB2 ABEND COPYBOOK                                              01410002
      *--------------------------------------------------------------*  01420002
           COPY DPWS900.                                                01430002
                                                                        01440002
      *--------------------------------------------------------------*  01450002
      * DB2 MESSAGE AREA                                                01460002
      *--------------------------------------------------------------*  01470002
           COPY SQLCA2.                                                 01480002
                                                                        01490002
      ***DB2 COMMUNICATIONS AREA                                        01500000
           EXEC SQL                                                     01510000
                INCLUDE SQLCA                                           01520000
           END-EXEC.                                                    01530000
      *--------------------------------------------------------------*  01540002
      * DB2 TABLE XST00063 (XST_PRCHG) PRICE CHANGE TABLE               01550002
      *--------------------------------------------------------------*  01560002
           EXEC SQL                                                     01570002
             INCLUDE XST00063                                           01580002
           END-EXEC.                                                    01590002
                                                                        01600002
      *--------------------------------------------------------------*  01610002
      * DB2 TABLE XST00064 (XST_STR_GP_PRCHG) STORE GROUP PRICE CHANGE  01620002
      *--------------------------------------------------------------*  01630002
           EXEC SQL                                                     01640002
             INCLUDE XST00064                                           01650002
           END-EXEC.                                                    01660002
                                                                        01670002
      *--------------------------------------------------------------*  01680002
      * DB2 TABLE XST00065 (XST_PRCHG_MDSE) PRICE CHANGE MERCHANDISE    01690002
      *--------------------------------------------------------------*  01700002
           EXEC SQL                                                     01710002
             INCLUDE XST00065                                           01720002
           END-EXEC.                                                    01730002
      *--------------------------------------------------------------*  01740002
      * DB2 TABLE TCKRSTCNTL (CHECK POINT TABLE)                        01750002
      *--------------------------------------------------------------*  01760002
                                                                        01770002
           EXEC SQL                                                     01780002
               INCLUDE TCKRSTCN                                         01790002
           END-EXEC.                                                    01800002
      ******************************************************************01810002
      *  TRANSACTION PREPARATION MODULE COMMUNICATIONS AREA             01820002
      ******************************************************************01830002
           COPY DPWS991.                                                01840002
                                                                        01850002
      ******************************************************************01860002
      *     OLD-PRCHG-CSR CURSOR FINDS ALL ROWS WITH NO CHILD ROWS      01870002
      ******************************************************************01880002
                                                                        01890002
           EXEC SQL                                                     01900002
             DECLARE OLD-PRCHG-CSR CURSOR WITH HOLD FOR                 01910002
              SELECT  A.PRCHG_ID                                        01920000
                FROM  XST_PRCHG A                                       01930000
               WHERE  NOT EXISTS                                        01940000
                     (SELECT 1                                          01950000
                        FROM XST_STR_GP_PRCHG B                         01960000
                       WHERE A.PRCHG_ID    = B.PRCHG_ID)                01970000
                  OR  NOT EXISTS                                        01980000
                     (SELECT 1                                          01990000
                        FROM XST_PRCHG_MDSE C                           02000000
                       WHERE A.PRCHG_ID    = C.PRCHG_ID)                02010000
                ORDER BY A.PRCHG_ID                                     02020000
           END-EXEC.                                                    02030002
                                                                        02040002
      ******************************************************************02050002
      *                   PRCHG-MDSE-CSR CURSOR                         02060002
      ******************************************************************02070002
                                                                        02080002
           EXEC SQL                                                     02090002
             DECLARE PRCHG-MDSE-CSR CURSOR WITH HOLD FOR                02100002
              SELECT  A.STR_GP_TYP_CDE                                  02110009
                     ,A.STR_GP_ID                                       02111009
                     ,A.PRCHG_ID                                        02112009
                FROM  XST_PRCHG_MDSE A                                  02120002
               WHERE  EXISTS                                            02130002
                     (SELECT 1                                          02140002
                        FROM XST_STR_GP_PRCHG B                         02150002
                       WHERE A.STR_GP_TYP_CDE  = B.STR_GP_TYP_CDE       02160002
                         AND A.STR_GP_ID       = B.STR_GP_ID            02170002
                         AND A.PRCHG_ID        = B.PRCHG_ID             02180002
                         AND B.PRCHG_EFF_DTE  <= :PC-CC-PURGE-DATE)     02190002
               ORDER BY  A.STR_GP_TYP_CDE                               02191011
                        ,A.STR_GP_ID                                    02192010
                        ,A.PRCHG_ID                                     02193010
           END-EXEC.                                                    02200002
      ******************************************************************02210002
      *                 STR-GP-PRCHG-CSR CURSOR                         02220002
      ******************************************************************02230002
                                                                        02240002
           EXEC SQL                                                     02250002
             DECLARE STR-GP-PRCHG-CSR CURSOR WITH HOLD FOR              02260002
              SELECT  STR_GP_TYP_CDE                                    02270009
                     ,STR_GP_ID                                         02271009
                     ,PRCHG_ID                                          02272009
                FROM  XST_STR_GP_PRCHG                                  02280002
               WHERE  PRCHG_EFF_DTE <= :PC-CC-PURGE-DATE                02290002
               ORDER BY  STR_GP_TYP_CDE                                 02300000
                        ,STR_GP_ID                                      02310000
                        ,PRCHG_ID                                       02320000
           END-EXEC.                                                    02330002
      *--------------------------------------------------------------*  02450002
      * PROGRAM COUNTERS                                                02460002
      *--------------------------------------------------------------*  02470002
       01  PV-PROGRAM-COUNTERS         COMP-3.                          02480002
           05  PV-NUMBER-OF-RETRIES     PIC 9(9) VALUE 0.               02490002
                                                                        02500002
      *--------------------------------------------------------------*  02510002
      * ERROR HANDLING                                                  02520002
      *--------------------------------------------------------------*  02530002
       01  PV-MISC-ABEND-FIELDS.                                        02540002
           05  PV-OPERATION-MSG-1      PIC X(40) VALUE SPACES.          02550002
           05  PV-OPERATION-MSG-2      PIC X(40) VALUE SPACES.          02560002
           05  PV-DB2-OPERATION        PIC X(30) VALUE SPACES.          02570002
                                                                        02580002
       01  ABEND-CODE                  PIC S9(4) VALUE 0  COMP SYNC.    02590002
           88  ABEND-4003-CTRL-CARD-ERROR        VALUE +4003.           02600002
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           02610002
           88  ABEND-4019-CAL-SUB-ERROR          VALUE +4019.           02620002
           88  ABEND-4020-MQ-ERROR               VALUE +4020.           02630002
           88  ABEND-4444-FORCED-ABEND           VALUE +4444.           02640002
           88  ABEND-4008                        VALUE +4008.           02650002
           88  ABEND-4005                        VALUE +4005.           02660002
                                                                        02670002
      *--------------------------------------------------------------*  02680002
       PROCEDURE DIVISION.                                              02690002
      *--------------------------------------------------------------*  02700002
       0000-MAINLINE.                                                   02710002
                                                                        02720002
           DISPLAY 'BEGIN MESSAGES FOR XSKUP096'.                       02730000
                                                                        02740000
           PERFORM 1000-INITIALIZE.                                     02750000
                                                                        02760000
           PERFORM 2000-OPEN-MDSE-CSR-CURSOR.                           02770000
                                                                        02780000
           PERFORM 3000-OPEN-STRGP-CSR-CURSOR.                          02790000
                                                                        02791006
           PERFORM 4000-OPEN-OLD-PRCHG-CURSOR.                          02800006
                                                                        02810006
           PERFORM 9200-DISPLAY-STATISTICS.                             02850000
                                                                        02860000
           STOP RUN.                                                    02870002
                                                                        02880002
      ******************************************************************02890000
      **INITIALIZATION PROCESSING                                       02900000
      ******************************************************************02910000
       1000-INITIALIZE.                                                 02920000
           MOVE '1000-INITIALIZE               ' TO PV-PARAGRAPH-NAME.  02930000
                                                                        02940000
           OPEN INPUT CONTROL-CARD-PURGE-DATE.                          02950000
                                                                        02960000
           READ CONTROL-CARD-PURGE-DATE INTO PC-CC-PURGE-DATE           02970000
                AT END                                                  02980000
                   SET PS-EOF-CC-PURGE TO TRUE                          02990000
           END-READ.                                                    03000000
                                                                        03010000
           CLOSE CONTROL-CARD-PURGE-DATE.                               03020000
                                                                        03030000
           DISPLAY 'CONTROL CARD PURGE DATE: ' PC-CC-PURGE-DATE.        03040000
                                                                        03050000
           EXEC SQL                                                     03060000
               SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                 03070000
           END-EXEC.                                                    03080000
      ******************************************************************03090000
      **OPEN CURSOR PRCHG_MDSE_CSR                                      03100000
      ******************************************************************03110000
       2000-OPEN-MDSE-CSR-CURSOR.                                       03120000
                                                                        03130000
           MOVE '2000-OPEN-MDSE-CSR-CURSOR' TO PV-PARAGRAPH-NAME.       03140000
                                                                        03150000
           INITIALIZE PV-RETRY-CNT.                                     03160000
           PERFORM WITH TEST AFTER                                      03170000
               UNTIL SQLCODE = ZERO                                     03180000
                  OR SQLCODE = +100                                     03190000
                  OR PV-RETRY-CNT > PC-MAX-RETRIES                      03200000
                     EXEC SQL                                           03210000
                         OPEN PRCHG-MDSE-CSR                            03220000
                     END-EXEC                                           03230000
                                                                        03240000
               EVALUATE TRUE                                            03250000
                   WHEN SQLCODE = ZERO                                  03260000
                        PERFORM 2500-FETCH-MDSE-CSR                     03270000
                          UNTIL PS-EOF-PRCHG-MDSE-CSR                   03280000
                        DISPLAY '***** PURGE PROCESS STARTED *****'     03290000
                   WHEN SQLCODE = +100                                  03300000
                        DISPLAY '*** NO VALID PRCHG FOUND TO PURGE ***' 03310000
                   WHEN SQLCODE = -904                                  03320000
                   WHEN SQLCODE = -911                                  03330000
                   WHEN SQLCODE = -913                                  03340000
                        ADD +1 TO PV-RETRY-CNT                          03350000
                   WHEN SQLWARN NOT = SPACES                            03360000
                   WHEN SQLCODE NOT = ZERO                              03370000
                        MOVE 'OPEN CURSOR1 OPERATION FAILED'            03380000
                                   TO PV-OPERATION-MSG-1                03390000
                        PERFORM 9200-DISPLAY-STATISTICS                 03400000
                        PERFORM 9900-SQL-ABEND-ROUTINE                  03410000
               END-EVALUATE                                             03420000
           END-PERFORM.                                                 03430000
                                                                        03440000
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             03450000
               MOVE 'OPEN CURSOR1 RETRIES EXCEEDED'                     03460000
                                      TO  PV-OPERATION-MSG-1            03470000
               PERFORM 9200-DISPLAY-STATISTICS                          03480000
               PERFORM 9900-SQL-ABEND-ROUTINE                           03490000
           END-IF.                                                      03500000
                                                                        03510000
      ******************************************************************03520000
      **PROCESSES STR-GP-PRCHG-CSR CURSOR                               03530000
      ******************************************************************03540000
       2500-FETCH-MDSE-CSR.                                             03550000
           MOVE '2500-FETCH-MDSE-CSR' TO PV-PARAGRAPH-NAME.             03560000
                                                                        03570000
           INITIALIZE PV-RETRY-CNT.                                     03580000
           PERFORM WITH TEST AFTER                                      03590000
             UNTIL SQLCODE = ZERO                                       03600000
                OR SQLCODE = +100                                       03610000
                OR PV-RETRY-CNT > PC-MAX-RETRIES                        03620000
                   EXEC SQL                                             03630000
                       FETCH PRCHG-MDSE-CSR                             03640000
                       INTO  :XST00065-STR-GP-TYP-CDE                   03650009
                            ,:XST00065-STR-GP-ID                        03651009
                            ,:XST00065-PRCHG-ID                         03652009
                   END-EXEC                                             03660000
                                                                        03670000
                   EVALUATE TRUE                                        03680000
                       WHEN SQLCODE = ZERO                              03690000
                            ADD 1 TO PV-PRCMD-FETCH-COUNT               03700000
                            PERFORM 5100-DELETE-MDSE-ROWS               03710000
                       WHEN SQLCODE = +100                              03720000
                            SET PS-EOF-PRCHG-MDSE-CSR TO TRUE           03730000
                            PERFORM 2750-CLOSE-MDSE-CSR                 03740000
                       WHEN SQLCODE = -904                              03750000
                       WHEN SQLCODE = -911                              03760000
                       WHEN SQLCODE = -913                              03770000
                        ADD +1 TO PV-RETRY-CNT                          03780000
                       WHEN SQLWARN NOT = SPACES                        03790000
                       WHEN SQLCODE NOT = ZERO                          03800000
                            MOVE 'FETCH PRCHG MDSE CURSOR PROBLEM'      03810000
                                              TO  PV-OPERATION-MSG-1    03820000
                            PERFORM 9200-DISPLAY-STATISTICS             03830000
                            PERFORM 9900-SQL-ABEND-ROUTINE              03840000
                   END-EVALUATE                                         03850000
           END-PERFORM.                                                 03860000
                                                                        03870000
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             03880000
               MOVE 'FETCH RETRIES EXCEEDED      '                      03890000
                                      TO  PV-OPERATION-MSG-1            03900000
               PERFORM 9200-DISPLAY-STATISTICS                          03910000
               PERFORM 9900-SQL-ABEND-ROUTINE                           03920000
           END-IF.                                                      03930000
                                                                        03940000
      ******************************************************************03950000
      **CLOSE CURSOR PRCHG_MDSE_CSR                                     03960000
      ******************************************************************03970000
       2750-CLOSE-MDSE-CSR.                                             03980000
           MOVE '2750-CLOSE-MDSE-CSR'  TO PV-PARAGRAPH-NAME.            03990000
                                                                        04000000
           INITIALIZE PV-RETRY-CNT.                                     04010000
           PERFORM WITH TEST AFTER                                      04020000
               UNTIL SQLCODE = ZERO                                     04030000
                  OR SQLCODE = +100                                     04040000
                  OR PV-RETRY-CNT > PC-MAX-RETRIES                      04050000
                     EXEC SQL                                           04060000
                          CLOSE PRCHG-MDSE-CSR                          04070000
                     END-EXEC                                           04080000
                                                                        04090000
               EVALUATE TRUE                                            04100000
                   WHEN SQLCODE = 0                                     04110000
                        PERFORM 9000-COMMIT-DATA                        04120000
                   WHEN SQLCODE = +100                                  04130000
                        CONTINUE                                        04140000
                   WHEN SQLCODE = -904                                  04150000
                   WHEN SQLCODE = -911                                  04160000
                   WHEN SQLCODE = -913                                  04170000
                        ADD +1 TO PV-RETRY-CNT                          04180000
                   WHEN SQLWARN NOT = SPACES                            04190000
                   WHEN SQLCODE NOT = ZERO                              04200000
                        MOVE 'CLOSE CURSOR1 OPERATION'                  04210000
                                   TO PV-OPERATION-MSG-1                04220000
                        PERFORM 9200-DISPLAY-STATISTICS                 04230000
                        PERFORM 9900-SQL-ABEND-ROUTINE                  04240000
               END-EVALUATE                                             04250000
           END-PERFORM.                                                 04260000
                                                                        04270000
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             04280000
               MOVE 'CLOSE CURSOR1 RETRIES EXCEEDED'                    04290000
                                      TO  PV-OPERATION-MSG-1            04300000
               PERFORM 9200-DISPLAY-STATISTICS                          04310000
               PERFORM 9900-SQL-ABEND-ROUTINE                           04320000
           END-IF.                                                      04330000
                                                                        04340000
                                                                        04350000
      ******************************************************************04360000
      **OPEN CURSOR XST_STR_GP_CSR                                      04370000
      ******************************************************************04380000
       3000-OPEN-STRGP-CSR-CURSOR.                                      04390000
                                                                        04400000
           MOVE '3000-OPEN-STRGP-CSR-CURSOR' TO PV-PARAGRAPH-NAME.      04410000
                                                                        04420000
           INITIALIZE PV-RETRY-CNT.                                     04430000
           PERFORM WITH TEST AFTER                                      04440000
               UNTIL SQLCODE = ZERO                                     04450000
                  OR SQLCODE = +100                                     04460000
                  OR PV-RETRY-CNT > PC-MAX-RETRIES                      04470000
                     EXEC SQL                                           04480000
                         OPEN STR-GP-PRCHG-CSR                          04490000
                     END-EXEC                                           04500000
                                                                        04510000
               EVALUATE TRUE                                            04520000
                   WHEN SQLCODE = ZERO                                  04530000
                        PERFORM 3500-FETCH-STRGP-CSR                    04540000
                          UNTIL PS-EOF-STR-GP-CSR                       04550000
                        DISPLAY '***** PURGE PROCESS STARTED *****'     04560000
                   WHEN SQLCODE = +100                                  04570000
                        DISPLAY '*** NO VALID STRGP FOUND TO PURGE ***' 04580000
                   WHEN SQLCODE = -904                                  04590000
                   WHEN SQLCODE = -911                                  04600000
                   WHEN SQLCODE = -913                                  04610000
                        ADD +1 TO PV-RETRY-CNT                          04620000
                   WHEN SQLWARN NOT = SPACES                            04630000
                   WHEN SQLCODE NOT = ZERO                              04640000
                        MOVE 'OPEN CURSOR1 OPERATION FAILED'            04650000
                                   TO PV-OPERATION-MSG-1                04660000
                        PERFORM 9200-DISPLAY-STATISTICS                 04670000
                        PERFORM 9900-SQL-ABEND-ROUTINE                  04680000
               END-EVALUATE                                             04690000
           END-PERFORM.                                                 04700000
                                                                        04710000
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             04720000
               MOVE 'OPEN CURSOR1 RETRIES EXCEEDED'                     04730000
                                      TO  PV-OPERATION-MSG-1            04740000
               PERFORM 9200-DISPLAY-STATISTICS                          04750000
               PERFORM 9900-SQL-ABEND-ROUTINE                           04760000
           END-IF.                                                      04770000
                                                                        04780000
      ******************************************************************04790000
      **PROCESSES STR_GP_PRCHG CURSOR                                   04800000
      ******************************************************************04810000
       3500-FETCH-STRGP-CSR.                                            04820000
           MOVE '3500-FETCH-STRGP-CSR' TO PV-PARAGRAPH-NAME.            04830000
                                                                        04840000
           INITIALIZE PV-RETRY-CNT.                                     04850000
           PERFORM WITH TEST AFTER                                      04860000
             UNTIL SQLCODE = ZERO                                       04870000
                OR SQLCODE = +100                                       04880000
                OR PV-RETRY-CNT > PC-MAX-RETRIES                        04890000
                   EXEC SQL                                             04900000
                       FETCH STR-GP-PRCHG-CSR                           04910000
                       INTO  :XST00064-STR-GP-TYP-CDE                   04920009
                            ,:XST00064-STR-GP-ID                        04921009
                            ,:XST00064-PRCHG-ID                         04922009
                   END-EXEC                                             04930000
                                                                        04940000
                   EVALUATE TRUE                                        04950000
                       WHEN SQLCODE = ZERO                              04960000
                            MOVE XST00064-PRCHG-ID TO PV-PRCHG-ID       04970000
                            ADD 1 TO PV-STRGP-FETCH-COUNT               04980000
                            PERFORM 5200-DELETE-STRGP-ROWS              04990000
                       WHEN SQLCODE = +100                              05000000
                            SET PS-EOF-STR-GP-CSR TO TRUE               05010000
                            PERFORM 3750-CLOSE-STRGP-CSR                05020000
                       WHEN SQLCODE = -904                              05030000
                       WHEN SQLCODE = -911                              05040000
                       WHEN SQLCODE = -913                              05050000
                        ADD +1 TO PV-RETRY-CNT                          05060000
                       WHEN SQLWARN NOT = SPACES                        05070000
                       WHEN SQLCODE NOT = ZERO                          05080000
                            MOVE 'FETCH STR GP PRCHG CURSOR PROBLEM'    05090000
                                              TO  PV-OPERATION-MSG-1    05100000
                            PERFORM 9200-DISPLAY-STATISTICS             05110000
                            PERFORM 9900-SQL-ABEND-ROUTINE              05120000
                   END-EVALUATE                                         05130000
           END-PERFORM.                                                 05140000
                                                                        05150000
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             05160000
               MOVE 'FETCH RETRIES EXCEEDED      '                      05170000
                                      TO  PV-OPERATION-MSG-1            05180000
               PERFORM 9200-DISPLAY-STATISTICS                          05190000
               PERFORM 9900-SQL-ABEND-ROUTINE                           05200000
           END-IF.                                                      05210000
                                                                        05220000
      ******************************************************************05230000
      **CLOSE CURSOR STR-GP-PRCHG-CSR                                   05240000
      ******************************************************************05250000
       3750-CLOSE-STRGP-CSR.                                            05260000
           MOVE '3750-CLOSE-STRGP-CSR' TO PV-PARAGRAPH-NAME.            05270000
                                                                        05280000
           INITIALIZE PV-RETRY-CNT.                                     05290000
           PERFORM WITH TEST AFTER                                      05300000
               UNTIL SQLCODE = ZERO                                     05310000
                  OR SQLCODE = +100                                     05320000
                  OR PV-RETRY-CNT > PC-MAX-RETRIES                      05330000
                     EXEC SQL                                           05340000
                          CLOSE STR-GP-PRCHG-CSR                        05350000
                     END-EXEC                                           05360000
                                                                        05370000
               EVALUATE TRUE                                            05380000
                   WHEN SQLCODE = 0                                     05390000
                        PERFORM 9000-COMMIT-DATA                        05400000
                   WHEN SQLCODE = +100                                  05410000
                        CONTINUE                                        05420000
                   WHEN SQLCODE = -904                                  05430000
                   WHEN SQLCODE = -911                                  05440000
                   WHEN SQLCODE = -913                                  05450000
                        ADD +1 TO PV-RETRY-CNT                          05460000
                   WHEN SQLWARN NOT = SPACES                            05470000
                   WHEN SQLCODE NOT = ZERO                              05480000
                        MOVE 'CLOSE CURSOR1 OPERATION'                  05490000
                                   TO PV-OPERATION-MSG-1                05500000
                        PERFORM 9200-DISPLAY-STATISTICS                 05510000
                        PERFORM 9900-SQL-ABEND-ROUTINE                  05520000
               END-EVALUATE                                             05530000
           END-PERFORM.                                                 05540000
                                                                        05550000
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             05560000
               MOVE 'CLOSE CURSOR1 RETRIES EXCEEDED'                    05570000
                                      TO  PV-OPERATION-MSG-1            05580000
               PERFORM 9200-DISPLAY-STATISTICS                          05590000
               PERFORM 9900-SQL-ABEND-ROUTINE                           05600000
           END-IF.                                                      05610000
                                                                        05620000
      ******************************************************************05630006
      **OPEN CURSOR OLD-PRCHG-CSR                                       05640006
      ******************************************************************05650006
       4000-OPEN-OLD-PRCHG-CURSOR.                                      05660006
           MOVE '4000-OPEN-OLD-PRCHG-CURSOR'    TO PV-PARAGRAPH-NAME.   05670006
                                                                        05680006
           INITIALIZE PV-RETRY-CNT.                                     05690006
           PERFORM WITH TEST AFTER                                      05700006
               UNTIL SQLCODE = ZERO                                     05710006
                  OR SQLCODE = +100                                     05720006
                  OR PV-RETRY-CNT > PC-MAX-RETRIES                      05730006
                     EXEC SQL                                           05740006
                         OPEN OLD-PRCHG-CSR                             05750006
                     END-EXEC                                           05760006
                                                                        05770006
               EVALUATE TRUE                                            05780006
                   WHEN SQLCODE = ZERO                                  05790006
                        DISPLAY '***** OLD PRCHG ID PURGE STARTED *****'05800006
                        PERFORM 4500-FETCH-OLD-PRCHG-CSR                05810006
                          UNTIL PS-EOF-OLD-PRCHG-CSR                    05820006
                   WHEN SQLCODE = +100                                  05830006
                        DISPLAY '***** NO OLD PRCHG ID TO PURGE *****'  05840006
                   WHEN SQLCODE = -904                                  05850006
                   WHEN SQLCODE = -911                                  05860006
                   WHEN SQLCODE = -913                                  05870006
                        ADD +1 TO PV-RETRY-CNT                          05880006
                   WHEN SQLWARN NOT = SPACES                            05890006
                   WHEN SQLCODE NOT = ZERO                              05900006
                        MOVE 'OPEN CURSOR2 OPERATION FAILED'            05910006
                                   TO PV-OPERATION-MSG-1                05920006
                        PERFORM 9200-DISPLAY-STATISTICS                 05930006
                        PERFORM 9900-SQL-ABEND-ROUTINE                  05940006
               END-EVALUATE                                             05950006
           END-PERFORM.                                                 05960006
                                                                        05970006
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             05980006
               MOVE 'OPEN CURSOR2 RETRIES EXCEEDED'                     05990006
                                      TO  PV-OPERATION-MSG-1            06000006
               PERFORM 9200-DISPLAY-STATISTICS                          06010006
               PERFORM 9900-SQL-ABEND-ROUTINE                           06020006
           END-IF.                                                      06030006
                                                                        06040006
      ******************************************************************06050006
      **PROCESSES OLD PRCHG CURSOR                                      06060006
      ******************************************************************06070006
       4500-FETCH-OLD-PRCHG-CSR.                                        06080006
           MOVE '4500-FETCH-OLD-PRCHG-CSR' TO PV-PARAGRAPH-NAME.        06090006
                                                                        06100006
           INITIALIZE PV-RETRY-CNT.                                     06110006
           PERFORM WITH TEST AFTER                                      06120006
             UNTIL SQLCODE = ZERO                                       06130006
                OR SQLCODE = +100                                       06140006
                OR PV-RETRY-CNT > PC-MAX-RETRIES                        06150006
                   EXEC SQL                                             06160006
                       FETCH OLD-PRCHG-CSR                              06170006
                       INTO  :XST00063-PRCHG-ID                         06180006
                   END-EXEC                                             06190006
                                                                        06200006
                   EVALUATE TRUE                                        06210006
                       WHEN SQLCODE = ZERO                              06220006
                            ADD 1 TO PV-OLD-FETCH-COUNT                 06230006
                            PERFORM 5300-DELETE-OLD-PRCHG-ROWS          06240006
                       WHEN SQLCODE = +100                              06250006
                            SET PS-EOF-OLD-PRCHG-CSR TO TRUE            06260006
                            PERFORM 4750-CLOSE-OLD-PRCHG-CURSOR         06270006
                       WHEN SQLCODE = -904                              06280006
                       WHEN SQLCODE = -911                              06290006
                       WHEN SQLCODE = -913                              06300006
                        ADD +1 TO PV-RETRY-CNT                          06310006
                       WHEN SQLWARN NOT = SPACES                        06320006
                       WHEN SQLCODE NOT = ZERO                          06330006
                            MOVE 'FETCH OLD PRCHG CURSOR PROBLEM'       06340006
                                              TO  PV-OPERATION-MSG-1    06350006
                            PERFORM 9200-DISPLAY-STATISTICS             06360006
                            PERFORM 9900-SQL-ABEND-ROUTINE              06370006
                   END-EVALUATE                                         06380006
           END-PERFORM.                                                 06390006
                                                                        06400006
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             06410006
               MOVE 'FETCH2 RETRIES EXCEEDED      '                     06420006
                                      TO  PV-OPERATION-MSG-1            06430006
               PERFORM 9200-DISPLAY-STATISTICS                          06440006
               PERFORM 9900-SQL-ABEND-ROUTINE                           06450006
           END-IF.                                                      06460006
                                                                        06470006
                                                                        06480006
      ******************************************************************06490006
      **CLOSE CURSOR2 OLD-PRCHG-CSR                                     06500006
      ******************************************************************06510006
       4750-CLOSE-OLD-PRCHG-CURSOR.                                     06520006
           MOVE '4750-CLOSE-OLD-PRCHG-CURSOR'    TO PV-PARAGRAPH-NAME.  06530006
                                                                        06540006
           INITIALIZE PV-RETRY-CNT.                                     06550006
           PERFORM WITH TEST AFTER                                      06560006
               UNTIL SQLCODE = ZERO                                     06570006
                  OR SQLCODE = +100                                     06580006
                  OR PV-RETRY-CNT > PC-MAX-RETRIES                      06590006
                     EXEC SQL                                           06600006
                          CLOSE OLD-PRCHG-CSR                           06610006
                     END-EXEC                                           06620006
                                                                        06630006
               EVALUATE TRUE                                            06640006
                   WHEN SQLCODE = 0                                     06650006
                        PERFORM 9000-COMMIT-DATA                        06660006
                   WHEN SQLCODE = +100                                  06670006
                        CONTINUE                                        06680006
                   WHEN SQLCODE = -904                                  06690006
                   WHEN SQLCODE = -911                                  06700006
                   WHEN SQLCODE = -913                                  06710006
                        ADD +1 TO PV-RETRY-CNT                          06720006
                   WHEN SQLWARN NOT = SPACES                            06730006
                   WHEN SQLCODE NOT = ZERO                              06740006
                        MOVE 'CLOSE CURSOR2 OPERATION'                  06750006
                                   TO PV-OPERATION-MSG-1                06760006
                        PERFORM 9200-DISPLAY-STATISTICS                 06770006
                        PERFORM 9900-SQL-ABEND-ROUTINE                  06780006
               END-EVALUATE                                             06790006
           END-PERFORM.                                                 06800006
                                                                        06810006
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             06820006
               MOVE 'CLOSE CURSOR2 RETRIES EXCEEDED'                    06830006
                                      TO  PV-OPERATION-MSG-1            06840006
               PERFORM 9200-DISPLAY-STATISTICS                          06850006
               PERFORM 9900-SQL-ABEND-ROUTINE                           06860006
           END-IF.                                                      06870006
                                                                        06880006
      ***************************************************************** 08200000
      **DELETE ROW(S) FROM TABLE XST_PRCHG_MDSE                         08210000
      ***************************************************************** 08220000
       5100-DELETE-MDSE-ROWS.                                           08230000
           MOVE '5100-DELETE-MDSE-ROWS' TO PV-PARAGRAPH-NAME.           08240000
                                                                        08250000
           INITIALIZE PV-RETRY-CNT.                                     08260000
           PERFORM WITH TEST AFTER                                      08270000
             UNTIL SQLCODE = ZERO                                       08280000
                OR SQLCODE = +100                                       08290000
                OR PV-RETRY-CNT > PC-MAX-RETRIES                        08300000
                   EXEC SQL                                             08310000
                      DELETE FROM  XST_PRCHG_MDSE                       08320000
                      WHERE  STR_GP_TYP_CDE = :XST00065-STR-GP-TYP-CDE  08330009
                        AND  STR_GP_ID      = :XST00065-STR-GP-ID       08331009
                        AND  PRCHG_ID       = :XST00065-PRCHG-ID        08332009
                   END-EXEC                                             08340000
                                                                        08350000
                   EVALUATE TRUE                                        08360000
                      WHEN SQLCODE = ZERO                               08370000
                         ADD +1 TO PV-DEL-MDSE-CNT                      08380000
                   EXEC SQL                                             08390000
                    SET :PV-CURRENT-TMST = CURRENT TIMESTAMP            08400000
                   END-EXEC                                             08410000
                         IF PV-CURRENT-TMST > PV-COMMIT-TMST            08420000
                         PERFORM 9000-COMMIT-DATA                       08430000
                         INITIALIZE PV-COMMIT-CNT                       08440000
                         END-IF                                         08450000
                      WHEN SQLCODE = -904                               08460000
                      WHEN SQLCODE = -911                               08470000
                      WHEN SQLCODE = -913                               08480000
                        ADD +1 TO PV-RETRY-CNT                          08490000
                      WHEN SQLCODE = +100                               08500000
                         DISPLAY 'ROW NOT FOUND FOR DELETE 5100'        08510008
                         DISPLAY 'PRCHG-ID  : ' XST00065-PRCHG-ID       08520000
                      WHEN OTHER                                        08530000
                         DISPLAY 'CANT DELETE FROM XST_PRCHG_MDSE'      08540000
                         DISPLAY 'PRCHG-ID  : ' XST00065-PRCHG-ID       08550000
                         PERFORM 9200-DISPLAY-STATISTICS                08560000
                         PERFORM 9900-SQL-ABEND-ROUTINE                 08570000
                   END-EVALUATE                                         08580000
           END-PERFORM.                                                 08590000
                                                                        08600000
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             08610000
               MOVE 'DEL TO XST_PRCHG TBL RETRY EXCEEDED'               08620000
                                      TO  PV-OPERATION-MSG-1            08630000
               PERFORM 9200-DISPLAY-STATISTICS                          08640000
               PERFORM 9900-SQL-ABEND-ROUTINE                           08650000
           END-IF.                                                      08660000
                                                                        08670000
      ***************************************************************** 08680000
      **DELETE ROW(S) FROM TABLE XST_STR_GP_PRCHG                       08690000
      ***************************************************************** 08700000
       5200-DELETE-STRGP-ROWS.                                          08710000
           MOVE '5200-DELETE-STRGP-ROWS' TO PV-PARAGRAPH-NAME.          08720000
                                                                        08730000
           INITIALIZE PV-RETRY-CNT.                                     08740000
           PERFORM WITH TEST AFTER                                      08750000
             UNTIL SQLCODE = ZERO                                       08760000
                OR SQLCODE = +100                                       08770000
                OR PV-RETRY-CNT > PC-MAX-RETRIES                        08780000
                   EXEC SQL                                             08790000
                      DELETE FROM  XST_STR_GP_PRCHG                     08800000
                      WHERE  STR_GP_TYP_CDE = :XST00064-STR-GP-TYP-CDE  08810009
                        AND  STR_GP_ID      = :XST00064-STR-GP-ID       08811009
                        AND  PRCHG_ID       = :XST00064-PRCHG-ID        08812009
                   END-EXEC                                             08820000
                                                                        08830000
                   EVALUATE TRUE                                        08840000
                      WHEN SQLCODE = ZERO                               08850000
                         ADD +1 TO PV-DEL-STRGP-CNT                     08860000
                   EXEC SQL                                             08870000
                    SET :PV-CURRENT-TMST = CURRENT TIMESTAMP            08880000
                   END-EXEC                                             08890000
                         IF PV-CURRENT-TMST > PV-COMMIT-TMST            08900000
                         PERFORM 9000-COMMIT-DATA                       08910000
                         INITIALIZE PV-COMMIT-CNT                       08920000
                         END-IF                                         08930000
                      WHEN SQLCODE = -904                               08940000
                      WHEN SQLCODE = -911                               08950000
                      WHEN SQLCODE = -913                               08960000
                        ADD +1 TO PV-RETRY-CNT                          08970000
                      WHEN SQLCODE = +100                               08980000
                         DISPLAY 'ROW NOT FOUND FOR DELETE 5200'        08990008
                         DISPLAY 'PRCHG-ID  : ' XST00064-PRCHG-ID       09000000
                      WHEN OTHER                                        09010000
                         DISPLAY 'CANT DELETE FROM XST_STR_GP_PRCHG'    09020000
                         DISPLAY 'PRCHG-ID  : ' XST00064-PRCHG-ID       09030000
                         PERFORM 9200-DISPLAY-STATISTICS                09040000
                         PERFORM 9900-SQL-ABEND-ROUTINE                 09050000
                   END-EVALUATE                                         09060000
           END-PERFORM.                                                 09070000
                                                                        09080000
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             09090000
               MOVE 'DEL TO XST_PRCHG TBL RETRY EXCEEDED'               09100000
                                      TO  PV-OPERATION-MSG-1            09110000
               PERFORM 9200-DISPLAY-STATISTICS                          09120000
               PERFORM 9900-SQL-ABEND-ROUTINE                           09130000
           END-IF.                                                      09140000
                                                                        09150000
      ***************************************************************** 09150106
      **DELETE OLD ROW(S) FROM TABLE XST_PRCHG                          09150206
      ***************************************************************** 09150306
       5300-DELETE-OLD-PRCHG-ROWS.                                      09151006
           MOVE '5300-DELETE-OLD-PRCHG-ROWS' TO PV-PARAGRAPH-NAME.      09152006
                                                                        09153006
           INITIALIZE PV-RETRY-CNT.                                     09154006
           PERFORM WITH TEST AFTER                                      09155006
             UNTIL SQLCODE = ZERO                                       09156006
                OR SQLCODE = +100                                       09157006
                OR PV-RETRY-CNT > PC-MAX-RETRIES                        09158006
                   EXEC SQL                                             09159006
                      DELETE FROM  XST_PRCHG                            09159106
                      WHERE  PRCHG_ID        = :XST00063-PRCHG-ID       09159206
                   END-EXEC                                             09159306
                                                                        09159406
                   EVALUATE TRUE                                        09159506
                      WHEN SQLCODE = ZERO                               09159606
                         ADD +1 TO PV-DEL-OLD-PRCHG-CNT                 09159706
                   EXEC SQL                                             09159806
                    SET :PV-CURRENT-TMST = CURRENT TIMESTAMP            09159906
                   END-EXEC                                             09160006
                         IF PV-CURRENT-TMST > PV-COMMIT-TMST            09160106
                         PERFORM 9000-COMMIT-DATA                       09160206
                         INITIALIZE PV-COMMIT-CNT                       09160306
                         END-IF                                         09160406
                      WHEN SQLCODE = -904                               09160506
                      WHEN SQLCODE = -911                               09160606
                      WHEN SQLCODE = -913                               09160706
                        ADD +1 TO PV-RETRY-CNT                          09160806
                      WHEN SQLCODE = +100                               09160906
                         DISPLAY 'ROW NOT FOUND FOR DELETE 5300'        09161008
                         DISPLAY 'PRCHG-ID  : ' XST00063-PRCHG-ID       09161106
                      WHEN OTHER                                        09161206
                         DISPLAY 'CANT DELETE FROM XST_PRCHG_MDSE'      09161306
                         DISPLAY 'PRCHG-ID  : ' XST00063-PRCHG-ID       09161406
                         PERFORM 9200-DISPLAY-STATISTICS                09161506
                         PERFORM 9900-SQL-ABEND-ROUTINE                 09161606
                   END-EVALUATE                                         09161706
           END-PERFORM.                                                 09161806
                                                                        09161906
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             09162006
               MOVE 'DEL TO XST_PRCHG TBL RETRY EXCEEDED'               09162106
                                      TO  PV-OPERATION-MSG-1            09162206
               PERFORM 9200-DISPLAY-STATISTICS                          09162306
               PERFORM 9900-SQL-ABEND-ROUTINE                           09162406
           END-IF.                                                      09162506
                                                                        09162606
      ******************************************************************10120000
      ****   COMMIT PARA                                                10130000
      ******************************************************************10140000
       9000-COMMIT-DATA.                                                10150000
           MOVE '9000-COMMIT-DATA           ' TO PV-PARAGRAPH-NAME.     10160000
           EXEC SQL                                                     10170000
                COMMIT                                                  10180000
           END-EXEC.                                                    10190000
                                                                        10200000
           EVALUATE TRUE                                                10210000
              WHEN SQLCODE = ZERO                                       10220000
                   ADD 1 TO PV-COMMITED-CNT                             10230000
                   PERFORM X500-SET-CHECKPOINT-TIME                     10240000
              WHEN SQLCODE = -904                                       10250000
              WHEN SQLCODE = -911                                       10260000
              WHEN SQLCODE = -913                                       10270000
                   CONTINUE                                             10280000
              WHEN OTHER                                                10290000
                   MOVE 'UNABLE TO DO A COMMIT' TO PV-OPERATION-MSG-1   10300000
                   PERFORM 9200-DISPLAY-STATISTICS                      10310000
                   PERFORM 9900-SQL-ABEND-ROUTINE                       10320000
           END-EVALUATE.                                                10330000
      *----------------------------------------------------------------*10340000
      *    SET THE TIME FOR THE NEXT CHECKPOINT.                       *10350000
      *----------------------------------------------------------------*10360000
       X500-SET-CHECKPOINT-TIME.                                        10370000
                                                                        10380000
           MOVE 'X500-SET-CHECKPOINT-TIME' TO PV-PARAGRAPH-NAME.        10390000
                                                                        10400000
           PERFORM WITH TEST AFTER                                      10410000
                   VARYING PV-RETRY-CNT FROM 1 BY 1                     10420000
                   UNTIL   PV-RETRY-CNT > PC-MAX-RETRIES OR             10430000
                           SQLCODE = ZERO                               10440000
                                                                        10450000
                   EXEC SQL                                             10460000
                    SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)10470000
                     INTO :PV-COMMIT-TMST                               10480000
                     FROM TCKRSTCNTL                                    10490000
                     WHERE PLAN_NAME = :PC-PGM-NAME                     10500000
                       AND JOB_NAME  = :PC-JOB-NAME                     10510000
                   END-EXEC                                             10520000
                                                                        10530000
                   EVALUATE TRUE                                        10540000
                       WHEN SQLCODE = ZERO                              10550000
                       WHEN SQLCODE = -904                              10560000
                       WHEN SQLCODE = -913                              10570000
                            CONTINUE                                    10580000
                      WHEN OTHER                                        10590000
                         DISPLAY 'ERROR IN UPDATING COMMIT TMST'        10600000
                         PERFORM 9200-DISPLAY-STATISTICS                10610000
                         PERFORM 9900-SQL-ABEND-ROUTINE                 10620000
                   END-EVALUATE                                         10630000
           END-PERFORM.                                                 10640000
                                                                        10650000
           IF PV-RETRY-CNT > PC-MAX-RETRIES                             10660000
               MOVE 'ERROR IN SETTING CHECKPOINT TIME'                  10670000
                                      TO  PV-OPERATION-MSG-1            10680000
               PERFORM 9200-DISPLAY-STATISTICS                          10690000
               PERFORM 9900-SQL-ABEND-ROUTINE                           10700000
           END-IF.                                                      10710000
                                                                        10720000
      *----------------------------------------------------------       10730000
      * 9200-DISPLAY-STATISTICS.                                        10740000
      *----------------------------------------------------------       10750000
       9200-DISPLAY-STATISTICS.                                         10760000
           DISPLAY '*************************************************'. 10770000
           DISPLAY '* PROGRAM STATISTICS'.                              10780000
           DISPLAY '* PROGRAM.................: ' PC-PGM-NAME.          10790000
           DISPLAY '* TOTAL PRC MD CSR FETCH  : ' PV-PRCMD-FETCH-COUNT. 10800005
           DISPLAY '* TOTAL STR GP CSR FETCH  : ' PV-STRGP-FETCH-COUNT. 10810000
           DISPLAY '* TOTAL PRCHG CSR FETCH   : ' PV-OLD-FETCH-COUNT.   10820007
           DISPLAY '* MDSE TABLE DELETES      : ' PV-DEL-MDSE-CNT.      10840000
           DISPLAY '* STR GP TABLE DELETES    : ' PV-DEL-STRGP-CNT.     10850007
           DISPLAY '* PRCHG TABLE DELETES     : ' PV-DEL-OLD-PRCHG-CNT. 10860006
           DISPLAY '* COMMIT COUNT............: ' PV-COMMITED-CNT.      10880000
           DISPLAY '*************************************************'. 10890000
                                                                        10900000
                                                                        10910000
      *----------------------------------------------------------       10920000
      * 9800-ABEND-ROUTINE.                                             10930000
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  10940000
      *----------------------------------------------------------       10950000
       9800-ABEND-ROUTINE.                                              10960000
           DISPLAY '** ABEND           **'.                             10970000
           DISPLAY '** PROGRAM         ** = ' PC-PGM-NAME.              10980000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        10990000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       11000000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       11010000
           MOVE +4000 TO PV-ABEND-CODE                                  11020000
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         11030000
                                                                        11040000
                                                                        11050000
      ******************************************************************11060000
      **SQL ABEND ROUTINE                                               11070000
      ******************************************************************11080000
       9900-SQL-ABEND-ROUTINE.                                          11090000
           DISPLAY '** ABEND     **'.                                   11100000
           DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME.                    11110000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              11120000
           DISPLAY '** OPERATION ** = ' PV-OPERATION-MSG-1.             11130000
      *    STMTS FOR USING SQLCA MSG DISPLAY MODULE "DSNTIAR"           11140000
           COPY DPPD004.                                                11150000
           MOVE 4013          TO PV-ABEND-CODE.                         11160000
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         11170000
