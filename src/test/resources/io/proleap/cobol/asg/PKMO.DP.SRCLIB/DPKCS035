000100*-----------------------*                                         03/19/93
000200 IDENTIFICATION DIVISION.                                         DPKCS035
000300*-----------------------*                                            LV013
000400 PROGRAM-ID.    DPKCS035.                                         DPKCS035
000500 AUTHOR.        CAL BUSH.                                         DPKCS035
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                          DPKCS035
000700 DATE-WRITTEN.  03-10-93.                                            CL*12
000800 DATE-COMPILED.                                                   DPKCS035
000900*----------------------------------------------------------------*DPKCS035
001000*    Z035 - SYSTEM MESSAGE CRITERIA SPECIFICATION                *   CL**2
001100*                                                                *DPKCS035
001200*    THIS PROGRAM ALLOWS THE USER ENTER DATA TO BE USED BY       *   CL**2
001300*    SUBSEQUENT PROGRAMS TO CREATE A LIST OR TO BRING UP A       *   CL**2
001400*    DETAIL SCREEN.  THE MESSAGE NUMBER, IF ENTERED, IS EDITTED  *   CL**2
001500*    TO BE A VALID NUMERIC.  ALL OTHER EDITS ARE DONE IN THE     *   CL**2
001601*    DESTINATION PROGRAMS WHEN THE 'GO-TO' TYPE FUNCTION IS USED *        
001602*    (LOCAL-FUNC-01 THRU LOCAL-FUNC-12, WITH DISPOSITION 'G').   *        
001700*----------------------------------------------------------------*DPKCS035
262023*  CHANGED 09/28/2021    BY JIM HUNTER      WR: CHG0262023       *        
262023*  CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE   *        
262023*  MAKING THE CALL DYNAMIC VS STATIC                             *        
001700*----------------------------------------------------------------*DPKCS035
001800                                                                  DPKCS035
001900                                                                  DPKCS035
002000                                                                  DPKCS035
002100 ENVIRONMENT DIVISION.                                            DPKCS035
002200                                                                  DPKCS035
002300                                                                  DPKCS035
002400                                                                  DPKCS035
002500 DATA DIVISION.                                                   DPKCS035
002600                                                                  DPKCS035
002700                                                                  DPKCS035
002800 WORKING-STORAGE SECTION.                                         DPKCS035
002900*                                                                 DPKCS035
003000 01  PC-PROGRAM-CONSTANTS.                                        DPKCS035
003100     05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'DP035A  '.    CL**7
003200     05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'DPKM035 '.    CL**7
003300     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'DPKCS035'.    CL**7
003400     05  PC-TSYMSG-NUMBERS.                                       DPKCS035
003500         10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.         CL**8
003600         10  PC-TSYMSG-00095       PIC  9(05)  VALUE  00095.         CL**7
003700         10  PC-TSYMSG-00224       PIC  9(05)  VALUE  00224.         CL**7
003800*                                                                 DPKCS035
003900 01  PS-PROGRAM-SWITCHES.                                         DPKCS035
004000     05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.           CL**7
004100         88  PS-ERROR                          VALUE  'Y'.           CL**7
004200         88  PS-NO-ERROR                       VALUE  'N'.           CL**7
004300                                                                     CL**7
004301 01  PM-PROGRAM-MESSAGES.                                                 
004303     05  PM-ABEND-INFO-A.                                                 
004304         10  FILLER                PIC X(15)   VALUE                      
004305                                   'DP020-SRC-AID: '.                     
004306         10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.              
004307         10  FILLER                PIC X(19)   VALUE                      
004308                                   '. DP020-FK-ACTION: '.                 
004309         10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.              
004310         10  FILLER                PIC X(22)   VALUE                      
004311                                   '. DP020-FK-DISPN-IND: '.              
004312         10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.              
004313         10  FILLER                PIC X(01)   VALUE '.'.                 
004314         10  FILLER                PIC X(19)   VALUE SPACE.               
004315                                                                          
004320                                                                          
004400*----------------------------------------------------------------*   CL**7
004500*    MAP LAYOUT                                                  *   CL**7
004600*----------------------------------------------------------------*   CL**7
004700                                                                     CL**7
004800     COPY DPKM035.                                                   CL**7
004900                                                                  DPKCS035
005000*----------------------------------------------------------------*DPKCS035
005100*    ATTRIBUTE SETTINGS COPYBOOK.                                *DPKCS035
005200*----------------------------------------------------------------*DPKCS035
005300                                                                  DPKCS035
005400     COPY DPWS015.                                                DPKCS035
005500                                                                  DPKCS035
005600*----------------------------------------------------------------*DPKCS035
005700*    FUNCTION KEYS COPYBOOK                                      *DPKCS035
005800*----------------------------------------------------------------*DPKCS035
005900                                                                  DPKCS035
006000     COPY DPWS016.                                                DPKCS035
006100                                                                     CL**7
006200*----------------------------------------------------------------*   CL**7
006300** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **   CL**7
006400** SUB-ROUTINE (DPKUT100).                                      **   CL**7
006500*----------------------------------------------------------------*   CL**7
006600     COPY DPWS010I.                                                  CL**7
006700                                                                     CL**7
      *----------------------------------------------------------------*        
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.           *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
007300                                                                     CL**7
007400*****************************************************************    CL**7
007500*    STANDARD COMMAREA.                                              CL**7
007600*****************************************************************    CL**7
007700                                                                     CL**7
007800     COPY DPWS020.                                                   CL**7
007900     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                   CL**7
008000                                                                     CL**7
008100*****************************************************************    CL**7
008200*    INTER-APPLICATION COMMAREA.                                     CL**7
008300*****************************************************************    CL**7
008400                                                                     CL**7
008500         10  INTER-APPL-COMM-AREA PIC X(200).                        CL**7
008600     COPY DPWS054.                                                   CL*13
008700*****************************************************************    CL**7
008800*    SPECIFIC COMMMAREA FOR DPKCS035.                                CL**7
008900*****************************************************************    CL**7
009000         10  ASC-SPECIFIC-COMMAREA.                                  CL**7
009100             15  ASC-MSG-NUMBER.                                     CL**7
009200                 20  ASC-MSG-NUMBER-N        PIC  9(05).             CL**7
009300             15  ASC-SYSTEM-ID               PIC  X(02).             CL**7
009400             15  ASC-MSG-TEXT                PIC  X(30).             CL**7
009500             15  ASC-REVIEW-IND              PIC  X(01).             CL**7
009600         10  FILLER                          PIC  X(2827).           CL**7
009700     EJECT                                                           CL**7
009800*----------------------------------------------------------------*   CL**7
009900*    ABEND PROCESSING COPYBOOK.                                  *   CL**7
010000*----------------------------------------------------------------*   CL**7
010100                                                                     CL**7
010200     COPY DPWS013.                                                   CL**7
010300                                                                     CL**7
010400                                                                  DPKCS035
010500 EJECT                                                            DPKCS035
010600 LINKAGE SECTION.                                                 DPKCS035
010700                                                                  DPKCS035
010800 01  DFHCOMMAREA.                                                 DPKCS035
010900     05  FILLER                         OCCURS  1 TO 4072 TIMES   DPKCS035
011000                                        DEPENDING ON EIBCALEN.    DPKCS035
011100         10  FILLER                     PIC X.                    DPKCS035
011200                                                                  DPKCS035
011300                                                                  DPKCS035
011400 PROCEDURE DIVISION.                                              DPKCS035
011500***************************************************************** DPKCS035
011600** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** DPKCS035
011700** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** DPKCS035
011800** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** DPKCS035
011900**                                                             ** DPKCS035
012000** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** DPKCS035
012100** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** DPKCS035
012200***************************************************************** DPKCS035
012300 0000-MAIN-MODULE.                                                DPKCS035
012400     INITIALIZE DP030-CICS-API-FIELDS.                            DPKCS035
012500     MOVE +1                       TO DP030-NUMBER-OF-MAPS.          CL*11
012600     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.             CL*11
012700     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).            CL*11
012800     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                          CL*11
012900     MOVE LENGTH OF DP035AI        TO DP030-MAP-LENGTH (1).          CL*11
013000     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).        CL*11
013100     PERFORM 0001-CALL-CICS-ARCH-API.                             DPKCS035
013200*                                                                 DPKCS035
013300     PERFORM 1000-CONTROL-PROCESSING                              DPKCS035
013400*                                                                 DPKCS035
013500     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).        CL*11
013600     PERFORM 0001-CALL-CICS-ARCH-API.                             DPKCS035
013700                                                                  DPKCS035
013800 0001-CALL-CICS-ARCH-API.                                         DPKCS035
013900     CALL DP930-CICS-ARCH-API USING DFHEIBLK                      DPKCS035
014000                                    DFHCOMMAREA                   DPKCS035
014100                                    DP030-CICS-API-FIELDS         DPKCS035
014200                                    DP020-STANDARD-COMMAREA       DPKCS035
014300                                    DP035AI.                      DPKCS035
014400*                                                                 DPKCS035
014500     IF  DP030-RC-CALL-SUCCESSFUL                                 DPKCS035
014600         CONTINUE                                                 DPKCS035
014700     ELSE                                                         DPKCS035
014800         SET DP013-NO-ROLLBACK                                       CL*11
014900             DP013-XCTL-DISPLAY-RESTART                              CL*11
015000             DP013-CICS-ABEND      TO TRUE                           CL*11
015100         MOVE 'BEFORE 0000-MAIN-MODULE'                              CL*11
015200                                   TO DP013-PARAGRAPH                CL*11
015300         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
015400-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
015500         MOVE DP030-RETURN-CODE                                   DPKCS035
015600                                   TO DP013-MESSAGE-TEXT (3)         CL*11
015700         PERFORM DP013-0000-PROCESS-ABEND                         DPKCS035
015800     END-IF.                                                      DPKCS035
015900 EJECT                                                            DPKCS035
016000*----------------------------------------------------------------*DPKCS035
016100*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *DPKCS035
016200*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *DPKCS035
016300*----------------------------------------------------------------*DPKCS035
016400                                                                  DPKCS035
016500 1000-CONTROL-PROCESSING.                                         DPKCS035
016600                                                                  DPKCS035
016700     EVALUATE TRUE                                                DPKCS035
016800         WHEN DP020-NEXT-ACT-INITIAL                              DPKCS035
016900             INITIALIZE ASC-SPECIFIC-COMMAREA                     DPKCS035
017000             PERFORM 4000-BUILD-INITIAL-PANEL                        CL**7
017100                                                                  DPKCS035
017200         WHEN DP020-NEXT-ACT-READ-MAP                             DPKCS035
017300             PERFORM 2000-FKEY-PROCESSING                         DPKCS035
017400                                                                  DPKCS035
017500         WHEN DP020-NEXT-ACT-RETURN                               DPKCS035
017600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**9
017700             PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**7
017800                                                                  DPKCS035
017900         WHEN OTHER                                               DPKCS035
018000             SET DP013-LOGIC-ABEND                                   CL*11
018100                 DP013-NO-ROLLBACK TO TRUE                           CL*11
018200             MOVE '1000-CONTROL-PROCESSING'                       DPKCS035
018300                                   TO  DP013-PARAGRAPH               CL*11
018400             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   DPKCS035
018500                                   TO  DP013-MESSAGE-TEXT(1)         CL*11
018600             MOVE DP020-NEXT-APPL-ACTIVITY                        DPKCS035
018700                                   TO DP013-MESSAGE-TEXT(2)          CL*11
018800             PERFORM DP013-0000-PROCESS-ABEND                     DPKCS035
018900     END-EVALUATE.                                                DPKCS035
019000                                                                  DPKCS035
019100*----------------------------------------------------------------*DPKCS035
019200* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*   CL**7
019300* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*   CL**7
019400* EDITS.                                                         *   CL**7
019500*----------------------------------------------------------------*DPKCS035
019600                                                                  DPKCS035
019700 2000-FKEY-PROCESSING.                                            DPKCS035
019800                                                                  DPKCS035
019900     EVALUATE TRUE                                                DPKCS035
020000                                                                  DPKCS035
020100         WHEN DP020-SRC-AID = DP016-CLEAR                         DPKCS035
020200             PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**7
020300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**9
020400                                                                  DPKCS035
020500         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     DPKCS035
020600             INITIALIZE ASC-SPECIFIC-COMMAREA                     DPKCS035
020700             PERFORM 4000-BUILD-INITIAL-PANEL                        CL**7
020800             PERFORM 3200-RESET-ATTRIBUTES                           CL*11
020900                                                                  DPKCS035
021000         WHEN OTHER                                                  CL**7
021100             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                    CL**7
021200             PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**7
021300             IF  PS-NO-ERROR                                         CL**7
021400                 SET DP030-CONTINUE-GO TO TRUE                       CL*11
021500                 PERFORM 2100-CHECK-FUNCTION-KEY                     CL**7
021600             ELSE                                                    CL*11
021700                 SET DP030-OVERRIDE-GO TO TRUE                       CL*11
021800             END-IF                                                  CL*11
021900     END-EVALUATE.                                                DPKCS035
022000******************************************************************   CL**7
022100** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **   CL**7
022200** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **   CL**7
022300** FROM THE CICS ARCHITECTURE API.                              **   CL**7
022400******************************************************************   CL**7
022500 2100-CHECK-FUNCTION-KEY.                                            CL**7
022600     EVALUATE TRUE                                                   CL**7
022700*        WHEN DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                    CL**7
022710         WHEN DP020-FK-DISPN-GO (DP020-SRC-AID)                           
022800             PERFORM 2110-PREPARE-INTER-APPL-COMM                    CL*11
022900                                                                     CL*12
023000         WHEN DP020-SRC-AID = DP016-ENTER                            CL*12
023100             CONTINUE                                                CL*12
023200                                                                     CL**7
023300         WHEN OTHER                                                  CL**7
023400             SET DP013-NO-ROLLBACK                                   CL*11
023500                 DP013-XCTL-DISPLAY-RESTART                          CL*11
023600                 DP013-CICS-ABEND  TO TRUE                           CL*11
023700             MOVE '2100-CHECK-FUNCTION-KEY'                          CL**7
023800                                   TO DP013-PARAGRAPH                CL*11
023900             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'         CL**7
024000                                   TO DP013-MESSAGE-TEXT (1)         CL*11
024010             MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:'         
024020                                   TO DP013-MESSAGE-TEXT (2)              
024021             MOVE DP020-SRC-AID    TO PM-A-SRC-AID                        
024022             MOVE DP020-FK-ACTION (DP020-SRC-AID)                         
024023                                   TO PM-A-FK-ACTION                      
024024             MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)                      
024025                                   TO PM-A-FK-DISPN                       
024030             MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)              
024070             MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'                
024080                                   TO DP013-MESSAGE-TEXT (4)              
024100             PERFORM DP013-0000-PROCESS-ABEND                        CL**7
024200     END-EVALUATE.                                                   CL**7
024300******************************************************************   CL**9
024400** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **   CL**9
024500** INTER-APPLICATION COMMAREA.                                  **   CL**9
024600******************************************************************   CL**9
024700 2110-PREPARE-INTER-APPL-COMM.                                       CL**9
024800     INITIALIZE DP054-INTER-APPL-COMMAREA.                           CL**9
024900     MOVE ASC-MSG-NUMBER-N         TO DP054-MESSAGE-NUMBER.          CL*11
025000     MOVE ASC-SYSTEM-ID            TO DP054-SYSTEM-ID.               CL*11
025100     MOVE ASC-MSG-TEXT             TO DP054-MESSAGE-TEXT.            CL*11
025200     MOVE ASC-REVIEW-IND           TO DP054-REVIEW-IND.              CL*11
025300 EJECT                                                            DPKCS035
025400*----------------------------------------------------------------*DPKCS035
025500* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*   CL**7
025600* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *   CL**7
025700* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *   CL**7
025800*----------------------------------------------------------------*DPKCS035
025900                                                                  DPKCS035
026000 2200-MOVE-SCREEN-TO-COMMAREA.                                       CL**7
026100     IF  AMSGNUML > ZERO                                             CL**2
026200         MOVE AMSGNUMI             TO ASC-MSG-NUMBER                 CL*11
026300     ELSE                                                         DPKCS035
026400         IF  AMSGNUMF = DP015-ERASE-EOF                              CL**2
026500             MOVE SPACE            TO ASC-MSG-NUMBER                 CL*11
026600         END-IF                                                   DPKCS035
026700     END-IF.                                                      DPKCS035
026800                                                                  DPKCS035
026900     IF  ASYSIDL > ZERO                                              CL**2
027000         MOVE ASYSIDI              TO ASC-SYSTEM-ID                  CL*11
027100     ELSE                                                            CL**2
027200         IF  ASYSIDF = DP015-ERASE-EOF                               CL**2
027300             MOVE SPACE            TO ASC-SYSTEM-ID                  CL*11
027400         END-IF                                                      CL**2
027500     END-IF.                                                         CL**2
027600                                                                     CL**2
027700     IF  ASHTTXTL > ZERO                                             CL**2
027800         MOVE ASHTTXTI             TO ASC-MSG-TEXT                   CL*11
027900     ELSE                                                         DPKCS035
028000         IF  ASHTTXTF = DP015-ERASE-EOF                              CL**2
028100             MOVE SPACE            TO ASC-MSG-TEXT                   CL*11
028200         END-IF                                                   DPKCS035
028300     END-IF.                                                      DPKCS035
028400                                                                  DPKCS035
028500     IF  ARVWINDL > ZERO                                             CL**2
028600         MOVE ARVWINDI             TO ASC-REVIEW-IND                 CL*11
028700     ELSE                                                            CL**2
028800         IF  ARVWINDF = DP015-ERASE-EOF                              CL**2
028900             MOVE SPACE            TO ASC-REVIEW-IND                 CL*11
029000         END-IF                                                      CL**2
029100     END-IF.                                                         CL**2
029200*----------------------------------------------------------------*DPKCS035
029300* ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *   CL**7
029400* CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *   CL**7
029500* A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*   CL**7
029600* IN THE DESTINATION PROGRAM, NOT HERE.                          *   CL**7
029700*----------------------------------------------------------------*   CL**7
029800                                                                  DPKCS035
029900 3000-EDIT-DATA-IN-COMMAREA.                                         CL**7
030000     PERFORM 3200-RESET-ATTRIBUTES.                                  CL*11
030100     IF  ASC-MSG-NUMBER > SPACE                                      CL**2
030200         MOVE ASC-MSG-NUMBER       TO DP010I-UNEDITED-FIELD          CL*11
030300         MOVE LENGTH OF ASC-MSG-NUMBER TO DP010I-MAXIMUM-DIGITS      CL**3
030400         MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS        CL*11
030500         SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                     CL**2
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
030700         IF  DP010I-ERROR-DETECTED                                   CL**3
030800             SET PS-ERROR                                            CL*11
030900                 DP020-MSG-FATAL   TO TRUE                           CL*11
031000             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER               CL*11
031100             MOVE DP015-UNP-ALP-BRT-OFF                              CL**2
031200                                   TO AMSGNUMA                       CL*11
031300             MOVE DP015-RED        TO AMSGNUMC                       CL*11
031400             MOVE DP015-REVERSE    TO AMSGNUMH                       CL*11
031500             MOVE -1               TO AMSGNUML                       CL*11
031600             SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL**2
031700         ELSE                                                        CL**2
031800             MOVE DP010I-NUMERIC-FIELD                               CL*11
031900                                   TO ASC-MSG-NUMBER-N               CL*11
032000                                      AMSGNUMO                       CL*11
032100         END-IF                                                      CL**2
032200     END-IF.                                                      DPKCS035
032300                                                                  DPKCS035
032400*                                                                 DPKCS035
032500     IF  PS-NO-ERROR                                              DPKCS035
032600         MOVE -1                   TO AMSGNUML                       CL*11
032700         SET DP030-SET-CURSOR-APPL-1                                 CL*11
032800                                   TO TRUE                           CL*11
032900     END-IF.                                                      DPKCS035
033000******************************************************************   CL**7
033100** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **   CL**7
033200** MAP.                                                         **   CL**7
033300******************************************************************   CL**7
033400 3200-RESET-ATTRIBUTES.                                              CL*11
033500     MOVE DP015-UNP-ALP-NOR-OFF    TO AMSGNUMA                       CL*11
033600                                      ASYSIDA                        CL*11
033700                                      ASHTTXTA                       CL*11
033800                                      ARVWINDA.                      CL*11
033900     MOVE DP015-GREEN              TO AMSGNUMC                       CL*11
034000                                      ASYSIDC                        CL*11
034100                                      ASHTTXTC                       CL*11
034200                                      ARVWINDC.                      CL*11
034300     MOVE DP015-UNDERLINE          TO AMSGNUMH                       CL*11
034400                                      ASYSIDH                        CL*11
034500                                      ASHTTXTH                       CL*11
034600                                      ARVWINDH.                      CL*11
034700*                                                                    CL**9
034800*----------------------------------------------------------------*DPKCS035
034900*  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *DPKCS035
035000*  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *DPKCS035
035100*  IT HERE.                                                      *DPKCS035
035200*----------------------------------------------------------------*DPKCS035
035300 4000-BUILD-INITIAL-PANEL.                                           CL**7
035400     SET DP030-SET-CURSOR-APPL-1   TO TRUE                           CL*11
035500     MOVE -1                       TO AMSGNUML.                      CL*11
035600*                                                                    CL**9
035700*----------------------------------------------------------------*   CL**9
035800*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *   CL**9
035900*----------------------------------------------------------------*   CL**9
036000 4400-MOVE-COMMAREA-TO-SCREEN.                                       CL**9
036100     MOVE ASC-MSG-NUMBER-N         TO AMSGNUMO.                      CL*11
036200     MOVE ASC-SYSTEM-ID            TO ASYSIDO.                       CL*11
036300     MOVE ASC-MSG-TEXT             TO ASHTTXTO.                      CL*11
036400     MOVE ASC-REVIEW-IND           TO ARVWINDO.                      CL*11
036500 EJECT                                                            DPKCS035
036600*----------------------------------------------------------------*DPKCS035
036700*    ABEND PROCESSOR MODULE                                      *DPKCS035
036800*----------------------------------------------------------------*DPKCS035
036900                                                                  DPKCS035
037000     COPY DPPD013.                                                DPKCS035
