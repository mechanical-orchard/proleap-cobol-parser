000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    RVKUT004.                                                 
000600 AUTHOR.        MICHAEL WALTERS.                                          
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   AUGUST, 1995.                                             
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES THE RETURN EXTRACT FILE FOR THE QUARTERLY         
001300*  VENDOR RETURN AGREEMENT REPORT.                                        
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900     SELECT RTV-REPORT-EXTRACT                                            
003000         ASSIGN TO OUT01.                                                 
003100                                                                          
003200     SELECT JCL-DATE-CARD                                                 
003300         ASSIGN TO OUT02.                                                 
003400                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
004100 FD  RTV-REPORT-EXTRACT                                                   
004200     RECORDING MODE IS F                                                  
004300     LABEL RECORDS ARE STANDARD                                           
004400     BLOCK CONTAINS 0 RECORDS.                                            
004500                                                                          
004600 01  RTV-REPORT-EXTRACT-RECORD       PIC  X(200).                         
004700*                                                                         
004800 FD  JCL-DATE-CARD                                                        
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005200                                                                          
005300 01  JCL-DATE-CARD-RECORD            PIC  X(080).                         
005400*                                                                         
005500******************************************************************        
005600 WORKING-STORAGE SECTION.                                                 
005700******************************************************************        
005800*                                                                         
005900******************************************************************        
006000*  DB2 FORMATTED MESSAGE AREA                                             
006100******************************************************************        
006200     COPY DPWS004.                                                        
006300*                                                                         
006400******************************************************************        
006500*  RTV REPORT EXTRACT RECORD LAYOUT                                       
006600******************************************************************        
006700     COPY RVWS010.                                                        
006800*                                                                         
006900******************************************************************        
007000*  WORK AREA FOR FISCAL CALENDAR ROUTINE.                                 
007100******************************************************************        
007200     COPY DPWS500.                                                        
007300*                                                                         
007310******************************************************************00731038
007320*  WORK AREA FOR THE LOGISTICS DOMAIN MODULE (XLKUT001)          *00732049
007330******************************************************************00733038
007340     COPY XLWS001.                                                      38
007350*                                                                         
007400******************************************************************        
007500*  COMMUNICATIONS AREA FOR DB2                                            
007600******************************************************************        
007700     EXEC SQL                                                             
007800          INCLUDE SQLCA                                                   
007900     END-EXEC.                                                            
008000*                                                                         
008100******************************************************************        
008200*  DB2 TABLE DECLARATION - DEFECTIVE ITEM HEADER TABLE (TDFCTOR)          
008300******************************************************************        
008400     EXEC SQL                                                             
008500          INCLUDE TDFCTOR                                                 
008600     END-EXEC.                                                            
008700*                                                                         
008800******************************************************************        
008900*  DB2 TABLE DECLARATION - DEFECTIVE ITEM DETAIL TABLE (TDFMEIT)          
009000******************************************************************        
009100     EXEC SQL                                                             
009200          INCLUDE TDFMEIT                                                 
009300     END-EXEC.                                                            
009400*                                                                         
010200******************************************************************        
010300*  DB2 TABLE DECLARATION - VENDOR/DEPT ASSOCIATION TABLE (TVNDDPT)        
010400******************************************************************        
010500     EXEC SQL                                                             
010600          INCLUDE TVNDDPT                                                 
010700     END-EXEC.                                                            
010800*                                                                         
010900******************************************************************        
011000*  DB2 TABLE DECLARATION - EXTERNAL ENTERPRISE TABLE (TEXTENT)            
011100******************************************************************        
011200     EXEC SQL                                                             
011300          INCLUDE TEXTENT                                                 
011400     END-EXEC.                                                            
011500*                                                                         
011600******************************************************************        
011700*  DB2 TABLE DECLARATION - ENTERPRISE NAME TABLE (TENTNME)                
011800******************************************************************        
011900     EXEC SQL                                                             
012000          INCLUDE TENTNME                                                 
012100     END-EXEC.                                                            
012200                                                                          
012300 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
012400                                                   COMP SYNC.             
012500 01  JCL-DATE-CARD-WORKING-STORAGE.                                       
012600     05  JCL-DATE-CARD-INCL-LINE.                                         
012700         10 JCL-INCL-BEGIN-DATE          PIC X(06)  VALUE SPACES.         
012800         10 JCL-INCL-END-DATE            PIC X(06)  VALUE SPACES.         
012900         10 FILLER                       PIC X(63)  VALUE SPACES.         
013000*                                                                         
013100 01  PS-PROGRAM-SWITCHES.                                                 
013200     05  PS-EOC-DRIVER-CURSOR-SW     PIC  X(01)    VALUE 'N'.             
013300         88  PS-EOC-DRIVER-CURSOR                  VALUE 'Y'.             
013400         88  PS-NOT-EOC-DRIVER-CURSOR              VALUE 'N'.             
013410     05  PS-DEPT-NBR-FOUND-SW        PIC  X(01)    VALUE 'N'.             
013420         88  PS-DEPT-NBR-FOUND                     VALUE 'Y'.             
013430         88  PS-DEPT-NBR-NOT-FOUND                 VALUE 'N'.             
013500*                                                                         
013600 01  PV-PROGRAM-VARIABLES.                                                
013700     05  PV-BEGIN-DATE                   PIC  X(10)  VALUE SPACE.         
013800     05  PV-BEGIN-FISCAL-DATE.                                            
013900         10  PV-BEGIN-FISCAL-YEAR        PIC  9(02)  VALUE 0.             
014000         10  PV-BEGIN-FISCAL-MONTH       PIC  9(02)  VALUE 0.             
014100     05  PV-BEGIN-CC-FISCAL-DATE.                                         
014200         10  PV-BEGIN-CC-FISCAL-YEAR     PIC  9(04)  VALUE 0.             
014300         10  PV-BEGIN-CC-FISCAL-MONTH    PIC  9(02)  VALUE 0.             
014400     05  PV-END-DATE                     PIC  X(10)  VALUE SPACE.         
014500     05  PV-END-FISCAL-DATE.                                              
014600         10  PV-END-FISCAL-YEAR          PIC  9(02)  VALUE 0.             
014700         10  PV-END-FISCAL-PERIOD        PIC  9(02)  VALUE 0.             
014800     05  PV-END-CC-FISCAL-DATE.                                           
014900         10  PV-END-CC-FISCAL-YEAR       PIC  9(04)  VALUE 0.             
015000         10  PV-END-CC-FISCAL-PERIOD     PIC  9(02)  VALUE 0.             
015100                                                                          
015200     05  PV-CURRENT-DATE                 PIC  X(10).                      
015300     05  PV-CURRENT-DATE-RFMT REDEFINES PV-CURRENT-DATE.                  
015400         10 PV-CURRENT-DATE-CC           PIC  X(2).                       
015500         10 PV-CURRENT-DATE-YY           PIC  X(2).                       
015600         10 FILLER                       PIC  X.                          
015700         10 PV-CURRENT-DATE-MM           PIC  X(2).                       
015800         10 FILLER                       PIC  X.                          
015900         10 PV-CURRENT-DATE-DD           PIC  X(2).                       
016000     05  PV-HOLD-DPT-NBR                 PIC  X(10).                      
016100     05  PV-HOLD-ITEM-NBR                PIC S9(15)  COMP-3.              
016200     05  PV-PREV-DAYS                    PIC  9(03).                      
016300     05  PV-ERROR-SQL-CODE               PIC  ZZZZ9-.                     
016700                                                                          
016800 01  CC-CONTROL-CARD.                                                     
016900     05  CC-BEGIN-DATE                   PIC  X(10)  VALUE SPACE.         
017000         88  CC-DEFAULT-BEGIN-DATE       VALUE 'DEFAULT   '.              
017100     05  CC-END-DATE                     PIC  X(10)  VALUE SPACE.         
017200         88  CC-DEFAULT-END-DATE         VALUE 'DEFAULT   '.              
017300     05  CC-RPT-TYPE                     PIC  X(01)  VALUE SPACE.         
017400         88  CC-VALID-RPT-TYPE           VALUE 'Q', 'R'.                  
017500         88  CC-REQUEST-REPORT           VALUE 'R'.                       
017600         88  CC-QUARTERLY-REPLORT        VALUE 'Q'.                       
017700*                                                                         
017800*  CURSOR DECLARATION FOR DEFECTIVE ITEMS THAT WERE "SHIPPED" BETW        
017900*  THE DATES ENTERED BY THE CONTROL CARD                                  
018000*                                                                         
018100     EXEC SQL                                                             
018200          DECLARE DITEM_CURSOR CURSOR FOR                                 
018300           SELECT                                                         
018400                  B.MDSE_DEPT_NBR                                         
018500                , B.DFCT_ITEM_NBR                                         
018600                , A.OPER_UNT_ID                                           
018700                , A.DFCT_ORD_ID_CDE                                       
018700                , A.DFCT_ORD_STAT_CDE                                     
018800                , B.RTL_COST_AMT                                          
018900                , B.UNIT_COST_AMT                                         
019000                , A.STATUS_CHG_DTE                                        
019100             FROM                                                         
019200                  TDFCTOR   A                                             
019300                , TDFMEIT   B                                             
019400             WHERE                                                        
019500                  A.DFCT_ORD_STAT_CDE IN ('S','A','D')                    
019600             AND  A.STATUS_CHG_DTE BETWEEN :PV-BEGIN-DATE                 
019700                                       AND :PV-END-DATE                   
019800             AND  A.DFCT_ORD_ID    = B.DFCT_ORD_ID                        
019900             AND  B.DFCT_ITEM_NBR > 0                                     
020000             ORDER BY B.DFCT_ITEM_NBR                                     
020100                    , B.MDSE_DEPT_NBR                                     
020200     END-EXEC.                                                            
020300*                                                                         
020400******************************************************************        
020500 LINKAGE SECTION.                                                         
020600******************************************************************        
020700                                                                          
020800******************************************************************        
020900 PROCEDURE DIVISION.                                                      
021000******************************************************************        
021100                                                                          
021200******************************************************************        
021300*  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM            
021400******************************************************************        
021500 0000-PROGRAM-MAINLINE.                                                   
021600     PERFORM 1000-INITIALIZE.                                             
021700                                                                          
021800     PERFORM 2000-CREATE-EXTRACT-FILE                                     
021900         UNTIL PS-EOC-DRIVER-CURSOR                                       
022000                                                                          
022100     PERFORM 7100-CLOSE-DRIVER-CURSOR.                                    
022200                                                                          
022300     PERFORM 9000-EOJ-ROUTINE.                                            
022400     STOP RUN.                                                            
022500                                                                          
022600******************************************************************        
022700*  1000-INITIALIZE -  OPENS ALL THE REPORT FILE, ACCEPTS, EDITS,          
022800*  AND DISPLAYS CONTROL CARD INFORMATION, OPENS AND FETCHES FIRST         
022900*  OCCURANCE OF DB2 CURSOR, PRINTS INITIAL PAGE HEADING TO REPORT         
023000*  FILE.                                                                  
023100******************************************************************        
023200 1000-INITIALIZE.                                                         
023300     INITIALIZE RV010-EXTRACT-RECORD.                                     
023400                                                                          
023500     OPEN OUTPUT RTV-REPORT-EXTRACT                                       
023600          OUTPUT JCL-DATE-CARD.                                           
023700                                                                          
023800     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
023900     DISPLAY 'CONTROL CARD = ' CC-CONTROL-CARD.                           
024000                                                                          
024100     EXEC SQL                                                             
024200         SET :PV-CURRENT-DATE = CURRENT DATE                              
024300     END-EXEC                                                             
024400                                                                          
024500     IF SQLCODE = 0                                                       
024600         CONTINUE                                                         
024700     ELSE                                                                 
024800         DISPLAY '** ABEND **'                                            
024900         DISPLAY '** PROGRAM = RVKUT004 **'                               
025000         DISPLAY '** PARAGRAPH = 1000-INITIALIZE **'                      
025100         DISPLAY '** ERROR SETTING CURRENT DATE **'                       
025200         PERFORM 9100-SQL-ABEND                                           
025300     END-IF.                                                              
025400                                                                          
025500     PERFORM 1100-VALIDATE-CONTROL-CARD.                                  
025600                                                                          
025700     DISPLAY 'DATES: ' PV-BEGIN-DATE ' ' PV-END-DATE.                     
025800     SET PS-NOT-EOC-DRIVER-CURSOR    TO TRUE.                             
025900                                                                          
026000     PERFORM 7000-OPEN-DRIVER-CURSOR.                                     
026100                                                                          
026200     PERFORM 7200-FETCH-DRIVER-CURSOR.                                    
026300                                                                          
026400     MOVE DFMEIT-DFCT-ITEM-NBR       TO PV-HOLD-ITEM-NBR.                 
026500     PERFORM 7300-SELECT-DEPT-NBR.                                        
026520     IF PS-DEPT-NBR-NOT-FOUND                                             
026570         MOVE 'UNKNOWN VENDOR' TO ENTNME-ENT-NAME-DESC                    
026580         MOVE '999999999'  TO EXTENT-DUN-NBR                              
026591     ELSE                                                                 
026592         PERFORM 7310-SELECT-VENDOR-INFORMATION                           
026593     END-IF.                                                              
026600                                                                          
026700******************************************************************        
026800* 1100-VALIDATE-CONTROL-CARD - VERIFIES THAT THE DATE RANGE SUP-          
026900* PLIED IS NUMERIC AND WITHIN VALID VALUES.                               
027000******************************************************************        
027100 1100-VALIDATE-CONTROL-CARD.                                              
027200     IF CC-VALID-RPT-TYPE                                                 
027300         CONTINUE                                                         
027400     ELSE                                                                 
027500        DISPLAY '** ABEND **'                                             
027600        DISPLAY '** PROGRAM = RVKUT004 ** '                               
027700        DISPLAY                                                           
027800        '** PARAGRAPH = 1100-VALIDATE-CONTROL-CARD **'                    
027900        DISPLAY '** INVALID RPT TYPE: ' CC-RPT-TYPE                       
028000        MOVE +4003 TO ABEND-CODE                                          
028100        CALL 'ILBOABN0' USING ABEND-CODE                                  
028200     END-IF.                                                              
028300                                                                          
028400     IF CC-DEFAULT-BEGIN-DATE AND                                         
028500        CC-DEFAULT-END-DATE                                               
028600         PERFORM 1110-GET-DEFAULT-DATES                                   
028700     ELSE                                                                 
028800* TWO CALLS TO THE CALENDAR ROUTINE ARE NEEDED.  BECAUSE WE ARE           
028900* USING THE VENDOR MASTER FILE, THE BEGIN AND END DATES MUST BE           
029000* FISCAL MONTH BEGIN AND END DATES.                                       
029100*                                                                         
029200* THE FIRST CALL WILL VERIFY THAT BOTH ENTERED DATES ARE VALID,           
029300* AND THAT THE BEGIN DATE COMES BEFORE THE END DATE.  THE BEGIN           
029400* DATE WILL THEN BE REPLACED WITH THE FIRST DAY OF THE FISCAL             
029500* MONTH.                                                                  
029600         INITIALIZE  DPG51 DPG52 DPG53                                    
029700                     DPG54 DPG55 DPG56                                    
029800                                                                          
029900         SET DPG51-FISCAL-454-CALENDAR                                    
030000                                     TO TRUE                              
030100         SET DPG52-LK-DTE-ISO-FMT    TO TRUE                              
030200                                                                          
030300         MOVE CC-BEGIN-DATE          TO DPG52-LK-DATE-INPUT               
030400         MOVE CC-END-DATE            TO DPG53-LK-CMPR-DATE-INPUT          
030500         SET DPG53-LK-CMPR-DTE-ISO-FMT TO TRUE                            
030600                                                                          
030700         CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53        
030800                                                 DPG54 DPG55 DPG56        
030900                                                                          
031000         EVALUATE TRUE                                                    
031100             WHEN DPG54-NO-ERROR                                          
031200                 MOVE DPG56-FIRST-FP-DB2-ISO-FMT                          
031300                                     TO PV-BEGIN-DATE                     
031400                 MOVE DPG56-FIRST-FD-DB2-ISO-FMT-YR                       
031500                                     TO PV-BEGIN-CC-FISCAL-YEAR           
031600                 MOVE DPG56-FISCAL-YEAR-NBR                               
031700                                     TO PV-BEGIN-FISCAL-YEAR              
031800                 MOVE DPG56-FISCAL-PERIOD-NBR                             
031900                                     TO PV-BEGIN-FISCAL-MONTH             
032000                                        PV-BEGIN-CC-FISCAL-MONTH          
032100             WHEN DPG54-DATE-INVALID                                      
032200                 DISPLAY '** ABEND **'                                    
032300                 DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '        
032400                         'DP500-KOHLS-CALENDAR-ROUTINE ***'               
032500                 DISPLAY '** PARAGRAPH = 1100-VALIDATE-DATES. **'         
032600                 DISPLAY '** INVALID START DATE: ' CC-BEGIN-DATE          
032700                 DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '        
032800                 DISPLAY DPG54-ERROR-MESSAGE                              
032900                 MOVE +4003 TO ABEND-CODE                                 
033000                 CALL 'ILBOABN0' USING ABEND-CODE                         
033100             WHEN DPG54-CMPR-DATE-INVALID                                 
033200                 DISPLAY '** ABEND **'                                    
033300                 DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '        
033400                         'DP500-KOHLS-CALENDAR-ROUTINE ***'               
033500                 DISPLAY '** PARAGRAPH = 1100-VALIDATE-DATES. **'         
033600                 DISPLAY '** INVALID END DATE: ' CC-END-DATE              
033700                 DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '        
033800                 DISPLAY DPG54-ERROR-MESSAGE                              
033900                 MOVE +4003 TO ABEND-CODE                                 
034000                 CALL 'ILBOABN0' USING ABEND-CODE                         
034100             WHEN DPG55-DAYS-DIFFERENCE > ZERO                            
034200                 DISPLAY '** ABEND **'                                    
034300                 DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '        
034400                         'DP500-KOHLS-CALENDAR-ROUTINE ***'               
034500                 DISPLAY '** PARAGRAPH = 1100-VALIDATE-DATES. **'         
034600                 DISPLAY '** END DATE IS NOT AFTER START DATE **'         
034700                 DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '        
034800                 DISPLAY DPG54-ERROR-MESSAGE                              
034900                 MOVE +4003 TO ABEND-CODE                                 
035000                 CALL 'ILBOABN0' USING ABEND-CODE                         
035100             WHEN OTHER                                                   
035200                 DISPLAY DPG54-ERROR-MESSAGE                              
035300         END-EVALUATE                                                     
035400                                                                          
035500* THE SECOND CALL WILL VERIFY THAT THE END DATE IS VALID, AND             
035600* REPLACE IT WITH THE LAST DAY OF THE FISCAL MONTH                        
035700         INITIALIZE  DPG51 DPG52 DPG53                                    
035800                     DPG54 DPG55 DPG56                                    
035900                                                                          
036000         SET DPG51-FISCAL-454-CALENDAR TO TRUE                            
036100         MOVE CC-END-DATE            TO DPG52-LK-DATE-INPUT               
036200         SET DPG52-LK-DTE-ISO-FMT    TO TRUE                              
036300                                                                          
036400         CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53        
036500                                                 DPG54 DPG55 DPG56        
036600                                                                          
036700         EVALUATE TRUE                                                    
036800             WHEN DPG54-NO-ERROR                                          
036900                 MOVE DPG56-LAST-FP-DB2-ISO-FMT                           
037000                                     TO PV-END-DATE                       
037100                 MOVE DPG56-FISCAL-YEAR-CCYY                              
037200                                     TO PV-END-CC-FISCAL-YEAR             
037300                 MOVE DPG56-FISCAL-YEAR-NBR                               
037400                                     TO PV-END-FISCAL-YEAR                
037500                 MOVE DPG56-FISCAL-PERIOD-NBR                             
037600                                     TO PV-END-FISCAL-PERIOD              
037700                                        PV-END-CC-FISCAL-PERIOD           
037800             WHEN DPG54-DATE-INVALID                                      
037900                 DISPLAY '** ABEND **'                                    
038000                 DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '        
038100                         'DP500-KOHLS-CALENDAR-ROUTINE **'                
038200                 DISPLAY '** PARAGRAPH = 1100-VALIDATE-DATES. **'         
038300                 DISPLAY '** INVALID START DATE: ' CC-BEGIN-DATE          
038400                 DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '        
038500                 DISPLAY DPG54-ERROR-MESSAGE                              
038600                 MOVE +4003 TO ABEND-CODE                                 
038700                 CALL 'ILBOABN0' USING ABEND-CODE                         
038800             WHEN OTHER                                                   
038900                 DISPLAY DPG54-ERROR-MESSAGE                              
039000         END-EVALUATE.                                                    
039100                                                                          
039200******************************************************************        
039300* 1110-GET-DEFAULT-DATES  -  RETRIEVES THE DEFAULT BEGIN AND END          
039400* DATES DEPENDING ON THE REPORT TYPE CODE.                                
039500******************************************************************        
039600 1110-GET-DEFAULT-DATES.                                                  
039700*    THE FIRST CALL TO THE UTILITY WILL BRING BACK THE CURRENT            
039800*    FISCAL WEEK OF THE QUARTER.  THIS WILL BE USED TO CALCULATE          
039900*    THE NUMBER OF DAYS TO GO BACK TO GET TO THE PREVIOUS QUARTER.        
040000                                                                          
040100     INITIALIZE  DPG51 DPG52 DPG53                                        
040200                 DPG54 DPG55 DPG56.                                       
040300                                                                          
040400     SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
040500     MOVE PV-CURRENT-DATE        TO DPG52-LK-DATE-INPUT.                  
040600     SET DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
040700                                                                          
040800     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
040900                                             DPG54 DPG55 DPG56.           
041000                                                                          
041100     EVALUATE TRUE                                                        
041200         WHEN DPG54-NO-ERROR                                              
041300             COMPUTE PV-PREV-DAYS = DPG56-FISCAL-QTR-WEEK * 7             
041400         WHEN DPG54-DATE-INVALID                                          
041500             DISPLAY DPG54-ERROR-MESSAGE                                  
041600             DISPLAY '** ABEND **'                                        
041700             DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '            
041800                     'DP500-KOHLS-CALENDAR-ROUTINE **'                    
041900             DISPLAY '** PARAGRAPH = 1110-GET-DEFAULT-DATES **'           
042000             DISPLAY '** DATE INVALID: ' PV-CURRENT-DATE                  
042100             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
042200             DISPLAY DPG54-ERROR-MESSAGE                                  
042300             MOVE +4003 TO ABEND-CODE                                     
042400             CALL 'ILBOABN0' USING ABEND-CODE                             
042500         WHEN OTHER                                                       
042600             DISPLAY DPG54-ERROR-MESSAGE                                  
042700             DISPLAY '** ABEND **'                                        
042800             DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '            
042900                     'DP500-KOHLS-CALENDAR-ROUTINE **'                    
043000             DISPLAY '** PARAGRAPH = 1110-GET-DEFAULT-DATES **'           
043100             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
043200             DISPLAY DPG54-ERROR-MESSAGE                                  
043300             MOVE +4003 TO ABEND-CODE                                     
043400             CALL 'ILBOABN0' USING ABEND-CODE                             
043500     END-EVALUATE.                                                        
043600                                                                          
043700*    THE SECOND CALL WILL ACTUALLY GET THE FISCAL QUARTER START           
043800*    AND END DATE...THE BEGIN AND END DATES OF THE REPORT.                
043900     INITIALIZE  DPG51 DPG52 DPG53                                        
044000                 DPG54 DPG55 DPG56.                                       
044100                                                                          
044200     SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
044300     MOVE PV-CURRENT-DATE        TO DPG52-LK-DATE-INPUT.                  
044400     SET DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
044500                                                                          
044600     MOVE PV-PREV-DAYS           TO DPG51-INCR-DECR-DAYS-9.               
044700     SET DPG51-DECREMENT-DATE    TO TRUE.                                 
044800                                                                          
044900     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
045000                                             DPG54 DPG55 DPG56.           
045100                                                                          
045200     EVALUATE TRUE                                                        
045300         WHEN DPG54-NO-ERROR                                              
045400             MOVE DPG56-FIRST-FD-DB2-ISO-FMT TO PV-BEGIN-DATE             
045500             MOVE DPG56-LAST-FQ-DB2-ISO-FMT  TO PV-END-DATE               
045600         WHEN DPG54-DATE-INVALID                                          
045700             DISPLAY DPG54-ERROR-MESSAGE                                  
045800             DISPLAY '** ABEND **'                                        
045900             DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '            
046000                     'DP500-KOHLS-CALENDAR-ROUTINE **'                    
046100             DISPLAY '** PARAGRAPH = 1110-GET-DEFAULT-DATES **'           
046200             DISPLAY '** DATE INVALID: ' PV-CURRENT-DATE                  
046300             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
046400             DISPLAY DPG54-ERROR-MESSAGE                                  
046500             MOVE +4003 TO ABEND-CODE                                     
046600             CALL 'ILBOABN0' USING ABEND-CODE                             
046700         WHEN OTHER                                                       
046800             DISPLAY DPG54-ERROR-MESSAGE                                  
046900             DISPLAY '** ABEND **'                                        
047000             DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '            
047100                     'DP500-KOHLS-CALENDAR-ROUTINE **'                    
047200             DISPLAY '** PARAGRAPH = 1110-GET-DEFAULT-DATES **'           
047300             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
047400             DISPLAY DPG54-ERROR-MESSAGE                                  
047500             MOVE +4003 TO ABEND-CODE                                     
047600             CALL 'ILBOABN0' USING ABEND-CODE                             
047700                                                                          
047800     END-EVALUATE.                                                        
047900                                                                          
048000*    THE THIRD CALL WILL GET THE FISCAL MONTH AND YEAR OF THE             
048100*    FIRST MONTH OF THE QUARTER.                                          
048200     INITIALIZE  DPG51 DPG52 DPG53                                        
048300                 DPG54 DPG55 DPG56.                                       
048400                                                                          
048500     SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
048600     MOVE PV-BEGIN-DATE          TO DPG52-LK-DATE-INPUT.                  
048700     SET DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
048800                                                                          
048900     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
049000                                             DPG54 DPG55 DPG56.           
049100                                                                          
049200     EVALUATE TRUE                                                        
049300         WHEN DPG54-NO-ERROR                                              
049400             MOVE DPG56-FISCAL-YEAR-NBR                                   
049500                                      TO PV-BEGIN-FISCAL-YEAR             
049600             MOVE DPG56-FISCAL-PERIOD-NBR                                 
049700                                      TO PV-BEGIN-FISCAL-MONTH            
049800                                         PV-BEGIN-CC-FISCAL-MONTH         
049900             MOVE DPG56-FIRST-FP-DB2-ISO-FMT-YR                           
050000                                      TO PV-BEGIN-CC-FISCAL-YEAR          
050100         WHEN DPG54-DATE-INVALID                                          
050200             DISPLAY DPG54-ERROR-MESSAGE                                  
050300             DISPLAY '** ABEND **'                                        
050400             DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '            
050500                     'DP500-KOHLS-CALENDAR-ROUTINE **'                    
050600             DISPLAY '** PARAGRAPH = 1110-GET-DEFAULT-DATES **'           
050700             DISPLAY '** DATE INVALID: ' PV-BEGIN-DATE                    
050800             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
050900             DISPLAY DPG54-ERROR-MESSAGE                                  
051000             MOVE +4003 TO ABEND-CODE                                     
051100             CALL 'ILBOABN0' USING ABEND-CODE                             
051200         WHEN OTHER                                                       
051300             DISPLAY DPG54-ERROR-MESSAGE                                  
051400             DISPLAY '** ABEND **'                                        
051500             DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '            
051600                     'DP500-KOHLS-CALENDAR-ROUTINE **'                    
051700             DISPLAY '** PARAGRAPH = 1110-GET-DEFAULT-DATES **'           
051800             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
051900             DISPLAY DPG54-ERROR-MESSAGE                                  
052000             MOVE +4003 TO ABEND-CODE                                     
052100             CALL 'ILBOABN0' USING ABEND-CODE                             
052200     END-EVALUATE.                                                        
052300                                                                          
052400*    THE FOURTH CALL WILL GET THE FISCAL MONTH AND YEAR OF THE            
052500*    LAST MONTH OF THE QUARTER.                                           
052600     INITIALIZE  DPG51 DPG52 DPG53                                        
052700                 DPG54 DPG55 DPG56.                                       
052800                                                                          
052900     SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
053000     MOVE PV-END-DATE            TO DPG52-LK-DATE-INPUT.                  
053100     SET DPG52-LK-DTE-ISO-FMT    TO TRUE.                                 
053200                                                                          
053300     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
053400                                             DPG54 DPG55 DPG56.           
053500                                                                          
053600     EVALUATE TRUE                                                        
053700         WHEN DPG54-NO-ERROR                                              
053800             MOVE DPG56-FISCAL-YEAR-NBR                                   
053900                                      TO PV-END-FISCAL-YEAR               
054000             MOVE DPG56-FISCAL-PERIOD-NBR                                 
054100                                      TO PV-END-FISCAL-PERIOD             
054200                                         PV-END-CC-FISCAL-PERIOD          
054300             MOVE DPG56-FISCAL-YEAR-CCYY                                  
054400                                      TO PV-END-CC-FISCAL-YEAR            
054500         WHEN DPG54-DATE-INVALID                                          
054600             DISPLAY DPG54-ERROR-MESSAGE                                  
054700             DISPLAY '** ABEND **'                                        
054800             DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '            
054900                     'DP500-KOHLS-CALENDAR-ROUTINE **'                    
055000             DISPLAY '** PARAGRAPH = 1110-GET-DEFAULT-DATES **'           
055100             DISPLAY '** DATE INVALID: ' PV-CURRENT-DATE                  
055200             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
055300             DISPLAY DPG54-ERROR-MESSAGE                                  
055400             MOVE +4003 TO ABEND-CODE                                     
055500             CALL 'ILBOABN0' USING ABEND-CODE                             
055600         WHEN OTHER                                                       
055700             DISPLAY DPG54-ERROR-MESSAGE                                  
055800             DISPLAY '** ABEND **'                                        
055900             DISPLAY '** PROGRAM = RVKUT004 -- CALLED MODULE '            
056000                     'DP500-KOHLS-CALENDAR-ROUTINE **'                    
056100             DISPLAY '** PARAGRAPH = 1110-GET-DEFAULT-DATES **'           
056200             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
056300             DISPLAY DPG54-ERROR-MESSAGE                                  
056400             MOVE +4003 TO ABEND-CODE                                     
056500             CALL 'ILBOABN0' USING ABEND-CODE                             
056600     END-EVALUATE.                                                        
056700                                                                          
056800******************************************************************        
056900* 2000-CREATE-EXTRACT-FILE                                                
057000* PROCESS:  THIS PARAGRAPH WILL SET UP AND WRITE EXTRACT RECORDS          
057100******************************************************************        
057200 2000-CREATE-EXTRACT-FILE.                                                
057300     INITIALIZE RV010-EXTRACT-RECORD                                      
057400     SET RV010-DETAIL-REC            TO TRUE.                             
057500                                                                          
057600     IF DFMEIT-DFCT-ITEM-NBR = PV-HOLD-ITEM-NBR                           
057700         CONTINUE                                                         
057800     ELSE                                                                 
057810         PERFORM 7300-SELECT-DEPT-NBR                                     
057910         IF PS-DEPT-NBR-NOT-FOUND                                         
057960             MOVE 'UNKNOWN VENDOR' TO ENTNME-ENT-NAME-DESC                
057970             MOVE '999999999'  TO EXTENT-DUN-NBR                          
057990         ELSE                                                             
057991             PERFORM 7310-SELECT-VENDOR-INFORMATION                       
057992         END-IF                                                           
058000         MOVE DFMEIT-DFCT-ITEM-NBR       TO PV-HOLD-ITEM-NBR              
058100     END-IF.                                                              
058200                                                                          
058300     IF DFMEIT-MDSE-DEPT-NBR = PV-HOLD-DPT-NBR                            
058400         CONTINUE                                                         
058500     ELSE                                                                 
058600         MOVE DFMEIT-MDSE-DEPT-NBR TO PV-HOLD-DPT-NBR                     
058700     END-IF                                                               
058800                                                                          
058900     MOVE DFMEIT-MDSE-DEPT-NBR     TO  RV010-DPT-NBR.                     
059000     MOVE DFMEIT-DFCT-ITEM-NBR     TO  RV010-ITEM-NBR.                    
059100     MOVE DFCTOR-OPER-UNT-ID       TO  RV010-OPER-UNT-ID.                 
059200     MOVE DFCTOR-DFCT-ORD-ID-CDE   TO  RV010-LIST-TYPE-CDE.               
  9200     MOVE DFCTOR-DFCT-ORD-STAT-CDE TO  RV010-LIST-STAT-CDE.               
059300     MOVE DFMEIT-RTL-COST-AMT      TO  RV010-RTL-COST-AMT.                
059400     MOVE DFMEIT-UNIT-COST-AMT     TO  RV010-UNIT-COST-AMT.               
059500     MOVE DFCTOR-STATUS-CHG-DTE    TO  RV010-RETURN-DATE.                 
059600     MOVE EXTENT-DUN-NBR           TO  RV010-DUNS-NBR.                    
059700     MOVE ENTNME-ENT-NAME-DESC     TO  RV010-VND-NAME.                    
059800                                                                          
059900     PERFORM 8000-WRITE-EXTRACT-RECORD.                                   
060000                                                                          
060100     PERFORM 7200-FETCH-DRIVER-CURSOR.                                    
060200                                                                          
060300******************************************************************        
060400* 7000-OPEN-DRIVER-CURSOR.                                       *        
060500******************************************************************        
060600 7000-OPEN-DRIVER-CURSOR.                                                 
060700     EXEC SQL                                                             
060800          OPEN DITEM_CURSOR                                               
060900     END-EXEC.                                                            
061000                                                                          
061100     EVALUATE TRUE                                                        
061200         WHEN SQLWARN0 NOT EQUAL SPACE                                    
061300         WHEN SQLCODE NOT EQUAL ZERO                                      
061400             DISPLAY '** ABEND **'                                        
061500             DISPLAY '** PROGRAM = RVKUT004 **'                           
061600             DISPLAY '** PARAGRAPH = 7000-OPEN-DRIVER-CURSOR **'          
061700             PERFORM 9100-SQL-ABEND                                       
061800     END-EVALUATE.                                                        
061900                                                                          
062000******************************************************************        
062100* 7100-CLOSE-DRIVER-CURSOR.                                      *        
062200******************************************************************        
062300 7100-CLOSE-DRIVER-CURSOR.                                                
062400     EXEC SQL                                                             
062500         CLOSE DITEM_CURSOR                                               
062600     END-EXEC.                                                            
062700                                                                          
062800     EVALUATE TRUE                                                        
062900         WHEN SQLWARN0 NOT EQUAL SPACE                                    
063000         WHEN SQLCODE NOT EQUAL ZERO                                      
063100             DISPLAY '** ABEND **'                                        
063200             DISPLAY '** PROGRAM = RVKUT004 **'                           
063300             DISPLAY '** PARAGRAPH = 7100-CLOSE-DRIVER-CURSOR**'          
063400             PERFORM 9100-SQL-ABEND                                       
063500     END-EVALUATE.                                                        
063600                                                                          
063700******************************************************************        
063800* 7200-FETCH-DRIVER-CURSOR.                                      *        
063900******************************************************************        
064000 7200-FETCH-DRIVER-CURSOR.                                                
064100     EXEC SQL                                                             
064200          FETCH DITEM_CURSOR                                              
064300          INTO                                                            
064400                  :DFMEIT-MDSE-DEPT-NBR                                   
064500                , :DFMEIT-DFCT-ITEM-NBR                                   
064600                , :DFCTOR-OPER-UNT-ID                                     
064700                , :DFCTOR-DFCT-ORD-ID-CDE                                 
064700                , :DFCTOR-DFCT-ORD-STAT-CDE                               
064800                , :DFMEIT-RTL-COST-AMT                                    
064900                , :DFMEIT-UNIT-COST-AMT                                   
065000                , :DFCTOR-STATUS-CHG-DTE                                  
065100     END-EXEC.                                                            
065200                                                                          
065300     EVALUATE TRUE                                                        
065400         WHEN SQLCODE = ZERO                                              
065500              CONTINUE                                                    
065600         WHEN SQLCODE = +100                                              
065700              SET PS-EOC-DRIVER-CURSOR TO TRUE                            
065800         WHEN SQLWARN0 NOT EQUAL SPACE                                    
065900         WHEN SQLCODE NOT EQUAL ZERO                                      
066000             DISPLAY '** ABEND **'                                        
066100             DISPLAY '** PROGRAM = RVKUT004 **'                           
066200             DISPLAY '** PARAGRAPH = 7200-FETCH-DRIVER-CURSOR**'          
066300             PERFORM 9100-SQL-ABEND                                       
066400     END-EVALUATE.                                                        
066500                                                                          
066600******************************************************************        
066700* 7300-SELECT-DEPT-NBR.                                          *        
066710*                                                                *        
066720* USE THE LOGISTICS DOMAIN MODULE (XLKUT001) TO FETCH THE        *        
066730* 3-CHARACTER VERSION OF THE DEPARTMENT NUMBER AND THE ENT-ID    *        
066740* FROM THE LOGISTICS DOMAIN TABLES.  THIS INFORMATION WILL BE    *        
066750* USED FETCH THE VENDOR NAME AND VENDOR DUNNS NUMBER.            *        
066800******************************************************************        
066900 7300-SELECT-DEPT-NBR.                                                    
066901     INITIALIZE XL001-ITEM-COMMAREA.                              02317342
066902                                                                  02317438
066903     SET XL001-BY-ITEM                                            02317538
066904         XL001-SELECT-SKU                                               38
066905         XL001-SELECT-VND-STYL      TO TRUE.                      02317638
066906                                                                  02317738
066907     MOVE DFMEIT-DFCT-ITEM-NBR      TO XL001-KEY-ITM-NBR.         02317838
066908                                                                  02317938
066909     CALL XL001-VSM-PGM USING XL001-ITEM-COMMAREA.                02318038
066910                                                                  02318338
066914     EVALUATE TRUE                                                02318442
066915        WHEN XL001-SUCCESSFUL                                     02318538
066916            SET PS-DEPT-NBR-FOUND   TO TRUE                             38
066917        WHEN XL001-SQL-ERROR                                      02318738
066923            EVALUATE TRUE                                               38
066924               WHEN XL001-SKU-SQLCODE NOT = 0                           38
066925                   IF  XL001-SKU-NOT-FOUND                              38
066926                       SET PS-DEPT-NBR-NOT-FOUND TO TRUE                38
066927                   ELSE                                                 38
066928                       DISPLAY '** ABEND **'                              
066929                       DISPLAY '** PROGRAM = RVKUT004 **'                 
066930                       DISPLAY '** PARAGRAPH = '                          
066931                               '7300-SELECT-DEPT-NBR'                     
066932                       MOVE XL001-SKU-SQLCODE TO PV-ERROR-SQL-CODE        
066933                       DISPLAY '** SQL CODE = ' PV-ERROR-SQL-CODE         
066934                       DISPLAY '** TABLE = XLT_SKU'                       
066935                       DISPLAY '** ATTEMPT TO SELECT XLT_SKU '            
066936                               'INFO FOR ITEM '                           
066937                                XL001-KEY-ITM-NBR                       38
066938                       MOVE XL001-SQLCA TO SQLCA                        38
066939                       PERFORM 9100-SQL-ABEND                           38
066940                   END-IF                                               38
066941               WHEN XL001-VND-STYL-SQLCODE NOT = 0                      38
066942                   IF  XL001-VND-STYL-NOT-FOUND                         38
066943                       SET PS-DEPT-NBR-NOT-FOUND TO TRUE                38
066944                   ELSE                                                 38
066945                       DISPLAY '** ABEND **'                              
066946                       DISPLAY '** PROGRAM = RVKUT004 **'                 
066947                       DISPLAY '** PARAGRAPH = '                          
066948                               '7300-SELECT-DEPT-NBR'                     
066949                       MOVE XL001-SKU-SQLCODE TO PV-ERROR-SQL-CODE        
066950                       DISPLAY '** SQL CODE = '                           
066951                                PV-ERROR-SQL-CODE                         
066952                       DISPLAY '** TABLE = XLT_VND_STYL'                  
066953                       DISPLAY '** ATTEMPT TO SELECT XLT_VND_STYL'        
066954                               ' INFO FOR ITEM '                          
066955                                XL001-KEY-ITM-NBR                       38
066956                       MOVE XL001-SQLCA TO SQLCA                        38
066957                       PERFORM 9100-SQL-ABEND                           38
066958                   END-IF                                               38
066959            END-EVALUATE                                                38
066960        WHEN XL001-INVALID-ACCESS                                 02318538
066961            DISPLAY '** ABEND **'                                         
066962            DISPLAY '** PROGRAM = RVKUT004 **'                            
066963            DISPLAY '** PARAGRAPH = 7300-SELECT-DEPT-NBR'                 
066964            DISPLAY '** INVALID ACCESS -- XLKUT001'                       
066965            DISPLAY '** ATTEMPT TO SELECT DEPT NBR INFO FOR ITEM '      38
066966                     XL001-KEY-ITM-NBR                            02319038
066967            PERFORM 9200-NON-SQL-ABEND                                  38
066968        WHEN OTHER                                                02319138
066969            DISPLAY '** ABEND **'                                         
066970            DISPLAY '** PROGRAM = RVKUT004 **'                            
066971            DISPLAY '** PARAGRAPH = 7300-SELECT-DEPT-NBR'                 
066972            DISPLAY '** ERROR WITH XLKUT001'                              
066973            DISPLAY '** RETURN CODE = ' XL001-RETURN-CODE                 
066974            DISPLAY '** ATTEMPT TO SELECT DEPT NBR INFO FOR ITEM '      38
066975                     XL001-KEY-ITM-NBR                            02319038
066976            PERFORM 9200-NON-SQL-ABEND                                  38
066977     END-EVALUATE.                                                02319738
066980                                                                  02318138
066985                                                                  02318138
066986******************************************************************        
066987* 7310-SELECT-VENDOR-INFORMATION.                                *        
066988******************************************************************        
066990 7310-SELECT-VENDOR-INFORMATION.                                          
067000     EXEC SQL                                                             
067100         SELECT  C.ENT_NAME_DESC                                          
067200            ,    B.DUN_NBR                                                
067300           INTO  :ENTNME-ENT-NAME-DESC                                    
067400            ,    :EXTENT-DUN-NBR                                          
067600           FROM  TVNDDPT A                                                
067700            ,    TEXTENT B                                                
067800            ,    TENTNME C                                                
068000          WHERE  A.DEPT_NBR     =  :XL001-DEPT-NBR-3CH                    
068100            AND  A.ENT_ID       =  :XL001-ENT-ID                          
068200            AND  A.ENT_ID       =  B.ENT_ID                               
068300            AND  B.ENT_ID       =  C.ENT_ID                               
068400            AND  C.TYPE_CDE     =  '01'                                   
068500     END-EXEC.                                                            
068600                                                                          
068700     EVALUATE TRUE                                                        
068800         WHEN SQLCODE = ZERO                                              
068900         WHEN SQLCODE = +100                                              
069000              CONTINUE                                                    
069100         WHEN SQLWARN0 NOT EQUAL SPACE                                    
069200         WHEN SQLCODE NOT EQUAL ZERO                                      
069300             DISPLAY '** ABEND **'                                        
069400             DISPLAY '** PROGRAM = RVKUT004 **'                           
069500             DISPLAY '** PARAGRAPH = 7310-SELECT-VENDOR-INFO **'          
069600             PERFORM 9100-SQL-ABEND                                       
069700     END-EVALUATE.                                                        
069800                                                                          
069900     IF SQLCODE = +100                                                    
070400         MOVE 'UNKNOWN VENDOR' TO ENTNME-ENT-NAME-DESC                    
070500         MOVE '999999999'  TO EXTENT-DUN-NBR                              
070700     END-IF.                                                              
070800                                                                          
070900*****************************************************************         
071000*  WRITE THE STYLE SUMMARY EXTRACT RECORD TO AN OUTPUT FILE.    *         
071100*****************************************************************         
071200 8000-WRITE-EXTRACT-RECORD.                                               
071300     WRITE RTV-REPORT-EXTRACT-RECORD                                      
071400       FROM RV010-EXTRACT-RECORD.                                         
071500                                                                          
071600*****************************************************************         
071700*  WRITE THE SORT CARD AND DATE CARD TO  AN OUTPUT FILE.                  
071800*****************************************************************         
071900 8100-WRITE-JCL-DATE-CARD.                                                
072000     DISPLAY 'DATE CARD WAS WRITTEN AS FOLLOWS:'                          
072100     DISPLAY '-------------- BEGINNING OF DATE CARD------------ '.        
072200     DISPLAY ' '.                                                         
072300     WRITE JCL-DATE-CARD-RECORD                                           
072400       FROM JCL-DATE-CARD-INCL-LINE.                                      
072500     DISPLAY  JCL-DATE-CARD-INCL-LINE.                                    
072600     DISPLAY ' '.                                                         
072700     DISPLAY '---------------- END OF DATE CARD --------------- '.        
072800                                                                          
072900******************************************************************        
073000* 9000-EOJ-ROUTINE - WRITES A HEADER RECORD, DATE CARD AND                
073100* CLOSES THE OUTPUT FILES                                                 
073200******************************************************************        
073300 9000-EOJ-ROUTINE.                                                        
073400     MOVE PV-CURRENT-DATE        TO RV010-CURR-DATE.                      
073500                                                                          
073600     MOVE PV-BEGIN-DATE          TO RV010-BEGIN-DATE.                     
073700     MOVE PV-BEGIN-FISCAL-DATE   TO RV010-BEGIN-FISCAL-DATE.              
073800     MOVE PV-END-DATE            TO RV010-END-DATE.                       
073900     MOVE PV-END-FISCAL-DATE     TO RV010-END-FISCAL-DATE                 
074000     MOVE CC-RPT-TYPE            TO RV010-RPT-TYPE.                       
074100                                                                          
074200     MOVE PV-BEGIN-CC-FISCAL-DATE  TO JCL-INCL-BEGIN-DATE.                
074300     MOVE PV-END-CC-FISCAL-DATE  TO JCL-INCL-END-DATE.                    
074400                                                                          
074500     SET RV010-HEADER-REC            TO TRUE.                             
074600                                                                          
074700* WRITE HEADER INFORMATION TO OUTPUT FILE.                                
074800     PERFORM 8000-WRITE-EXTRACT-RECORD.                                   
074900                                                                          
075000* WRITE THE DATE CARD CONTROL MEMBER TO LIMIT THE VENDOR                  
075100* MASTER FILE                                                             
075200     PERFORM 8100-WRITE-JCL-DATE-CARD.                                    
075300                                                                          
075400     CLOSE RTV-REPORT-EXTRACT                                             
075500           JCL-DATE-CARD.                                                 
075600                                                                          
075700******************************************************************        
075800* 9100-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
075900* EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
076000* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
076100* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
076200* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
076300* FORMAT.                                                                 
076400******************************************************************        
076500 9100-SQL-ABEND.                                                          
076600     COPY DPPD004.                                                        
076700                                                                          
076800     MOVE +4013 TO ABEND-CODE.                                            
076900     CALL 'ILBOABN0' USING ABEND-CODE.                                    
076910                                                                          
077000******************************************************************        
077100* 9200-NON-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE          
077200* EVENT THAT A NON-SQL ERROR OCCURS.                                      
077700******************************************************************        
077800 9200-NON-SQL-ABEND.                                                      
078100     MOVE +4014 TO ABEND-CODE.                                            
078200     CALL 'ILBOABN0' USING ABEND-CODE.                                    
