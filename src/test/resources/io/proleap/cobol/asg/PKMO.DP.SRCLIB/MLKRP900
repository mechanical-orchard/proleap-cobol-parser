       IDENTIFICATION DIVISION.                                         05/14/99
                                                                        MLKRP900
       PROGRAM-ID.    MLKRP900                                             LV001
       AUTHOR.        AMY LAWSON.                                       MLKRP900
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          MLKRP900
       DATE-WRITTEN.  10/31/00.                                         MLKRP900
       DATE-COMPILED.                                                   MLKRP900
                                                                        MLKRP900
      ******************************************************************MLKRP900
      *  THIS PROGRAM REPORTS SKU'S THAT WERE DAMAGED WHEN STORE 016            
      *  HAD A FILE IN THE TOY DEPARTMENT.  WE NEED TO CREATE A REPORT          
      *  FOR THE INSURANCE COMPANY.                                             
      *                                                                 MLKRP900
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
W04232* W04232   04-23-2002  FIRE STORE 054 TO 801. ADD SKU DESCRIPTION         
W04232*                      -ROD JANSSEN                                       
W11122* W11122   11-08-2002  FIRE STORE 016 TO 811 CHANGES.                     
W11122*                      1.  NI-SKU CHANGES.                                
W11122*                      2.  BREAKS AT DEPT/MAJ CLASS CHANGE                
W11122*                      3.  SUMMARY REPORT (RPT02)                         
W11122*                      -ROD JANSSEN                                       
W21731* WR21731  04-25-2003  CHANGES TO USE MLRD019 INSTEAD OF MLRD005O         
W21731*                      AND REPLACE USAGE OF TSK WITH TUPC/TUPCPLS.        
W21731*                      -V. LEE YANG                                       
      * LY1003   10-28-2003  INTRODUCE OPTION TO EITHER SHOW COST/RETAIL        
      *                      OR NOT.                                            
      *                      -V. LEE YANG                                       
      * W26006   09-17-2004  FIX ENDLESS LOOP WHEN 'NO AMT' IS SPECIFIED        
      *                      -V. LEE YANG                                       
PM3403*PM3403  04-20-2012    LARGE RETAIL RECOMPILE ONLY(MLRD019)          CL**6
PM3403*                      S. JACKSON                                    CL**6
      ******************************************************************        
                                                                        MLKRP900
       ENVIRONMENT DIVISION.                                            MLKRP900
       CONFIGURATION SECTION.                                           MLKRP900
       SOURCE-COMPUTER.        IBM-3090.                                MLKRP900
       OBJECT-COMPUTER.        IBM-3090.                                MLKRP900
                                                                        MLKRP900
       INPUT-OUTPUT SECTION.                                            MLKRP900
       FILE-CONTROL.                                                    MLKRP900
           SELECT TRANSFER-FILE                                         MLKRP900
               ASSIGN TO INP01.                                         MLKRP900
                                                                                
LY1003     SELECT OPTIONAL RQ-REQUEST-CNTL-FILE                         MLKRP900
LY1003         ASSIGN TO INP02.                                         MLKRP900
LY1003                                                                          
           SELECT INSURANCE-REPORT                                      MLKRP900
               ASSIGN TO RPT01.                                         MLKRP900
                                                                        MLKRP900
W11122     SELECT SUMMARY-REPORT                                        MLKRP900
W11122         ASSIGN TO RPT02.                                         MLKRP900
W11122                                                                  MLKRP900
       DATA DIVISION.                                                   MLKRP900
       FILE SECTION.                                                    MLKRP900
                                                                        MLKRP900
       FD  TRANSFER-FILE                                                MLKRP900
           LABEL RECORDS ARE STANDARD                                   MLKRP900
           BLOCK CONTAINS 0 RECORDS                                     MLKRP900
           RECORDING  MODE IS F.                                        MLKRP900
       01  TRANSFER-RECORD             PIC X(80).                       MLKRP900
                                                                        MLKRP900
LY1003 FD  RQ-REQUEST-CNTL-FILE                                         MLKRP900
LY1003     LABEL RECORDS ARE STANDARD                                   MLKRP900
LY1003     BLOCK CONTAINS 0 RECORDS                                     MLKRP900
LY1003     RECORDING  MODE IS F.                                        MLKRP900
LY1003 01  RQ-REQUEST-CNTL-RECORD      PIC X(80).                       MLKRP900
LY1003                                                                  MLKRP900
       FD  INSURANCE-REPORT                                             MLKRP900
           LABEL RECORDS ARE OMITTED                                    MLKRP900
           BLOCK CONTAINS 0 RECORDS                                     MLKRP900
           RECORDING  MODE IS F.                                        MLKRP900
       01  INSURANCE-REPORT-LINE       PIC X(150).                      MLKRP900
                                                                        MLKRP900
W11122 FD  SUMMARY-REPORT                                               MLKRP900
 |         LABEL RECORDS ARE OMITTED                                    MLKRP900
 |         BLOCK CONTAINS 0 RECORDS                                     MLKRP900
 |         RECORDING  MODE IS F.                                        MLKRP900
 |     01  SUMMARY-REPORT-LINE         PIC X(150).                      MLKRP900
W11122                                                                  MLKRP900
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                         MLKRP900
      ******************************************************************        
                                                                        MLKRP900
       01  CC-CONTROL-CARD.                                             MLKRP900
           05  CC-OPTION                   PIC X(08)       VALUE SPACES.MLKRP900
               88  CC-OPTION-NO-AMOUNT                     VALUE                
                                                           'NO AMT  '.          
           05  FILLER                      PIC X(72)       VALUE SPACES.        
                                                                        MLKRP900
       01  PC-PROGRAM-CONSTANTS.                                        MLKRP900
           05  PC-MAX-LINE-COUNT            PIC S9(04)    VALUE +56     MLKRP900
                                                          COMP SYNC.    MLKRP900
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'MLKRP900'.MLKRP800
W11122     05  PC-SUB-CLASS                    PIC X(03)   VALUE 'SCL'. MLKUT002
21731 *    05  PC-MAJOR-CL                     PIC X(03)   VALUE 'MCL'. MLKUT002
W11122     05  PC-OPERCO-NBR                   PIC X(02)   VALUE '03'.  MLKUT002
21731      05  PC-CORP-STORE-000               PIC S9(04)  VALUE ZEROES MLKUT002
21731                          COMP.                                            
W11122     05  PC-APLSYS-CDE                   PIC X(02)   VALUE 'ML'.  MLKUT002
                                                                        MLKRP900
       01  PV-PROGRAM-VARIABLES.                                        MLKRP900
           05  PV-ADVANCE                   PIC S9(03)    VALUE ZERO    MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-LINE-COUNT                PIC S9(04)    VALUE ZERO    MLKRP900
                                                          COMP SYNC.    MLKRP900
W11122     05  PV-LINE-COUNT2               PIC S9(04)    VALUE ZERO    MLKRP900
W11122                                                    COMP SYNC.    MLKRP900
           05  PV-PAGE-COUNT                PIC S9(05)    VALUE ZERO.   MLKRP900
W11122     05  PV-PAGE-COUNT2               PIC S9(05)    VALUE ZERO.   MLKRP900
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACE.  MLKRP900
00126      05  PV-DB2-OPER-ATTEMPTED        PIC  X(30)    VALUE SPACES.         
W26600*    05  PV-PREV-FROM-STORE           PIC  9(03)    VALUE ZEROES. MLKRP900
W26600     05  PV-PREV-FROM-STORE           PIC  9(04)    VALUE ZEROES. MLKRP900
W26600     05  PV-PREV-FROM-STORE-X  REDEFINES                          MLKRP900
W26600         PV-PREV-FROM-STORE           PIC  X(04).                 MLKRP900
           05  PV-PREV-DEPT-NBR             PIC  X(03)    VALUE SPACE.  MLKRP900
21731      05  PV-PREV-MAJ-CLASS            PIC  X(02)    VALUE ZEROES. MLKRP900
21731      05  FILLER REDEFINES PV-PREV-MAJ-CLASS.                              
21731          10  PV-PREV-MAJ-CLASS-1      PIC  X(01).                         
21731          10  PV-PREV-MAJ-CLASS-2      PIC  X(01).                         
           05  PV-UNIT-COST-AMT             PIC S9(4)V9(3) COMP-3.      MLKRP900
W04232     05  PV-SKU-DESC                  PIC  X(25)    VALUE SPACES. MLKRP900
21731 *    05  PV-DEPT-NBR                  PIC  X(03)    VALUE SPACES.         
21731 *    05  PV-CLASS-NBR                 PIC  X(02)    VALUE SPACES.         
21731 *    05  PV-SEQ-NBR                   PIC  X(03)    VALUE SPACES.         
21731 *    05  PV-SKU                       PIC  X(08)    VALUE SPACES.         
           05  PV-EXT-RTL-AMT               PIC S9(09)V99 VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-EXT-CST-AMT               PIC S9(09)V99 VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-CUM-STR-EXT-RTL           PIC S9(09)V99 VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-CUM-STR-EXT-CST           PIC S9(09)V99 VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-CUM-STR-QTY               PIC S9(09)    VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-CUM-DPT-EXT-RTL           PIC S9(09)V99 VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-CUM-DPT-EXT-CST           PIC S9(09)V99 VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-CUM-DPT-QTY               PIC S9(09)    VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-CUM-EXT-RTL               PIC S9(09)V99 VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-CUM-EXT-CST               PIC S9(09)V99 VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
           05  PV-CUM-QTY                   PIC S9(09)    VALUE +0      MLKRP900
                                                          COMP-3.       MLKRP900
W11122     05  PV-CUM-MAJ-EXT-RTL           PIC S9(09)V99 VALUE +0      MLKRP900
 |                                                        COMP-3.       MLKRP900
 |         05  PV-CUM-MAJ-EXT-CST           PIC S9(09)V99 VALUE +0      MLKRP900
 |                                                        COMP-3.       MLKRP900
 |         05  PV-CUM-MAJ-QTY               PIC S9(09)    VALUE +0      MLKRP900
 |                                                        COMP-3.       MLKRP900
 |         05  PV-SUB-CL-NBR-WORK.                                      MLKUT002
 |             10  PV-SUB-CL-NBR-1             PIC X(01)   VALUE '0'.   MLKUT002
 |             10  PV-SUB-CL-NBR-2-3           PIC X(02)   VALUE SPACE. MLKUT002
 |         05  PV-SUB-CL-NBR                   REDEFINES                MLKUT002
W11122         PV-SUB-CL-NBR-WORK              PIC X(03).               MLKUT002
                                                                        MLKRP900
       01  SW-SWITCHES.                                                 MLKRP900
           05  SW-END-OF-INPUT-FILE-SW PIC X       VALUE 'N'.           MLKRP900
               88  SW-END-OF-INPUT-FILE            VALUE 'Y'.           MLKRP900
               88  SW-NOT-END-OF-INPUT-FILE        VALUE 'N'.           MLKRP900
LY1003     05  SW-END-OF-CNTL-FILE-SW  PIC X       VALUE 'N'.           MLKRP900
LY1003         88  SW-END-OF-CNTL-FILE             VALUE 'Y'.           MLKRP900
                                                                        MLKRP900
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.MLKRP900
                                                                        MLKRP900
      ***************************************************************** MLKRP900
      * TRANSFER-RECORD LAYOUT.                                         MLKRP900
      ***************************************************************** MLKRP900
21731      COPY MLRD019.                                                   CL**5
                                                                        MLKRP900
      ******************************************************************MLKRP900
      * STANDARD REPORT HEADING                                         MLKRP900
      ******************************************************************MLKRP900
W11122     COPY DPWS150O.                                               MLKRP900
                                                                        MLKRP900
       01  RD-REPORT-TITLE-LINE.                                        MLKRP900
           05  FILLER                  PIC  X(59)    VALUE SPACES.      MLKRP900
           05  FILLER                  PIC  X(27)    VALUE              MLKRP900
               'INSURANCE REPORT FOR STORE '.                                   
           05  RD-REPORT-TITLE-STORE   PIC  9(04)    VALUE ZEROES.      MLKRP900
           05  RD-REPORT-TITLE-STORE-X      REDEFINES                   MLKRP900
               RD-REPORT-TITLE-STORE   PIC  X(04).                      MLKRP900
                                                                        MLKRP900
       01  RD-REPORT-HDR-LINE3.                                         MLKRP900
           05  FILLER                            PIC X(02) VALUE SPACES.        
           05  FILLER                            PIC X(04) VALUE                
               'DEPT'.                                                          
W04232     05  FILLER                            PIC X(02) VALUE SPACES.        
W11122     05  FILLER                            PIC X(03) VALUE                
W11122         'MAJ'.                                                           
W04232     05  FILLER                            PIC X(02) VALUE SPACES.        
W04232     05  FILLER                            PIC X(03) VALUE                
W11122         'CL '.                                                           
W11122     05  FILLER                            PIC X(01) VALUE SPACES.        
W04232     05  FILLER                            PIC X(03) VALUE                
W11122         'SKU'.                                                           
W04232     05  FILLER                            PIC X(07) VALUE SPACES.        
W04232     05  FILLER                            PIC X(11) VALUE                
W04232         'DESCRIPTION'.                                                   
W04232     05  FILLER                            PIC X(17) VALUE SPACES.        
           05  FILLER                            PIC X(08) VALUE                
               'QUANTITY'.                                                      
W04232     05  FILLER                            PIC X(16) VALUE SPACES.        
           05  FILLER                            PIC X(06) VALUE                
               'RETAIL'.                                                        
           05  FILLER                            PIC X(11) VALUE SPACES.        
           05  FILLER                            PIC X(04) VALUE                
               'COST'.                                                          
           05  FILLER                            PIC X(10) VALUE SPACES.        
           05  FILLER                            PIC X(10) VALUE                
               'EXT RETAIL'.                                                    
           05  FILLER                            PIC X(10) VALUE SPACES.        
           05  FILLER                            PIC X(08) VALUE                
               'EXT COST'.                                                      
           05  FILLER                            PIC X(06) VALUE SPACES.        
                                                                        MLKRP900
       01  RD-REPORT-HDR-LINE4.                                         MLKRP900
           05  FILLER                            PIC X(02) VALUE SPACES.        
           05  FILLER                            PIC X(04) VALUE                
               '----'.                                                          
W04232     05  FILLER                            PIC X(02) VALUE SPACES.        
W11122     05  FILLER                            PIC X(03) VALUE                
               '---'.                                                           
W04232     05  FILLER                            PIC X(02) VALUE SPACES.        
W04232     05  FILLER                            PIC X(03) VALUE                
               '---'.                                                           
W04232     05  FILLER                            PIC X(01) VALUE SPACES.        
W11122     05  FILLER                            PIC X(08) VALUE                
W11122         '--------'.                                                      
W11122     05  FILLER                            PIC X(02) VALUE SPACES.        
W04232     05  FILLER                            PIC X(11) VALUE                
W04232         '-----------'.                                                   
W04232     05  FILLER                            PIC X(17) VALUE SPACES.        
           05  FILLER                            PIC X(08) VALUE                
               '--------'.                                                      
W04232     05  FILLER                            PIC X(16) VALUE SPACES.        
           05  FILLER                            PIC X(06) VALUE                
               '------'.                                                        
W04232     05  FILLER                            PIC X(11) VALUE SPACES.        
           05  FILLER                            PIC X(04) VALUE                
               '----'.                                                          
           05  FILLER                            PIC X(10) VALUE SPACES.        
           05  FILLER                            PIC X(10) VALUE                
               '----------'.                                                    
           05  FILLER                            PIC X(10) VALUE SPACES.        
           05  FILLER                            PIC X(08) VALUE                
               '--------'.                                                      
                                                                        MLKRP900
       01  RT-REPORT-TOTAL-LINE-1.                                      MLKRP900
W11122     05  FILLER                  PIC  X(49)    VALUE SPACES.      MLKRP900
           05  FILLER                  PIC  X(14)    VALUE              MLKRP900
               '--------------'.                                                
W04232     05  FILLER                  PIC  X(43)    VALUE SPACES.      MLKRP900
           05  FILLER                  PIC  X(14)    VALUE              MLKRP900
               '--------------'.                                                
           05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP900
           05  FILLER                  PIC  X(14)    VALUE              MLKRP900
               '--------------'.                                                
           05  FILLER                  PIC  X(06)    VALUE SPACES.      MLKRP900
                                                                        MLKRP900
       01  RT-REPORT-TOTAL-LINE-2.                                      MLKRP900
           05  FILLER                  PIC  X(05)    VALUE SPACES.      MLKRP900
           05  FILLER                  PIC  X(13)    VALUE              MLKRP900
               ' GRAND TOTAL '.                                         MLKRP900
W11122     05  FILLER                  PIC  X(34)    VALUE SPACES.      MLKRP900
           05  RT-QUANTITY-TTL         PIC  ZZZ,ZZZ,ZZ9-                MLKRP900
                                                     VALUE ZERO.                
W11122     05  FILLER                  PIC  X(42)    VALUE SPACES.      MLKRP900
           05  RT-RETAIL-AMT-TTL       PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP900
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP900
           05  RT-COST-AMT-TTL         PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP900
                                                     VALUE ZERO.                
                                                                        MLKRP900
       01  RT-REPORT-SUB-TOTAL-LINE-1.                                  MLKRP900
W11122     05  FILLER                  PIC  X(49)    VALUE SPACES.      MLKRP900
           05  FILLER                  PIC  X(14)    VALUE              MLKRP900
               '--------------'.                                                
W11122     05  FILLER                  PIC  X(43)    VALUE SPACES.      MLKRP900
           05  FILLER                  PIC  X(14)    VALUE              MLKRP900
               '--------------'.                                                
           05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP900
           05  FILLER                  PIC  X(14)    VALUE              MLKRP900
               '--------------'.                                                
                                                                        MLKRP900
       01  RT-REPORT-SUB-TOTAL-LINE-2.                                  MLKRP900
           05  FILLER                  PIC  X(01)    VALUE SPACES.      MLKRP900
W11122     05  RT-DESCRIPTION          PIC  X(40)    VALUE SPACES.              
LY1003         88  RT-DEPT-TOTAL                     VALUE                      
LY1030         'DEPARTMENT TOTAL                        '.              MLKRP900
LY1003         88  RT-STORE-TOTAL                    VALUE                      
LY1030         '     STORE TOTAL                        '.              MLKRP900
LY1003         88  RT-GRAND-TOTAL                    VALUE                      
LY1030         '     GRAND TOTAL                        '.              MLKRP900
W11122     05  FILLER                  PIC  X(11)    VALUE SPACES.      MLKRP900
           05  RT-DEPT-QTY-TTL         PIC  ZZZ,ZZZ,ZZ9-                MLKRP900
                                                     VALUE ZERO.                
W11122     05  FILLER                  PIC  X(42)    VALUE SPACES.      MLKRP900
           05  RT-DEPT-RTL-TTL         PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP900
                                                     VALUE ZERO.                
           05  RT-DEPT-RTL-TTL-X REDEFINES RT-DEPT-RTL-TTL                      
                                       PIC  X(15).                              
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP900
           05  RT-DEPT-CST-TTL         PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP900
                                                     VALUE ZERO.                
           05  RT-DEPT-CST-TTL-X REDEFINES RT-DEPT-CST-TTL                      
                                       PIC  X(15).                              
           05  FILLER                  PIC  X(05)    VALUE SPACES.      MLKRP900
                                                                        MLKRP900
       01  RD-REPORT-DETAIL-LINE.                                       MLKRP900
           05  FILLER                            PIC X(02) VALUE SPACES.        
W04232     05  R1-D1-DEPT-NBR                    PIC 999.                       
W11122     05  FILLER                            PIC X(03) VALUE SPACES.        
21731      05  R1-D1-MAJ-CLASS.                                                 
21731          10  R1-D1-MAJ-CLASS-1             PIC 9.                         
21731          10  R1-D1-MAJ-CLASS-2             PIC 9.                         
W04232     05  FILLER                            PIC X(03) VALUE SPACES.        
W11122     05  R1-D1-CLASS-NBR                   PIC 99.                        
W04232     05  FILLER                            PIC X(02) VALUE SPACES.        
W11122     05  R1-D1-SKU                         PIC 9(08).                     
W11122     05  FILLER                            PIC X(02) VALUE SPACES.        
W04232     05  R1-SKU-DESCRIP                    PIC X(24).                     
W04232     05  FILLER                            PIC X(01) VALUE SPACES.        
           05  R1-D1-QTY                         PIC ZZZ,ZZZ,ZZ9.               
           05  FILLER                            PIC X(08) VALUE SPACES.        
           05  R1-D1-RTL-AMT                     PIC ZZZ,ZZZ,ZZZ.99-.           
           05  R1-D1-CST-AMT                     PIC ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                            PIC X(05) VALUE SPACES.        
           05  R1-D1-EXT-RTL-AMT                 PIC ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                            PIC X(03) VALUE SPACES.        
           05  R1-D1-EXT-CST-AMT                 PIC ZZZ,ZZZ,ZZZ.99-.           
           05  FILLER                            PIC X(07).                     
                                                                        MLKRP900
       01  ER-END-OF-REPORT-LINE.                                       MLKRP900
           05  FILLER                       PIC  X(55)    VALUE SPACES.         
           05  FILLER                       PIC  X(21)    VALUE         MLKRP900
                                                '*** END OF REPORT ***'.MLKRP900
           05  FILLER                       PIC  X(57)    VALUE SPACES.         
                                                                                
DB2   ******************************************************************        
DB2   *  DB2 FORMATTED MESSAGES                                        *        
DB2   ******************************************************************        
DB2        COPY DPWS004.                                                        
DB2                                                                             
DB2   ******************************************************************        
DB2   *  REQUIRED SQL COMMUNICATIONS AREA                                       
DB2   ******************************************************************        
DB2        EXEC SQL                                                             
DB2            INCLUDE SQLCA                                                    
DB2        END-EXEC.                                                            
DB2                                                                             
DB2   ******************************************************************        
           EXEC SQL                                                             
21731          INCLUDE TUPC                                                     
           END-EXEC                                                             
                                                                                
21731 ******************************************************************        
21731      EXEC SQL                                                             
21731          INCLUDE TUPCPLS                                                  
21731      END-EXEC                                                             
21731                                                                           
W11122***************************************************************** *       
 |         EXEC SQL                                                             
 |             INCLUDE TSKXREF                                                  
 |         END-EXEC                                                             
 |                                                                              
 |    ******************************************************************        
 |         EXEC SQL                                                             
 |             INCLUDE TMDSEAR                                                  
 |         END-EXEC                                                             
 |                                                                              
 |    ******************************************************************        
 |         EXEC SQL                                                             
 |             INCLUDE TAPLAVL                                                  
 |         END-EXEC                                                             
W11122                                                                          
DB2   ******************************************************************        
DB2   *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING                   
DB2   *  SQL CODES FOR DISPLAY                                                  
DB2   ******************************************************************        
DB2        COPY SQLCA2.                                                         
      *                                                                         
                                                                        MLKRP900
       LINKAGE SECTION.                                                 MLKRP900
                                                                        MLKRP900
                                                                        MLKRP900
       PROCEDURE DIVISION.                                              MLKRP900
                                                                        MLKRP900
       A100-MAIN-MODULE.                                                MLKRP900
                                                                        MLKRP900
           OPEN INPUT  TRANSFER-FILE                                    MLKRP900
LY1003                 RQ-REQUEST-CNTL-FILE                             MLKRP900
W11122          OUTPUT SUMMARY-REPORT                                   MLKRP900
                OUTPUT INSURANCE-REPORT.                                MLKRP900
                                                                        MLKRP900
           PERFORM B100-INITIALIZATION.                                 MLKRP900
                                                                        MLKRP900
           PERFORM C000-PROCESS-STORE                                   MLKRP900
               UNTIL SW-END-OF-INPUT-FILE.                              MLKRP900
                                                                        MLKRP900
           IF PV-CUM-QTY NOT = ZEROES                                           
               OR PV-CUM-EXT-RTL NOT = ZEROES                                   
               OR PV-CUM-EXT-CST NOT = ZEROES                                   
               PERFORM D000-PRINT-GRAND-TOTAL                                   
           END-IF.                                                              
           CLOSE TRANSFER-FILE                                          MLKRP900
LY1003           RQ-REQUEST-CNTL-FILE                                   MLKRP900
W11122           SUMMARY-REPORT                                         MLKRP900
                 INSURANCE-REPORT.                                      MLKRP900
                                                                        MLKRP900
           STOP RUN.                                                    MLKRP900
                                                                        MLKRP900
       B100-INITIALIZATION.                                             MLKRP900
                                                                        MLKRP900
W11122     MOVE FUNCTION CURRENT-DATE (3:2) TO DP150O-RUN-YEAR.         MLKRP800
W11122     MOVE FUNCTION CURRENT-DATE (5:2) TO DP150O-RUN-MONTH.        MLKRP800
W11122     MOVE FUNCTION CURRENT-DATE (7:2) TO DP150O-RUN-DAY.          MLKRP800
W11122     MOVE FUNCTION CURRENT-DATE (9:2) TO DP150O-RUN-HOUR.         MLKRP800
W11122     MOVE FUNCTION CURRENT-DATE (11:2) TO DP150O-RUN-MINUTE.      MLKRP800
W11122     MOVE PC-PROGRAM-NAME     TO DP150O-PROGRAM-NAME.             MLKRP800
W11122     MOVE 1                   TO DP150O-REPORT-NUMBER.            MLKRP800
                                                                        MLKRP900
LY1003     READ RQ-REQUEST-CNTL-FILE                                    MLKRP900
LY1003         INTO CC-CONTROL-CARD                                     MLKRP900
LY1003             AT END SET SW-END-OF-CNTL-FILE TO TRUE.              MLKRP900
LY1003                                                                  MLKRP900
           PERFORM Z100-READ-TRANSFER-FILE.                             MLKRP900
                                                                        MLKRP900
           MOVE +0 TO  PV-PAGE-COUNT.                                           
W11122     MOVE +0 TO  PV-PAGE-COUNT2.                                          
                                                                        MLKRP900
LY1003******************************************************************        
LY1003* READ AND PROCESS THE INPUT FILE FOR EACH STORE                          
LY1003******************************************************************        
LY1003 C000-PROCESS-STORE.                                                      
LY1003                                                                          
LY1003*    MOVE ML019-TRANSFER-FROM-STORE-9 TO RD-REPORT-TITLE-STORE            
LY1003*                                        PV-PREV-FROM-STORE.              
LY1003*                                        PV-PREV-FROM-STORE.              
W26006*    IF CC-OPTION-NO-AMOUNT                                               
W26006*       MOVE SPACES                  TO RD-REPORT-TITLE-STORE-X           
W26006*                                       PV-PREV-FROM-STORE-X              
W26006*    ELSE                                                                 
W26006     MOVE ML019-TRANSFER-FROM-STORE-9 TO RD-REPORT-TITLE-STORE            
W26006                                         PV-PREV-FROM-STORE.              
W26006*    END-IF.                                                              
W26600*                                                                         
LY1003     MOVE ML019-TRANSFER-DEPT-NBR       TO PV-PREV-DEPT-NBR.              
LY1003     MOVE ML019-TRANSFER-CLASS-NBR(1:1) TO PV-PREV-MAJ-CLASS-1.           
LY1003     MOVE ZEROES                        TO PV-LINE-COUNT                  
LY1003                                           PV-LINE-COUNT2.                
LY1003     PERFORM Z300-PRINT-RPT-HEADINGS.                                     
LY1003     PERFORM Z350-PRINT-RPT-HEADINGS.                                     
LY1003                                                                          
LY1003     PERFORM C100-PROCESS-INPUT-FILE                                      
LY1003         UNTIL SW-END-OF-INPUT-FILE                                       
LY1003*            OR ML019-TRANSFER-FROM-STORE-9                               
LY1003*                NOT = PV-PREV-FROM-STORE.                                
W26600             OR ML019-TRANSFER-FROM-STORE-9                               
W26600                 NOT = PV-PREV-FROM-STORE.                                
LY1003                                                                  MLKRP900
LY1003     PERFORM D150-PRINT-MAJ-CL-TOTAL.                                     
LY1003     PERFORM D100-PRINT-SECTION-TOTAL.                                    
LY1003                                                                          
LY1003     PERFORM D050-PRINT-STORE-TOTAL.                                      
LY1003                                                                          
LY1003                                                                          
      ******************************************************************MLKRP900
      * READ AND PROCESS THE INPUT FILE.                                MLKRP900
      * WRITE A REPORT DETAIL LINE FOR EACH INPUT RECORD.               MLKRP900
      ******************************************************************MLKRP900
       C100-PROCESS-INPUT-FILE.                                         MLKRP900
                                                                        MLKRP900
21731      PERFORM Z400-GET-COST-DESC.                                          
                                                                                
21731      IF ML019-TRANSFER-DEPT-NBR NOT = PV-PREV-DEPT-NBR            MLKRP900
LY1003         OR ML019-TRANSFER-CLASS-NBR(1:1)                                 
LY1003             NOT = PV-PREV-MAJ-CLASS-1                                    
W11122         PERFORM D150-PRINT-MAJ-CL-TOTAL                          MLKRP900
21731          MOVE ML019-TRANSFER-CLASS-NBR(1:1) TO PV-PREV-MAJ-CLASS-1MLKRP900
LY1003         IF ML019-TRANSFER-DEPT-NBR NOT = PV-PREV-DEPT-NBR                
LY1003             PERFORM D100-PRINT-SECTION-TOTAL                         MLKR
LY1003             MOVE ZEROES                 TO PV-LINE-COUNT                 
LY1003             MOVE ML019-TRANSFER-DEPT-NBR                         MLKRP900
LY1003                 TO PV-PREV-DEPT-NBR                              MLKRP900
LY1003         END-IF                                                           
           END-IF.                                                      MLKRP900
                                                                                
           IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                         MLKRP900
               OR PV-LINE-COUNT = 0                                             
               PERFORM Z300-PRINT-RPT-HEADINGS                          MLKRP900
           END-IF.                                                      MLKRP900
                                                                        MLKRP900
           MOVE SPACES                  TO RD-REPORT-DETAIL-LINE                
                                                                                
21731      MOVE ML019-TRANSFER-DEPT-NBR     TO R1-D1-DEPT-NBR.                  
21731      MOVE ML019-TRANSFER-CLASS-NBR(1:1) TO R1-D1-MAJ-CLASS-1.             
21731      MOVE ZEROES                      TO R1-D1-MAJ-CLASS-2.               
21731      MOVE ML019-TRANSFER-CLASS-NBR    TO R1-D1-CLASS-NBR.                 
21731      MOVE ML019-SKU                   TO R1-D1-SKU.                       
21731      MOVE ML019-TRANSFER-QTY          TO R1-D1-QTY.                       
                                                                                
LY1003     ADD  ML019-TRANSFER-QTY          TO PV-CUM-MAJ-QTY.                  
                                                                                
W04232     MOVE PV-SKU-DESC                  TO R1-SKU-DESCRIP.                 
LY1003     IF CC-OPTION-NO-AMOUNT                                               
W26600         MOVE SPACES TO RD-REPORT-TITLE-STORE-X                           
LY1003     ELSE                                                                 
LY1003         MOVE ML019-TRANSFER-PER-UNT-RTL-AMT TO R1-D1-RTL-AMT             
LY1003         MOVE PV-UNIT-COST-AMT             TO R1-D1-CST-AMT               
LY1003                                                                          
LY1003         COMPUTE PV-EXT-RTL-AMT =                                         
LY1003             ML019-TRANSFER-PER-UNT-RTL-AMT * ML019-TRANSFER-QTY          
LY1003                                                                          
LY1003         MOVE PV-EXT-RTL-AMT              TO R1-D1-EXT-RTL-AMT            
LY1003         ADD  PV-EXT-RTL-AMT              TO PV-CUM-MAJ-EXT-RTL           
LY1003                                                                          
LY1003         COMPUTE PV-EXT-CST-AMT =                                         
LY1003             PV-UNIT-COST-AMT * ML019-TRANSFER-QTY                        
LY1003         MOVE PV-EXT-CST-AMT              TO R1-D1-EXT-CST-AMT            
LY1003         ADD  PV-EXT-CST-AMT              TO PV-CUM-MAJ-EXT-CST           
LY1003     END-IF.                                                              
                                                                                
           MOVE RD-REPORT-DETAIL-LINE TO INSURANCE-REPORT-LINE.         MLKRP900
           PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP900
                                                                        MLKRP900
           PERFORM Z100-READ-TRANSFER-FILE.                             MLKRP900
                                                                        MLKRP900
                                                                        MLKRP900
LY1003 D000-PRINT-GRAND-TOTAL.                                          MLKRP900
LY1003                                                                          
LY1003     SET RT-GRAND-TOTAL               TO TRUE.                            
LY1003     MOVE PV-CUM-EXT-RTL          TO RT-DEPT-RTL-TTL.                     
LY1003     MOVE PV-CUM-EXT-CST          TO RT-DEPT-CST-TTL.                     
LY1003     MOVE PV-CUM-QTY              TO RT-DEPT-QTY-TTL.                     
LY1003                                                                          
LY1003     PERFORM D500-PRINT-TOTALS.                                           
LY1003                                                                          
LY1003     MOVE ER-END-OF-REPORT-LINE TO INSURANCE-REPORT-LINE.         MLKRP900
LY1003     MOVE 3 TO PV-ADVANCE.                                        MLKRP900
LY1003     PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP900
LY1003                                                                  MLKRP900
LY1003     MOVE ER-END-OF-REPORT-LINE TO SUMMARY-REPORT-LINE.           MLKRP900
LY1003     MOVE 3 TO PV-ADVANCE.                                        MLKRP900
LY1003     PERFORM Z250-WRITE-REPORT-LINE.                              MLKRP900
LY1003                                                                  MLKRP900
LY1003                                                                  MLKRP900
LY1003 D050-PRINT-STORE-TOTAL.                                          MLKRP900
LY1003                                                                          
LY1003     SET RT-STORE-TOTAL               TO TRUE.                            
LY1003     MOVE PV-CUM-STR-EXT-RTL          TO RT-DEPT-RTL-TTL.                 
LY1003     MOVE PV-CUM-STR-EXT-CST          TO RT-DEPT-CST-TTL.                 
LY1003     MOVE PV-CUM-STR-QTY              TO RT-DEPT-QTY-TTL.                 
LY1003                                                                          
LY1003*    PERFORM D500-PRINT-TOTALS.                                           
LY1003                                                                          
LY1003     ADD PV-CUM-STR-EXT-RTL      TO PV-CUM-EXT-RTL.                       
LY1003     ADD PV-CUM-STR-QTY          TO PV-CUM-QTY.                           
LY1003     ADD PV-CUM-STR-EXT-CST      TO PV-CUM-EXT-CST.                       
LY1003                                                                          
LY1003     MOVE ZEROES                 TO PV-CUM-STR-EXT-RTL                    
LY1003                                    PV-CUM-STR-QTY                        
LY1003                                    PV-CUM-STR-EXT-CST.                   
LY1003     MOVE 2  TO PV-ADVANCE.                                       MLKRP900
LY1003                                                                  MLKRP900
LY1003                                                                  MLKRP900
       D100-PRINT-SECTION-TOTAL.                                        MLKRP900
                                                                                
LY1003     SET RT-DEPT-TOTAL                TO TRUE.                            
LY1003     MOVE PV-CUM-DPT-EXT-RTL          TO RT-DEPT-RTL-TTL.                 
LY1003     MOVE PV-CUM-DPT-EXT-CST          TO RT-DEPT-CST-TTL.                 
           MOVE PV-CUM-DPT-QTY              TO RT-DEPT-QTY-TTL.                 
                                                                                
LY1003     PERFORM D500-PRINT-TOTALS.                                           
LY1003                                                                          
LY1003     ADD PV-CUM-DPT-EXT-RTL      TO PV-CUM-STR-EXT-RTL.                   
LY1003     ADD PV-CUM-DPT-QTY          TO PV-CUM-STR-QTY.                       
LY1003     ADD PV-CUM-DPT-EXT-CST      TO PV-CUM-STR-EXT-CST.                   
LY1003                                                                          
           MOVE ZEROES                 TO PV-CUM-DPT-EXT-RTL                    
                                          PV-CUM-DPT-QTY                        
                                          PV-CUM-DPT-EXT-CST.                   
LY1003     MOVE 2  TO PV-ADVANCE.                                       MLKRP900
LY1003                                                                  MLKRP900
                                                                                
W11122 D150-PRINT-MAJ-CL-TOTAL.                                         MLKRP900
W11122                                                                          
W11122     MOVE PV-PREV-DEPT-NBR            TO MDSEAR-DEPT-NBR.                 
 |         MOVE PV-PREV-MAJ-CLASS           TO MDSEAR-MAJ-CL-NBR.               
 |                                                                      MLKRP900
 |         EXEC SQL                                                             
 |             SELECT DISTINCT                                                  
 |                    DEPT_NBR                                                  
 |                  , MAJ_CL_NBR                                                
 |                  , MDSEAR_DESC                                               
 |             INTO :MDSEAR-DEPT-NBR                                            
 |                 ,:MDSEAR-MAJ-CL-NBR                                          
 |                 ,:MDSEAR-DESC                                                
 |             FROM TMDSEAR                                                     
 |             WHERE DEPT_NBR   = :MDSEAR-DEPT-NBR                              
 |               AND MAJ_CL_NBR = :MDSEAR-MAJ-CL-NBR                            
 |               AND MDSELV_CDE = 'MCL'                                         
 |         END-EXEC.                                                            
 |                                                                              
 |         EVALUATE TRUE                                                        
 |            WHEN SQLCODE = 0                                                  
 |                STRING '  ' DELIMITED BY SIZE                                 
 |                       MDSEAR-DEPT-NBR DELIMITED BY SIZE                      
 |                       '-' DELIMITED BY SIZE                                  
 |                       MDSEAR-MAJ-CL-NBR DELIMITED BY SIZE                    
 |                       ' ' DELIMITED BY SIZE                                  
 |                       MDSEAR-DESC DELIMITED BY SIZE                          
 |                                       INTO RT-DESCRIPTION                    
 |            WHEN OTHER                                                        
 |                STRING '  ' DELIMITED BY SIZE                                 
 |                       MDSEAR-DEPT-NBR DELIMITED BY SIZE                      
 |                       '-' DELIMITED BY SIZE                                  
 |                       MDSEAR-MAJ-CL-NBR DELIMITED BY SIZE                    
 |                       ' *********' DELIMITED BY SIZE                         
 |                                       INTO RT-DESCRIPTION                    
 |         END-EVALUATE.                                                        
W11122                                                                          
LY1003     MOVE PV-CUM-MAJ-EXT-RTL          TO RT-DEPT-RTL-TTL.                 
LY1003     MOVE PV-CUM-MAJ-EXT-CST          TO RT-DEPT-CST-TTL.                 
W11122     MOVE PV-CUM-MAJ-QTY              TO RT-DEPT-QTY-TTL.                 
W11122                                                                          
LY1003     PERFORM D500-PRINT-TOTALS.                                           
LY1003                                                                          
LY1003     ADD PV-CUM-MAJ-EXT-RTL      TO PV-CUM-DPT-EXT-RTL.                   
LY1003     ADD PV-CUM-MAJ-QTY          TO PV-CUM-DPT-QTY.                       
LY1003     ADD PV-CUM-MAJ-EXT-CST      TO PV-CUM-DPT-EXT-CST.                   
LY1003                                                                          
W11122     MOVE ZEROES                 TO PV-CUM-MAJ-EXT-RTL                    
 |                                        PV-CUM-MAJ-QTY                        
 |                                        PV-CUM-MAJ-EXT-CST.                   
LY1003                                                                  MLKRP900
LY1003     MOVE 2  TO PV-ADVANCE.                                       MLKRP900
LY1003                                                                  MLKRP900
W11122                                                                  MLKRP900
LY1003 D500-PRINT-TOTALS.                                                       
LY1003                                                                          
LY1003* THIS SECTION NEEDS 3+ADDITIONAL LINES TO COMPLETE, CHECK                
LY1003* IF LINE WILL EXCEED THE MAX LINES PER PAGE.                             
LY1003     IF PV-LINE-COUNT + 3 + PV-ADVANCE > PC-MAX-LINE-COUNT        MLKRP900
LY1003         OR PV-LINE-COUNT = 0                                             
LY1003         PERFORM Z300-PRINT-RPT-HEADINGS                          MLKRP900
LY1003     END-IF.                                                      MLKRP900
LY1003                                                                          
LY1003     MOVE RT-REPORT-SUB-TOTAL-LINE-1  TO INSURANCE-REPORT-LINE.   MLKRP900
LY1003     PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP900
LY1003                                                                  MLKRP900
LY1003     IF CC-OPTION-NO-AMOUNT                                               
LY1003         MOVE SPACES                      TO RT-DEPT-RTL-TTL-X            
LY1003                                             RT-DEPT-CST-TTL-X            
LY1003     END-IF.                                                              
LY1003                                                                          
LY1003     MOVE RT-REPORT-SUB-TOTAL-LINE-2  TO INSURANCE-REPORT-LINE.   MLKRP900
LY1003     PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP900
LY1003                                                                  MLKRP900
LY1003     IF PV-LINE-COUNT2 + 2 + PV-ADVANCE > PC-MAX-LINE-COUNT       MLKRP900
LY1003         OR PV-LINE-COUNT = 0                                             
LY1003         PERFORM Z350-PRINT-RPT-HEADINGS                          MLKRP900
LY1003     END-IF.                                                      MLKRP900
LY1003                                                                          
LY1003     MOVE RT-REPORT-SUB-TOTAL-LINE-2  TO SUMMARY-REPORT-LINE.     MLKRP900
LY1003     PERFORM Z250-WRITE-REPORT-LINE.                              MLKRP900
LY1003                                                                  MLKRP900
LY1003     IF RT-DEPT-TOTAL                                                     
LY1003         MOVE SPACES                  TO SUMMARY-REPORT-LINE      MLKRP900
LY1003         PERFORM Z250-WRITE-REPORT-LINE                           MLKRP900
LY1003     END-IF.                                                              
LY1003                                                                          
       Z100-READ-TRANSFER-FILE.                                         MLKRP900
                                                                        MLKRP900
           READ TRANSFER-FILE                                           MLKRP900
21731          INTO ML019-TRANSFER-RECORD                               MLKRP900
                   AT END SET SW-END-OF-INPUT-FILE TO TRUE.             MLKRP900
                                                                        MLKRP900
       Z200-WRITE-REPORT-LINE.                                          MLKRP900
                                                                        MLKRP900
           WRITE INSURANCE-REPORT-LINE                                  MLKRP900
               AFTER ADVANCING PV-ADVANCE.                              MLKRP900
           ADD PV-ADVANCE TO PV-LINE-COUNT.                             MLKRP900
LY1003     MOVE 1  TO PV-ADVANCE.                                       MLKRP900
                                                                        MLKRP900
W11122 Z250-WRITE-REPORT-LINE.                                          MLKRP900
 |                                                                      MLKRP900
 |         WRITE SUMMARY-REPORT-LINE                                    MLKRP900
 |             AFTER ADVANCING PV-ADVANCE.                              MLKRP900
W11122     ADD PV-ADVANCE TO PV-LINE-COUNT2.                            MLKRP900
LY1003     MOVE 1  TO PV-ADVANCE.                                       MLKRP900
W11122                                                                  MLKRP900
      ******************************************************************MLKRP900
      * PRINT THE REPORT HEADING LINES.                                 MLKRP900
      ******************************************************************MLKRP900
       Z300-PRINT-RPT-HEADINGS.                                         MLKRP900
                                                                        MLKRP900
W11122     MOVE 1                   TO DP150O-REPORT-NUMBER.            MLKRP800
W11122                                                                          
           ADD 1 TO PV-PAGE-COUNT.                                      MLKRP900
W11122     MOVE PV-PAGE-COUNT TO DP150O-PAGE-NUMBER.                    MLKRP900
           WRITE INSURANCE-REPORT-LINE                                  MLKRP900
W11122         FROM DP150O-STANDRD-HEADER-150-COLS                      MLKRP900
               AFTER ADVANCING PAGE.                                    MLKRP900
           MOVE +1 TO PV-LINE-COUNT.                                    MLKRP900
                                                                        MLKRP900
           WRITE INSURANCE-REPORT-LINE FROM RD-REPORT-TITLE-LINE        MLKRP900
               AFTER ADVANCING 1 LINES.                                 MLKRP900
                                                                        MLKRP900
           WRITE INSURANCE-REPORT-LINE FROM RD-REPORT-HDR-LINE3         MLKRP900
               AFTER ADVANCING 1 LINES.                                 MLKRP900
           WRITE INSURANCE-REPORT-LINE FROM RD-REPORT-HDR-LINE4         MLKRP900
               AFTER ADVANCING 1 LINES.                                 MLKRP900
LY1003     ADD +3 TO PV-LINE-COUNT.                                     MLKRP900
           MOVE 2  TO PV-ADVANCE.                                       MLKRP900
                                                                        MLKRP900
W11122******************************************************************MLKRP900
 |    * PRINT THE REPORT HEADING LINES FOR REPORT 2.                    MLKRP900
 |    ******************************************************************MLKRP900
 |     Z350-PRINT-RPT-HEADINGS.                                         MLKRP900
 |                                                                      MLKRP900
 |         MOVE 2                   TO DP150O-REPORT-NUMBER.            MLKRP800
 |                                                                              
 |         ADD 1 TO PV-PAGE-COUNT2.                                     MLKRP900
 |         MOVE PV-PAGE-COUNT2 TO DP150O-PAGE-NUMBER.                   MLKRP900
 |         WRITE SUMMARY-REPORT-LINE                                    MLKRP900
 |             FROM DP150O-STANDRD-HEADER-150-COLS                      MLKRP900
 |             AFTER ADVANCING PAGE.                                    MLKRP900
 |         MOVE +1 TO PV-LINE-COUNT2.                                   MLKRP900
 |                                                                      MLKRP900
 |         WRITE SUMMARY-REPORT-LINE FROM RD-REPORT-TITLE-LINE          MLKRP900
 |             AFTER ADVANCING 1 LINES.                                 MLKRP900
 |                                                                      MLKRP900
 |         WRITE SUMMARY-REPORT-LINE FROM RD-REPORT-HDR-LINE3           MLKRP900
 |             AFTER ADVANCING 1 LINES.                                 MLKRP900
 |         WRITE SUMMARY-REPORT-LINE FROM RD-REPORT-HDR-LINE4           MLKRP900
 |             AFTER ADVANCING 1 LINES.                                 MLKRP900
LY1003     ADD +3 TO PV-LINE-COUNT2.                                    MLKRP900
W11122     MOVE 2  TO PV-ADVANCE.                                       MLKRP900
W11122                                                                  MLKRP900
      ******************************************************************MLKRP900
21731 * GET COST AND DESC FROM TUPC AND TUPCPLS                         MLKRP900
      ******************************************************************MLKRP900
21731  Z400-GET-COST-DESC.                                              MLKRP900
                                                                                
21731          MOVE ML019-SKU                TO SKXREF-SKU.                     
                                                                                
01020          EXEC SQL                                                         
21731              SELECT  B.UNT_COST_AMT                                       
21731                     ,C.INTR_UPC_DESC                                      
21731              INTO  :UPCPLS-UNT-COST-AMT                                   
21731                   ,:UPC-INTR-UPC-DESC                                     
21731              FROM  TSKXREF A                                              
21731                   ,TUPCPLS B                                              
21731                   ,TUPC    C                                              
21731              WHERE A.SKU            = :SKXREF-SKU                         
21731                AND B.ITM_NBR        = A.ITM_NBR                           
21731                AND B.OPERCO_NBR     = :PC-OPERCO-NBR                      
21731                AND B.STR_NBR        = :PC-CORP-STORE-000                  
21731                AND B.EFF_BEG_TMST  <= CURRENT TIMESTAMP                   
21731                AND (B.EFF_END_TMST >= CURRENT TIMESTAMP OR                
21731                     B.EFF_END_TMST IS NULL)                               
21731                AND C.UPC_ID         = B.UPC_ID                            
21731                AND C.EFF_BEG_DTE   <= CURRENT DATE                        
21731                AND (C.EFF_END_DTE   >= CURRENT DATE OR                    
21731                     C.EFF_END_DTE  IS NULL)                               
01035          END-EXEC                                                         
01036                                                                           
01037          EVALUATE TRUE                                                    
01038              WHEN SQLCODE = 0                                             
21731                  MOVE UPCPLS-UNT-COST-AMT TO PV-UNIT-COST-AMT             
21731                  MOVE UPC-INTR-UPC-DESC   TO PV-SKU-DESC                  
01038              WHEN SQLCODE = +100                                          
01039                  MOVE ZERO              TO PV-UNIT-COST-AMT               
W11122                 PERFORM Z450-CHECK-MBS                                   
01044              WHEN OTHER                                                   
21731                  DISPLAY '  SKU: ' SKXREF-SKU                             
21731                  MOVE 'Z400-GET-COST-DESC' TO                             
01046                                          PV-PARAGRAPH-NAME                
21731                  MOVE 'ERROR SELECTING UPC TBL' TO                        
01048                                          PV-DB2-OPER-ATTEMPTED            
01049                  PERFORM Z900-SQL-ABEND                                   
01050          END-EVALUATE.                                                    
01052                                                                           
W11122******************************************************************MLKRP900
21731 * CHECK MBS FOR VALID DEPT AND CLASS                              MLKRP900
W11122******************************************************************MLKRP900
 |     Z450-CHECK-MBS.                                                  MLKRP900
W11122                                                                          
21731      MOVE ML019-TRANSFER-DEPT-NBR     TO MDSEAR-DEPT-NBR.                 
21731      MOVE ML019-TRANSFER-CLASS-NBR    TO PV-SUB-CL-NBR-2-3.       MLKUT002
W11122     MOVE '0'                         TO PV-SUB-CL-NBR-1.         MLKUT002
 |         MOVE PV-SUB-CL-NBR               TO MDSEAR-SUB-CL-NBR.               
 |                                                                      MLKRP900
 |         EXEC SQL                                                             
 |                     SELECT     A.DEPT_NBR                            MLKUT002
 |                          ,     A.SUB_CL_NBR                          MLKUT002
 |                          ,     MDSEAR_DESC                                   
 |                       INTO    :MDSEAR-DEPT-NBR                       MLKUT002
 |                          ,    :MDSEAR-SUB-CL-NBR                     MLKUT002
 |                          ,    :MDSEAR-DESC                           MLKUT002
 |                       FROM     TMDSEAR A                             MLKUT002
 |                          ,     TAPLAVL B                             MLKUT002
 |                      WHERE    (A.DEPT_NBR    = :MDSEAR-DEPT-NBR)     MLKUT002
 |                        AND    (A.SUB_CL_NBR  = :MDSEAR-SUB-CL-NBR)   MLKUT002
 |                        AND    (A.MDSELV_CDE  = :PC-SUB-CLASS)        MLKUT002
 |                        AND    (A.OPERCO_NBR  = :PC-OPERCO-NBR)       MLKUT002
 |                        AND    (B.APLSYS_CDE  = :PC-APLSYS-CDE)       MLKUT002
 |                        AND    (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)       MLKUT002
 |                        AND    (CURRENT DATE     BETWEEN              MLKUT002
 |                                CHAR(B.MDSEAR_STRT_DTE,ISO)  AND      MLKUT002
 |                                CHAR(B.MDSEAR_END_DTE,ISO))           MLKUT002
 |                        AND    (CURRENT DATE     BETWEEN              MLKUT002
 |                                CHAR(A.STRT_DTE,ISO)         AND      MLKUT002
 |                                CHAR(A.END_DTE,ISO))                  MLKUT002
 |         END-EXEC.                                                            
 |                                                                              
 |         EVALUATE TRUE                                                        
 |            WHEN SQLCODE = 0                                                  
 |                MOVE MDSEAR-DESC  TO PV-SKU-DESC                              
 |            WHEN OTHER                                                        
 |                MOVE '*****'      TO PV-SKU-DESC                              
 |         END-EVALUATE.                                                        
 |                                                                              
W11122                                                                  MLKRP900
01059 ******************************************************************        
01060 *  STANDARD DB2 ERROR ABEND ROUTINE                                       
01061 *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
01062 ******************************************************************        
01063  Z900-SQL-ABEND.                                                          
01064                                                                           
01065      MOVE +4013                 TO ABEND-CODE.                            
01066      DISPLAY '**  ABEND     **'.                                          
21731      DISPLAY '**  PROGRAM   **  =  MLKRP900'.                             
01068      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
01069      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
01070                                                                           
01071 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
01072 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
01073                                                                           
01074      COPY DPPD004.                                                        
01075                                                                           
01076      CALL 'ILBOABN0'  USING ABEND-CODE.                                   
