00001 ******************************************************************08/16/94
00002  IDENTIFICATION DIVISION.                                         ETKCS322
00003 ******************************************************************   LV010
00004  PROGRAM-ID.    ETKCS322.                                         ETKCS322
00005  AUTHOR.        TROY BIRK.                                        ETKCS322
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          ETKCS322
00007  DATE-WRITTEN.  JUNE 1994.                                        ETKCS322
00008  DATE-COMPILED.                                                   ETKCS322
00009 ******************************************************************   CL**9
00010 * PROGRAM NUMBER: ETKCS322       SYSTEM: ET     LANGUAGE: COBOL II   CL**9
00011 *                                                                    CL**9
00012 * TRANID:  E322   MNEMONIC:  ETSDOCXM           MAP: ETKM322         CL**9
00013 *                                                                    CL**9
00014 * PROGRAM NAME : EDI DOCUMENT TRANS SELECTION                        CL**9
00015 *                                                                    CL**9
00016 * ORIGINAL WORK REQUEST #: 2026                                      CL**9
00017 *                                                                    CL**9
00018 * PROGRAM OBJECTIVE:                                                 CL**9
00019 *                                                                    CL**9
00020 *   TO ALLOW THE USER TO SPECIFY VARIOUS CRITERIA THAT WILL BE       CL**9
00021 *   USED TO DETERMINE WHICH INTERCHANGE TRANSMISSION WILL BE         CL**9
00022 *   LISTED BY THE  INTERCHANGE TRANSMISSION LIST (ETKCS321).         CL**9
00023 *                                                                    CL**9
00024 * DATASETS USED: NONE                                                CL**9
00025 *  DSN                                 COPY USED  I/O  VSAM?  DB?    CL**9
00026 * --------------------------------    ----------- ---- -----  ---    CL**9
00027 *                                                                    CL**9
00028 * PROGRAM NARRATIVE:                                                 CL**9
00029 *                                                                    CL**9
00030 *   THIS SCREEN IS ACCESSED FROM THE ETMENU. THE USER IS REQUIRED    CL**9
00031 *   TO ENTER A DOCUMENT DETAIL NUMBER, A TRANSACTION SET, AND        CL**9
00032 *   A DIRECTION. THE DIRECTION MUST BE AN 'S' OR 'R' AND THE         CL**9
00033 *   TRANSACTION SET WILL BE VALIDATED IN ETKCS321.                   CL**9
00034 *                                                                    CL**9
00035 *   ANYTHING MAY BE ENTERED FOR THE DOCUMENT DETAIL, BUT AN EXACT    CL**9
00036 *   MATCH MUST BE FOUND ON THE DATABASE. HOWEVER, THE USER CAN       CL**9
00037 *   ALSO ENTER ASTERISKS AS A WILD CARD CHARACTER.                   CL**9
00038 *   ONCE ALL FIELDS HAVE VALID DATA ENTERED, THE USER CAN HIT        CL**9
00039 *   PF23 TO INVOKE THE INTERCHANGE TRANMISSION LIST.                 CL**9
00040 *                                                                    CL**9
00041 * CONTROL CARD REQUIREMENTS: N/A                                     CL**9
00042 * JOBS PROGRAM IS FOUND IN:  N/A                                     CL**9
00043 * REPORTS PRODUCED:          N/A                                     CL**9
00044 *                                                                    CL**9
00045 *****************************************************************    CL**9
CICS  *  CHANGED 10/26/2022    BY BARB SCHULTZ    WR: CHG0277210           CL**9
CICS  *                                                                    CL**9
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL        CL**9
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.         CL**9
00052 ******************************************************************ETKCS322
00053                                                                   ETKCS322
00054  ENVIRONMENT DIVISION.                                            ETKCS322
00055                                                                   ETKCS322
00056  DATA DIVISION.                                                   ETKCS322
00057                                                                   ETKCS322
00058  WORKING-STORAGE SECTION.                                         ETKCS322
00059 *                                                                 ETKCS322
00060  01  PC-PROGRAM-CONSTANTS.                                        ETKCS322
00061      05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'ET322A  '. ETKCS322
00062      05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'ETKM322 '. ETKCS322
00063      05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'ETKCS322'. ETKCS322
00064      05  PC-TSYMSG-NUMBERS.                                       ETKCS322
00065          10  PC-TSYMSG-00005       PIC  9(05)  VALUE  00005.      ETKCS322
00066          10  PC-TSYMSG-00007       PIC  9(05)  VALUE  00007.      ETKCS322
00067          10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.      ETKCS322
00068          10  PC-TSYMSG-00010       PIC  9(05)  VALUE  00010.      ETKCS322
00069          10  PC-TSYMSG-00017       PIC  9(05)  VALUE  00017.      ETKCS322
00070          10  PC-TSYMSG-00137       PIC  9(05)  VALUE  00137.      ETKCS322
00071          10  PC-TSYMSG-00146       PIC  9(05)  VALUE  00146.      ETKCS322
00072          10  PC-TSYMSG-00550       PIC  9(05)  VALUE  00550.      ETKCS322
00073          10  PC-TSYMSG-00551       PIC  9(05)  VALUE  00551.      ETKCS322
00074          10  PC-TSYMSG-00718       PIC  9(05)  VALUE  00718.      ETKCS322
00075      05  PC-CALENDAR-ROUTINE       PIC  X(08)  VALUE  'DPKUT500'. ETKCS322
00076 *                                                                 ETKCS322
00077  01  PS-PROGRAM-SWITCHES.                                         ETKCS322
00078      05  PS-ERROR-SW               PIC  X      VALUE  'N'.        ETKCS322
00079          88  PS-ERROR                          VALUE  'Y'.        ETKCS322
00080          88  PS-NO-ERROR                       VALUE  'N'.        ETKCS322
00081                                                                   ETKCS322
00082  01  PV-PROGRAM-VARIABLES.                                        ETKCS322
00083      05  PV-ASTERISK-CNT           PIC S9(04)  VALUE +0           ETKCS322
00084                                                COMP SYNC.         ETKCS322
00085      05  PV-30-CHAR-FIELD                 PIC X(30)  VALUE SPACES.   CL**7
00086      05  PV-TALLY                         PIC S9(04) COMP            CL**7
00087                                                      VALUE +0.       CL**7
00088                                                                   ETKCS322
00089 ******************************************************************ETKCS322
00090 *    MAP LAYOUT                                                  *ETKCS322
00091 ******************************************************************ETKCS322
00092                                                                   ETKCS322
00093      COPY ETKM322.                                                ETKCS322
00094      EJECT                                                        ETKCS322
00095 ******************************************************************ETKCS322
00096 *  EXTENDED ATTIBUTES COPYBOOK                                   *ETKCS322
00097 ******************************************************************ETKCS322
00098                                                                   ETKCS322
00099      COPY DPWS015.                                                ETKCS322
00100                                                                   ETKCS322
00101 ******************************************************************ETKCS322
00102 *  FUNCTION KEYS  COPYBOOK                                       *ETKCS322
00103 ******************************************************************ETKCS322
00104                                                                   ETKCS322
00105      COPY DPWS016.                                                ETKCS322
00106                                                                   ETKCS322
00107 ******************************************************************ETKCS322
00108 *    COPY BOOK FOR NUMERIC EDIT SUBROUTINE (DPKUT100) PARAMETER  *ETKCS322
00109 *    LIST.                                                       *ETKCS322
00110 ******************************************************************ETKCS322
00111                                                                   ETKCS322
00112      COPY DPWS010I.                                               ETKCS322
00113                                                                   ETKCS322
00114 ******************************************************************ETKCS322
00115 *    COPY BOOK FOR CURRENT SYSTEM DATE/TIME PROCEDURE DIVISION   *ETKCS322
00116 *    ROUTINE                                                      ETKCS322
00117 ******************************************************************ETKCS322
00118                                                                   ETKCS322
00119      COPY DPWS017.                                                ETKCS322
00120                                                                   ETKCS322
00121 ******************************************************************ETKCS322
CICS  *  PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.            **ETKCS322
00124 ******************************************************************ETKCS322
00125      COPY DPWS030.                                                ETKCS322
CICS       COPY DPWS930.                                                ETKCS322
00126                                                                   ETKCS322
00127 ***************************************************************** ETKCS322
00128 *    STANDARD COMMAREA.                                           ETKCS322
00129 ***************************************************************** ETKCS322
00130                                                                   ETKCS322
00131      COPY DPWS020.                                                ETKCS322
00132      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                ETKCS322
00133                                                                   ETKCS322
00134 ***************************************************************** ETKCS322
00135 *    INTER-APPLICATION COMMAREA.                                  ETKCS322
00136 ***************************************************************** ETKCS322
00137                                                                   ETKCS322
00138          10  INTER-APPL-COMM-AREA PIC X(200).                     ETKCS322
00139                                                                   ETKCS322
00140      COPY ETWS307.                                                   CL*10
00141 ***************************************************************** ETKCS322
00142 *    SPECIFIC COMMMAREA FOR ETKCS322.                             ETKCS322
00143 ***************************************************************** ETKCS322
00144          10  ASC-SPECIFIC-COMMAREA.                               ETKCS322
00145              15  ASC-DOC-DTL                 PIC  X(30).             CL**2
00146              15  ASC-TRAN-SET                PIC  X(03).             CL**2
00147              15  ASC-DIRECTION-IND           PIC X(01).              CL**5
00148              15  FILLER                      PIC  X(2831).           CL**5
00149      EJECT                                                        ETKCS322
00150 ***************************************************************** ETKCS322
00151 *   ABEND COPYBOOK.                                               ETKCS322
00152 ***************************************************************** ETKCS322
00153      COPY DPWS013.                                                ETKCS322
00154                                                                   ETKCS322
00155                                                                   ETKCS322
00156  EJECT                                                            ETKCS322
00157  LINKAGE SECTION.                                                 ETKCS322
00158                                                                   ETKCS322
00159  01  DFHCOMMAREA.                                                 ETKCS322
00160      05  FILLER                         OCCURS  1 TO 4072 TIMES   ETKCS322
00161                                         DEPENDING ON EIBCALEN.    ETKCS322
00162          10  FILLER                     PIC X.                    ETKCS322
00163                                                                   ETKCS322
00164                                                                   ETKCS322
00165  PROCEDURE DIVISION.                                              ETKCS322
00166 ***************************************************************** ETKCS322
00167 ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** ETKCS322
00168 ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** ETKCS322
00169 ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** ETKCS322
00170 **                                                             ** ETKCS322
00171 ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** ETKCS322
00172 ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** ETKCS322
00173 ***************************************************************** ETKCS322
00174  0000-MAIN-MODULE.                                                ETKCS322
00175      INITIALIZE DP030-CICS-API-FIELDS.                            ETKCS322
00176      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       ETKCS322
00177      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          ETKCS322
00178      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         ETKCS322
00179      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       ETKCS322
00180      MOVE LENGTH OF ET322AI        TO DP030-MAP-LENGTH (1).       ETKCS322
00181      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     ETKCS322
00182      PERFORM 0001-CALL-CICS-ARCH-API.                             ETKCS322
00183 *                                                                 ETKCS322
00184      PERFORM 1000-CONTROL-PROCESSING.                             ETKCS322
00185 *                                                                 ETKCS322
00186      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     ETKCS322
00187      PERFORM 0001-CALL-CICS-ARCH-API.                             ETKCS322
00188                                                                   ETKCS322
00189  0001-CALL-CICS-ARCH-API.                                         ETKCS322
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                      ETKCS322
00191                                     DFHCOMMAREA                   ETKCS322
00192                                     DP030-CICS-API-FIELDS         ETKCS322
00193                                     DP020-STANDARD-COMMAREA       ETKCS322
00194                                     ET322AI.                      ETKCS322
00195 *                                                                 ETKCS322
00196      IF  DP030-RC-CALL-SUCCESSFUL                                 ETKCS322
00197          CONTINUE                                                 ETKCS322
00198      ELSE                                                         ETKCS322
00199          SET DP013-NO-ROLLBACK                                    ETKCS322
00200              DP013-XCTL-DISPLAY-RESTART                           ETKCS322
00201              DP013-CICS-ABEND      TO TRUE                        ETKCS322
00202          MOVE 'BEFORE 0000-MAIN-MODULE'                           ETKCS322
00203                                    TO DP013-PARAGRAPH             ETKCS322
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE OETKCS322
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)      ETKCS322
00206          MOVE DP030-RETURN-CODE                                   ETKCS322
00207                                    TO DP013-MESSAGE-TEXT (3)      ETKCS322
00208          PERFORM DP013-0000-PROCESS-ABEND                         ETKCS322
00209      END-IF.                                                      ETKCS322
00210  EJECT                                                            ETKCS322
00211 ******************************************************************ETKCS322
00212 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *ETKCS322
00213 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *ETKCS322
00214 ******************************************************************ETKCS322
00215                                                                   ETKCS322
00216  1000-CONTROL-PROCESSING.                                         ETKCS322
00217                                                                   ETKCS322
00218      EVALUATE TRUE                                                ETKCS322
00219          WHEN DP020-NEXT-ACT-INITIAL                              ETKCS322
00220              INITIALIZE ASC-SPECIFIC-COMMAREA                     ETKCS322
00221              PERFORM 4000-BUILD-INITIAL-PANEL                     ETKCS322
00222                                                                   ETKCS322
00223          WHEN DP020-NEXT-ACT-READ-MAP                             ETKCS322
00224              PERFORM 2000-FKEY-PROCESSING                         ETKCS322
00225                                                                   ETKCS322
00226          WHEN DP020-NEXT-ACT-RETURN                               ETKCS322
00227              MOVE -1                     TO ADOCDTLL                 CL**4
00228              SET DP030-SET-CURSOR-APPL-1 TO TRUE                  ETKCS322
00229              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   ETKCS322
00230              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 ETKCS322
00231                                                                   ETKCS322
00232          WHEN OTHER                                               ETKCS322
00233              SET DP013-LOGIC-ABEND                                ETKCS322
00234                  DP013-NO-ROLLBACK TO TRUE                        ETKCS322
00235              MOVE '1000-CONTROL-PROCESSING'                       ETKCS322
00236                                    TO  DP013-PARAGRAPH            ETKCS322
00237              MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   ETKCS322
00238                                    TO  DP013-MESSAGE-TEXT(1)      ETKCS322
00239              MOVE DP020-NEXT-APPL-ACTIVITY                        ETKCS322
00240                                    TO DP013-MESSAGE-TEXT(2)       ETKCS322
00241              PERFORM DP013-0000-PROCESS-ABEND                     ETKCS322
00242      END-EVALUATE.                                                ETKCS322
00243                                                                   ETKCS322
00244 ******************************************************************ETKCS322
00245 * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*ETKCS322
00246 * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*ETKCS322
00247 * EDITS.                                                         *ETKCS322
00248 ******************************************************************ETKCS322
00249                                                                   ETKCS322
00250  2000-FKEY-PROCESSING.                                            ETKCS322
00251      EVALUATE TRUE                                                ETKCS322
00252                                                                   ETKCS322
00253          WHEN DP020-SRC-AID = DP016-CLEAR                         ETKCS322
00254              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   ETKCS322
00255              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 ETKCS322
00256                                                                   ETKCS322
00257          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     ETKCS322
00258              INITIALIZE ASC-SPECIFIC-COMMAREA                     ETKCS322
00259              PERFORM 4000-BUILD-INITIAL-PANEL                     ETKCS322
00260              PERFORM 3200-RESET-ATTRIBUTES                        ETKCS322
00261                                                                   ETKCS322
00262          WHEN OTHER                                               ETKCS322
00263              PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 ETKCS322
00264              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   ETKCS322
00265              IF  PS-NO-ERROR                                      ETKCS322
00266                  SET DP030-CONTINUE-GO TO TRUE                    ETKCS322
00267                  PERFORM 2100-CHECK-FUNCTION-KEY                  ETKCS322
00268              ELSE                                                 ETKCS322
00269                  SET DP030-OVERRIDE-GO TO TRUE                    ETKCS322
00270                  PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             ETKCS322
00271              END-IF                                               ETKCS322
00272      END-EVALUATE.                                                ETKCS322
00273                                                                   ETKCS322
00274                                                                   ETKCS322
00275 ******************************************************************ETKCS322
00276 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED       *ETKCS322
00277 * FIRST. NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED    *ETKCS322
00278 * FROM THE CICS ARCHITECTURE API.                                *ETKCS322
00279 ******************************************************************ETKCS322
00280                                                                   ETKCS322
00281  2100-CHECK-FUNCTION-KEY.                                         ETKCS322
00282      EVALUATE TRUE                                                ETKCS322
00283          WHEN DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                 ETKCS322
00284              PERFORM 2110-PREPARE-INTER-APPL-COMM                 ETKCS322
00285                                                                   ETKCS322
00286          WHEN DP020-SRC-AID = DP016-ENTER                         ETKCS322
00287              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 ETKCS322
00288                                                                   ETKCS322
00289          WHEN OTHER                                               ETKCS322
00290              SET DP013-NO-ROLLBACK                                ETKCS322
00291                  DP013-XCTL-DISPLAY-RESTART                       ETKCS322
00292                  DP013-CICS-ABEND  TO TRUE                        ETKCS322
00293              MOVE '2100-CHECK-FUNCTION-KEY'                       ETKCS322
00294                                    TO DP013-PARAGRAPH             ETKCS322
00295              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      ETKCS322
00296                                    TO DP013-MESSAGE-TEXT (1)      ETKCS322
00297              PERFORM DP013-0000-PROCESS-ABEND                     ETKCS322
00298      END-EVALUATE.                                                ETKCS322
00299                                                                   ETKCS322
00300                                                                   ETKCS322
00301 ******************************************************************ETKCS322
00302 ** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **ETKCS322
00303 ** INTER-APPLICATION COMMAREA.                                  **ETKCS322
00304 ******************************************************************ETKCS322
00305                                                                   ETKCS322
00306  2110-PREPARE-INTER-APPL-COMM.                                    ETKCS322
00307      MOVE ASC-DOC-DTL     TO ET307-DOC-DTL.                          CL**2
00308      MOVE ASC-TRAN-SET    TO ET307-TRAN-SET.                         CL**4
00309      MOVE ASC-DIRECTION-IND    TO ET307-DIRECTION-IND.               CL**5
00310                                                                   ETKCS322
00311 ******************************************************************ETKCS322
00312 *                                                                 ETKCS322
00313 ******************************************************************ETKCS322
00314                                                                   ETKCS322
00315  2200-MOVE-SCREEN-TO-COMMAREA.                                    ETKCS322
00316                                                                   ETKCS322
00317      IF  ADOCDTLL > ZERO                                             CL**2
00318          MOVE ADOCDTLI       TO ASC-DOC-DTL                          CL**2
00319      ELSE                                                         ETKCS322
00320          IF  ADOCDTLF = DP015-ERASE-EOF                              CL**2
00321              MOVE SPACE      TO ASC-DOC-DTL                          CL**2
00322          END-IF                                                   ETKCS322
00323      END-IF.                                                      ETKCS322
00324                                                                   ETKCS322
00325      IF  ATSETL > ZERO                                               CL**2
00326          MOVE ATSETI         TO ASC-TRAN-SET                         CL**2
00327      ELSE                                                         ETKCS322
00328          IF  ATSETF = DP015-ERASE-EOF                                CL**2
00329              MOVE SPACE      TO ASC-TRAN-SET                         CL**2
00330          END-IF                                                   ETKCS322
00331      END-IF.                                                      ETKCS322
00332                                                                      CL**5
00333      IF  ADIRL > ZERO                                                CL**5
00334          MOVE ADIRI          TO ASC-DIRECTION-IND                    CL**5
00335      ELSE                                                            CL**5
00336          IF  ADIRF = DP015-ERASE-EOF                                 CL**5
00337              MOVE SPACE      TO ASC-DIRECTION-IND                    CL**5
00338          END-IF                                                      CL**5
00339      END-IF.                                                         CL**5
00340                                                                   ETKCS322
00341 ******************************************************************ETKCS322
00342 * VERIFY THAT DATA WAS ENTERED IN ALL FIELDS. REMOVE LEADING         CL**9
00343 * ZEROS FROM THE DOCUMENT DETAIL.  MAKE SURE THAT THE DIRECTION      CL**9
00344 * IS EITHER 'S'  OR 'R'.                                             CL**9
00345 ******************************************************************ETKCS322
00346                                                                   ETKCS322
00347  3000-EDIT-DATA-IN-COMMAREA.                                      ETKCS322
00348                                                                   ETKCS322
00349      PERFORM 3200-RESET-ATTRIBUTES.                               ETKCS322
00350                                                                   ETKCS322
00351      IF  ASC-DOC-DTL = SPACE                                         CL**2
00352          SET  PS-ERROR                                            ETKCS322
00353               DP020-MSG-FATAL        TO TRUE                      ETKCS322
00354 *        ---- REQUIRED ENTRY ----                                 ETKCS322
00355          MOVE PC-TSYMSG-00007        TO DP020-MSG-NUMBER          ETKCS322
00356          MOVE DP015-UNP-ALP-BRT-OFF  TO ADOCDTLA                     CL**2
00357          MOVE DP015-RED              TO ADOCDTLC                     CL**2
00358          MOVE DP015-REVERSE          TO ADOCDTLH                     CL**2
00359          MOVE -1                     TO ADOCDTLL                     CL**2
00360          SET DP030-SET-CURSOR-APPL-1 TO TRUE                      ETKCS322
00361      ELSE                                                            CL**7
00362          MOVE +0 TO PV-TALLY                                         CL**8
00363          INSPECT ASC-DOC-DTL                                         CL**8
00364              TALLYING PV-TALLY                                       CL**8
00365              FOR ALL '*'                                             CL**8
00366                                                                      CL**8
00367          IF PV-TALLY = 0                                             CL**8
00368              MOVE SPACES TO PV-30-CHAR-FIELD                         CL**8
00369                                                                      CL**7
00370              INSPECT ASC-DOC-DTL                                     CL**8
00371                  TALLYING PV-TALLY                                   CL**8
00372                  FOR                                                 CL**8
00373                      LEADING ZEROES                                  CL**8
00374                                                                      CL**8
00375              MOVE ASC-DOC-DTL(1 + PV-TALLY:30 - PV-TALLY) TO         CL**8
00376                   PV-30-CHAR-FIELD                                   CL**8
00377                                                                      CL**8
00378              MOVE PV-30-CHAR-FIELD TO ASC-DOC-DTL                    CL**8
00379          END-IF                                                      CL**8
00380      END-IF.                                                      ETKCS322
00381                                                                   ETKCS322
00382      IF  PS-NO-ERROR                                                 CL**2
00383          IF  ASC-TRAN-SET = SPACE                                    CL**2
00384              SET  PS-ERROR                                           CL**2
00385                   DP020-MSG-FATAL        TO TRUE                     CL**2
00386 *        ---- REQUIRED ENTRY ----                                    CL**2
00387              MOVE PC-TSYMSG-00007        TO DP020-MSG-NUMBER         CL**2
00388              MOVE DP015-UNP-NUM-BRT-OFF  TO ATSETA                   CL**2
00389              MOVE DP015-RED              TO ATSETC                   CL**2
00390              MOVE DP015-REVERSE          TO ATSETH                   CL**2
00391              MOVE -1                     TO ATSETL                   CL**2
00392              SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL**2
00393          END-IF                                                      CL**2
00394      END-IF.                                                         CL**2
00395                                                                      CL**2
00396      IF  PS-NO-ERROR                                                 CL**6
00397        IF ASC-DIRECTION-IND = SPACES                                 CL**6
00398            MOVE DP015-UNP-ALP-BRT-OFF  TO ADIRA                      CL**6
00399            MOVE DP015-RED              TO ADIRC                      CL**6
00400            MOVE DP015-REVERSE          TO ADIRH                      CL**6
00401            MOVE -1                     TO ADIRL                      CL**6
00402            SET DP030-SET-CURSOR-APPL-1 TO TRUE                       CL**6
00403            IF PS-NO-ERROR                                            CL**6
00404                MOVE PC-TSYMSG-00007    TO DP020-MSG-NUMBER           CL**6
00405                SET PS-ERROR                                          CL**6
00406                    DP020-MSG-FATAL     TO TRUE                       CL**6
00407            END-IF                                                    CL**6
00408        ELSE                                                          CL**6
00409            IF (ASC-DIRECTION-IND = 'R') OR                           CL**6
00410               (ASC-DIRECTION-IND = 'S')                              CL**6
00411                CONTINUE                                              CL**6
00412            ELSE                                                      CL**6
00413                SET PS-ERROR                                          CL**6
00414                    DP020-MSG-FATAL         TO TRUE                   CL**6
00415                MOVE PC-TSYMSG-00005        TO DP020-MSG-NUMBER       CL**6
00416                MOVE DP015-UNP-ALP-BRT-OFF  TO ADIRA                  CL**6
00417                MOVE DP015-RED              TO ADIRC                  CL**6
00418                MOVE DP015-REVERSE          TO ADIRH                  CL**6
00419                MOVE -1                     TO ADIRL                  CL**6
00420                SET DP030-SET-CURSOR-APPL-1 TO TRUE                   CL**6
00421            END-IF                                                    CL**6
00422        END-IF                                                        CL**6
00423      END-IF.                                                         CL**6
00424                                                                   ETKCS322
00425      IF  PS-NO-ERROR                                              ETKCS322
00426          MOVE -1                      TO ADOCDTLL                    CL**4
00427          SET DP030-SET-CURSOR-APPL-1  TO TRUE                     ETKCS322
00428      END-IF.                                                      ETKCS322
00429                                                                   ETKCS322
00430 ******************************************************************ETKCS322
00431 **  UNPROTECT, UNDERLINE, AND MAKE GREEN ALL THE FIELDS.             CL**9
00432 ******************************************************************ETKCS322
00433  3200-RESET-ATTRIBUTES.                                           ETKCS322
00434                                                                   ETKCS322
00435      MOVE DP015-UNP-NUM-NOR-OFF  TO ATSETA.                          CL**2
00435      MOVE DP015-UNP-ALP-NOR-OFF  TO ADOCDTLA                         CL**2
00436                                     ADIRA.                           CL**5
00438      MOVE DP015-GREEN            TO ADOCDTLC                         CL**2
00439                                     ADIRC                            CL**5
00440                                     ATSETC.                          CL**2
00441      MOVE DP015-UNDERLINE        TO ADOCDTLH                         CL**2
00442                                     ADIRH                            CL**5
00443                                     ATSETH.                          CL**2
00444                                                                   ETKCS322
00445 ******************************************************************ETKCS322
00446 *  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *ETKCS322
00447 *  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *ETKCS322
00448 *  IT HERE.                                                      *ETKCS322
00449 ******************************************************************ETKCS322
00450                                                                   ETKCS322
00451  4000-BUILD-INITIAL-PANEL.                                        ETKCS322
00452                                                                   ETKCS322
00453      MOVE -1                     TO ADOCDTLL.                        CL**2
00454                                                                   ETKCS322
00455      SET DP030-SET-CURSOR-APPL-1 TO TRUE.                         ETKCS322
00456      MOVE SPACE                  TO ATSETO                           CL**2
00457                                     ADOCDTLO.                        CL**2
00458                                                                   ETKCS322
00459 ******************************************************************ETKCS322
00460 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *ETKCS322
00461 ******************************************************************ETKCS322
00462                                                                   ETKCS322
00463  4400-MOVE-COMMAREA-TO-SCREEN.                                    ETKCS322
00464                                                                   ETKCS322
00465      MOVE ASC-DOC-DTL     TO ADOCDTLO.                               CL**2
00466      MOVE ASC-TRAN-SET    TO ATSETO.                                 CL**2
00467      MOVE ASC-DIRECTION-IND      TO  ADIRO.                          CL**5
00468                                                                      CL**2
00469  EJECT                                                            ETKCS322
00470 ******************************************************************ETKCS322
00471 *    CURRENT SYSTEM DATE/TIME MODULE                              ETKCS322
00472 ******************************************************************ETKCS322
00473                                                                   ETKCS322
00474      COPY DPPD017.                                                ETKCS322
00475                                                                   ETKCS322
00476 ******************************************************************ETKCS322
00477 *    ABEND PROCESSOR MODULE                                      *ETKCS322
00478 ******************************************************************ETKCS322
00479                                                                   ETKCS322
00480      COPY DPPD013.                                                ETKCS322
