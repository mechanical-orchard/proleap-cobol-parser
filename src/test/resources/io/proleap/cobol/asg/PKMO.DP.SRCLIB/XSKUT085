       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      XSKUT085.                                       00020000
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    01/30/2006.                                     00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **XS DOMAIN TABLE INTEGRITY PROCESS - FOR TABLE XST_BUS_GP_ATTR   00080000
      ******************************************************************00090000
      **THIS PROGRAM VALIDATES THERE IS A ROW IN XST_BUS_GP_ATTR FOR    00100000
      **EVERY ROW IN XST_BUS_GP.                                        00110000
      ******************************************************************00120000
      *DATE:01/30/2006   MADE BY:J HELING  INITIAL PROGRAM WRITE        00130000
      ******************************************************************00140000
       ENVIRONMENT DIVISION.                                            00150000
       CONFIGURATION SECTION.                                           00160000
       SOURCE-COMPUTER.        IBM-3090.                                00170000
       OBJECT-COMPUTER.        IBM-3090.                                00180000
       INPUT-OUTPUT SECTION.                                            00190000
       FILE-CONTROL.                                                    00200000
                                                                        00210000
           SELECT OUTPUT-FILE                                           00280000
                  ASSIGN TO OUT01.                                      00290000
                                                                        00300000
       DATA DIVISION.                                                   00310000
       FILE SECTION.                                                    00320000
                                                                        00330000
       FD  OUTPUT-FILE                                                  00550000
           RECORDING MODE F                                             00560000
           BLOCK CONTAINS 0 RECORDS                                     00570000
           LABEL RECORDS ARE OMITTED                                    00580000
           DATA RECORD IS OUTPUT-RECORD.                                00590000
       01  OUTPUT-RECORD                       PIC X(80).               00600000
                                                                        00610000
       WORKING-STORAGE SECTION.                                         00620000
      ******************************************************************00840000
      **TIMESTAMP VARIABLES                                             00850000
      ******************************************************************00860000
       01  SD-TIMESTAMP                        PIC X(26) VALUE SPACES.  00870000
       01  FILLER REDEFINES SD-TIMESTAMP.                               00880000
           05  SD-CURR-DTE                     PIC X(10).               00890000
           05  FILLER                          PIC X(01).               00900000
           05  SD-HOUR                         PIC X(02).               00910000
           05  FILLER                          PIC X(01).               00920000
           05  SD-MINUTE                       PIC X(02).               00930000
           05  FILLER                          PIC X(01).               00940000
           05  SD-SECOND                       PIC X(02).               00950000
           05  FILLER                          PIC X(01).               00960000
           05  SD-MICROSEC                     PIC X(06).               00970000
      ******************************************************************00980000
      **PROGRAM CONSTANTS                                               00990000
      ******************************************************************01000000
       01  PC-PROGRAM-CONSTANTS.                                        01010000
           05  PC-PROGRAM-NAME                 PIC X(08)                01020000
                                                       VALUE 'XSKUT085'.01030000
      ******************************************************************01040000
      **PROGRAM COUNTERS                                                01050000
      ******************************************************************01060000
       01  PC-PROGRAM-COUNTERS.                                         01070000
           05  PC-RECS-WRITTEN                 PIC 9(08) VALUE 0.       01080000
      ******************************************************************01081002
      **PROGRAM SWITCHES                                                01082002
      ******************************************************************01083002
       01  PS-PROGRAM-SWITCHES.                                         01084002
           05  PS-CSR-SWITCH                   PIC X(01) VALUE 'N'.     01085002
               88 PS-NOT-END-OF-CSR                      VALUE 'N'.     01086002
               88 PS-END-OF-CSR                          VALUE 'Y'.     01087002
      ******************************************************************01090000
      **OUTPUR RECORD LAYOUT                                            01100000
      ******************************************************************01110000
       01  OR-OUTPUT-RECORD.                                            01120000
           05  OR-BUS-GP-NBR                   PIC 9(03) VALUE 0.       01130003
           05  FILLER                          PIC X(01) VALUE SPACES.  01140000
           05  OR-BUS-GP-DESC                  PIC X(25) VALUE SPACES.  01150000
           05  FILLER                          PIC X(51) VALUE SPACES.  01160000
      ******************************************************************01280000
      **ERROR HANDLING                                                  01290000
      ******************************************************************01300000
       01  PV-MISC-ABEND-FIELDS.                                        01310000
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01320000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01330000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01340000
           05  PV-DB2-OPERATION-MSG         PIC X(30) VALUE SPACE.      01350005
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01360000
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01370000
      ******************************************************************01380000
      **COPYBOOKS                                                       01390000
      ******************************************************************01400000
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01470000
           COPY DPWS004.                                                01480000
      ***DB2 ABEND COPYBOOK                                             01490000
           COPY DPWS900.                                                01500000
      ***DB2 MESSAGE AREA                                               01510000
           COPY SQLCA2.                                                 01520000
      ******************************************************************01530000
      **TABLE INCLUSIONS                                                01540000
      ******************************************************************01550000
      ***DB2 COMMUNICATIONS AREA                                        01560000
           EXEC SQL                                                     01570000
                INCLUDE SQLCA                                           01580000
           END-EXEC.                                                    01590000
      ***TABLE XST_BUS_GP                                               01600000
           EXEC SQL                                                     01610000
                INCLUDE XST00016                                        01620000
           END-EXEC.                                                    01630000
      ***TABLE XST_BUS_GP_ATTR                                          01641000
           EXEC SQL                                                     01642000
                INCLUDE XST00106                                        01643000
           END-EXEC.                                                    01644000
                                                                        01644100
           EXEC SQL                                                     01644200
                DECLARE BUS-GRP-CSR CURSOR                              01644300
                FOR SELECT BUS_GP_NBR                                   01644400
                          ,BUS_GP_DESC                                  01644500
                    FROM   XST_BUS_GP                                   01644800
                    WHERE  BUS_GP_NBR NOT IN (SELECT BUS_GP_NBR         01645000
                                              FROM XST_BUS_GP_ATTR)     01646000
           END-EXEC                                                     01646100
                                                                        01646200
                                                                        01647000
       PROCEDURE DIVISION.                                              01650000
      ***************************************************************** 01660000
       0000-MAINLINE.                                                   01670000
                                                                        01680000
           PERFORM 1000-INITIALIZATION.                                 01690000
           PERFORM 2000-PROCESS-TABLE.                                  01700000
           PERFORM 9000-WRAPUP.                                         01760000
           GOBACK.                                                      01770000
                                                                        01780000
      ******************************************************************01800000
      **INITIALIZATION AREA                                             01810000
      ******************************************************************01820000
       1000-INITIALIZATION.                                             01830000
                                                                        01840000
           OPEN OUTPUT OUTPUT-FILE.                                     01920000
                                                                        01930000
           PERFORM 1100-SELECT-DB2-TIME.                                01940000
                                                                        01960000
                                                                        02000000
      ******************************************************************02020000
      **RETRIEVE TIME FROM DB2                                          02030000
      ******************************************************************02040000
       1100-SELECT-DB2-TIME.                                            02050000
                                                                        02060000
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            02070000
                                                                        02080000
           EXEC SQL                                                     02090000
                SELECT CURRENT TIMESTAMP                                02100000
                INTO  :SD-TIMESTAMP                                     02110000
                FROM   SYSIBM.SYSDUMMY1                                 02120000
           END-EXEC.                                                    02130000
                                                                        02140000
           EVALUATE TRUE                                                02150000
              WHEN SQLCODE = ZERO                                       02160000
                   CONTINUE                                             02170000
              WHEN OTHER                                                02180000
               PERFORM 9200-ABEND-ROUTINE                               02190000
           END-EVALUATE.                                                02200000
                                                                        02210000
      ******************************************************************02440000
      **PROCESS TABLE                                                   02450000
      ******************************************************************02460000
       2000-PROCESS-TABLE.                                              02470000
                                                                        02470100
           PERFORM 3000-OPEN-CURSOR.                                    02472004
           PERFORM 4000-FETCH-CURSOR UNTIL PS-END-OF-CSR.               02472104
           PERFORM 5000-CLOSE-CURSOR.                                   02473000
                                                                        02480000
      ***************************************************************** 04220100
      **OPEN CURSOR                                                     04220204
      ***************************************************************** 04220300
       3000-OPEN-CURSOR.                                                04220404
                                                                        04220500
           MOVE '3000-OPEN-CURSOR' TO PV-PARAGRAPH-NAME.                04221104
                                                                        04221204
           EXEC SQL                                                     04221304
               OPEN BUS-GRP-CSR                                         04221406
           END-EXEC.                                                    04221504
                                                                        04221604
           IF  SQLCODE NOT EQUAL ZERO                                   04221704
               DISPLAY 'XST_BUS_GP_ATTR'                                04221805
               DISPLAY 'ERROR ON OPEN OF CURSOR'                        04221905
               SET ABEND-4013-DB2-ERROR TO TRUE                         04222104
               PERFORM 9200-ABEND-ROUTINE                               04222204
           END-IF.                                                      04222304
                                                                        04222404
      ***************************************************************** 04222504
      **FETCH CURSOR                                                    04222604
      ***************************************************************** 04222704
       4000-FETCH-CURSOR.                                               04222804
                                                                        04222904
           MOVE '4000-FETCH-CURSOR' TO PV-PARAGRAPH-NAME.               04223004
                                                                        04223104
           EXEC SQL                                                     04223204
              FETCH BUS-GRP-CSR                                         04223304
                INTO  :XST00016-BUS-GP-NBR                              04223404
                     ,:XST00016-BUS-GP-DESC                             04223504
           END-EXEC.                                                    04223604
                                                                        04223704
           EVALUATE TRUE                                                04223804
               WHEN SQLCODE = ZERO                                      04223904
                 MOVE XST00016-BUS-GP-NBR  TO OR-BUS-GP-NBR             04224004
                 MOVE XST00016-BUS-GP-DESC TO OR-BUS-GP-DESC            04224104
                 PERFORM 8000-WRITE-RECORD                              04224204
               WHEN SQLCODE = +100                                      04224304
                 SET PS-END-OF-CSR TO TRUE                              04224404
               WHEN SQLWARN0 NOT = SPACE                                04224504
               WHEN SQLCODE NOT = ZERO                                  04224604
                  MOVE '4000-FETCH-CURSOR' TO PV-PARAGRAPH-NAME         04224704
                  MOVE 'FETCH CURSOR'      TO PV-OPERATION-MSG-1        04224804
                  SET ABEND-4013-DB2-ERROR TO TRUE                      04224904
                  PERFORM 9200-ABEND-ROUTINE                            04225004
           END-EVALUATE.                                                04225104
                                                                        04225204
      ***************************************************************** 04225304
      **CLOSE CURSOR                                                    04225404
      ***************************************************************** 04225504
       5000-CLOSE-CURSOR.                                               04225604
                                                                        04225704
           MOVE '5000-CLOSE-CURSOR' TO PV-PARAGRAPH-NAME.               04225804
                                                                        04225904
           EXEC SQL                                                     04226004
               CLOSE BUS-GRP-CSR                                        04226104
           END-EXEC.                                                    04226204
                                                                        04226304
           EVALUATE TRUE                                                04226404
               WHEN SQLCODE = ZERO                                      04226504
                   CONTINUE                                             04226604
               WHEN SQLWARN0 NOT = SPACES                               04226704
               WHEN SQLCODE < ZERO                                      04226804
               WHEN SQLCODE > ZERO                                      04226904
                   SET ABEND-4013-DB2-ERROR TO TRUE                     04227004
                   PERFORM 9200-ABEND-ROUTINE                           04227104
           END-EVALUATE.                                                04227204
                                                                        04227304
      ***************************************************************** 04227404
      **WRITE OUTPUT RECORD                                             04227504
      ***************************************************************** 04227604
       8000-WRITE-RECORD.                                               04227704
                                                                        04228000
           WRITE OUTPUT-RECORD FROM OR-OUTPUT-RECORD.                   04230000
                                                                        04240000
           ADD +1 TO PC-RECS-WRITTEN.                                   04250000
                                                                        04260000
      ****************************************************************  04280000
      *END OF PROGRAM ROUTINE                                           04290000
      ****************************************************************  04300000
       9000-WRAPUP.                                                     04310000
                                                                        04320000
           CLOSE OUTPUT-FILE.                                           04330000
                                                                        04370000
           PERFORM 9100-DISPLAY-STATISTICS.                             04380000
                                                                        04390000
           IF  PC-RECS-WRITTEN > 0                                      04400000
               MOVE 04 TO RETURN-CODE                                   04410000
           END-IF.                                                      04420000
                                                                        04430000
      ****************************************************************  04450000
      *DISPLAY PROGRAM STATISTICS                                       04460000
      ****************************************************************  04470000
       9100-DISPLAY-STATISTICS.                                         04480000
                                                                        04490000
           DISPLAY '*************************************************'. 04500000
           DISPLAY '* PROGRAM..............: ' PC-PROGRAM-NAME.         04510002
           DISPLAY '* DATE/TIMESTAMP.......: ' SD-TIMESTAMP.            04520000
           DISPLAY '* RECORDS WRITTEN:       ' PC-RECS-WRITTEN.         04560000
           DISPLAY '*************************************************'. 04570000
                                                                        04580000
      ******************************************************************04600000
      **SQL ABEND ROUTINE                                               04610000
      ******************************************************************04620000
       9200-ABEND-ROUTINE.                                              04630000
                                                                        04640000
           DISPLAY '** ABEND     **'.                                   04650000
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                04660002
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04670000
      ******************************************************************04690000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     04700000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      04710000
      ******************************************************************04720000
           COPY DPPD004.                                                04730000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04740000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04750000
                                                                        04760000
