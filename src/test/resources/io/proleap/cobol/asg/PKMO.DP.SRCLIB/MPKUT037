000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    MPKUT037.                                                 
000300 AUTHOR.        DENNIS STEFFEN.                                           
000400 INSTALLATION.  KOHLS.                                                    
000500 DATE-WRITTEN   OCTOBER 2004.                                             
000600 DATE-COMPILED.                                                           
000700                                                                          
000800*  TOTAL THE STORE EXTRACT FILE AND CPMARE THOSE TOTALS WITH              
000900*  THE FILE FROM MPKRP017                                                 
001000                                                                          
001100 ENVIRONMENT DIVISION.                                                    
001200 CONFIGURATION SECTION.                                                   
001300 SOURCE-COMPUTER.        IBM-3090.                                        
001400 OBJECT-COMPUTER.        IBM-3090.                                        
001500 INPUT-OUTPUT SECTION.                                                    
001600 FILE-CONTROL.                                                            
001700                                                                          
001800     SELECT STORE-EXTRACT-FILE                                            
001900         ASSIGN TO INP01.                                                 
002000     SELECT RECEIPTS-TOTAL-FILE                                           
002100         ASSIGN TO INP02.                                                 
002200                                                                          
002300 DATA DIVISION.                                                           
002400                                                                          
002500 FILE SECTION.                                                            
002600                                                                          
002700 FD  STORE-EXTRACT-FILE                                                   
002800     RECORDING MODE IS F                                                  
002900     LABEL RECORDS ARE STANDARD                                           
003000     BLOCK CONTAINS 0 RECORDS.                                            
003100                                                                          
003200 01  STORE-EXTRACT-RECORD             PIC X(300).                         
003300*                                                                         
003400 FD  RECEIPTS-TOTAL-FILE                                                  
003500     RECORDING MODE IS F                                                  
003600     LABEL RECORDS ARE STANDARD                                           
003700     BLOCK CONTAINS 0 RECORDS.                                            
003800                                                                          
003900 01  RECEIPTS-TOTAL-RECORD            PIC X(080).                         
004000*                                                                         
004100 WORKING-STORAGE SECTION.                                                 
004200*                                                                         
004300     COPY STOREEXT.                                                       
004400     COPY MPRD052.                                                        
004500                                                                          
004600 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
004700                                                   COMP SYNC.             
004800                                                                          
004900 01  PS-SWITCHES.                                                         
005000     05  PS-END-OF-STORE-FILE-SW     PIC  X(01)    VALUE 'N'.             
005100         88  PS-END-STORES-FILE-YES                VALUE 'Y'.             
005200         88  PS-END-STORES-FILE-NO                 VALUE 'N'.             
005300     05  PS-END-OF-RECEIPT-FILE-SW   PIC  X(01)   VALUE 'N'.              
005400         88  PS-END-RECEIPT-FILE-YES               VALUE 'Y'.             
005500         88  PS-END-RECEIPT-FILE-NO                VALUE 'N'.             
005600 01  PC-PROGRAM-CONSTANTS.                                                
005700     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
005800         'MPKUT037'.                                                      
005900                                                                          
006000 01  PV-COUNTERS COMP-3.                                                  
006100     05  PV-TOT-RCVR                 PIC S9(09)    VALUE ZERO.            
006200     05  PV-TOT-MARKED               PIC S9(15)V99 VALUE ZERO.            
006300     05  PV-TOT-COST                 PIC S9(15)V99 VALUE ZERO.            
006400     05  PV-TOT-RET                  PIC S9(15)V99 VALUE ZERO.            
006500     05  PV-STORE-READ-COUNT         PIC S9(09)    VALUE ZERO.            
006600                                                                          
006700     05  PV-TOT-RCVR-RCPT            PIC S9(09)    VALUE ZERO.            
006800     05  PV-TOT-MARKED-RCPT          PIC S9(15)V99 VALUE ZERO.            
006900     05  PV-TOT-COST-RCPT            PIC S9(15)V99 VALUE ZERO.            
007000     05  PV-TOT-RET-RCPT             PIC S9(15)V99 VALUE ZERO.            
007100                                                                          
007200 01  PV-CRAPOLA.                                                          
007300     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
007400     05  PV-DISPLAY-BUCKS            PIC ZZZ,ZZZ,ZZZ,ZZ9.99.              
007500     05  PV-DISPLAY-NBR              PIC ZZZ,ZZZ,ZZZ,ZZ9.                 
007600/                                                                         
007700 PROCEDURE DIVISION.                                                      
007800                                                                          
007900 0000-PROGRAM-MAINLINE.                                                   
008000                                                                          
008100     PERFORM 1000-INITIALIZE.                                             
008200                                                                          
008300     PERFORM 2000-PROCESS-STORE-EXTRACTS                                  
008400       UNTIL PS-END-STORES-FILE-YES                                       
008500                                                                          
008600     PERFORM 3000-COMPARE-TO-RPT-TOTAL                                    
008700                                                                          
008800     PERFORM 9000-EOJ-ROUTINE.                                            
008900                                                                          
009000     GOBACK.                                                              
009100                                                                          
009200 1000-INITIALIZE.                                                         
009300                                                                          
009400     OPEN INPUT STORE-EXTRACT-FILE                                        
009500                RECEIPTS-TOTAL-FILE                                       
009600                                                                          
009700     PERFORM 8000-READ-STORES-EXTRACT.                                    
009800                                                                          
009900 2000-PROCESS-STORE-EXTRACTS.                                             
010000                                                                          
010100     ADD SE-RETAIL-UNITS-MARK         TO PV-TOT-MARKED                    
010200     ADD SE-COST-EXTENSION            TO PV-TOT-COST                      
010300     ADD SE-RETAIL-EXTENSION          TO PV-TOT-RET                       
010400                                                                          
010500     PERFORM 8000-READ-STORES-EXTRACT.                                    
010600                                                                          
010700 3000-COMPARE-TO-RPT-TOTAL.                                               
010800                                                                          
010900     PERFORM 8100-READ-RECEIPTS-FILE                                      
011000                                                                          
011100     PERFORM UNTIL PS-END-RECEIPT-FILE-YES                                
011200         ADD MP052-OUT1-MARKED-TOT TO PV-TOT-MARKED-RCPT                  
011300         ADD MP052-OUT1-COST-TOT   TO PV-TOT-COST-RCPT                    
011400         ADD MP052-OUT1-RET-TOT    TO PV-TOT-RET-RCPT                     
011500         PERFORM 8100-READ-RECEIPTS-FILE                                  
011600     END-PERFORM                                                          
011700                                                                          
011800     IF      PV-TOT-MARKED-RCPT     = PV-TOT-MARKED                       
011900         AND PV-TOT-COST-RCPT       = PV-TOT-COST                         
012000         AND PV-TOT-RET-RCPT        = PV-TOT-RET                          
012100         DISPLAY ' TOTALS MATCH - NO PROBLEMS TODAY'                      
012200     ELSE                                                                 
012300         DISPLAY ' TOTALS DO NOT MATCH - CHECK FOR MISSING DATA'          
012400         PERFORM 9100-ABEND                                               
012500     END-IF.                                                              
012600                                                                          
012700 8000-READ-STORES-EXTRACT.                                                
012800     READ STORE-EXTRACT-FILE                                              
012900             INTO STORE-EXTRACT-LINE                                      
013000         AT END                                                           
013100             SET PS-END-STORES-FILE-YES                                   
013200                                      TO TRUE                             
013300         NOT AT END                                                       
013400             ADD 1                    TO PV-STORE-READ-COUNT              
013500     END-READ.                                                            
013600                                                                          
013700 8100-READ-RECEIPTS-FILE.                                                 
013800                                                                          
013900     INITIALIZE MP052-RECEIPT-TOTAL-RECORD                                
014000     READ RECEIPTS-TOTAL-FILE                                             
014100         INTO MP052-RECEIPT-TOTAL-RECORD                                  
014200     AT END                                                               
014300         SET PS-END-RECEIPT-FILE-YES  TO TRUE                             
014400     END-READ.                                                            
014500                                                                          
014600 9000-EOJ-ROUTINE.                                                        
014700                                                                          
014800     PERFORM 9010-DISPLAY-TOTALS.                                         
014900                                                                          
015000     CLOSE STORE-EXTRACT-FILE                                             
015100           RECEIPTS-TOTAL-FILE.                                           
015200                                                                          
015300 9010-DISPLAY-TOTALS.                                                     
015400     MOVE PV-STORE-READ-COUNT         TO PV-DISPLAY-NBR                   
015500     DISPLAY 'TOTAL EXTRACTS READ = ' PV-DISPLAY-NBR                      
015600                                                                          
015700*    DISPLAY SPACES                                                       
015800*    MOVE PV-TOT-RCVR                 TO PV-DISPLAY-NBR                   
015900*    DISPLAY 'TOTAL RCVRS         = ' PV-DISPLAY-NBR                      
016000                                                                          
016100     DISPLAY SPACES                                                       
016200     MOVE PV-TOT-MARKED               TO PV-DISPLAY-NBR                   
016300     DISPLAY 'TOTAL MARKED        = ' PV-DISPLAY-NBR                      
016400     MOVE PV-TOT-MARKED-RCPT          TO PV-DISPLAY-NBR                   
016500     DISPLAY 'TOTAL RPT MARKED    = ' PV-DISPLAY-NBR                      
016600                                                                          
016700     DISPLAY SPACES                                                       
016800     MOVE PV-TOT-COST                 TO PV-DISPLAY-BUCKS                 
016900     DISPLAY 'TOTAL COST          = ' PV-DISPLAY-BUCKS                    
017000     MOVE PV-TOT-COST-RCPT            TO PV-DISPLAY-BUCKS                 
017100     DISPLAY 'TOTAL RPT COST      = ' PV-DISPLAY-BUCKS                    
017200                                                                          
017300     DISPLAY SPACES                                                       
017400     MOVE PV-TOT-RET                  TO PV-DISPLAY-BUCKS                 
017500     DISPLAY 'TOTAL RETAIL        = ' PV-DISPLAY-BUCKS.                   
017600     MOVE PV-TOT-RET-RCPT             TO PV-DISPLAY-BUCKS                 
017700     DISPLAY 'TOTAL RPT RETAIL    = ' PV-DISPLAY-BUCKS.                   
017800                                                                          
017900 9100-ABEND.                                                              
018000                                                                          
018100     PERFORM 9010-DISPLAY-TOTALS.                                         
018200                                                                          
018300     CLOSE STORE-EXTRACT-FILE                                             
018400           RECEIPTS-TOTAL-FILE.                                           
018500                                                                          
018600     MOVE +4013                  TO ABEND-CODE.                           
018700     CALL 'ILBOABN0' USING ABEND-CODE.                                    
