      ******************************************************************00010008
       IDENTIFICATION DIVISION.                                         00020008
      ******************************************************************00030008
                                                                        00040008
       PROGRAM-ID.             IMKUT030.                                00050008
       AUTHOR.                 D. THEEL                                 00060008
       INSTALLATION.           KOHLS.                                   00070008
       DATE-WRITTEN            9/19/2002.                               00080008
       DATE-COMPILED.                                                   00090008
                                                                        00100008
      *                                                                 00110008
      *-----------------------------------------------------------------00120008
      *    CREATE ITEM DOMAIN UPC EXTRACT RECORDS                       00130008
      *-----------------------------------------------------------------00140008
      *  THE PURPOSE OF THIS PROGRAM IS TO READ IN THE BMC UNLOAD ITEM  00150008
      *  UPC EXTRACT FILE, INCLUDING ALL ACTIVE INTERNAL, VENDOR, AND   00160008
      *  RENUMBERED UPCS, GET SIZE DESCRIPTION, NRF COLOR DESCRIPTION,  00160108
      *  AND NRF SHORT COLOR DESCRIPTION.  WRITE OUT ITEM UPC DOMAIN    00160208
      *  EXTRACT RECORD.                                                00160308
      *-----------------------------------------------------------------00160508
      *    WR/PITS#     DATE        DESCRIPTION                         00160608
      *    --------   --------     ---------------------------------    00160708
      *    P000573025 09/11/03     CHANGED INTR UPC ID DISPLAY TO       00160808
      *                            NUMERIC                              00160908
      *    INC571903  10/24/08     ADDED TSKXREF INPUT FILE IN ORDER TO 00161010
      *                            VERIFY VALID INTERNAL UPC IDS ARE ON 00161108
      *                            THE TUPC INPUT FILE.                 00161208
      *-----------------------------------------------------------------00161308
                                                                        00161408
      ******************************************************************00161508
       ENVIRONMENT DIVISION.                                            00161608
      ******************************************************************00161708
                                                                        00161808
       CONFIGURATION SECTION.                                           00162008
                                                                        00163008
       SOURCE-COMPUTER.        IBM-3090.                                00164008
       OBJECT-COMPUTER.        IBM-3090.                                00165008
                                                                        00166008
       INPUT-OUTPUT SECTION.                                            00167008
                                                                        00168008
       FILE-CONTROL.                                                    00169008
                                                                        00170008
           SELECT UPC-UNLOAD-IN                                         00180008
               ASSIGN TO INP01.                                         00190008
                                                                        00200008
           SELECT TSKXREF-UNLOAD-FILE                                   00201008
               ASSIGN TO INP02.                                         00202008
                                                                        00203008
           SELECT UPC-EXTRACT-OUT                                       00210008
               ASSIGN TO OUT01.                                         00220008
                                                                        00230008
                                                                        00240008
      ******************************************************************00250008
       DATA DIVISION.                                                   00260008
      ******************************************************************00270008
                                                                        00280008
       FILE SECTION.                                                    00290008
                                                                        00300008
       FD  UPC-UNLOAD-IN                                                00310008
           RECORDING MODE IS F                                          00320008
           BLOCK CONTAINS 0 RECORDS                                     00330008
           LABEL RECORDS ARE STANDARD.                                  00340008
       01  UPC-UNLOAD-IN-REC                  PIC X(69).                00350008
                                                                        00360008
       FD  TSKXREF-UNLOAD-FILE                                          00361008
           RECORDING MODE IS F                                          00362008
           LABEL RECORDS ARE STANDARD                                   00363008
           BLOCK CONTAINS 0 RECORDS.                                    00364008
                                                                        00365008
       01  TSKXREF-UNLOAD-RECORD              PIC X(19).                00366008
                                                                        00367008
       FD  UPC-EXTRACT-OUT                                              00370008
           RECORDING MODE IS F                                          00380008
           BLOCK CONTAINS 0 RECORDS                                     00390008
           LABEL RECORDS ARE STANDARD.                                  00400008
       01  UPC-EXTRACT-OUT-REC                PIC X(250).               00410008
                                                                        00420008
      ******************************************************************00430008
       WORKING-STORAGE SECTION.                                         00440008
      ******************************************************************00450008
       01  ABEND-CODE                         PIC S9(04)  VALUE +0      00460008
                                                          COMP SYNC.    00470008
       01  PC-PROGRAM-CONSTANTS.                                        00480008
           05  PC-PGM-NAME             PIC  X(08)     VALUE 'IMKUT030'. 00490008
           05  PC-MAX-RETRIES          PIC S9(04)     VALUE +2.         00500008
                                                                        00515008
       01  PS-PROGRAM-SWITCHES.                                         00516008
           05  PS-TUPC-UNLOAD-DONE-SW         PIC X(01)    VALUE 'N'.   00517008
               88  PS-TUPC-UNLOAD-DONE                     VALUE 'Y'.   00518008
               88  PS-TUPC-UNLOAD-NOT-DONE                 VALUE 'N'.   00518108
           05  PS-TSKXREF-UNLOAD-DONE-SW      PIC X(01)    VALUE 'N'.   00518308
               88  PS-TSKXREF-UNLOAD-DONE                  VALUE 'Y'.   00518408
               88  PS-TSKXREF-UNLOAD-NOT-DONE              VALUE 'N'.   00518508
           05  PS-SQL-ERROR-SW                PIC X(01)    VALUE 'N'.   00519008
               88  PS-SQL-ERROR                            VALUE 'Y'.   00520008
               88  PS-NO-SQL-ERROR                         VALUE 'N'.   00530008
                                                                        00540008
       01  PV-PROGRAM-VARIABLES.                                        00550008
           05  PV-UPC-RECS-WRITTEN            PIC S9(09) VALUE +0  COMP.00560008
           05  PV-UPC-RECS-READ               PIC S9(09) VALUE +0  COMP.00570008
           05  PV-TSKXREF-UNLOAD-READ         PIC S9(09) VALUE +0  COMP.00571008
           05  PV-TUPC-UNLOAD-DROPPED         PIC S9(09) VALUE +0  COMP.00573008
           05  PV-PARAGRAPH-NAME              PIC  X(30)  VALUE SPACE.  00580008
           05  PV-DB2-OPERATION               PIC  X(30)  VALUE SPACE.  00590008
           05  PV-RETRY-COUNT                 PIC S9(04)  VALUE ZERO    00600008
                                                          COMP SYNC.    00610008
           05  PV-INTR-UPC-ID-TEN             PIC S9(10) VALUE +0 COMP. 00611008
           05  PV-DISPLAY-INTERNAL            PIC  9(10) VALUE ZERO.    00612008
                                                                        00613008
      *--------------------------------------------------               00620008
      *  COPYBOOK FOR BMC UPC UNLOAD INPUT FILE.                        00630008
      *--------------------------------------------------               00640008
           COPY IMRD054.                                                00650008
                                                                        00660008
      *--------------------------------------------------               00660108
      *  RECORD LAYOUT FOR THE ITEM DOMAIN TSKXREF UNLOAD INPUT FILE    00662008
      *--------------------------------------------------               00662108
           COPY IMRD062.                                                00664008
                                                                        00665008
      *--------------------------------------------------               00670008
      *  COPYBOOK FOR ITEM DOMAIN UPC EXTRACT OUTPUT FILE.              00680008
      *--------------------------------------------------               00690008
           COPY IMRD055.                                                00700008
                                                                        00710008
      *--------------------------------------------------               00800008
      *  COLOR TABLE                                                    00810008
      *--------------------------------------------------               00820008
           EXEC SQL                                                     00830008
               INCLUDE TCOLOR                                           00840008
           END-EXEC.                                                    00850008
                                                                        00860008
      *--------------------------------------------------               00870008
      *  SIZE TABLE                                                     00880008
      *--------------------------------------------------               00890008
           EXEC SQL                                                     00900008
               INCLUDE TSIZE                                            00910008
           END-EXEC.                                                    00920008
                                                                        00930008
      *--------------------------------------------------               00947008
      *  COMMUNICATION AREA FOR DB2                                     00948008
      *--------------------------------------------------               00949008
           EXEC SQL                                                     00950008
               INCLUDE SQLCA                                            00960008
           END-EXEC.                                                    00970008
                                                                        00980008
      *--------------------------------------------------               00990008
      *  DB2 SQL ERROR ROUTINE                                          01000008
      *--------------------------------------------------               01010008
           COPY DPWS004.                                                01020008
                                                                        01030008
       PROCEDURE DIVISION.                                              01040008
      *-----------------------------------------------------------------01050008
      *  MAIN LINE OF THE PROGRAM PERFORMS THE                          01060008
      *  INITIALIZATION ROUTINE AND CONTROLS THE PROCESSING OF THE PGM. 01070008
      *-----------------------------------------------------------------01080008
       0000-MAINLINE.                                                   01090008
                                                                        01100008
           PERFORM 0100-HOUSEKEEPING.                                   01110008
                                                                        01120008
           PERFORM 1000-PROCESS-INPUT-FILES                             01130008
               UNTIL PS-TUPC-UNLOAD-DONE                                01140008
                 OR  PS-TSKXREF-UNLOAD-DONE.                            01141008
                                                                        01150008
           PERFORM 9000-EOJ.                                            01160008
           GOBACK.                                                      01170008
                                                                        01180008
      *-----------------------------------------------------------------01190008
      *  OPEN FILES AND PERFORM FIRST READ FROM INPUT FILE              01200008
      *-----------------------------------------------------------------01210008
       0100-HOUSEKEEPING.                                               01220008
                                                                        01230008
           OPEN INPUT UPC-UNLOAD-IN                                     01240008
                      TSKXREF-UNLOAD-FILE                               01241008
                OUTPUT UPC-EXTRACT-OUT.                                 01250008
                                                                        01260008
           PERFORM 6000-READ-UPC-UNLOAD-FILE.                           01270008
                                                                        01280008
           IF PS-TUPC-UNLOAD-DONE                                       01290008
              DISPLAY '*** '                                            01300008
              DISPLAY '*** IMKUT030 - NO INPUT UPC RECS FOUND'          01310008
              DISPLAY '*** '                                            01320008
              MOVE +4003 TO ABEND-CODE                                  01330008
              PERFORM 9999-ABEND                                        01340008
           END-IF.                                                      01350008
                                                                        01360008
           PERFORM 6100-READ-TSKXREF-UNLOAD-DATA.                       01361008
                                                                        01361108
           IF PS-TSKXREF-UNLOAD-DONE                                    01361208
              DISPLAY '*** '                                            01361308
              DISPLAY '*** IMKUT030 - NO INPUT TSKXREF RECS FOUND'      01361408
              DISPLAY '*** '                                            01361508
              MOVE +4004 TO ABEND-CODE                                  01361608
              PERFORM 9999-ABEND                                        01361708
           END-IF.                                                      01361808
                                                                        01362008
      *-----------------------------------------------------------------01365108
      *  PROCESS ALL INPUT RECORDS AND FORMAT ITEM UPC DOMAIN EXTRACT   01365208
      *  RECORDS UNTIL END OF INPUT FILE.                               01365308
      *-----------------------------------------------------------------01365408
       1000-PROCESS-INPUT-FILES.                                        01365508
      *                                                                 01367008
           IF IM054-INTR-UPC-ID = IM062-INTR-UPC-ID                     01368008
               PERFORM 1100-CREATE-UPC-OUTPUT-REC                       01369008
               PERFORM 6000-READ-UPC-UNLOAD-FILE                        01369108
           ELSE                                                         01369308
               IF IM054-INTR-UPC-ID > IM062-INTR-UPC-ID                 01369408
                   PERFORM 6100-READ-TSKXREF-UNLOAD-DATA                01370008
               ELSE                                                     01370108
                   ADD +1 TO PV-TUPC-UNLOAD-DROPPED                     01370208
                   MOVE IM054-UPC-ID       TO PV-INTR-UPC-ID-TEN        01370309
                   MOVE PV-INTR-UPC-ID-TEN TO PV-DISPLAY-INTERNAL       01370409
                   DISPLAY 'EXTRA TUPC UNLOAD RECORD DROPPED FOR UPC ID:01370508
      -                    ' ' PV-DISPLAY-INTERNAL                      01370609
                   PERFORM 6000-READ-UPC-UNLOAD-FILE                    01370708
               END-IF                                                   01370808
           END-IF.                                                      01370908
                                                                        01371008
      *-----------------------------------------------------------------01372008
      *  FORMAT AND WRITE A UPC DOMAIN EXTRACT RECORD.                  01380008
      *-----------------------------------------------------------------01400008
       1100-CREATE-UPC-OUTPUT-REC.                                      01410008
                                                                        01420008
           MOVE IM054-INTR-UPC-ID       TO PV-INTR-UPC-ID-TEN.          01421008
           MOVE PV-INTR-UPC-ID-TEN      TO PV-DISPLAY-INTERNAL.         01422008
           PERFORM 1300-GET-SIZE-DESC.                                  01430008
           MOVE SIZE-KOHLS-DESC         TO IM055-KOHLS-SIZE-DESC.       01440008
                                                                        01450008
           PERFORM 1400-GET-COLOR-DESCS.                                01460008
           MOVE COLOR-NRF-CLOR-DESC     TO IM055-NRF-CLOR-DESC.         01470008
           MOVE COLOR-SHRT-CLOR-DESC    TO IM055-SHRT-CLOR-DESC.        01480008
                                                                        01490008
           MOVE IM054-UPC-ID            TO IM055-UPC-ID.                01704408
           MOVE IM054-INTR-UPC-ID       TO IM055-INTR-UPC-ID.           01704508
           MOVE IM054-UPC-TYP-CDE       TO IM055-UPC-TYP-CDE.           01704608
           MOVE IM054-UPC-NBR           TO IM055-UPC-NBR.               01704708
           MOVE IM054-EDI-ORD-IND       TO IM055-EDI-ORD-IND.           01704808
           MOVE IM054-PREPK-IND         TO IM055-PREPK-IND.             01704908
           MOVE IM054-CERTD-IND         TO IM055-CERTD-IND.             01705008
           MOVE IM054-CERTD-DTE         TO IM055-CERTD-DTE.             01705108
           MOVE IM054-UPC-SRC-CDE       TO IM055-UPC-SRC-CDE.           01705208
           MOVE IM054-KOHLS-SIZE-CDE    TO IM055-KOHLS-SIZE-CDE.        01705308
           MOVE IM054-NRF-CLOR-CDE      TO IM055-NRF-CLOR-CDE.          01705408
           MOVE IM054-CSTM-CLOR-DESC    TO IM055-CSTM-CLOR-DESC.        01705508
           MOVE IM054-WT-UM-CDE         TO IM055-WT-UM-CDE.             01705608
           MOVE IM054-WT-UM-QTY         TO IM055-WT-UM-QTY.             01705708
           MOVE IM054-SIZE-UM-CDE       TO IM055-SIZE-UM-CDE.           01705808
           MOVE IM054-WD-UM-QTY         TO IM055-WD-UM-QTY.             01705908
           MOVE IM054-HT-UM-QTY         TO IM055-HT-UM-QTY.             01706008
           MOVE IM054-DPTH-UM-QTY       TO IM055-DPTH-UM-QTY.           01706108
           MOVE SPACES                  TO IM055-ITEM-UPC-FILLER.       01706208
                                                                        01706408
      ***  WRITE UPC OUTPUT RECORD                                      01706508
           PERFORM 7000-WRITE-UPC-OUTPUT-REC.                           01706608
                                                                        01706808
      *-----------------------------------------------------------------01926008
      * GET THE SIZE DESCRIPTION FROM TSIZE BASED ON THE KOHLS SIZE CDE 01927008
      * FROM THE INPUT FILE.                                            01928008
      *-----------------------------------------------------------------01929008
       1300-GET-SIZE-DESC.                                              01930008
                                                                        01940008
           INITIALIZE DCLTSIZE.                                         01950008
                                                                        01960008
           PERFORM WITH TEST AFTER                                      01970008
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       01980008
                 UNTIL SQLCODE = +0                                     01990008
                    OR SQLCODE = +100                                   02000008
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  02010008
               EXEC SQL                                                 02020008
                   SELECT KOHLS_DESC                                    02030008
                     INTO :SIZE-KOHLS-DESC                              02040008
                     FROM TSIZE                                         02050008
                    WHERE KOHLS_SIZE_CDE = :IM054-KOHLS-SIZE-CDE        02060008
                    WITH UR                                             02070008
               END-EXEC                                                 02080008
                                                                        02090008
               EVALUATE TRUE                                            02100008
                   WHEN SQLCODE = -904                                  02110008
                   WHEN SQLCODE = -913                                  02120008
                       CONTINUE                                         02130008
                   WHEN SQLCODE = ZERO                                  02140008
                       CONTINUE                                         02150008
                   WHEN SQLCODE = +100                                  02160008
                       MOVE SPACES TO SIZE-KOHLS-DESC                   02170008
                   WHEN OTHER                                           02180008
                       DISPLAY '** '                                    02190008
                               PC-PGM-NAME                              02200008
                               ' - INTR UPC ID: '                       02210008
                               PV-DISPLAY-INTERNAL                      02220008
                       MOVE '1300-GET-SIZE-DESC' TO PV-PARAGRAPH-NAME   02230008
                       MOVE 'SELECT FROM TSIZE'  TO PV-DB2-OPERATION    02240008
                       PERFORM 9998-SQL-ERROR                           02250008
               END-EVALUATE                                             02260008
           END-PERFORM.                                                 02270008
                                                                        02280008
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02290008
               DISPLAY '** '                                            02300008
                       PC-PGM-NAME                                      02310008
                       ' - INTR UPC ID: '                               02320008
                       PV-DISPLAY-INTERNAL                              02321008
               MOVE '1300-GET-SIZE-DESC' TO PV-PARAGRAPH-NAME           02340008
               MOVE 'SELECT FROM TSIZE'  TO PV-DB2-OPERATION            02350008
               PERFORM 9998-SQL-ERROR                                   02360008
           END-IF.                                                      02370008
                                                                        02380008
      *-----------------------------------------------------------------02390008
      * GET THE COLOR DESCS FROM TCOLOR BASED ON THE NRF COLOR CODE     02400008
      * FROM THE INPUT FILE.                                            02410008
      *-----------------------------------------------------------------02420008
       1400-GET-COLOR-DESCS.                                            02430008
                                                                        02440008
           INITIALIZE DCLTCOLOR.                                        02450008
                                                                        02460008
           PERFORM WITH TEST AFTER                                      02461008
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       02462008
                 UNTIL SQLCODE = +0                                     02463008
                    OR SQLCODE = +100                                   02464008
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  02465008
               EXEC SQL                                                 02466008
                   SELECT NRF_CLOR_DESC                                 02467008
                         ,SHRT_CLOR_DESC                                02468008
                     INTO :COLOR-NRF-CLOR-DESC                          02469008
                         ,:COLOR-SHRT-CLOR-DESC                         02470008
                     FROM TCOLOR                                        02470108
                    WHERE NRF_CLOR_CDE = :IM054-NRF-CLOR-CDE            02470208
                    WITH UR                                             02470308
               END-EXEC                                                 02470408
                                                                        02470508
               EVALUATE TRUE                                            02470608
                   WHEN SQLCODE = -904                                  02470708
                   WHEN SQLCODE = -913                                  02470808
                       CONTINUE                                         02470908
                   WHEN SQLCODE = ZERO                                  02471008
                       CONTINUE                                         02471108
                   WHEN SQLCODE = +100                                  02471208
                       MOVE SPACES TO COLOR-NRF-CLOR-DESC               02471308
                       MOVE SPACES TO COLOR-SHRT-CLOR-DESC              02471408
                   WHEN OTHER                                           02471508
                       DISPLAY '** '                                    02471608
                               PC-PGM-NAME                              02471708
                               ' - INTR UPC ID: '                       02471808
                               PV-DISPLAY-INTERNAL                      02471908
                       MOVE '1400-GET-COLOR-DESCS' TO PV-PARAGRAPH-NAME 02472108
                       MOVE 'SELECT FROM TCOLOR'   TO PV-DB2-OPERATION  02472208
                       PERFORM 9998-SQL-ERROR                           02472308
               END-EVALUATE                                             02472408
           END-PERFORM.                                                 02472508
                                                                        02472608
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02472708
               DISPLAY '** '                                            02472808
                       PC-PGM-NAME                                      02472908
                       ' - INTR UPC ID: '                               02473008
                       PV-DISPLAY-INTERNAL                              02473108
               MOVE '1400-GET-COLOR-DESCS' TO PV-PARAGRAPH-NAME         02473308
               MOVE 'SELECT FROM TCOLOR'   TO PV-DB2-OPERATION          02473408
               PERFORM 9998-SQL-ERROR                                   02473508
           END-IF.                                                      02473608
                                                                        02473708
      *-----------------------------------------------------------------02969108
      * READ THE UPC UNLOAD INPUT FILE                                  02969208
      *-----------------------------------------------------------------02969308
       6000-READ-UPC-UNLOAD-FILE.                                       02969408
                                                                        02969508
           INITIALIZE IM054-ITEM-UPC-UNLD-REC.                          02969608
                                                                        02969708
           READ UPC-UNLOAD-IN INTO IM054-ITEM-UPC-UNLD-REC              02969808
              AT END                                                    02969908
                 SET PS-TUPC-UNLOAD-DONE TO TRUE                        02970008
              NOT AT END                                                02970108
                 ADD +1   TO PV-UPC-RECS-READ.                          02970208
                                                                        02970308
      *-----------------------------------------------------------------02970408
      *  READ AN ITEM DOMAIN TSKXREF UNLOAD FILE RECORD                 02970608
      *-----------------------------------------------------------------02970708
       6100-READ-TSKXREF-UNLOAD-DATA.                                   02970908
                                                                        02971008
           READ TSKXREF-UNLOAD-FILE INTO IM062-ITEM-TSKXREF-UNLD-REC    02971108
             AT END                                                     02971208
                 SET PS-TSKXREF-UNLOAD-DONE TO TRUE                     02971308
             NOT AT END                                                 02971408
                 ADD +1   TO PV-TSKXREF-UNLOAD-READ                     02971508
           END-READ.                                                    02971608
                                                                        02971708
      *-----------------------------------------------------------------02971808
      * WRITE THE UPC ITEM DOMAIN OUTPUT FILE.                          02971908
      *-----------------------------------------------------------------02972008
       7000-WRITE-UPC-OUTPUT-REC.                                       02972108
                                                                        02972208
           WRITE UPC-EXTRACT-OUT-REC FROM   IM055-ITEM-UPC-DOMAIN-REC.  02972308
                                                                        02972408
           ADD +1 TO PV-UPC-RECS-WRITTEN.                               02972508
                                                                        02972608
                                                                        02972708
      *-----------------------------------------------------------------02972808
      *  PROCESS EXTRA TUPC RECORDS.                                    02973008
      *-----------------------------------------------------------------02973108
       8100-EXTRA-TUPC-RECS.                                            02973308
      *                                                                 02973408
           ADD +1 TO PV-TUPC-UNLOAD-DROPPED.                            02973508
           MOVE IM054-UPC-ID       TO PV-INTR-UPC-ID-TEN.               02973609
           MOVE PV-INTR-UPC-ID-TEN TO PV-DISPLAY-INTERNAL.              02973709
           DISPLAY 'EXTRA TUPC UNLOAD RECORD DROPPED FOR UPC ID: '      02973808
                   PV-DISPLAY-INTERNAL.                                 02973909
           PERFORM 6000-READ-UPC-UNLOAD-FILE.                           02974008
                                                                        02974108
      *-----------------------------------------------------------------02974208
      * 9000-EOJ-ROUTINE - CLOSES THE FILES ASSOCIATED WITH THE JOB.    02974308
      *-----------------------------------------------------------------02974408
       9000-EOJ.                                                        02974508
                                                                        02974608
           IF PS-TUPC-UNLOAD-NOT-DONE                                   02974708
               PERFORM 8100-EXTRA-TUPC-RECS                             02974808
                 UNTIL PS-TUPC-UNLOAD-DONE                              02974908
           END-IF.                                                      02975008
                                                                        02975108
           DISPLAY ' '.                                                 02975208
           DISPLAY '============================================='.     02975308
           DISPLAY ' '.                                                 02975408
           DISPLAY ' ******  IMKUT030 PROCESSING SUMMARY  ******  '.    02975508
           PERFORM 9100-DISPLAY-PROCESSING-INFO.                        02975608
           DISPLAY '============================================='.     02975708
                                                                        02975808
                                                                        02976008
           CLOSE UPC-UNLOAD-IN                                          02977008
                 TSKXREF-UNLOAD-FILE                                    02977108
                 UPC-EXTRACT-OUT.                                       02978008
                                                                        02979008
      *-----------------------------------------------------------------02980008
      * DISPLAY PROCESSED RECORDS COUNTS                                02990008
      *-----------------------------------------------------------------03000008
       9100-DISPLAY-PROCESSING-INFO.                                    03010008
                                                                        03020008
           DISPLAY ' '.                                                 03030008
           DISPLAY '    UPC RECORDS READ         = '                    03040008
                 PV-UPC-RECS-READ.                                      03050008
           DISPLAY '    UPC RECORDS WRITTEN      = '                    03060008
                 PV-UPC-RECS-WRITTEN.                                   03070008
           DISPLAY '    TSKXREF UNLOAD RECS READ = '                    03071008
                 PV-TSKXREF-UNLOAD-READ.                                03072008
           DISPLAY '    TUPC UNLOAD RECS DROPPED = '                    03073008
                 PV-TUPC-UNLOAD-DROPPED.                                03074008
           DISPLAY ' '.                                                 03080008
      *-----------------------------------------------------------------03090008
      * 9800-SQL-ABEND - DISPLAY DB2 ABEND INFO                         03100008
      *-----------------------------------------------------------------03110008
       9998-SQL-ERROR.                                                  03120008
                                                                        03130008
           SET PS-SQL-ERROR TO TRUE.                                    03140008
                                                                        03150008
           DISPLAY '** '                                                03160008
                   PC-PGM-NAME                                          03170008
                   ' ABEND IN PARAGRAPH '                               03180008
                   PV-PARAGRAPH-NAME.                                   03190008
           DISPLAY '** '                                                03200008
                   PC-PGM-NAME                                          03210008
                   ' OPERATION = ' PV-DB2-OPERATION.                    03220008
                                                                        03230008
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT     03240008
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.               03250008
           COPY DPPD004.                                                03260008
                                                                        03270008
           EXEC SQL                                                     03280008
               ROLLBACK                                                 03290008
           END-EXEC.                                                    03300008
                                                                        03310008
           MOVE +4013 TO ABEND-CODE                                     03320008
           PERFORM 9999-ABEND.                                          03330008
                                                                        03340008
      *-----------------------------------------------------------------03350008
      * 9999-ABEND - FORCES PROGRAM ABEND WITH RETURN CODE              03360008
      *-----------------------------------------------------------------03370008
       9999-ABEND.                                                      03380008
                                                                        03390008
           CALL 'ILBOABN0' USING ABEND-CODE.                            03400008
                                                                        03410008
