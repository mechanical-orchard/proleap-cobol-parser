       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT009.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            11/20/2001.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  INVENTORY DATA TRANSMISSION GDG BASE REMOVAL                  *        
      *                                                                *        
      *  THIS PROGRAM WILL CREATE A LIST OF IDCAMS CONTROL CARDS TO    *        
      *  DELETE OBSOLETE DATA TRANSMISSION DATASET FOR VARIOUS STORES. *        
      *                                                                *        
      *  THE INPUT TO THIS PROGRAM WILL CONTAIN AN IDCAMS CATALOG      *        
      *  LEVEL LISTING OF DATASET TO BE DELETED.                       *        
      *                                                                *        
      *  THIS PROGRAM WILL ONLY CREATE CARDS FOR DELETING THE DATASET  *        
      *  GENERATIONS UNDER THE GDG BASE.  THE GDG BASE WILL REMAIN     *        
      *  INTACT FOR FUTURE USES.                                       *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
23511 * 23511    05/28/2002  THE DELETE PROCESS CANNOT HANDLE A TRUE            
23511 *                      44 BYTE FILE NAME. MAKE ADJUSTMENTS.               
23511 *                      - ROD JANSSEN                                      
472007* 472007   06/03/2002  MORE GDG DELETE SCRIPT ISSUES.                     
472007*                      - ROD JANSSEN                                      
      * 25248    01/05/2004  UPC CAPABILITIES & STORE NUMBER EXPANSION.         
      *                      - V. LEE YANG                                      
W30692* W30692   06/13/2006  NOT DELETE GDG GENS PROPERLY, WHY?                 
W30692*                      - ROD JANSSEN                                      
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DEL-REQUEST-CNTL-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT GDG-DELETE-CNTL-FILE                                          
               ASSIGN TO OUT01.                                                 
                                                                                
25248      SELECT INVALID-GDG-CNTL-FILE                                         
25248          ASSIGN TO OUT02.                                                 
25248                                                                           
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  DEL-REQUEST-CNTL-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DEL-REQUEST-CNTL-REC            PIC X(80).                           
                                                                                
       FD  GDG-DELETE-CNTL-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  GDG-DELETE-CNTL-REC             PIC X(80).                           
                                                                                
25248  FD  INVALID-GDG-CNTL-FILE                                                
25248      RECORDING MODE IS F                                                  
25248      LABEL RECORDS ARE STANDARD                                           
25248      BLOCK CONTAINS 0 RECORDS.                                            
25248  01  INVALID-GDG-CNTL-REC            PIC X(80).                           
25248                                                                           
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
       01  RP-REPORT-DETAIL.                                                    
25248      05  RP-STORE-NBR                PIC X(04)       VALUE SPACES.        
25248          88  RP-PIV-STORE-TEST                       VALUE 'TEST'.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
23511      05  RP-FILE-NAME                PIC X(45)       VALUE SPACES.        
23511 **** 05  RP-FILE-NAME                PIC X(44)       VALUE SPACES.        
           05  FILLER                      PIC X(03)       VALUE SPACES.        
           05  RP-COMMENTS                 PIC X(60)       VALUE SPACES.        
           05  FILLER                      PIC X(03)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-INCOMPLETE-DTE           PIC X(10)       VALUE                
                                                           '9999-09-09'.        
           05  PC-Y                        PIC X           VALUE 'Y'.           
           05  PC-N                        PIC X           VALUE 'N'.           
           05  PC-QUOTE                    PIC X           VALUE "'".           
           05  PC-MID-DSN-QUAL             PIC X(13)       VALUE                
               '.IN.KOHLS.STR'.                                                 
           05  PC-DELETE-CMD-FIRST         PIC X(09)       VALUE                
               ' DELETE ('.                                                     
           05  PC-DELETE-GDG-CMD           PIC X(11)       VALUE                
               ') GDG FORCE'.                                                   
           05  PC-DELETE-SEQ-CMD           PIC X(01)       VALUE                
               ')'.                                                             
           05  PC-DELETE-CMD-LAST          PIC X(06)       VALUE                
               ' PURGE'.                                                        
           05  PC-INVALID-REQUEST          PIC X(60)       VALUE                
               '*** INVALID REQUEST TYPE, CARD IGNORED ***'.                    
           05  PC-INVALID-DSN              PIC X(60)       VALUE                
25248          '*** INVALID DATASET, USE ALTERNATE CMD TO DELETE ***'.          
           05  PC-INVALID-GDGBASE          PIC X(60)       VALUE                
25248          '*** INVALID GDG BASE, USE ALTERNATE CMD TO DELETE ***'.         
           05  PC-GDGBASE-NOT-DELETE       PIC X(60)       VALUE                
               '*** GDG BASE WILL NOT BE DELETED ***'.                          
           05  PC-ERR-IN-BUILDING-CMD      PIC X(60)       VALUE                
               '*** ERROR IN BUILDING COMMAND ***'.                             
25248      05  PC-IGNORE-PIV-STORE-TEST    PIC X(60)       VALUE                
25248          '*** IGNORE PIV STORE TEST GDG/DATASET ***'.                     
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-DEL-CNTL-FILE     PIC X           VALUE 'N'.           
               88  END-OF-DEL-CNTL-FILE                    VALUE 'Y'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** DELETE REQUEST CONTROL REC ***'.                                
                                                                                
       01  RQ-DEL-CNTL-AREA.                                                    
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RQ-DEL-DSN-TYPE             PIC X(08)       VALUE SPACES.        
               88  RQ-VALID-DSN-TYPE                       VALUES ARE           
                   'GDG BASE'                                                   
                   'NONVSAM '.                                                  
               88  RQ-DEL-GDG-DSN                          VALUE                
                   'GDG BASE'.                                                  
               88  RQ-DEL-SEQ-DSN                          VALUE                
                   'NONVSAM '.                                                  
           05  FILLER                      PIC X(08)       VALUE SPACES.        
23511      05  RQ-DEL-DSN-NAME             PIC X(45)       VALUE SPACES.        
           05  RQ-DEL-CNTL-RDW             PIC X(04)       VALUE SPACES.        
           05  FILLER REDEFINES RQ-DEL-CNTL-RDW.                                
               10  RQ-DEL-CNTL-LENGTH      PIC S9(4)                            
                               COMP.                                            
               10  FILLER                  PIC X(02).                           
W30692     05  FILLER                      PIC X(14)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-DEFINED-STORE-COUNT      PIC S9(9)       VALUE ZEROES.        
           05  PV-DSN-DELETED-COUNT        PIC S9(9)       VALUE ZEROES.        
           05  PV-DSN-INVALID-COUNT        PIC S9(9)       VALUE ZEROES.        
           05  PV-GDG-INVALID-COUNT        PIC S9(9)       VALUE ZEROES.        
           05  PV-DSN-IGNORED-COUNT        PIC S9(9)       VALUE ZEROES.        
           05  PV-GDG-IGNORED-COUNT        PIC S9(9)       VALUE ZEROES.        
           05  PV-REQUEST-COUNT            PIC S9(9)       VALUE ZEROES.        
           05  PV-INVALID-REQ-COUNT        PIC S9(9)       VALUE ZEROES.        
           05  PV-NUMERIC-DISPLAY          PIC ZZZ,ZZZ,ZZ9 VALUE ZEROES.        
           05  PV-SPC-HIGH-LVL-QUAL        PIC X(28)       VALUE 'PKMO'.        
           05  FILLER REDEFINES PV-SPC-HIGH-LVL-QUAL.                           
               10  PV-SPC-HIGH-LVL-QUAL-1  PIC X(01).                           
               10  FILLER                  PIC X(27).                           
           05  PV-DSN-JUNK-QUAL            PIC X(30)       VALUE SPACES.        
           05  PV-FIELDS-COMPLETED         PIC S9(04)      VALUE ZEROES.        
           05  PV-HIGH-LVL-COUNT           PIC S9(04)      VALUE ZEROES.        
           05  PV-STORE-NBR-COUNT          PIC S9(04)      VALUE ZEROES.        
           05  PV-DSN-JUNK-COUNT           PIC S9(04)      VALUE ZEROES.        
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
25248      05  PV-PREV-STORE-NBR           PIC X(04)       VALUE SPACES.        
           05  PV-PREV-DSN-DELETED-COUNT   PIC S9(9)       VALUE ZEROES.        
25248      05  PV-STORE-NUM-9              PIC 9(04)       VALUE ZEROES.        
           05  PV-STORE-NUM-X REDEFINES PV-STORE-NUM-9                          
25248                                      PIC X(04).                           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN RPOCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z200-READ-REQUEST-FILE.                                      
                                                                                
           PERFORM B200-PROCESS-DEL-CNTL                                        
               UNTIL END-OF-DEL-CNTL-FILE.                                      
                                                                                
           PERFORM B400-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
           DISPLAY ' PROCESS DATE ' PV-PROCESSING-DATE.                         
                                                                                
           OPEN OUTPUT GDG-DELETE-CNTL-FILE                                     
25248                  INVALID-GDG-CNTL-FILE                                    
                INPUT  DEL-REQUEST-CNTL-FILE.                                   
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' PROCESSING: '.                                             
                                                                                
      ******************************************************************        
      *    PROCESS DELETE CONTROL FILE                                          
      ******************************************************************        
       B200-PROCESS-DEL-CNTL.                                                   
                                                                                
           ADD 1 TO PV-REQUEST-COUNT.                                           
                                                                                
           IF RQ-VALID-DSN-TYPE                                                 
               MOVE SPACES TO RP-COMMENTS                                       
                                                                                
               PERFORM C100-GET-STORE-NBR                                       
                                                                                
               MOVE RQ-DEL-DSN-NAME TO RP-FILE-NAME                             
                                                                                
               IF RP-COMMENTS = SPACES                                          
                   PERFORM C200-CREATE-DEL-CMDS                                 
               ELSE                                                             
25248              IF RP-PIV-STORE-TEST                                         
25248                  MOVE PC-IGNORE-PIV-STORE-TEST TO RP-COMMENTS             
25248              ELSE                                                         
                       IF RQ-DEL-SEQ-DSN                                        
                           ADD 1 TO PV-DSN-INVALID-COUNT                        
25248                      PERFORM C400-CREATE-INVALID-SEQ                      
                       ELSE                                                     
                           MOVE PC-INVALID-GDGBASE TO RP-COMMENTS               
25248                      PERFORM C300-CREATE-INVALID-GDG                      
                           ADD 1 TO PV-GDG-INVALID-COUNT                        
                       END-IF                                                   
25248              END-IF                                                       
25248              MOVE SPACES TO RP-STORE-NBR                                  
               END-IF                                                           
                                                                                
               IF RP-STORE-NBR NOT = PV-PREV-STORE-NBR                          
                   IF RP-STORE-NBR NOT = SPACES                                 
                       ADD 1 TO PV-DEFINED-STORE-COUNT                          
                       IF PV-DSN-DELETED-COUNT                                  
                           NOT = PV-PREV-DSN-DELETED-COUNT                      
      *                                                                         
      * WRITE A BLANK LINE TO SEPARATE THE CONTROL CARD BETWEEN STORES          
      *                                                                         
                           MOVE SPACES TO GDG-DELETE-CNTL-REC                   
                           PERFORM Z500-WRITE-GDG-COMMANDS                      
                       END-IF                                                   
                       MOVE PV-DSN-DELETED-COUNT                                
                           TO PV-PREV-DSN-DELETED-COUNT                         
                   END-IF                                                       
                   DISPLAY ' '                                                  
                   MOVE RP-STORE-NBR TO PV-PREV-STORE-NBR                       
               END-IF                                                           
           ELSE                                                                 
               ADD 1 TO PV-INVALID-REQ-COUNT                                    
               MOVE PC-INVALID-REQUEST TO RP-COMMENTS                           
           END-IF.                                                              
                                                                                
           DISPLAY '  ' RP-REPORT-DETAIL.                                       
                                                                                
           PERFORM Z200-READ-REQUEST-FILE.                                      
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B400-FINALIZE.                                                           
                                                                                
           CLOSE   DEL-REQUEST-CNTL-FILE                                        
25248              INVALID-GDG-CNTL-FILE                                        
                   GDG-DELETE-CNTL-FILE.                                        
                                                                                
           DISPLAY ' ------------------------------------------------'.         
           MOVE PV-DEFINED-STORE-COUNT       TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' EST. NUMBER OF STORE(S):   ' PV-NUMERIC-DISPLAY.           
           DISPLAY ' '.                                                         
           MOVE PV-DSN-DELETED-COUNT         TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' VALID DATASET DELETED:     ' PV-NUMERIC-DISPLAY.           
           MOVE PV-DSN-IGNORED-COUNT         TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' VALID DATASET NOT DELETED: ' PV-NUMERIC-DISPLAY.           
           MOVE PV-GDG-IGNORED-COUNT         TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' VALID GDG BASE ENTRIES:    ' PV-NUMERIC-DISPLAY.           
           MOVE PV-DSN-INVALID-COUNT         TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' INVALID DATASET:           ' PV-NUMERIC-DISPLAY.           
           MOVE PV-GDG-INVALID-COUNT         TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' INVALID GDG BASE ENTRIES:  ' PV-NUMERIC-DISPLAY.           
           DISPLAY ' '.                                                         
           MOVE PV-REQUEST-COUNT             TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' TOTAL REQUEST CARDS(S):    ' PV-NUMERIC-DISPLAY.           
           MOVE PV-INVALID-REQ-COUNT         TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' INVALID CONTROL CARD(S):   ' PV-NUMERIC-DISPLAY.           
                                                                                
      ******************************************************************        
      *    GET STORE NUMBER FROM DATASET NAME                                   
      ******************************************************************        
       C100-GET-STORE-NBR.                                                      
                                                                                
           MOVE ZEROES TO PV-FIELDS-COMPLETED                                   
                          PV-HIGH-LVL-COUNT                                     
                          PV-STORE-NBR-COUNT                                    
                          PV-DSN-JUNK-COUNT.                                    
           UNSTRING RQ-DEL-DSN-NAME                                             
W30692         DELIMITED BY PC-MID-DSN-QUAL OR SPACE                            
               INTO PV-SPC-HIGH-LVL-QUAL COUNT IN PV-HIGH-LVL-COUNT             
25248               RP-STORE-NBR         COUNT IN PV-STORE-NBR-COUNT            
                    PV-DSN-JUNK-QUAL     COUNT IN PV-DSN-JUNK-COUNT             
               TALLYING IN PV-FIELDS-COMPLETED                                  
               ON OVERFLOW                                                      
                   PERFORM D100-CHECK-DSN                                       
           END-UNSTRING.                                                        
                                                                                
           MOVE RP-STORE-NBR TO PV-STORE-NUM-X.                                 
           IF PV-STORE-NUM-X NOT NUMERIC                                        
               OR (PV-SPC-HIGH-LVL-QUAL-1 < 'A' OR > 'Z')                       
               MOVE PC-INVALID-DSN TO RP-COMMENTS                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    CREATE THE DELETE COMMANDS                                           
      ******************************************************************        
       C200-CREATE-DEL-CMDS.                                                    
                                                                                
           IF RQ-DEL-SEQ-DSN                                                    
               STRING PC-DELETE-CMD-FIRST                                       
472007                DELIMITED BY SIZE                                         
                      PC-QUOTE                                                  
472007                DELIMITED BY SIZE                                         
                      RP-FILE-NAME                                              
472007                DELIMITED BY SPACE                                        
                      PC-QUOTE                                                  
472007                DELIMITED BY SIZE                                         
                      PC-DELETE-SEQ-CMD                                         
472007                DELIMITED BY SIZE                                         
                      PC-DELETE-CMD-LAST                                        
472007                DELIMITED BY SIZE                                         
472007***********  DELIMITED BY '  '                                            
                   INTO GDG-DELETE-CNTL-REC                                     
                   ON OVERFLOW                                                  
                       MOVE SPACES TO GDG-DELETE-CNTL-REC                       
                       MOVE PC-ERR-IN-BUILDING-CMD TO RP-COMMENTS               
               END-STRING                                                       
               IF GDG-DELETE-CNTL-REC = SPACES                                  
                   ADD 1 TO PV-DSN-IGNORED-COUNT                                
               ELSE                                                             
                   ADD 1 TO PV-DSN-DELETED-COUNT                                
                   PERFORM Z500-WRITE-GDG-COMMANDS                              
               END-IF                                                           
           ELSE                                                                 
               ADD 1 TO PV-GDG-IGNORED-COUNT                                    
               MOVE PC-GDGBASE-NOT-DELETE TO RP-COMMENTS                        
           END-IF.                                                              
                                                                                
25248 ******************************************************************        
25248 *    CREATE THE DELETE COMMANDS FOR INVALID GDGS                          
25248 ******************************************************************        
25248  C300-CREATE-INVALID-GDG.                                                 
25248                                                                           
25248      IF RQ-VALID-DSN-TYPE                                                 
25248          STRING PC-DELETE-CMD-FIRST                                       
25248                 DELIMITED BY SIZE                                         
25248                 PC-QUOTE                                                  
25248                 DELIMITED BY SIZE                                         
25248                 RP-FILE-NAME                                              
25248                 DELIMITED BY SPACE                                        
25248                 PC-QUOTE                                                  
25248                 DELIMITED BY SIZE                                         
25248                 PC-DELETE-GDG-CMD                                         
25248                 DELIMITED BY SIZE                                         
25248                 PC-DELETE-CMD-LAST                                        
25248                 DELIMITED BY SIZE                                         
25248              INTO INVALID-GDG-CNTL-REC                                    
25248              ON OVERFLOW                                                  
25248                  MOVE SPACES TO INVALID-GDG-CNTL-REC                      
25248                  MOVE PC-ERR-IN-BUILDING-CMD TO RP-COMMENTS               
25248          END-STRING                                                       
25248          IF INVALID-GDG-CNTL-REC NOT = SPACES                             
25248              PERFORM Z600-WRITE-INVALID-GDG                               
25248          END-IF                                                           
25248      END-IF.                                                              
25248                                                                           
25248 ******************************************************************        
25248 *    CREATE THE DELETE COMMANDS                                           
25248 ******************************************************************        
25248  C400-CREATE-INVALID-SEQ.                                                 
25248                                                                           
25248      IF RQ-DEL-SEQ-DSN                                                    
25248          STRING PC-DELETE-CMD-FIRST                                       
25248                 DELIMITED BY SIZE                                         
25248                 PC-QUOTE                                                  
25248                 DELIMITED BY SIZE                                         
25248                 RP-FILE-NAME                                              
25248                 DELIMITED BY SPACE                                        
25248                 PC-QUOTE                                                  
25248                 DELIMITED BY SIZE                                         
25248                 PC-DELETE-SEQ-CMD                                         
25248                 DELIMITED BY SIZE                                         
25248                 PC-DELETE-CMD-LAST                                        
25248                 DELIMITED BY SIZE                                         
25248              INTO INVALID-GDG-CNTL-REC                                    
25248              ON OVERFLOW                                                  
25248                  MOVE SPACES TO INVALID-GDG-CNTL-REC                      
25248                  MOVE PC-ERR-IN-BUILDING-CMD TO RP-COMMENTS               
25248          END-STRING                                                       
25248          IF INVALID-GDG-CNTL-REC NOT = SPACES                             
25248              PERFORM Z600-WRITE-INVALID-GDG                               
25248          END-IF                                                           
25248      END-IF.                                                              
25248                                                                           
      ******************************************************************        
      *    CHECK DATASET NAME                                                   
      ******************************************************************        
       D100-CHECK-DSN.                                                          
                                                                                
           IF PV-HIGH-LVL-COUNT = 0                                             
               MOVE PC-INVALID-DSN TO RP-COMMENTS                               
           END-IF.                                                              
                                                                                
25248      IF PV-STORE-NBR-COUNT NOT = 4                                        
W30692       IF PV-STORE-NBR-COUNT NOT = 13                                     
               MOVE PC-INVALID-DSN TO RP-COMMENTS                               
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF PV-DSN-JUNK-COUNT > 0                                             
               MOVE PC-INVALID-DSN TO RP-COMMENTS                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    READ THE CONTROL CARD FILE                                           
      ******************************************************************        
       Z200-READ-REQUEST-FILE.                                                  
                                                                                
           READ DEL-REQUEST-CNTL-FILE INTO RQ-DEL-CNTL-AREA                     
               AT END MOVE PC-Y TO PS-END-OF-DEL-CNTL-FILE.                     
                                                                                
      ******************************************************************        
      *    WRITE OUT THE COMMAND EXTRACT                                        
      ******************************************************************        
       Z500-WRITE-GDG-COMMANDS.                                                 
                                                                                
           INSPECT GDG-DELETE-CNTL-REC                                          
               REPLACING ALL LOW-VALUES BY SPACES                               
                             HIGH-VALUES BY SPACES.                             
                                                                                
           WRITE GDG-DELETE-CNTL-REC.                                           
                                                                                
           MOVE SPACES TO GDG-DELETE-CNTL-REC.                                  
                                                                                
25248 ******************************************************************        
25248 *    WRITE OUT THE COMMAND EXTRACT FOR INVALIDS                           
25248 ******************************************************************        
25248  Z600-WRITE-INVALID-GDG.                                                  
25248                                                                           
25248      INSPECT INVALID-GDG-CNTL-REC                                         
25248          REPLACING ALL LOW-VALUES BY SPACES                               
25248                        HIGH-VALUES BY SPACES.                             
25248                                                                           
25248      WRITE INVALID-GDG-CNTL-REC.                                          
25248                                                                           
25248      MOVE SPACES TO INVALID-GDG-CNTL-REC.                                 
25248                                                                           
