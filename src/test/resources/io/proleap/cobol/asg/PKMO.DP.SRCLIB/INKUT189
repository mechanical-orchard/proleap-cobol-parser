       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT189.                                                 
       AUTHOR.        RONNA BUTZ.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  06/14/2000.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      *            INKUT189 - CREATE DEPT SHORTAGE PERCENT FILE                 
      *                                                                         
      * THIS PROGRAM CREATES CALCULATES THE SHORTAGE PERCENTS THAT              
      * WILL BE USED TO DETERMINE ESTIMATE INVENTORY. THIS PROGRAM              
      * CREATES AN OUTPUT FILE AND A REPORT THAT CONTAINS THE                   
      * SHORTAGE PERCENT BY DEPARTMENT.                                         
      *                                                                         
      * THE PROGRAM READS AN INPUT FILE THAT CONTAINS SHORTAGE DATA FOR         
      * STORES THAT ARE DOING A PHYSICAL INVENTORY, SUMMED TO A                 
      * DEPARTMENT LEVEL.  THEN THE PROGRAM CALCULATES THE SHORTAGE             
      * PERCENT, WRITES A RECORD TO THE OUTPUT FILE, THEN WRITES A              
      * DETAIL LINE TO THE REPORT FILE.                                         
      *                                                                         
      * THE CALCULATION FOR THE SHORTAGE PERCENT IS                             
      * (SHORTAGE RETAIL AMOUNT * 100)/SALES BASE RETAIL AMOUNT                 
      *                                                                         
      * WHEN THERE IS AN ON SIZE ERROR ON SHORTAGE PERCENT, PUT 999.99          
      * IN THE SHORTAGE PERCENT FIELD.                                          
      * ---------------------------------------------------------------*        
      *  DATE: 01/31/2001    CHANGE ID: ESTPCT    WHO: S.JACKSON       *        
      *  FINANCE NEEDS TO BE ABLE TO BALANCE ACT PERCENTAGE ADJUSTMNT  *        
      *  FOR ESTIMATED STORES EQUAL TO SAME PRECISION AS PHYSICALLY    *        
      *  COUNTED STORES. INCREASE PV-PCT, PC-PCT-OVER FIELDS TO ALLOW  *        
      *  CALCULATION OF ADJUSTMENT % TO 2 DECIMAL PLACES OF PRECISION. *        
      *  COPYBOOK INRD134 WAS EXPANDED TO ACCOMODATE AN ADDITIONAL     *        
      *  POSITION OF PRECISION TO IN134-SHORTAGE-PCT.                  *        
      ******************************************************************        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES     -S.JACKSON     *        
      * -------  ----------  ----------------------------------------  *        
      *  22842   10/23/2001  FOLLOWING CHGS MADE TO SHORT PERCENT CALC *        
      *                      - IF SHORT_RETAIL = 0,THEN SHORT % = 0    *        
      *          ID:SJ1001   - IF TOTAL_RETAIL = 0,THEN SHORT % = 999.9*        
      *                      - FORCE SHORT % PERCENT SIGN (+/-) TO BE  *        
      *                        THE SAME SIGN AS SHORT_RETAIL FIELD.    *        
      *                                                                *        
W26600* WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES                  
W26600* ------- ---------- ------------ --------------------------              
W26600* W26600  04/01/2004 M.JOHNSON    STORE EXPANSION                         
W20445* W20445  09-01-2004 ROD JANSSEN  DETAILED INVNENTORY ADJUSTMENT          
W33304* W33304  12-03-2008 SATHISH/     HOLDING INVENTORY LEDGER OPEN.          
W33304*                    INFOSYS                                              
W26600******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SHRTAGE-EXTRACT-FILE                                          
               ASSIGN TO  INP01.                                                
                                                                                
           SELECT SHRTAGE-PCT-FILE                                              
               ASSIGN TO  OUT01.                                                
                                                                                
           SELECT SHRTAGE-PCT-RPT-FILE                                          
               ASSIGN TO  RPT01.                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  SHRTAGE-EXTRACT-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
W33304 01  SHRTAGE-EXTRACT-RECORD      PIC X(170).                              
                                                                                
       FD  SHRTAGE-PCT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SHRTAGE-PCT-RECORD          PIC X(20).                               
                                                                                
                                                                                
       FD  SHRTAGE-PCT-RPT-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SHRTAGE-PCT-RPT-RECORD      PIC X(100).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      * SHORTAGE EXTRACT FILE LAYOUT                                            
      ******************************************************************        
W20445     COPY INRD014.                                                        
                                                                                
      ******************************************************************        
      * DEPARTMENTAL SHORTAGE PERCENT FILE                                      
      ******************************************************************        
           COPY INRD134.                                                        
                                                                                
      ******************************************************************        
      * 200 CHARACTER REPORT LAYOUT                                             
      ******************************************************************        
           COPY DPWS100.                                                        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-FUTURE-DATE           PIC X(10)         VALUE                 
                                                          '9999-09-09'.         
ESTPCT     05  PC-PCT-OVER              PIC S9(09)V99     COMP-3                
ESTPCT                                                    VALUE +999.99.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-SHRTAGE-EXT-SW     PIC X(01)     VALUE 'N'.            
               88  PS-END-OF-SHRTAGE-EXT                  VALUE 'Y'.            
               88  PS-NOT-END-OF-SHRTAGE-EXT              VALUE 'N'.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ABEND-CODE                PIC S9(04)    COMP VALUE +0.        
           05  PV-PARAGRAPH-NAME            PIC X(30).                          
           05  PV-RECS-READ                 PIC S9(04)    COMP VALUE +0.        
           05  PV-OUT01-RECS-WRITTEN        PIC S9(04)    COMP VALUE +0.        
ESTPCT     05  PV-PCT                       PIC S9(09)V99 COMP-3                
                                                          VALUE ZERO.           
           05  PV-LINE-MAX                  PIC S9(02)    VALUE +80.            
           05  PV-LINE-CNT                  PIC S9(02)    VALUE +81.            
           05  PV-PAGE-NBR                  PIC S9(05)    VALUE ZEROS.          
W26600     05  PV-BASE-SALES-TOT            PIC S9(13)V99 COMP-3                
                                                          VALUE ZERO.           
W26600     05  PV-SHORTAGE-RTL-TOT          PIC S9(13)V99 COMP-3                
                                                          VALUE ZERO.           
                                                                                
       01  TD-TODAYS-DATE.                                                      
W26600     05  TD-YEAR                      PIC  9(04)     VALUE ZERO.          
           05  TD-MONTH                     PIC  9(02)     VALUE ZERO.          
           05  TD-DAY                       PIC  9(02)     VALUE ZERO.          
                                                                                
       01  TT-TODAYS-TIME.                                                      
           05  TT-HOUR                      PIC  9(02)     VALUE ZERO.          
           05  TT-MINUTE                    PIC  9(02)     VALUE ZERO.          
           05  FILLER                       PIC  9(04)     VALUE ZERO.          
                                                                                
       01  PH2-PAGE-HEADING-2.                                                  
           05  FILLER                       PIC X(32) VALUE SPACE.              
           05  FILLER                       PIC X(36) VALUE                     
               'DEPARTMENTAL SHORTAGE PERCENT REPORT'.                          
           05  FILLER                       PIC X(32) VALUE SPACE.              
                                                                                
                                                                                
       01  DH1-DETAIL-HEADING-1.                                                
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  DH1-DEPT-HEADING             PIC X(04) VALUE 'DEPT'.             
           05  FILLER                       PIC X(04) VALUE SPACE.              
           05  DH1-DEPT-DESC-HEADING        PIC X(22) VALUE SPACE.              
           05  FILLER                       PIC X(08) VALUE SPACE.              
           05  DH1-BASE-SALES-HEADING       PIC X(18) VALUE SPACE.              
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  DH1-SHORTAGE-RETAIL          PIC X(18) VALUE SPACE.              
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  DH1-SHORTAGE-PCT             PIC X(08) VALUE 'SHORTAGE'.         
           05  FILLER                       PIC X(03) VALUE SPACE.              
                                                                                
       01  DH2-DETAIL-HEADING-2.                                                
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  DH2-DEPT-HEADING              PIC X(04) VALUE 'NBR '.            
           05  FILLER                       PIC X(04) VALUE SPACE.              
           05  DH2-DEPT-DESC-HEADING         PIC X(22) VALUE                    
                                          'DEPARTMENT DESCRIPTION'.             
           05  FILLER                       PIC X(08) VALUE SPACE.              
           05  DH2-BASE-SALES-HEADING        PIC X(18) VALUE                    
                                          'BASE SALES        '.                 
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  DH2-SHORTAGE-RETAIL           PIC X(18) VALUE                    
                                          'SHORTAGE RETAIL   '.                 
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  DH2-SHORTAGE-PCT              PIC X(08) VALUE                    
                                          'PCT     '.                           
           05  FILLER                       PIC X(03) VALUE SPACE.              
                                                                                
                                                                                
       01  UL1-UNDER-LINE-1.                                                    
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  UL1-DEPT-HEADING              PIC X(04) VALUE '----'.            
           05  FILLER                       PIC X(04) VALUE SPACE.              
           05  UL1-DEPT-DESC-HEADING         PIC X(22) VALUE                    
                                          '----------------------'.             
           05  FILLER                       PIC X(08) VALUE SPACE.              
           05  UL1-BASE-SALES-HEADING        PIC X(18) VALUE                    
                                          '------------------'.                 
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  UL1-SHORTAGE-RETAIL           PIC X(18) VALUE                    
                                          '------------------'.                 
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  UL1-SHORTAGE-PCT              PIC X(08) VALUE                    
                                          '--------'.                           
           05  FILLER                       PIC X(03) VALUE SPACE.              
                                                                                
                                                                                
       01  UL1-UNDER-LINE-TOT-1.                                                
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  UL1-DEPT-HEADING             PIC X(04) VALUE SPACE.              
           05  FILLER                       PIC X(04) VALUE SPACE.              
           05  UL1-DEPT-DESC-HEADING        PIC X(22) VALUE SPACE.              
           05  FILLER                       PIC X(08) VALUE SPACE.              
           05  UL1-BASE-SALES-HEADING       PIC X(18) VALUE                     
                                          '=================='.                 
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  UL1-SHORTAGE-RETAIL          PIC X(18) VALUE                     
                                          '=================='.                 
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  UL1-SHORTAGE-PCT             PIC X(08) VALUE                     
                                          '========'.                           
           05  FILLER                       PIC X(03) VALUE SPACE.              
                                                                                
                                                                                
       01  DD1-DETAIL-DATA-1.                                                   
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  DD1-DEPT-NBR                 PIC X(03) VALUE SPACE.              
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  DD1-DEPT-DESC                PIC X(25) VALUE SPACE.              
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  DD1-BASE-SALES               PIC ZZ,ZZZ,ZZZ,ZZZ.99-              
                                                      VALUE ZERO.               
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  DD1-SHORTAGE-RTL             PIC ZZ,ZZZ,ZZZ,ZZZ.99-              
                                                      VALUE ZERO.               
           05  FILLER                       PIC X(05) VALUE SPACE.              
ESTADJ     05  DD1-SHORTAGE-PCT             PIC ZZ9.99-                         
                                                      VALUE ZERO.               
           05  FILLER                       PIC X(04) VALUE SPACE.              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 7000-READ-SHRTAGE-EXT.                                       
           IF PS-END-OF-SHRTAGE-EXT                                             
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM INKUT189 **'                                 
               DISPLAY '** PARAGRAPH 0000-MAIN-MODULE'                          
               DISPLAY '** INPUT FILE, INP01, IS EMPTY.'                        
               MOVE +4008 TO PV-ABEND-CODE                                      
               PERFORM 9999-ABEND.                                              
                                                                                
           PERFORM UNTIL PS-END-OF-SHRTAGE-EXT                                  
                                                                                
               PERFORM 2000-CALC-SHORTAGE-PCT                                   
               PERFORM 3000-CREATE-OUT01                                        
               PERFORM 4000-CREATE-RPT01                                        
               PERFORM 7000-READ-SHRTAGE-EXT                                    
                                                                                
           END-PERFORM.                                                         
                                                                                
           PERFORM 4200-PRINT-TOTALS.                                           
                                                                                
           CLOSE SHRTAGE-EXTRACT-FILE                                           
                 SHRTAGE-PCT-FILE                                               
                 SHRTAGE-PCT-RPT-FILE.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  SHRTAGE-EXTRACT-FILE                                     
                OUTPUT SHRTAGE-PCT-FILE                                         
                       SHRTAGE-PCT-RPT-FILE.                                    
                                                                                
      *    BUILD STANDARD REPORT HEADING.                                       
                                                                                
           ACCEPT TD-TODAYS-DATE FROM DATE YYYYMMDD.                            
           MOVE TD-YEAR    TO DP100-RUN-YEAR.                                   
           MOVE TD-MONTH   TO DP100-RUN-MONTH.                                  
           MOVE TD-DAY     TO DP100-RUN-DAY.                                    
                                                                                
           ACCEPT TT-TODAYS-TIME FROM TIME.                                     
           MOVE TT-HOUR    TO DP100-RUN-HOUR.                                   
           MOVE TT-MINUTE  TO DP100-RUN-MINUTE.                                 
                                                                                
           MOVE 'INKUT189' TO DP100-PROGRAM-NAME.                               
           MOVE 1          TO DP100-REPORT-NUMBER.                              
                                                                                
                                                                                
      ******************************************************************        
      * CALCULATE THE SHORTAGE PERCENT                                          
      ******************************************************************        
       2000-CALC-SHORTAGE-PCT.                                                  
                                                                                
      *    SHORTAGE PERCENT                                                     
           COMPUTE PV-PCT ROUNDED = (IN014-SHORTAGE-RTL-AMT                     
                                  *  100)                                       
                                  /  IN014-SLS-BSE-RTL-AMT                      
                   ON SIZE ERROR                                                
                       MOVE PC-PCT-OVER TO PV-PCT                               
           END-COMPUTE.                                                         
SJ1001*                                                                         
SJ1001*        SHRINK PERCENT MUST BE SAME SIGN (+/-) AS SHORT-RETAIL           
SJ1001         IF IN014-SHORTAGE-RTL-AMT > 0                                    
SJ1001             IF PV-PCT < 0                                                
SJ1001                 COMPUTE PV-PCT = (PV-PCT * -1)                           
SJ1001             END-IF                                                       
SJ1001         ELSE                                                             
SJ1001            IF IN014-SHORTAGE-RTL-AMT < 0                                 
SJ1001                IF PV-PCT > 0                                             
SJ1001                    COMPUTE PV-PCT = (PV-PCT * -1)                        
SJ1001                END-IF                                                    
SJ1001            ELSE                                                          
SJ1001                MOVE ZERO TO PV-PCT                                       
SJ1001            END-IF                                                        
SJ1001         END-IF                                                           
SJ1001*                                                                         
                                                                                
ESTPCT     IF PV-PCT > 999.99 OR                                                
ESTPCT        PV-PCT < - 999.99                                                 
               MOVE PC-PCT-OVER         TO PV-PCT                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * MAP DATA TO OUTPUT FILE FOR DEPARTMENTAL SHORTAGE PERCENT               
      * AND WRITE A RECORD                                                      
      ******************************************************************        
       3000-CREATE-OUT01.                                                       
                                                                                
           MOVE IN014-DEPT-NBR          TO IN134-DEPT-NBR.                      
           MOVE PV-PCT                  TO IN134-SHORTAGE-PCT.                  
                                                                                
           WRITE SHRTAGE-PCT-RECORD                                             
               FROM IN134-DEPT-SHORTAGE-PCT-REC.                                
                                                                                
           ADD +1                       TO PV-OUT01-RECS-WRITTEN.               
                                                                                
                                                                                
      ******************************************************************        
      *  MAP DATA TO REPORT DETAIL RECORD AND WRITE DETAIL LINE                 
      ******************************************************************        
       4000-CREATE-RPT01.                                                       
                                                                                
           IF PV-LINE-CNT > PV-LINE-MAX                                         
               PERFORM 4100-PRODUCE-HEADINGS                                    
           END-IF.                                                              
                                                                                
           MOVE IN014-DEPT-NBR          TO DD1-DEPT-NBR.                        
                                                                                
           MOVE IN014-DEPT-MDSEAR-DESC  TO DD1-DEPT-DESC.                       
                                                                                
           MOVE IN014-SLS-BSE-RTL-AMT   TO DD1-BASE-SALES.                      
                                                                                
           MOVE IN014-SHORTAGE-RTL-AMT  TO DD1-SHORTAGE-RTL.                    
                                                                                
           MOVE PV-PCT                  TO DD1-SHORTAGE-PCT.                    
                                                                                
           ADD  IN014-SLS-BSE-RTL-AMT   TO PV-BASE-SALES-TOT.                   
                                                                                
           ADD  IN014-SHORTAGE-RTL-AMT  TO PV-SHORTAGE-RTL-TOT.                 
                                                                                
           WRITE SHRTAGE-PCT-RPT-RECORD                                         
               FROM DD1-DETAIL-DATA-1                                           
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           ADD +1 TO PV-LINE-CNT.                                               
                                                                                
                                                                                
      ******************************************************************        
      * CREATE HEADINGS                                                         
      ******************************************************************        
       4100-PRODUCE-HEADINGS.                                                   
                                                                                
           ADD 1 TO PV-PAGE-NBR.                                                
           MOVE PV-PAGE-NBR TO DP100-PAGE-NUMBER.                               
                                                                                
           WRITE SHRTAGE-PCT-RPT-RECORD                                         
               FROM DP100-STANDRD-HEADER-100-COLS                               
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE SHRTAGE-PCT-RPT-RECORD                                         
               FROM PH2-PAGE-HEADING-2                                          
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           WRITE SHRTAGE-PCT-RPT-RECORD                                         
               FROM DH1-DETAIL-HEADING-1                                        
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           WRITE SHRTAGE-PCT-RPT-RECORD                                         
               FROM DH2-DETAIL-HEADING-2                                        
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           WRITE SHRTAGE-PCT-RPT-RECORD                                         
               FROM UL1-UNDER-LINE-1                                            
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           MOVE +7 TO PV-LINE-CNT.                                              
                                                                                
                                                                                
      ******************************************************************        
      *  PRINT TOTAL LINE                                                       
      ******************************************************************        
       4200-PRINT-TOTALS.                                                       
                                                                                
           IF PV-LINE-CNT > (PV-LINE-MAX - 2)                                   
               PERFORM 4100-PRODUCE-HEADINGS                                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO DD1-DEPT-NBR                         
                                           DD1-DEPT-DESC.                       
                                                                                
           MOVE PV-BASE-SALES-TOT       TO DD1-BASE-SALES.                      
                                                                                
           MOVE PV-SHORTAGE-RTL-TOT     TO DD1-SHORTAGE-RTL.                    
                                                                                
      *    SHORTAGE PERCENT                                                     
           COMPUTE PV-PCT ROUNDED = (PV-SHORTAGE-RTL-TOT                        
                                  *  100)                                       
                                  /  PV-BASE-SALES-TOT                          
                   ON SIZE ERROR                                                
                       MOVE PC-PCT-OVER TO PV-PCT                               
           END-COMPUTE.                                                         
                                                                                
SJ1001*                                                                         
SJ1001*        SHRINK PERCENT MUST BE SAME SIGN (+/-) AS SHORT-RETAIL           
SJ1001         IF PV-SHORTAGE-RTL-TOT > 0                                       
SJ1001             IF PV-PCT < 0                                                
SJ1001                 COMPUTE PV-PCT = (PV-PCT * -1)                           
SJ1001             END-IF                                                       
SJ1001         ELSE                                                             
SJ1001            IF PV-SHORTAGE-RTL-TOT < 0                                    
SJ1001                IF PV-PCT > 0                                             
SJ1001                    COMPUTE PV-PCT = (PV-PCT * -1)                        
SJ1001                END-IF                                                    
SJ1001            ELSE                                                          
SJ1001                MOVE ZERO TO PV-PCT                                       
SJ1001            END-IF                                                        
SJ1001         END-IF                                                           
SJ1001*                                                                         
                                                                                
           IF PV-PCT > 999.9 OR                                                 
              PV-PCT < - 999.9                                                  
               MOVE PC-PCT-OVER         TO PV-PCT                               
           END-IF.                                                              
                                                                                
           MOVE PV-PCT                  TO DD1-SHORTAGE-PCT.                    
                                                                                
           WRITE SHRTAGE-PCT-RPT-RECORD                                         
               FROM UL1-UNDER-LINE-TOT-1                                        
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           WRITE SHRTAGE-PCT-RPT-RECORD                                         
               FROM DD1-DETAIL-DATA-1                                           
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           ADD +1 TO PV-LINE-CNT.                                               
                                                                                
                                                                                
      ******************************************************************        
      * READ SHORTAGE EXTRACT INPUT FILE                                        
      ******************************************************************        
       7000-READ-SHRTAGE-EXT.                                                   
                                                                                
           READ SHRTAGE-EXTRACT-FILE INTO IN014-SHORTAGE-EXTR-REC               
               AT END SET PS-END-OF-SHRTAGE-EXT TO TRUE                         
           END-READ.                                                            
                                                                                
           IF PS-NOT-END-OF-SHRTAGE-EXT                                         
               ADD +1                       TO PV-RECS-READ.                    
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
