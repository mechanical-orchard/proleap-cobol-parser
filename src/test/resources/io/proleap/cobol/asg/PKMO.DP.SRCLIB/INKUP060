       ID  DIVISION.                                                            
       PROGRAM-ID.         INKUP060.                                            
       AUTHOR.             J SEIDLER.                                           
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       1/99.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *           PHYSICAL INVENTORY DATA DELETE                                
      *                                                                         
      *   THIS PROGRAMS DELETES THE PHYSICAL COUNT DATA FROM THE                
      *              TSUMINV                                                    
      *              TADJADD                                                    
      *              TADJDTL                                                    
      *              TSTKRMW                                                    
      *              TINVDTL                                                    
      *              TINVLOC                                                    
      *              INT_INV_CTOFF (INT00000)                                   
      *    ROWS WILL BE DELETED FOR STORES WHOSE PLND-INV-DATE IS               
      *    IN THE FUTURE AND THE CUTOFF HAS NOT TAKEN PLACE.                    
      *                                                                         
L27808*    THE PROGRAM WILL TAKE IN AN OPTIONAL CONTROL CARD FOR                
L27808*    EXECUTION DATE.  THE DATE WILL DEFAULT TO SYSTEM DATE                
L27808*    IF THERE IS NO VALID DATE IN THE OPTIONAL CONTROL CARD.              
      *                                                                         
      *    ALSO AN INPUT CONTROL CARD WILL LIST ANY OTHER STORES                
      *    WHOSE PHYSICAL INVENTORY DATA NEEDS TO BE DELETED.                   
      *                                                                         
21015 *CHANGE HISTORY:                                                          
21015 * 01/03/2001 - ROD JANSSEN - WR# 21015                                    
21015 *              ADDED TINENTY TO THE PURGE PROCESS.....                    
21015 *                                                                         
W26600*  W26600  06-  -2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
W26600*                                                                         
      * 04/13/2005 - ERIN SEARL  - WR# 28543                                    
      *              ADDED INT_INV_CTOFF TO THE PURGE PROCESS                   
      *                                                                         
W28543* 05/11/2005 - S. JACKSON  - WR# 28543                                    
      *              DELETION FROM CUTOFF DB EXCEEDED PAGE LOCK LIMIT           
      *                                                                         
      * 05/12/2005 - ERIN SEARL  - WR# 28543                                    
      *              REMOVED INT_INV_CTOFF FROM THE PURGE PROCESS               
      *                                                                         
      * 05/26/2005 - ERIN SEARL  - WR# 28543                                    
      *              SEND 4095 RETURN CODE WHEN DATA IS PURGED                  
      *                                                                         
W27808* 08/08/2005 - S. JACKSON - WR# 27808                                     
W27808*              INCLUDE THE CUTOFF TABLE IN THE CLEAN UP PROCESS           
W27808*                                                                         
L27808* 09/07/2005 - V.L. YANG  - WR# 27808                                     
L27808*              REMOVE OBSOLETE CODE AND UPDATE THE RETURN CODE/           
L27808*              CONDITION AS THE LAST THING TO DO.                         
L27808*                                                                         
W28545* 07/19/2006 - J. SELWITSCHKA / STRATAGEM  - WR# 28545                    
W28545*              DEPARTMENT LEVEL INVENTORY. INCORPORATE INVENTORY          
W28545*              ID.                                                        
      *                                                                         
      * 01/14/2008 - TODD C. MICHALEK - INC000000222945                         
      *              - CHANGED THE PROGRAM'S DELETE/COMMIT STRATEGY FROM        
      *              DOING A COMMIT AFTER A LOCATION'S WORTH OF DATA WAS        
      *              DELETED FROM EACH OF THE 7 DELETE TABLES (TSTKRMW,         
      *              TINENTY, TADJADD, TADJDTL, TINVDTL, TSUMINV,               
      *              TINVLOC), TO READING A LOCATION NUMBER, DOING AN           
      *              INDIVIDUAL TABLE LOCK ON EACH OF THE 7 DELETE              
      *              TABLES, DELETING THE LOCATION'S WORTH OF DATA ON           
      *              ALL THE DELETE TABLES, AND THEN DOING A COMMIT FOR         
      *              THE COMPLETED LOCATION PACKET.                             
W33304* 04/03/2008 - CHUCK EBERT HOLD INVENTORY LEDGER OPEN ACROSS              
W33304*              PERIODS                                                    
249895* 10/08/2020 - MAKING THE PROGRAM TO COMMIT FOR EVERY TABLE               
249895*              DELETE TO REDUCE THE LOGS FILLED IN DB2.                   
0423RM* 04/13/2023 - BOB MCASEY  CHG                                            
0423RM*              CHANGE DELETE LOGIC TO USE FETCH FIRST N ROWS ONLY         
0423RM*              FOR THE INT_INV_CTOFF AND TSUMINV TABLES                   
0423RM* 01/08/2024 - JEAN KURK   CHG0298376                                     
0423RM*              CHANGE DELETE LOGIC TO USE FETCH FIRST N ROWS ONLY         
0423RM*              FOR THE TINENTY TABLE                                      
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
L27808     SELECT MANUAL-REQ-STORES-CC                                          
               ASSIGN TO INP01.                                                 
                                                                                
L27808     SELECT OPTIONAL REQ-EXEC-OPT-CC                                      
L27808         ASSIGN TO INP02.                                                 
                                                                                
      *    SELECT STORE-FILE-CC                                                 
      *        ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
L27808 FD  MANUAL-REQ-STORES-CC                                                 
           RECORDING  MODE IS F                                                 
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
L27808 01  MANUAL-REQ-STORES-CC-REC        PIC X(80).                           
                                                                                
L27808 FD  REQ-EXEC-OPT-CC                                                      
L27808     RECORDING  MODE IS F                                                 
L27808     LABEL RECORDS ARE STANDARD                                           
L27808     BLOCK CONTAINS 0 RECORDS.                                            
L27808 01  REQ-EXEC-OPT-CC-REC             PIC X(80).                           
                                                                                
      *FD  STORE-FILE-CC                                                        
      *    RECORDING  MODE IS F                                                 
      *    LABEL RECORDS ARE STANDARD                                           
      *    BLOCK CONTAINS 0 RECORDS.                                            
      *01  STORE-RECORD                    PIC X(5).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZERO           
                               COMP SYNC.                                       
L27808     88  ABEND-WS-TABLE                              VALUE +4003.         
L27808     88  ABEND-CONTROL-CARD                          VALUE +4005.         
L27808     88  ABEND-SQL-ISSUE                             VALUE +4013.         
                                                                                
W26600******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-TINVPAR-CSR-EOF-SW       PIC X(01)       VALUE 'N'.           
               88  PS-TINVPAR-CSR-EOF                      VALUE 'Y'.           
L27808         88  PS-TINVPAR-CSR-NOT-EOF                  VALUE 'N'.           
           05  PS-REQ-STORES-EOF-SW        PIC X(01)       VALUE 'N'.           
               88  PS-REQ-STORES-EOF                       VALUE 'Y'.           
L27808     05  PS-REQ-OPTION-EOF-SW        PIC X(01)       VALUE 'N'.           
L27808         88  PS-REQ-OPTION-EOF                       VALUE 'Y'.           
0423RM     05  PS-TSUMINV-EOF-SW           PIC X(01)       VALUE 'N'.           
0423RM         88  PS-TSUMINV-EOF                          VALUE 'Y'.           
0423RM     05  PS-TINENTY-EOF-SW           PIC X(01)       VALUE 'N'.           
0423RM         88  PS-TINENTY-EOF                          VALUE 'Y'.           
0423RM     05  PS-INT-INV-CTOFF-EOF-SW     PIC X(01)       VALUE 'N'.           
0423RM         88  PS-INT-INV-CTOFF-EOF                    VALUE 'Y'.           
                                                                                
W26600******************************************************************        
W26600 01  PT-PROGRAM-TOTALS.                                                   
W28543     05  PT-NBR-TSTKRMW-RCDS-DLTD    PIC 9(09)       VALUE ZEROS.         
W28543     05  PT-NBR-TINENTY-RCDS-DLTD    PIC 9(09)       VALUE ZEROS.         
W28543     05  PT-NBR-TADJADD-RCDS-DLTD    PIC 9(09)       VALUE ZEROS.         
W28543     05  PT-NBR-TADJDTL-RCDS-DLTD    PIC 9(09)       VALUE ZEROS.         
W28543     05  PT-NBR-TINVDTL-RCDS-DLTD    PIC 9(09)       VALUE ZEROS.         
W28543     05  PT-NBR-TSUMINV-RCDS-DLTD    PIC 9(09)       VALUE ZEROS.         
W28543     05  PT-NBR-TINVLOC-RCDS-DLTD    PIC 9(09)       VALUE ZEROS.         
L27808     05  PT-NBR-INT00000-RCDS-DLTD   PIC 9(13)       VALUE ZEROS.         
L27808     05  PT-TTL-STORE-DELETES        PIC 9(13)       VALUE ZEROS.         
L27808     05  PT-GRAND-TOTAL-DELETES      PIC 9(15)       VALUE ZEROS.         
           05  PT-COMMIT-COUNT             PIC 9(09)       VALUE ZEROS.         
0423RM     05  WS-NBR-INT00000-RCDS-DLTD   PIC 9(09)       VALUE ZEROS.         
                                                                                
W26600******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME           PIC X(30)       VALUE SPACE.         
L27808     05  PV-PROGRAM-NAME             PIC X(08)       VALUE                
L27808         'INKUP060'.                                                      
           05  PV-DB2-OPER-ATTEMPTED       PIC X(30)       VALUE SPACE.         
           05  PV-DEFAULT-DATE             PIC X(10)       VALUE                
               '9999-09-09'.                                                    
           05  PV-SYSTEM-DATE.                                                  
L27808         10  PV-SYS-CCYY             PIC 9(04)       VALUE ZEROES.        
L27808         10  FILLER REDEFINES PV-SYS-CCYY.                                
                   15  PV-SYS-CC           PIC 9(02).                           
                   15  PV-SYS-YY           PIC 9(02).                           
               10  PV-SYS-MM               PIC 9(02)       VALUE ZEROES.        
               10  PV-SYS-DD               PIC 9(02)       VALUE ZEROES.        
L27808     05  PV-DB2-DATE                 PIC X(10)       VALUE                
L27808         '9999-09-09'.                                                    
L27808     05  FILLER REDEFINES PV-DB2-DATE.                                    
L27808         10  PV-DB2-CCYY             PIC 9(04).                           
L27808         10  FILLER REDEFINES PV-DB2-CCYY.                                
                   15  PV-DB2-CC           PIC 9(02).                           
                   15  PV-DB2-YY           PIC 9(02).                           
               10  FILLER                  PIC X(01).                           
               10  PV-DB2-MM               PIC 9(02).                           
               10  FILLER                  PIC X(01).                           
               10  PV-DB2-DD               PIC 9(02).                           
                                                                                
W26600******************************************************************        
      *  MANUALLY REQUESTED LIST OF STORES TO BE PURGED/DELETED                 
      *****************************************************************         
L27808 01  CC-MANUAL-REQ-STORES.                                                
           05  CC-DELETE-STORE             PIC X(04)       VALUE ZEROS.         
L27808     05  FILLER                      PIC X(76)       VALUE SPACES.        
                                                                                
L27808******************************************************************        
L27808*  OPTIONAL EXECUTION OPTION/DATE                                         
L27808*****************************************************************         
L27808 01  CC-EXEC-OPTION.                                                      
L27808     05  CC-EXEC-DATE                PIC X(10)       VALUE SPACES.        
L27808     05  FILLER REDEFINES CC-EXEC-DATE.                                   
L27808         10  CC-EXEC-DATE-CCYY       PIC X(04).                           
L27808         10  FILLER                  PIC X(01).                           
L27808             88  CC-EXEC-DATE-VALID-DASH-1           VALUE '-'.           
L27808         10  CC-EXEC-DATE-MM         PIC X(02).                           
L27808         10  FILLER                  PIC X(01).                           
L27808             88  CC-EXEC-DATE-VALID-DASH-2           VALUE '-'.           
L27808         10  CC-EXEC-DATE-DD         PIC X(02).                           
L27808     05  FILLER                      PIC X(70)       VALUE SPACES.        
                                                                                
W26600******************************************************************        
       01  TB-DELETE-STORE-TABLE.                                               
L27808     05  TB-DELETE-STORE-MAX         PIC 9(04)       VALUE 9999.          
L27808     05  TB-DELETE-STORE-SUB         PIC 9(05)       VALUE ZEROES.        
L27808     05  TB-DELETE-STORE-CNT         PIC 9(04)       VALUE ZEROS.         
           05  TB-DELETE-STORE-TAB     OCCURS 9999 TIMES.                       
W26600         10  TB-DELETE-STORE-NBR     PIC 9(04).                           
               10  TB-PLND-INV-DTE         PIC X(10).                           
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DB2 INVENTORY PARMS                                                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
W28545*****************************************************************         
W28545* DB2 INVENTORY PARMS CONTROL TABLE                                       
W28545*****************************************************************         
W28545     EXEC SQL                                                             
W28545         INCLUDE TINCNTL                                                  
W28545     END-EXEC.                                                            
W28545                                                                          
      *****************************************************************         
      * DB2 INVENTORY LOCATION                                                  
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVLOC                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 SUMMARY INVENTORY                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSUMINV                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 STOCKROOM ADJUSTMENTS                                               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSTKRMW                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 ADJUSTMENTS DETAIL                                                  
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TADJDTL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 ADJUSTMENTS ADD                                                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TADJADD                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 INVALID DETAIL                                                      
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVDTL                                                  
           END-EXEC.                                                            
                                                                                
28543 *****************************************************************         
28543 * DB2 CUTOFF TABLE                                                        
28543 *****************************************************************         
27808      EXEC SQL                                                             
27808          INCLUDE INT00000                                                 
27808      END-EXEC.                                                            
27808                                                                           
21015 *****************************************************************         
  |   * DB2 SHEET INVENTORY                                                     
  |   *****************************************************************         
  |        EXEC SQL                                                             
  |            INCLUDE TINENTY                                                  
21015      END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      /                                                                         
      *****************************************************************         
      *  TINVPAR CURSOR                                                         
      *****************************************************************         
           EXEC SQL DECLARE TINVPAR_CSR CURSOR FOR                              
                                                                                
W26600        SELECT                                                            
                     LOC_NBR                                                    
                    ,PLND_INV_DTE                                               
              FROM                                                              
W28545             TINVPAR A                                                    
W28545            ,TINCNTL B                                                    
              WHERE                                                             
W33304              A.LOC_INV_STAT_CDE = 'IN'                                   
                AND ACTL_FIN_BK_DTE = :PV-DEFAULT-DATE                          
L27808          AND (PLND_INV_DTE > :PV-DB2-DATE)                               
                AND ITM_CTOFF_IND = 'N'                                         
W28545          AND B.ACTV_IND    = 'Y'                                         
W28545          AND A.INV_ID      = B.INV_ID                                    
W26600        ORDER BY LOC_NBR                                                  
           END-EXEC.                                                            
                                                                                
W26600/                                                                         
W26600*****************************************************************         
       LINKAGE SECTION.                                                         
                                                                                
W26600                                                                          
W26600*****************************************************************         
       PROCEDURE DIVISION.                                                      
                                                                                
W26600 0000-PHYSICAL-INV-DLT.                                                   
                                                                                
           PERFORM 0100-INITIALIZATION.                                         
           PERFORM 0200-LOAD-WS-STORE-TABLE.                                    
                                                                                
L27808     IF TB-DELETE-STORE-SUB > 0                                           
L27808         DISPLAY ' ... ' FUNCTION CURRENT-DATE                            
               PERFORM 1000-DELETE-SLCTD-STORES                                 
L27808             VARYING TB-DELETE-STORE-SUB FROM 1 BY 1                      
L27808             UNTIL TB-DELETE-STORE-SUB GREATER THAN                       
L27808                 TB-DELETE-STORE-CNT                                      
                                                                                
L27808         DISPLAY ' '                                                      
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - TSTKRMW RECS DELETED '                               
                       PT-NBR-TSTKRMW-RCDS-DLTD                                 
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - TINENTY RECS DELETED '                               
                       PT-NBR-TINENTY-RCDS-DLTD                                 
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - TADJADD RECS DELETED '                               
                       PT-NBR-TADJADD-RCDS-DLTD                                 
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - TADJDTL RECS DELETED '                               
                       PT-NBR-TADJDTL-RCDS-DLTD                                 
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - TINVDTL RECS DELETED '                               
                       PT-NBR-TINVDTL-RCDS-DLTD                                 
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - TSUMINV RECS DELETED '                               
                       PT-NBR-TSUMINV-RCDS-DLTD                                 
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - TINVLOC RECS DELETED '                               
                       PT-NBR-TINVLOC-RCDS-DLTD                                 
                                                                                
L27808         DISPLAY ' '                                                      
L27808         PERFORM 2000-PURGE-FROM-INT00000                                 
L27808         ADD PT-NBR-INT00000-RCDS-DLTD TO PT-GRAND-TOTAL-DELETES          
L27808*        PERFORM 7990-COMMIT-WORK                                         
                                                                                
L27808         DISPLAY ' '                                                      
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - COMMITS TAKEN ' PT-COMMIT-COUNT                      
L27808                 ' ... ' FUNCTION CURRENT-DATE                            
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - GRAND TOTAL DELETES '                                
                       PT-GRAND-TOTAL-DELETES                                   
           END-IF.                                                              
                                                                                
L27808     PERFORM 0900-CLEAN-UP.                                               
                                                                                
W28543     IF (PT-NBR-TSTKRMW-RCDS-DLTD NOT EQUAL ZEROS) OR                     
W28543        (PT-NBR-TINENTY-RCDS-DLTD NOT EQUAL ZEROS) OR                     
W28543        (PT-NBR-TADJADD-RCDS-DLTD NOT EQUAL ZEROS) OR                     
W28543        (PT-NBR-TADJDTL-RCDS-DLTD NOT EQUAL ZEROS) OR                     
W28543        (PT-NBR-TINVDTL-RCDS-DLTD NOT EQUAL ZEROS) OR                     
W28543        (PT-NBR-TSUMINV-RCDS-DLTD NOT EQUAL ZEROS) OR                     
W28543        (PT-NBR-TINVLOC-RCDS-DLTD NOT EQUAL ZEROS) OR                     
L27808        (PT-NBR-INT00000-RCDS-DLTD NOT EQUAL ZEROS)                       
W28543         MOVE 4095 TO RETURN-CODE                                         
W28543     ELSE                                                                 
W28543         MOVE 0    TO RETURN-CODE                                         
W28543     END-IF.                                                              
                                                                                
           GOBACK.                                                              
                                                                                
W26600*****************************************************************         
       0100-INITIALIZATION.                                                     
                                                                                
L27808     OPEN INPUT MANUAL-REQ-STORES-CC                                      
L27808                REQ-EXEC-OPT-CC.                                          
      *         OUTPUT STORE-FILE-CC.                                           
                                                                                
L27808     READ REQ-EXEC-OPT-CC INTO CC-EXEC-OPTION                             
L27808         AT END                                                           
L27808             SET PS-REQ-OPTION-EOF TO TRUE.                               
L27808     IF PS-REQ-OPTION-EOF                                                 
L27808         OR CC-EXEC-DATE-CCYY NOT NUMERIC                                 
L27808         OR CC-EXEC-DATE-MM   NOT NUMERIC                                 
L27808         OR CC-EXEC-DATE-DD   NOT NUMERIC                                 
L27808         OR NOT CC-EXEC-DATE-VALID-DASH-1                                 
L27808         OR NOT CC-EXEC-DATE-VALID-DASH-2                                 
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
L27808                 ' - USING SYSTEM DATE FOR EXECUTION DATE'                
L27808         ACCEPT PV-SYSTEM-DATE       FROM DATE YYYYMMDD                   
L27808         MOVE PV-SYS-MM              TO PV-DB2-MM                         
L27808         MOVE PV-SYS-DD              TO PV-DB2-DD                         
L27808         MOVE PV-SYS-CCYY            TO PV-DB2-CCYY                       
L27808     ELSE                                                                 
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
L27808                 ' - USING PROVIDED EXECUTION DATE'                       
L27808         MOVE CC-EXEC-DATE-MM        TO PV-DB2-MM                         
L27808         MOVE CC-EXEC-DATE-DD        TO PV-DB2-DD                         
L27808         MOVE CC-EXEC-DATE-CCYY      TO PV-DB2-CCYY                       
L27808     END-IF.                                                              
L27808     DISPLAY ' ' PV-PROGRAM-NAME                                          
                   ' - RUN DATE ' PV-DB2-DATE.                                  
L27808     DISPLAY ' '.                                                         
                                                                                
W26600*****************************************************************         
      * LOAD WS DELETE STORE TABLE                                              
      *****************************************************************         
       0200-LOAD-WS-STORE-TABLE.                                                
                                                                                
           MOVE ZEROS      TO TB-DELETE-STORE-SUB.                              
L27808     SET PS-TINVPAR-CSR-NOT-EOF TO TRUE.                                  
                                                                                
W26600     PERFORM 7230-OPEN-TINVPAR-CSR.                                       
                                                                                
W26600     PERFORM 7231-FETCH-TINVPAR-CSR                                       
               UNTIL TB-DELETE-STORE-SUB > TB-DELETE-STORE-MAX                  
               OR PS-TINVPAR-CSR-EOF.                                           
                                                                                
W26600     PERFORM 7232-CLOSE-TINVPAR-CSR.                                      
                                                                                
           IF TB-DELETE-STORE-SUB > TB-DELETE-STORE-MAX                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
W26600                 ' - MORE THAN 9999 STORES EXIST   '                      
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - MUST EXPAND TABLE             '                      
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - A B E N D                   * '                      
               MOVE '*0200-LOAD-WS-STORE-TABLE*' TO PV-PARAGRAPH-NAME           
L27808         SET ABEND-WS-TABLE  TO TRUE                                      
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM 8100-READ-CONTROL-CARD-STRS                                  
               UNTIL TB-DELETE-STORE-SUB > TB-DELETE-STORE-MAX                  
                   OR PS-REQ-STORES-EOF.                                        
                                                                                
           IF TB-DELETE-STORE-SUB > TB-DELETE-STORE-MAX                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
W26600                 ' - MORE THAN 9999 STORES EXIST   '                      
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - MUST EXPAND TABLE             '                      
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - A B E N D                   * '                      
               MOVE '*0200-LOAD-WS-STORE-TABLE*' TO PV-PARAGRAPH-NAME           
L27808         SET ABEND-WS-TABLE  TO TRUE                                      
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
L27808     IF TB-DELETE-STORE-SUB = ZEROES                                      
               DISPLAY '* * * * * * * * * * * * * * * '                         
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - NO STORES QUALIFY FOR DELETION'                      
               DISPLAY '* * * * * * * * * * * * * * * '                         
           ELSE                                                                 
               MOVE TB-DELETE-STORE-SUB TO TB-DELETE-STORE-CNT                  
           END-IF.                                                              
                                                                                
W26600*****************************************************************         
       0900-CLEAN-UP.                                                           
                                                                                
L27808     CLOSE MANUAL-REQ-STORES-CC                                           
L27808           REQ-EXEC-OPT-CC.                                               
      *          STORE-FILE-CC.                                                 
                                                                                
W26600/                                                                         
W26600*****************************************************************         
       1000-DELETE-SLCTD-STORES.                                                
                                                                                
           MOVE '1000-DELETE-SLCTD-STORES  '                                    
               TO PV-PARAGRAPH-NAME.                                            
           PERFORM 1100-LOCK-DELETE-TABLES.                                     
           MOVE TB-DELETE-STORE-NBR (TB-DELETE-STORE-SUB)                       
                                       TO INVPAR-LOC-NBR.                       
           PERFORM 7340-DELETE-TSTKRMW.                                         
249895     PERFORM 7990-COMMIT-WORK.                                            
           MOVE 'N' TO PS-TINENTY-EOF-SW.                                       
           PERFORM 7440-DELETE-TINENTY                                          
               UNTIL PS-TINENTY-EOF.                                            
249895*    PERFORM 7990-COMMIT-WORK.                                            
           PERFORM 7540-DELETE-TADJADD.                                         
249895     PERFORM 7990-COMMIT-WORK.                                            
           PERFORM 7640-DELETE-TADJDTL.                                         
249895     PERFORM 7990-COMMIT-WORK.                                            
           PERFORM 7740-DELETE-TINVDTL.                                         
249895     PERFORM 7990-COMMIT-WORK.                                            
0423RM     MOVE 'N' TO PS-TSUMINV-EOF-SW.                                       
           PERFORM 7840-DELETE-TSUMINV                                          
0423RM         UNTIL PS-TSUMINV-EOF.                                            
           PERFORM 7940-DELETE-TINVLOC.                                         
249895     PERFORM 7990-COMMIT-WORK.                                            
                                                                                
L27808     DISPLAY ' ' PV-PROGRAM-NAME                                          
                   ' - ' PT-TTL-STORE-DELETES                                   
                   ' ROWS DELETED FOR STORE '                                   
                   TB-DELETE-STORE-NBR(TB-DELETE-STORE-SUB)                     
L27808             ' ... ' FUNCTION CURRENT-DATE.                               
                                                                                
249895*    PERFORM 7990-COMMIT-WORK.                                            
                                                                                
249895*    DISPLAY ' '                                                          
249895*    DISPLAY ' ' PV-PROGRAM-NAME                                          
249895*            ' - COMMITS TAKEN ' PT-COMMIT-COUNT                          
249895*            ' ... ' FUNCTION CURRENT-DATE.                               
                                                                                
           ADD PT-TTL-STORE-DELETES    TO PT-GRAND-TOTAL-DELETES.               
           MOVE ZEROES                 TO PT-TTL-STORE-DELETES.                 
      ****************************************************************          
      * LOCK THE 7 DELETE TABLES IN EXCLUSIVE MODE (TABLESPACE LOCK):           
      * TSTKRMW, TINENTY, TADJADD, TADJDTL, TINVDTL, TSUMINV, TINVLOC           
      ****************************************************************          
       1100-LOCK-DELETE-TABLES.                                                 
           EXEC SQL                                                             
               LOCK TABLE TSTKRMW IN EXCLUSIVE MODE                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE 'TSTKRMW LOCK TABLE FAILED'                                 
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '1100-LOCK-DELETE-TABLES'                                   
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
               LOCK TABLE TINENTY IN EXCLUSIVE MODE                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE 'TINENTY LOCK TABLE FAILED'                                 
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '1100-LOCK-DELETE-TABLES'                                   
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
               LOCK TABLE TADJADD IN EXCLUSIVE MODE                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE 'TADJADD LOCK TABLE FAILED'                                 
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '1100-LOCK-DELETE-TABLES'                                   
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
               LOCK TABLE TADJDTL IN EXCLUSIVE MODE                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE 'TADJDTL LOCK TABLE FAILED'                                 
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '1100-LOCK-DELETE-TABLES'                                   
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
               LOCK TABLE TINVDTL IN EXCLUSIVE MODE                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE 'TINVDTL LOCK TABLE FAILED'                                 
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '1100-LOCK-DELETE-TABLES'                                   
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
               LOCK TABLE TSUMINV IN EXCLUSIVE MODE                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE 'TSUMINV LOCK TABLE FAILED'                                 
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '1100-LOCK-DELETE-TABLES'                                   
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
               LOCK TABLE TINVLOC IN EXCLUSIVE MODE                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - 7 DELETE TABLES LOCKED IN EXCLUSIVE MODE'            
             WHEN OTHER                                                         
               MOVE 'TINVLOC LOCK TABLE FAILED'                                 
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '1100-LOCK-DELETE-TABLES'                                   
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
      *                                                                         
      ****************************************************************          
W27808* LOCK THE CUTOFF DATABASE, THEN SELECTIVELY PURGE LOCATIONS              
W27808****************************************************************          
W27808*                                                                         
W27808 2000-PURGE-FROM-INT00000.                                                
W27808*                                                                         
W27808     PERFORM 2100-LOCK-TABLE-INT00000.                                    
W27808     PERFORM 2200-DELETE-SLCTD-STORES                                     
W27808         VARYING TB-DELETE-STORE-SUB FROM 1 BY 1                          
W27808           UNTIL TB-DELETE-STORE-SUB GREATER THAN                         
W27808                 TB-DELETE-STORE-CNT.                                     
W27808*                                                                         
L27808     DISPLAY ' ' PV-PROGRAM-NAME                                          
W27808             ' - INT00000 RECS DELETED '                                  
W27808             PT-NBR-INT00000-RCDS-DLTD.                                   
W27808****************************************************************          
W27808* LOCK THE CUTOFF DATABASE IN EXCLUSIVE MODE (TABLESPACE LOCK)            
W27808****************************************************************          
W27808 2100-LOCK-TABLE-INT00000.                                                
           EXEC SQL                                                             
               LOCK TABLE INT_INV_CTOFF IN EXCLUSIVE MODE                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - INT_INV_CTOFF IS LOCKED IN EXCLUSIVE MODE'           
             WHEN OTHER                                                         
               MOVE 'INT00000 LOCK TABLE FAILED'                                
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '2100-LOCK-TABLE-INT00000'                                  
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
      *                                                                         
      ****************************************************************          
      *  DELETE INT_INV_CTOFF DATA                                              
      ****************************************************************          
       2200-DELETE-SLCTD-STORES.                                                
                                                                                
           MOVE '2200-DELETE-SLCTD-STORES  '                                    
               TO PV-PARAGRAPH-NAME.                                            
           MOVE TB-DELETE-STORE-NBR (TB-DELETE-STORE-SUB)                       
               TO INVPAR-LOC-NBR.                                               
L27808     DISPLAY ' ' PV-PROGRAM-NAME                                          
W27808             ' - BEGIN PURGING CUTOFF ROWS FOR LOCATION '                 
L27808             TB-DELETE-STORE-NBR (TB-DELETE-STORE-SUB)                    
L27808             ' ... ' FUNCTION CURRENT-DATE.                               
                                                                                
0423RM     MOVE 'N' TO PS-INT-INV-CTOFF-EOF-SW.                                 
0423RM     MOVE ZEROES TO WS-NBR-INT00000-RCDS-DLTD.                            
0423RM     PERFORM 7240-DELETE-INT00000                                         
0423RM         UNTIL PS-INT-INV-CTOFF-EOF.                                      
0423RM     DISPLAY ' ' PV-PROGRAM-NAME                                          
0423RM             ' - ' WS-NBR-INT00000-RCDS-DLTD ' ROWS DELETED'.             
                                                                                
W26600*****************************************************************         
W26600 7230-OPEN-TINVPAR-CSR.                                                   
W26600                                                                          
W26600     EXEC SQL                                                             
W26600         OPEN TINVPAR_CSR                                                 
W26600     END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               MOVE '7230-OPEN-TINVPAR-CSR'                                     
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'OPEN TINVPAR_CSR' TO PV-DB2-OPER-ATTEMPTED                 
               SET PS-TINVPAR-CSR-EOF  TO TRUE                                  
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
W26600                                                                          
W26600*****************************************************************         
W26600 7231-FETCH-TINVPAR-CSR.                                                  
                                                                                
           MOVE '7231-FETCH-TINVPAR-CSR'                                        
                                       TO PV-PARAGRAPH-NAME.                    
                                                                                
           EXEC SQL                                                             
               FETCH TINVPAR_CSR                                                
W26600         INTO :INVPAR-LOC-NBR,                                            
                    :INVPAR-PLND-INV-DTE                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   ADD 1              TO TB-DELETE-STORE-SUB                    
                   MOVE INVPAR-LOC-NBR                                          
                       TO TB-DELETE-STORE-NBR (TB-DELETE-STORE-SUB)             
L27808             MOVE INVPAR-PLND-INV-DTE                                     
L27808                 TO TB-PLND-INV-DTE (TB-DELETE-STORE-SUB)                 
      *            PERFORM 7980-BUILD-STORE-FILE                                
L27808             DISPLAY ' ' PV-PROGRAM-NAME                                  
                           ' - TINVPAR STORE '                                  
                           TB-DELETE-STORE-NBR(TB-DELETE-STORE-SUB)             
                           ' SELECTED FOR DELETION'                             
               WHEN SQLCODE = +100                                              
L27808             SET PS-TINVPAR-CSR-EOF TO TRUE                               
               WHEN OTHER                                                       
                   MOVE 'FETCH TINVPAR CURSOR'                                  
                       TO PV-DB2-OPER-ATTEMPTED                                 
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
W26600*****************************************************************         
W26600 7232-CLOSE-TINVPAR-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               CLOSE TINVPAR_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE 'CLOSE TINVPAR_CSR'                                         
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '7232-CLOSE-TINVPAR-CSR'                                    
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
       7240-DELETE-INT00000.                                                    
      *                                                                         
           EXEC SQL                                                             
               DELETE FROM INT_INV_CTOFF                                        
               WHERE LOC_NBR = :INVPAR-LOC-NBR                                  
0423RM         FETCH FIRST 50000 ROWS ONLY                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               ADD SQLERRD (3)         TO PT-NBR-INT00000-RCDS-DLTD             
0423RM                                    WS-NBR-INT00000-RCDS-DLTD             
L27808*        DISPLAY ' ' PV-PROGRAM-NAME                                      
      *                ' - ' SQLERRD(3) ' ROWS DELETED'                         
             WHEN +100                                                          
      *        CONTINUE                                                         
0423RM         MOVE 'Y' TO PS-INT-INV-CTOFF-EOF-SW                              
             WHEN OTHER                                                         
               MOVE 'INT00000 DELETE FAILED'                                    
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '7240-DELETE-INT00000'                                      
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
249895     PERFORM 7990-COMMIT-WORK.                                            
      *                                                                         
                                                                                
       7340-DELETE-TSTKRMW.                                                     
                                                                                
           EXEC SQL                                                             
               DELETE FROM TSTKRMW                                              
               WHERE LOC_NBR = :INVPAR-LOC-NBR                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               ADD SQLERRD (3)         TO PT-NBR-TSTKRMW-RCDS-DLTD              
                                          PT-TTL-STORE-DELETES                  
             WHEN +100                                                          
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE 'TSTKRMW DELETE FAILED'                                     
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '7340-DELETE-TSTKRMW'                                       
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
W26600*****************************************************************         
       7440-DELETE-TINENTY.                                                     
                                                                                
           EXEC SQL                                                             
               DELETE FROM TINENTY                                              
               WHERE STR_NBR = :INVPAR-LOC-NBR                                  
               FETCH FIRST 50000 ROWS ONLY                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               ADD SQLERRD (3)         TO PT-NBR-TINENTY-RCDS-DLTD              
                                          PT-TTL-STORE-DELETES                  
             WHEN +100                                                          
      *        CONTINUE                                                         
               MOVE 'Y' TO PS-TINENTY-EOF-SW                                    
             WHEN OTHER                                                         
               MOVE 'TINENTY DELETE FAILED'                                     
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '7440-DELETE-TINENTY'                                       
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           PERFORM 7990-COMMIT-WORK.                                            
                                                                                
W26600*****************************************************************         
       7540-DELETE-TADJADD.                                                     
                                                                                
           EXEC SQL                                                             
               DELETE FROM TADJADD                                              
               WHERE LOC_NBR = :INVPAR-LOC-NBR                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               ADD SQLERRD (3)         TO PT-NBR-TADJADD-RCDS-DLTD              
                                          PT-TTL-STORE-DELETES                  
             WHEN +100                                                          
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE 'TADJADD DELETE FAILED'                                     
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '7540-DELETE-TADJADD'                                       
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
W26600*****************************************************************         
       7640-DELETE-TADJDTL.                                                     
                                                                                
           EXEC SQL                                                             
               DELETE FROM TADJDTL                                              
               WHERE LOC_NBR = :INVPAR-LOC-NBR                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               ADD SQLERRD (3)         TO PT-NBR-TADJDTL-RCDS-DLTD              
                                          PT-TTL-STORE-DELETES                  
             WHEN +100                                                          
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE 'TADJDTL DELETE FAILED'                                     
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '7640-DELETE-TADJDTL'                                       
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
W26600*****************************************************************         
       7740-DELETE-TINVDTL.                                                     
                                                                                
           EXEC SQL                                                             
               DELETE FROM TINVDTL                                              
               WHERE LOC_NBR = :INVPAR-LOC-NBR                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               ADD SQLERRD (3)         TO PT-NBR-TINVDTL-RCDS-DLTD              
                                          PT-TTL-STORE-DELETES                  
             WHEN +100                                                          
               CONTINUE                                                         
             WHEN OTHER                                                         
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - ' INVPAR-LOC-NBR                                     
               MOVE 'TINVDTL DELETE FAILED'                                     
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '7740-DELETE-TINVDTL'                                       
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
W26600*****************************************************************         
       7840-DELETE-TSUMINV.                                                     
                                                                                
           EXEC SQL                                                             
               DELETE FROM TSUMINV                                              
               WHERE LOC_NBR = :INVPAR-LOC-NBR                                  
0423RM         FETCH FIRST 50000 ROWS ONLY                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               ADD SQLERRD (3)         TO PT-NBR-TSUMINV-RCDS-DLTD              
                                          PT-TTL-STORE-DELETES                  
             WHEN +100                                                          
0423RM*        CONTINUE                                                         
0423RM         MOVE 'Y' TO PS-TSUMINV-EOF-SW                                    
             WHEN OTHER                                                         
               MOVE 'TSUMINV DELETE FAILED'                                     
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '7840-DELETE-TSUMINV'                                       
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           PERFORM 7990-COMMIT-WORK.                                            
                                                                                
W26600*****************************************************************         
       7940-DELETE-TINVLOC.                                                     
                                                                                
           EXEC SQL                                                             
               DELETE FROM TINVLOC                                              
               WHERE LOC_NBR = :INVPAR-LOC-NBR                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN 0                                                             
               ADD SQLERRD (3)         TO PT-NBR-TINVLOC-RCDS-DLTD              
                                          PT-TTL-STORE-DELETES                  
             WHEN +100                                                          
               CONTINUE                                                         
             WHEN OTHER                                                         
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - ' INVPAR-LOC-NBR                                     
               MOVE 'TINVLOC DELETE FAILED'                                     
                                       TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '7940-DELETE-TINVLOC'                                       
                                       TO PV-PARAGRAPH-NAME                     
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
W26600*****************************************************************         
       7990-COMMIT-WORK.                                                        
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               ADD +1                  TO PT-COMMIT-COUNT                       
             WHEN OTHER                                                         
               MOVE 'COMMIT FAILED'    TO PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
W26600*****************************************************************         
       8100-READ-CONTROL-CARD-STRS.                                             
                                                                                
L27808     READ MANUAL-REQ-STORES-CC INTO CC-MANUAL-REQ-STORES                  
               AT END                                                           
                   SET PS-REQ-STORES-EOF TO TRUE.                               
                                                                                
           IF NOT PS-REQ-STORES-EOF                                             
             IF CC-DELETE-STORE NUMERIC                                         
               ADD 1                   TO TB-DELETE-STORE-SUB                   
               MOVE CC-DELETE-STORE                                             
                                       TO TB-DELETE-STORE-NBR                   
                                          (TB-DELETE-STORE-SUB)                 
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - CONTROL CARD STORE '                                 
                       TB-DELETE-STORE-NBR (TB-DELETE-STORE-SUB)                
                       ' SELECTED FOR DELETION'                                 
             ELSE                                                               
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
L27808         DISPLAY ' ' PV-PROGRAM-NAME                                      
                       ' - CONTROL CARD STORE NUMBER NOT NUMERIC'               
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               MOVE '*Z100-LOAD-CONTROL-CARD-STRS' TO PV-PARAGRAPH-NAME         
L27808         SET ABEND-CONTROL-CARD TO TRUE                                   
               PERFORM 9999-ABEND                                               
             END-IF                                                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9900-SQL-ABEND.                                                          
                                                                                
L27808     SET ABEND-SQL-ISSUE TO TRUE.                                         
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
L27808     PERFORM 9999-ABEND.                                                  
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = INKUP060'.                              
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
