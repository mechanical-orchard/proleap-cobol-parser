      *-----------------------*                                         05/02/95
       IDENTIFICATION DIVISION.                                         TFKCS422
      *-----------------------*                                            LV002
       PROGRAM-ID.    TFKCS422.                                         TFKCS422
       AUTHOR.        JOHN OBERHAUSER - COMPUWARE.                      TFKCS422
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          TFKCS422
       DATE-WRITTEN.  SEPTEMBER 96.                                     TFKCS422
       DATE-COMPILED.                                                   TFKCS422
                                                                        TFKCS422
      *---------------------------------------------------------------* TFKCS422
      *    TFKCS422 - SPECIFY PRINTER DESTINATION.                    * TFKCS422
      *                                                               * TFKCS422
      *    THIS PROGRAM ASKS FOR THE PRINTER DESTINATION ID FOR       * TFKCS422
      *    A REPORT TO PRINT TO THROUGH DRS.  WHEN THE ID IS ENTERED  * TFKCS422
      *    OR ACCEPTED, PRESS THE 'PRINT' KEY AND THIS WILL DO A      * TFKCS422
      *    'LOCAL-RETURN' AND RETURN TO THE PREVIOUS PROGRAM WITH     * TFKCS422
      *    THE PRINTER ID.                                            * TFKCS422
      *---------------------------------------------------------------* TFKCS422
      *  1/20/2003 - RAY GRAF                                                   
      *   PROCESS NONINTELLIGENT SKU.                                           
      ******************************************************************        
080204**DATE:08/02/2004 CHANGED BY: RAY GRAF                                    
080204**     CHANGE PRINTER DESTINATION TO NEW 8 CHARACTER FORMAT               
      *****************************************************************         
100404** 10/04/2004 CHANGED BY: RAY GRAF                                        
100404**     EXPAND TRANSFER NUMBER.                                            
      *****************************************************************         
120704** 12/07/2004 CHANGED BY: DAVID WELLER                                    
120704**     USE TFKCS418 INSTEAD OF TFKCS423                                   
      *****************************************************************         
120704** 01/15/2005 CHANGED BY: RAY GRAF                                        
120704**     RECOMPILE                                                          
      *****************************************************************         
CICS  * CHANGED 11/17/2022    BY JIM HUNTER      WR: CHG0278345                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
       EJECT                                                            TFKCS422
       ENVIRONMENT DIVISION.                                            TFKCS422
                                                                        TFKCS422
       DATA DIVISION.                                                   TFKCS422
                                                                        TFKCS422
       WORKING-STORAGE SECTION.                                         TFKCS422
                                                                        TFKCS422
       01  DK-DB2-KEYS.                                                 TFKCS422
           15  DK-BUS-GROUP                   PIC  X(25)  VALUE SPACE.  TFKCS422
                                                                        TFKCS422
       01  PC-PROGRAM-CONSTANTS.                                        TFKCS422
           05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         TFKCS422
                                                          'TF422A  '.   TFKCS422
           05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         TFKCS422
                                                          'TFKM422 '.   TFKCS422
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         TFKCS422
                                                          'TFKCS422'.   TFKCS422
           05  PC-OPERCO                      PIC  X(02)  VALUE '03'.   00470010
           05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.      TFKCS422
           05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.      TFKCS422
           05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.    TFKCS422
           05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +15     TFKCS422
                                                          COMP SYNC.    TFKCS422
           05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2      TFKCS422
                                                          COMP SYNC.    TFKCS422
           05  PC-MAX-ITEMS                   PIC S9(09)  VALUE         TFKCS422
                                                          +1000000      TFKCS422
                                                          COMP-3.       TFKCS422
           05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5      TFKCS422
                                                          COMP SYNC.    TFKCS422
           05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3       TFKCS422
                                                          COMP-3.       TFKCS422
           05  PC-PERCENT                     PIC  X(01)  VALUE '%'.    TFKCS422
           05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.  TFKCS422
           05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.  TFKCS422
           05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.  TFKCS422
           05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.  TFKCS422
           05  PC-LEAVE-SYSTEM-LENGTH         PIC S9(04)   VALUE +160   00560012
                                                           COMP SYNC.   00570012
      *                                                                 TFKCS422
           05  PC-TSYMSG-NUMBERS.                                       TFKCS422
               10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.  TFKCS422
               10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.  TFKCS422
               10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.  TFKCS422
               10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.  TFKCS422
               10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.  TFKCS422
               10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.  TFKCS422
               10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.  TFKCS422
               10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.  TFKCS422
               10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.  TFKCS422
               10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.  TFKCS422
               10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.  TFKCS422
               10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.  TFKCS422
               10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.  TFKCS422
               10  PC-TSYMSG-00746            PIC  9(05)  VALUE 00746.  TFKCS422
               10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.  TFKCS422
      *                                                                 TFKCS422
       01  PS-PROGRAM-SWITCHES.                                         TFKCS422
           05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.    TFKCS422
               88  PS-NO-ERROR                            VALUE 'Y'.    TFKCS422
               88  PS-ERROR                               VALUE 'N'.    TFKCS422
           05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.    TFKCS422
               88  PS-COMMAREA-VALID                      VALUE 'Y'.    TFKCS422
               88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.    TFKCS422
           05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.    TFKCS422
               88  PS-DONE                                VALUE 'Y'.    TFKCS422
               88  PS-NOT-DONE                            VALUE 'N'.    TFKCS422
           05  PS-ABEND-IND                   PIC X(01)    VALUE 'N'.   00930012
               88  PS-NO-ABEND-IN-PROGRESS                 VALUE 'N'.   00940012
               88  PS-ABEND-IN-PROGRESS                    VALUE 'Y'.   00950012
           05  PS-INTERNAL-READER-ENQ-DEQ-IND                           00890012
                                              PIC X(01)    VALUE 'D'.   00900012
               88  PS-INTERNAL-READER-ENQUEUED             VALUE 'E'.   00910012
               88  PS-INTERNAL-READER-DEQUEUED             VALUE 'D'.   00920012
      *                                                                 TFKCS422
       01  PV-PROGRAM-VARIABLES.                                        TFKCS422
           05  PV-XFER-NBR.                                                     
               10  FILLER                     PIC X(1)    VALUE '0'.            
               10  PV-ASC-XFER-NBR            PIC X(10).                        
           05  PV-LEAVE-SYSTEM-MESSAGE        PIC X(160)   VALUE SPACES.01040012
           05  PV-CICS-RESP                   PIC S9(04)  VALUE +0      TFKCS422
                                                          COMP SYNC.    TFKCS422
           05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0      TFKCS422
                                                          COMP SYNC.    TFKCS422
           05  PV-PRINTER-DEST.                                         04630013
080204         10  FILLER                  PIC X(01).                           
080204         10  PV-PRINTER-STORE-LOC    PIC X(05).                           
080204         10  FILLER                  PIC X(02).                           
                                                                                
      *---------------------------------------------------------------* TFKCS422
      *  RF COPYBOOK TO FORMAT PRINTER DESTINATION                              
      *---------------------------------------------------------------* TFKCS422
      *                                                                         
080204     COPY RFWS009.                                                        
                                                                                
      *---------------------------------------------------------------* TFKCS422
      *   TFWS005 - IS THE OUTPUT LAYOUT FOR THE COMMAREA TO BE       * TFKCS422
      *             PASSED TO THE TRANSFER DOCUMENT PRINT PROGRAM.    * TFKCS422
      *---------------------------------------------------------------* TFKCS422
                                                                        TFKCS422
           COPY TFWS005.                                                TFKCS422
                                                                        TFKCS422
      *---------------------------------------------------------------* TFKCS422
      *  RECORD LAYOUT FOR THE MESSAGE SELECTION TEMP STORAGE QUEUE.  * TFKCS422
      *---------------------------------------------------------------* TFKCS422
                                                                        TFKCS422
      ***INCLUDE RNWS001                                                TFKCS422
      **+INCLUDE DPWS057                                                      DP
                                                                        TFKCS422
           EJECT                                                        TFKCS422
      *---------------------------------------------------------------* TFKCS422
      *    MAP LAYOUT                                                 * TFKCS422
      *---------------------------------------------------------------* TFKCS422
                                                                        TFKCS422
           COPY TFKM422.                                                TFKCS422
      *                                                                 TFKCS422
      *---------------------------------------------------------------* TFKCS422
      *    ATTRIBUTE SETTINGS COPYBOOK.                               * TFKCS422
      *---------------------------------------------------------------* TFKCS422
                                                                        TFKCS422
           COPY DPWS015.                                                TFKCS422
                                                                        TFKCS422
      *---------------------------------------------------------------* TFKCS422
      *    FUNCTION KEYS COPYBOOK.                                    * TFKCS422
      *---------------------------------------------------------------* TFKCS422
                                                                        TFKCS422
           COPY DPWS016.                                                TFKCS422
                                                                        TFKCS422
      *----------------------------------------------------------------*04430030
      **  SECURITY SUBROUTINE PARAMETERS                                04440030
      *----------------------------------------------------------------*04450030
           COPY DPWS008.                                                04460030
                                                                        04460131
      *---------------------------------------------------------------* TFKCS422
      *    NUMERIC EDIT LAYOUT.                                       * TFKCS422
      *---------------------------------------------------------------* TFKCS422
                                                                        TFKCS422
           COPY DPWS010I.                                               TFKCS422
       EJECT                                                            TFKCS422
      *---------------------------------------------------------------* TFKCS422
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          * TFKCS422
      *---------------------------------------------------------------* TFKCS422
                                                                        TFKCS422
           COPY DPWS030.                                                TFKCS422
CICS       COPY DPWS930.                                                TFKCS422
       EJECT                                                            TFKCS422
      *---------------------------------------------------------------* TFKCS422
      *    STANDARD COMMAREA.                                         * TFKCS422
      *---------------------------------------------------------------* TFKCS422
                                                                        TFKCS422
           COPY DPWS020.                                                TFKCS422
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                TFKCS422
                                                                        TFKCS422
      *---------------------------------------------------------------* TFKCS422
      * INTER-APPLICATION COMMAREA.                                   * TFKCS422
      **  DPWS054 - IS THE INPUT LAYOUT WHEN COMING FROM THE CRIT.    * TFKCS422
      **            SPEC SCREEN.                                      * TFKCS422
      **  DPWS056 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     * TFKCS422
      **            THIS LIST PROGRAMMING FOR A LOCAL-GO FUNCTION.    * TFKCS422
      *   TFWS003 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     * TFKCS422
      *             THIS LIST PROGRAMMING FOR A LOCAL-GO FUNCTION.    * TFKCS422
      *---------------------------------------------------------------* TFKCS422
                                                                        TFKCS422
               10  INTER-APPL-COMM-AREA       PIC X(200).               TFKCS422
           COPY TFWS001.                                                TFKCS422
      *---------------------------------------------------------------* TFKCS422
      *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   * TFKCS422
      *---------------------------------------------------------------* TFKCS422
               10  ASC-SPECIFIC-COMMAREA.                               TFKCS422
080204             15  ASC-PRINTER-DEST                    PIC  X(08).  TFKCS422
100404*            15  ASC-XFER-NBR                        PIC  9(09).  TFKCS422
100404             15  ASC-XFER-NBR                        PIC  9(10).  TFKCS422
                   15  ASC-DKEY-ID                         PIC  9(09).  TFKCS422
           EJECT                                                        TFKCS422
                                                                        01650012
       01  SE-SYSTEM-ERROR-MESSAGE.                                     01550012
           05  FILLER                  PIC X(43)    VALUE               01560012
               'A SYSTEM ERROR HAS OCCURRED - CONTACT DP - '.           01570012
           05  FILLER                  PIC X(33)    VALUE               01580012
               'PROGRAM TFKCS422       EIBRESP = '.                     01590012
           05  SE-CICS-RESPONSE        PIC 9(4)     VALUE ZERO.         01600012
           05  FILLER                  PIC X(5)     VALUE SPACES.       01610012
           05  FILLER                  PIC X(14)    VALUE               01620012
               'ATTEMPTING TO '.                                        01630012
           05  SE-ATTEMPTED-ACTION     PIC X(61)    VALUE SPACES.       01640012
      ****************************************************************          
      * WORKING STOREAGE FOR THE JCL.                                           
      ****************************************************************          
       01  TD-TRANSIENT-DATA-AREA.                                      01660012
           05  TD-QUEUE-NAME           PIC X(4)     VALUE 'IRTD'.       01670012
           05  TD-QUEUE-NAME-LENGTH    PIC S9(4)    VALUE +4            01680012
                                       COMP SYNC.                       01690012
           05  TD-JCL-LINE             PIC X(80)    VALUE SPACES.       01700012
           05  TD-JCL-LINE-LENGTH      PIC S9(4)    VALUE +80           01710012
                                       COMP SYNC.                       01720012
      *---------------------------------------------------------------* TFKCS422
      *    ABEND PROCESSING COPYBOOK.                                 * TFKCS422
      *---------------------------------------------------------------* TFKCS422
                                                                        TFKCS422
           COPY DPWS013.                                                TFKCS422
                                                                        TFKCS422
       EJECT                                                            TFKCS422
       LINKAGE SECTION.                                                 TFKCS422
                                                                        TFKCS422
       01  DFHCOMMAREA.                                                 TFKCS422
           05  FILLER                         OCCURS  1 TO 4072 TIMES   TFKCS422
                                              DEPENDING ON EIBCALEN.    TFKCS422
               10  FILLER                     PIC X.                    TFKCS422
                                                                        TFKCS422
       EJECT                                                            TFKCS422
       PROCEDURE DIVISION.                                              TFKCS422
      ******************************************************************TFKCS422
      **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **TFKCS422
      **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **TFKCS422
      **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **TFKCS422
      ******************************************************************TFKCS422
       0000-MAIN-MODULE.                                                TFKCS422
           INITIALIZE DP030-CICS-API-FIELDS.                            TFKCS422
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.       TFKCS422
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          TFKCS422
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         TFKCS422
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       TFKCS422
           MOVE LENGTH OF TF422AI        TO DP030-MAP-LENGTH (1).       TFKCS422
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     TFKCS422
           PERFORM 0001-CALL-CICS-ARCH-API.                             TFKCS422
      *                                                                 TFKCS422
           PERFORM 1000-CONTROL-PROCESSING.                             TFKCS422
      *                                                                 TFKCS422
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     TFKCS422
           PERFORM 0001-CALL-CICS-ARCH-API.                             TFKCS422
                                                                        TFKCS422
      ******************************************************************TFKCS422
      **  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **TFKCS422
      **  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **TFKCS422
      ******************************************************************TFKCS422
       0001-CALL-CICS-ARCH-API.                                         TFKCS422
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          TF422AI.                              
      *                                                                 TFKCS422
           IF  DP030-RC-CALL-SUCCESSFUL                                 TFKCS422
               CONTINUE                                                 TFKCS422
           ELSE                                                         TFKCS422
               SET DP013-NO-ROLLBACK                                    TFKCS422
                   DP013-XCTL-DISPLAY-RESTART                           TFKCS422
                   DP013-CICS-ABEND      TO TRUE                        TFKCS422
               MOVE '0001-CALL-CICS-ARCH-API'                           TFKCS422
                                         TO DP013-PARAGRAPH             TFKCS422
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      TFKCS422
               PERFORM DP013-0000-PROCESS-ABEND                         TFKCS422
           END-IF.                                                      TFKCS422
      *----------------------------------------------------------------*TFKCS422
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *TFKCS422
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *TFKCS422
      *----------------------------------------------------------------*TFKCS422
                                                                        TFKCS422
       1000-CONTROL-PROCESSING.                                         TFKCS422
                                                                        TFKCS422
           EVALUATE TRUE                                                TFKCS422
               WHEN DP020-NEXT-ACT-INITIAL                              TFKCS422
                   INITIALIZE ASC-SPECIFIC-COMMAREA                     TFKCS422
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                 TFKCS422
      ***-----$TEMPLATE NOTE$-------------------------------------------TFKCS422
      *** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,TFKCS422
      *** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO  TFKCS422
      *** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.TFKCS422
      ***---------------------------------------------------------------TFKCS422
      *            IF  PS-COMMAREA-NOT-VALID                            TFKCS422
      ***              MOVE PC-TSYMSG-99901                             TFKCS422
      ***                                TO DP020-MSG-NUMBER            TFKCS422
      ***              SET DP020-MSG-FATAL                              TFKCS422
      ***                                TO TRUE                        TFKCS422
      *                SET DP020-NEXT-ACT-APPL-ERROR                    TFKCS422
      *                                  TO TRUE                        TFKCS422
      *            ELSE                                                 TFKCS422
                   PERFORM 4000-BUILD-INITIAL-PANEL                         TFKC
      *            END-IF                                               TFKCS422
                                                                        TFKCS422
               WHEN DP020-NEXT-ACT-READ-MAP                             TFKCS422
                   PERFORM 2000-PROCESS-PANEL                           TFKCS422
                                                                        TFKCS422
               WHEN DP020-NEXT-ACT-RETURN                               TFKCS422
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 TFKCS422
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**2
                                                                        TFKCS422
               WHEN OTHER                                               TFKCS422
                   SET DP013-LOGIC-ABEND TO TRUE                        TFKCS422
                   SET DP013-NO-ROLLBACK TO TRUE                        TFKCS422
                   MOVE '1000-CONTROL-PROCESSING'                       TFKCS422
                                         TO DP013-PARAGRAPH             TFKCS422
                   MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'    TFKCS422
                                         TO DP013-MESSAGE-TEXT(1)       TFKCS422
                   PERFORM DP013-0000-PROCESS-ABEND                     TFKCS422
           END-EVALUATE.                                                TFKCS422
                                                                        TFKCS422
       EJECT                                                            TFKCS422
      *----------------------------------------------------------------*TFKCS422
      * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *TFKCS422
      * IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *TFKCS422
      *----------------------------------------------------------------*TFKCS422
       1100-PROCESS-INTER-APPL-COMM.                                    TFKCS422
                                                                                
      *  THE FOLLOWING STATEMENTS CONVERT THE OUTPUT DEFAULT PRINTER            
      *  DESTINATIONS FOR THE DC'S AND RTV'S - IF CHANGES ARE MADE,             
      *  IT MAY ALSO BE NECESSARY TO MAKE SIMILAR CHANGES TO THE                
      *  TRANSFER DICSREPANCY REPORT - TFKCS455                                 
080204     MOVE TF001-PRINTER-DEST  TO PV-PRINTER-DEST.                 08510013
080204     IF PV-PRINTER-STORE-LOC = '00085' OR '00801'                         
080204         MOVE RFWS009-PRINTER-DEST-801 TO TF001-PRINTER-DEST              
           END-IF.                                                              
080204     IF PV-PRINTER-STORE-LOC = '00810' OR '00811'                         
080204         MOVE RFWS009-PRINTER-DEST-811 TO TF001-PRINTER-DEST      10310002
           END-IF.                                                              
080204     IF PV-PRINTER-STORE-LOC = '00803'                                    
080204         MOVE RFWS009-PRINTER-DEST-803 TO TF001-PRINTER-DEST      10310002
           END-IF.                                                              
           MOVE TF001-PRINTER-DEST       TO APRTDSTO                            
                                            ASC-PRINTER-DEST.                   
           MOVE TF001-XFER-NBR           TO ASC-XFER-NBR.                       
           MOVE TF001-DKEY-ID            TO ASC-DKEY-ID.                        
      *                                                                 TFKCS422
      *----------------------------------------------------------------*TFKCS422
      * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *TFKCS422
      * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *TFKCS422
      * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *TFKCS422
      * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *TFKCS422
      * FUNCTION KEYS CAN GET THIS FAR.                                *TFKCS422
      *----------------------------------------------------------------*TFKCS422
                                                                        TFKCS422
       2000-PROCESS-PANEL.                                              TFKCS422
           EVALUATE TRUE                                                TFKCS422
               WHEN DP020-SRC-AID = DP016-CLEAR                         TFKCS422
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   TFKCS422
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 TFKCS422
                                                                        TFKCS422
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    TFKCS422
                   PERFORM 4000-BUILD-INITIAL-PANEL                     TFKCS422
                                                                        TFKCS422
               WHEN OTHER                                               TFKCS422
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 TFKCS422
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   TFKCS422
                   IF  PS-NO-ERROR                                      TFKCS422
                       SET DP030-CONTINUE-GO                            TFKCS422
                                       TO TRUE                          TFKCS422
                       PERFORM 2100-CHECK-FUNCTION-KEY                  TFKCS422
                   ELSE                                                 TFKCS422
                       SET DP030-OVERRIDE-GO                            TFKCS422
                                       TO TRUE                          TFKCS422
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN               RNKCS0
                   END-IF                                               TFKCS422
           END-EVALUATE.                                                TFKCS422
       EJECT                                                            TFKCS422
      *----------------------------------------------------------------*TFKCS422
      * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*TFKCS422
      * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *TFKCS422
      * CICS ARCHITECTURE API.                                         *TFKCS422
      *----------------------------------------------------------------*TFKCS422
       2100-CHECK-FUNCTION-KEY.                                         TFKCS422
                                                                                
           EVALUATE TRUE                                                TFKCS422
                                                                        TFKCS422
               WHEN DP020-SRC-AID = DP016-ENTER                         TFKCS422
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 S422    
                                                                        TFKCS422
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)              07630012
                 SET TF001-PRINT-YES TO TRUE                            0028    
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)              07630012
                 INITIALIZE TF005-COMMAREA                              DPKCS036
                 MOVE ASC-PRINTER-DEST  TO TF005-PRINTER-DEST                   
120704           MOVE ASC-XFER-NBR      TO PV-ASC-XFER-NBR                      
                 MOVE PV-XFER-NBR       TO TF005-XFER-DOC-NBR                   
                 MOVE DP020-USERID      TO TF005-USER-ID                        
                 EXEC CICS                                                      
120704*             START TRANSID ('P423')                                      
120704              START TRANSID ('P418')                                      
                          FROM    (TF005-COMMAREA)                              
                 END-EXEC                                                       
                 SET TF001-PRINT-DOC TO TRUE                            0028    
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-03 (DP020-SRC-AID)              07630012
                 INITIALIZE TF001-INTER-APPL-COMMAREA                   DPKCS036
                 MOVE ASC-PRINTER-DEST  TO TF001-PRINTER-DEST                   
                 MOVE ASC-DKEY-ID       TO TF001-DKEY-ID                06660022
                 EXEC CICS                                                      
                    START TRANSID ('P442')                                      
                          FROM    (TF001-INTER-APPL-COMMAREA)                   
                 END-EXEC                                                       
                 SET TF001-PRINT-YES TO TRUE                            0028    
                                                                        TFKCS422
               WHEN OTHER                                               TFKCS422
                   SET DP013-NO-ROLLBACK                                TFKCS422
                       DP013-XCTL-DISPLAY-RESTART                       TFKCS422
                       DP013-CICS-ABEND  TO TRUE                        TFKCS422
                   MOVE '2100-CHECK-FUNCTION-KEY'                       TFKCS422
                                         TO DP013-PARAGRAPH             TFKCS422
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      TFKCS422
                                         TO DP013-MESSAGE-TEXT (1)      TFKCS422
                   PERFORM DP013-0000-PROCESS-ABEND                     TFKCS422
           END-EVALUATE.                                                TFKCS422
      *                                                                 TFKCS422
      ******************************************************************TFKCS422
      ** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **TFKCS422
      ** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **TFKCS422
      ******************************************************************TFKCS422
       2200-MOVE-SCREEN-TO-COMMAREA.                                    TFKCS422
                                                                        TFKCS422
           IF  APRTDSTL > ZERO                                          TFKCS422
               MOVE APRTDSTI         TO ASC-PRINTER-DEST                TFKCS422
                                        TF001-PRINTER-DEST              TFKCS422
           ELSE                                                         TFKCS422
               IF  APRTDSTF = DP015-ERASE-EOF                           TFKCS422
                   INITIALIZE ASC-PRINTER-DEST                          TFKCS422
               END-IF                                                   TFKCS422
           END-IF.                                                      TFKCS422
      ******************************************************************TFKCS422
      ** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **TFKCS422
      ** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **TFKCS422
      ******************************************************************TFKCS422
       3000-EDIT-DATA-IN-COMMAREA.                                      TFKCS422
           SET PS-NO-ERROR               TO TRUE.                       TFKCS422
           PERFORM 3200-RESET-ATTRIBUTES.                               TFKCS422
080204     IF ASC-PRINTER-DEST (1:1) NOT = 'U'  AND                             
080204        ASC-PRINTER-DEST (1:1) NOT = 'S'                                  
080204         MOVE DP015-RED             TO APRTDSTC                           
080204         MOVE DP015-REVERSE         TO APRTDSTH                           
080204         IF PS-NO-ERROR                                                   
080204             MOVE PC-TSYMSG-00746   TO DP020-MSG-NUMBER                   
080204             SET DP020-MSG-FATAL    TO TRUE                               
080204             MOVE -1                TO APRTDSTL                           
080204             SET DP030-SET-CURSOR-APPL-1                                  
080204                 PS-ERROR           TO TRUE                               
080204         END-IF                                                           
080204     END-IF.                                                              
      *                                                                 TFKCS422
       EJECT                                                            TFKCS422
      *----------------------------------------------------------------*TFKCS422
      *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *TFKCS422
      *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*TFKCS422
      *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *TFKCS422
      *                                                                *TFKCS422
      *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *TFKCS422
      *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *TFKCS422
      *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *TFKCS422
      *    US IN THE 2ND BLOCK OF THE ARRAY.                           *TFKCS422
      *----------------------------------------------------------------*TFKCS422
                                                                        TFKCS422
      ******************************************************************TFKCS422
      ** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **TFKCS422
      ******************************************************************TFKCS422
                                                                        TFKCS422
       3200-RESET-ATTRIBUTES.                                           TFKCS422
           MOVE DP015-UNP-ALP-NOR-OFF    TO APRTDSTA.                   TFKCS422
           MOVE DP015-GREEN              TO APRTDSTC.                   TFKCS422
           MOVE DP015-UNDERLINE          TO APRTDSTH.                   TFKCS422
      *                                                                 TFKCS422
      *----------------------------------------------------------------*TFKCS422
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *TFKCS422
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *TFKCS422
      *    DISPLAY THE PANEL TO USER.                                  *TFKCS422
      *----------------------------------------------------------------*TFKCS422
       4000-BUILD-INITIAL-PANEL.                                        TFKCS422
                                                                                
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        S422    
                                                                                
      *----------------------------------------------------------------*TFKCS422
      * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *TFKCS422
      *----------------------------------------------------------------*TFKCS422
                                                                        TFKCS422
       4400-MOVE-COMMAREA-TO-SCREEN.                                    TFKCS422
           MOVE ASC-PRINTER-DEST        TO APRTDSTO.                            
           MOVE -1                      TO APRTDSTL.                    04890045
           SET DP030-SET-CURSOR-APPL-1  TO TRUE.                                
                                                                                
      *----------------------------------------------------------------*TFKCS422
      *    ABEND PROCESSOR MODULE                                      *TFKCS422
      *----------------------------------------------------------------*TFKCS422
                                                                        TFKCS422
           COPY DPPD013.                                                TFKCS422
      ******************************************************************19290009
      *  EXIT, DISPLAYING A                                             19300009
      *  REASON FOR THE PROGRAM ABEND.                                 *19310009
      ******************************************************************19320009
                                                                        19330009
       9900-LEAVE-SYSTEM.                                               19340009
           EXEC CICS                                                    19350009
               SEND                                                     19360009
                   FROM(PV-LEAVE-SYSTEM-MESSAGE)                        19370009
                   LENGTH(PC-LEAVE-SYSTEM-LENGTH)                       19380009
                   ERASE                                                19390009
           END-EXEC.                                                    19400009
                                                                        19410009
           EXEC CICS                                                    19420009
               RETURN                                                   19430009
           END-EXEC.                                                    19440009
