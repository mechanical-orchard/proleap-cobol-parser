       IDENTIFICATION DIVISION.                                         00010021
       PROGRAM-ID.    TFKUT001.                                         00020045
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030021
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040021
       DATE-WRITTEN.  07/29/2005.                                       00050045
       DATE-COMPILED.                                                   00060021
      ******************************************************************00070021
      **THIS PROGRAM WILL UPDATE THE TRANFSER BATCH TABLE WITH THE NEW  00080045
      **     BATCH ID AND OTHER INFORMATION.                            00090045
      ******************************************************************00100021
      **DATE:07/29/2005 MADE BY:JULIE TEWES INITIAL PROGRAM WRITE       00110045
      ******************************************************************00120021
       ENVIRONMENT DIVISION.                                            00130021
       CONFIGURATION SECTION.                                           00140021
       SOURCE-COMPUTER.    IBM-3090.                                    00150021
       OBJECT-COMPUTER.    IBM-3090.                                    00160021
       INPUT-OUTPUT SECTION.                                            00170021
       FILE-CONTROL.                                                    00180021
                                                                        00190021
           SELECT INPUT-FILE                                            00200021
                  ASSIGN TO INP01.                                      00210021
           SELECT OUTPUT-FILE                                           00220048
                  ASSIGN TO OUT01.                                      00230045
                                                                        00240021
       DATA DIVISION.                                                   00250021
       FILE SECTION.                                                    00260021
                                                                        00270021
       FD  INPUT-FILE                                                   00280021
           RECORDING MODE IS F                                          00290021
           LABEL RECORDS OMITTED                                        00300021
           BLOCK CONTAINS 0 RECORDS                                     00310021
           DATA RECORD IS INPUT-RECORD.                                 00320021
       01  INPUT-RECORD                        PIC X(080).              00330045
                                                                        00340045
       FD  OUTPUT-FILE                                                  00350045
           RECORDING MODE IS F                                          00360045
           LABEL RECORDS OMITTED                                        00370045
           BLOCK CONTAINS 0 RECORDS                                     00380045
           DATA RECORD IS OUTPUT-RECORD.                                00390045
       01  OUTPUT-RECORD                       PIC X(080).              00400045
                                                                        00410021
       WORKING-STORAGE SECTION.                                         00420021
      ******************************************************************00430024
      **INPUT RECORD LAYOUT                                             00440045
      ******************************************************************00450024
       01 IL-INPUT-LAYOUT.                                              00460045
          05 IL-RUN-DATE.                                               00470045
             10  IL-RUN-DATE-YEAR              PIC 9(04) VALUE ZEROS.   00480051
             10  FILLER                        PIC X(01) VALUE '-'.     00490045
             10  IL-RUN-DATE-MONTH             PIC 9(02) VALUE ZEROS.   00500045
             10  FILLER                        PIC X(01) VALUE '-'.     00510045
             10  IL-RUN-DATE-DAY               PIC 9(02) VALUE ZEROS.   00520045
          05 FILLER                            PIC X(70) VALUE SPACES.  00530045
      ******************************************************************00540045
      **DATE TIME FIELDS                                                00550045
      ******************************************************************00560045
       01 DF-DATE-TIME-FIELDS.                                          00570045
          05 DF-ACCEPT-DATE                    PIC X(06) VALUE SPACES.  00580045
          05 DF-ACCEPT-TIME                    PIC X(08) VALUE SPACES.  00590045
          05 DF-FORMAT-JULIAN-YEAR.                                     00600046
             10  DF-FORMAT-DATE-CC             PIC 9(02) VALUE ZEROS.   00610046
             10  DF-FORMAT-DATE-YEAR           PIC 9(02) VALUE ZEROS.   00620046
      ******************************************************************00630023
      **PROGRAM COUNTERS                                                00640023
      ******************************************************************00650023
       01  PC-PROGRAM-COUNTERS.                                         00660023
           05  PC-INPUT-RECORDS-READ           PIC 9(04) VALUE ZEROS.   00670021
           05  PC-OUTPUT-RECORDS-WRITTEN       PIC 9(04) VALUE ZEROS.   00680021
      ****************************************************************  00690021
      **ABEND INFORMATION                                               00700021
      ****************************************************************  00710021
       01  AC-ABEND-FIELDS.                                             00720021
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00730021
                                                           COMP SYNC.   00740021
               88  AC-TABLE-ERROR                        VALUE +4004.   00750021
               88  AC-DB2-ERROR                          VALUE +4013.   00760021
       01  PV-MISC-ABEND-FIELDS.                                        00770021
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00780021
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   00790021
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   00800021
           05  PV-DB2-OPERATION                PIC X(30) VALUE SPACE.   00810021
      ******************************************************************00820021
      **COPYBOOKS                                                       00830021
      ******************************************************************00840021
      ****OUTPUT RECORD LAYOUT                                          00850045
           COPY TFRD052.                                                00860045
      ****KOHLS DATE ROUTINE                                            00870045
           COPY DPWS500.                                                00880045
      ****ABEND ROUTINE INFORMATION                                     00890021
           COPY SQLCA2.                                                 00900021
      ****ABEND ROUTINE INFORMATION                                     00910021
           COPY DPWS004.                                                00920021
      ******************************************************************00930021
      **TABLE INCLUSIONS                                                00940021
      ******************************************************************00950021
      ***TRANSFERS BATCH CONTROL TABLE                                  00960045
           EXEC SQL                                                     00970021
                INCLUDE TTFBTCH                                         00980045
           END-EXEC.                                                    00990021
      *****DB2 TABLE INCLUSION                                          01000021
           EXEC SQL                                                     01010021
                INCLUDE SQLCA                                           01020021
           END-EXEC.                                                    01030021
                                                                        01040029
       PROCEDURE DIVISION.                                              01050021
      ******************************************************************01060021
       000-MAINLINE-PROCESSING.                                         01070021
                                                                        01080021
           PERFORM 1000-INITIALIZATION.                                 01090021
           PERFORM 2000-PROCESS-RECORDS.                                01100045
           PERFORM 9999-WRAPUP.                                         01110021
           GOBACK.                                                      01120021
                                                                        01130021
       EJECT.                                                           01140021
      ******************************************************************01150021
      **INITIALIZATION ROUTINE                                          01160021
      ******************************************************************01170021
       1000-INITIALIZATION.                                             01180021
                                                                        01190021
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01200021
                                                                        01210021
           OPEN INPUT  INPUT-FILE                                       01220024
                OUTPUT OUTPUT-FILE.                                     01230045
                                                                        01240021
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01250021
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01260021
                                                                        01270021
           PERFORM 9999-READ-INPUT-RECORD.                              01280021
                                                                        01290021
       EJECT.                                                           01300021
      ******************************************************************01310021
      **PROCESS INPUT RECORD TO CHECK ROWS                              01320021
      ******************************************************************01330021
       2000-PROCESS-RECORDS.                                            01340021
                                                                        01350021
           MOVE '2000-PROCESS-RECORDS'  TO PV-PARAGRAPH-NAME.           01360021
                                                                        01370021
           INITIALIZE TFRD052-BATCH-CONTROL-FILE.                       01380045
                                                                        01390032
           MOVE IL-RUN-DATE             TO TFBTCH-BTCH-CRE-DTE.         01400051
           PERFORM 2100-RETRIEVE-JULIAN-DATE.                           01410046
           PERFORM 2200-RETRIEVE-MAX-BATCH-DKEY.                        01420048
           PERFORM 2300-INSERT-NEW-BATCH-DKEY.                          01430048
                                                                        01440021
           MOVE TFBTCH-BTCH-DKEY-ID     TO TFRD052-BATCH-DKEY-ID.       01450048
           MOVE TFBTCH-BTCH-JULN-DAY    TO TFRD052-JULIAN-DAY.          01460048
           MOVE IL-RUN-DATE             TO TFRD052-BTCH-CRE-DTE.        01461054
           PERFORM 9999-WRITE-OUTPUT-RECORD.                            01470048
                                                                        01480048
       EJECT.                                                           01490021
      ******************************************************************01500046
      **USE KOHLS DATE ROUTINE TO RETRIEVE JULIAN DATE INFO             01510046
      ******************************************************************01520046
       2100-RETRIEVE-JULIAN-DATE.                                       01530046
                                                                        01540046
           MOVE '2100-RETRIEVE-JULIAN-DATE' TO PV-PARAGRAPH-NAME.       01550046
                                                                        01560046
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01570046
           SET DPG51-FISCAL-454-CALENDAR   TO TRUE.                     01580051
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                     01590046
           SET DPG51-DO-NOT-INCR-DECR-DATE TO TRUE.                     01600046
           MOVE IL-INPUT-LAYOUT            TO DPG52-LK-DATE-INPUT.      01610046
                                                                        01620046
           CALL DP500-KOHLS-CALENDAR-ROUTINE                            01630046
                USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01640046
                                                                        01650046
           EVALUATE TRUE                                                01660046
              WHEN DPG54-NO-ERROR                                       01670046
                   MOVE DPG55-JULIAN-DATE-FMT-DY                        01680046
                                           TO TFBTCH-BTCH-JULN-DAY      01690048
                   MOVE IL-RUN-DATE-YEAR   TO TFBTCH-BTCH-JULN-YR       01700051
                   MOVE DPG56-FISCAL-YEAR-CCYY                          01710048
                                           TO TFBTCH-KOHLS-FSCL-YR      01720048
                   MOVE DPG56-FISCAL-PERIOD-NBR                         01730048
                                           TO TFBTCH-KOHLS-FSCL-MN      01740048
              WHEN OTHER                                                01750046
                   DISPLAY 'DATE ROUTINE ERROR....'                     01760046
                   DISPLAY 'ERROR MESSAGE: ' DPG54-ERROR-MESSAGE        01770046
                   DISPLAY 'DATE ENTERED : ' DPG52-LK-DATE-INPUT        01780046
                   PERFORM 9999-SQL-ERROR                               01790047
                   PERFORM 9999-DISPLAY-STATISTICS                      01800047
           END-EVALUATE.                                                01810046
                                                                        01820046
       EJECT.                                                           01830046
      ******************************************************************01840048
      **RETRIEVE MAX BATCH ID                                           01850048
      ******************************************************************01860048
       2200-RETRIEVE-MAX-BATCH-DKEY.                                    01870048
                                                                        01880048
           MOVE '2200-RETRIEVE-MAX-BATCH-DKEY' TO PV-PARAGRAPH-NAME.    01890048
                                                                        01900048
           EXEC SQL                                                     01910053
                SELECT MAX(BTCH_DKEY_ID)                                01920053
                INTO  :TFBTCH-BTCH-DKEY-ID                              01930053
                FROM   TTFBTCH                                          01940053
                WITH UR                                                 01950053
           END-EXEC.                                                    01960053
                                                                        01970048
           EVALUATE TRUE                                                01980053
              WHEN SQLCODE = +0                                         01990053
                   COMPUTE TFBTCH-BTCH-DKEY-ID = TFBTCH-BTCH-DKEY-ID + 102000053
              WHEN OTHER                                                02010053
                   DISPLAY 'ERROR ON BATCH KEY LOOKUP...'               02020053
                   PERFORM 9999-DISPLAY-STATISTICS                      02030053
                   PERFORM 9999-SQL-ERROR                               02040053
           END-EVALUATE.                                                02050053
                                                                        02060048
       EJECT.                                                           02070048
      ******************************************************************02080048
      **INSERT NEW BATCH DKEY ID                                        02090048
      ******************************************************************02100048
       2300-INSERT-NEW-BATCH-DKEY.                                      02110048
                                                                        02120048
           MOVE '2300-INSERT-NEW-BATCH-DKEY' TO PV-PARAGRAPH-NAME.      02130048
                                                                        02140048
           EXEC SQL                                                     02150048
                INSERT INTO TTFBTCH                                     02160048
                       ( BTCH_DKEY_ID                                   02170048
                        ,BTCH_CRE_DTE                                   02180048
                        ,BTCH_JULN_DAY                                  02190048
                        ,BTCH_JULN_YR                                   02200048
                        ,BTCH_RTL_TOT_AMT                               02210048
                        ,BTCH_UNT_TOT_QTY                               02220048
                        ,KOHLS_FSCL_MN                                  02230048
                        ,KOHLS_FSCL_YR)                                 02240048
                VALUES ( :TFBTCH-BTCH-DKEY-ID                           02250048
                        ,:TFBTCH-BTCH-CRE-DTE                           02260048
                        ,:TFBTCH-BTCH-JULN-DAY                          02270048
                        ,:TFBTCH-BTCH-JULN-YR                           02280048
                        ,0                                              02290048
                        ,0                                              02300048
                        ,:TFBTCH-KOHLS-FSCL-MN                          02310048
                        ,:TFBTCH-KOHLS-FSCL-YR)                         02320048
           END-EXEC.                                                    02330048
                                                                        02340048
           EVALUATE TRUE                                                02350048
              WHEN SQLCODE = +0                                         02360048
                   CONTINUE                                             02370048
              WHEN OTHER                                                02380048
                   DISPLAY 'ERROR BATCH DKEY INSERT....'                02390048
                   DISPLAY 'BATCH DKEY ID ' TFBTCH-BTCH-DKEY-ID         02400048
                   PERFORM 9999-DISPLAY-STATISTICS                      02410048
                   PERFORM 9999-SQL-ERROR                               02420048
           END-EVALUATE.                                                02430048
                                                                        02440048
                                                                        02450048
       EJECT.                                                           02460048
      ******************************************************************02470023
      **READ INPUT RECORD                                               02480023
      ******************************************************************02490023
       9999-READ-INPUT-RECORD.                                          02500023
                                                                        02510023
           READ INPUT-FILE INTO IL-INPUT-LAYOUT.                        02520045
                                                                        02530021
       EJECT.                                                           02540021
      ****************************************************************  02550021
      **WRITE OUTPUT RECORD                                             02560021
      ****************************************************************  02570021
       9999-WRITE-OUTPUT-RECORD.                                        02580021
                                                                        02590021
           WRITE OUTPUT-RECORD FROM TFRD052-BATCH-CONTROL-FILE.         02600048
                                                                        02610021
       EJECT.                                                           02620021
      ******************************************************************02630021
      **CLOSE INPUT FILES.                                              02640021
      ******************************************************************02650021
       9999-WRAPUP.                                                     02660021
                                                                        02670021
           CLOSE INPUT-FILE                                             02680024
                 OUTPUT-FILE.                                           02690024
                                                                        02700021
           PERFORM 9999-DISPLAY-STATISTICS.                             02710021
                                                                        02720021
       EJECT.                                                           02730021
      ******************************************************************02740021
      **DISPLAY PROGRAM STATS                                           02750021
      ******************************************************************02760021
       9999-DISPLAY-STATISTICS.                                         02770021
                                                                        02780021
           DISPLAY '*************************************************'. 02790021
           DISPLAY '* PROGRAM...........: TFKUT001'.                    02800045
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             02810021
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             02820021
           DISPLAY '* INPUT DATE........: ' IL-INPUT-LAYOUT.            02830045
           DISPLAY '* BATCH DKEY INSERTED..: ' TFBTCH-BTCH-DKEY-ID.     02840051
           DISPLAY '*************************************************'. 02850021
                                                                        02860021
       EJECT.                                                           02870021
      ******************************************************************02880021
      * ABEND ROUTINE FOR BAD SQL CALLS                                *02890021
      ******************************************************************02900021
       9999-SQL-ERROR.                                                  02910021
                                                                        02920021
           DISPLAY '** ABEND     **'.                                   02930021
           DISPLAY '** PROGRAM   ** = TFKUT001'.                        02940045
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02950021
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               02960021
      ***************************************************************** 02970021
      *    COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED  02980021
      *    TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.   02990021
      ***************************************************************** 03000021
           COPY DPPD004.                                                03010021
           SET AC-DB2-ERROR TO TRUE.                                    03020021
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         03030021
                                                                        03040021
       EJECT.                                                           03050021
