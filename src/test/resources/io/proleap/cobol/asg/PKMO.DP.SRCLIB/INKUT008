       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT008.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            11/20/2001.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  INVENTORY DATA TRANSMISSION GDG BASE CREATION                 *        
      *                                                                *        
      *  THIS PROGRAM WILL GO THROUGH TINVPAR AND CREATE A LIST OF     *        
      *  IDCAMS CONTROL CARDS TO DELETE THEN DEFINE THE NECESSARY      *        
      *  GDG BASE FOR THE CURRENT INVENTORYING STORES.                 *        
      *                                                                *        
      *  THIS PROGRAM WILL ALSO CREATE THE HIGH-LEVEL CATALOGING       *        
      *  CONTROL CARD FOR IDCAMS FOR LISTING OF DSN TO DELETE.         *        
      *                                                                *        
      *  THE OPTIONAL CONTROL CARD WILL OVERRIDE THE HIGH LEVEL        *        
      *  QUALIFIER OF THE GDG TO SOMETHING OTHER THAN 'PKMO'.          *        
      *  CONTROL CARD LAYOUT:                                          *        
      *      START COL   LENGTH    OPT/DESC                            *        
      *              1        1    VALID CHARACTER ARE ' ' AND '*'.    *        
      *                            '*' CARDS WILL BE IGNORED.          *        
      *                            IT WILL PROCESS UNTIL END OF FILE   *        
      *                            AND WILL ONLY RETAIN THE LAST VALID *        
      *                            OVERRIDE REQUEST.                   *        
      *              2        1    VALID OPTION IS 'O' FOR OVERRIDE.   *        
      *              4       17    HIGH LEVEL DSN QUALIFIER.           *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
25248 * 25248    10-06-2003  UPC CAPABILITIES & STORE NUMBER EXPANSION          
25248 *                      -ROD JANSSEN                                       
W28545* W28545   07-24-2006  DEPARTMENT LEVEL INVENTORY. INCORPORATE            
W28545*                      INVENTORY ID.                                      
W28545*                      -J SELWITSCHKA / STRATAGEM                         
W33304* W33304   06-09-2008  HILO - HOLD INVENTORY LEDGER OPEN                  
W33304*                      ---------------------------------                  
W33304*                      THE GOAL FOR THESE CHANGES FOR INVEN-              
W33304*                      TORY DATE HANDLING IS TO ENSURE PROPER             
W33304*                      HANDLING OF ALL ACTIVE CYCLES FOR ALL              
W33304*                      STORES FOR ALL POSSIBLE SCENARIOS.                 
W33304*                      ----------------------------------                 
W33304*                      COMPILE PROG TO PICK UP NEW CHANGES IN             
W33304*                      TINVPAR & TINCNTL.                                 
W33304*                      CHANGE TINVPAR-CURSOR TO FILTER OUT                
W33304*                      DELETE LOCATIONS.                                  
W33304*                      CHANGE B100-HOUSEKEEPING QUERY 'MN'                
W33304*                      DATES TO 'WK' DATES AND MAKE ASSOCIATED            
W33304*                      CHANGES IN LOGIC THAT CHECKS SQLCODE.              
W33304*                                                                         
W34557* W34557   09-13-2011  MURALITHARAN V                                     
W34557*                      THIS PROCESS IS BEING CHANGED/FIXED SO             
W34557*                      THAT IT CAN RUN WEEKLY. THE RUN DATE               
W34557*                      WILL BE PASSED IN FROM THE SCHEDULE VIA            
W34557*                      A CONTROL CARD. THE RUN DATE WILL BE THE           
W34557*                      CURRENT DATE FROM THE SCHEDULE.                    
W34557*                      THE RUN DATE PLUS 7 DAYS WILL BE USED TO           
W34557*                      COMPARE AGAINST THE PLANNED INVENTORY DATE         
W34557*                      IN TINVPAR TO DETERMINE WHICH STORES WILL          
W34557*                      BE TAKING INVENTORY THE NEXT WEEK. THIS            
W34557*                      CRITERIA WILL THEN BE USED TO IDENTIFY             
W34557*                      THE NECESSARY CRITERIA FOR DATA SET                
W34557*                      CLEAN UP AND GDG GENERATION.                       
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SPC-REQUEST-CNTL-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
W34557     SELECT PROCESS-DATE-FILE                                             
W34557         ASSIGN TO INP02.                                                 
W34557                                                                          
           SELECT GDG-CREATE-CNTL-FILE                                          
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT LVL-LISTCAT-CNTL-FILE                                         
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  SPC-REQUEST-CNTL-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SPC-REQUEST-CNTL-REC            PIC X(80).                           
                                                                                
W34557 FD  PROCESS-DATE-FILE                                                    
W34557     LABEL RECORDS ARE STANDARD                                           
W34557     RECORDING MODE F                                                     
W34557     BLOCK CONTAINS 0 RECORDS.                                            
W34557                                                                          
W34557 01  PROCESS-DATE-FILE-REC           PIC X(80).                           
W34557                                                                          
       FD  GDG-CREATE-CNTL-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  GDG-CREATE-CNTL-REC             PIC X(80).                           
                                                                                
       FD  LVL-LISTCAT-CNTL-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  LVL-LISTCAT-CNTL-REC            PIC X(80).                           
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
       01  RP-REPORT-DETAIL.                                                    
25248      05  RP-STORE-NBR                PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(03)       VALUE SPACES.        
           05  RP-PLAN-INV-DATE            PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(03)       VALUE SPACES.        
           05  RP-ACTL-INV-DATE            PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(03)       VALUE SPACES.        
           05  RP-FILE-NAME                PIC X(44)       VALUE SPACES.        
25248      05  FILLER                      PIC X(02)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-INCOMPLETE-DTE           PIC X(10)       VALUE                
                                                           '9999-09-09'.        
           05  PC-Y                        PIC X           VALUE 'Y'.           
           05  PC-N                        PIC X           VALUE 'N'.           
           05  PC-QUOTE                    PIC X           VALUE "'".           
           05  PC-MID-DSN-QUAL             PIC X(09)       VALUE                
               '.IN.KOHLS'.                                                     
           05  PC-STR-DSN-QUAL             PIC X(04)       VALUE                
               '.STR'.                                                          
           05  PC-DEFINE-CMD-FIRST         PIC X(15)       VALUE                
               ' DEF GDG (NAME('.                                               
           05  PC-DEFINE-CMD-LAST          PIC X(20)       VALUE                
               ') SCRATCH LIMIT(10))'.                                          
           05  PC-LISTCAT-CMD-FIRST        PIC X(15)       VALUE                
               ' LISTCAT LEVEL('.                                               
           05  PC-DELETE-CMD-FIRST         PIC X(09)       VALUE                
               ' DELETE ('.                                                     
           05  PC-DELETE-CMD-LAST          PIC X(17)       VALUE                
               ') GDG FORCE PURGE'.                                             
           05  PC-LISTCAT-CMD-LAST         PIC X(17)       VALUE                
               ')                '.                                             
           05  PC-DEFAULT-LISTCAT          PIC X(45)       VALUE                
               ' LISTCAT LEVEL(PKMO.IN.KOHLS) CLUSTER NONVSAM'.                 
           05  PC-INVALID-REQUEST          PIC X(35)       VALUE                
               '  *** INVALID REQUEST TYPE ***'.                                
           05  PC-CANNOT-ALLOCATE          PIC X(44)       VALUE                
               '*** CANNOT ALLOCATE, INVENTORY STARTED ***'.                    
           05  PC-ERR-IN-BUILDING-DSN      PIC X(44)       VALUE                
               '*** ERROR IN BUILDING DSN ***'.                                 
           05  PC-ERR-IN-BUILDING-DELETE   PIC X(44)       VALUE                
               '*** ERROR IN BUILDING DELETE COMMAND ***'.                      
           05  PC-ERR-IN-BUILDING-DEFINE   PIC X(44)       VALUE                
               '*** ERROR IN BUILDING DEFINE COMMAND ***'.                      
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-TINVPAR           PIC X           VALUE 'N'.           
               88  END-OF-TINVPAR                          VALUE 'Y'.           
           05  PS-END-OF-SPC-CNTL-FILE     PIC X           VALUE 'N'.           
               88  END-OF-SPC-CNTL-FILE                    VALUE 'Y'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SPECIAL REQUEST CONTROL REC ***'.                               
                                                                                
       01  RQ-SPC-CNTL-AREA.                                                    
           05  RQ-SPC-CODE                 PIC X(01)       VALUE SPACES.        
               88  RQ-SPC-VALIDS                           VALUES ARE           
                   ' '                                                          
                   '*'.                                                         
               88  RQ-SPC-COMMENT                          VALUE                
                   '*'.                                                         
           05  RQ-SPC-REQUEST-TYPE         PIC X(01)       VALUE SPACES.        
               88  RQ-SPC-OVERRIDE                         VALUE                
                   'O'.                                                         
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RQ-SPC-VALUE                PIC X(17)       VALUE SPACES.        
           05  RQ-SPC-FILLER               PIC X(60)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
W34557*****************************************************************         
W34557*  THE LAYOUT FOR INPUT DATE CONTROL CARD                       *         
W34557*****************************************************************         
W34557 01  PV-CC-CW-INPUT-CONTROL.                                              
W34557     03  PV-CC-CW-INPUT-DTE        PIC  X(10).                            
W34557     03  FILLER                    PIC  X(70)  VALUE SPACES.              
W34557                                                                          
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-DEFINED-STORE-COUNT      PIC S9(9)       VALUE ZEROES.        
           05  PV-IGNORED-STORE-COUNT      PIC S9(9)       VALUE ZEROES.        
           05  PV-NUMERIC-DISPLAY          PIC ZZZ,ZZZ,ZZ9 VALUE ZEROES.        
           05  PV-SPC-HIGH-LVL-QUAL        PIC X(17)       VALUE 'PKMO'.        
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
25248      05  PV-STORE-NUM-9              PIC 9(04)       VALUE ZEROES.        
           05  PV-STORE-NUM-X REDEFINES PV-STORE-NUM-9                          
25248                                      PIC X(04).                           
W34557     05  PV-OPERATION-MSG-1          PIC X(60)  VALUE SPACES.             
W34557     05  PV-OPERATION-MSG-2          PIC X(60)  VALUE SPACES.             
W34557     05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACES.             
W34557     05  PV-PROCESS-DATE             PIC X(10)  VALUE SPACES.             
W34557     05  FILLER                REDEFINES PV-PROCESS-DATE.                 
W34557         10  PV-PROCESS-DATE-CCYY    PIC X(04).                           
W34557         10  PV-PROCESS-DATE-DASH-1  PIC X(01).                           
W34557             88  PV-VALID-PROCESS-DATE-DASH-1  VALUE '-'.                 
W34557         10  PV-PROCESS-DATE-MM      PIC X(02).                           
W34557         10  PV-PROCESS-DATE-DASH-2  PIC X(01).                           
W34557             88  PV-VALID-PROCESS-DATE-DASH-2  VALUE '-'.                 
W34557         10  PV-PROCESS-DATE-DD      PIC X(02).                           
W34557                                                                          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4003                                  VALUE +4003.         
W34557     88  ABEND-4015-DATE-ERROR                       VALUE +4015.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR - PHYSICAL INVENTORY TABLE                         * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
               INCLUDE TINVPAR                                          INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
W28545*---------------------------------------------------------------* INKCS046
W28545*    TINCNTL - PHYSICAL INVENTORY CYCLE CONTROL TABLE           * INKCS046
W28545*---------------------------------------------------------------* INKCS046
W28545     EXEC SQL                                                     INKCS046
W28545         INCLUDE TINCNTL                                          INKCS046
W28545     END-EXEC.                                                    INKCS046
W28545                                                                  INKCS046
W34557*---------------------------------------------------------------* INKCS046
W34557*    TMLCNTL - M/L OPEN FISCAL MTH TABLE                        * INKCS046
W34557*---------------------------------------------------------------* INKCS046
W34557*    EXEC SQL                                                     INKCS046
W34557*        INCLUDE TMLCNTL                                          INKCS046
W34557*    END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TDTATTR - ML DATE ATTRIBUTES                               * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
               INCLUDE TDTATTR                                          INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------*         
      *    TINVPAR - INVENTORY PARAMETER CURSOR FOR ALL LOCATION      *         
      *---------------------------------------------------------------*         
           EXEC SQL   DECLARE TINVPAR-CURSOR CURSOR FOR                         
25248           SELECT   LOC_NBR                                        INKCS046
                       , ACTL_INV_DTE                                           
                       , PLND_INV_DTE                                           
W28545*         FROM   TINVPAR                                          INKCS046
W28545          FROM   TINVPAR A                                        INKCS046
W28545                ,TINCNTL B                                        INKCS046
                WHERE  SCAN_INV_IND     = :PC-Y                                 
W33304             AND SCHD_PHY_CNT_CDE IN ('PI', 'NS')                         
W33304             AND PLND_INV_DTE >=    :DTATTR-FSCL-WK-BEG-DTE               
W34557             AND PLND_INV_DTE <=    :DTATTR-FSCL-WK-END-DTE               
                   AND ACTL_FIN_BK_DTE  = :PC-INCOMPLETE-DTE            INKCS046
W33304             AND A.LOC_INV_STAT_CDE = 'IN'                                
W28545             AND ACTV_IND         = :PC-Y                                 
W28545             AND A.INV_ID         = B.INV_ID                              
25248           ORDER BY LOC_NBR                                        INKCS046
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN RPOCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z100-FETCH-TINVPAR.                                          
           PERFORM B200-PROCESS-TINVPAR                                         
               UNTIL END-OF-TINVPAR.                                            
                                                                                
           PERFORM B400-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
W34557     PERFORM B150-PROCESS-DATE-FILE                                       
W34557                                                                          
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
W33304         SELECT  FSCL_WK_BEG_DTE                                          
W33304                ,FSCL_WK_END_DTE                                          
W33304             INTO  :DTATTR-FSCL-WK-BEG-DTE                                
W33304                  ,:DTATTR-FSCL-WK-END-DTE                                
                   FROM  TDTATTR                                                
W34557*                 ,TMLCNTL                                                
W34557*W33304             WHERE KY_DTE = PSTG_FSCL_WK_DTE                       
W34557            WHERE KY_DTE = (DATE(DAYS(:DTATTR-KY-DTE)+ 7))                
           END-EXEC.                                                            
           IF SQLCODE NOT = +0                                                  
               MOVE 'SELECT TDATTR' TO PV-OPER-ATTEMPTED                        
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           DISPLAY ' PROCESS DATE ' PV-PROCESSING-DATE.                         
           DISPLAY ' FOR STORE(S) INVENTORYING BETWEEN '                        
                   DTATTR-FSCL-WK-BEG-DTE                                       
                   ' AND '                                                      
                   DTATTR-FSCL-WK-END-DTE.                                      
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TINVPAR-CURSOR' TO PV-OPER-ATTEMPTED                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           OPEN OUTPUT GDG-CREATE-CNTL-FILE                                     
                       LVL-LISTCAT-CNTL-FILE                                    
                INPUT  SPC-REQUEST-CNTL-FILE.                                   
                                                                                
           DISPLAY ' REQUEST CONTROL:'.                                         
           DISPLAY '  ----+----1----+----2----+----3----+----4'.                
           PERFORM Z200-READ-REQUEST-FILE.                                      
           PERFORM                                                              
               UNTIL END-OF-SPC-CNTL-FILE                                       
               IF RQ-SPC-VALIDS                                                 
                   IF NOT RQ-SPC-COMMENT                                        
                       IF RQ-SPC-OVERRIDE                                       
                           AND RQ-SPC-VALUE NOT = SPACES                        
                           MOVE RQ-SPC-VALUE TO PV-SPC-HIGH-LVL-QUAL            
                       ELSE                                                     
                           MOVE PC-INVALID-REQUEST TO RQ-SPC-FILLER             
                       END-IF                                                   
                   END-IF                                                       
                   DISPLAY '  ' RQ-SPC-CNTL-AREA                                
               END-IF                                                           
               PERFORM Z200-READ-REQUEST-FILE                                   
           END-PERFORM.                                                         
                                                                                
           MOVE SPACES TO LVL-LISTCAT-CNTL-REC.                                 
           STRING PC-LISTCAT-CMD-FIRST                                          
                  PV-SPC-HIGH-LVL-QUAL                                          
                  PC-MID-DSN-QUAL                                               
                  PC-LISTCAT-CMD-LAST                                           
               DELIMITED BY '  '                                                
               INTO LVL-LISTCAT-CNTL-REC                                        
               ON OVERFLOW                                                      
                   MOVE PC-DEFAULT-LISTCAT TO LVL-LISTCAT-CNTL-REC              
           END-STRING.                                                          
           INSPECT LVL-LISTCAT-CNTL-REC                                         
               REPLACING ALL LOW-VALUES BY SPACES                               
                             HIGH-VALUES BY SPACES.                             
           PERFORM Z400-WRITE-GDG-LIST-CMD.                                     
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR-CURSOR                                              
           END-EXEC.                                                            
           IF SQLCODE = +0                                                      
               DISPLAY ' '                                                      
               DISPLAY ' PROCESSING STORES:'                                    
           ELSE                                                                 
               MOVE 'OPEN TINVPAR-CURSOR' TO PV-OPER-ATTEMPTED                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
W34557****************************************************************          
W34557* READ AND VALIDATE THE DATE FILE                                         
W34557****************************************************************          
W34557 B150-PROCESS-DATE-FILE.                                                  
W34557                                                                          
W34557     OPEN INPUT                         PROCESS-DATE-FILE                 
W34557                                                                          
W34557     READ PROCESS-DATE-FILE         INTO PV-CC-CW-INPUT-CONTROL           
W34557         AT END                                                           
W34557           SET ABEND-4003                                                 
W34557                                         TO TRUE                          
W34557           MOVE 'B150-PROCESS-DATE-FILE' TO PV-PARAGRAPH-NAME             
W34557           MOVE 'READ PROCESS DATE FILE' TO PV-OPERATION-MSG-1            
W34557                                                                          
W34557           MOVE 'MISSING INPUT CONTROL DATE'                              
W34557                                         TO PV-OPERATION-MSG-2            
W34557         PERFORM Z900-ABEND                                               
W34557     END-READ.                                                            
W34557                                                                          
W34557* VALIDATE THE DATE READ FROM PROCESS DATE FILE                           
W34557     MOVE PV-CC-CW-INPUT-DTE          TO PV-PROCESS-DATE                  
W34557                                                                          
W34557     IF  PV-PROCESS-DATE-CCYY IS NUMERIC                                  
W34557     AND PV-PROCESS-DATE-MM IS NUMERIC                                    
W34557     AND PV-PROCESS-DATE-DD IS NUMERIC                                    
W34557     AND PV-VALID-PROCESS-DATE-DASH-1                                     
W34557     AND PV-VALID-PROCESS-DATE-DASH-2                                     
W34557         CONTINUE                                                         
W34557     ELSE                                                                 
W34557         SET ABEND-4015-DATE-ERROR    TO TRUE                             
W34557         MOVE 'B150-PROCESS-DATE-FILE'                                    
W34557                                      TO PV-PARAGRAPH-NAME                
W34557         MOVE 'PV-CC-CW-INPUT-DTE VALIDATION'                             
W34557                                      TO PV-OPERATION-MSG-1               
W34557         DISPLAY 'PV-CC-CW-INPUT-DTE = ' PV-CC-CW-INPUT-DTE               
W34557         MOVE 'DATE IS INVALID'       TO PV-OPERATION-MSG-2               
W34557         PERFORM Z900-ABEND                                               
W34557     END-IF.                                                              
W34557                                                                          
W34557     MOVE PV-CC-CW-INPUT-DTE          TO DTATTR-KY-DTE.                   
W34557                                                                          
      ******************************************************************        
      *    PROCESS TINVPAR CURSOR AND OTHER TABLES                              
      ******************************************************************        
       B200-PROCESS-TINVPAR.                                                    
                                                                                
25248      MOVE INVPAR-LOC-NBR       TO RP-STORE-NBR                            
                                        PV-STORE-NUM-X.                         
           MOVE INVPAR-ACTL-INV-DTE  TO RP-ACTL-INV-DATE.                       
           MOVE INVPAR-PLND-INV-DTE  TO RP-PLAN-INV-DATE.                       
                                                                                
           IF INVPAR-ACTL-INV-DTE = PC-INCOMPLETE-DTE                           
               MOVE SPACES TO RP-FILE-NAME                                      
               STRING PV-SPC-HIGH-LVL-QUAL                                      
                      PC-MID-DSN-QUAL                                           
                      PC-STR-DSN-QUAL                                           
                      RP-STORE-NBR                                              
                   DELIMITED BY SPACE                                           
                   INTO RP-FILE-NAME                                            
                   ON OVERFLOW                                                  
                       MOVE PC-ERR-IN-BUILDING-DSN TO RP-FILE-NAME              
               END-STRING                                                       
               IF RP-FILE-NAME = PC-ERR-IN-BUILDING-DSN                         
                   ADD 1 TO PV-IGNORED-STORE-COUNT                              
               ELSE                                                             
      *                                                                         
      *  CREATE THE DELETE COMMAND                                              
      *                                                                         
                   STRING PC-DELETE-CMD-FIRST                                   
                          PC-QUOTE                                              
                          RP-FILE-NAME                                          
                          PC-QUOTE                                              
                          PC-DELETE-CMD-LAST                                    
                       DELIMITED BY '  '                                        
                       INTO GDG-CREATE-CNTL-REC                                 
                       ON OVERFLOW                                              
                           MOVE SPACES TO GDG-CREATE-CNTL-REC                   
                           MOVE PC-ERR-IN-BUILDING-DELETE                       
                               TO RP-FILE-NAME                                  
                   END-STRING                                                   
                   IF GDG-CREATE-CNTL-REC = SPACES                              
                       DISPLAY '  ' RP-REPORT-DETAIL                            
                   ELSE                                                         
                       PERFORM Z500-WRITE-GDG-DEF-CMDS                          
                   END-IF                                                       
                                                                                
      *                                                                         
      *  CREATE THE DEFINE COMMAND                                              
      *                                                                         
                   STRING PC-DEFINE-CMD-FIRST                                   
                          RP-FILE-NAME                                          
                          PC-DEFINE-CMD-LAST                                    
                       DELIMITED BY '  '                                        
                       INTO GDG-CREATE-CNTL-REC                                 
                       ON OVERFLOW                                              
                           MOVE SPACES TO GDG-CREATE-CNTL-REC                   
                           MOVE PC-ERR-IN-BUILDING-DEFINE                       
                               TO RP-FILE-NAME                                  
                   END-STRING                                                   
                   IF GDG-CREATE-CNTL-REC NOT = SPACES                          
                       ADD 1 TO PV-DEFINED-STORE-COUNT                          
                       PERFORM Z500-WRITE-GDG-DEF-CMDS                          
      *                                                                         
      * WRITE A BLANK LINE TO SEPARATE BETWEEN EACH STORE'S COMMANDS            
      *                                                                         
                       PERFORM Z500-WRITE-GDG-DEF-CMDS                          
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               ADD 1 TO PV-IGNORED-STORE-COUNT                                  
               MOVE PC-CANNOT-ALLOCATE TO RP-FILE-NAME                          
           END-IF.                                                              
                                                                                
           DISPLAY '  ' RP-REPORT-DETAIL.                                       
                                                                                
           PERFORM Z100-FETCH-TINVPAR.                                          
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B400-FINALIZE.                                                           
                                                                                
           EXEC SQL                                                             
                CLOSE TINVPAR-CURSOR                                            
           END-EXEC                                                             
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE TINVPAR-CURSOR' TO PV-OPER-ATTEMPTED                 
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           CLOSE   SPC-REQUEST-CNTL-FILE                                        
                   LVL-LISTCAT-CNTL-FILE                                        
                   GDG-CREATE-CNTL-FILE.                                        
                                                                                
           DISPLAY ' ------------------------------------------------'.         
           MOVE PV-DEFINED-STORE-COUNT       TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' STORE(S) PROCESSED:       ' PV-NUMERIC-DISPLAY.            
           MOVE PV-IGNORED-STORE-COUNT       TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' STORE(S) NOT PROCESSED:   ' PV-NUMERIC-DISPLAY.            
                                                                                
      ******************************************************************        
      *    FETCH THE NEXT RECORD FROM TINVPAR CURSOR                            
      ******************************************************************        
       Z100-FETCH-TINVPAR.                                                      
                                                                                
           EXEC SQL FETCH TINVPAR-CURSOR                                        
25248         INTO :INVPAR-LOC-NBR                                              
                  ,:INVPAR-ACTL-INV-DTE                                         
                  ,:INVPAR-PLND-INV-DTE                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET END-OF-TINVPAR TO TRUE                                   
               WHEN OTHER                                                       
                   MOVE 'FETCH TINVPAR-CURSOR' TO  PV-OPER-ATTEMPTED            
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    READ THE CONTROL CARD FILE                                           
      ******************************************************************        
       Z200-READ-REQUEST-FILE.                                                  
                                                                                
           READ SPC-REQUEST-CNTL-FILE INTO RQ-SPC-CNTL-AREA                     
               AT END MOVE PC-Y TO PS-END-OF-SPC-CNTL-FILE.                     
                                                                                
      ******************************************************************        
      *    WRITE OUT THE GDG LISTCAT COMMAND                                    
      ******************************************************************        
       Z400-WRITE-GDG-LIST-CMD.                                                 
                                                                                
           WRITE LVL-LISTCAT-CNTL-REC.                                          
                                                                                
      ******************************************************************        
      *    WRITE OUT THE GDG DEF COMMANDS                                       
      ******************************************************************        
       Z500-WRITE-GDG-DEF-CMDS.                                                 
                                                                                
           INSPECT GDG-CREATE-CNTL-REC                                          
               REPLACING ALL LOW-VALUES BY SPACES                               
                             HIGH-VALUES BY SPACES.                             
           WRITE GDG-CREATE-CNTL-REC.                                           
                                                                                
           MOVE SPACES TO GDG-CREATE-CNTL-REC.                                  
                                                                                
W34557*****************************************************************         
W34557* Z900-ABEND.                                                   *         
W34557*  - PERFORM AN ABEND THAT OCCURED RELATED TO FILE              *         
W34557*****************************************************************         
W34557 Z900-ABEND.                                                              
W34557                                                                          
W34557     DISPLAY '** ABEND           **'.                                     
W34557     DISPLAY '** PROGRAM         ** =  INKUT008'.                         
W34557     DISPLAY '** PARAGRAPH       ** = '  PV-PARAGRAPH-NAME.               
W34557     DISPLAY '** OPERATION       ** = '  PV-OPERATION-MSG-1.              
W34557     DISPLAY '**                 ** = '  PV-OPERATION-MSG-2.              
W34557     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
W34557                                                                          
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT008'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
