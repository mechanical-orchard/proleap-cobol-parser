       IDENTIFICATION DIVISION.                                         00010099
P28545 PROGRAM-ID.     INKUP307.                                        00020099
       INSTALLATION.   KOHLS DEPARTMENT STORES.                         00030099
       DATE-WRITTEN.   06/28/2006.                                      00040099
       DATE-COMPILED.                                                   00050099
                                                                        00060099
      ******************************************************************00070099
      *PURPOSE:    THE MODULE TAKES READS CLEARANCE AREAS ON TSUMINV    00080099
      *          USING DEFINED LOCATION NUMBER(S). PRICE IS VALIDATED   00090099
      *          COMPARING INVENTORIED PRICE TO THE CURRENT RETAIL.   . 00100099
      *          IF THE VARIANCE IS GREATER THAN ZERO, ADJUST DOWN THE  00110099
      *          SKU EXTD RETAIL AND INSERT A ROW TO TADJADD/TADJDTL.   00111099
      *          THE MODULE TAKES IN THE DAILY FAST TRANSFER SYSTEM     00120099
      *                                                                 00121099
      *CHG ID/                                                          00130099
      *WR/PROJ DATE       PGMR        DESCRIPTION                       00140099
      *------- ---------- ----------- ----------------------------------00150099
W26603* W26603 06-29-2006 ROD JANSSEN CREATION                          00151099
P28545* 28545  04-11-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP00152099
P28545*                               (REMOVE HARDCODED INWS400/INPD400 00153099
P28545*                                LOGIC)                           00154099
      ******************************************************************00160099
                                                                        00170099
       ENVIRONMENT DIVISION.                                            00180099
       CONFIGURATION SECTION.                                           00190099
       INPUT-OUTPUT SECTION.                                            00200099
       FILE-CONTROL.                                                    00210099
                                                                        00220099
           SELECT LOCATION-CONTROL-FILE                                 00230099
               ASSIGN TO INP01.                                         00240099
                                                                        00250099
           SELECT TSUMINV-REPORT                                        00260099
               ASSIGN TO RPT01.                                         00270099
                                                                        00280099
       DATA DIVISION.                                                   00290099
       FILE SECTION.                                                    00300099
                                                                        00310099
       FD  LOCATION-CONTROL-FILE                                        00320099
           RECORDING MODE IS F                                          00330099
           LABEL RECORDS ARE STANDARD                                   00340099
           BLOCK CONTAINS 0 RECORDS.                                    00350099
       01  LOCATION-CNTL-RECORD.                                        00360099
           05  LCR-LOC-NBR          PIC 9(05).                          00361099
           05  LCR-REMAINDER        PIC X(75).                          00362099
                                                                        00370099
       FD  TSUMINV-REPORT                                               00380099
           LABEL RECORDS ARE OMITTED                                    00390099
           RECORD CONTAINS 132 CHARACTERS                               00400099
           RECORDING MODE IS F                                          00410099
           DATA RECORD IS REPORT-LINE.                                  00420099
       01  REPORT-LINE              PIC X(132).                         00430099
                                                                        00440099
      /*****************************************************************00450099
       WORKING-STORAGE SECTION.                                         00460099
      ******************************************************************00470099
                                                                        00480099
       01  PA-PROGRAM-ACCUMULATORS.                                     00490099
           05  PA-FS-WRITE-COUNT       PIC 9(09) VALUE ZEROES.          00500099
           05  PA-COMMIT-COUNT         PIC 9(09) VALUE ZEROES.          00510099
           05  PA-INSERT-COUNT         PIC 9(09) VALUE ZEROES.          00520099
           05  PA-UPDATE-COUNT         PIC 9(09) VALUE ZEROES.          00530099
           05  PA-EXTRACT-RECS-READ    PIC 9(09) VALUE ZEROES.          00540099
           05  PA-ZERO-REC-DROPPED     PIC 9(09) VALUE ZEROES.          00550099
           05  PA-DUPLICATE-REC        PIC 9(09) VALUE ZEROES.          00560099
           05  PA-ITEM-QTY             PIC S9(15)    COMP-3 VALUE ZEROS.00570099
           05  PA-EXTD-RTL-AMT         PIC S9(15)V99 COMP-3 VALUE ZEROS.00580099
           05  PA-TFR-ITEM-QTY         PIC S9(15)    COMP-3 VALUE ZEROS.00590099
           05  PA-TFR-EXTD-RTL-AMT     PIC S9(15)V99 COMP-3 VALUE ZEROS.00600099
           05  PA-DFF-EXTD-RTL-AMT     PIC S9(15)V99 COMP-3 VALUE ZEROS.00610099
           05  PA-LOC-ITEM-QTY         PIC S9(15)    COMP-3 VALUE ZEROS.00620099
           05  PA-LOC-EXTD-RTL-AMT     PIC S9(15)V99 COMP-3 VALUE ZEROS.00630099
           05  PA-LOC-TFR-ITEM-QTY     PIC S9(15)    COMP-3 VALUE ZEROS.00640099
           05  PA-LOC-TFR-EXTD-RTL-AMT PIC S9(15)V99 COMP-3 VALUE ZEROS.00650099
           05  PA-LOC-DFF-EXTD-RTL-AMT PIC S9(15)V99 COMP-3 VALUE ZEROS.00660099
           05  PA-TOT-ITEM-QTY         PIC S9(15)    COMP-3 VALUE ZEROS.00670099
           05  PA-TOT-EXTD-RTL-AMT     PIC S9(15)V99 COMP-3 VALUE ZEROS.00680099
           05  PA-TOT-TFR-ITEM-QTY     PIC S9(15)    COMP-3 VALUE ZEROS.00690099
           05  PA-TOT-TFR-EXTD-RTL-AMT PIC S9(15)V99 COMP-3 VALUE ZEROS.00700099
           05  PA-TOT-DFF-EXTD-RTL-AMT PIC S9(15)V99 COMP-3 VALUE ZEROS.00710099
                                                                        00720099
       01  PC-PROGRAM-CONSTANTS-AREA.                                   00730099
           05  PC-PROGRAM-NAME      PIC X(08)  VALUE 'INKUP307'.        00740099
           05  PC-JOB-NAME          PIC X(08)  VALUE 'INK332  '.        00750099
           05  PC-PRINT-LINE-MAX    PIC S9(04) VALUE +60 COMP.          00760099
           05  PC-MAX-RETRIES       PIC S9(04) VALUE +3 COMP SYNC.      00770099
           05  PC-OPERCO-NBR        PIC X(02)  VALUE '03'.              00780099
           05  PC-11                PIC S9(04) COMP VALUE 11.           00790099
           05  PC-ONE-BILLION       PIC S9(11) COMP-3 VALUE +1000000000.00800099
           05  PC-PRIME1            PIC S9(05) COMP-3 VALUE +38543.     00810099
           05  PC-PRIME2            PIC S9(05) COMP-3 VALUE +58889.     00820099
                                                                        00830099
       01  PV-PROGRAM-VARIABLES-AREA.                                   00840099
           05  PV-COUNT                 PIC S9(04) COMP VALUE ZEROS.    00850099
           05  PV-EDIT-SKU-NBR          PIC 9(08).                      00860099
           05  FILLER REDEFINES PV-EDIT-SKU-NBR.                        00870099
               10  PV-EDIT-SKU-NBR-DPT  PIC 9(03).                      00880099
               10  PV-EDIT-SKU-NBR-CLS  PIC 9(02).                      00890099
               10  PV-EDIT-REMAINDER    PIC 9(03).                      00900099
           05  FILLER REDEFINES PV-EDIT-SKU-NBR.                        00910099
               10  PV-EDIT-SKU-NBR-P1   PIC X(05).                      00920099
               10  PV-EDIT-SKU-NBR-P2   PIC X(03).                      00930099
                   88  PV-ZERO-SEQUENCE-SKU  VALUES '000' '999'.        00940099
           05  PV-EDIT-UPC-NBR          PIC 9(15).                      00950099
           05  PV-MAJ-CL-NBR.                                           00960099
               10 PV-MAJ-CL-NBR-1      PIC X(01)  VALUE SPACES.         00970099
               10 PV-MAJ-CL-NBR-2      PIC X(01)  VALUE SPACES.         00980099
           05  PV-MAJ-CL-NBR-9 REDEFINES                                00990099
               PV-MAJ-CL-NBR           PIC 9(02).                       01000099
           05  PV-SUB-CL-NBR.                                           01010099
               10 PV-SUB-CL-NBR-1      PIC X(01)  VALUE SPACES.         01020099
               10 PV-SUB-CL-NBR-2-3    PIC X(02)    VALUE SPACES.       01030099
           05  PV-WORK-FIELD           PIC S9(17) COMP-3 VALUE ZERO.    01040099
           05  PV-WORK-FIELD2          PIC S9(17) COMP-3 VALUE ZERO.    01050099
           05  PV-SEED                 PIC S9(11) COMP-3 VALUE ZERO.    01060099
           05  PV-ADD-NUM              PIC S9(04) COMP VALUE ZERO.      01070099
           05  PV-RANDOM-NUMBER        PIC S9(11) COMP-3 VALUE +0.      01080099
           05  PV-TODAYS-DATE       PIC 9(06)  VALUE ZERO.              01090099
           05  FILLER REDEFINES PV-TODAYS-DATE.                         01100099
               10  PV-CD-YY         PIC XX.                             01110099
               10  PV-CD-MM         PIC XX.                             01120099
               10  PV-CD-DD         PIC XX.                             01130099
           05  PV-TODAYS-TIME       PIC 9(08)  VALUE ZERO.              01140099
           05  FILLER REDEFINES PV-TODAYS-TIME.                         01150099
               10  PV-HH            PIC 9(02).                          01160099
               10  PV-MIN           PIC 9(02).                          01170099
               10  PV-SEC           PIC 9(02).                          01180099
               10  PV-HUND-SEC      PIC 9(02).                          01190099
           05  PV-CURRENT-TIME.                                         01200099
               10  PV-CT-HH            PIC XX.                          01210099
               10  PV-CT-MM            PIC XX.                          01220099
               10  PV-CT-REMAINDER     PIC XXXX.                        01230099
           05  PV-INITIAL-SEED      PIC 9(08)    VALUE ZERO.            01240099
           05  FILLER REDEFINES PV-INITIAL-SEED.                        01250099
               10  PV-SEED-BYTE-1-2 PIC 9(02).                          01260099
               10  PV-SEED-BYTE-3-4 PIC 9(02).                          01270099
               10  PV-SEED-BYTE-5-6 PIC 9(02).                          01280099
               10  PV-SEED-BYTE-7-8 PIC 9(02).                          01290099
           05  PV-SKU-NBR           PIC 9(8).                           01300099
           05  PV-SKU-NBR-ALPH REDEFINES                                01310099
               PV-SKU-NBR           PIC X(8).                           01320099
           05  PV-PAGE-COUNT           PIC S9(05) COMP-3 VALUE 0.       01330099
           05  PV-LINE-COUNT           PIC S9(05) COMP-3 VALUE 0.       01340099
           05  PV-CURRENT-TIMESTAMP        PIC  X(26)      VALUE SPACES.01350099
           05  PV-COMMIT-TIMESTAMP         PIC  X(26)      VALUE SPACES.01360099
           05  PV-PARAGRAPH-NAME           PIC  X(30)      VALUE SPACES.01370099
           05  PV-ABEND-MESSAGE            PIC  X(75)      VALUE SPACES.01380099
           05  PV-OPERATION-ATTEMPTED      PIC  X(30)      VALUE SPACES.01390099
           05  PV-STR-NBR-SAVE      PIC S9(04) COMP VALUE ZEROS.        01400099
           05  PV-AREA-NBR-SAVE     PIC S9(09) COMP VALUE ZEROS.        01410099
           05  PV-ITEM-QTY          PIC S9(04) COMP VALUE ZEROS.        01420099
           05  PV-SKU-NBR-SAVE      PIC S9(08)V COMP-3 VALUE ZEROS.     01430099
           05  PV-UPC-NBR-SAVE      PIC S9(15)V COMP-3 VALUE ZEROS.     01440099
           05  PV-EXTD-AMOUNT       PIC S9(09)V99 COMP-3 VALUE ZEROS.   01450099
           05  PV-AREA-NBR          PIC 9(06) VALUE ZEROS.              01460099
           05  PV-RETRY-COUNT       PIC S9(04) COMP VALUE ZEROS.        01470099
           05  PV-OPER-ATTEMPTED    PIC X(25)  VALUE SPACES.            01480099
           05  PV-RECORDS-READ      PIC S9(15) COMP-3 VALUE ZEROS.      01490099
           05  PV-STORE-ISSUE-COUNT PIC S9(15) COMP-3 VALUE ZEROS.      01500099
           05  PV-NO-UNIT-COUNT     PIC S9(15) COMP-3 VALUE ZEROS.      01510099
           05  PV-NO-PRICE-COUNT    PIC S9(15) COMP-3 VALUE ZEROS.      01520099
           05  PV-DUMMY-SKU-COUNT   PIC S9(15) COMP-3 VALUE ZEROS.      01530099
           05  PV-CLEARANCE-COUNT   PIC S9(15) COMP-3 VALUE ZEROS.      01540099
           05  PV-RED-COUNT         PIC S9(15) COMP-3 VALUE ZEROS.      01550099
           05  PV-GROUP-PRICE-COUNT PIC S9(15) COMP-3 VALUE ZEROS.      01560099
           05  PV-RECORDS-ACCEPTED  PIC S9(15) COMP-3 VALUE ZEROS.      01570099
           05  PV-UNT-RTL-AMT-HOLD  PIC S9(15)V99 COMP-3 VALUE ZEROS.   01580099
           05  PV-WORK-AMOUNT       PIC S9(15)V99 COMP-3 VALUE ZEROS.   01581099
           05  PV-UPC-STAT-CHG-DTE  PIC X(10)  VALUE SPACES.            01590099
           05  PV-UNT-RTL-CHG-DTE   PIC X(10)  VALUE SPACES.            01600099
           05  PV-UNT-QTY-COUNT     PIC S9(15) COMP-3 VALUE ZEROS.      01610099
           05  PV-UNT-RTL-AMT-COUNT PIC S9(15)V99 COMP-3 VALUE ZEROS.   01620099
           05  PV-TFR-QTY-COUNT     PIC S9(15) COMP-3 VALUE ZEROS.      01630099
           05  PV-TFR-RTL-AMT-COUNT PIC S9(15)V99 COMP-3 VALUE ZEROS.   01640099
           05  PV-STR-QTY-COUNT     PIC S9(15) COMP-3 VALUE ZEROS.      01650099
           05  PV-STR-RTL-AMT-COUNT PIC S9(15)V99 COMP-3 VALUE ZEROS.   01660099
           05  PV-TOT-QTY-COUNT     PIC S9(15) COMP-3 VALUE ZEROS.      01670099
           05  PV-TOT-RTL-AMT-COUNT PIC S9(15)V99 COMP-3 VALUE ZEROS.   01680099
           05  PV-DISPLAY      PIC ZZZZZZZZZZZZZZZZZ9.99 VALUE ZEROS.   01690099
           05  PV-DISPLAY-2    PIC ZZZZZZZZZZZZZZ  VALUE ZEROS.         01700099
           05  PV-EDIT-DOC-NBR.                                         01710099
               10  PV-EDIT-DOC-FROM-LOC-NBR  PIC 9(05) VALUE ZEROS.     01720099
               10  FILLER REDEFINES PV-EDIT-DOC-FROM-LOC-NBR.           01730099
                   15  PV-EDIT-DOC-FR-LOC-P1 PIC 9(01).                 01740099
                   15  PV-EDIT-DOC-FR-LOC-P2 PIC 9(04).                 01750099
               10  PV-EDIT-DOC-SEQ-NBR       PIC 9(09) VALUE ZEROS.     01760099
               10  PV-EDIT-DOC-SEQ-NBR-RED REDEFINES                    01770099
                   PV-EDIT-DOC-SEQ-NBR.                                 01780099
                   15  PV-EDIT-DOC-1ST-3     PIC 9(03).                 01790099
                   15  PV-EDIT-DOC-LAST-6.                              01800099
                       20  PV-EDIT-DOC-PART1     PIC 9(03).             01810099
                       20  PV-EDIT-DOC-PART2     PIC 9(03).             01820099
           05  PV-PLND-INV-TMST            PIC  X(26) VALUE SPACES.     01830099
           05  PV-PLND-INV-TIMESTAMP.                                   01840099
               10  PV-PLND-INV-DTE         PIC  X(10)  VALUE SPACES.    01850099
               10  PV-DASH                 PIC  X(01)  VALUE '-'.       01860099
               10  PV-PLND-DTE-HOUR        PIC  X(02)  VALUE '23'.      01870099
               10  PV-SEPARATOR            PIC  X(01)  VALUE '.'.       01880099
               10  PV-PLND-DTE-MINUTE      PIC  X(02)  VALUE '59'.      01890099
               10  PV-SEPARATOR            PIC  X(01)  VALUE '.'.       01900099
               10  PV-PLND-DTE-SECOND      PIC  X(02)  VALUE '59'.      01910099
               10  PV-SEPARATOR            PIC  X(01)  VALUE '.'.       01920099
               10  PV-PLND-DTE-MSEC        PIC  X(06)  VALUE '999999'.  01930099
                                                                        01940099
       01  PS-SWITCHES-AREA.                                            01950099
           05  PS-END-OF-TSUMINV-DATA      PIC X           VALUE 'N'.   01960099
               88  END-OF-TSUMINV-DATA                     VALUE 'Y'.   01970099
           05  PS-END-OF-FILE-SW           PIC X           VALUE 'N'.   02060099
               88  PS-END-OF-FILE                          VALUE 'Y'.   02070099
                                                                        02110099
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES 02120099
                               COMP SYNC.                               02130099
           88  ABEND-4004                                  VALUE +4004. 02140099
           88  ABEND-4013                                  VALUE +4013. 02150099
                                                                        02160099
       01  HL1-HEADING-LINE-1.                                          02170099
           05  FILLER                  PIC X(40) VALUE SPACES.          02180099
           05  FILLER                  PIC X(53) VALUE                  02190099
               'ADJUSTMENTS TO PHYSICAL INVENTORY 9000 AREA LOCATIONS'. 02200099
                                                                        02210099
       01  HL2-HEADING-LINE-2.                                          02220099
           05  FILLER                  PIC X(02) VALUE SPACES.          02230099
           05  FILLER                  PIC X(05) VALUE 'STORE'.         02240099
           05  FILLER                  PIC X(02) VALUE SPACES.          02250099
           05  FILLER                  PIC X(09) VALUE '         '.     02260099
           05  FILLER                  PIC X(08) VALUE SPACES.          02270099
           05  FILLER                  PIC X(04) VALUE 'SUB-'.          02280099
           05  FILLER                  PIC X(45) VALUE SPACES.          02290099
           05  FILLER                  PIC X(06) VALUE 'INVNTY'.        02300099
           05  FILLER                  PIC X(09) VALUE SPACES.          02310099
           05  FILLER                  PIC X(08) VALUE 'CUTOFF  '.      02340099
           05  FILLER                  PIC X(05) VALUE SPACES.          02350099
           05  FILLER                  PIC X(08) VALUE 'DIFF    '.      02360099
           05  FILLER                  PIC X(09) VALUE SPACES.          02370099
           05  FILLER                  PIC X(08) VALUE 'EXTENDED'.      02380099
                                                                        02390099
       01  HL3-HEADING-LINE-3.                                          02400099
           05  FILLER                  PIC X(02) VALUE SPACES.          02410099
           05  FILLER                  PIC X(05) VALUE 'NBR  '.         02420099
           05  FILLER                  PIC X(02) VALUE SPACES.          02430099
           05  FILLER                  PIC X(09) VALUE 'AREA NBR '.     02440099
           05  FILLER                  PIC X(02) VALUE SPACES.          02450099
           05  FILLER                  PIC X(04) VALUE 'DEPT'.          02460099
           05  FILLER                  PIC X(02) VALUE SPACES.          02470099
           05  FILLER                  PIC X(05) VALUE 'CLASS'.         02480099
           05  FILLER                  PIC X(02) VALUE SPACES.          02490099
           05  FILLER                  PIC X(08) VALUE 'SKU     '.      02500099
           05  FILLER                  PIC X(05) VALUE SPACES.          02510099
           05  FILLER                  PIC X(08) VALUE 'UPC     '.      02511099
           05  FILLER                  PIC X(10) VALUE SPACES.          02512099
           05  FILLER                  PIC X(03) VALUE 'QTY'.           02520099
           05  FILLER                  PIC X(08) VALUE SPACES.          02530099
           05  FILLER                  PIC X(06) VALUE 'PRICE '.        02540099
           05  FILLER                  PIC X(09) VALUE SPACES.          02550099
           05  FILLER                  PIC X(06) VALUE 'PRICE '.        02580099
           05  FILLER                  PIC X(07) VALUE SPACES.          02590099
           05  FILLER                  PIC X(11) VALUE 'CTOFF PRICE'.   02600099
           05  FILLER                  PIC X(06) VALUE SPACES.          02610099
           05  FILLER                  PIC X(11) VALUE 'ADJUST MADE'.   02620099
                                                                        02630099
       01  DL-DETAIL-LINE.                                              02640099
           05  FILLER                  PIC X(02) VALUE SPACES.          02650099
           05  DL-LOC-NBR              PIC Z9999.                       02660099
           05  FILLER                  PIC X(02) VALUE SPACES.          02670099
           05  DL-SHEET-NBR            PIC ZZZ999999.                   02680099
           05  FILLER                  PIC X(02) VALUE SPACES.          02690099
           05  DL-DEPT-NBR             PIC 9(04).                       02700099
           05  FILLER                  PIC X(02) VALUE SPACES.          02710099
           05  DL-SUBCLASS-NBR         PIC 9(04).                       02720099
           05  FILLER                  PIC X(03) VALUE SPACES.          02730099
           05  DL-SKU-NBR              PIC 99999999.                    02740099
           05  FILLER                  PIC X(02) VALUE SPACES.          02750099
           05  DL-UPC-NBR              PIC ZZZZZ9999999999.             02751099
           05  FILLER                  PIC X(02) VALUE SPACES.          02752099
           05  DL-UNIT-QTY             PIC Z,ZZZ,ZZ9-.                  02760099
           05  FILLER                  PIC X(01) VALUE SPACES.          02770099
           05  DL-UNIT-RTL-AMT         PIC Z,ZZZ,ZZ9.99-.               02780099
           05  FILLER                  PIC X(02) VALUE SPACES.          02790099
           05  DL-TFR-RTL-AMT          PIC Z,ZZZ,ZZ9.99-.               02820099
           05  FILLER                  PIC X(03) VALUE SPACES.          02830099
           05  DL-EXTD-TFR-RTL-AMT     PIC ZZ,ZZZ,ZZ9.99-.              02840099
           05  FILLER                  PIC X(02) VALUE SPACES.          02850099
           05  DL-DIFF-RTL-AMT         PIC ZZ,ZZZ,ZZ9.99-.              02860099
                                                                        02870099
       01  TL-TOTAL-LINE.                                               02880099
           05  FILLER                  PIC X(04) VALUE SPACES.          02890099
           05  TL-VERBIAGE             PIC X(22).                       02900099
           05  FILLER                  PIC X(32) VALUE SPACES.          02910099
           05  TL-UNIT-QTY             PIC ZZ,ZZZ,ZZ9-.                 02920099
           05  FILLER                  PIC X(15) VALUE SPACES.          02930099
           05  TL-EXTD-TFR-RTL-AMT     PIC ZZZ,ZZZ,ZZZ.ZZ-.             02960099
           05  FILLER                  PIC X(18) VALUE SPACES.          02970099
           05  TL-DIFF-RTL-AMT         PIC ZZZ,ZZZ,ZZ9.99-.             02980099
                                                                        02990099
       01  NL-NO-REPORT-LINE.                                           03000099
           05  FILLER                  PIC X(02) VALUE SPACES.          03010099
           05  FILLER                  PIC X(26)                        03020099
               VALUE 'NOTHING TO REPORT'.                               03030099
                                                                        03040099
      ******************************************************************03050099
      * COPYBOOK - KOHLS STANDARD REPORT HEADINGS, DETAIL FOR 132 BYTES 03060099
      ******************************************************************03070099
           COPY DPWS132O.                                               03080099
                                                                        03090099
      ******************************************************************03100099
      * COPYBOOK -                                                      03110099
      *    DPWS004 CONTAINS THE FIELDS NECESSARY TO CALL THE SQLCA      03120099
      * MESSAGE MODULE 'DSNTIAR' AND DISPLAY THE FORMATTED RESULTS.     03130099
      ******************************************************************03140099
           COPY DPWS004.                                                03150099
                                                                        03160099
      ******************************************************************03170099
           EXEC SQL                                                     03180099
               INCLUDE SQLCA                                            03190099
           END-EXEC.                                                    03200099
                                                                        03210099
      ******************************************************************03220099
      *    TADJDTL - INVENTORY ADJUSTMENT DETAIL TABLE.                 03230099
      ******************************************************************03240099
           EXEC SQL                                                     03250099
               INCLUDE TADJDTL                                          03260099
           END-EXEC.                                                    03270099
                                                                        03280099
      ******************************************************************03290099
      *    TADJADD - INVENTORY ADJUSTMENT ADD TABLE.                    03300099
      ******************************************************************03310099
           EXEC SQL                                                     03320099
               INCLUDE TADJADD                                          03330099
           END-EXEC.                                                    03340099
                                                                        03350099
      ******************************************************************03360099
      *    TSUMINV -                                                    03370099
      ******************************************************************03380099
           EXEC SQL                                                     03390099
               INCLUDE TSUMINV                                          03400099
           END-EXEC.                                                    03410099
                                                                        03420099
      ******************************************************************03500099
      *    TINVPAR - INVENTORY PARAMATERS TABLE                         03510099
      ******************************************************************03520099
           EXEC SQL                                                     03530099
               INCLUDE TINVPAR                                          03540099
           END-EXEC.                                                    03550099
                                                                        03560099
P28545******************************************************************03560199
P28545*    TINCNTL - INVENTORY CONTROL MASTER TABLE                     03560299
P28545******************************************************************03560399
P28545     EXEC SQL                                                     03560499
P28545         INCLUDE TINCNTL                                          03560599
P28545     END-EXEC.                                                    03560699
P28545                                                                  03560799
      ******************************************************************03561099
      * COPYBOOK - PRICING MODULE                                       03562099
      ******************************************************************03563099
           COPY INWS400.                                                03564099
                                                                        03565099
      ***************************************************************** 03566099
      ******************************************************************03660699
      * TABLE CURSOR FOR THE TRADING PARTNER FUNCTIONAL CONTROL GROUP.  03660799
      ******************************************************************03660899
           EXEC SQL                                                     03660999
               DECLARE TSUMINV_CURSOR CURSOR FOR                        03661099
                  SELECT LOC_NBR                                        03661199
                        ,AREA_NBR                                       03661299
                        ,SKU_NBR                                        03661399
                        ,UPC_NBR                                        03661499
                        ,INV_UNIT_PR_AMT                                03662099
                        ,INV_QTY                                        03670099
                        ,MISSED_MD_IND                                  03680099
                        ,DKEY_NBR                                       03690099
                        ,CHG_TMST                                       03700099
                        ,CHG_ID_NBR                                     03710099
                        ,EXTD_AMT                                       03720099
                        ,DEPT_NBR                                       03730099
                        ,MAJ_CL_NBR                                     03740099
                        ,SUB_CL_NBR                                     03750099
                  FROM TSUMINV                                          03760099
                  WHERE LOC_NBR = :SUMINV-LOC-NBR                       03770099
                    AND AREA_NBR BETWEEN 9000 AND 9999                  03780099
                  WITH UR                                               03790099
           END-EXEC.                                                    03800099
                                                                        03810099
      ******************************************************************03870099
      *FLOW:     THE MODULE TAKES READS CLEARANCE AREAS ON TSUMINV      03880099
      *      USING DEFINED LOCATION NUMBER(S). PRICE IS VALIDATED       03890099
      *      COMPARING INVENTORIED PRICE TO THE CURRENT RETAIL.   .     03900099
      *      IF THE VARIANCE IS GREATER THAN ZERO, ADJUST DOWN THE      03900199
      *      SKU EXTD RETAIL AND INSERT A ROW TO TADJADD/TADJDTL.       03900299
      *      THE MODULE TAKES IN THE DAILY FAST TRANSFER SYSTEM         03900399
      *                                                                 03906099
      *         BREAKS OCCUR AT THE LOCATION, AREA, AND SKU/UPC LEVELS. 03910099
      *      ACCUMULATE TOTALS AND INTERNAL CONTROLS AT THESE LEVELS.   03920099
      *      WRITE REPORT DETAIL, SUBTOTALS, AND TOTALS ACCORDINGLY.    03930099
      *                                                                 03940099
      *         DEVELOP PRICE BASED ON THE SKU/UPC AND THE PLANNED      03950099
      *      INVENTORY DATE. PASS THE SKU/UPC TO THE INVENTORY PRICE    03960099
      *      MODULE AND DEVELOP RESULTS BASED ON THE OUTPUT.            03970099
      *                                                                 03990099
      *         FORMAT AN ADJUSTMENT TABLE RECORDS AND INSERT INTO      04000099
      *      TABLE TADJADD/TADJDTL.                                     04010099
      *                                                                 04020099
      *      INPUT:                                                     04030099
      *         INP01   - LOCATION CONTROL                              04040099
      *                                                                 04050099
      *      OUTPUT:                                                    04060099
      *         TADJADD - INSERT DOLLAR ADJUSTMENTS                     04071099
      *         TADJDTL - INSERT DOLLAR ADJUSTMENTS                     04080099
      *         RPT01   - SUMMARY REPORT......                          04100099
      *                                                                 04110099
      *      TABLES USED:                                               04120099
      *         TSUMINV    - PHYSICAL INVENTORY SUMMARY TABLE           04130099
      *         TADJADD    - INVENTORY ADJUSTMENT ADD                   04140099
      *         TADJDTL    - INVENTORY ADJUSTMENT DETAIL                04150099
      *         INWS400/INPD400 - INVENTORY PRICING MODULE              04160099
      *                                                                 04170099
      ******************************************************************04180099
       PROCEDURE DIVISION.                                              04190099
      ******************************************************************04200099
       0000-MAINLINE-DRIVER-SECTION.                                    04210099
                                                                        04220099
           PERFORM 1000-INITIALIZATION.                                 04230099
                                                                        04240099
           PERFORM 2000-MAIN-PROCESS-LOOP                               04250099
               UNTIL PS-END-OF-FILE.                                    04260099
                                                                        04270099
           PERFORM 9000-END-OF-JOB-TASKS.                               04280099
                                                                        04290099
       9999-EXIT-PROGRAM.                                               04300099
           STOP RUN.                                                    04310099
                                                                        04320099
      /*****************************************************************04330099
      * 1000-INITIALIZATION.                                            04340099
      ******************************************************************04350099
       1000-INITIALIZATION.                                             04360099
                                                                        04370099
           OPEN INPUT  LOCATION-CONTROL-FILE                            04390099
                OUTPUT TSUMINV-REPORT.                                  04391099
                                                                        04400099
           ACCEPT PV-TODAYS-DATE    FROM DATE.                          04410099
           ACCEPT PV-CURRENT-TIME   FROM TIME.                          04420099
                                                                        04430099
           MOVE PV-CD-MM            TO DP132O-RUN-MONTH.                04440099
           MOVE PV-CD-DD            TO DP132O-RUN-DAY.                  04450099
           MOVE PV-CD-YY            TO DP132O-RUN-YEAR.                 04460099
           MOVE PV-CT-HH            TO DP132O-RUN-HOUR.                 04470099
           MOVE PV-CT-MM            TO DP132O-RUN-MINUTE.               04480099
           MOVE PC-PROGRAM-NAME     TO DP132O-PROGRAM-NAME.             04490099
           MOVE 1                   TO DP132O-REPORT-NUMBER.            04500099
           MOVE ZEROS               TO PV-PAGE-COUNT.                   04510099
                                                                        04520099
           MOVE ZEROS          TO PV-RECORDS-READ                       04530099
                                  PV-STR-NBR-SAVE                       04540099
                                  PV-STORE-ISSUE-COUNT                  04550099
                                  PV-NO-UNIT-COUNT                      04560099
                                  PV-NO-PRICE-COUNT                     04570099
                                  PV-DUMMY-SKU-COUNT                    04580099
                                  PV-CLEARANCE-COUNT                    04590099
                                  PV-GROUP-PRICE-COUNT                  04600099
                                  PV-RECORDS-ACCEPTED.                  04610099
                                                                        04620099
      *** READ THE RECALC EXTRACT FILE...                               04770099
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                          04780099
                                                                        04790099
           PERFORM 8500-READ-NEXT-LOCATION.                             04790199
                                                                        04794099
           PERFORM 7000-PRINT-HEADINGS.                                 04800099
                                                                        04810099
      /*****************************************************************05170099
      * 2000-MAIN-PROCESS-LOOP.                                         05180099
      ******************************************************************05190099
       2000-MAIN-PROCESS-LOOP.                                          05200099
                                                                        05210099
           ADD 1               TO PV-RECORDS-READ.                      05220099
           MOVE LCR-LOC-NBR    TO SUMINV-LOC-NBR.                       05240199
           MOVE 'N'            TO PS-END-OF-TSUMINV-DATA.               05240299
                                                                        05240399
           PERFORM 8000-OPEN-TSUMINV.                                   05240499
                                                                        05240599
           PERFORM 8400-FETCH-TSUMINV.                                  05240699
                                                                        05240799
           IF (NOT END-OF-TSUMINV-DATA)                                 05240899
                                                                        05240999
               PERFORM 8300-READ-TINVPAR                                05241099
                                                                        05241199
               MOVE SUMINV-LOC-NBR      TO PV-STR-NBR-SAVE              05241299
               MOVE SUMINV-AREA-NBR     TO PV-AREA-NBR-SAVE             05242099
               MOVE SUMINV-SKU-NBR      TO PV-SKU-NBR-SAVE              05243099
               MOVE SUMINV-UPC-NBR      TO PV-UPC-NBR-SAVE              05244099
                                                                        05245099
               PERFORM 2500-PROCESS-TSUMINV-STORE                       05246099
                   UNTIL END-OF-TSUMINV-DATA                            05247099
                                                                        05248099
               PERFORM 5100-FORMAT-TSUMINV-TOTALS                       05249099
                                                                        05249199
               PERFORM 5200-FORMAT-STORE-TOTALS                         05249299
                                                                        05249399
               IF PV-STR-QTY-COUNT NOT EQUAL ZEROS     OR               05249499
                  PV-STR-RTL-AMT-COUNT NOT EQUAL ZEROS                  05249599
                     PERFORM 7000-PRINT-HEADINGS                        05249699
               END-IF                                                   05249799
                                                                        05249899
               MOVE ZEROS               TO PV-UNT-QTY-COUNT             05249999
               MOVE ZEROS               TO PV-UNT-RTL-AMT-COUNT         05250099
               MOVE ZEROS               TO PV-TFR-QTY-COUNT             05250199
               MOVE ZEROS               TO PV-TFR-RTL-AMT-COUNT         05251099
               MOVE ZEROS               TO PV-STR-QTY-COUNT             05252099
               MOVE ZEROS               TO PV-STR-RTL-AMT-COUNT         05253099
           END-IF.                                                      05253299
                                                                        05254099
           PERFORM 8100-CLOSE-TSUMINV.                                  05260099
                                                                        05270099
           PERFORM 8500-READ-NEXT-LOCATION.                             05280099
                                                                        05290099
      /*****************************************************************06021099
      * 2500-PROCESS-TSUMINV-STORE.                                     06022099
      ******************************************************************06023099
       2500-PROCESS-TSUMINV-STORE.                                      06024099
                                                                        06025099
           ADD 1               TO PA-EXTRACT-RECS-READ.                 06027099
                                                                        06028099
      *** BREAK LOGIC PERFORMED AT LOCATION, AREA, AND SKU/UPC LEVEL..  06029099
           IF SUMINV-AREA-NBR EQUAL PV-AREA-NBR-SAVE                    06029299
              IF (SUMINV-SKU-NBR EQUAL PV-SKU-NBR-SAVE AND              06029399
                   SUMINV-UPC-NBR EQUAL PV-UPC-NBR-SAVE)                06029499
                 PERFORM 3000-GATHER-VSM-INFORMATION                    06029699
              ELSE                                                      06029799
      *** SKU/UPC BREAK....                                             06029899
                 MOVE SUMINV-SKU-NBR    TO PV-SKU-NBR-SAVE              06029999
                 MOVE SUMINV-UPC-NBR    TO PV-UPC-NBR-SAVE              06030099
                 MOVE ZEROS             TO PV-UNT-QTY-COUNT             06030199
                 MOVE ZEROS             TO PV-UNT-RTL-AMT-COUNT         06030299
                 PERFORM 3000-GATHER-VSM-INFORMATION                    06030399
              END-IF                                                    06030499
           ELSE                                                         06030599
      *** AREA NUMBER BREAK.....                                        06030699
               PERFORM 5100-FORMAT-TSUMINV-TOTALS                       06030799
               MOVE SUMINV-AREA-NBR     TO PV-AREA-NBR-SAVE             06030899
               MOVE SUMINV-SKU-NBR      TO PV-SKU-NBR-SAVE              06030999
               MOVE SUMINV-UPC-NBR      TO PV-UPC-NBR-SAVE              06031099
               MOVE ZEROS               TO PV-UNT-QTY-COUNT             06031199
               MOVE ZEROS               TO PV-UNT-RTL-AMT-COUNT         06031299
               MOVE ZEROS               TO PV-TFR-QTY-COUNT             06031399
               MOVE ZEROS               TO PV-TFR-RTL-AMT-COUNT         06031499
               PERFORM 3000-GATHER-VSM-INFORMATION                      06031599
           END-IF.                                                      06033399
                                                                        06033499
           PERFORM 8400-FETCH-TSUMINV.                                  06033599
                                                                        06033699
      /*****************************************************************06037099
      * 3000-GATHER-VSM-INFORMATION.                                    06040099
      *     EDIT AND GATHER UPC INFORMATION FOR PRICING & UPDATES TO    06050099
      * TADJDTL (BASED ON THE TSUMINV SKU NBR)                          06060099
      ******************************************************************06070099
       3000-GATHER-VSM-INFORMATION.                                     06080099
                                                                        06090099
           INITIALIZE IN400-PV-INPUT-AREA.                              06100099
                                                                        06110099
           MOVE SUMINV-LOC-NBR      TO IN400-CONVERT-LOC.               06120099
           MOVE IN400-CONVERT-LOC   TO IN400-PV-LOC-IN.                 06130099
                                                                        06140099
           MOVE PV-PLND-INV-DTE     TO IN400-PV-EFF-DTE-IN.             06150099
                                                                        06160099
           MOVE SUMINV-SKU-NBR      TO PV-EDIT-SKU-NBR.                 06170099
           MOVE PV-EDIT-SKU-NBR     TO IN400-PV-SKU-IN.                 06180099
                                                                        06190099
           MOVE SUMINV-UPC-NBR      TO PV-EDIT-UPC-NBR.                 06200099
           MOVE PV-EDIT-UPC-NBR     TO IN400-PV-UPC-NBR-IN.             06210099
                                                                        06220099
           PERFORM IN400-LOOKUP-RETAIL.                                 06230099
                                                                        06240099
           IF IN400-PS-DUMMY-SKU                                        06250099
               ADD 1                TO PV-DUMMY-SKU-COUNT               06251099
           ELSE                                                         06350099
           IF IN400-PS-RTL-FOUND                                        06370099
               MOVE IN400-PRC-UNIT-RTL-OUT TO PV-UNT-RTL-AMT-HOLD       06390099
               COMPUTE PV-WORK-AMOUNT      = SUMINV-INV-UNIT-PR-AMT     06400099
                                           - PV-UNT-RTL-AMT-HOLD        06410099
               IF PV-WORK-AMOUNT IS GREATER THAN ZERO                   06420099
                   PERFORM 4000-MAKE-ADJUSTMENT                         06430099
               END-IF                                                   06440099
           ELSE                                                         06450099
           IF IN400-PV-INVALID-KEY-INPUT                                06451099
               DISPLAY '*** INVALID - ',                                06452099
                       ' SKU    :  ' SUMINV-SKU-NBR,                    06453099
                       ' UPC    :  ' SUMINV-UPC-NBR,                    06454099
                       ' STORE  :  ' SUMINV-LOC-NBR,                    06455099
                       ' AREA   :  ' SUMINV-AREA-NBR,                   06456099
                       ' PRC SQL - ', IN400-PV-PRC-SQLCDE,              06457099
                       ' ATT SQL - ', IN400-PV-ATT-SQLCDE,              06458099
                       '  ', IN400-PV-RETURN-RSN                        06459099
               DISPLAY IN400-PV-PARAGRAPH                               06459199
               ADD 1                       TO PV-NO-UNIT-COUNT          06459299
           ELSE                                                         06459399
               DISPLAY '*** NO PRICE - ',                               06460099
                       ' SKU    :  ' SUMINV-SKU-NBR,                    06470099
                       ' UPC    :  ' SUMINV-UPC-NBR,                    06480099
                       ' STORE  :  ' SUMINV-LOC-NBR,                    06490099
                       ' AREA   :  ' SUMINV-AREA-NBR,                   06500099
                       ' PRC SQL - ', IN400-PV-PRC-SQLCDE,              06510099
                       ' ATT SQL - ', IN400-PV-ATT-SQLCDE,              06520099
                       '  ', IN400-PV-RETURN-RSN                        06530099
               DISPLAY IN400-PV-PARAGRAPH                               06540099
               ADD 1            TO PV-NO-PRICE-COUNT                    06550099
           END-IF.                                                      06560099
                                                                        06570099
      ******************************************************************06580099
      * 4000-MAKE-ADJUSTMENT.                                           06590099
      ******************************************************************06600099
       4000-MAKE-ADJUSTMENT.                                            06610099
                                                                        06620099
           MOVE ZEROS                   TO PV-COUNT.                    06630099
                                                                        06640099
           MOVE SUMINV-LOC-NBR          TO ADJADD-LOC-NBR.              06650099
           MOVE SUMINV-SKU-NBR          TO ADJADD-SKU-NBR.              06660099
           MOVE SUMINV-UPC-NBR          TO ADJADD-UPC-NBR.              06670099
                                                                        06690099
           MOVE SUMINV-AREA-NBR         TO PV-EDIT-DOC-SEQ-NBR.         06691099
           MOVE SUMINV-LOC-NBR          TO PV-EDIT-DOC-PART1.           06692099
           MOVE PV-EDIT-DOC-LAST-6      TO PV-AREA-NBR.                 06693099
           MOVE PV-AREA-NBR             TO ADJADD-AREA-NBR.             06694099
                                                                        06695099
           PERFORM                                                      06700099
               WITH TEST AFTER                                          06710099
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       06720099
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                  06730099
                   OR (SQLCODE = 0)                                     06740099
                   OR (SQLCODE = +100)                                  06750099
                                                                        06760099
               EXEC SQL                                                 06770099
                   SELECT 1                                             06780099
                   INTO :PV-COUNT                                       06790099
                   FROM TADJADD                                         06800099
                   WHERE LOC_NBR = :ADJADD-LOC-NBR                      06810099
                     AND SKU_NBR = :ADJADD-SKU-NBR                      06820099
                     AND UPC_NBR = :ADJADD-UPC-NBR                      06830099
                     AND AREA_NBR = :ADJADD-AREA-NBR                    06840099
                   WITH UR                                              06850099
               END-EXEC                                                 06860099
                                                                        06870099
               EVALUATE TRUE                                            06880099
                   WHEN SQLCODE = 0                                     06890099
                     IF PV-COUNT IS GREATER THAN ZEROS                  06900099
                       PERFORM 6000-BUILD-ADJUST-TADJDTL                06910099
                     ELSE                                               06920099
                       PERFORM 6100-BUILD-ADJUST-TADJADD                06930099
                     END-IF                                             06940099
                   WHEN SQLCODE = -904                                  06950099
                   WHEN SQLCODE = -911                                  06960099
                       CONTINUE                                         06970099
                   WHEN SQLCODE = +100                                  06980099
                       PERFORM 6100-BUILD-ADJUST-TADJADD                06990099
                   WHEN OTHER                                           07000099
                       DISPLAY '***PARAGRAPH - 4000-MAKE-ADJUSTMENT ***'07010099
                       DISPLAY ' SKU    :  ' SUMINV-SKU-NBR             07020099
                               ' UPC    :  ' SUMINV-UPC-NBR             07030099
                               ' STORE  :  ' SUMINV-LOC-NBR             07040099
                               ' AREA   :  ' SUMINV-AREA-NBR            07050099
                       PERFORM 9900-SQL-ERROR                           07060099
               END-EVALUATE                                             07070099
           END-PERFORM.                                                 07080099
                                                                        07090099
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           07100099
               DISPLAY '*** MAX RETRIES - 4000-MAKE-ADJUSTMENT ***'     07110099
               DISPLAY ' SKU    :  ' SUMINV-SKU-NBR                     07120099
                       ' UPC    :  ' SUMINV-UPC-NBR                     07130099
                       ' STORE  :  ' SUMINV-LOC-NBR                     07140099
                       ' AREA   :  ' SUMINV-AREA-NBR                    07150099
               PERFORM 9900-SQL-ERROR                                   07160099
           END-IF.                                                      07170099
                                                                        07180099
      ******************************************************************07190099
      * 5000-FORMAT-DETAIL-LINE.                                        07200099
      ******************************************************************07210099
       5000-FORMAT-DETAIL-LINE.                                         07220099
                                                                        07230099
           IF PV-LINE-COUNT IS GREATER THAN PC-PRINT-LINE-MAX           07240099
               PERFORM 7000-PRINT-HEADINGS                              07250099
           END-IF.                                                      07260099
                                                                        07270099
           MOVE SUMINV-LOC-NBR        TO DL-LOC-NBR.                    07280099
                                                                        07290099
           MOVE SUMINV-AREA-NBR     TO PV-EDIT-DOC-SEQ-NBR.             07291099
           MOVE SUMINV-LOC-NBR      TO PV-EDIT-DOC-PART1.               07292099
           MOVE PV-EDIT-DOC-LAST-6  TO PV-AREA-NBR.                     07293099
           MOVE PV-AREA-NBR         TO DL-SHEET-NBR.                    07294099
                                                                        07295099
           MOVE IN400-ATT-DEPT-OUT    TO DL-DEPT-NBR.                   07310099
           MOVE IN400-ATT-SCL-OUT     TO DL-SUBCLASS-NBR.               07320099
                                                                        07330099
           MOVE SUMINV-SKU-NBR        TO DL-SKU-NBR.                    07340099
           MOVE SUMINV-UPC-NBR        TO DL-UPC-NBR.                    07341099
           MOVE SUMINV-INV-QTY        TO DL-UNIT-QTY                    07350099
           MOVE SUMINV-INV-UNIT-PR-AMT TO DL-UNIT-RTL-AMT.              07380099
           MOVE PV-UNT-RTL-AMT-HOLD    TO DL-TFR-RTL-AMT.               07390099
                                                                        07400099
           ADD SUMINV-INV-QTY       TO PA-ITEM-QTY.                     07410099
           ADD SUMINV-INV-QTY       TO PA-LOC-ITEM-QTY.                 07420099
           ADD SUMINV-INV-QTY       TO PA-TOT-ITEM-QTY.                 07430099
           ADD SUMINV-INV-QTY       TO PA-TFR-ITEM-QTY.                 07440099
           ADD SUMINV-INV-QTY       TO PA-LOC-TFR-ITEM-QTY.             07450099
           ADD SUMINV-INV-QTY       TO PA-TOT-TFR-ITEM-QTY.             07460099
                                                                        07470099
           COMPUTE PV-EXTD-AMOUNT   = ((SUMINV-INV-UNIT-PR-AMT          07560199
                                    - PV-UNT-RTL-AMT-HOLD)              07560399
                                    * -1).                              07560499
                                                                        07561099
           MOVE PV-EXTD-AMOUNT      TO DL-EXTD-TFR-RTL-AMT.             07570099
           ADD PV-EXTD-AMOUNT       TO PA-TFR-EXTD-RTL-AMT.             07580099
           ADD PV-EXTD-AMOUNT       TO PA-LOC-TFR-EXTD-RTL-AMT.         07590099
           ADD PV-EXTD-AMOUNT       TO PA-TOT-TFR-EXTD-RTL-AMT.         07600099
                                                                        07610099
           COMPUTE PV-EXTD-AMOUNT   = ((SUMINV-INV-UNIT-PR-AMT          07620099
                                    - PV-UNT-RTL-AMT-HOLD) * -1)        07640099
                                    * SUMINV-INV-QTY.                   07650099
           MOVE PV-EXTD-AMOUNT      TO DL-DIFF-RTL-AMT.                 07660099
                                                                        07670099
           ADD PV-EXTD-AMOUNT       TO PA-DFF-EXTD-RTL-AMT.             07680099
           ADD PV-EXTD-AMOUNT       TO PA-LOC-DFF-EXTD-RTL-AMT.         07690099
           ADD PV-EXTD-AMOUNT       TO PA-TOT-DFF-EXTD-RTL-AMT.         07700099
                                                                        07710099
           WRITE REPORT-LINE FROM DL-DETAIL-LINE AFTER ADVANCING 1 LINE.07720099
                                                                        07730099
           ADD 1                    TO PV-LINE-COUNT.                   07740099
                                                                        07750099
      ******************************************************************07760099
      * 5100-FORMAT-TSUMINV-TOTALS.                                     07770099
      ******************************************************************07780099
       5100-FORMAT-TSUMINV-TOTALS.                                      07790099
                                                                        07800099
             MOVE ZEROS               TO PA-ITEM-QTY.                   07810099
             MOVE ZEROS               TO PA-EXTD-RTL-AMT.               07820099
             MOVE ZEROS               TO PA-TFR-ITEM-QTY.               07830099
             MOVE ZEROS               TO PA-TFR-EXTD-RTL-AMT.           07840099
             MOVE ZEROS               TO PA-DFF-EXTD-RTL-AMT.           07850099
                                                                        07860099
      ******************************************************************07870099
      * 5200-FORMAT-STORE-TOTALS.                                       07880099
      ******************************************************************07890099
       5200-FORMAT-STORE-TOTALS.                                        07900099
                                                                        07910099
           IF PV-STR-QTY-COUNT NOT EQUAL ZEROS     OR                   07920099
              PV-STR-RTL-AMT-COUNT NOT EQUAL ZEROS                      07930099
                                                                        07940099
             IF PV-LINE-COUNT IS GREATER THAN (PC-PRINT-LINE-MAX - 2)   07950099
                 PERFORM 7000-PRINT-HEADINGS                            07960099
             END-IF                                                     07970099
                                                                        07980099
             MOVE '  STORE TOTAL   '      TO TL-VERBIAGE                07990099
             MOVE PA-LOC-ITEM-QTY         TO TL-UNIT-QTY                08000099
             MOVE PA-LOC-DFF-EXTD-RTL-AMT TO TL-DIFF-RTL-AMT            08030099
                                                                        08040099
             WRITE REPORT-LINE FROM TL-TOTAL-LINE                       08050099
                                      AFTER ADVANCING 2 LINE            08060099
                                                                        08070099
             ADD 2                    TO PV-LINE-COUNT                  08080099
                                                                        08090099
             MOVE ZEROS               TO PA-ITEM-QTY                    08100099
             MOVE ZEROS               TO PA-EXTD-RTL-AMT                08110099
             MOVE ZEROS               TO PA-TFR-ITEM-QTY                08120099
             MOVE ZEROS               TO PA-TFR-EXTD-RTL-AMT            08130099
             MOVE ZEROS               TO PA-DFF-EXTD-RTL-AMT            08140099
             MOVE ZEROS               TO PA-LOC-ITEM-QTY                08150099
             MOVE ZEROS               TO PA-LOC-EXTD-RTL-AMT            08160099
             MOVE ZEROS               TO PA-LOC-TFR-ITEM-QTY            08170099
             MOVE ZEROS               TO PA-LOC-TFR-EXTD-RTL-AMT        08180099
             MOVE ZEROS               TO PA-LOC-DFF-EXTD-RTL-AMT        08190099
                                                                        08200099
           END-IF.                                                      08210099
                                                                        08220099
      ******************************************************************08230099
      * 5300-FORMAT-GRAND-TOTALS.                                       08240099
      ******************************************************************08250099
       5300-FORMAT-GRAND-TOTALS.                                        08260099
                                                                        08270099
           IF PV-LINE-COUNT IS GREATER THAN (PC-PRINT-LINE-MAX - 2)     08280099
               PERFORM 7000-PRINT-HEADINGS                              08290099
           END-IF.                                                      08300099
                                                                        08310099
           MOVE 'TOTAL ADJUSTED  '      TO TL-VERBIAGE.                 08320099
           MOVE PA-TOT-ITEM-QTY         TO TL-UNIT-QTY.                 08330099
           MOVE PA-TOT-DFF-EXTD-RTL-AMT TO TL-DIFF-RTL-AMT.             08360099
                                                                        08370099
           WRITE REPORT-LINE FROM TL-TOTAL-LINE AFTER ADVANCING 2 LINES.08380099
                                                                        08390099
      ******************************************************************08400099
      * 5400-NOTHING-TO-REPORT.                                         08410099
      ******************************************************************08420099
       5400-NOTHING-TO-REPORT.                                          08430099
                                                                        08440099
           IF PV-LINE-COUNT IS GREATER THAN (PC-PRINT-LINE-MAX - 2)     08450099
               PERFORM 7000-PRINT-HEADINGS                              08460099
           END-IF.                                                      08470099
                                                                        08480099
           WRITE REPORT-LINE                                            08490099
               FROM NL-NO-REPORT-LINE AFTER ADVANCING 2 LINE.           08500099
                                                                        08510099
      /*****************************************************************08520099
      * 6000-BUILD-ADJUST-TADJDTL.                                      08530099
      ******************************************************************08540099
       6000-BUILD-ADJUST-TADJDTL.                                       08550099
                                                                        08560099
      *** FORMAT THE TSUMINV AS AN INVENTORY ADJUSTMENT TO TADJDTL...   08570099
           MOVE SUMINV-LOC-NBR TO ADJDTL-LOC-NBR.                       08580099
           MOVE 'EXCEPT RPT'        TO ADJDTL-ADJ-RSN-TXT.              08630099
                                                                        08680099
           MOVE SUMINV-AREA-NBR     TO PV-EDIT-DOC-SEQ-NBR.             08681099
           MOVE SUMINV-LOC-NBR      TO PV-EDIT-DOC-PART1.               08690099
           MOVE PV-EDIT-DOC-LAST-6  TO PV-AREA-NBR.                     08700099
           MOVE PV-AREA-NBR         TO ADJDTL-AREA-NBR.                 08710099
                                                                        08720099
           MOVE SUMINV-SKU-NBR      TO ADJDTL-SKU-NBR.                  08770099
           MOVE SUMINV-UPC-NBR      TO ADJDTL-UPC-NBR.                  08780099
                                                                        08790099
           MOVE PC-PROGRAM-NAME     TO ADJDTL-CRE-ID-NBR.               08810099
           MOVE 'SI'                TO ADJDTL-ADJ-TYPE-CDE.             08820099
                                                                        08869599
           COMPUTE PV-EXTD-AMOUNT   = ((SUMINV-INV-UNIT-PR-AMT          08869699
                                    - PV-UNT-RTL-AMT-HOLD)              08869799
                                    * -1).                              08869899
           MOVE PV-EXTD-AMOUNT      TO ADJDTL-ADJ-UNIT-PR-AMT.          08870099
                                                                        08880099
           MOVE ZEROS               TO ADJDTL-ADJ-QTY                   08890099
                                                                        08900099
           COMPUTE PV-EXTD-AMOUNT   = ((SUMINV-INV-UNIT-PR-AMT          08901099
                                    - PV-UNT-RTL-AMT-HOLD) * -1)        08902099
                                    * SUMINV-INV-QTY.                   08903099
           MOVE PV-EXTD-AMOUNT      TO ADJDTL-ADJ-EXTD-AMT.             08910099
                                                                        08920099
           MOVE SUMINV-DKEY-NBR    TO ADJDTL-DKEY-NBR.                  08930099
                                                                        08940099
      *** ADD THE DAILY TSUMINV ASSUMPT RECEIVE ADJUSTMENT TO TADJDTL.  08950099
           PERFORM 8200-INSERT-INTO-TADJDTL.                            08960099
           IF SQLCODE IS EQUAL TO -803                                  08970099
             PERFORM                                                    08980099
               UNTIL SQLCODE = 0                                        08990099
                  OR SQLCODE = +100                                     09000099
                                                                        09010099
                 MOVE ZEROS              TO SQLCODE                     09020099
                                                                        09030099
                 ADD 11                   TO PV-ADD-NUM                 09040099
                 ACCEPT PV-TODAYS-TIME    FROM TIME                     09050099
                 COMPUTE PV-TODAYS-TIME   = (PV-TODAYS-DATE             09060099
                                          + PV-ADD-NUM)                 09070099
                                          * 11                          09080099
                 MOVE PV-HUND-SEC         TO PV-SEED-BYTE-1-2           09090099
                 MOVE PV-SEC              TO PV-SEED-BYTE-3-4           09100099
                 MOVE PV-MIN              TO PV-SEED-BYTE-5-6           09110099
                 MOVE PV-HH               TO PV-SEED-BYTE-7-8           09120099
                 MOVE PV-INITIAL-SEED     TO PV-SEED                    09130099
                                                                        09140099
                 COMPUTE PV-WORK-FIELD = ((PV-SEED + PC-PRIME1)         09150099
                                       * PC-PRIME2)                     09160099
                                                                        09170099
                 DIVIDE PV-WORK-FIELD BY  PC-ONE-BILLION                09180099
                     GIVING PV-WORK-FIELD2                              09190099
                     REMAINDER PV-RANDOM-NUMBER                         09200099
                                                                        09210099
                 MOVE PV-RANDOM-NUMBER    TO ADJDTL-DKEY-NBR            09220099
                                                                        09230099
                 PERFORM 8200-INSERT-INTO-TADJDTL                       09240099
                                                                        09250099
             END-PERFORM                                                09260099
           END-IF.                                                      09270099
                                                                        09280099
      *** WRITE DETAIL REPORT INFORMATION....                           09290099
           PERFORM 5000-FORMAT-DETAIL-LINE.                             09300099
                                                                        09310099
           ADD ADJDTL-ADJ-QTY       TO PV-UNT-QTY-COUNT.                09320099
           ADD ADJDTL-ADJ-QTY       TO PV-TFR-QTY-COUNT.                09330099
           ADD ADJDTL-ADJ-QTY       TO PV-STR-QTY-COUNT.                09340099
           ADD ADJDTL-ADJ-QTY       TO PV-TOT-QTY-COUNT.                09350099
                                                                        09360099
           ADD PV-EXTD-AMOUNT       TO PV-UNT-RTL-AMT-COUNT.            09370099
           ADD PV-EXTD-AMOUNT       TO PV-TFR-RTL-AMT-COUNT.            09380099
           ADD PV-EXTD-AMOUNT       TO PV-STR-RTL-AMT-COUNT.            09390099
           ADD PV-EXTD-AMOUNT       TO PV-TOT-RTL-AMT-COUNT.            09400099
                                                                        09410099
      ******************************************************************09420099
      * 6100-BUILD-ADJUST-TADJADD.                                      09430099
      ******************************************************************09440099
       6100-BUILD-ADJUST-TADJADD.                                       09450099
                                                                        09460099
      *** FORMAT THE TSUMINV AS AN INVENTORY ADJUSTMENT TO TADJADD...   09470099
           MOVE SUMINV-LOC-NBR      TO ADJADD-LOC-NBR.                  09480099
           MOVE 'EXCEPT RPT'        TO ADJADD-ADJ-RSN-TXT.              09530099
                                                                        09580099
           MOVE SUMINV-AREA-NBR     TO PV-EDIT-DOC-SEQ-NBR.             09581099
           MOVE SUMINV-LOC-NBR      TO PV-EDIT-DOC-PART1.               09590099
           MOVE PV-EDIT-DOC-LAST-6  TO PV-AREA-NBR.                     09600099
           MOVE PV-AREA-NBR         TO ADJADD-AREA-NBR.                 09610099
                                                                        09620099
           MOVE SUMINV-SKU-NBR      TO ADJADD-SKU-NBR.                  09670099
           MOVE SUMINV-UPC-NBR      TO ADJADD-UPC-NBR.                  09680099
                                                                        09700099
           MOVE PC-PROGRAM-NAME     TO ADJADD-CHG-ID-NBR.               09710099
           MOVE 'N'                 TO ADJADD-MISSED-MD-IND             09720099
                                                                        09730099
           COMPUTE PV-EXTD-AMOUNT   = ((SUMINV-INV-UNIT-PR-AMT          09740099
                                    - PV-UNT-RTL-AMT-HOLD)              09750099
                                    * -1).                              09751099
           MOVE PV-EXTD-AMOUNT      TO ADJADD-ADJ-UNIT-PR-AMT.          09752099
                                                                        09753099
           MOVE ZEROS               TO ADJADD-ADJ-QTY                   09760099
                                                                        09910099
           COMPUTE PV-EXTD-AMOUNT   = ((SUMINV-INV-UNIT-PR-AMT          09918099
                                    - PV-UNT-RTL-AMT-HOLD) * -1)        09919099
                                    * SUMINV-INV-QTY.                   09919199
           MOVE PV-EXTD-AMOUNT      TO ADJADD-ADJ-EXTD-AMT.             09960099
                                                                        09970099
      *** GENERATE A RANDOM NUMBER FOR THE DKEY.....                    09980099
           ADD 11                   TO PV-ADD-NUM.                      09990099
           ACCEPT PV-TODAYS-TIME    FROM TIME.                          10000099
           COMPUTE PV-TODAYS-TIME   = (PV-TODAYS-DATE                   10010099
                                    + PV-ADD-NUM)                       10020099
                                    * 11.                               10030099
           MOVE PV-HUND-SEC         TO PV-SEED-BYTE-1-2.                10040099
           MOVE PV-SEC              TO PV-SEED-BYTE-3-4.                10050099
           MOVE PV-MIN              TO PV-SEED-BYTE-5-6.                10060099
           MOVE PV-HH               TO PV-SEED-BYTE-7-8.                10070099
           MOVE PV-INITIAL-SEED     TO PV-SEED.                         10080099
                                                                        10090099
           COMPUTE PV-WORK-FIELD = ((PV-SEED + PC-PRIME1)               10100099
                                 * PC-PRIME2).                          10110099
                                                                        10120099
           DIVIDE PV-WORK-FIELD BY  PC-ONE-BILLION                      10130099
               GIVING PV-WORK-FIELD2                                    10140099
               REMAINDER PV-RANDOM-NUMBER.                              10150099
                                                                        10160099
           MOVE PV-RANDOM-NUMBER    TO ADJADD-DKEY-NBR.                 10170099
                                                                        10180099
      *** ADD THE DAILY TSUMINV ASSUMPT RECEIVE ADJUSTMENT TO TADJADD.  10190099
           PERFORM 8700-INSERT-INTO-TADJADD.                            10200099
           IF SQLCODE IS EQUAL TO -803                                  10210099
             PERFORM                                                    10220099
               UNTIL SQLCODE = 0                                        10230099
                  OR SQLCODE = +100                                     10240099
                                                                        10250099
                 MOVE ZEROS              TO SQLCODE                     10260099
                                                                        10270099
                 ADD 11                   TO PV-ADD-NUM                 10280099
                 ACCEPT PV-TODAYS-TIME    FROM TIME                     10290099
                 COMPUTE PV-TODAYS-TIME   = (PV-TODAYS-DATE             10300099
                                          + PV-ADD-NUM)                 10310099
                                          * 11                          10320099
                 MOVE PV-HUND-SEC         TO PV-SEED-BYTE-1-2           10330099
                 MOVE PV-SEC              TO PV-SEED-BYTE-3-4           10340099
                 MOVE PV-MIN              TO PV-SEED-BYTE-5-6           10350099
                 MOVE PV-HH               TO PV-SEED-BYTE-7-8           10360099
                 MOVE PV-INITIAL-SEED     TO PV-SEED                    10370099
                                                                        10380099
                 COMPUTE PV-WORK-FIELD = ((PV-SEED + PC-PRIME1)         10390099
                                       * PC-PRIME2)                     10400099
                                                                        10410099
                 DIVIDE PV-WORK-FIELD BY  PC-ONE-BILLION                10420099
                     GIVING PV-WORK-FIELD2                              10430099
                     REMAINDER PV-RANDOM-NUMBER                         10440099
                                                                        10450099
                 MOVE PV-RANDOM-NUMBER    TO ADJADD-DKEY-NBR            10460099
                                                                        10470099
                 PERFORM 8700-INSERT-INTO-TADJADD                       10480099
                                                                        10490099
             END-PERFORM                                                10500099
           END-IF.                                                      10510099
                                                                        10520099
      *** WRITE DETAIL REPORT INFORMATION....                           10530099
           PERFORM 5000-FORMAT-DETAIL-LINE.                             10540099
                                                                        10550099
           ADD ADJADD-ADJ-QTY       TO PV-UNT-QTY-COUNT.                10560099
           ADD ADJADD-ADJ-QTY       TO PV-TFR-QTY-COUNT.                10570099
           ADD ADJADD-ADJ-QTY       TO PV-STR-QTY-COUNT.                10580099
           ADD ADJADD-ADJ-QTY       TO PV-TOT-QTY-COUNT.                10590099
                                                                        10600099
           ADD PV-EXTD-AMOUNT       TO PV-UNT-RTL-AMT-COUNT.            10610099
           ADD PV-EXTD-AMOUNT       TO PV-TFR-RTL-AMT-COUNT.            10620099
           ADD PV-EXTD-AMOUNT       TO PV-STR-RTL-AMT-COUNT.            10630099
           ADD PV-EXTD-AMOUNT       TO PV-TOT-RTL-AMT-COUNT.            10640099
                                                                        10650099
      ******************************************************************10660099
      * 7000-PRINT-HEADINGS.                                            10670099
      ******************************************************************10680099
       7000-PRINT-HEADINGS.                                             10690099
                                                                        10700099
           ADD 1                    TO PV-PAGE-COUNT.                   10710099
           MOVE PV-PAGE-COUNT       TO DP132O-PAGE-NUMBER.              10720099
                                                                        10730099
           WRITE REPORT-LINE                                            10740099
             FROM DP132O-STANDRD-HEADER-132-COLS AFTER ADVANCING PAGE.  10750099
                                                                        10760099
           WRITE REPORT-LINE                                            10770099
             FROM HL1-HEADING-LINE-1 AFTER ADVANCING 1 LINE.            10780099
                                                                        10790099
           WRITE REPORT-LINE                                            10800099
             FROM HL2-HEADING-LINE-2 AFTER ADVANCING 2 LINE.            10810099
                                                                        10820099
           WRITE REPORT-LINE                                            10830099
             FROM HL3-HEADING-LINE-3 AFTER ADVANCING 1 LINE.            10840099
                                                                        10850099
           MOVE 4                   TO PV-LINE-COUNT.                   10860099
                                                                        10870099
      ******************************************************************11200099
      * 8000-OPEN-TSUMINV.                                              11210099
      ******************************************************************11220099
       8000-OPEN-TSUMINV.                                               11230099
                                                                        11240099
           EXEC SQL                                                     11240199
               OPEN TSUMINV_CURSOR                                      11240299
           END-EXEC.                                                    11240399
                                                                        11240499
           EVALUATE TRUE                                                11240599
             WHEN SQLCODE IS EQUAL TO ZERO                              11240699
                 CONTINUE                                               11240799
             WHEN SQLWARN IS NOT EQUAL TO SPACES                        11240899
             WHEN SQLCODE IS NOT EQUAL TO ZERO                          11240999
                 MOVE '8000-OPEN' TO PV-PARAGRAPH-NAME                  11241099
                 MOVE 'OPEN CURSOR' TO PV-OPER-ATTEMPTED                11241199
                 PERFORM 9900-SQL-ERROR                                 11241299
           END-EVALUATE.                                                11241399
                                                                        11241499
      ******************************************************************11241599
      * 8100-CLOSE-TSUMINV.                                             11241699
      ******************************************************************11241799
       8100-CLOSE-TSUMINV.                                              11241899
                                                                        11241999
           EXEC SQL                                                     11242099
               CLOSE TSUMINV_CURSOR                                     11242199
           END-EXEC.                                                    11242299
                                                                        11242399
           EVALUATE TRUE                                                11242499
             WHEN SQLCODE IS EQUAL TO ZERO                              11242599
               CONTINUE                                                 11242699
             WHEN SQLWARN IS NOT EQUAL TO SPACES                        11242799
             WHEN SQLCODE IS NOT EQUAL TO ZERO                          11242899
              MOVE '8100-CLOSE' TO PV-PARAGRAPH-NAME                    11242999
              MOVE 'CLOSE CURSOR' TO PV-OPER-ATTEMPTED                  11243099
              PERFORM 9900-SQL-ERROR                                    11243199
           END-EVALUATE.                                                11243299
                                                                        11243399
      ******************************************************************11243499
      * 8200-INSERT-INTO-TADJDTL.                                       11243599
      ******************************************************************11243699
       8200-INSERT-INTO-TADJDTL.                                        11244099
                                                                        11245099
           PERFORM WITH TEST AFTER                                      11250099
              VARYING PV-RETRY-COUNT FROM 1 BY 1                        11260099
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                  11270099
                   OR (SQLCODE = 0)                                     11280099
                   OR (SQLCODE = +100)                                  11290099
                   OR (SQLCODE = -803)                                  11300099
                                                                        11310099
             EXEC SQL                                                   11320099
               INSERT INTO TADJDTL                                      11330099
                    ( LOC_NBR                                           11340099
                     ,AREA_NBR                                          11350099
                     ,SKU_NBR                                           11360099
                     ,UPC_NBR                                           11370099
                     ,CRE_TMST                                          11380099
                     ,ADJ_RSN_TXT                                       11390099
                     ,ADJ_TYPE_CDE                                      11400099
                     ,ADJ_UNIT_PR_AMT                                   11410099
                     ,ADJ_QTY                                           11420099
                     ,ADJ_EXTD_AMT                                      11430099
                     ,DKEY_NBR                                          11440099
                     ,CRE_ID_NBR )                                      11450099
               VALUES                                                   11460099
                   (:ADJDTL-LOC-NBR                                     11470099
                   ,:ADJDTL-AREA-NBR                                    11480099
                   ,:ADJDTL-SKU-NBR                                     11490099
                   ,:ADJDTL-UPC-NBR                                     11500099
                   ,CURRENT TIMESTAMP                                   11510099
                   ,:ADJDTL-ADJ-RSN-TXT                                 11520099
                   ,:ADJDTL-ADJ-TYPE-CDE                                11530099
                   ,:ADJDTL-ADJ-UNIT-PR-AMT                             11540099
                   ,:ADJDTL-ADJ-QTY                                     11550099
                   ,:ADJDTL-ADJ-EXTD-AMT                                11560099
                   ,:ADJDTL-DKEY-NBR                                    11570099
                   ,:ADJDTL-CRE-ID-NBR)                                 11580099
             END-EXEC                                                   11590099
                                                                        11600099
             EVALUATE TRUE                                              11610099
               WHEN SQLCODE = ZERO                                      11620099
                   ADD 1            TO PV-RECORDS-ACCEPTED              11630099
                   ADD 1            TO PA-INSERT-COUNT                  11640099
               WHEN SQLCODE = -803                                      11650099
                   ADD 1            TO PA-DUPLICATE-REC                 11660099
               WHEN SQLCODE = -904                                      11670099
               WHEN SQLCODE = -911                                      11680099
                   CONTINUE                                             11690099
               WHEN OTHER                                               11700099
                       DISPLAY '***PARAGRAPH - 8200-INSERT-INTO ***'    11710099
                       DISPLAY ' SKU    :  ' SUMINV-SKU-NBR             11711099
                               ' UPC    :  ' SUMINV-UPC-NBR             11712099
                               ' STORE  :  ' SUMINV-LOC-NBR             11713099
                               ' AREA   :  ' SUMINV-AREA-NBR            11714099
                       PERFORM 9900-SQL-ERROR                           11770099
               END-EVALUATE                                             11780099
           END-PERFORM.                                                 11790099
                                                                        11800099
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          11810099
               DISPLAY '*** MAX RETRIES - 8200-INSERT-INTO ***'         11820099
               DISPLAY ' SKU    :  ' SUMINV-SKU-NBR                     11821099
                       ' UPC    :  ' SUMINV-UPC-NBR                     11822099
                       ' STORE  :  ' SUMINV-LOC-NBR                     11823099
                       ' AREA   :  ' SUMINV-AREA-NBR                    11824099
               PERFORM 9900-SQL-ERROR                                   11880099
           END-IF.                                                      11890099
                                                                        11900099
      ******************************************************************11910099
      * 8300-READ-TINVPAR.                                              11920099
      ******************************************************************11930099
       8300-READ-TINVPAR.                                               11940099
                                                                        11950099
           MOVE SUMINV-LOC-NBR TO INVPAR-LOC-NBR.                       11960099
                                                                        11970099
           PERFORM                                                      11980099
               WITH TEST AFTER                                          11990099
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       12000099
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                  12010099
                   OR (SQLCODE = 0)                                     12020099
                   OR (SQLCODE = +100)                                  12030099
                                                                        12040099
               EXEC SQL                                                 12050099
                   SELECT PLND_INV_DTE                                  12060099
                         ,ACTL_UNIT_BK_DTE                              12070099
                   INTO :INVPAR-PLND-INV-DTE                            12080099
                       ,:INVPAR-ACTL-UNIT-BK-DTE                        12090099
P28545             FROM TINVPAR A,                                      12100099
P28545                  TINCNTL B                                       12101099
P28545             WHERE B.ACTV_IND = 'Y'                               12102099
P28545               AND A.INV_ID = B.INV_ID                            12103099
P28545               AND LOC_NBR = :INVPAR-LOC-NBR                      12110099
                     AND ACTL_FIN_BK_DTE = '9999-09-09'                 12120099
                   WITH UR                                              12130099
               END-EXEC                                                 12140099
                                                                        12150099
               EVALUATE TRUE                                            12160099
               WHEN SQLCODE = 0                                         12170099
                   MOVE INVPAR-PLND-INV-DTE   TO PV-PLND-INV-DTE        12180099
                   MOVE PV-PLND-INV-TIMESTAMP TO PV-PLND-INV-TMST       12190099
               WHEN SQLCODE = -913                                      12200099
               WHEN SQLCODE = -904                                      12210099
               WHEN SQLCODE = -911                                      12220099
               WHEN SQLCODE = +100                                      12230099
                   DISPLAY '** PARAGRAPH - 8300-READ-TINVPAR **'        12240099
                   DISPLAY '** ERROR - STORE NOT IN AN INVENTORY'       12250099
                           ' STATUS **'                                 12260099
                   DISPLAY ' STORE  :     ' SUMINV-LOC-NBR              12270099
                   PERFORM 9900-SQL-ERROR                               12280099
               WHEN SQLWARN NOT = SPACE                                 12290099
               WHEN SQLCODE NOT = ZERO                                  12300099
                   DISPLAY '** PARAGRAPH - 8300-READ-TINVPAR **'        12310099
                   DISPLAY ' STORE  :     ' SUMINV-LOC-NBR              12320099
                   PERFORM 9900-SQL-ERROR                               12330099
               END-EVALUATE                                             12340099
           END-PERFORM.                                                 12350099
                                                                        12360099
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          12370099
               DISPLAY '** MAX RETRIES - 8300-READ-TINVPAR **'          12380099
               DISPLAY ' STORE  :     ' SUMINV-LOC-NBR                  12390099
               PERFORM 9900-SQL-ERROR                                   12400099
           END-IF.                                                      12410099
                                                                        12420099
      ******************************************************************12421099
      * 8400-FETCH-TSUMINV.                                             12422099
      ******************************************************************12423099
       8400-FETCH-TSUMINV.                                              12424099
                                                                        12425099
           EXEC SQL                                                     12426099
               FETCH TSUMINV_CURSOR                                     12427099
               INTO :SUMINV-LOC-NBR                                     12428099
                   ,:SUMINV-AREA-NBR                                    12429099
                   ,:SUMINV-SKU-NBR                                     12429199
                   ,:SUMINV-UPC-NBR                                     12429299
                   ,:SUMINV-INV-UNIT-PR-AMT                             12429399
                   ,:SUMINV-INV-QTY                                     12429499
                   ,:SUMINV-MISSED-MD-IND                               12429599
                   ,:SUMINV-DKEY-NBR                                    12429699
                   ,:SUMINV-CHG-TMST                                    12429799
                   ,:SUMINV-CHG-ID-NBR                                  12429899
                   ,:SUMINV-EXTD-AMT                                    12429999
                   ,:SUMINV-DEPT-NBR                                    12430099
                   ,:SUMINV-MAJ-CL-NBR                                  12430199
                   ,:SUMINV-SUB-CL-NBR                                  12430299
           END-EXEC.                                                    12430399
                                                                        12430499
           EVALUATE TRUE                                                12430599
             WHEN SQLCODE IS EQUAL TO ZERO                              12430699
                 CONTINUE                                               12430799
             WHEN SQLCODE IS EQUAL TO +100                              12433399
                 MOVE 'Y'      TO PS-END-OF-TSUMINV-DATA                12433499
             WHEN SQLWARN IS NOT EQUAL TO SPACES                        12433599
             WHEN SQLCODE IS NOT EQUAL TO ZERO                          12433699
                 MOVE '8400-FETCH' TO PV-PARAGRAPH-NAME                 12433799
                 MOVE 'FETCH TSUMINV' TO PV-OPER-ATTEMPTED              12433899
                 MOVE 'Y'      TO PS-END-OF-TSUMINV-DATA                12433999
                 PERFORM 9900-SQL-ERROR                                 12434099
           END-EVALUATE.                                                12434199
                                                                        12434299
      ******************************************************************12434399
      * 8500-READ-NEXT-LOCATION.                                        12434499
      ******************************************************************12434599
       8500-READ-NEXT-LOCATION.                                         12434699
                                                                        12434799
           READ LOCATION-CONTROL-FILE NEXT                              12434899
               AT END  MOVE 'Y'     TO PS-END-OF-FILE-SW                12434999
           END-READ.                                                    12435099
                                                                        12435199
      ******************************************************************12436099
      * 8700-INSERT-INTO-TADJADD.                                       12440099
      ******************************************************************12450099
       8700-INSERT-INTO-TADJADD.                                        12460099
                                                                        12470099
           PERFORM WITH TEST AFTER                                      12480099
              VARYING PV-RETRY-COUNT FROM 1 BY 1                        12490099
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                  12500099
                   OR (SQLCODE = 0)                                     12510099
                   OR (SQLCODE = +100)                                  12520099
                   OR (SQLCODE = -803)                                  12530099
                                                                        12540099
             EXEC SQL                                                   12550099
               INSERT INTO TADJADD                                      12560099
                         (LOC_NBR                                       12570099
                        , AREA_NBR                                      12580099
                        , SKU_NBR                                       12590099
                        , UPC_NBR                                       12600099
                        , ADJ_UNIT_PR_AMT                               12610099
                        , ADJ_QTY                                       12620099
                        , MISSED_MD_IND                                 12630099
                        , DKEY_NBR                                      12640099
                        , CHG_TMST                                      12650099
                        , CHG_ID_NBR                                    12660099
                        , ADJ_RSN_TXT                                   12670099
                        , ADJ_EXTD_AMT)                                 12680099
                   VALUES                                               12690099
                         (:ADJADD-LOC-NBR                               12700099
                        , :ADJADD-AREA-NBR                              12710099
                        , :ADJADD-SKU-NBR                               12720099
                        , :ADJADD-UPC-NBR                               12730099
                        , :ADJADD-ADJ-UNIT-PR-AMT                       12740099
                        , :ADJADD-ADJ-QTY                               12750099
                        , :ADJADD-MISSED-MD-IND                         12760099
                        , :ADJADD-DKEY-NBR                              12770099
                        ,  CURRENT TIMESTAMP                            12780099
                        , :ADJADD-CHG-ID-NBR                            12790099
                        , :ADJADD-ADJ-RSN-TXT                           12800099
                        , :ADJADD-ADJ-EXTD-AMT)                         12810099
             END-EXEC                                                   12820099
                                                                        12830099
             EVALUATE TRUE                                              12840099
               WHEN SQLCODE = ZERO                                      12850099
                   ADD 1            TO PV-RECORDS-ACCEPTED              12860099
                   ADD 1            TO PA-INSERT-COUNT                  12870099
               WHEN SQLCODE = -803                                      12880099
                   ADD 1            TO PA-DUPLICATE-REC                 12890099
               WHEN SQLCODE = -904                                      12900099
               WHEN SQLCODE = -911                                      12910099
                   CONTINUE                                             12920099
               WHEN OTHER                                               12930099
                   DISPLAY '***PARAGRAPH - 8700-INSERT-INTO ***'        12940099
                   DISPLAY ' SKU    :  ' SUMINV-SKU-NBR                 12941099
                           ' UPC    :  ' SUMINV-UPC-NBR                 12942099
                           ' STORE  :  ' SUMINV-LOC-NBR                 12943099
                           ' AREA   :  ' SUMINV-AREA-NBR                12944099
                   PERFORM 9900-SQL-ERROR                               13000099
               END-EVALUATE                                             13010099
           END-PERFORM.                                                 13020099
                                                                        13030099
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          13040099
               DISPLAY '*** MAX RETRIES - 8700-INSERT-INTO ***'         13050099
               DISPLAY ' SKU    :  ' SUMINV-SKU-NBR                     13060099
                       ' UPC    :  ' SUMINV-UPC-NBR                     13070099
                       ' STORE  :  ' SUMINV-LOC-NBR                     13080099
                       ' AREA   :  ' SUMINV-AREA-NBR                    13090099
               PERFORM 9900-SQL-ERROR                                   13110099
           END-IF.                                                      13120099
                                                                        13130099
      ******************************************************************13140099
      * 9000-END-OF-JOB-TASKS.                                          13150099
      ******************************************************************13160099
       9000-END-OF-JOB-TASKS.                                           13170099
                                                                        13180099
      *** STORE TOTALS.....                                             13190099
           IF PV-TOT-QTY-COUNT IS EQUAL TO ZEROS     AND                13200099
              PV-TOT-RTL-AMT-COUNT IS EQUAL TO ZEROS                    13210099
               PERFORM 5400-NOTHING-TO-REPORT                           13220099
           ELSE                                                         13230099
               PERFORM 5100-FORMAT-TSUMINV-TOTALS                       13240099
               PERFORM 5200-FORMAT-STORE-TOTALS                         13250099
               PERFORM 5300-FORMAT-GRAND-TOTALS                         13260099
           END-IF.                                                      13270099
                                                                        13280099
      *** FINAL TOTALS.....                                             13290099
           DISPLAY '***********************'.                           13300099
           DISPLAY '* ' PC-PROGRAM-NAME ' SUMMARY    *'.                13310099
           DISPLAY '***********************'.                           13320099
           DISPLAY ' '.                                                 13330099
           DISPLAY ' LOCATIONS PROCESSD: ' PV-RECORDS-READ.             13340099
           DISPLAY ' '.                                                 13340199
           DISPLAY ' RECORDS READ:       ' PA-EXTRACT-RECS-READ.        13341099
           DISPLAY '   NO UNIT COUNT:      ' PV-NO-UNIT-COUNT.          13350099
           DISPLAY '   NO PRICE COUNT:     ' PV-NO-PRICE-COUNT.         13360099
           DISPLAY '   DUMMY SKU:          ' PV-DUMMY-SKU-COUNT.        13370099
           DISPLAY ' RECORDS ACCEPTED:   ' PV-RECORDS-ACCEPTED.         13380099
           DISPLAY ' '.                                                 13430099
           DISPLAY '***********************'.                           13440099
           DISPLAY '*** TABLE COMMIT STATISTICS      ***'.              13450099
           DISPLAY '                                             '.     13460099
           DISPLAY 'INPUT EXTRACT RECORDS READ ' PA-EXTRACT-RECS-READ.  13470099
           DISPLAY '  ROWS UPDATED   ', PA-UPDATE-COUNT.                13480099
           DISPLAY '  ROWS INSERTED  ', PA-INSERT-COUNT.                13490099
           DISPLAY '  DROP(S)        ', PA-ZERO-REC-DROPPED.            13500099
           DISPLAY '  DUPLICATE(S)   ', PA-DUPLICATE-REC.               13510099
           DISPLAY '                                             '.     13520099
           DISPLAY '  COMMITS TAKEN  ', PA-COMMIT-COUNT.                13530099
           DISPLAY '                                             '.     13540099
           DISPLAY '***********************'.                           13550099
                                                                        13560099
           CLOSE LOCATION-CONTROL-FILE                                  13720099
                 TSUMINV-REPORT.                                        13721099
                                                                        13730099
      ******************************************************************13740099
      * 9900-SQL-ERROR.                                                 13750099
      ******************************************************************13760099
       9900-SQL-ERROR.                                                  13770099
                                                                        13780099
           SET ABEND-4013            TO TRUE.                           13790099
           DISPLAY '**  ABEND     **'.                                  13800099
           DISPLAY '**  PROGRAM   **  =  ' PC-PROGRAM-NAME              13810099
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED            13820099
                                                                        13830099
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         13840099
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        13850099
                                                                        13860099
           COPY DPPD004.                                                13870099
                                                                        13880099
           PERFORM 9990-ABEND.                                          13890099
                                                                        13900099
       9990-ABEND.                                                      13910099
                                                                        13920099
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              13930099
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            13940099
           CALL 'ILBOABN0'  USING ABEND-CODE.                           13950099
                                                                        13960099
      ******************************************************************13970099
      *  PRICING MODULE.                                                13980099
      ******************************************************************13990099
           COPY INPD400.                                                14000099
                                                                        14010099
