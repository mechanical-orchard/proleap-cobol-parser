       IDENTIFICATION DIVISION.                                         00010012
       PROGRAM-ID.    MLKUP342                                          00020012
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030012
       DATE-WRITTEN.  09-01-2000.                                       00040012
       DATE-COMPILED.                                                   00050012
                                                                        00060012
      ******************************************************************00070012
      *PURPOSE: LEDGER DATABASE UPDATE PROGRAM FOR MONTH DEPARTMENT     00080012
      *         CHANNEL TABLE, TWKDPTC....                              00090012
      *              (THIS PROGRAM WAS MODELED AFTER MLKUP341)          00100012
      *                                                                 00110012
      *CHANGE HISTORY:                                                  00120012
      * WR/PROJ DATE       PROGRAMMER   DESCRIPTION                     00130012
      * ------- ---------- ------------ --------------------------      00140012
23565 * 23565   09-10-2003 ROD JANSSEN  DETAILED PURCH ADJUSTMENTS ENH. 00150012
25554 * 25554   12-24-2003 PAUL KEBER   MARKDOWN REFINEMENT FOR PLANNING00160012
W26603******************************************************************00170012
W26603* CHG ID   DATE        DESCRIPTION                               *00180012
W26603* ------   ----------  ----------------------------------------- *00190012
W26603* W26603   04-19-2004  STORE EXPANSION.                          *00200012
W26603*                      EXPANDED WORKING STORAGE VARIABLES.       *00210012
W26603*                      ADDED 'WITH UR' TO DB2 SELECT.            *00220012
W26603*                      - RONNA MCDONALD                          *00230012
W27511* W27511   12-28-2004  BROADCAST OPTIMIZATION SPLIT PLANNING     *00240012
W27511*                      - J LINDEMANN                             *00250012
W28630* W28630   04-15-2005  LICENSE FEE SPLIT PLANNING                *00260012
W28630*                      - S ASEN                                  *00270012
      ******************************************************************00280012
                                                                        00290012
       ENVIRONMENT DIVISION.                                            00300012
       CONFIGURATION SECTION.                                           00310012
       SOURCE-COMPUTER.        IBM-3090.                                00320012
       OBJECT-COMPUTER.        IBM-3090.                                00330012
       INPUT-OUTPUT SECTION.                                            00340012
       FILE-CONTROL.                                                    00350012
           SELECT CONDENSED-FILE                                        00360012
               ASSIGN TO INP01.                                         00370012
                                                                        00380012
       DATA DIVISION.                                                   00390012
       FILE SECTION.                                                    00400012
                                                                        00410012
       FD  CONDENSED-FILE                                               00420012
           LABEL RECORDS ARE STANDARD                                   00430012
           RECORDING MODE IS F                                          00440012
           BLOCK CONTAINS 0 RECORDS.                                    00450012
25554  01  CONDENSED-RECORD            PIC X(257).                      00460012
                                                                        00470012
      ******************************************************************00480012
       WORKING-STORAGE SECTION.                                         00490012
      ******************************************************************00500012
                                                                        00510012
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00520012
                                                                        00530012
       01  PC-PROGRAM-CONSTANTS.                                        00540012
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK391'.    00550012
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP342'.  00560012
           05  PC-MAX-RETRIES          PIC S9(03) VALUE +3.             00570012
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00580012
                                                                        00590012
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00600012
           05  PE-MSG-01                       PIC X(50) VALUE          00610012
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED   '.    00620012
           05  PE-MSG-02                       PIC X(50) VALUE          00630012
                'ATTEMPT TO UPDATE ROW ON TABLE TWKDPTC FAILED     '.   00640012
           05  PE-MSG-03                       PIC X(50) VALUE          00650012
                'ATTEMPT TO INSERT ROW ON TABLE TWKDPTC FAILED     '.   00660012
           05  PE-MSG-04                       PIC X(50) VALUE          00670012
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00680012
           05  PE-MSG-05                       PIC X(50) VALUE          00690012
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00700012
           05  PE-MSG-06                       PIC X(50) VALUE          00710012
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00720012
           05  PE-MSG-07                       PIC X(50) VALUE          00730012
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00740012
           05  PE-MSG-08                       PIC X(50) VALUE          00750012
                'SELECT OF CHECKPOINT RESTART INFO FROM TCKRSTINF'.     00760012
           05  PE-MSG-09                       PIC X(50) VALUE          00770012
                'SET HOLD TIMESTAMP EQUAL TO CURRENT TIMESTAMP  '.      00780012
           05  PE-MSG-10                       PIC X(50) VALUE          00790012
                'UPDATE OF STATUS CODE ON TCKRSTCNTL UNSUCCESSFUL'.     00800012
           05  PE-MSG-11                       PIC X(50) VALUE          00810012
                'ATTEMPT TO SELECT TIMESTAMP + CKPT_FRQNCY_QTY   '.     00820012
      *                                                                 00830012
       01  PS-PROGRAM-SWITCHES.                                         00840012
           05  PS-CONDENSED-FILE-EOF-SW     PIC  X        VALUE 'N'.    00850012
               88  PS-MORE-INPUT-RECORDS                  VALUE 'N'.    00860012
               88  PS-NO-MORE-INPUT-RECORDS               VALUE 'Y'.    00870012
           05  PS-EMPTY-FILE-SW             PIC  X        VALUE 'N'.    00880012
               88  PS-EMPTY-INPUT-FILE                    VALUE 'Y'.    00890012
           05  PS-MNDPTC-CURSOR-EOF-SW      PIC  X        VALUE 'N'.    00900012
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00910012
           05  PS-FIRST-COMMIT-SW           PIC X         VALUE 'N'.    00920012
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00930012
           05  PS-DEADLOCK-RESTART-SW       PIC X         VALUE 'N'.    00940012
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00950012
           05  PS-PROCESSING-OPTIONS        PIC X         VALUE 'P'.    00960012
               88  PS-UPDATE-WKDPTC-ROW                   VALUE 'U'.    00970012
               88  PS-INSERT-WKDPTC-ROW                   VALUE 'I'.    00980012
      *                                                                 00990012
       01  PROGRAM-VARIABLES.                                           01000012
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     01010012
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01020012
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01030012
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01040012
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01050012
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01060012
      *                                                                 01070012
       01  PA-PROGRAM-ACCUMULATORS.                                     01080012
           05  PA-RECORD-COUNTS.                                        01090012
               10  PA-INPUT-COUNT              PIC  9(9)  VALUE ZEROES. 01100012
               10  PA-UPDATE-COUNT             PIC  9(9)  VALUE ZEROES. 01110012
               10  PA-INSERT-COUNT             PIC  9(9)  VALUE ZEROES. 01120012
               10  PA-DEPTS-PROCESSED          PIC  9(9)  VALUE ZEROES. 01130012
               10  PA-COMMIT-COUNT             PIC  9(9)  VALUE ZEROES. 01140012
      *                                                                 01150012
       01  SAVE-AREA.                                                   01160012
           05  SV-PRIMARY-KEY.                                          01170012
               10  SV-FSCL-WK-NBR        PIC X(02)         VALUE SPACES.01180012
               10  SV-FSCL-WK-END-DTE    PIC X(10)         VALUE SPACES.01190012
               10  SV-DEPT-NBR           PIC X(03)         VALUE SPACES.01200012
               10  SV-CHNL-CDE           PIC X(01)         VALUE SPACES.01210012
           05  SV-UPDATE-FIELDS.                                        01220012
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01230012
W26603         10  SV-MKDN-PLU-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01240012
W26603         10  SV-MKDN-RGST-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01250012
W26603         10  SV-MKDN-UMTCH-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01260012
W26603         10  SV-MKDN-BYR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01270012
W26603         10  SV-MKDN-STR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01280012
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01290012
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01300012
W26603         10  SV-XFER-OUT-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01310012
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01320012
W26603         10  SV-RCPT-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01330012
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01340012
W26603         10  SV-PADJ-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01350012
W26603         10  SV-PADJ-FRGT-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01360012
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01370012
W26603         10  SV-SHNK-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01380012
W26603         10  SV-END-INV-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01390012
               10  SV-CUM-MKUP-PCT       PIC S9(4)V9(7) COMP-3 VALUE +0.01400012
W26603         10  SV-END-INV-COST-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01410012
W26603         10  SV-SLS-COST-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01420012
W26603         10  SV-GRO-MRGN-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01430012
W26603         10  SV-PADJ-RTV-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01440012
W26603         10  SV-PADJ-RTV-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01450012
W26603         10  SV-PADJ-DA-COST-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01460012
W26603         10  SV-PADJ-DA-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01470012
W26603         10  SV-PADJ-OTH-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01480012
W26603         10  SV-PADJ-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01490012
W26603         10  SV-MKDN-PROMO-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01500012
W26603         10  SV-MKDN-PRPT-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01510012
W26603         10  SV-MKDN-GLDST-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01520012
W26603         10  SV-MKDN-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01530012
W27511         10  SV-PADJ-BROP-COST-AMT                                01540012
W27511                                   PIC S9(11)V99  COMP-3 VALUE +0.01550012
W28630         10  SV-LIC-FEE-COST-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01560012
W30578         10  SV-MKDN-EMP-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01570013
      *                                                                 01580012
      ******************************************************************01590012
      * WEEKLY TRANSACTION UPDATE FOR TWKDPTC ARGUMENTS....             01600012
      ******************************************************************01610012
23565      COPY MLRD148.                                                01620012
      *                                                                 01630012
      ******************************************************************01640012
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01650012
      ******************************************************************01660012
           COPY DPWS004.                                                01670012
      *                                                                 01680012
      ******************************************************************01690012
      *  USED FOR DB2 ERRORS                                            01700012
      ******************************************************************01710012
           EXEC SQL                                                     01720012
               INCLUDE SQLCA                                            01730012
           END-EXEC.                                                    01740012
      *                                                                 01750012
      ******************************************************************01760012
      *  M/L WEEKLY DEPARTMENT/CHANNEL TABLE                            01770012
      ******************************************************************01780012
           EXEC SQL                                                     01790012
               INCLUDE TWKDPTC                                          01800012
           END-EXEC.                                                    01810012
      *                                                                 01820012
      ***************************************************************** 01830012
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01840012
      ***************************************************************** 01850012
           EXEC SQL                                                     01860012
               INCLUDE TCKRSTCN                                         01870012
           END-EXEC.                                                    01880012
      *                                                                 01890012
           EXEC SQL                                                     01900012
               INCLUDE TCKRSTIN                                         01910012
           END-EXEC.                                                    01920012
      *                                                                 01930012
      ******************************************************************01940012
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01950012
      ******************************************************************01960012
       01  CR-CHECKPOINT-RESTART-AREA.                                  01970012
           05  CR-AREA-LEN                  PIC S9(04) VALUE +62  COMP. 01980012
           05  CR-CHECKPOINT-RESTART-VAR.                               01990012
               10  CR-PREV-DTL-KEY          PIC  X(16) VALUE SPACES.    02000012
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    02010012
                   15  CR-FSCL-WK-NBR       PIC  X(02).                 02020012
                   15  CR-FSCL-WK-END-DTE   PIC  X(10).                 02030012
                   15  CR-DEPT-NBR          PIC  X(03).                 02040012
                   15  CR-CHNL-CDE          PIC  X(01).                 02050012
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    02060012
               10  CR-UPDATE-COUNT          PIC  9(09) VALUE ZEROES.    02070012
               10  CR-INSERT-COUNT          PIC  9(09) VALUE ZEROES.    02080012
               10  CR-DEPTS-PROCESSED       PIC  9(09) VALUE ZEROES.    02090012
               10  CR-TWKDPTC-COMMIT-COUNT  PIC  9(09) VALUE ZEROES.    02100012
      *                                                                 02110012
       01  CK-CHECKPOINT-SAVE-AREA.                                     02120012
           05  CK-SAVE-PREV-KEY         PIC  X(16) VALUE SPACES.        02130012
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       02140012
               10  CK-FSCL-WK-NBR       PIC  X(02).                     02150012
               10  CK-FSCL-WK-END-DTE   PIC  X(10).                     02160012
               10  CK-DEPT-NBR          PIC  X(03).                     02170012
               10  CK-CHNL-CDE          PIC  X(01).                     02180012
                                                                        02190012
      ***************************************************************** 02200012
       PROCEDURE DIVISION.                                              02210012
      ***************************************************************** 02220012
                                                                        02230012
       0000-MAIN-MODULE.                                                02240012
                                                                        02250012
           PERFORM 1000-INITIALIZATION.                                 02260012
                                                                        02270012
           PERFORM 2000-PROCESS-INPUT                                   02280012
                UNTIL PS-NO-MORE-INPUT-RECORDS.                         02290012
                                                                        02300012
           PERFORM 8000-EOJ-ROUTINE.                                    02310012
                                                                        02320012
           STOP RUN.                                                    02330012
                                                                        02340012
      ******************************************************************02350012
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02360012
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02370012
      *  TCKRSTCNTL-CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION  02380012
      ******************************************************************02390012
      *                                                                 02400012
       1000-INITIALIZATION.                                             02410012
      *                                                                 02420012
           OPEN INPUT CONDENSED-FILE.                                   02430012
      *                                                                 02440012
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02450012
      *                                                                 02460012
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02470012
                PERFORM 7500-SELECT-RESTART-INFO                        02480012
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                   02490012
                                              CR-CHECKPOINT-RESTART-VAR 02500012
                MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY       02510012
           ELSE                                                         02520012
               SET PS-FIRST-COMMIT TO TRUE                              02530012
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02540012
           END-IF.                                                      02550012
      *                                                                 02560012
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02570012
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02580012
            OR PS-NO-MORE-INPUT-RECORDS.                                02590012
      *                                                                 02600012
      * IF PROGRAM IN RESTART MODE, BUT NO RECORDS EXIST AFTER LAST CKPT02610012
      * MOVE RESTART COUNTS TO PA-FIELDS FOR ACCURATE DISPLAY AT EOJ    02620012
           IF PS-NO-MORE-INPUT-RECORDS                                  02630012
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02640012
                   MOVE CR-INPUT-READ-COUNT     TO PA-INPUT-COUNT       02650012
                   MOVE CR-TWKDPTC-COMMIT-COUNT TO PA-COMMIT-COUNT      02660012
                   MOVE CR-UPDATE-COUNT         TO PA-UPDATE-COUNT      02670012
                   MOVE CR-INSERT-COUNT         TO PA-INSERT-COUNT      02680012
                   MOVE CR-DEPTS-PROCESSED      TO PA-DEPTS-PROCESSED   02690012
      *        ELSE                                                     02700012
      *            DISPLAY '** NO RECORDS ON INPUT FILE **'             02710012
           ELSE                                                         02720012
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02730012
      *        DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02740012
      *             TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02750012
           END-IF.                                                      02760012
      *                                                                 02770012
      ******************************************************************02780012
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02790012
      ******************************************************************02800012
       2000-PROCESS-INPUT.                                              02810012
      *                                                                 02820012
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02830012
      *                                                                 02840012
           PERFORM 7000-SELECT-DB-ROW.                                  02850012
      *                                                                 02860012
           EVALUATE TRUE                                                02870012
               WHEN PS-UPDATE-WKDPTC-ROW                                02880012
                   PERFORM 7100-UPDATE-WKDPTC-ROW                       02890012
               WHEN PS-INSERT-WKDPTC-ROW                                02900012
                   PERFORM 7200-INSERT-WKDPTC-ROW                       02910012
               WHEN OTHER                                               02920012
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     02930012
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             02940012
                   PERFORM 9999-SQL-ABEND                               02950012
           END-EVALUATE.                                                02960012
      *                                                                 02970012
           IF PS-PROGRAM-DEADLOCKED                                     02980012
               CONTINUE                                                 02990012
           ELSE                                                         03000012
               PERFORM 7700-COMMIT-PROCESSING                           03010012
               PERFORM 4000-READ-CONDENSED-FILE.                        03020012
      *                                                                 03030012
      ******************************************************************03040012
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         03050012
      ******************************************************************03060012
       2100-CALCULATE-UPDATES.                                          03070012
      *                                                                 03080012
           COMPUTE SV-SLS-RTL-AMT        = WKDPTC-SLS-RTL-AMT           03090012
                                         + ML148-SLS-RTL-AMT.           03100012
           COMPUTE SV-MKDN-PLU-RTL-AMT   = WKDPTC-MKDN-PLU-RTL-AMT      03110012
                                         + ML148-MKDN-PLU-RTL-AMT.      03120012
           COMPUTE SV-MKDN-RGST-RTL-AMT  = WKDPTC-MKDN-RGST-RTL-AMT     03130012
                                         + ML148-MKDN-RGST-RTL-AMT.     03140012
           COMPUTE SV-MKDN-UMTCH-RTL-AMT = WKDPTC-MKDN-UMTCH-RTL-AMT    03150012
                                         + ML148-MKDN-UNMTCH-RTL-AMT.   03160012
           COMPUTE SV-MKDN-BYR-RTL-AMT   = WKDPTC-MKDN-BYR-RTL-AMT      03170012
                                         + ML148-MKDN-BYR-RTL-AMT.      03180012
           COMPUTE SV-MKDN-STR-RTL-AMT   = WKDPTC-MKDN-STR-RTL-AMT      03190012
                                         + ML148-MKDN-STR-RTL-AMT.      03200012
           COMPUTE SV-MKUP-RTL-AMT       = WKDPTC-MKUP-RTL-AMT          03210012
                                         + ML148-MKUP-RTL-AMT.          03220012
           COMPUTE SV-XFER-IN-RTL-AMT    = WKDPTC-XFER-IN-RTL-AMT       03230012
                                         + ML148-XFER-IN-RTL-AMT.       03240012
           COMPUTE SV-XFER-OUT-RTL-AMT   = WKDPTC-XFER-OUT-RTL-AMT      03250012
                                         + ML148-XFER-OUT-RTL-AMT.      03260012
           COMPUTE SV-RCPT-RTL-AMT       = WKDPTC-RCPT-RTL-AMT          03270012
                                         + ML148-RCPT-RTL-AMT.          03280012
           COMPUTE SV-RCPT-NET-COST-AMT  = WKDPTC-RCPT-NET-COST-AMT     03290012
                                         + ML148-RCPT-NET-COST-AMT.     03300012
           COMPUTE SV-PADJ-RTL-AMT       = WKDPTC-PADJ-RTL-AMT          03310012
                                         + ML148-PADJ-RTL-AMT.          03320012
           COMPUTE SV-PADJ-NET-COST-AMT  = WKDPTC-PADJ-NET-COST-AMT     03330012
                                         + ML148-PADJ-NET-COST-AMT.     03340012
           COMPUTE SV-PADJ-FRGT-COST-AMT = WKDPTC-PADJ-FRGT-COST-AMT    03350012
                                         + ML148-PADJ-FRGT-COST-AMT.    03360012
           COMPUTE SV-INV-ADJ-RTL-AMT    = WKDPTC-INV-ADJ-RTL-AMT       03370012
                                         + ML148-INV-ADJ-RTL-AMT.       03380012
23565      COMPUTE SV-PADJ-RTV-COST-AMT  = WKDPTC-PADJ-RTV-COST-AMT     03390012
23565                                    + ML148-PADJ-RTV-COST-AMT.     03400012
23565      COMPUTE SV-PADJ-RTV-RTL-AMT   = WKDPTC-PADJ-RTV-RTL-AMT      03410012
23565                                    + ML148-PADJ-RTV-RTL-AMT.      03420012
23565      COMPUTE SV-PADJ-DA-COST-AMT   = WKDPTC-PADJ-DA-COST-AMT      03430012
23565                                    + ML148-PADJ-DA-COST-AMT.      03440012
23565      COMPUTE SV-PADJ-DA-RTL-AMT    = WKDPTC-PADJ-DA-RTL-AMT       03450012
23565                                    + ML148-PADJ-DA-RTL-AMT.       03460012
23565      COMPUTE SV-PADJ-OTH-COST-AMT  = WKDPTC-PADJ-OTH-COST-AMT     03470012
23565                                    + ML148-PADJ-OTH-COST-AMT.     03480012
23565      COMPUTE SV-PADJ-OTH-RTL-AMT   = WKDPTC-PADJ-OTH-RTL-AMT      03490012
23565                                    + ML148-PADJ-OTH-RTL-AMT.      03500012
25554      COMPUTE SV-MKDN-PROMO-RTL-AMT = WKDPTC-MKDN-PROMO-RTL-AMT    03510012
  |                                      + ML148-MKDN-PROMO-RTL-AMT.    03520012
  |        COMPUTE SV-MKDN-PRPT-RTL-AMT  = WKDPTC-MKDN-PRPT-RTL-AMT     03530012
  |                                      + ML148-MKDN-PRPT-RTL-AMT.     03540012
  |        COMPUTE SV-MKDN-GLDST-RTL-AMT = WKDPTC-MKDN-GLDST-RTL-AMT    03550012
  |                                      + ML148-MKDN-GLDST-RTL-AMT.    03560012
  |        COMPUTE SV-MKDN-OTH-RTL-AMT   = WKDPTC-MKDN-OTH-RTL-AMT      03570012
25554                                    + ML148-MKDN-OTH-RTL-AMT.      03580012
W27511     COMPUTE SV-PADJ-BROP-COST-AMT                                03590012
  |                                   = WKDPTC-PADJ-BROP-COST-AMT       03600012
W27511                                + ML148-PADJ-BROP-COST-AMT.       03610012
W28630     COMPUTE SV-LIC-FEE-COST-AMT   = WKDPTC-LIC-FEE-COST-AMT      03620012
W30578                                   + ML148-LIC-FEE-COST-AMT.      03630012
W30578     COMPUTE SV-MKDN-EMP-RTL-AMT   = WKDPTC-MKDN-EMP-RTL-AMT      03640013
W30578                                   + ML148-MKDN-EMP-RTL-AMT.      03650013
      *                                                                 03660012
      ******************************************************************03670012
      * READS THE CONDENSED FILE INTO THE TWKDPTC LAYOUT                03680012
      ******************************************************************03690012
       4000-READ-CONDENSED-FILE.                                        03700012
            READ CONDENSED-FILE INTO ML148-REC                          03710012
               AT END MOVE 'Y' TO PS-CONDENSED-FILE-EOF-SW.             03720012
      *                                                                 03730012
            IF PS-MORE-INPUT-RECORDS                                    03740012
                ADD 1                        TO PA-INPUT-COUNT.         03750012
      *                                                                 03760012
      ******************************************************************03770012
      * 7000-SELECT-DB-ROW.                                             03780012
      ******************************************************************03790012
       7000-SELECT-DB-ROW.                                              03800012
      *                                                                 03810012
           EXEC SQL                                                     03820012
               SELECT SLS_RTL_AMT                                       03830012
                     ,MKDN_PLU_RTL_AMT                                  03840012
                     ,MKDN_RGST_RTL_AMT                                 03850012
                     ,MKDN_UMTCH_RTL_AMT                                03860012
                     ,MKDN_BYR_RTL_AMT                                  03870012
                     ,MKDN_STR_RTL_AMT                                  03880012
                     ,MKUP_RTL_AMT                                      03890012
                     ,XFER_IN_RTL_AMT                                   03900012
                     ,XFER_OUT_RTL_AMT                                  03910012
                     ,RCPT_RTL_AMT                                      03920012
                     ,RCPT_NET_COST_AMT                                 03930012
                     ,PADJ_RTL_AMT                                      03940012
                     ,PADJ_NET_COST_AMT                                 03950012
                     ,PADJ_FRGT_COST_AMT                                03960012
                     ,INV_ADJ_RTL_AMT                                   03970012
23565                ,PADJ_RTV_COST_AMT                                 03980012
23565                ,PADJ_RTV_RTL_AMT                                  03990012
23565                ,PADJ_DA_COST_AMT                                  04000012
23565                ,PADJ_DA_RTL_AMT                                   04010012
23565                ,PADJ_OTH_COST_AMT                                 04020012
23565                ,PADJ_OTH_RTL_AMT                                  04030012
25554                ,MKDN_PROMO_RTL_AMT                                04040012
25554                ,MKDN_PRPT_RTL_AMT                                 04050012
25554                ,MKDN_GLDST_RTL_AMT                                04060012
25554                ,MKDN_OTH_RTL_AMT                                  04070012
W27511               ,PADJ_BROP_COST_AMT                                04080012
W28630               ,LIC_FEE_COST_AMT                                  04090012
W30578               ,MKDN_EMP_RTL_AMT                                  04100013
                 INTO :WKDPTC-SLS-RTL-AMT                               04110012
                     ,:WKDPTC-MKDN-PLU-RTL-AMT                          04120012
                     ,:WKDPTC-MKDN-RGST-RTL-AMT                         04130012
                     ,:WKDPTC-MKDN-UMTCH-RTL-AMT                        04140012
                     ,:WKDPTC-MKDN-BYR-RTL-AMT                          04150012
                     ,:WKDPTC-MKDN-STR-RTL-AMT                          04160012
                     ,:WKDPTC-MKUP-RTL-AMT                              04170012
                     ,:WKDPTC-XFER-IN-RTL-AMT                           04180012
                     ,:WKDPTC-XFER-OUT-RTL-AMT                          04190012
                     ,:WKDPTC-RCPT-RTL-AMT                              04200012
                     ,:WKDPTC-RCPT-NET-COST-AMT                         04210012
                     ,:WKDPTC-PADJ-RTL-AMT                              04220012
                     ,:WKDPTC-PADJ-NET-COST-AMT                         04230012
                     ,:WKDPTC-PADJ-FRGT-COST-AMT                        04240012
                     ,:WKDPTC-INV-ADJ-RTL-AMT                           04250012
23565                ,:WKDPTC-PADJ-RTV-COST-AMT                         04260012
23565                ,:WKDPTC-PADJ-RTV-RTL-AMT                          04270012
23565                ,:WKDPTC-PADJ-DA-COST-AMT                          04280012
23565                ,:WKDPTC-PADJ-DA-RTL-AMT                           04290012
23565                ,:WKDPTC-PADJ-OTH-COST-AMT                         04300012
23565                ,:WKDPTC-PADJ-OTH-RTL-AMT                          04310012
25554                ,:WKDPTC-MKDN-PROMO-RTL-AMT                        04320012
25554                ,:WKDPTC-MKDN-PRPT-RTL-AMT                         04330012
25554                ,:WKDPTC-MKDN-GLDST-RTL-AMT                        04340012
25554                ,:WKDPTC-MKDN-OTH-RTL-AMT                          04350012
W27511               ,:WKDPTC-PADJ-BROP-COST-AMT                        04360012
W28630               ,:WKDPTC-LIC-FEE-COST-AMT                          04370012
W30578               ,:WKDPTC-MKDN-EMP-RTL-AMT                          04380013
              FROM TWKDPTC                                              04390012
              WHERE FSCL_WK_NBR         = :ML148-FSCL-WK-NBR            04400012
                AND FSCL_WK_END_DTE     = :ML148-FSCL-WK-END-DTE        04410012
                AND DEPT_NBR            = :ML148-DEPT-NBR               04420012
                AND CHNL_CDE            = :ML148-CHNL-CDE               04430012
W26603        WITH UR                                                   04440012
           END-EXEC.                                                    04450012
      *                                                                 04460012
           EVALUATE TRUE                                                04470012
               WHEN SQLCODE IS EQUAL TO ZERO                            04480012
                   SET PS-UPDATE-WKDPTC-ROW       TO TRUE               04490012
               WHEN SQLCODE IS EQUAL TO +100                            04500012
                   SET PS-INSERT-WKDPTC-ROW       TO TRUE               04510012
               WHEN SQLWARN IS NOT EQUAL TO SPACES                      04520012
               WHEN SQLCODE IS NOT EQUAL TO ZERO                        04530012
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04540012
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04550012
                   PERFORM 9999-SQL-ABEND                               04560012
           END-EVALUATE.                                                04570012
      *                                                                 04580012
      ***************************************************************** 04590012
      * 7100-UPDATE-WKDPTC-ROW.                                         04600012
      ***************************************************************** 04610012
       7100-UPDATE-WKDPTC-ROW.                                          04620012
      *                                                                 04630012
           PERFORM 2100-CALCULATE-UPDATES.                              04640012
      *                                                                 04650012
           EXEC SQL                                                     04660012
               UPDATE TWKDPTC                                           04670012
                  SET SLS_RTL_AMT          = :SV-SLS-RTL-AMT            04680012
                     ,MKDN_PLU_RTL_AMT     = :SV-MKDN-PLU-RTL-AMT       04690012
                     ,MKDN_RGST_RTL_AMT    = :SV-MKDN-RGST-RTL-AMT      04700012
                     ,MKDN_UMTCH_RTL_AMT   = :SV-MKDN-UMTCH-RTL-AMT     04710012
                     ,MKDN_BYR_RTL_AMT     = :SV-MKDN-BYR-RTL-AMT       04720012
                     ,MKDN_STR_RTL_AMT     = :SV-MKDN-STR-RTL-AMT       04730012
                     ,MKUP_RTL_AMT         = :SV-MKUP-RTL-AMT           04740012
                     ,XFER_IN_RTL_AMT      = :SV-XFER-IN-RTL-AMT        04750012
                     ,XFER_OUT_RTL_AMT     = :SV-XFER-OUT-RTL-AMT       04760012
                     ,RCPT_RTL_AMT         = :SV-RCPT-RTL-AMT           04770012
                     ,RCPT_NET_COST_AMT    = :SV-RCPT-NET-COST-AMT      04780012
                     ,PADJ_RTL_AMT         = :SV-PADJ-RTL-AMT           04790012
                     ,PADJ_NET_COST_AMT    = :SV-PADJ-NET-COST-AMT      04800012
                     ,PADJ_FRGT_COST_AMT   = :SV-PADJ-FRGT-COST-AMT     04810012
                     ,INV_ADJ_RTL_AMT      = :SV-INV-ADJ-RTL-AMT        04820012
23565                ,PADJ_RTV_COST_AMT    = :SV-PADJ-RTV-COST-AMT      04830012
23565                ,PADJ_RTV_RTL_AMT     = :SV-PADJ-RTV-RTL-AMT       04840012
23565                ,PADJ_DA_COST_AMT     = :SV-PADJ-DA-COST-AMT       04850012
23565                ,PADJ_DA_RTL_AMT      = :SV-PADJ-DA-RTL-AMT        04860012
23565                ,PADJ_OTH_COST_AMT    = :SV-PADJ-OTH-COST-AMT      04870012
23565                ,PADJ_OTH_RTL_AMT     = :SV-PADJ-OTH-RTL-AMT       04880012
25554                ,MKDN_PROMO_RTL_AMT   = :SV-MKDN-PROMO-RTL-AMT     04890012
25554                ,MKDN_PRPT_RTL_AMT    = :SV-MKDN-PRPT-RTL-AMT      04900012
25554                ,MKDN_GLDST_RTL_AMT   = :SV-MKDN-GLDST-RTL-AMT     04910012
25554                ,MKDN_OTH_RTL_AMT     = :SV-MKDN-OTH-RTL-AMT       04920012
W27511               ,PADJ_BROP_COST_AMT   = :SV-PADJ-BROP-COST-AMT     04930012
W28630               ,LIC_FEE_COST_AMT     = :SV-LIC-FEE-COST-AMT       04940012
W30578               ,MKDN_EMP_RTL_AMT     = :SV-MKDN-EMP-RTL-AMT       04950013
              WHERE FSCL_WK_NBR         = :ML148-FSCL-WK-NBR            04960012
                AND FSCL_WK_END_DTE     = :ML148-FSCL-WK-END-DTE        04970012
                AND DEPT_NBR            = :ML148-DEPT-NBR               04980012
                AND CHNL_CDE            = :ML148-CHNL-CDE               04990012
           END-EXEC.                                                    05000012
      *                                                                 05010012
           EVALUATE TRUE                                                05020012
               WHEN SQLCODE IS EQUAL TO ZERO                            05030012
                   CONTINUE                                             05040012
               WHEN SQLCODE IS EQUAL TO -911                            05050012
                   ADD +1             TO PV-DEADLOCK-COUNT              05060012
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05070012
                       MOVE '7100-UPDATE-WKDPTC-ROW' TO                 05080012
                                                    PV-PARAGRAPH-NAME   05090012
                       PERFORM 9000-DEADLOCK-ERROR                      05100012
                   ELSE                                                 05110012
                       PERFORM 7999-RESTART-PROCESS                     05120012
                   END-IF                                               05130012
               WHEN SQLWARN IS NOT EQUAL TO SPACES                      05140012
               WHEN SQLCODE IS NOT EQUAL TO ZERO                        05150012
                   MOVE '7100-UPDATE-WKDPTC-ROW' TO PV-PARAGRAPH-NAME   05160012
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED05170012
                   PERFORM 9999-SQL-ABEND                               05180012
           END-EVALUATE.                                                05190012
      *                                                                 05200012
           IF PS-PROGRAM-DEADLOCKED                                     05210012
               CONTINUE                                                 05220012
           ELSE                                                         05230012
               ADD 1 TO PA-UPDATE-COUNT                                 05240012
                        PA-DEPTS-PROCESSED.                             05250012
      *                                                                 05260012
      ***************************************************************** 05270012
      * 7200-INSERT-WKDPTC-ROW.                                         05280012
      ***************************************************************** 05290012
       7200-INSERT-WKDPTC-ROW.                                          05300012
      *                                                                 05310012
           EXEC SQL                                                     05320012
               INSERT INTO TWKDPTC                                      05330012
                     (FSCL_WK_NBR                                       05340012
                     ,FSCL_WK_END_DTE                                   05350012
                     ,DEPT_NBR                                          05360012
                     ,CHNL_CDE                                          05370012
                     ,SLS_RTL_AMT                                       05380012
                     ,MKDN_PLU_RTL_AMT                                  05390012
                     ,MKDN_RGST_RTL_AMT                                 05400012
                     ,MKDN_UMTCH_RTL_AMT                                05410012
                     ,MKDN_BYR_RTL_AMT                                  05420012
                     ,MKDN_STR_RTL_AMT                                  05430012
                     ,MKUP_RTL_AMT                                      05440012
                     ,XFER_IN_RTL_AMT                                   05450012
                     ,XFER_OUT_RTL_AMT                                  05460012
                     ,RCPT_RTL_AMT                                      05470012
                     ,RCPT_NET_COST_AMT                                 05480012
                     ,PADJ_RTL_AMT                                      05490012
                     ,PADJ_NET_COST_AMT                                 05500012
                     ,PADJ_FRGT_COST_AMT                                05510012
                     ,INV_ADJ_RTL_AMT                                   05520012
                     ,SHNK_RTL_AMT                                      05530012
                     ,END_INV_RTL_AMT                                   05540012
                     ,CUM_MKUP_PCT                                      05550012
                     ,END_INV_COST_AMT                                  05560012
                     ,SLS_COST_AMT                                      05570012
23565                ,GRO_MRGN_AMT                                      05580012
23565                ,PADJ_RTV_COST_AMT                                 05590012
23565                ,PADJ_RTV_RTL_AMT                                  05600012
23565                ,PADJ_DA_COST_AMT                                  05610012
23565                ,PADJ_DA_RTL_AMT                                   05620012
23565                ,PADJ_OTH_COST_AMT                                 05630012
23565                ,PADJ_OTH_RTL_AMT                                  05640012
25554                ,MKDN_PROMO_RTL_AMT                                05650012
25554                ,MKDN_PRPT_RTL_AMT                                 05660012
25554                ,MKDN_GLDST_RTL_AMT                                05670012
25554                ,MKDN_OTH_RTL_AMT                                  05680012
W27511               ,PADJ_BROP_COST_AMT                                05690012
W28630               ,LIC_FEE_COST_AMT                                  05700012
W30578               ,MKDN_EMP_RTL_AMT)                                 05710013
               VALUES                                                   05720012
                     (:ML148-FSCL-WK-NBR                                05730012
                     ,:ML148-FSCL-WK-END-DTE                            05740012
                     ,:ML148-DEPT-NBR                                   05750012
                     ,:ML148-CHNL-CDE                                   05760012
                     ,:ML148-SLS-RTL-AMT                                05770012
                     ,:ML148-MKDN-PLU-RTL-AMT                           05780012
                     ,:ML148-MKDN-RGST-RTL-AMT                          05790012
                     ,:ML148-MKDN-UNMTCH-RTL-AMT                        05800012
                     ,:ML148-MKDN-BYR-RTL-AMT                           05810012
                     ,:ML148-MKDN-STR-RTL-AMT                           05820012
                     ,:ML148-MKUP-RTL-AMT                               05830012
                     ,:ML148-XFER-IN-RTL-AMT                            05840012
                     ,:ML148-XFER-OUT-RTL-AMT                           05850012
                     ,:ML148-RCPT-RTL-AMT                               05860012
                     ,:ML148-RCPT-NET-COST-AMT                          05870012
                     ,:ML148-PADJ-RTL-AMT                               05880012
                     ,:ML148-PADJ-NET-COST-AMT                          05890012
                     ,:ML148-PADJ-FRGT-COST-AMT                         05900012
                     ,:ML148-INV-ADJ-RTL-AMT                            05910012
                     ,0                                                 05920012
                     ,0                                                 05930012
                     ,0                                                 05940012
                     ,0                                                 05950012
                     ,0                                                 05960012
                     ,0                                                 05970012
23565                ,:ML148-PADJ-RTV-COST-AMT                          05980012
23565                ,:ML148-PADJ-RTV-RTL-AMT                           05990012
23565                ,:ML148-PADJ-DA-COST-AMT                           06000012
23565                ,:ML148-PADJ-DA-RTL-AMT                            06010012
23565                ,:ML148-PADJ-OTH-COST-AMT                          06020012
23565                ,:ML148-PADJ-OTH-RTL-AMT                           06030012
25554                ,:ML148-MKDN-PROMO-RTL-AMT                         06040012
25554                ,:ML148-MKDN-PRPT-RTL-AMT                          06050012
25554                ,:ML148-MKDN-GLDST-RTL-AMT                         06060012
25554                ,:ML148-MKDN-OTH-RTL-AMT                           06070012
W27511               ,:ML148-PADJ-BROP-COST-AMT                         06080012
W28630               ,:ML148-LIC-FEE-COST-AMT                           06090012
W30578               ,:ML148-MKDN-EMP-RTL-AMT)                          06100013
           END-EXEC.                                                    06110012
      *                                                                 06120012
           EVALUATE TRUE                                                06130012
               WHEN SQLCODE IS EQUAL TO ZERO                            06140012
                   CONTINUE                                             06150012
               WHEN SQLCODE IS EQUAL TO -911                            06160012
                   ADD +1             TO PV-DEADLOCK-COUNT              06170012
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              06180012
                       MOVE '7200-INSERT-WKDPTC-ROW' TO                 06190012
                                                    PV-PARAGRAPH-NAME   06200012
                       PERFORM 9000-DEADLOCK-ERROR                      06210012
                   ELSE                                                 06220012
                       PERFORM 7999-RESTART-PROCESS                     06230012
                   END-IF                                               06240012
               WHEN SQLWARN IS NOT EQUAL TO SPACES                      06250012
               WHEN SQLCODE IS NOT EQUAL TO ZERO                        06260012
                   MOVE '7200-INSERT-WKDPTC-ROW' TO PV-PARAGRAPH-NAME   06270012
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED06280012
                   PERFORM 9999-SQL-ABEND                               06290012
           END-EVALUATE.                                                06300012
      *                                                                 06310012
           IF PS-PROGRAM-DEADLOCKED                                     06320012
               CONTINUE                                                 06330012
           ELSE                                                         06340012
               ADD 1 TO PA-INSERT-COUNT                                 06350012
                        PA-DEPTS-PROCESSED.                             06360012
      *                                                                 06370012
      ******************************************************************06380012
      * 7300-GET-COMMIT-TIMESTAMP.                                     *06390012
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *06400012
      *            CONTROL TABLE                                       *06410012
      ******************************************************************06420012
       7300-GET-COMMIT-TIMESTAMP.                                       06430012
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        06440012
                                                                        06450012
           EXEC SQL                                                     06460012
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       06470012
               INTO :PV-COMMIT-TIMESTAMP                                06480012
               FROM TCKRSTCNTL                                          06490012
              WHERE JOB_NAME  = :PC-JOB-NAME                            06500012
                AND PLAN_NAME = :PC-PROGRAM-NAME                        06510012
           END-EXEC.                                                    06520012
                                                                        06530012
           EVALUATE TRUE                                                06540012
               WHEN SQLCODE = 0                                         06550012
                   CONTINUE                                             06560012
               WHEN SQLCODE NOT EQUAL ZERO                              06570012
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME06580012
                   MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED 06590012
                   PERFORM 9999-SQL-ABEND                               06600012
           END-EVALUATE.                                                06610012
      *                                                                 06620012
      ******************************************************************06630012
      * 7400-INIT-CHECKPOINT-SELECT                                    *06640012
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *06650012
      *            IF WE ARE IN A RESTART CONDITION.                   *06660012
      ******************************************************************06670012
       7400-INIT-CHECKPOINT-SELECT.                                     06680012
                                                                        06690012
           EXEC SQL                                                     06700012
             SELECT  CKPT_STAT_CODE                                     06710012
                    ,CKPT_FRQNCY_QTY                                    06720012
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         06730012
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        06740012
               FROM  TCKRSTCNTL                                         06750012
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06760012
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06770012
           END-EXEC.                                                    06780012
                                                                        06790012
           IF SQLCODE = 0                                               06800012
               CONTINUE                                                 06810012
           ELSE                                                         06820012
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  06830012
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     06840012
               PERFORM 9999-SQL-ABEND                                   06850012
           END-IF.                                                      06860012
      *                                                                 06870012
      ******************************************************************06880012
      * 7500-SELECT-RESTART-INFO                                       *06890012
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *06900012
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *06910012
      ******************************************************************06920012
       7500-SELECT-RESTART-INFO.                                        06930012
                                                                        06940012
           EXEC SQL                                                     06950012
             SELECT  WORK_STRG_DESC                                     06960012
               INTO  :TCKRSTINF-WORK-STRG-DESC                          06970012
               FROM  TCKRSTINF                                          06980012
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06990012
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07000012
           END-EXEC.                                                    07010012
                                                                        07020012
           IF SQLCODE = 0                                               07030012
               CONTINUE                                                 07040012
           ELSE                                                         07050012
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   07060012
               MOVE PE-MSG-08             TO PV-OPERATION-ATTEMPTED     07070012
               PERFORM 9999-SQL-ABEND                                   07080012
           END-IF.                                                      07090012
      *                                                                 07100012
      ******************************************************************07110012
      * 7600-SET-CURRENT-TIMESTAMP.                                    *07120012
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *07130012
      ******************************************************************07140012
       7600-SET-CURRENT-TIMESTAMP.                                      07150012
                                                                        07160012
           EXEC SQL                                                     07170012
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           07180012
           END-EXEC.                                                    07190012
                                                                        07200012
           IF SQLCODE = 0                                               07210012
               CONTINUE                                                 07220012
           ELSE                                                         07230012
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 07240012
               MOVE PE-MSG-09             TO PV-OPERATION-ATTEMPTED     07250012
               PERFORM 9999-SQL-ABEND                                   07260012
           END-IF.                                                      07270012
      *                                                                 07280012
      ******************************************************************07290012
      * 7700-COMMIT-PROCESSING.                                        *07300012
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *07310012
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*07320012
      ******************************************************************07330012
       7700-COMMIT-PROCESSING.                                          07340012
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     07350012
                                                                        07360012
                                                                        07370012
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          07380012
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                07390012
      *        PREPARE COMMIT RECORD FOR UPDATE                         07400012
               ADD 1                          TO PA-COMMIT-COUNT        07410012
               MOVE ML148-FSCL-WK-NBR         TO CR-FSCL-WK-NBR         07420012
               MOVE ML148-FSCL-WK-END-DTE     TO CR-FSCL-WK-END-DTE     07430012
               MOVE ML148-DEPT-NBR            TO CR-DEPT-NBR            07440012
               MOVE ML148-CHNL-CDE            TO CR-CHNL-CDE            07450012
               MOVE PA-COMMIT-COUNT           TO CR-TWKDPTC-COMMIT-COUNT07460012
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    07470012
               MOVE PA-UPDATE-COUNT           TO CR-UPDATE-COUNT        07480012
               MOVE PA-INSERT-COUNT           TO CR-INSERT-COUNT        07490012
               MOVE PA-DEPTS-PROCESSED        TO CR-DEPTS-PROCESSED     07500012
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  07510012
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       07520012
                                             TCKRSTINF-WORK-STRG-DESC   07530012
               IF PS-FIRST-COMMIT                                       07540012
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                07550012
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                07560012
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       07570012
               END-IF                                                   07580012
               PERFORM 7800-COMMIT-CHANGES                              07590012
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        07600012
           END-IF.                                                      07610012
      *                                                                 07620012
      ******************************************************************07630012
      * 7800-COMMIT-CHANGES.                                            07640012
      *   PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE. TAKE A COMMIT 07650012
      ******************************************************************07660012
       7800-COMMIT-CHANGES.                                             07670012
                                                                        07680012
           EXEC SQL                                                     07690012
             UPDATE TCKRSTINF                                           07700012
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07710012
              WHERE JOB_NAME       = :PC-JOB-NAME                       07720012
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07730012
           END-EXEC.                                                    07740012
                                                                        07750012
           IF SQLCODE = 0                                               07760012
               CONTINUE                                                 07770012
           ELSE                                                         07780012
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED07790012
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07800012
               PERFORM 9999-SQL-ABEND                                   07810012
           END-IF.                                                      07820012
                                                                        07830012
           EXEC SQL                                                     07840012
               COMMIT                                                   07850012
           END-EXEC.                                                    07860012
                                                                        07870012
           IF SQLCODE = 0                                               07880012
               CONTINUE                                                 07890012
           ELSE                                                         07900012
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED07910012
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07920012
               PERFORM 9999-SQL-ABEND                                   07930012
           END-IF.                                                      07940012
      *                                                                 07950012
      ******************************************************************07960012
      * 7900-UPDATE-CHECKPOINT-STATUS                                   07970012
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   07980012
      *                                                                 07990012
      ******************************************************************08000012
       7900-UPDATE-CHECKPOINT-STATUS.                                   08010012
                                                                        08020012
           EXEC SQL                                                     08030012
             UPDATE  TCKRSTCNTL                                         08040012
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        08050012
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08060012
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08070012
           END-EXEC.                                                    08080012
                                                                        08090012
           IF SQLCODE = 0                                               08100012
               CONTINUE                                                 08110012
           ELSE                                                         08120012
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME08130012
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED08140012
               PERFORM 9999-SQL-ABEND                                   08150012
           END-IF.                                                      08160012
                                                                        08170012
           EJECT                                                        08180012
      ******************************************************************08190012
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TWKDPTC ROW FOR UPDATE08200012
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  08210012
      ******************************************************************08220012
       7999-RESTART-PROCESS.                                            08230012
      *                                                                 08240012
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           08250012
      *                                                                 08260012
           CLOSE CONDENSED-FILE.                                        08270012
      *                                                                 08280012
           PERFORM 7500-SELECT-RESTART-INFO.                            08290012
      *                                                                 08300012
      *    DISPLAY '**** RESTART **** '.                                08310012
      *    DISPLAY '** SQLCODE -911 ** ', PV-DEADLOCK-COUNT, 'TIMES'.   08320012
      *    DISPLAY 'TCKRSTINF-LAST CKPT ', TCKRSTINF-WORK-STRG-DESC.    08330012
      *                                                                 08340012
           MOVE TCKRSTINF-WORK-STRG-DESC TO CR-CHECKPOINT-RESTART-AREA. 08350012
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 08360012
      *                                                                 08370012
           OPEN INPUT CONDENSED-FILE.                                   08380012
           MOVE ZEROES                          TO PA-INPUT-COUNT.      08390012
      *                                                                 08400012
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          08410012
           PERFORM 4000-READ-CONDENSED-FILE                             08420012
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              08430012
      *                                                                 08440012
      *RE-ESTABLISH COUNTS FOR PROCESSED INPUT (FROM CHECKPOINT RECORD) 08450012
           MOVE CR-UPDATE-COUNT        TO PA-UPDATE-COUNT.              08460012
           MOVE CR-INSERT-COUNT        TO PA-INSERT-COUNT.              08470012
           MOVE CR-DEPTS-PROCESSED     TO PA-DEPTS-PROCESSED.           08480012
      *                                                                 08490012
      *                                                                 08500012
      ******************************************************************08510012
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08520012
      ******************************************************************08530012
       8000-EOJ-ROUTINE.                                                08540012
      *                                                                 08550012
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       08560012
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       08570012
      *                                                                 08580012
           DISPLAY '                                             '.     08590012
           DISPLAY 'INPUT RECORDS   ', PA-INPUT-COUNT.                  08600012
           DISPLAY 'UPDATE COUNT    ', PA-UPDATE-COUNT.                 08610012
           DISPLAY 'INSERT COUNT    ', PA-INSERT-COUNT.                 08620012
      *    DISPLAY 'DEPTS PROCESSED ', PA-DEPTS-PROCESSED.              08630012
           DISPLAY '                                             '.     08640012
      *    DISPLAY 'COMMITS TAKEN   ', PA-COMMIT-COUNT.                 08650012
      *                                                                 08660012
           CLOSE CONDENSED-FILE.                                        08670012
      *                                                                 08680012
      ******************************************************************08690012
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       08700012
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   08710012
      ******************************************************************08720012
       9000-DEADLOCK-ERROR.                                             08730012
      *                                                                 08740012
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     08750012
               PERFORM 9999-SQL-ABEND.                                  08760012
      *                                                                 08770012
      ******************************************************************08780012
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08790012
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08800012
      ******************************************************************08810012
       9999-SQL-ABEND.                                                  08820012
                                                                        08830012
           MOVE +4013                 TO ABEND-CODE.                    08840012
      *    DISPLAY '**  ABEND     **'.                                  08850012
      *    DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08860012
      *    DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08870012
      *    DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08880012
                                                                        08890012
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08900012
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08910012
                                                                        08920012
           COPY DPPD004.                                                08930012
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08940012
