000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    WSKUT064.                                                 
000400 AUTHOR.        GERMAN BANDA.                                             
000500 DATE-WRITTEN.  JUNE 2018.                                                
000510*****************************************************************         
000520*  THIS PROGRAM WILL READ IN A FILE OF RESERVE AND ACTIVE   .   *         
000530*  LOCATIONS GENERATED FROM AN ORACLE QUERY.  THE PROGRAM WILL  *         
000540*  TOTAL THE UNITS AND WRITE TO AN OUTPUT FILE                  *         
000550*                                                               *         
000560*****************************************************************         
000600 ENVIRONMENT DIVISION.                                                    
000700 INPUT-OUTPUT SECTION.                                                    
000800                                                                          
000900 FILE-CONTROL.                                                            
001000     SELECT PHYSL-INV-MISSG-LOC       ASSIGN TO INP01.                    
001120     SELECT PHYSL-INV-TOTAL-OUT       ASSIGN TO OUT01.                    
001200                                                                          
001300 DATA DIVISION.                                                           
001400 FILE SECTION.                                                            
001500                                                                          
001600                                                                          
001700 FD  PHYSL-INV-MISSG-LOC                                                  
001800     RECORDING MODE IS F                                                  
001900     LABEL RECORDS ARE STANDARD                                           
002000     RECORD CONTAINS 80                                                   
002100     BLOCK CONTAINS 0 RECORDS                                             
002200     DATA RECORD IS PHYS-INV-LOC-RECORD.                                  
002300 01  PHYS-INV-LOC-RECORD              PIC X(80).                          
002400                                                                          
002500 FD  PHYSL-INV-TOTAL-OUT                                                  
002600     RECORDING MODE IS F                                                  
002700     LABEL RECORDS ARE STANDARD                                           
002800     RECORD CONTAINS 80                                                   
002900     BLOCK CONTAINS 0 RECORDS                                             
003000     DATA RECORD IS PHYSL-INV-TOTAL-RECORD.                               
003100 01  PHYSL-INV-TOTAL-RECORD      PIC X(80).                               
003200                                                                          
003300 WORKING-STORAGE SECTION.                                                 
003400                                                                          
003500 01  PV-ABEND-CODE                    PIC S9(04)   VALUE ZERO             
003600                                                   COMP SYNC.             
003700                                                                          
003800 01  PS-PROGRAM-SWITCHES.                                                 
003900     05  PS-END-EXTRACT-SW            PIC X(1)   VALUE 'N'.               
004000         88  PS-END-EXTRACT                      VALUE 'Y'.               
004300                                                                          
004400                                                                          
004500 01  PV-PROGRAM-VARIABLES.                                                
004600     05  PV-RETRY-COUNT               PIC S9(04)  VALUE ZERO              
004700                                                  COMP SYNC.              
004800     05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
004900     05  PV-ERROR-MESSAGE-1           PIC X(60)   VALUE SPACES.           
005000     05  PV-ERROR-MESSAGE-2           PIC X(60)   VALUE SPACES.           
005010     05  PV-TMST                      PIC X(26)   VALUE SPACES.           
005100     05  PV-SQLCODE                   PIC 9(9)    VALUE ZERO.             
005700     05  PV-DISP-NBR                  PIC Z(8)9.                          
006200     05  PV-AUDIT-DURATION            PIC S9(9) COMP-3 VALUE ZERO.        
006300                                                                          
006400 01  PV-COUNTERS COMP-3.                                                  
006500     05  PV-READ-COUNT                PIC S9(9)    VALUE ZERO.            
006600     05  PV-WRITE-COUNT               PIC S9(9)    VALUE ZERO.            
006700     05  PV-SKIP-COUNT                PIC S9(9)    VALUE ZERO.            
006710     05  PV-COMMIT-COUNT              PIC S9(9)    VALUE ZERO.            
006720     05  PV-QTY-NO-LOC-HDR            PIC S9(8)    VALUE ZERO.            
006730     05  PV-QTY-TOT-UNITS             PIC S9(8)    VALUE ZERO.            
006740     05  PV-QTY-DIFFERENCE            PIC S9(8)    VALUE ZERO.            
006800                                                                          
006900 01  PC-PROGRAM-CONSTANTS.                                                
007000     05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'WSKUT064'.        
007100     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
007200                                                 COMP SYNC.               
007300     05  PC-COMMIT                    PIC S9(04) VALUE 999.               
007400     05  PC-EFC-STORE-LOC             PIC X(22)  VALUE                    
007500                                      'EFC_STORE_LOCATION_HDR'.           
007510     05  PC-RESERVE-NO-LOC            PIC X(22)  VALUE                    
007520                                      'RESERVE_QTY_NO_LOC_HDR'.           
007600     05  PC-RESERVE-TOT               PIC X(22)  VALUE                    
007700                                      'RESERVE_QTY_TOTAL_UNTS'.           
008510 01  PV-IN-RECORD.                                                        
008520     05  PV-QUANTITY-HEADING          PIC X(22).                          
008521     05  FILLER                       PIC X(01).                          
008530     05  PV-RES-ACTV-QTY              PIC 9(08).                          
008531     05  FILLER                       PIC X(01).                          
008532     05  PV-COUNT-HEADING             PIC X(22).                          
008533     05  FILLER                       PIC X(01).                          
008534     05  PV-RES-ACTV-CNT              PIC 9(08).                          
008535     05  FILLER                       PIC X(05).                          
008540 01  PV-OUT-RECORD.                                                       
008550     05  PV-BEG-LINE.                                                     
008551         10  PV-LINE-TOP                 PIC X(55)  VALUE                 
008552        '======================================================='.        
008553         10  FILLER                      PIC X(25)  VALUE SPACE.          
008554     05  PV-LOCATION.                                                     
008555         10  PV-LOCATION-HDG             PIC X(11)  VALUE                 
008556             ' LOCATION: '.                                               
008557         10  PV-LOCATION-STR             PIC X(04).                       
008559         10  FILLER                      PIC X(65)  VALUE SPACE.          
008560     05  PV-RESERVE.                                                      
008561         10  PV-RESERVE-TEXT             PIC X(46)  VALUE                 
008562             ' TOTAL RESERVE UNITS WITHOUT LOCATION HEADER: '.            
008563         10  PV-RESERVE-DIFF             PIC ZZZZZZ99.                    
008564         10  FILLER                      PIC X(26)  VALUE SPACE.          
008565     05  PV-BLANK-LINE.                                                   
008566         10  FILLER                      PIC X(80)  VALUE SPACE.          
011000/                                                                         
011100 PROCEDURE DIVISION.                                                      
011200                                                                          
011300     PERFORM 1000-INIT.                                                   
011400                                                                          
011500     PERFORM 2000-PROCESS UNTIL PS-END-EXTRACT.                           
011702                                                                          
011710     PERFORM 9000-TERMINATE.                                              
011800                                                                          
011900     GOBACK.                                                              
012000                                                                          
012100 1000-INIT.                                                               
012200                                                                          
012300     OPEN INPUT  PHYSL-INV-MISSG-LOC                                      
012400          OUTPUT PHYSL-INV-TOTAL-OUT                                      
012500                                                                          
012700                                                                          
012800     PERFORM 8000-READ-INV-MISSG-LOC.                                     
012900                                                                          
013000 2000-PROCESS.                                                            
013001**                        *** 'EFC_STORE_LOCATION_HDR'                    
013010     IF PV-QUANTITY-HEADING  = PC-EFC-STORE-LOC                           
013100        MOVE PV-RES-ACTV-QTY TO  PV-LOCATION-STR                          
013105     END-IF                                                               
013106                                                                          
013107     IF PV-QUANTITY-HEADING  = PC-RESERVE-NO-LOC                          
013108        MOVE PV-RES-ACTV-QTY TO  PV-QTY-NO-LOC-HDR                        
013109     END-IF                                                               
013110                                                                          
013111**                        *** 'RESERVE_QTY_TOTAL_UNTS'                    
013112     IF PV-QUANTITY-HEADING  = PC-RESERVE-TOT                             
013113        MOVE PV-RES-ACTV-QTY TO PV-QTY-TOT-UNITS                          
013120                                                                          
013200        COMPUTE PV-QTY-DIFFERENCE =                                       
013210                PV-QTY-NO-LOC-HDR - PV-QTY-TOT-UNITS                      
013220        MOVE PV-QTY-DIFFERENCE  TO PV-RESERVE-DIFF                        
013230        PERFORM 8100-WRITE-EXTRACT-REC                                    
013300     END-IF                                                               
013310                                                                          
013600     PERFORM 8000-READ-INV-MISSG-LOC.                                     
013700/                                                                         
026100 8000-READ-INV-MISSG-LOC.                                                 
026200                                                                          
026300     READ PHYSL-INV-MISSG-LOC INTO PV-IN-RECORD                           
026500     AT END                                                               
026600         SET PS-END-EXTRACT           TO TRUE                             
026700     NOT AT END                                                           
026800         ADD 1                        TO PV-READ-COUNT                    
026900     END-READ.                                                            
027000                                                                          
027100 8100-WRITE-EXTRACT-REC.                                                  
027200                                                                          
027300     ADD 1                         TO PV-WRITE-COUNT.                     
027400                                                                          
027500     WRITE PHYSL-INV-TOTAL-RECORD FROM PV-BEG-LINE.                       
027510     WRITE PHYSL-INV-TOTAL-RECORD FROM PV-LOCATION.                       
027520     WRITE PHYSL-INV-TOTAL-RECORD FROM PV-RESERVE.                        
027530     WRITE PHYSL-INV-TOTAL-RECORD FROM PV-BLANK-LINE.                     
027531     WRITE PHYSL-INV-TOTAL-RECORD FROM PV-BEG-LINE.                       
027540                                                                          
027600/                                                                         
027700 9000-TERMINATE.                                                          
027800                                                                          
027900     CLOSE PHYSL-INV-MISSG-LOC                                            
028000           PHYSL-INV-TOTAL-OUT                                            
028100                                                                          
028200     DISPLAY '************************'.                                  
028300     DISPLAY '******* WSKUT064 *******'.                                  
028400     DISPLAY '************************'.                                  
028500     DISPLAY SPACES                                                       
028600                                                                          
028700     MOVE PV-READ-COUNT               TO PV-DISP-NBR                      
028800     DISPLAY 'EXTRACT RECORDS READ         = ' PV-DISP-NBR                
028900                                                                          
029000     MOVE PV-WRITE-COUNT              TO PV-DISP-NBR                      
029100     DISPLAY 'SELECTED EXTRACTS WRITTEN    = ' PV-DISP-NBR                
029200                                                                          
029300     DISPLAY SPACES                                                       
029400     DISPLAY '************************'.                                  
029500                                                                          
033300                                                                          
