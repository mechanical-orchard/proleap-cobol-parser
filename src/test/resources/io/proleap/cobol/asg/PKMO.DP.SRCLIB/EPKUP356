       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      EPKUP356.                                       00020000
       AUTHOR.          COGNIZANT.                                      00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    AUG   2010.                                     00050000
       DATE-COMPILED.                                                   00060000
      ***************************************************************** 00541000
      ******************************************************************00542000
      *  MODULE TYPE:   COBOL2 DB2 BATCH.                               00543000
      *                                                                 00544000
      *  DAILY LOAD OF XSTW_STR_GP_PRCHG  WITH DATA FROM XST_PRCHG &    00545000
      *  XST_STR_GP_PRCHG                                               00546000
      *                                                                 00547000
      *========================== CHANGE LOG ===========================00548000
      *  AUTH        BY            DATE                 DESCRIPTION     00549000
      *=================================================================00549100
      * 3132A      COGNIZANT     08-31-10   CREATE PROGRAM              00549200
      ******************************************************************00549300
       ENVIRONMENT DIVISION.                                            00550000
       CONFIGURATION SECTION.                                           00560000
       SOURCE-COMPUTER. IBM-3090.                                       00570000
       OBJECT-COMPUTER. IBM-3090.                                       00580000
                                                                        00590000
       INPUT-OUTPUT SECTION.                                            00600000
       FILE-CONTROL.                                                    00610000
                                                                        00620000
           SELECT CC-TOMORROW                                           00660000
                  ASSIGN TO INP01.                                      00670000
                                                                        00680000
           SELECT CC-PREV-NEXT-NEW-DAY                                  00690000
                  ASSIGN TO INP02.                                      00700000
                                                                        00710000
       DATA DIVISION.                                                   00820000
       FILE SECTION.                                                    00830000
                                                                        00840000
       FD  CC-TOMORROW                                                  00920000
           RECORDING MODE F                                             00930000
           BLOCK CONTAINS 0 RECORDS                                     00940000
           LABEL RECORDS ARE OMITTED                                    00950000
           DATA RECORD IS CC-TOMORROW-RECORD.                           00960000
       01  CC-TOMORROW-RECORD      PIC  X(080).                         00970000
                                                                        00980000
       FD  CC-PREV-NEXT-NEW-DAY                                         00990000
           RECORDING MODE F                                             01000000
           BLOCK CONTAINS 0 RECORDS                                     01010000
           LABEL RECORDS ARE OMITTED                                    01020000
           DATA RECORD IS CC-PREV-NEXT-NEW-DAY-RECORD.                  01030000
       01  CC-PREV-NEXT-NEW-DAY-RECORD     PIC  X(080).                 01040000
                                                                        01050000
       WORKING-STORAGE SECTION.                                         01190000
      ******************************************************************01200000
      * PROGRAM SWITCHES                                                01210000
      ******************************************************************01220000
       01  PS-PROGRAM-SWITCHES.                                         01230000
      * USED TO DETERMINE IF DISPLAYS SHOULD APPEAR                     01240000
           05  PS-END-OF-CNTL-CRD-SW        PIC  X(001).                01241000
               88  PS-END-OF-CNTL-CRD-FILE              VALUE 'Y'.      01242000
               88  PS-NOT-END-OF-CNTL-CRD-FILE          VALUE 'N'.      01243000
                                                                        01244000
           05  PS-END-OF-TOMO-CRD-SW        PIC  X(001).                01245000
               88  PS-END-OF-TOMO-CRD-FILE              VALUE 'Y'.      01246000
               88  PS-NOT-END-OF-TOMO-CRD-FILE          VALUE 'N'.      01247000
                                                                        01248000
           05  PS-DISPLAY-SW               PIC  X(01)  VALUE 'N'.       01250000
               88  PS-DISPLAY-YES                      VALUE 'Y'.       01260000
               88  PS-DISPLAY-NO                       VALUE 'N'.       01270000
      * USED TO DETERMINE IF UPC IS CLEARANCED                          01280000
           05  PS-END-OF-CSR-SW         PIC  X(01)  VALUE 'N'.          01620000
               88  PS-END-OF-CSR                VALUE 'Y'.              01630000
               88  PS-NOT-EO-CSR                VALUE 'N'.              01640000
      ******************************************************************01830000
      * PROGRAM CONSTANTS                                               01840000
      ******************************************************************01850000
                                                                        01880000
       01  PC-PROGRAM-CONSTANTS.                                        01884000
           05  PC-PROGRAM-NAME          PIC X(08) VALUE 'EPKUP356'.     01885000
           05  PC-MAX-RETRIES           PIC S9(4) VALUE +5 COMP.        01886000
           05  PC-FIVE-BLANKS           PIC X(05) VALUE SPACES.         01887000
           05  PC-TEN-BLANKS            PIC X(10) VALUE SPACES.         01888000
           05  PC-19-ZEROS              PIC X(19) VALUE ALL '0'.        01889000
           05  PC-ASTERISK-LINE         PIC X(80) VALUE ALL '*'.        01889100
           05  PC-ACTIVE-STAT           PIC XX    VALUE '20'.           01889200
           05  PC-CLEARANCE-STAT        PIC XX    VALUE '30'.           01889300
           05  PC-TCKT-CLR-YELLOW       PIC X     VALUE 'Y'.            01889400
           05  PC-TCKT-CLR-WHITE        PIC X     VALUE 'W'.            01889500
           05  PC-ALL-COLORS            PIC XX    VALUE 'AL'.           01889600
           05  PC-NULLS                 PIC X     VALUE '~'.            01889700
           05  PC-COLON                 PIC X     VALUE ':'.            01889800
           05  PC-3CLOCK                PIC X(09) VALUE ' 03:00:00'.    01889900
                                                                        01890000
      ******************************************************************01891000
      * PROGRAM VARIBLES                                                01900000
      ******************************************************************01910000
       01  PV-PROGRAM-VARIBLES.                                         01920000
           05  PV-PARAGRAPH-NAME           PIC  X(30)  VALUE SPACES.    01930000
           05  PV-OPERATION-MSG-1          PIC  X(40)  VALUE SPACES.    01940000
           05  PV-OPERATION-MSG-2          PIC  X(40)  VALUE SPACES.    01950000
           05  PV-XS-ROWS-FETCHED          PIC  9(9)   VALUE ZEROES.    01951000
           05  PA-ROWS-INSERTED            PIC  9(9)   VALUE ZEROES.    01952000
           05  PA-ROWS-DUP-INSERTED        PIC  9(9)   VALUE ZEROES.    01953000
           05  PV-STR-NBR                  PIC  S9(4)  COMP.            01960000
           05  PV-DT                       PIC  X(10)  VALUE SPACES.    01970000
           05  PV-CURRENT-TIMESTAMP        PIC  X(26)  VALUE SPACES.    01980000
           05  PV-WORK-DATE                PIC  X(10)  VALUE SPACES.    01990000
      ******************************************************************01991000
      * PV- DB2 VARIABLES.                                              02000000
      ******************************************************************02001000
       01  PV-DB2-VARIABLES.                                            02020000
           05  PV-DB2-DEPT              PIC S9(4)V COMP-3 VALUE 0.      02030000
           05  PV-DB2-MAJ-CL            PIC S9(4)V COMP-3 VALUE 0.      02040000
           05  PV-DB2-SUB-CL            PIC S9(4)V COMP-3 VALUE 0.      02050000
           05  PV-DB2-STYLE             PIC S9(8)V COMP-3 VALUE 0.      02060000
           05  PV-DB2-SKU-NBR           PIC S9(8)V COMP-3 VALUE 0.      02070000
           05  PV-DB2-INTR-UPC-ID       PIC S9(09) COMP   VALUE 0.      02080000
           05  PV-NULL-DEPT             PIC S9(04) VALUE +0 COMP SYNC.  02090000
           05  PV-NULL-MAJ-CL           PIC S9(04) VALUE +0 COMP SYNC.  02100000
           05  PV-NULL-SUB-CL           PIC S9(04) VALUE +0 COMP SYNC.  02110000
           05  PV-NULL-STYLE            PIC S9(04) VALUE +0 COMP SYNC.  02120000
           05  PV-NULL-SKU              PIC S9(04) VALUE +0 COMP SYNC.  02130000
           05  PV-NULL-UNIT-RTL         PIC S9(04) VALUE +0 COMP SYNC.  02140000
           05  PV-NULL-NCLR-RTL-AMT     PIC S9(04) VALUE +0 COMP SYNC.  02150000
           05  PV-NULL-GP-PRIC-ID       PIC S9(04) VALUE +0 COMP SYNC.  02160000
           05  PV-NULL-GP-RTL-QTY       PIC S9(04) VALUE +0 COMP SYNC.  02170000
           05  PV-NULL-GP-RTL-AMT       PIC S9(04) VALUE +0 COMP SYNC.  02180000
                                                                        02190000
       01  ABEND-CODE                      PIC S9(04)  COMP VALUE +0.   02200000
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.     02210000
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     02220000
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.     02230000
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.     02240000
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.     02250000
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.     02260000
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.     02270000
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.     02280000
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.     02290000
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.     02300000
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.     02310000
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.     02320000
                                                                        02330000
      ******************************************************************03300000
      * COPYBOOKS                                                       03310000
      ******************************************************************03320000
      *--------------------------------------------------------------*  03449000
      * EPWS112 CONTROL CARD LAYOUT - TOMMORROWS DATE                   03449100
      *--------------------------------------------------------------*  03449200
           COPY EPWS112.                                                03449300
                                                                        03449400
      *--------------------------------------------------------------*  03450000
      * EPWS161 CONTROL CARD LAYOUT - FOR PREV & NEXT "NEW" DAY DATES   03450100
      *--------------------------------------------------------------*  03450200
           COPY EPWS161.                                                03450300
                                                                        03450400
      *--------------------------------------------------------------*  03501000
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         03502000
      *--------------------------------------------------------------*  03503000
           COPY DPWS004.                                                03504000
                                                                        03505000
      *--------------------------------------------------------------*  03506000
      * DATE ROUTINE COPY MEMBER                                        03507000
      *--------------------------------------------------------------*  03508000
           COPY DPWS500.                                                03509000
                                                                        03509100
      *--------------------------------------------------------------*  03509700
      * DB2 MESSAGE AREA                                                03509800
      *--------------------------------------------------------------*  03509900
           COPY SQLCA2.                                                 03510000
                                                                        03510100
      ******************************************************************03511000
      * TABLE INCLUSIONS                                                03520000
      ******************************************************************03530000
           EXEC SQL INCLUDE SQLCA    END-EXEC.                          03540000
                                                                        03550000
      *--------------------------------------------------------------*  03551000
      * DB2 TABLE XST00063 (XST_PRCHG) FUTURE PERM PRICE CHANGE         03552000
      *--------------------------------------------------------------*  03553000
           EXEC SQL                                                     03554000
             INCLUDE XST00063                                           03555000
           END-EXEC.                                                    03556000
                                                                        03557000
      *--------------------------------------------------------------*  03558000
      * DB2 TABLE XST00064 (XST_STR_GP_PRCHG) FUTURE PERM STORE GRP     03559000
      *--------------------------------------------------------------*  03559100
           EXEC SQL                                                     03559200
             INCLUDE XST00064                                           03559300
           END-EXEC.                                                    03559400
                                                                        03559500
      *--------------------------------------------------------------*  03559699
      * DB2 TABLE XST00134 (XSTW_STR_GP_PRCHG_EXTR) WORK TABLE          03559799
      *--------------------------------------------------------------*  03559899
           EXEC SQL                                                     03559999
             INCLUDE XST00134                                           03560099
           END-EXEC.                                                    03570099
      ******************************************************************04290000
      * GETS ALL XS INFO FOR GIVEM COMBO                                04300000
      ******************************************************************04310000
           EXEC SQL                                                     04320000
               DECLARE XS-PRC-CSR CURSOR FOR                            04330000
                SELECT S.STR_GP_TYP_CDE                                 04340000
                     , S.STR_GP_ID                                      04344000
                     , S.PRCHG_EFF_DTE                                  04349100
                     , P.PRCHG_ID                                       04349200
                     , P.PRCHG_TYP_CDE                                  04349300
                    FROM XST_PRCHG        P                             04420099
                         ,XST_STR_GP_PRCHG S                            04430099
                  WHERE (S.PRCHG_EFF_DTE  >= :CC-EPCC112-TOMORROW-DATE  04440000
                     AND S.PRCHG_EFF_DTE  <= :CC-EPCC161-PREV-NEW-DATE) 04450000
                     AND  S.LAST_ACTN_CDE  <> 'D'                       04460000
                     AND  P.PRCHG_ID       = S.PRCHG_ID                 04470000
                     AND  P.PRCHG_ST_CDE = 'APR'                        04480000
                     AND  P.EVNT_CTG_CDE IN ('ACT','CLR','MK2','FNL')   04490000
                     FOR FETCH ONLY                                     04500000
                  WITH UR                                               04660000
           END-EXEC.                                                    04670000
                                                                        04680000
       PROCEDURE DIVISION.                                              10320000
      ******************************************************************10330000
      ******************************************************************10331000
      * MAINLINE                                                        10332000
      ******************************************************************10333000
       0000-MAINLINE.                                                   10340000
                                                                        10350000
           PERFORM 1000-INITIALIZE.                                     10360000
                                                                        10410000
           PERFORM 2000-OPEN-XS-PRC-CSR.                                10410100
                                                                        10410200
           PERFORM 2050-LOCK-WORK-TABLE.                                10410399
                                                                        10410499
           PERFORM 2100-FETCH-XS-PRC-CSR                                10410500
             UNTIL PS-END-OF-CSR.                                       10410600
                                                                        10410700
           PERFORM 2300-CLS-XS-PRC-CSR.                                 10410800
                                                                        10410900
           PERFORM 9999-WRAPUP.                                         10411000
           GOBACK.                                                      10411100
      ******************************************************************10412000
      ******************************************************************10500000
      * INITIALIZATION AREA - SETUP QUEUES FOR PROCESSING               10510000
      ******************************************************************10520000
       1000-INITIALIZE.                                                 10530000
                                                                        10540000
           DISPLAY ' '.                                                 10550000
           DISPLAY '=================================================='.10560000
           DISPLAY '=               PROGRAM START                     '.10570000
           DISPLAY '=================================================='.10580000
           DISPLAY ' '.                                                 10590000
                                                                        10591000
           OPEN INPUT  CC-PREV-NEXT-NEW-DAY,                            10591100
                       CC-TOMORROW.                                     10591400
                                                                        10591500
           PERFORM 1100-READ-PREV-NEXT-NEW-RECORD.                      10591600
                                                                        10591700
           PERFORM 1200-READ-TOMORROW-RECORD.                           10593000
                                                                        10594000
      *--------------------------------------------------------------*  10620000
      * 1100-READ-PREV-NEXT-NEW-RECORD.                                 10630000
      *     READ CONTROL CARD EPCC161. THIS CONTROL CARD CONTAINS       10640000
      *     THE PREVIOUS AND NEXT NEW FPLU DAY.                         10650000
      *--------------------------------------------------------------*  10660000
       1100-READ-PREV-NEXT-NEW-RECORD.                                  10670000
                                                                        10680000
           READ CC-PREV-NEXT-NEW-DAY INTO CC-EPCC161-CONTROL-CARD       10690000
             AT END                                                     10700000
                SET PS-END-OF-CNTL-CRD-FILE TO TRUE.                    10710000
                                                                        10720000
           IF PS-END-OF-CNTL-CRD-FILE                                   10730000
              DISPLAY ' '                                               10740000
              DISPLAY '** ABEND     **'                                 10750000
              DISPLAY '** PARAGRAPH **  = 1100-READ-PREV-NEXT-NEW'      10770000
              DISPLAY '** MISSING PREV NEXT NEW DAY CONTROL CARD - 161' 10780000
              DISPLAY ' '                                               10790000
              MOVE +4003 TO ABEND-CODE                                  10800000
              CALL 'ILBOABN0' USING ABEND-CODE                          10810000
           ELSE                                                         10820000
              DISPLAY '** PREV NEXT NEW DAY CONTROL CARD  = '           10830000
                      CC-PREV-NEXT-NEW-DAY-RECORD,  ' **'               10840000
              DISPLAY PC-FIVE-BLANKS, '** EPCC161 NEXT NEW DATE  = '    10850000
                      CC-EPCC161-NEW-DATE, ' **'                        10860000
              DISPLAY PC-FIVE-BLANKS, '** EPCC161 PREV NEW DATE  = '    10870000
                      CC-EPCC161-PREV-NEW-DATE, ' **'                   10880000
              DISPLAY ' '                                               10890000
           END-IF.                                                      10900000
                                                                        10900100
           MOVE CC-EPCC161-NEW-DATE        TO PV-WORK-DATE.             10901000
           PERFORM 1400-VALIDATE-DATE.                                  10911000
                                                                        10912000
           MOVE CC-EPCC161-PREV-NEW-DATE   TO PV-WORK-DATE.             10913000
           PERFORM 1400-VALIDATE-DATE.                                  10915000
                                                                        10916000
      *1100-EXIT.                                                       10920000
                                                                        10930000
      *--------------------------------------------------------------*  10943000
      * 1200-READ-TOMORROW-RECORD.                                      10950000
      *     READ CONTROL CARD EPCC155. THIS CONTROL CARD CONTAINS       10960000
      *     TOMORROWS DATE FOR FPLU.                                    10970000
      *--------------------------------------------------------------*  10980000
       1200-READ-TOMORROW-RECORD.                                       10990000
                                                                        11000000
           READ CC-TOMORROW INTO CC-EPCC112-CONTROL-CARD                11010000
             AT END                                                     11020000
                SET PS-END-OF-TOMO-CRD-FILE TO TRUE.                    11030000
                                                                        11040000
           IF PS-END-OF-TOMO-CRD-FILE                                   11050000
              DISPLAY ' '                                               11060000
              DISPLAY '** ABEND     **'                                 11070000
              DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME             11080000
              DISPLAY '** PARAGRAPH **  = 1200-READ-TOMORROW-RECORD'    11090000
              DISPLAY '** MISSING TOMORROW CONTROL CARD - 155'          11100000
              DISPLAY ' '                                               11101000
              MOVE +4003 TO ABEND-CODE                                  11102000
              CALL 'ILBOABN0' USING ABEND-CODE                          11103000
           ELSE                                                         11104000
              DISPLAY '** TOMORROW CONTROL CARD  = '                    11105000
                      CC-TOMORROW-RECORD,  ' **'                        11106000
              DISPLAY PC-FIVE-BLANKS, '** EPCC150 TOMORROW       = '    11107000
                      CC-EPCC112-TOMORROW-DATE, ' **'                   11108000
              DISPLAY ' '                                               11109000
           END-IF.                                                      11109100
                                                                        11109200
           MOVE CC-EPCC112-TOMORROW-DATE   TO PV-WORK-DATE.             11109300
           PERFORM 1400-VALIDATE-DATE.                                  11109400
                                                                        11109500
      *1200-EXIT.                                                       11109600
                                                                        11109700
      *--------------------------------------------------------------*  11109800
      * 1400-VALIDATE-DATE.                                             11109900
      *     VALIDATE THE INPUT DATE FROM CONTROL CARD                   11110000
      *--------------------------------------------------------------*  11110100
       1400-VALIDATE-DATE.                                              11110200
                                                                        11110300
           INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.         11110400
           SET  DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                     11110500
           SET  DPG52-LK-DTE-ISO-FMT       TO TRUE.                     11110600
           MOVE PV-WORK-DATE               TO DPG52-LK-DATE-INPUT.      11110700
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    11110800
                                                   DPG54 DPG55 DPG56.   11110900
           EVALUATE TRUE                                                11111000
               WHEN DPG54-NO-ERROR                                      11111100
                   DISPLAY 'CONTROL CARD NEXT NEW DATE VALID: '         11111200
                      DPG55-DB2-ISO-DATE-FMT                            11111300
               WHEN DPG54-SEVERE-ERROR                                  11111400
                   DISPLAY ' '                                          11111500
                   DISPLAY '** ABEND ** IN EPKUP356 '                   11111699
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'    11111700
                   DISPLAY '** INVALID DATE :'                          11111800
                            PV-WORK-DATE                                11111900
                   DISPLAY '** CALLED BY 1400-VALIDATE-DATE '           11112000
                   DISPLAY '** ERROR MESSAGE = '                        11112100
                                     DPG54-ERROR-MESSAGE                11112200
                   DISPLAY '** DPG51 = ' DPG51                          11112300
                   DISPLAY '** DPG52 = ' DPG52                          11112400
                   DISPLAY '** DPG53 = ' DPG53                          11112500
                   DISPLAY '** DPG54 = ' DPG54                          11112600
                   DISPLAY '** DPG55 = ' DPG55                          11112700
                   DISPLAY '** DPG56 = ' DPG56                          11112800
                   MOVE '1400-VALIDATE-DATE '                           11112900
                                        TO PV-PARAGRAPH-NAME            11113000
                   MOVE 'ERROR VALIDATING DATE '                        11113100
                                        TO PV-OPERATION-MSG-1           11113200
                   MOVE DPG54-ERROR-MESSAGE  TO PV-OPERATION-MSG-2      11113300
                   MOVE 4019 TO ABEND-CODE                              11113400
                   CALL 'ILBOABN0' USING ABEND-CODE                     11113500
           END-EVALUATE.                                                11113600
                                                                        11113700
      *1400-EXIT.                                                       11113800
                                                                        11113900
      ******************************************************************11114000
      *2000-OPEN-XS-PRC-CSR.                                            11114100
      ******************************************************************11114200
       2000-OPEN-XS-PRC-CSR.                                            11114300
                                                                        11114400
           EXEC SQL                                                     11114500
               OPEN XS-PRC-CSR                                          11114600
           END-EXEC.                                                    11115000
                                                                        11116000
           EVALUATE TRUE                                                11117000
               WHEN SQLCODE = ZERO                                      11118000
                   CONTINUE                                             11119000
                                                                        11119100
               WHEN OTHER                                               11119200
                   MOVE '2000-OPEN-XS-PRC-CSR'                          11119400
                                    TO PV-PARAGRAPH-NAME                11119500
                   PERFORM 9000-SQL-ABEND                               11119600
           END-EVALUATE.                                                11119700
                                                                        11119800
      ******************************************************************11900000
      * LOCK THE WORK TABLE                                             11910099
      ******************************************************************11920000
       2050-LOCK-WORK-TABLE.                                            11930099
                                                                        11970000
           EXEC SQL                                                     11970199
               LOCK TABLE XSTW_STR_GP_PRCHG_EXTR IN EXCLUSIVE MODE      11970299
           END-EXEC.                                                    11970399
                                                                        11970499
           EVALUATE TRUE                                                11970599
               WHEN SQLCODE = ZERO                                      11970699
                   CONTINUE                                             11970799
                                                                        11970899
               WHEN OTHER                                               11970999
                   MOVE '2050-LOCK-WORK-TABLE' TO PV-PARAGRAPH-NAME     11971099
                   PERFORM 9000-SQL-ABEND                               11971399
           END-EVALUATE.                                                11971499
                                                                        11971599
      ******************************************************************11971699
      * GET SIGN/CLUSTER INFO                                           11972099
      ******************************************************************11973099
       2100-FETCH-XS-PRC-CSR.                                           11974099
                                                                        11975099
           EXEC SQL                                                     11980000
               FETCH XS-PRC-CSR                                         11990000
                INTO :XST00064-STR-GP-TYP-CDE                           12000000
                   , :XST00064-STR-GP-ID                                12010000
                   , :XST00064-PRCHG-EFF-DTE                            12020000
                   , :XST00063-PRCHG-ID                                 12030000
                   , :XST00063-PRCHG-TYP-CDE                            12040000
           END-EXEC.                                                    12113000
                                                                        12114000
           EVALUATE TRUE                                                12120000
               WHEN SQLCODE = +0                                        12130000
               WHEN SQLCODE = -811                                      12140000
                   CONTINUE                                             12141000
                                                                        12160000
               WHEN SQLCODE = +100                                      12170000
                   SET PS-END-OF-CSR TO TRUE                            12180000
                                                                        12190000
               WHEN OTHER                                               12200000
                   MOVE '2100-FETCH-XS-PRC-CSR' TO PV-PARAGRAPH-NAME    12210000
                                                                        12230000
                   PERFORM 9000-SQL-ABEND                               12240000
           END-EVALUATE.                                                12250000
                                                                        12260000
           IF NOT PS-END-OF-CSR                                         12261000
              ADD +1 TO PV-XS-ROWS-FETCHED                              12270000
              PERFORM 2200-INSERT-INTO-TEMP                             12280000
           END-IF.                                                      12290000
                                                                        12300000
      ******************************************************************12320000
      *2200-INSERT-INTO-TEMP.                                           12321000
      ******************************************************************12321100
       2200-INSERT-INTO-TEMP.                                           12321200
                                                                        12322000
             EXEC SQL                                                   12323000
                INSERT INTO XSTW_STR_GP_PRCHG_EXTR                      12324099
                       (STR_GP_TYP_CDE                                  12326100
                      , STR_GP_ID                                       12326200
                      , PRCHG_EFF_DTE                                   12326300
                      , PRCHG_ID                                        12326400
                      , PRCHG_TYP_CDE)                                  12326500
                VALUES                                                  12329600
                       (:XST00064-STR-GP-TYP-CDE                        12329700
                      , :XST00064-STR-GP-ID                             12329800
                      , :XST00064-PRCHG-EFF-DTE                         12329900
                      , :XST00063-PRCHG-ID                              12330000
                      , :XST00063-PRCHG-TYP-CDE)                        12330100
            END-EXEC.                                                   12331900
                                                                        12332000
           EVALUATE TRUE                                                12332100
               WHEN SQLCODE = +0                                        12332200
                   ADD +1               TO PA-ROWS-INSERTED             12332300
                                                                        12332400
               WHEN SQLCODE = -803                                      12332500
                   ADD +1              TO PA-ROWS-DUP-INSERTED          12332600
                    DISPLAY '** DUP INSERT **'                          12332700
                    DISPLAY 'PRCHG-ID         :' XST00063-PRCHG-ID      12332800
                    DISPLAY 'XST00063-DEPT-NBR:' XST00063-DEPT-NBR      12333000
                    DISPLAY 'PRCHG-TYP-CDE    :' XST00063-PRCHG-TYP-CDE 12333200
               WHEN OTHER                                               12334500
                    DISPLAY 'PRCHG-ID          ' XST00063-PRCHG-ID      12334600
                    DISPLAY 'XST00063-DEPT-NBR ' XST00063-DEPT-NBR      12334700
                    DISPLAY 'PRCHG-TYP-CDE     ' XST00063-PRCHG-TYP-CDE 12334800
                    MOVE '2200-INSERT-INTO-TEMP'                        12336200
                                     TO PV-PARAGRAPH-NAME               12336300
                    PERFORM 9000-SQL-ABEND                              12336600
           END-EVALUATE.                                                12336700
                                                                        12337000
      ******************************************************************12430000
      * CLOSE SIGN/CLUSTER CURSOR                                       12440000
      ******************************************************************12450000
       2300-CLS-XS-PRC-CSR.                                             12460000
                                                                        12470000
           EXEC SQL                                                     12480000
               CLOSE XS-PRC-CSR                                         12490000
           END-EXEC.                                                    12500000
                                                                        12510000
           EVALUATE TRUE                                                12520000
               WHEN SQLCODE = ZERO                                      12530000
                   CONTINUE                                             12540000
                                                                        12550000
               WHEN OTHER                                               12560000
                   MOVE '2300-CLS-XS-PRC-CSR'                           12570000
                                    TO PV-PARAGRAPH-NAME                12580000
                   PERFORM 9000-SQL-ABEND                               12590000
           END-EVALUATE.                                                12600000
                                                                        12610000
      ******************************************************************34800100
      * KA-BOOM                                                         34800200
      ******************************************************************34800300
       9000-SQL-ABEND.                                                  34800400
                                                                        34800500
           DISPLAY '9000-SQL-ABEND'.                                    34800600
           DISPLAY '**  ABEND     **'.                                  34800700
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              34800800
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            34800900
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-MSG-1.           34801000
           DISPLAY '**            **  = ' PV-OPERATION-MSG-2.           34801100
                                                                        34801200
      ******************************************************************34801300
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    34801400
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         34801500
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     34801600
      * FORMAT.                                                         34801700
      ******************************************************************34801800
           COPY DPPD004.                                                34801900
                                                                        34802000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            34802100
           CALL 'ILBOABN0' USING ABEND-CODE.                            34802200
                                                                        34802300
       9999-WRAPUP.                                                     34802400
           CLOSE CC-PREV-NEXT-NEW-DAY,                                  34802500
                 CC-TOMORROW.                                           34803000
                                                                        34809200
           DISPLAY ' '.                                                 34809300
           DISPLAY '*************************************************'. 34809400
           DISPLAY ' '.                                                 34809500
           DISPLAY 'NUMBER OF ROWS FETCHED            :      '          34809600
                    PV-XS-ROWS-FETCHED.                                 34809700
           DISPLAY 'NUMBER OF ROWS INSERTED           :      '          34809899
                    PA-ROWS-INSERTED.                                   34809900
           DISPLAY 'NUMBER OF DUPLICATES              :      '          34810099
                    PA-ROWS-DUP-INSERTED.                               34810100
