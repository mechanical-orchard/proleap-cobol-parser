/*  REXX    */                                                                  
CURRPGM = ' SEKRX001 - '                                                        
"EXECIO * DISKR INP01  (STEM REQRESC. FINIS)"                                   
IF RC /=0 & RC /= 2 THEN                                                        
DO                                                                              
  SAY CURRPGM 'ERROR' RC 'READING INP01 DATASET.'                               
  EXIT 4                                                                        
END                                                                             
DO I=1 TO REQRESC.0                                                             
  INP01 = INP01 LEFT(REQRESC.I,52)                                              
END                                                                             
INP02_EOF = 'N'                                                                 
CALL READ_RACF_UNLOAD                                                           
DO WHILE (INP02_EOF = 'N')                                                      
    PARSE PULL RACF_RCD                                                         
    RECORD_TYPE = SUBSTR(RACF_RCD,1,4)                                          
    SELECT                                                                      
            /* "DATASET ACCESS" */                                              
        WHEN (RECORD_TYPE = '0404') THEN CALL DATASET_ACCESS                    
            /* "GENERAL RESOURCE ACCESS" - RESOURCE ACCESS */                   
        WHEN (RECORD_TYPE = '0505') THEN CALL RESOURCE_ACCESS                   
            /* "DATASET BASIC DATA" - DATASET PROFILE DEFINITION */             
        WHEN (RECORD_TYPE = '0400') THEN CALL DATASET_DEFN                      
            /* "GENERAL RESOURCE BASIC DATA" - RESOURCE DEFINITION */           
        WHEN (RECORD_TYPE = '0500') THEN CALL RESOURCE_DEFN                     
            /* "GROUP MEMBERS" - USER GROUP/USERID CONNECTION */                
        WHEN (RECORD_TYPE = '0102') THEN CALL GROUP_USER_ASSOC                  
            /* "GROUP BASIC DATA" - USER GROUP DEFINITION */                    
        WHEN (RECORD_TYPE = '0100') THEN CALL USER_GROUP_DEFN                   
            /* "USER BASIC DATA" - USER DEFINITION */                           
        WHEN (RECORD_TYPE = '0200') THEN CALL USER_DEFN                         
        OTHERWISE NOP                                                           
    END                                                                         
    CALL READ_RACF_UNLOAD                                                       
END                                                                             
"EXECIO 0 DISKR INP02 (FINIS"                                                   
"EXECIO 0 DISKR OUT01 (FINIS"                                                   
"EXECIO 0 DISKR OUT02 (FINIS"                                                   
"EXECIO 0 DISKR OUT03 (FINIS"                                                   
"EXECIO 0 DISKR OUT04 (FINIS"                                                   
"EXECIO 0 DISKR OUT05 (FINIS"                                                   
RETURN                                                                          
/*----------------------------------------------------------------*/            
DATASET_ACCESS:                                                                 
    RESOURCE_CLASS = 'DATASET'                                                  
    RESOURCE_CLASS = LEFT(RESOURCE_CLASS,8)                                     
    RESOURCE_NAME = SUBSTR(RACF_RCD,6,44)                                       
    GRANTED_TO = SUBSTR(RACF_RCD,58,8)                                          
    ACCESS_LEVEL  = SUBSTR(RACF_RCD,67,8)                                       
    REQUESTED_RESOURCE = RESOURCE_CLASS || RESOURCE_NAME                        
    IF WORDPOS(REQUESTED_RESOURCE,INP01) > 0 THEN DO                            
        ACCESS_RCD = RESOURCE_CLASS || RESOURCE_NAME ||,                        
                  GRANTED_TO || ACCESS_LEVEL                                    
        CALL WRITE_ACCESS_FILE                                                  
    END                                                                         
RETURN                                                                          
/*----------------------------------------------------------------*/            
RESOURCE_ACCESS:                                                                
    RESOURCE_CLASS = SUBSTR(RACF_RCD,253,8)                                     
    RESOURCE_NAME = SUBSTR(RACF_RCD,6,44)                                       
    GRANTED_TO = SUBSTR(RACF_RCD,262,8)                                         
    ACCESS_LEVEL  = SUBSTR(RACF_RCD,271,8)                                      
    REQUESTED_RESOURCE = RESOURCE_CLASS || RESOURCE_NAME                        
    IF WORDPOS(REQUESTED_RESOURCE,INP01) > 0 THEN DO                            
        ACCESS_RCD = RESOURCE_CLASS || RESOURCE_NAME ||,                        
                  GRANTED_TO || ACCESS_LEVEL                                    
        CALL WRITE_ACCESS_FILE                                                  
    END                                                                         
RETURN                                                                          
/*----------------------------------------------------------------*/            
DATASET_DEFN:                                                                   
    RESOURCE_CLASS = 'DATASET'                                                  
    RESOURCE_CLASS = LEFT(RESOURCE_CLASS,8)                                     
    RESOURCE_NAME = SUBSTR(RACF_RCD,6,44)                                       
    UNIVERSAL_ACCESS = SUBSTR(RACF_RCD,129,8)                                   
    DESCRIPTION_1 = SUBSTR(RACF_RCD,192,255) /* VERIFY OFFSETS */               
    DESCRIPTION_2 = LEFT('',255)                                                
    REQUESTED_RESOURCE = RESOURCE_CLASS || RESOURCE_NAME                        
    IF WORDPOS(REQUESTED_RESOURCE,INP01) > 0 THEN DO                            
        RESOURCE_RCD = RESOURCE_CLASS || RESOURCE_NAME ||,                      
                  UNIVERSAL_ACCESS ||,                                          
                  DESCRIPTION_1 || DESCRIPTION_2                                
        CALL WRITE_RESOURCE_FILE                                                
    END                                                                         
RETURN                                                                          
/*----------------------------------------------------------------*/            
RESOURCE_DEFN:                                                                  
    RESOURCE_CLASS = SUBSTR(RACF_RCD,253,8)                                     
    RESOURCE_NAME = SUBSTR(RACF_RCD,6,44)                                       
    UNIVERSAL_ACCESS = SUBSTR(RACF_RCD,337,8)                                   
    DESCRIPTION_1 = SUBSTR(RACF_RCD,368,255)                                    
    DESCRIPTION_2 = SUBSTR(RACF_RCD,754,255)                                    
    REQUESTED_RESOURCE = RESOURCE_CLASS || RESOURCE_NAME                        
    IF WORDPOS(REQUESTED_RESOURCE,INP01) > 0 THEN DO                            
        RESOURCE_RCD = RESOURCE_CLASS || RESOURCE_NAME ||,                      
                  UNIVERSAL_ACCESS ||,                                          
                  DESCRIPTION_1 || DESCRIPTION_2                                
        CALL WRITE_RESOURCE_FILE                                                
    END                                                                         
RETURN                                                                          
/*----------------------------------------------------------------*/            
GROUP_USER_ASSOC:                                                               
    USER_GROUP = SUBSTR(RACF_RCD,6,8)                                           
    USER_ID = SUBSTR(RACF_RCD,15,8)                                             
    GROUP_ASSOC_RCD = USER_GROUP || USER_ID                                     
    CALL WRITE_GROUP_ASSOC_FILE                                                 
RETURN                                                                          
/*----------------------------------------------------------------*/            
USER_GROUP_DEFN:                                                                
    USER_GROUP = SUBSTR(RACF_RCD,6,8)                                           
    USER_GROUP_INST_DATA = SUBSTR(RACF_RCD,58,255)                              
    DESC_POSITION = POS('DESC(',USER_GROUP_INST_DATA)                           
    IF (DESC_POSITION > 0) THEN DO                                              
        DESC_POSITION = DESC_POSITION + 5                                       
        DESC_TEMP = SUBSTR(USER_GROUP_INST_DATA,DESC_POSITION)                  
        END_POSITION = POS(')',DESC_TEMP)                                       
        IF (END_POSITION > 1) THEN DO                                           
            END_POSITION = END_POSITION - 1                                     
            USER_GROUP_DESC = LEFT(SUBSTR(DESC_TEMP,1,END_POSITION),92)         
        END                                                                     
        ELSE                                                                    
            USER_GROUP_DESC = LEFT('',92)                                       
    END                                                                         
    ELSE                                                                        
        USER_GROUP_DESC = LEFT('',92)                                           
    USER_GROUP_RCD = USER_GROUP || USER_GROUP_DESC                              
    CALL WRITE_USER_GROUP_FILE                                                  
RETURN                                                                          
/*----------------------------------------------------------------*/            
USER_DEFN:                                                                      
    USER_ID = SUBSTR(RACF_RCD,6,8)                                              
    PROGRAMMER_NAME = SUBSTR(RACF_RCD,75,20)                                    
    USER_INST_DATA = SUBSTR(RACF_RCD,125,255)                                   
    NAME_POSITION = POS('NAME(',USER_INST_DATA)                                 
    IF (NAME_POSITION > 0) THEN DO                                              
        NAME_POSITION = NAME_POSITION + 5                                       
        NAME_TEMP = SUBSTR(USER_INST_DATA,NAME_POSITION)                        
        END_POSITION = POS(')',NAME_TEMP)                                       
        IF (END_POSITION > 1) THEN DO                                           
            END_POSITION = END_POSITION - 1                                     
            USER_NAME = LEFT(SUBSTR(NAME_TEMP,1,END_POSITION),40)               
        END                                                                     
        ELSE                                                                    
            USER_NAME = LEFT('',40)                                             
    END                                                                         
    ELSE                                                                        
        USER_NAME = LEFT('',40)                                                 
    IF STRIP(USER_NAME) = '' THEN                                               
        USER_NAME = PROGRAMMER_NAME                                             
    CAT_POSITION = POS('CAT(',USER_INST_DATA)                                   
    IF (CAT_POSITION > 0) THEN DO                                               
        CAT_POSITION = CAT_POSITION + 4                                         
        CAT_TEMP = SUBSTR(USER_INST_DATA,CAT_POSITION)                          
        END_POSITION = POS(')',CAT_TEMP)                                        
        IF (END_POSITION > 1) THEN DO                                           
            END_POSITION = END_POSITION - 1                                     
            USER_CATEGORY = LEFT(SUBSTR(CAT_TEMP,1,END_POSITION),8)             
        END                                                                     
        ELSE                                                                    
            USER_CATEGORY = LEFT('',8)                                          
    END                                                                         
    ELSE                                                                        
        USER_CATEGORY = LEFT('',8)                                              
    EMPLID_POSITION = POS('EMPLID(',USER_INST_DATA)                             
    IF (EMPLID_POSITION > 0) THEN DO                                            
        EMPLID_POSITION = EMPLID_POSITION + 7                                   
        EMPLID_TEMP = SUBSTR(USER_INST_DATA,EMPLID_POSITION)                    
        END_POSITION = POS(')',EMPLID_TEMP)                                     
        IF (END_POSITION > 1) THEN DO                                           
            END_POSITION = END_POSITION - 1                                     
            USER_EMPLID = LEFT(SUBSTR(EMPLID_TEMP,1,END_POSITION),7)            
        END                                                                     
        ELSE                                                                    
            USER_EMPLID = LEFT('',7)                                            
    END                                                                         
    ELSE                                                                        
        USER_EMPLID = LEFT('',7)                                                
    USER_RCD = USER_ID || USER_NAME || USER_CATEGORY || USER_EMPLID             
    CALL WRITE_USER_FILE                                                        
RETURN                                                                          
/*----------------------------------------------------------------*/            
READ_RACF_UNLOAD:                                                               
    "EXECIO 1 DISKR INP02"              /* READ ONE RECORD        */            
    IF RC = 2 THEN                                                              
        INP02_EOF = 'Y'                                                         
    RETURN                                                                      
/*----------------------------------------------------------------*/            
WRITE_ACCESS_FILE:                                                              
  OUT01_RECORD = LEFT(ACCESS_RCD,68)                                            
  PUSH OUT01_RECORD                                                             
  "EXECIO 1 DISKW OUT01"                                                        
RETURN                                                                          
/*----------------------------------------------------------------*/            
WRITE_RESOURCE_FILE:                                                            
  OUT02_RECORD = LEFT(RESOURCE_RCD,570)                                         
  PUSH OUT02_RECORD                                                             
  "EXECIO 1 DISKW OUT02"                                                        
RETURN                                                                          
/*----------------------------------------------------------------*/            
WRITE_GROUP_ASSOC_FILE:                                                         
  OUT03_RECORD = LEFT(GROUP_ASSOC_RCD,16)                                       
  PUSH OUT03_RECORD                                                             
  "EXECIO 1 DISKW OUT03"                                                        
RETURN                                                                          
/*----------------------------------------------------------------*/            
WRITE_USER_GROUP_FILE:                                                          
  OUT04_RECORD = LEFT(USER_GROUP_RCD,100)                                       
  PUSH OUT04_RECORD                                                             
  "EXECIO 1 DISKW OUT04"                                                        
RETURN                                                                          
/*----------------------------------------------------------------*/            
WRITE_USER_FILE:                                                                
  OUT05_RECORD = LEFT(USER_RCD,63)                                              
  PUSH OUT05_RECORD                                                             
  "EXECIO 1 DISKW OUT05"                                                        
RETURN                                                                          
