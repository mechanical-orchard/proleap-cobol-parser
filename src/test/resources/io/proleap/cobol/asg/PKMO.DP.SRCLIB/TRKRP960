       IDENTIFICATION DIVISION.                                                 
      *                                                                         
       PROGRAM-ID.             TRKRP960.                                        
       AUTHOR.                 BALAMURUGAN.                                     
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           APRIL 2013.                                      
       DATE-COMPILED.                                                           
      *                                                                         
      *---------------------------------------------------------------*         
      * DATE:        04/26/2013                                                 
      * PROJ#:       QMF MIGRATION                                              
      * WR/IR#:      CHG0052305                                                 
      * PROGRAMMER:  BALAMURUGAN (COGNIZANT)                                    
      *                                                                         
      *  PROGRAM NARRATIVE:                                                     
      *  ------- ----------                                                     
      *  THE FILE WHICH IS CREATED BY THE SQL UNLOAD STEP IS USED               
      *  AS INPUT FOR THIS PROGRAM. THIS PROGRAM READS THE INPUT FILE           
      *  UNTIL IT REACHES ITS END AND PRODUCES THE REPORT "DECON                
      *  CONTROL PANEL FOR LAST WEEK AND FORWARD".                              
      *  IF THE INPUT FILE IS EMPTY, THEN THE PROGRAM WRITES                    
      *  "NO DATA TO REPORT" IN THE REPORT. THE BREAK ARE HANDLED               
      *  AT THE "VESSEL" LEVEL AND "ETA" LEVEL.                                 
      *---------------------------------------------------------------*         
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.                   IBM-3090.                             
       OBJECT-COMPUTER.                   IBM-3090.                             
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT RUL-CNSTR-ADJ-INP-FILE  ASSIGN TO INP01.                      
      *                                                                         
           SELECT RUL-CNSTR-ADJ-OUT-REPT  ASSIGN TO RPT01.                      
      *                                                                         
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  RUL-CNSTR-ADJ-INP-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RUL-CNSTR-ADJ-FILE-RECORD      PIC  X(183).                          
      *                                                                         
       FD  RUL-CNSTR-ADJ-OUT-REPT                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RUL-CNSTR-ADJ-REPT-RECORD      PIC  X(132).                          
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
      *****************************************************************         
      **   PROGRAM CONSTANTS                                                    
      *****************************************************************         
       01 PC-PROGRAM-CONSTANTS.                                                 
          05  PC-CURRENT-PGM              PIC  X(08)   VALUE 'TRKRP960'.        
          05  PC-TOTL-LIMIT               PIC  9(02)   VALUE 60.                
          05  PC-ONE                      PIC  9(01)   VALUE 1.                 
      *                                                                         
      *****************************************************************         
      **   PROGRAM SWITCHES                                                     
      *****************************************************************         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SWITCH      PIC  X(01)   VALUE 'N'.               
               88  PS-EOF                              VALUE 'Y'.               
               88  PS-NOT-EOF                          VALUE 'N'.               
      *                                                                         
      *****************************************************************         
      **   PROGRAM VARIABLES                                                    
      *****************************************************************         
       01  PV-VARIABLES.                                                        
           05  PV-RPT-LINE-CNT            PIC 9(03)    VALUE 65.                
           05  PV-RPT-PAGE-CNT            PIC 9(03)    VALUE ZEROS.             
           05  PV-RECORDS-READ            PIC S9(07)   VALUE +0 COMP-3.         
           05  PV-RECORDS-WRITTEN         PIC S9(07)   VALUE +0 COMP-3.         
           05  PV-RUL-VESSEL-PREV         PIC X(30)    VALUE SPACES.            
           05  PV-RUL-ETA-PREV            PIC X(10)    VALUE SPACES.            
           05  PV-CHANGE-IND01            PIC X(01)    VALUE 'N'.               
           05  PV-DISP-BLK-LINE           PIC X(01)    VALUE 'Y'.               
      *                                                                         
           05  PV-PD-DTE.                                                       
               10  PV-PD-CY               PIC 9(04)    VALUE ZERO.              
               10  FILLER                 PIC X(01).                            
               10  PV-PD-MM               PIC 9(02)    VALUE ZERO.              
               10  FILLER                 PIC X(01).                            
               10  PV-PD-DD               PIC 9(02)    VALUE ZERO.              
      *                                                                         
       01  SYSTEM-DATE-TIME.                                                    
           05  SD-SYSTEM-DATE.                                                  
               10  SD-CD-CCYY             PIC 9(04)    VALUE ZEROS.             
               10  SD-CD-MM               PIC 9(02)    VALUE ZEROS.             
               10  SD-CD-DD               PIC 9(02)    VALUE ZEROS.             
           05  ST-SYSTEM-TIME.                                                  
               10  ST-CT-HH               PIC 9(02)    VALUE ZEROS.             
               10  ST-CT-MM               PIC 9(02)    VALUE ZEROS.             
               10  FILLER                 PIC 9(04)    VALUE ZEROS.             
      *                                                                         
      ******************************************************************        
      **   TRRD960 - INPUT FILE LAYOUT                                          
      ******************************************************************        
           COPY TRRD960.                                                        
      *                                                                         
      *****************************************************************         
      **   STANDARD HEADING LAYOUT - 132 COLUMNS                                
      *****************************************************************         
           COPY DPWS132.                                                        
      *                                                                         
       01  PH1-PAGE-HEADING.                                                    
           05  FILLER                     PIC X(44)    VALUE SPACES.            
           05  FILLER                     PIC X(45)    VALUE                    
               'DECON CONTROL PANEL FOR LAST WEEK AND FORWARD'.                 
           05  FILLER                     PIC X(43)    VALUE SPACES.            
      *                                                                         
       01  DH1-DETAIL-HEADING-1.                                                
           05  FILLER                     PIC X(12)    VALUE SPACES.            
           05  FILLER                     PIC X(03)    VALUE                    
               'RUL'.                                                           
           05  FILLER                     PIC X(106)   VALUE SPACES.            
           05  FILLER                     PIC X(04)    VALUE                    
               'RULE'.                                                          
           05  FILLER                     PIC X(07)    VALUE SPACES.            
      *                                                                         
       01  DH1-DETAIL-HEADING-2.                                                
           05  FILLER                     PIC X(05)    VALUE SPACES.            
           05  FILLER                     PIC X(03)    VALUE                    
               'RUL'.                                                           
           05  FILLER                     PIC X(03)    VALUE SPACES.            
           05  FILLER                     PIC X(05)    VALUE                    
               'CNSTR'.                                                         
           05  FILLER                     PIC X(02)    VALUE SPACES.            
           05  FILLER                     PIC X(03)    VALUE                    
               'RUL'.                                                           
           05  FILLER                     PIC X(02)    VALUE SPACES.            
           05  FILLER                     PIC X(03)    VALUE                    
               'RUL'.                                                           
           05  FILLER                     PIC X(62)    VALUE SPACES.            
           05  FILLER                     PIC X(04)    VALUE                    
               'RULE'.                                                          
           05  FILLER                     PIC X(06)    VALUE SPACES.            
           05  FILLER                     PIC X(04)    VALUE                    
               'WEEK'.                                                          
           05  FILLER                     PIC X(07)    VALUE SPACES.            
           05  FILLER                     PIC X(04)    VALUE                    
               'WEEK'.                                                          
           05  FILLER                     PIC X(07)    VALUE SPACES.            
           05  FILLER                     PIC X(07)    VALUE                    
               'CREATED'.                                                       
           05  FILLER                     PIC X(05)    VALUE SPACES.            
      *                                                                         
       01  DH1-DETAIL-HEADING-3.                                                
           05  FILLER                     PIC X(05)    VALUE SPACES.            
           05  FILLER                     PIC X(02)    VALUE                    
               'ID'.                                                            
           05  FILLER                     PIC X(05)    VALUE SPACES.            
           05  FILLER                     PIC X(03)    VALUE                    
               'CDE'.                                                           
           05  FILLER                     PIC X(02)    VALUE SPACES.            
           05  FILLER                     PIC X(05)    VALUE                    
               'CNSTR'.                                                         
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  FILLER                     PIC X(03)    VALUE                    
               'TYP'.                                                           
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  FILLER                     PIC X(05)    VALUE                    
               'DECON'.                                                         
           05  FILLER                     PIC X(02)    VALUE SPACES.            
           05  FILLER                     PIC X(06)    VALUE                    
               'VESSEL'.                                                        
           05  FILLER                     PIC X(12)    VALUE SPACES.            
           05  FILLER                     PIC X(09)    VALUE                    
               'CONTAINER'.                                                     
           05  FILLER                     PIC X(09)    VALUE SPACES.            
           05  FILLER                     PIC X(03)    VALUE                    
               'POE'.                                                           
           05  FILLER                     PIC X(03)    VALUE SPACES.            
           05  FILLER                     PIC X(03)    VALUE                    
               'ETA'.                                                           
           05  FILLER                     PIC X(08)    VALUE SPACES.            
           05  FILLER                     PIC X(07)    VALUE                    
               'CREATED'.                                                       
           05  FILLER                     PIC X(04)    VALUE SPACES.            
           05  FILLER                     PIC X(05)    VALUE                    
               'BEGIN'.                                                         
           05  FILLER                     PIC X(06)    VALUE SPACES.            
           05  FILLER                     PIC X(03)    VALUE                    
               'END'.                                                           
           05  FILLER                     PIC X(10)    VALUE SPACES.            
           05  FILLER                     PIC X(02)    VALUE                    
               'BY'.                                                            
           05  FILLER                     PIC X(09)    VALUE SPACES.            
      *                                                                         
       01  DH1-DETAIL-HEADING-4.                                                
           05  FILLER                     PIC X(45)    VALUE                    
               '--------  ------ ----- --- ------ -----------'.                 
           05  FILLER                     PIC X(44)    VALUE                    
               '------ ----------------- ----- ---------- --'.                  
           05  FILLER                     PIC X(39)    VALUE                    
               '-------- ---------- ---------- --------'.                       
           05  FILLER                     PIC X(04)    VALUE SPACES.            
      *                                                                         
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                     PIC X(02)    VALUE SPACES.            
           05  DL-RUL-ID                  PIC Z(05)9   VALUE ZEROS.             
           05  FILLER                     PIC X(02)    VALUE SPACES.            
           05  DL-RUL-CNSTR-CDE           PIC -Z(04)9  VALUE ZEROS.             
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  DL-RUL-CNSTR-TYP-CDE       PIC X(04)    VALUE SPACES.            
           05  FILLER                     PIC X(02)    VALUE SPACES.            
           05  DL-RUL-CDE                 PIC X(03)    VALUE SPACES.            
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  DL-RUL-CNSTR-VAL-DECON     PIC X(06)    VALUE SPACES.            
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  DL-RUL-CNSTR-VAL-VESSEL    PIC X(17)    VALUE SPACES.            
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  DL-RUL-CNSTR-VAL-CONT      PIC X(17)    VALUE SPACES.            
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  DL-RUL-CNSTR-VAL-POE       PIC X(05)    VALUE SPACES.            
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  DL-RUL-ETA                 PIC X(10)    VALUE SPACES.            
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  DL-RUL-CREATE              PIC X(10)    VALUE SPACES.            
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  DL-RUL-WEEK-BEGIN          PIC X(10)    VALUE SPACES.            
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  DL-RUL-WEEK-END            PIC X(10)    VALUE SPACES.            
           05  FILLER                     PIC X(01)    VALUE SPACES.            
           05  DL-RUL-CRE-BY-USR-ID       PIC X(08)    VALUE SPACES.            
           05  FILLER                     PIC X(04)    VALUE SPACES.            
      *                                                                         
       01  DL-BLANK-LINE.                                                       
           05  FILLER                     PIC X(132)   VALUE SPACES.            
                                                                                
       01  END-OF-REPORT-LINE.                                                  
           05  FILLER                     PIC X(31)    VALUE SPACES.            
           05  FILLER                     PIC X(26)    VALUE                    
               '************************* '.                                    
           05  FILLER                     PIC X(14)    VALUE                    
               'END OF REPORT '.                                                
           05  FILLER                     PIC X(26)    VALUE                    
               '************************* '.                                    
                                                                                
       01  NO-DATA-REPORT-LINE.                                                 
           05  FILLER                     PIC X(42)    VALUE SPACES.            
           05  FILLER                     PIC X(48)    VALUE                    
               '*************  NO DATA TO REPORT  **************'.              
           05  FILLER                     PIC X(42)    VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      *                                                                         
      *****************************************************************         
      **   MAINLINE LOGIC FOR PROGRAM                                           
      *****************************************************************         
      *                                                                         
       0000-MAINLINE.                                                           
      *                                                                         
           PERFORM 1000-INITIALIZE.                                             
      *                                                                         
           PERFORM 2000-PROCESS UNTIL PS-EOF.                                   
      *                                                                         
           PERFORM 6000-END-OF-REPORT.                                          
      *                                                                         
           GOBACK.                                                              
      *                                                                         
      *****************************************************************         
      **   INITIALIZATION PARAGRAPH                                             
      *****************************************************************         
      *                                                                         
       1000-INITIALIZE.                                                         
      *                                                                         
           OPEN INPUT  RUL-CNSTR-ADJ-INP-FILE                                   
                OUTPUT RUL-CNSTR-ADJ-OUT-REPT.                                  
      *                                                                         
           PERFORM 1100-GET-CURR-DATE.                                          
      *                                                                         
           PERFORM 4000-READ-FILE.                                              
      *                                                                         
           IF PS-EOF                                                            
               PERFORM   5000-WRITE-HEADINGS                                    
               WRITE     RUL-CNSTR-ADJ-REPT-RECORD                              
                 FROM    NO-DATA-REPORT-LINE                                    
                   AFTER ADVANCING 3 LINES                                      
           END-IF.                                                              
      *                                                                         
      *****************************************************************         
      **   GET THE CURRENT DATE AND POPULATE INTO SYSTEM VARIABLES              
      *****************************************************************         
      *                                                                         
       1100-GET-CURR-DATE.                                                      
      *                                                                         
           MOVE FUNCTION CURRENT-DATE (1:8) TO SD-SYSTEM-DATE.                  
           MOVE FUNCTION CURRENT-DATE (9:6) TO ST-SYSTEM-TIME.                  
      *                                                                         
           MOVE SD-CD-CCYY                  TO DP132-RUN-YEAR.                  
           MOVE SD-CD-MM                    TO DP132-RUN-MONTH.                 
           MOVE SD-CD-DD                    TO DP132-RUN-DAY.                   
           MOVE ST-CT-HH                    TO DP132-RUN-HOUR.                  
           MOVE ST-CT-MM                    TO DP132-RUN-MINUTE.                
           MOVE PC-CURRENT-PGM              TO DP132-PROGRAM-NAME.              
           MOVE PC-ONE                      TO DP132-REPORT-NUMBER.             
      *                                                                         
      *****************************************************************         
      **   PROCESS EACH CONTROL OR DETAIL ERROR RECORD.                         
      **   WRITE THE REPORT AND READ THE NEXT RECORD.                           
      *****************************************************************         
      *                                                                         
       2000-PROCESS.                                                            
      *                                                                         
           PERFORM 2100-MOVE-DET-RECORD.                                        
      *                                                                         
           PERFORM 3000-WRITE-DETAIL-LINE.                                      
      *                                                                         
           PERFORM 4000-READ-FILE.                                              
      *                                                                         
      ******************************************************************        
      **   MOVE THE VALUES READ FROM THE FILE TO THE REPORT VARIABLES           
      ******************************************************************        
      *                                                                         
       2100-MOVE-DET-RECORD.                                                    
      *                                                                         
           MOVE TR960-RUL-ID                 TO DL-RUL-ID.                      
           MOVE TR960-RUL-CNSTR-CDE          TO DL-RUL-CNSTR-CDE.               
           MOVE TR960-RUL-CNSTR-TYP-CDE      TO DL-RUL-CNSTR-TYP-CDE.           
           MOVE TR960-RUL-CDE                TO DL-RUL-CDE.                     
           MOVE TR960-RUL-CNSTR-VAL-DECON    TO DL-RUL-CNSTR-VAL-DECON.         
      *                                                                         
           IF (TR960-RUL-CNSTR-VAL-VESSEL NOT = PV-RUL-VESSEL-PREV OR           
               PV-RUL-VESSEL-PREV             = SPACES)                         
             MOVE TR960-RUL-CNSTR-VAL-VESSEL TO DL-RUL-CNSTR-VAL-VESSEL         
             MOVE TR960-RUL-CNSTR-VAL-VESSEL TO PV-RUL-VESSEL-PREV              
             MOVE 'Y' TO PV-CHANGE-IND01                                        
           ELSE                                                                 
             MOVE SPACES TO DL-RUL-CNSTR-VAL-VESSEL                             
           END-IF.                                                              
      *                                                                         
           IF TR960-RUL-CNSTR-VAL-CONT = LOW-VALUE THEN                         
             MOVE '-'                        TO DL-RUL-CNSTR-VAL-CONT           
           ELSE                                                                 
             MOVE TR960-RUL-CNSTR-VAL-CONT   TO DL-RUL-CNSTR-VAL-CONT           
           END-IF.                                                              
      *                                                                         
           MOVE TR960-RUL-CNSTR-VAL-POE      TO DL-RUL-CNSTR-VAL-POE.           
      *                                                                         
           IF (TR960-RUL-ETA              NOT = PV-RUL-ETA-PREV    OR           
               PV-RUL-ETA-PREV                = SPACES) OR                      
               PV-CHANGE-IND01 = 'Y'                                            
              MOVE TR960-RUL-ETA             TO DL-RUL-ETA                      
              MOVE TR960-RUL-ETA             TO PV-RUL-ETA-PREV                 
              MOVE 'N' TO PV-CHANGE-IND01                                       
           ELSE                                                                 
             MOVE SPACES TO DL-RUL-ETA                                          
           END-IF.                                                              
      *                                                                         
           MOVE TR960-RUL-CREATE             TO DL-RUL-CREATE.                  
           MOVE TR960-RUL-WEEK-BEGIN         TO DL-RUL-WEEK-BEGIN.              
           MOVE TR960-RUL-WEEK-END           TO DL-RUL-WEEK-END.                
           MOVE TR960-RUL-CRE-BY-USR-ID      TO DL-RUL-CRE-BY-USR-ID.           
      *                                                                         
      *****************************************************************         
      **   WRITE THE DETAIL LINES INTO REPORT                                   
      *****************************************************************         
      *                                                                         
       3000-WRITE-DETAIL-LINE.                                                  
      *                                                                         
           IF PV-RPT-LINE-CNT > PC-TOTL-LIMIT                                   
                PERFORM 5000-WRITE-HEADINGS                                     
                MOVE TR960-RUL-CNSTR-VAL-VESSEL TO                              
                     DL-RUL-CNSTR-VAL-VESSEL                                    
                MOVE TR960-RUL-ETA              TO                              
                     DL-RUL-ETA                                                 
                MOVE 'N'                        TO PV-DISP-BLK-LINE             
           END-IF.                                                              
      *                                                                         
           IF PV-DISP-BLK-LINE = 'Y' THEN                                       
              IF DL-RUL-CNSTR-VAL-VESSEL NOT = SPACES                           
                 WRITE RUL-CNSTR-ADJ-REPT-RECORD                                
                  FROM DL-BLANK-LINE                                            
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                          TO PV-DISP-BLK-LINE             
           END-IF.                                                              
      *                                                                         
           WRITE RUL-CNSTR-ADJ-REPT-RECORD                                      
               FROM DL-DETAIL-LINE.                                             
      *                                                                         
           ADD 2  TO PV-RPT-LINE-CNT.                                           
           ADD 1  TO PV-RECORDS-WRITTEN.                                        
      *                                                                         
      *****************************************************************         
      **   READ THE INPUT FILE                                                  
      *****************************************************************         
      *                                                                         
       4000-READ-FILE.                                                          
      *                                                                         
           READ RUL-CNSTR-ADJ-INP-FILE                                          
             INTO TR960-RECORD                                                  
               AT END                                                           
                  SET PS-EOF TO TRUE                                            
           END-READ.                                                            
      *                                                                         
           IF PS-NOT-EOF                                                        
               ADD +1 TO PV-RECORDS-READ                                        
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      **   WRITE THE HEADING LINES INTO REPORT                                  
      ******************************************************************        
      *                                                                         
       5000-WRITE-HEADINGS.                                                     
      *                                                                         
           ADD  PC-ONE          TO PV-RPT-PAGE-CNT.                             
           MOVE PV-RPT-PAGE-CNT TO DP132-PAGE-NUMBER.                           
      *                                                                         
           WRITE RUL-CNSTR-ADJ-REPT-RECORD                                      
               FROM DP132-STANDRD-HEADER-132-COLS AFTER ADVANCING PAGE.         
      *                                                                         
           WRITE RUL-CNSTR-ADJ-REPT-RECORD                                      
               FROM PH1-PAGE-HEADING     AFTER ADVANCING 2 LINES.               
      *                                                                         
           WRITE RUL-CNSTR-ADJ-REPT-RECORD                                      
               FROM DH1-DETAIL-HEADING-1 AFTER ADVANCING 2 LINES.               
      *                                                                         
           WRITE RUL-CNSTR-ADJ-REPT-RECORD                                      
               FROM DH1-DETAIL-HEADING-2.                                       
      *                                                                         
           WRITE RUL-CNSTR-ADJ-REPT-RECORD                                      
               FROM DH1-DETAIL-HEADING-3.                                       
      *                                                                         
           WRITE RUL-CNSTR-ADJ-REPT-RECORD                                      
               FROM DH1-DETAIL-HEADING-4.                                       
      *                                                                         
           MOVE 11 TO PV-RPT-LINE-CNT.                                          
      *                                                                         
      *****************************************************************         
      **   END OF REPORT PROCESSING                                             
      *****************************************************************         
      *                                                                         
       6000-END-OF-REPORT.                                                      
      *                                                                         
           WRITE RUL-CNSTR-ADJ-REPT-RECORD                                      
               FROM END-OF-REPORT-LINE AFTER ADVANCING 3 LINES.                 
      *                                                                         
           DISPLAY '*************************************************'.         
           DISPLAY '*****        RECORD COUNTS FOR TRKRP960    ******'.         
           DISPLAY '***** RECORDS READ FROM INP FILE = '                        
                   PV-RECORDS-READ.                                             
           DISPLAY '***** RECORDS WRITTEN TO REPORT  = '                        
                   PV-RECORDS-WRITTEN.                                          
           DISPLAY '*************************************************'.         
      *                                                                         
           CLOSE RUL-CNSTR-ADJ-INP-FILE                                         
                 RUL-CNSTR-ADJ-OUT-REPT.                                        
                                                                                
