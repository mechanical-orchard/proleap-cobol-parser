       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    SDKUT510.                                                 
       AUTHOR.        KOHLS DEPARTMENT STORES.                                  
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  09-11-01.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *    PURGE DATA FROM THE STORE ADMINISTRATION DATABASES                   
      *    DATA IS DELETED FROM TABLES AT 14 FISCAL PERIODS BACK                
      ******************************************************************        
032105* 03/21/2005 WR28285       JACKIE HELING                                  
032105*   MODIFY CRITERIA FOR SDT_STR_ADD_BCK                                   
102706* 10/27/2006 WR30066/29451 JACKIE HELING                                  
102706*  ADD TABLE SDT_WK_STR_ADDBCK. NAME CHANGE OF SDT_WKLY_STR_TRLR          
102706*   TO SDT_DLY_STR_TRLR, CHANGE CRITERIA.                                 
102706* 10/15/2007 WR32675       JOYCE KNOKE/C KRYSIAK                          
      *   ADD 8 MORE SDT TABLES RELATING TO THE TRUCK ADDBACK PROJECT           
042408* 04/24/2008 WR34706       JOYCE KNOKE                                    
042408*        REMOVED TABLE SDT_STR_ADD_BCK (SDT00003)                         
042408*                      SDT_MNLY_TRLR_TOT (SDT00005)                       
090108* 09/01/2008 WR35115       JACKIE HELING                                  
090108*        ADD TABLE SDT_WKLY_STR_ADJ(SDT00034)                             
100609* 10/06/2009 WR35177       JACKIE HELING                                  
100609*        REMOVE CONTROLLABLE EXPENSE TABLE.  OBSOLETE.                    
100609*        REMOVE WEEK DEPT PAYROLL TABLE.  OBSOLETE.                       
100609*        REMOVE ADD_BCK_TRLR TABLE.  OBSOLETE.                            
100609*                                                                         
100609* 07/21/2022 CHG0272331    JIM HUNTER                                     
100609*        REMOVE REFERENCES TO THE FOLLOWING OBSOLETE TABLES:              
100609*        SDT_DLY_STR_TRLR   (PARA 450-)                                   
100609*        SDT_WKLY_BGT_TOT   (PARA 500-)                                   
100609*        SDT_MNLY_ADD_BCK   (PARA 550-)                                   
100609*        SDT_WK_STR_ADD_BCK (PARA 600-)                                   
100609*        SDT_ADD_BCK_SFT_PM (PARA 650-)                                   
100609*        SDT_WK_ADD_BCK_TYP (PARA 700-)                                   
100609*        SDT_MLT_DAY_UNLD   (PARA 750-)                                   
100609*        SDT_ADD_BCK_VARN   (PARA 800-)                                   
100609*        SDT_MDSE_ADD_BCK   (PARA 850-)                                   
100609*                                                                         
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM                  PIC X(08) VALUE 'SDKUT510'.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARA-NAME                PIC X(20) VALUE SPACES.              
           05  PV-TABLE-NAME               PIC X(20) VALUE SPACES.              
           05  PV-LAST-DAY-IN-PERIOD       PIC X(10) VALUE SPACES.              
           05  PV-DELETE-DATE              PIC X(10) VALUE SPACES.              
           05  PV-COUNT                    PIC S9(06)  VALUE 0 COMP.            
           05  PV-COUNT-WKLY-BGT           PIC S9(06)  VALUE 0 COMP.            
090108     05  PV-COUNT-WKLY-ADJ           PIC S9(06)  VALUE 0 COMP.            
           05  PV-COUNT-STR-RCVG-MDSE      PIC S9(06)  VALUE 0 COMP.            
           05  PV-COUNT-STR-RCVG           PIC S9(06)  VALUE 0 COMP.            
           05  PV-YYYYPP.                                                       
               10  PV-FISCAL-YEAR          PIC 9(04).                           
               10  PV-FISCAL-PERIOD        PIC 9(02).                           
                                                                                
           05  WS-DATE.                                                         
               10 WS-YEAR              PIC 9(2)       VALUE ZEROES.             
               10 WS-MONTH             PIC 9(2)       VALUE ZEROES.             
               10 WS-DAY               PIC 9(2)       VALUE ZEROES.             
                                                                                
           05  WS-DATE-GREG.                                                    
               10 WS-GREG-MONTH        PIC 9(2)       VALUE ZEROES.             
               10 WS-GREG-DAY          PIC 9(2)       VALUE ZEROES.             
               10 WS-GREG-YEAR         PIC 9(2)       VALUE ZEROES.             
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE +0 COMP SYNC.          
           88  AC-DB2-ERROR                        VALUE +4013.                 
           88  AC-DATE-ROUTINE-ERROR               VALUE +4019.                 
                                                                                
      *----------------------------------------------------------------*        
      * PARAMETER LIST FOR THE CALENDAR ROUTINE                                 
      *----------------------------------------------------------------*        
           COPY DPWS500.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *        
      *----------------------------------------------------------------*        
           COPY DPWS004.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * DB2 MESSAGE AREA                                                        
      *----------------------------------------------------------------*        
           COPY SQLCA2.                                                         
                                                                                
      *----------------------------------------------------------------*        
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                         
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      * SDT_WKLY_STR_BGT TABLE                                                  
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE SDT00000                                                 
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
090108* SDT_WKLY_STR_ADJ TABLE                                                  
      *----------------------------------------------------------------*        
090108     EXEC SQL                                                             
090108         INCLUDE SDT00034                                                 
090108     END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      * SDT_STR_RCVG_MDSE TABLE                                                 
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE SDT00033                                                 
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      * SDT_STR_RCVG TABLE                                                      
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE SDT00032                                                 
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
       000-MAIN.                                                                
                                                                                
           PERFORM 100-INIT-HOUSEKEEPING.                                       
           PERFORM 200-DELETE-WKLY-BGT.                                         
032105     PERFORM 1400-COMMIT.                                                 
           PERFORM 950-DELETE-STR-RCVG-MDSE.                                    
           PERFORM 1400-COMMIT.                                                 
           PERFORM 1000-DELETE-STR-RCVG.                                        
           PERFORM 1400-COMMIT.                                                 
090108     PERFORM 1100-DELETE-WKLY-ADJ.                                        
090108     PERFORM 1400-COMMIT.                                                 
           PERFORM 1300-PGM-STATS.                                              
                                                                                
           STOP RUN.                                                            
                                                                                
      *----------------------------------------------------------------         
      *  INITIAL HOUSEKEEPING                                                   
      *----------------------------------------------------------------         
       100-INIT-HOUSEKEEPING.                                                   
                                                                                
      *----------------------------------------------------------------         
      *  DETERMINE CURRENT FISCAL PERIOD                                        
      *----------------------------------------------------------------         
           ACCEPT WS-DATE FROM DATE.                                            
                                                                                
           MOVE WS-YEAR TO WS-GREG-YEAR.                                        
           MOVE WS-MONTH TO WS-GREG-MONTH.                                      
           MOVE WS-DAY TO WS-GREG-DAY.                                          
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
           SET DPG52-LK-DTE-GREG TO TRUE.                                       
           MOVE WS-DATE-GREG TO DPG52-GREG-DATE.                                
                                                                                
           PERFORM 1200-CALL-DATE-ROUTINE.                                      
                                                                                
           EVALUATE TRUE                                                        
              WHEN DPG54-NO-ERROR                                               
                  MOVE DPG56-LAST-FP-DB2-ISO-FMT TO                             
                                              PV-LAST-DAY-IN-PERIOD             
                  MOVE DPG56-FISCAL-PERIOD-NBR TO PV-FISCAL-PERIOD              
                  MOVE DPG56-FISCAL-YEAR-CCYY TO PV-FISCAL-YEAR                 
              WHEN OTHER                                                        
                   MOVE '100-INIT-HOUSEKEEPING' TO PV-PARA-NAME                 
                   DISPLAY '*****************************************'          
                   DISPLAY 'PROGRAM: ' PC-PROGRAM                               
                   DISPLAY 'PARAGRAPH: ' PV-PARA-NAME                           
                   DISPLAY 'DETERMINE CURRENT FISCAL PERIOD'                    
                   DISPLAY 'WARNING: ' DPG54-ERROR-MESSAGE                      
                   DISPLAY 'DPG1 PARAMETER VALUES: ' DPG51                      
                   DISPLAY 'DPG1 PARAMETER VALUES: ' DPG52                      
                   DISPLAY 'DPG1 PARAMETER VALUES: ' DPG53                      
                   DISPLAY '*****************************************'          
                   SET AC-DATE-ROUTINE-ERROR TO TRUE                            
                   CALL 'ILBOABN0' USING ABEND-CODE                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------         
      *  SUBTRACT 14 FISCAL PERIODS IN ORDER TO DETERMINE DATE TO               
      *  DELETE DATA FROM                                                       
      *----------------------------------------------------------------         
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-VALID-FISCAL-CALENDAR TO TRUE.                             
           SET DPG51-DECREMENT-DATE TO TRUE.                                    
           SET DPG52-LK-DTE-FISC-PRD-CC TO TRUE.                                
           MOVE 14 TO DPG51-INCR-DECR-FISCAL-PRD-9.                             
           MOVE PV-YYYYPP TO DPG52-FISCAL-CC-PRD.                               
                                                                                
           PERFORM 1200-CALL-DATE-ROUTINE.                                      
                                                                                
           EVALUATE TRUE                                                        
              WHEN DPG54-NO-ERROR                                               
                  MOVE DPG56-LAST-FP-DB2-ISO-FMT TO PV-DELETE-DATE              
              WHEN OTHER                                                        
                   MOVE '100-INIT-HOUSEKEEPING' TO PV-PARA-NAME                 
                   DISPLAY '*****************************************'          
                   DISPLAY 'PROGRAM: ' PC-PROGRAM                               
                   DISPLAY 'PARAGRAPH: ' PV-PARA-NAME                           
                   DISPLAY 'DETERMINE DELETE DATE'                              
                   DISPLAY 'WARNING: ' DPG54-ERROR-MESSAGE                      
                   DISPLAY 'DPG1 PARAMETER VALUES: ' DPG51                      
                   DISPLAY 'DPG1 PARAMETER VALUES: ' DPG52                      
                   DISPLAY 'DPG1 PARAMETER VALUES: ' DPG53                      
                   DISPLAY '*****************************************'          
                   SET AC-DATE-ROUTINE-ERROR TO TRUE                            
                   CALL 'ILBOABN0' USING ABEND-CODE                             
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------         
      *  DELETE DATA FROM SDT_WKLY_STR_BGT TABLE                                
      *----------------------------------------------------------------         
       200-DELETE-WKLY-BGT.                                                     
                                                                                
           MOVE 0 TO PV-COUNT.                                                  
                                                                                
           EXEC SQL                                                             
             SELECT COUNT(*)                                                    
             INTO :PV-COUNT-WKLY-BGT                                            
             FROM SDT_WKLY_STR_BGT                                              
             WHERE WK_END_DTE <= :PV-DELETE-DATE                                
           END-EXEC.                                                            
                                                                                
           IF PV-COUNT-WKLY-BGT > 0                                             
              EXEC SQL                                                          
                DELETE FROM SDT_WKLY_STR_BGT                                    
                WHERE WK_END_DTE <= :PV-DELETE-DATE                             
              END-EXEC                                                          
              IF SQLCODE = 0                                                    
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE 'SDT_WKLY_STR_BGT' TO PV-TABLE-NAME                       
                 PERFORM 9000-DB2-ABEND                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
102706*----------------------------------------------------------------         
102706*  DELETE DATA FROM SDT_STR_RCVG_MDSE TABLE                               
102706*----------------------------------------------------------------         
       950-DELETE-STR-RCVG-MDSE.                                                
                                                                                
102706     EXEC SQL                                                             
102706       SELECT COUNT(*)                                                    
102706       INTO :PV-COUNT-STR-RCVG-MDSE                                       
102706       FROM SDT_STR_RCVG_MDSE                                             
102706       WHERE TRLR_ARRVL_DTE <= :PV-DELETE-DATE                            
102706     END-EXEC.                                                            
                                                                                
102706     IF PV-COUNT-STR-RCVG-MDSE > 0                                        
102706        EXEC SQL                                                          
102706          DELETE FROM SDT_STR_RCVG_MDSE                                   
102706          WHERE TRLR_ARRVL_DTE <= :PV-DELETE-DATE                         
102706        END-EXEC                                                          
                                                                                
102706        IF SQLCODE = 0                                                    
102706           CONTINUE                                                       
102706        ELSE                                                              
102706           MOVE 'SDT_STR_RCVG_MDSE' TO PV-TABLE-NAME                      
102706           PERFORM 9000-DB2-ABEND                                         
102706        END-IF                                                            
102706     END-IF.                                                              
                                                                                
                                                                                
102706*----------------------------------------------------------------         
102706*  DELETE DATA FROM SDT_STR_RCVG TABLE                                    
102706*----------------------------------------------------------------         
       1000-DELETE-STR-RCVG.                                                    
                                                                                
102706     EXEC SQL                                                             
102706       SELECT COUNT(*)                                                    
102706       INTO :PV-COUNT-STR-RCVG                                            
102706       FROM SDT_STR_RCVG                                                  
102706       WHERE TRLR_ARRVL_DTE <= :PV-DELETE-DATE                            
102706     END-EXEC.                                                            
                                                                                
102706     IF PV-COUNT-STR-RCVG > 0                                             
102706        EXEC SQL                                                          
102706          DELETE FROM SDT_STR_RCVG                                        
102706          WHERE TRLR_ARRVL_DTE <= :PV-DELETE-DATE                         
102706        END-EXEC                                                          
                                                                                
102706        IF SQLCODE = 0                                                    
102706           CONTINUE                                                       
102706        ELSE                                                              
102706           MOVE 'SDT_STR_RCVG' TO PV-TABLE-NAME                           
102706           PERFORM 9000-DB2-ABEND                                         
102706        END-IF                                                            
102706     END-IF.                                                              
                                                                                
                                                                                
090108*----------------------------------------------------------------         
090108*  DELETE DATA FROM SDT_WKLY_STR_ADJ                                      
090108*----------------------------------------------------------------         
090108 1100-DELETE-WKLY-ADJ.                                                    
                                                                                
090108     EXEC SQL                                                             
090108       SELECT COUNT(*)                                                    
090108       INTO :PV-COUNT-WKLY-ADJ                                            
090108       FROM SDT_WKLY_STR_ADJ                                              
090108       WHERE WK_END_DTE <= :PV-DELETE-DATE                                
090108     END-EXEC.                                                            
                                                                                
090108     IF PV-COUNT-WKLY-ADJ > 0                                             
090108        EXEC SQL                                                          
090108          DELETE FROM SDT_WKLY_STR_ADJ                                    
090108          WHERE WK_END_DTE <= :PV-DELETE-DATE                             
090108        END-EXEC                                                          
                                                                                
090108        IF SQLCODE = 0                                                    
090108           CONTINUE                                                       
090108        ELSE                                                              
090108           MOVE 'SDT_WKLY_STR_ADJ' TO PV-TABLE-NAME                       
090108           PERFORM 9000-DB2-ABEND                                         
090108        END-IF                                                            
090108     END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------         
      *  CALL DATE ROUTINE                                                      
      *----------------------------------------------------------------         
       1200-CALL-DATE-ROUTINE.                                                  
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                        
                                                   DPG52                        
                                                   DPG53                        
                                                   DPG54                        
                                                   DPG55                        
                                                   DPG56.                       
                                                                                
                                                                                
      *----------------------------------------------------------------         
      *  DISPLAY PROGRAM STATISTICS                                             
      *----------------------------------------------------------------         
       1300-PGM-STATS.                                                          
                                                                                
           DISPLAY '**********************************************'.            
           DISPLAY 'PROGRAM = ' PC-PROGRAM.                                     
           DISPLAY 'DELETE FROM DATE = ' PV-DELETE-DATE.                        
           DISPLAY 'SDT_WKLY_STR_BGT  ' PV-COUNT-WKLY-BGT                       
                   ' ROWS DELETED'.                                             
090108     DISPLAY 'SDT_WKLY_STR_ADJ  ' PV-COUNT-WKLY-ADJ                       
090108             ' ROWS DELETED'.                                             
102706     DISPLAY 'SDT_STR_RCVG_MDSE  ' PV-COUNT-STR-RCVG-MDSE                 
102706             ' ROWS DELETED'.                                             
102706     DISPLAY 'SDT_STR_RCVG  ' PV-COUNT-STR-RCVG                           
102706             ' ROWS DELETED'.                                             
           DISPLAY '**********************************************'.            
                                                                                
      *----------------------------------------------------------------         
      *  COMMIT CHANGES TO THE DATABASES                                        
      *----------------------------------------------------------------         
       1400-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
                COMMIT                                                          
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------         
      *  DB2 ABEND                                                              
      *----------------------------------------------------------------         
       9000-DB2-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO SQLCODE2.                                            
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           SET AC-DB2-ERROR TO TRUE.                                            
                                                                                
           PERFORM 1300-PGM-STATS.                                              
           DISPLAY '**********************************************'.            
           DISPLAY 'ABEND IN PROGRAM ' PC-PROGRAM.                              
           DISPLAY 'TABLE          = ' PV-TABLE-NAME.                           
           DISPLAY 'SQL ERROR      = ' SQLCODE2.                                
           DISPLAY 'SQLERRM        = ' SQLERRM.                                 
           DISPLAY '**********************************************'.            
      *----------------------------------------------------------------*        
      *  THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-           
      *  MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      *  FORMAT.                                                                
      *----------------------------------------------------------------*        
           COPY DPPD004.                                                        
                                                                                
           CALL 'DSNTIAR' USING SQLCA                                           
                                DP004-FORMATTED-MESSAGE                         
                                DP004-ERROR-LINE-LENGTH.                        
                                                                                
           DISPLAY DP004-ASTERISK-LINE.                                         
                                                                                
           PERFORM                                                              
              VARYING DP004-MSG-IDX                                             
              FROM 1 BY 1                                                       
              UNTIL DP004-MSG-IDX > DP004-MAX-MESSAGE-LINES                     
                  DISPLAY '**'                                                  
                          DP004-MESSAGE-LINE (DP004-MSG-IDX)                    
                          ' **'                                                 
           END-PERFORM.                                                         
                                                                                
           DISPLAY DP004-ASTERISK-LINE.                                         
                                                                                
           IF SQLCODE NOT = -911                                                
               EXEC SQL                                                         
                   COMMIT                                                       
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           SET AC-DB2-ERROR TO TRUE.                                            
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
