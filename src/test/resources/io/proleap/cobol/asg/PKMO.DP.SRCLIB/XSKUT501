      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    XSKUT501.                                                 
       AUTHOR.        STRATAGEM-ESH.                                            
       DATE-WRITTEN.  APRIL 2, 2007                                             
       DATE-COMPILED.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *   ** CREATE TIME STAMP FILE OF FILE NAMES FOR THE MIDTIER **   *        
      *----------------------------------------------------------------*        
      *   THIS PROGRAM PROCESSES A FILE OF FILE NAMES THAT HAVE BEEN            
      *   FTP'D TO THE MIDTIER SERVER AND CREATES A TIMESTAMP FILES IN          
      *   XML FORMAT THAT IS SUBSEQUENTLY FTP'D TO THE MIDTIER. THE             
      *   PROGRAM EXECUTES IN EACH JOB THAT FTP'S THE FILES ASSOCIATED          
      *   WITH A UNIT OF WORK. FOR EXAMPLE. PLU AND XREF ARE PART OF            
      *   THE PRICING UNIT OF WORK.                                             
      *                                                                         
      *----------------------------------------------------------------*        
      *   ** W A R N I N G -- THIS PGM USE NON-CAPITAL LETTERS, SO,    *        
      *                       CAPS ARE TURNED OFF. BE CAREFULL WHEN    *        
      *                       MAKING CODING CHANGES.                   *        
      *----------------------------------------------------------------*        
      * HISTORY LOG:                                                            
      *----------------------------------------------------------------*        
      * DATE:                                                                   
      * WR/IR#:                                                                 
      * PROGRAMMER:                                                             
      * DESCRIPTION:                                                            
      *                                                                         
      * DATE: JAN 2010                                                          
      * WR/IR#: WR #35484                                           *           
      * PROGRAMMER: RICH KELLEN                                                 
      * DESCRIPTION: INITIAL CREATE                                             
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE-01                     ASSIGN TO INP01.            
                                                                                
           SELECT VARIABLE-FILE-01                  ASSIGN TO OUT01.            
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE-01                                                        
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS INPUT-RECORD-01.                                      
                                                                                
       01  INPUT-RECORD-01               PIC X(80).                             
                                                                                
       FD  VARIABLE-FILE-01                                                     
           RECORDING MODE IS V                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORD IS VARYING FROM 1 TO 80                                       
           DEPENDING ON PV-BYTE-COUNT                                           
           DATA RECORD IS VARIABLE-RECORD-01.                                   
                                                                                
       01  VARIABLE-RECORD-01.                                                  
           05 VARIABLE-BYTE              PIC X(01)                              
                                         OCCURS 1 TO 80  TIMES                  
                                         DEPENDING ON PV-BYTE-COUNT.            
                                                                                
      *----------------------------------------------------------------*        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *----------------------------------------------------------------*        
      * PV- PROGRAM STATUS                                             *        
      *----------------------------------------------------------------*        
       01 PV-PROGRAM-STATUS.                                                    
          05  FILLER                     PIC X(27)  VALUE                       
              'WORKING STORAGE STARTS HERE'.                                    
          05  PV-PARAGRAPH-NAME          PIC X(50)  VALUE SPACES.               
          05  PV-DB2-OPERATION           PIC X(50)  VALUE SPACES.               
          05  PV-OPERATION-MSG-1         PIC X(40)  VALUE SPACES.               
          05  PV-OPERATION-MSG-2         PIC X(40)  VALUE SPACES.               
                                                                                
       01  AC-ABEND-CODE                 PIC S9(04) VALUE +0 COMP.              
           88  AC-DB2-ERROR                         VALUE +4013.                
                                                                                
      *----------------------------------------------------------------*        
      * PC- PROGRAM CONSTANTS                                          *        
      *----------------------------------------------------------------*        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'XSKUT501'.            
           05  PC-MAX-RETRIES            PIC 9(01) VALUE 5.                     
                                                                                
      *----------------------------------------------------------------*        
      * PS- PROGRAM SWITCHES                                           *        
      *----------------------------------------------------------------*        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW         PIC X(01)  VALUE 'N'.                  
               88  PS-END-OF-FILE                   VALUE 'Y'.                  
               88  PS-NOT-END-OF-FILE               VALUE 'N'.                  
                                                                                
      *----------------------------------------------------------------*        
      * PV- PROGRAM VARIABLES                                          *        
      *----------------------------------------------------------------*        
       01 PV-PROGRAM-VARIABLES.                                                 
          05  PV-BYTE-COUNT              PIC 9(04) VALUE ZERO.                  
          05  PV-SQL-SUB                 PIC 9(01) VALUE ZERO.                  
          05  PV-TALLY-CNT               PIC 9(02) VALUE ZERO.                  
          05  PV-WORK-REC                PIC X(80) VALUE SPACES.                
          05  PV-INPUT-REC               PIC X(80) VALUE SPACES.                
          05  PV-SPACE                   PIC X(01).                             
                                                                                
          05  PV-VARIABLE-OUTPUT-FIELD.                                         
              10  PV-BIT-VARIABLE        PIC  X(01)                             
                                         VALUE SPACES                           
                                         OCCURS 80 TIMES                        
                                         INDEXED BY                             
                                             PV-BIT-IDX                         
                                             PV-STR-IDX                         
                                             PV-EOF-IDX.                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * PV- PROGRAM COUNT VARIABLES                                    *        
      *----------------------------------------------------------------*        
       01 PV-PROGRAM-COUNTERS.                                                  
          05  PV-RECS-READ               PIC 9(09) VALUE 0.                     
          05  PV-RECS-WRITE              PIC 9(09) VALUE 0.                     
                                                                                
      *----------------------------------------------------------------*        
      * FILE COPYBOOK                                                  *        
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
      * CONTROL CARD COPYBOOK                                          *        
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
      * PD- DB2 VARIABLES (NON DCLGEN)                                 *        
      *----------------------------------------------------------------*        
       01  PD-DB2-VARIABLES.                                                    
           05  PD-DB2-CURRENT-DATE       PIC X(10).                             
           05  PD-DB2-CURRENT-TIME       PIC X(08).                             
           05  PD-DB2-CURRENT-TMST       PIC X(26).                             
                                                                                
      *----------------------------------------------------------------*        
      * DB2 TABLE DCLGEN COPYBOOK                                      *        
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
      * DB2 COMMUNICATIONS AREA                                        *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
      *----------------------------------------------------------------*        
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        *        
      *----------------------------------------------------------------*        
           COPY DPWS004.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * DB2 ABEND COPYBOOK                                             *        
      *----------------------------------------------------------------*        
           COPY DPWS900.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * DB2 MESSAGE AREA                                               *        
      *----------------------------------------------------------------*        
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 0100-INITIALIZATION.                                         
                                                                                
           PERFORM 1000-PROCESS                                                 
             UNTIL PS-END-OF-FILE.                                              
                                                                                
           PERFORM 9200-WRAPUP.                                                 
                                                                                
           GOBACK.                                                              
                                                                                
      *0000-EXIT.                                                               
       EJECT.                                                                   
      ******************************************************************        
      *     INTIALIZES VARIABLES, OPENS ANY FILES, AND PERFORMS        *        
      *     ANY SET-UP ROUTINES.                                       *        
      ******************************************************************        
       0100-INITIALIZATION.                                                     
           MOVE '0100-INITIALIZATION'              TO PV-PARAGRAPH-NAME.        
                                                                                
           PERFORM 9990-GET-CURRENT.                                            
                                                                                
           DISPLAY '****************************************'.                  
           DISPLAY '**          XSKUT501 STARTED          **'.                  
           DISPLAY '****************************************'.                  
           DISPLAY '** CURRENT DATE ' PD-DB2-CURRENT-DATE                       
                   '            **'.                                            
           DISPLAY '** CURRENT TIME ' PD-DB2-CURRENT-TIME                       
                   '              **'.                                          
           DISPLAY '****************************************'.                  
                                                                                
           OPEN INPUT  INPUT-FILE-01                                            
                OUTPUT VARIABLE-FILE-01.                                        
                                                                                
           MOVE '<files>'                TO PV-WORK-REC.                        
           MOVE 7                        TO PV-BYTE-COUNT.                      
           WRITE VARIABLE-RECORD-01      FROM PV-WORK-REC.                      
           ADD 1                         TO PV-RECS-WRITE.                      
                                                                                
      *0100-EXIT.                                                               
       EJECT.                                                                   
      ******************************************************************        
      *     MAIN DRIVING PARAGRAPH OF PROGRAM.                         *        
      ******************************************************************        
       1000-PROCESS.                                                            
           MOVE '1000-PROCESS'                     TO PV-PARAGRAPH-NAME.        
                                                                                
           INITIALIZE PV-INPUT-REC.                                             
                                                                                
           READ INPUT-FILE-01               INTO PV-INPUT-REC                   
               AT END                                                           
                   SET PS-END-OF-FILE       TO TRUE.                            
                                                                                
           IF PS-NOT-END-OF-FILE                                                
              ADD 1                      TO PV-RECS-READ                        
              PERFORM 1100-PROCESS-FILE-NAME                                    
           END-IF.                                                              
                                                                                
      *1000-EXIT.  EXIT.                                                        
       EJECT.                                                                   
      ******************************************************************        
      *     PROCESS NAME FILES TO OUTPUT FILE.                         *        
      ******************************************************************        
       1100-PROCESS-FILE-NAME.                                                  
           MOVE '1100-PROCESS-FILE-NAME          ' TO PV-PARAGRAPH-NAME.        
                                                                                
           MOVE SPACES                   TO VARIABLE-RECORD-01.                 
           INITIALIZE PV-TALLY-CNT                                              
                      PV-WORK-REC.                                              
                                                                                
           STRING '<file>'                          delimited by size           
                  PV-INPUT-REC                      delimited by ' '            
                  '</file>'                         delimited by size           
             INTO PV-WORK-REC.                                                  
                                                                                
            MOVE 0                       TO PV-BYTE-COUNT.                      
            MOVE PV-WORK-REC             TO PV-VARIABLE-OUTPUT-FIELD            
            PERFORM 1101-FIND-DATA-END                                          
                VARYING PV-EOF-IDX FROM 80 BY -1                                
                  UNTIL PV-BIT-VARIABLE (PV-EOF-IDX) NOT = SPACE.               
            PERFORM 1102-FIELD-MOVER                                            
                VARYING PV-BIT-IDX FROM 1 BY 1                                  
                  UNTIL PV-BIT-IDX > PV-EOF-IDX.                                
                                                                                
           WRITE VARIABLE-RECORD-01.                                            
           ADD 1                         TO PV-RECS-WRITE.                      
                                                                                
      *1100-EXIT.  EXIT.                                                        
       EJECT.                                                                   
      *----------------------------------------------------------               
      *  FINDS END OF "REAL" DATA IS LARGE TEXT FIELDS                          
      *----------------------------------------------------------               
       1101-FIND-DATA-END.                                                      
                                                                                
           MOVE ' '                     TO PV-SPACE.                            
                                                                                
      *1101-EXIT.  EXIT.                                                        
      *----------------------------------------------------------               
      *  MOVES, CHARACTER BY CHARACTER, EP468 FIELDS TO E470                    
      *  FIELDS                                                                 
      *----------------------------------------------------------               
       1102-FIELD-MOVER.                                                        
                                                                                
           ADD 1                        TO PV-BYTE-COUNT.                       
           MOVE PV-BIT-VARIABLE(PV-BIT-IDX) TO                                  
                VARIABLE-BYTE(PV-BYTE-COUNT).                                   
                                                                                
      *4001-EXIT.  EXIT.                                                        
      ******************************************************************        
      *     CLOSES ALL FILES AND DISPLAYS ANY NEEDED INFORMATION.      *        
      ******************************************************************        
       9200-WRAPUP.                                                             
           MOVE '9200-WRAPUP'                      TO PV-PARAGRAPH-NAME.        
                                                                                
           MOVE '</files>'               TO VARIABLE-RECORD-01.                 
           MOVE 8                        TO PV-BYTE-COUNT.                      
           WRITE VARIABLE-RECORD-01.                                            
           ADD 1                         TO PV-RECS-WRITE.                      
                                                                                
           PERFORM 9201-DISPLAY-TOTALS.                                         
                                                                                
           IF NOT AC-DB2-ERROR                                                  
              PERFORM 9990-GET-CURRENT.                                         
                                                                                
           DISPLAY '****************************************'.                  
           DISPLAY '**          XSKUT501 END              **'.                  
           DISPLAY '****************************************'.                  
           DISPLAY '**     END DATE ' PD-DB2-CURRENT-DATE                       
                   '            **'.                                            
           DISPLAY '**     END TIME ' PD-DB2-CURRENT-TIME                       
                   '              **'.                                          
           DISPLAY '****************************************'.                  
                                                                                
           CLOSE INPUT-FILE-01                                                  
                 VARIABLE-FILE-01.                                              
                                                                                
      *9200-EXIT.  EXIT.                                                        
       EJECT                                                                    
      ******************************************************************        
      *     DISPLAY END OF JOB TOTALS                                  *        
      ******************************************************************        
       9201-DISPLAY-TOTALS.                                                     
           MOVE '9201-DISPLAY-TOTALS'              TO PV-PARAGRAPH-NAME.        
                                                                                
           DISPLAY '** RECORDS READ ==========> '                               
                   PV-RECS-READ                                                 
                   ' **'.                                                       
           DISPLAY '** RECORDS WRITTEN =======> '                               
                   PV-RECS-WRITE                                                
                   ' **'.                                                       
           DISPLAY '****************************************'.                  
                                                                                
      *9201-EXIT.  EXIT.                                                        
       EJECT                                                                    
      ******************************************************************        
      *     GET CURRENT DATE, TIME AND TIMESTAMP FROM DB2              *        
      ******************************************************************        
       9990-GET-CURRENT.                                                        
           MOVE '9990-GET-CURRENT'                 TO PV-PARAGRAPH-NAME.        
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
                         OR  SQLCODE = 0)                                       
                                                                                
                 EXEC SQL                                                       
                    SELECT                                                      
                           CURRENT DATE                                         
                          ,CURRENT TIME                                         
                          ,CURRENT TIMESTAMP                                    
                      INTO                                                      
                           :PD-DB2-CURRENT-DATE                                 
                          ,:PD-DB2-CURRENT-TIME                                 
                          ,:PD-DB2-CURRENT-TMST                                 
                      FROM                                                      
                           SYSIBM.SYSDUMMY1                                     
                 END-EXEC                                                       
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                        MOVE '9990-GET-CURRENT'                                 
                                           TO PV-PARAGRAPH-NAME                 
                        MOVE 'SELECT CURR DTE, TME & TMST'                      
                                           TO PV-DB2-OPERATION                  
                        PERFORM 9998-DB2-ERROR                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
              MOVE '1200-OPEN-REFRESH-CURSOR'                                   
                                   TO  PV-PARAGRAPH-NAME                        
              MOVE 'OPEN RETRIES EXCEEDED'                                      
                                    TO  PV-DB2-OPERATION                        
              PERFORM 9998-DB2-ERROR                                            
           END-IF.                                                              
                                                                                
      *9990-EXIT.  EXIT.                                                        
       EJECT.                                                                   
      *****************************************************************         
      *     DB2 ABEND ROUTINE.                                        *         
      *****************************************************************         
       9998-DB2-ERROR.                                                          
                                                                                
           PERFORM 9201-DISPLAY-TOTALS.                                         
                                                                                
           SET AC-DB2-ERROR              TO TRUE.                               
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.                       
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                                 
                                                                                
      *9998-EXIT.  EXIT.                                                        
