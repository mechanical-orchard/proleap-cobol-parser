       ID  DIVISION.                                                            
       PROGRAM-ID.         INKUT166.                                            
       AUTHOR.             J SEIDLER.                                           
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       8/98.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *           EXTRACT THE INVENTORY PHYSICAL COUNT DATA                     
      *                                                                         
      *   THIS PROGRAMS EXTRACTS THE PHYSICAL COUNT DATA FROM THE               
      *              TSUMINV                                                    
      *              TADJADD                                                    
      *              TADJDTL                                                    
      *              TSTKRMW                                                    
      *                                                                         
      *   THIS PROGRAM HAS BEEN MODIFIED TO TAKE IN TWO OPTIONAL                
      *   INPUT FILE.  THE FIRST FILE WILL OVERRIDE THE DEFAULT                 
      *   PROCESSING DATE OPTION OF CURRENT ML OPEN FISCAL MONTH.               
      *   THE FORMAT OF THE CONTROL CARD IS AS FOLLOW:                          
      *     VALID VALUES ARE: {ONLY|FSCL MONTH|BEFORE}                          
      *       ONLY       - PROCESS STORES FOR A GIVEN DATE ONLY                 
      *       FSCL MONTH - PROCESS STORES FOR FISCAL MONTH OF THE               
      *                    GIVEN DATE.                                          
      *       BEFORE     - PROCESS STORES FOR FISCAL MONTH OF THE               
      *                    GIVEN DATE UP TO THE GIVEN DATE.                     
      *                                                                         
      *   THE SECOND FILE WILL LIST THE DEPT/CLASS THAT SHOULD BE               
      *   BLOCKED FROM THE PHYSICAL EXTRACT.  THE BLOCKED DEPT/CLASS            
      *   WILL BE WRITTEN TO A SEPARATE FILE IN THE FORMAT OF MLRD123           
      *   FOR REPORTING.                                                        
      *                                                                         
      ******************************************************************        
      *                    CHANGE LOG                                  *        
      ******************************************************************        
      * 02/22   M. KLAMIK                                              *        
      *         CHANGED LOGIC OF PROGRAM TO CAPTURE DATA FOR STORES    *        
      *         DOING INVENTORY DURING THE OPEN FISCAL MONTH.          *        
      *           - ADDED STORE PARAMETER TO ALL CURSORS               *        
      *           - ADDED (ADJ-TYP-CDE = 'SA') TO TADJDTL_CURSOR.      *        
      *             ADJUSTMENTS)                                       *        
      ******************************************************************        
      * 04/04/2000 BOB MCASEY  WR:20839                                *        
      *         RECOMPILED PROGRAM TO BRING IN NEW VERSION OF COPYBOOK *        
      *         INRD014                                                *        
      ******************************************************************        
      * 06/08/2000 SCOTT JACKSON  WR21015               CHGID: PHYADJ  *        
      *     ADDED CONDITION TO TINVPAR_CSR TO ONLY SELECT THOSE STORES *        
      *     THAT WILL BE PERFORMING A PHYSICAL COUNT. 'CLOSED' STORES  *        
      *     ARE ALSO SELECTED - BUT NOT STORES CONSIDERED AS RECEIVING *        
      *     'ESTIMATED ADJUSTMENTS'. EXCLUDED STORES ARE IDENTIFIED AS *        
      *     TINVPAR_SCHD_PHY_CNT_IND ='N'                              *        
      ******************************************************************        
W21732* 02/17/2003 SUDHARSHAN ADDURI/IGSI WR # 21732    CHGID: TK20917 *        
W21732*     CHANGED THE CURSORS TSUMINV_ALL_CURSOR, TSTKRMW_CURSOR,    *        
W21732*     RENUM_CURSOR, TSTKRMW_000999, TADJDTL_CURSOR AND           *        
W21732*     TADJADD_CURSOR TO RETRIEVE 4 DIGIT DEPT AND 4 DIGIT SUB-   *        
W21732*     CLASS.REMOVED THE CALL TO DPKUT040 AND CODED THE SAME LOGIC*        
W21732*     WHICH GENERATES INTERNAL UPC WITHIN THE PROGRAM.           *        
W21732******************************************************************        
R30409* 04/09/2003 RONNA BUTZ             WR # 21732    CHGID: R30409  *        
R30409*     FOUND BUG IN CODE FROM NIS PROJECT                         *        
R30409*     TSTKRMW_000999 CURSOR WOULD NEVER BRING ANYTHING BACK      *        
R30409*     BECAUSE THE TABLE TSKXREF DOES NOT HAVE ANY DUMMY SKUS     *        
R30409*     ALSO, CORRECTED LOGIC FOR RENUMBERING TO BRING BACK THE             
R30409*     CURRENT SKU FOR THE RENUMBER ON THE FIRST CALL                      
R30409******************************************************************        
24521 * 07/01/2003 ROD JANSSEN            WR # 24521    CHGID:                  
24521 *     ZERO SEQUENCE SKUS ARE NOT BEING REPORTED FROM THE TADJDTL          
24521 *     AND TADJADD TABLES FOR INVENTORY SHORTAGE. WHY?                     
      *                                                                         
W26600* W26600  04-05-2004 ROD JANSSEN INV LEDGER STORE EXPANSION               
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVNENTORY ADJUSTMENT           
W29908* W29908  01-31-2006 LEE YANG EST. DAILY INV. LEDGER OPTIONS AND          
      *                    BLOCKED DEPT/CLASS LOGIC.                            
W28545* W28545  08-10-2006 ADD LOGIC FOR DEPARTMENT LEVEL                       
W28545*                    INVENTORY PROCESS. JOIN TINVPAR/TINCNTL              
W28545*                    -B. GRAHAM - STRATAGEM                               
W28582* W28582  09-07-2006 VENDOR BRAND CHANGES                                 
W28582*                    -B. GRAHAM - STRATAGEM                               
W33304* W33304  11-26-2008 HOLDING INVENTORY LEDGER OPEN ACROSS PERIODS         
W33304*                    -NIJESH/N INFOSYS.                                   
W34557* W34557  09-13-2011 PROVIDE FUNCTIONALITY TO ALLOW INVENTORIES           
W34557*                    TO BE SCHEDULED DURING THE CLOSING WEEK              
W34557*                    OF MERCHANDISE LEDGER WHICH IS ALSO THE FIRST        
W34557*                    OF THE ACCOUNTING MONTH. THIS CHANGE WILL            
W34557*                    ALLOW THE SHORTAGE PROCESS TO INCLUDE                
W34557*                    STORES COUNTING DURING CLOSE.                        
W34557*                    - KARTHIK M.                                         
W34557*                                                                         
W34557* PLEASE SEE INRD001 FOR EXAMPLES OF THE ALTERNATE DATE CARD              
W34557* AND ITS VALUES                                                          
W34557*                                                                         
24521 ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
W29908     SELECT OPTIONAL PROCESS-OPTION-FILE                                  
W29908         ASSIGN TO INP01.                                                 
W34557     SELECT ALTERNATE-DATE-CNTL-FILE                                      
W34557         ASSIGN TO INP02.                                                 
           SELECT PHY-INV-COUNT-OUTPUT                                          
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
W29908 FD  PROCESS-OPTION-FILE                                                  
W29908     RECORDING  MODE IS F                                                 
W29908     LABEL RECORDS ARE STANDARD                                           
W29908     BLOCK CONTAINS 0 RECORDS.                                            
W29908 01  PROCESS-OPTION-REC              PIC X(80).                           
                                                                                
W34557 FD  ALTERNATE-DATE-CNTL-FILE                                             
W34557     LABEL RECORDS ARE STANDARD                                           
W34557     RECORDING MODE F                                                     
W34557     BLOCK CONTAINS 0 RECORDS.                                            
W34557                                                                          
W34557 01  ALTERNATE-DATE-CNTL-REC     PIC X(80).                               
W34557                                                                          
       FD  PHY-INV-COUNT-OUTPUT                                                 
           RECORDING  MODE IS F                                                 
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  OUTPUT-REC                      PIC X(200).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
W29908     88  ABEND-4003                                  VALUE +4003.         
W29908     88  ABEND-SQL-4013                              VALUE +4013.         
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           10  PC-MAX-RETRIES          PIC S9(4) COMP SYNC VALUE +2.            
W28582     10  PC-TSUMINV-NAME         PIC  X(8) VALUE 'TSUMINV'.               
W28582     10  PC-TSTKRMW-NAME         PIC  X(8) VALUE 'TSTKRMW'.               
W28582     10  PC-TADJDTL-NAME         PIC  X(8) VALUE 'TADJDTL'.               
W28582     10  PC-TADJADD-NAME         PIC  X(8) VALUE 'TADJADD'.               
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
W28582         10  PA-OUTPUT-COUNT         PIC S9(09)      VALUE ZEROES         
W28582                                                     COMP-3.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
W29908     10  PV-STORE-NBR-9              PIC 9(5)        VALUE ZEROES.        
W29908     10  PV-STORE-NBR-X REDEFINES PV-STORE-NBR-9                          
W29908                                     PIC X(05).                           
           10  PV-MAJ-CLASS.                                                    
               15  FILLER              PIC X(01)  VALUE SPACES.                 
               15  PV-MAJ-CLASS-ZERO   PIC X(01)  VALUE '0'.                    
W29908     10  PV-MAJ-CLASS-9 REDEFINES PV-MAJ-CLASS                            
W29908                                 PIC 9(02).                               
W21732     10  PV-DEPT-NBR             PIC 9(04)  VALUE ZEROS.                  
W21732     10  FILLER                  REDEFINES PV-DEPT-NBR.                   
W21732         15  PV-DEPT-NBR-1       PIC 9(01).                               
W21732         15  PV-DEPT-NBR-2-3-4   PIC 9(03).                               
W21732     10  PV-SUB-CLASS            PIC 9(04)   VALUE ZEROS.                 
W21732     10  FILLER                  REDEFINES PV-SUB-CLASS.                  
W21732         15  PV-SUB-CLASS-1-2    PIC 9(02).                               
W21732         15  PV-SUB-CLASS-3-4    PIC 9(02).                               
R30409     10  PV-DEPT                 PIC X(3).                                
R30409     10  PV-SUBCLASS             PIC X(2).                                
                                                                                
           05  PV-PARA                 PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.               
           05  PV-RETRY-COUNT          PIC S9(4) COMP SYNC VALUE +0.            
           05  PV-ACTL-FIN-BK-DATE     PIC X(10)    VALUE '9999-09-09'.         
                                                                                
24521                                                                           
W26600     05  PV-EDIT-LOC-NBR         PIC 9(05)  VALUE ZEROS.                  
W26600     05  PV-EDIT-LOC-NBR-ALPH REDEFINES                                   
W26600         PV-EDIT-LOC-NBR         PIC X(05).                               
W21732*                                                                         
                                                                                
       01  SWITCHES.                                                            
           05  END-OF-CONTROL-CARD-IND     PIC X           VALUE 'N'.           
               88  END-OF-CONTROL-CARD                     VALUE 'Y'.           
           05  END-OF-CURSOR-IND           PIC X           VALUE 'N'.           
               88  END-OF-CURSOR                           VALUE 'Y'.           
           05  END-OF-TINVPAR-CURSOR-IND   PIC X           VALUE 'N'.           
               88  END-OF-TINVPAR-CURSOR                   VALUE 'Y'.           
               88  NOT-END-OF-TINVPAR-CURSOR               VALUE 'N'.           
                                                                                
W29908 01  PT-PROGRAM-TABLE-AREA.                                               
W29908     05  PT-INV-STORE.                                                    
W29908         10  PT-INV-STORE-INDX       PIC S9(05)      VALUE ZEROES         
W29908                         COMP-3.                                          
W29908         10  PT-INV-STORE-COUNT      PIC S9(05)      VALUE ZEROES         
W29908                         COMP-3.                                          
W29908         10  PT-INV-STORE-MAX        PIC S9(05)      VALUE +10000         
W29908                         COMP-3.                                          
W29908         10  PT-INV-STORE-TBL OCCURS 10000 TIMES.                         
W29908             15  PT-INV-STORE-NBR    PIC S9(04)      VALUE ZEROES         
W29908                         COMP.                                            
W28582             15  PT-INV-ID           PIC S9(09)      VALUE ZEROS          
W28582                         COMP.                                            
W28582             15  PT-ACTL-INV-DTE     PIC  X(10)      VALUE SPACES.        
W33304             15  PT-OPN-FSCL-MN-DTE  PIC  X(10)      VALUE SPACES.        
W33304             15  PT-FINCL-INV-STAT-CDE                                    
W33304                                     PIC  X(02)      VALUE SPACES.        
                                                                                
W29908 01  RQ-REQUEST-CONTROL.                                                  
W29908     05  RQ-COMMENT-IND              PIC X(01)       VALUE SPACES.        
W29908         88  RQ-NOT-COMMENT                          VALUE SPACES.        
W29908         88  RQ-COMMENT                              VALUE '*'.           
W29908     05  RQ-PROCESS-DATE             PIC X(10)       VALUE SPACES.        
W29908     05  FILLER REDEFINES RQ-PROCESS-DATE.                                
W29908         10  RQ-PROCESS-DATE-CCYY    PIC X(04).                           
W29908         10  RQ-PROCESS-DATE-DASH-1  PIC X(01).                           
W29908             88  RQ-VALID-PROCESS-DATE-DASH-1        VALUE '-'.           
W29908         10  RQ-PROCESS-DATE-MM      PIC X(02).                           
W29908         10  RQ-PROCESS-DATE-DASH-2  PIC X(01).                           
W29908             88  RQ-VALID-PROCESS-DATE-DASH-2        VALUE '-'.           
W29908         10  RQ-PROCESS-DATE-DD      PIC X(02).                           
W29908     05  FILLER                      PIC X(01)       VALUE SPACES.        
W29908     05  RQ-PROCESS-OPTION           PIC X(10)       VALUE SPACES.        
W29908         88  RQ-VALID-PROCESS-OPTION                 VALUES ARE           
W29908             'ONLY      '                                                 
W29908             'FSCL MONTH'                                                 
W29908             'BEFORE    '.                                                
W29908         88  RQ-PROCESS-ONLY                         VALUE                
W29908             'ONLY      '.                                                
W29908         88  RQ-PROCESS-FSCL-MONTH                   VALUE                
W29908             'FSCL MONTH'.                                                
W29908         88  RQ-PROCESS-BEFORE                       VALUE                
W29908             'BEFORE    '.                                                
W29908     05  FILLER                      PIC X(01)       VALUE SPACES.        
W29908     05  RQ-FIN-BK-DATE              PIC X(10)       VALUE SPACES.        
W29908     05  FILLER REDEFINES RQ-FIN-BK-DATE.                                 
W29908         10  RQ-FIN-BK-DATE-CCYY     PIC X(04).                           
W29908         10  RQ-FIN-BK-DATE-DASH-1   PIC X(01).                           
W29908             88  RQ-VALID-FIN-BK-DATE-DASH-1         VALUE '-'.           
W29908         10  RQ-FIN-BK-DATE-MM       PIC X(02).                           
W29908         10  RQ-FIN-BK-DATE-DASH-2   PIC X(01).                           
W29908             88  RQ-VALID-FIN-BK-DATE-DASH-2         VALUE '-'.           
W29908         10  RQ-FIN-BK-DATE-DD       PIC X(02).                           
W29908     05  FILLER                      PIC X(47)       VALUE SPACES.        
                                                                                
                                                                                
      *****************************************************************         
      *  STORE SHORTAGE EXTRACT RECORD                                          
      *****************************************************************         
W26600     COPY INRD034.                                                        
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DB2 MERCH LDGR CONTROL                                                  
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
      *****************************************************************         
      * DB2 DATE ATTRIBUTES                                                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
28545                                                                           
28545 *****************************************************************         
28545 * TINCNTL DCLGEN                                                          
28545 *****************************************************************         
28545      EXEC SQL                                                             
28545          INCLUDE TINCNTL                                                  
28545      END-EXEC.                                                            
      *****************************************************************         
      * DB2 INVENTORY PARMS                                                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
      *****************************************************************         
      * DB2 SUMMARY INVENTORY                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSUMINV                                                  
           END-EXEC.                                                            
      *****************************************************************         
      * DB2 STOCKROOM ADJUSTMENTS                                               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSTKRMW                                                  
           END-EXEC.                                                            
      *****************************************************************         
      * DB2 ADJUSTMENTS DETAIL                                                  
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TADJDTL                                                  
           END-EXEC.                                                            
      *****************************************************************         
      * DB2 ADJUSTMENTS ADD                                                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TADJADD                                                  
           END-EXEC.                                                            
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
           EJECT                                                                
W34557*****************************************************************         
W34557*  COPYBOOK OF INPUT ALTERNATE DATE CONTROL CARD RECORD LAYOUT  *         
W34557*****************************************************************         
W34557*                                                                         
W34557     COPY INRD001.                                                        
W34557                                                                          
W34557*****************************************************************         
W34557*  COPYBOOK OF FIELDS USED IN THE EDIT / VALIDATE PROCESS.      *         
W34557*****************************************************************         
W34557                                                                          
W34557     COPY INWS050.                                                        
W34557                                                                          
      *****************************************************************         
      * TINVPAR CURSOR                                                          
      *****************************************************************         
           EXEC SQL DECLARE TINVPAR_CSR CURSOR FOR                              
W33304        SELECT A.LOC_NBR                                                  
W33304              ,B.OPN_FSCL_MN_DTE                                          
W33304              ,A.ACTL_INV_DTE                                             
W28582              ,A.INV_ID                                                   
W33304              ,A.FINCL_INV_STAT_CDE                                       
28545         FROM TINVPAR A                                                    
28545             ,TINCNTL B                                                    
W33304          WHERE  A.ACTL_FIN_BK_DTE = :PV-ACTL-FIN-BK-DATE                 
W34557*W33304   AND A.PLND_INV_DTE     <= :DTATTR-FSCL-MN-END-DTE               
W34557          AND (A.PLND_INV_DTE    <= :DTATTR-FSCL-MN-END-DTE               
W34557           OR A.PLND_INV_DTE     <= :IN001-OPN-FSCL-MN-DTE)               
W33304          AND A.SCHD_PHY_CNT_CDE <> 'EI'                                  
W33304          AND A.LOC_INV_STAT_CDE = 'IN'                                   
28545           AND A.INV_ID = B.INV_ID                                         
28545           AND B.ACTV_IND = 'Y'                                            
W33304        ORDER BY A.LOC_NBR                                                
W29908        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TSUMINV ALL CURSOR                                                      
      *****************************************************************         
           EXEC SQL DECLARE TSUMINV_ALL_CSR CURSOR FOR                          
W21732        SELECT A.DEPT_NBR                                                 
W21732              ,A.SUB_CL_NBR                                               
W26600              ,A.LOC_NBR                                                  
W20445              ,A.SKU_NBR                                                  
                    ,A.EXTD_AMT                                                 
              FROM TSUMINV A                                                    
W26600        WHERE  A.LOC_NBR   = :SUMINV-LOC-NBR                              
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TSTKRMW CURSOR                                                          
      *****************************************************************         
                                                                                
           EXEC SQL DECLARE TSTKRMW_CURSOR CURSOR FOR                           
W28582        SELECT A.LOC_NBR                                                  
W20445              ,A.SKU_NBR                                                  
                    ,A.ITM_EXTD_AMT                                             
              FROM TSTKRMW A                                                    
W28582        WHERE  A.LOC_NBR         = :STKRMW-LOC-NBR                        
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TADJDTL CURSOR                                                          
      *****************************************************************         
           EXEC SQL DECLARE TADJDTL_CURSOR CURSOR FOR                           
24521        SELECT  A.SKU_NBR                                                  
W26600              ,A.LOC_NBR                                                  
                    ,A.ADJ_EXTD_AMT                                             
               FROM  TADJDTL A                                                  
W28582         WHERE A.LOC_NBR        = :ADJDTL-LOC-NBR                         
                AND  A.ADJ_TYPE_CDE   IN ('AD','CO','SI','MM','SA')             
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TADJADD CURSOR                                                          
      *****************************************************************         
           EXEC SQL DECLARE TADJADD_CURSOR CURSOR FOR                           
W28582       SELECT  A.SKU_NBR                                                  
W26600              ,A.LOC_NBR                                                  
                    ,A.ADJ_EXTD_AMT                                             
               FROM  TADJADD A                                                  
W26600         WHERE A.LOC_NBR        = :ADJADD-LOC-NBR                         
           END-EXEC.                                                            
                                                                                
           EJECT                                                                
       LINKAGE SECTION.                                                         
                                                                                
      /****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       A100-MAIN-MODULE.                                                        
                                                                                
           PERFORM A150-INITIALIZE.                                             
           PERFORM A200-LOAD-STORE-TABLE.                                       
                                                                                
W29908     IF PT-INV-STORE-COUNT > 0                                            
W29908         DISPLAY ' '                                                      
               PERFORM B200-MAIN-PROCESS                                        
W29908             VARYING PT-INV-STORE-INDX FROM 1 BY 1                        
W29908             UNTIL PT-INV-STORE-INDX > PT-INV-STORE-COUNT                 
           END-IF.                                                              
                                                                                
           PERFORM B300-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
           EJECT                                                                
                                                                                
       A150-INITIALIZE.                                                         
           MOVE '*A150-INITIALIZE.                *' TO PV-PARA                 
                                                                                
W29908     OPEN INPUT  PROCESS-OPTION-FILE                                      
W34557                 ALTERNATE-DATE-CNTL-FILE                                 
                OUTPUT PHY-INV-COUNT-OUTPUT.                                    
W34557                                                                          
W34557     INITIALIZE IN001-RECORD.                                             
W34557                                                                          
W34557     MOVE 'INKUT166'                  TO PV-IN050-PGM-NUMBER              
W34557     PERFORM 1100-EDIT-VALIDATE-CNTL.                                     
W34557                                                                          
                                                                                
W29908     PERFORM Z050-READ-CONTROL.                                           
W29908     DISPLAY ' ----+----1----+----2----+----3----+----4'.                 
W29908     PERFORM                                                              
W29908         UNTIL END-OF-CONTROL-CARD                                        
W29908             OR RQ-NOT-COMMENT                                            
W29908         DISPLAY ' ' RQ-REQUEST-CONTROL                                   
W29908         PERFORM Z050-READ-CONTROL                                        
W29908     END-PERFORM.                                                         
                                                                                
W29908     IF END-OF-CONTROL-CARD                                               
W29908         MOVE SPACES     TO RQ-REQUEST-CONTROL                            
W29908     ELSE                                                                 
W29908         DISPLAY ' ' RQ-REQUEST-CONTROL                                   
W29908         IF RQ-FIN-BK-DATE-CCYY NUMERIC                                   
W29908             AND RQ-FIN-BK-DATE-MM NUMERIC                                
W29908             AND RQ-FIN-BK-DATE-DD NUMERIC                                
W29908             AND RQ-VALID-FIN-BK-DATE-DASH-1                              
W29908             AND RQ-VALID-FIN-BK-DATE-DASH-2                              
W29908             DISPLAY ' OVERRIDING ACTUAL FINANCIAL BOOK DATE'             
W29908                     ' FROM ' PV-ACTL-FIN-BK-DATE                         
W29908                     ' TO ' RQ-FIN-BK-DATE                                
W29908             MOVE RQ-FIN-BK-DATE TO PV-ACTL-FIN-BK-DATE                   
W29908         END-IF                                                           
W29908         IF RQ-PROCESS-DATE-CCYY NUMERIC                                  
W29908             AND RQ-PROCESS-DATE-MM NUMERIC                               
W29908             AND RQ-PROCESS-DATE-DD NUMERIC                               
W29908             AND RQ-VALID-PROCESS-DATE-DASH-1                             
W29908             AND RQ-VALID-PROCESS-DATE-DASH-2                             
W29908             CONTINUE                                                     
W29908         ELSE                                                             
W29908             MOVE SPACES     TO RQ-REQUEST-CONTROL                        
W29908         END-IF                                                           
W29908     END-IF.                                                              
W29908     DISPLAY ' '.                                                         
                                                                                
W29908     IF RQ-REQUEST-CONTROL = SPACES                                       
W29908         OR RQ-PROCESS-FSCL-MONTH                                         
W29908         OR RQ-PROCESS-BEFORE                                             
W29908         IF RQ-PROCESS-FSCL-MONTH                                         
W29908             OR RQ-PROCESS-BEFORE                                         
W29908             MOVE RQ-PROCESS-DATE      TO MLCNTL-OPN-FSCL-MN-DTE          
W29908             DISPLAY 'ON-REQUEST DATE: ' MLCNTL-OPN-FSCL-MN-DTE           
W29908         ELSE                                                             
W29908             PERFORM Z100-GET-OPEN-FSCL-MN                                
W29908             DISPLAY 'OPEN FISCAL MONTH: ' MLCNTL-OPN-FSCL-MN-DTE         
W29908             DISPLAY 'OPEN FISCAL NBR: ' MLCNTL-OPN-FSCL-MN-NBR           
W29908         END-IF                                                           
W29908                                                                          
W29908         PERFORM Z150-GET-OPEN-DATE-ATTRIBUTES                            
W29908         IF RQ-PROCESS-BEFORE                                             
W29908             MOVE RQ-PROCESS-DATE      TO DTATTR-FSCL-MN-END-DTE          
W29908         END-IF                                                           
W29908     ELSE                                                                 
W29908         MOVE RQ-PROCESS-DATE      TO DTATTR-FSCL-MN-BEG-DTE              
W29908                                      DTATTR-FSCL-MN-END-DTE              
W29908     END-IF.                                                              
W29908     DISPLAY 'BEGINNING DATE: ' DTATTR-FSCL-MN-BEG-DTE.                   
W29908     DISPLAY 'ENDING DATE:    ' DTATTR-FSCL-MN-END-DTE.                   
                                                                                
                                                                                
       A200-LOAD-STORE-TABLE.                                                   
           MOVE '*A200-LOAD-STORE-TABLE.          *' TO PV-PARA                 
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CSR                                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TINVPAR_CSR' TO PV-DB2-OPER-ATTEMPTED                 
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
W29908     DISPLAY ' '.                                                         
           PERFORM Z200-FETCH-TINVPAR                                           
W29908         UNTIL PT-INV-STORE-INDX > PT-INV-STORE-MAX                       
                   OR  END-OF-TINVPAR-CURSOR.                                   
                                                                                
W29908     IF PT-INV-STORE-INDX > PT-INV-STORE-MAX                              
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
W29908         DISPLAY 'MORE THAN ' PT-INV-STORE-MAX ' STORES EXIST '           
               DISPLAY 'MUST EXPAND INV-STORE TABLE   '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY 'A B E N D                   * '                         
W29908         SET ABEND-4003         TO TRUE                                   
W29908         PERFORM Z950-ABEND                                               
           END-IF.                                                              
                                                                                
           IF PT-INV-STORE-INDX = 0                                             
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY 'NO STORES DOING INVENTORY     '                         
               DISPLAY 'DURING THE OPEN FISCAL MONTH  '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
           ELSE                                                                 
W29908         MOVE PT-INV-STORE-INDX TO PT-INV-STORE-COUNT                     
W29908     END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               CLOSE TINVPAR_CSR                                                
           END-EXEC.                                                            
                                                                                
       B200-MAIN-PROCESS.                                                       
           MOVE '*B200-MAIN-PROCESS.              *' TO PV-PARA                 
                                                                                
W29908     DISPLAY ' PROCESSING STORE: '                                        
W29908             PT-INV-STORE-NBR(PT-INV-STORE-INDX)                          
W29908             ' (' PT-INV-STORE-INDX                                       
W29908             ' OF ' PT-INV-STORE-COUNT ')'.                               
                                                                                
W29908     MOVE PT-INV-STORE-NBR(PT-INV-STORE-INDX) TO SUMINV-LOC-NBR.          
           EXEC SQL                                                             
               OPEN TSUMINV_ALL_CSR                                             
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TSUMINV_ALL_CSR' TO PV-DB2-OPER-ATTEMPTED             
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           MOVE 'N' TO END-OF-CURSOR-IND.                                       
           PERFORM Z300-FETCH-TSUMINV-ALL                                       
               UNTIL END-OF-CURSOR.                                             
                                                                                
           EXEC SQL                                                             
              CLOSE TSUMINV_ALL_CSR                                             
           END-EXEC.                                                            
      *    DISPLAY 'TSUMINV EXTRACT COMPLETE'.                                  
                                                                                
W29908     MOVE PT-INV-STORE-NBR(PT-INV-STORE-INDX) TO STKRMW-LOC-NBR.          
           EXEC SQL                                                             
               OPEN TSTKRMW_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TSTKRMW_CURSOR' TO PV-DB2-OPER-ATTEMPTED              
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           MOVE 'N' TO END-OF-CURSOR-IND.                                       
           PERFORM Z400-FETCH-TSTKRMW                                           
               UNTIL END-OF-CURSOR.                                             
                                                                                
           EXEC SQL                                                             
              CLOSE TSTKRMW_CURSOR                                              
           END-EXEC.                                                            
      *    DISPLAY 'TSTKRMW EXTRACT COMPLETE'.                                  
                                                                                
                                                                                
W29908     MOVE PT-INV-STORE-NBR(PT-INV-STORE-INDX) TO ADJDTL-LOC-NBR.          
           EXEC SQL                                                             
               OPEN TADJDTL_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TADJDTL_CURSOR' TO PV-DB2-OPER-ATTEMPTED              
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           MOVE 'N' TO END-OF-CURSOR-IND                                        
           PERFORM Z500-FETCH-TADJDTL                                           
               UNTIL END-OF-CURSOR.                                             
                                                                                
           EXEC SQL                                                             
              CLOSE TADJDTL_CURSOR                                              
           END-EXEC.                                                            
      *    DISPLAY 'ADJDTL EXTRACT COMPLETE'.                                   
                                                                                
W29908     MOVE PT-INV-STORE-NBR(PT-INV-STORE-INDX) TO ADJADD-LOC-NBR.          
           EXEC SQL                                                             
               OPEN TADJADD_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TADJADD_CURSOR' TO PV-DB2-OPER-ATTEMPTED              
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           MOVE 'N' TO END-OF-CURSOR-IND                                        
           PERFORM Z700-FETCH-TADJADD                                           
               UNTIL END-OF-CURSOR.                                             
                                                                                
           EXEC SQL                                                             
              CLOSE TADJADD_CURSOR                                              
           END-EXEC.                                                            
      *    DISPLAY 'TADJADD EXTRACT COMPLETE'.                                  
                                                                                
                                                                                
       B300-EOJ.                                                                
           MOVE '*B300-EOJ.                       *' TO PV-PARA                 
                                                                                
W28582     DISPLAY 'PHYS-INV-COUNT-OUTPUT RECORD COUNT '                        
                                                   PA-OUTPUT-COUNT.             
W29908     CLOSE PROCESS-OPTION-FILE                                            
W34557           ALTERNATE-DATE-CNTL-FILE                                       
                 PHY-INV-COUNT-OUTPUT.                                          
           EJECT                                                                
                                                                                
                                                                                
W29908 Z050-READ-CONTROL.                                                       
W29908     READ PROCESS-OPTION-FILE  INTO RQ-REQUEST-CONTROL                    
W29908       AT END                                                             
W29908         SET END-OF-CONTROL-CARD      TO TRUE.                            
                                                                                
       Z100-GET-OPEN-FSCL-MN.                                                   
           MOVE '*Z100-GET-OPEN-FSCL-MN           *' TO PV-PARA                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                        OPN_FSCL_MN_DTE                                         
                       ,OPN_FSCL_MN_NBR                                         
                    INTO                                                        
                       :MLCNTL-OPN-FSCL-MN-DTE                                  
                      ,:MLCNTL-OPN-FSCL-MN-NBR                                  
                    FROM TMLCNTL                                                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000 OR -904 OR -911                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE    'Z100-GET-OPEN-FSCL-MN'                          
                                                TO PV-PARA                      
                       MOVE    'SELECT ROW FROM TMLCNTL'                        
                                                TO PV-DB2-OPER-ATTEMPTED        
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '** MAX RETRIES EXCEEDED ON TMLCNTL **'                  
               MOVE 'Z100-GET-OPEN-FSCL-MN'                                     
                                                TO PV-PARA                      
               MOVE 'SELECT ROW FROM TMLCNTL'                                   
                                                TO PV-DB2-OPER-ATTEMPTED        
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       Z150-GET-OPEN-DATE-ATTRIBUTES.                                           
           MOVE '*Z150-GET-OPEN-DATE-ATTRIBUTES   *' TO PV-PARA                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                       FSCL_MN_BEG_DTE                                          
                      ,FSCL_MN_END_DTE                                          
                    INTO                                                        
                       :DTATTR-FSCL-MN-BEG-DTE                                  
                      ,:DTATTR-FSCL-MN-END-DTE                                  
                    FROM                                                        
                        TDTATTR                                                 
                    WHERE                                                       
                        KY_DTE = :MLCNTL-OPN-FSCL-MN-DTE                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000 OR -904 OR -911                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY 'MLCNTL-OPN-FSCL-MN-DTE: '                       
                                MLCNTL-OPN-FSCL-MN-DTE                          
                       MOVE    'Z150-GET-OPEN-DATE-ATTRIBUTES'                  
                                                TO PV-PARA                      
                       MOVE    'SELECT ROW FROM TDTATTR'                        
                                           TO PV-DB2-OPER-ATTEMPTED             
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
       Z200-FETCH-TINVPAR.                                                      
           MOVE '*Z200-FETCH-TINVPAR.             *' TO PV-PARA                 
                                                                                
           INITIALIZE  DCLTINVPAR.                                              
                                                                                
           EXEC SQL                                                             
               FETCH TINVPAR_CSR                                                
W26600         INTO  :INVPAR-LOC-NBR                                            
W33304              ,:INCNTL-OPN-FSCL-MN-DTE                                    
                    ,:INVPAR-ACTL-INV-DTE                                       
W28582              ,:INVPAR-INV-ID                                             
W33304              ,:INVPAR-FINCL-INV-STAT-CDE                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
W29908             ADD 1 TO PT-INV-STORE-INDX                                   
W26600             MOVE INVPAR-LOC-NBR                                          
W29908                 TO PT-INV-STORE-NBR (PT-INV-STORE-INDX)                  
                          PV-EDIT-LOC-NBR                                       
                   DISPLAY ' TINVPAR STORE '                                    
W26600                     PV-EDIT-LOC-NBR                                      
W28582             MOVE INVPAR-INV-ID                                           
W28582                 TO PT-INV-ID (PT-INV-STORE-INDX)                         
W28582             DISPLAY ' TINVPAR INV ID '                                   
W28582                     PT-INV-ID (PT-INV-STORE-INDX)                        
W28582                    ' SELECTED FOR INVENTORY'                             
W28582             MOVE INVPAR-ACTL-INV-DTE                                     
W28582                 TO PT-ACTL-INV-DTE (PT-INV-STORE-INDX)                   
W33304             MOVE INCNTL-OPN-FSCL-MN-DTE                                  
W33304                 TO PT-OPN-FSCL-MN-DTE (PT-INV-STORE-INDX)                
W33304             MOVE INVPAR-FINCL-INV-STAT-CDE                               
W33304                 TO PT-FINCL-INV-STAT-CDE (PT-INV-STORE-INDX)             
               WHEN SQLCODE = +100                                              
                   SET END-OF-TINVPAR-CURSOR TO TRUE                            
               WHEN OTHER                                                       
                   MOVE 'FETCH TINVPAR CURSOR'                                  
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       Z300-FETCH-TSUMINV-ALL.                                                  
           MOVE '*Z300-FETCH-TSUMINV-ALL          *' TO PV-PARA                 
                                                                                
W28582     INITIALIZE  IN034-SHORTAGE-EXTR-REC.                                 
                                                                                
           EXEC SQL                                                             
              FETCH TSUMINV_ALL_CSR                                             
W21732         INTO  :SUMINV-DEPT-NBR                                           
W21732              ,:SUMINV-SUB-CL-NBR                                         
W26600              ,:SUMINV-LOC-NBR                                            
W20445              ,:SUMINV-SKU-NBR                                            
W21732              ,:SUMINV-EXTD-AMT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
W21732             MOVE SUMINV-DEPT-NBR     TO PV-DEPT-NBR                      
W28582             MOVE PV-DEPT-NBR-2-3-4   TO IN034-DEPT-NBR                   
W21732             MOVE SUMINV-SUB-CL-NBR   TO PV-SUB-CLASS                     
W28582             MOVE PV-SUB-CLASS-3-4    TO IN034-SUB-CL-NBR                 
W28582             MOVE SUMINV-LOC-NBR      TO IN034-STR-NBR                    
W20445                                                                          
W28582             MOVE SUMINV-SKU-NBR      TO IN034-SKU                        
W28582             MOVE ZEROS               TO IN034-ML-LWST-ID                 
W20445                                                                          
W28582             MOVE SUMINV-EXTD-AMT     TO IN034-PHY-INV-RTL-AMT            
W28582             MOVE PC-TSUMINV-NAME     TO IN034-TBL-NAME                   
                   PERFORM Z800-WRITE-OUTPUT-REC                                
               WHEN SQLCODE = +100                                              
                   SET END-OF-CURSOR TO TRUE                                    
               WHEN OTHER                                                       
                   MOVE 'FETCH TSUMINV "ALL" CURSOR'                            
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       Z400-FETCH-TSTKRMW.                                                      
           MOVE '*Z400-FETCH-TSTKRMW              *' TO PV-PARA                 
                                                                                
W28582     INITIALIZE  IN034-SHORTAGE-EXTR-REC.                                 
                                                                                
           EXEC SQL                                                             
              FETCH TSTKRMW_CURSOR                                              
W28582         INTO  :STKRMW-LOC-NBR                                            
W20445              ,:STKRMW-SKU-NBR                                            
W21732              ,:STKRMW-ITM-EXTD-AMT                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
      *-->         TO MAKE SOME LOWER LEVEL REPORTING PROGRAM MORE              
      *-->         STRAIGHTFORWARD, MAKE STOCKROOM WITHDRAWAL A NEGATIVE        
      *-->         AMOUNT.                                                      
W28582             MOVE SPACES              TO IN034-DEPT-NBR                   
W28582             MOVE SPACES              TO IN034-SUB-CL-NBR                 
W28582             MOVE STKRMW-LOC-NBR      TO IN034-STR-NBR                    
W20445                                                                          
W28582             MOVE STKRMW-SKU-NBR      TO IN034-SKU                        
W28582             MOVE ZEROS               TO IN034-ML-LWST-ID                 
W20445                                                                          
W28582             MOVE STKRMW-ITM-EXTD-AMT TO IN034-PHY-INV-RTL-AMT            
W28582             COMPUTE IN034-PHY-INV-RTL-AMT                                
W28582                      = (IN034-PHY-INV-RTL-AMT * -1)                      
W28582             MOVE PC-TSTKRMW-NAME     TO IN034-TBL-NAME                   
                   PERFORM Z800-WRITE-OUTPUT-REC                                
               WHEN SQLCODE = +100                                              
                   SET END-OF-CURSOR TO TRUE                                    
               WHEN OTHER                                                       
                   MOVE 'FETCH TSTKRMW CURSOR'                                  
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       Z500-FETCH-TADJDTL.                                                      
           MOVE '*Z500-FETCH-TADJDTL              *' TO PV-PARA                 
                                                                                
W28582     INITIALIZE  IN034-SHORTAGE-EXTR-REC.                                 
                                                                                
           EXEC SQL                                                             
              FETCH TADJDTL_CURSOR                                              
W28582         INTO  :ADJDTL-SKU-NBR                                            
W26600              ,:ADJDTL-LOC-NBR                                            
W21732              ,:ADJDTL-ADJ-EXTD-AMT                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
W28582             MOVE SPACES              TO IN034-DEPT-NBR                   
W28582             MOVE SPACES              TO IN034-MAJ-CL-NBR                 
W28582             MOVE SPACES              TO IN034-SUB-CL-NBR                 
W28582             MOVE ADJDTL-LOC-NBR      TO IN034-STR-NBR                    
W20445                                                                          
W28582             MOVE ADJDTL-SKU-NBR      TO IN034-SKU                        
W28582             MOVE ZEROS               TO IN034-ML-LWST-ID                 
W20445                                                                          
W28582             MOVE ADJDTL-ADJ-EXTD-AMT TO IN034-PHY-INV-RTL-AMT            
W28582             MOVE PC-TADJDTL-NAME     TO IN034-TBL-NAME                   
                   PERFORM Z800-WRITE-OUTPUT-REC                                
               WHEN SQLCODE = +100                                              
                   SET END-OF-CURSOR TO TRUE                                    
               WHEN OTHER                                                       
                   MOVE 'FETCH TADJDTL CURSOR'                                  
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       Z700-FETCH-TADJADD.                                                      
           MOVE '*Z700-FETCH-TADJADD              *' TO PV-PARA                 
                                                                                
W28582     INITIALIZE  IN034-SHORTAGE-EXTR-REC.                                 
                                                                                
           EXEC SQL                                                             
              FETCH TADJADD_CURSOR                                              
W28582         INTO  :ADJADD-SKU-NBR                                            
W26600              ,:ADJADD-LOC-NBR                                            
W21732              ,:ADJADD-ADJ-EXTD-AMT                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
W28582             MOVE SPACES              TO IN034-DEPT-NBR                   
W28582             MOVE SPACES              TO IN034-SUB-CL-NBR                 
W28582             MOVE SPACES              TO IN034-MAJ-CL-NBR                 
W28582             MOVE ADJADD-LOC-NBR      TO IN034-STR-NBR                    
W20445                                                                          
W28582             MOVE ADJADD-SKU-NBR      TO IN034-SKU                        
W28582             MOVE ZEROS               TO IN034-ML-LWST-ID                 
W20445                                                                          
W28582             MOVE ADJADD-ADJ-EXTD-AMT TO IN034-PHY-INV-RTL-AMT            
W28582             MOVE PC-TADJADD-NAME     TO IN034-TBL-NAME                   
                   PERFORM Z800-WRITE-OUTPUT-REC                                
               WHEN SQLCODE = +100                                              
                   SET END-OF-CURSOR TO TRUE                                    
               WHEN OTHER                                                       
                   MOVE 'FETCH TADJADD CURSOR'                                  
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       Z800-WRITE-OUTPUT-REC.                                                   
                                                                                
           MOVE IN034-SUB-CL-NBR TO  PV-MAJ-CLASS.                              
           MOVE ZERO             TO  PV-MAJ-CLASS-ZERO.                         
           MOVE PV-MAJ-CLASS     TO  IN034-MAJ-CL-NBR.                          
                                                                                
W28582     MOVE PT-ACTL-INV-DTE (PT-INV-STORE-INDX)                             
W28582                           TO IN034-ACTL-INV-DTE.                         
W28582     MOVE PT-INV-ID (PT-INV-STORE-INDX)                                   
W28582                           TO  IN034-INV-ID.                              
W33304     MOVE PT-OPN-FSCL-MN-DTE (PT-INV-STORE-INDX)                          
W33304                           TO  IN034-OPN-FSCL-MN-DTE.                     
W33304     MOVE PT-FINCL-INV-STAT-CDE (PT-INV-STORE-INDX)                       
W33304                           TO  IN034-FINCL-INV-STAT-CDE.                  
W28582                                                                          
W28582     WRITE OUTPUT-REC FROM IN034-SHORTAGE-EXTR-REC.                       
W28582                                                                          
W28582     ADD +1 TO PA-OUTPUT-COUNT.                                           
W28582                                                                          
W28582     IF IN034-SKU = ZEROS                                                 
W28582                        DISPLAY 'ZERO SKU ' IN034-STR-NBR.                
W28582                                                                          
W28582                                                                          
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT166'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                              
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
W29908     PERFORM Z950-ABEND.                                                  
                                                                                
W29908 Z950-ABEND.                                                              
W29908     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
W34557*****************************************************************         
W34557*  COPYBOOK OF DATE EDIT LOGIC FOR INPUT ALTERNATE DATES        *         
W34557*****************************************************************         
W34557     COPY INPD050.                                                        
W34557*****************************************************************         
