00001  ID  DIVISION.                                                    05/31/95
00002  PROGRAM-ID.         ETKUP032.                                    ETKUP032
00003  AUTHOR.             STEVE FLUNKER.                                  LV091
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                        CL**1
00005  DATE-WRITTEN.       APR 1995.                                       CL*48
00006  DATE-COMPILED.                                                      CL**1
00007                                                                      CL**1
00008 ******************************************************************   CL**1
00009 *  ETKUP032 - PO/UPC RECYCLE UTILITY                                 CL*79
00010 *  WORK REQUEST #  - 3044                                            CL**2
00011 ******************************************************************   CL**1
00012 *  THIS PROGRAM WILL VALIDATE INCOMING PURCHASE ORDERS               CL*49
************************************************************************ETKUP034
JF0403*  DATE  04/22/03                                                         
JF0403*  CHANGE MADE:                                                           
JF0403*   UPDATED PROGRAM SO IT IS COMPLIANT WITH NI-SKU OBJECTIVES.            
JF0403*  MADE BY: JON FIEDLER             WR# : 22777                           
************************************************************************        
JF0304*  DATE  03/22/04                                                         
JF0304*  CHANGE MADE:                                                           
JF0304*   ADD CANCEL DATE TO 855 APPLICATION FILE.                              
JF0304*  MADE BY: JON FIEDLER             WR# : 26812                           
************************************************************************        
************************************************************************        
WG0610*  DATE  06/10/10                                                         
WG0610*  CHANGE MADE:                                                           
WG0610*   ADD PARENT-ENT-ID AND FACTORY-ENT-ID..                                
WG0610*  MADE BY: WADE M GROVE            WR# : 36565                           
************************************************************************        
00014 *                                                                    CL**1
00015  ENVIRONMENT DIVISION.                                               CL**1
00016 *                                                                    CL**1
00017  CONFIGURATION SECTION.                                              CL**1
00018 *                                                                    CL**1
00019  SOURCE-COMPUTER.        IBM-3090.                                   CL**1
00020  OBJECT-COMPUTER.        IBM-3090.                                   CL**1
00021 *                                                                    CL**1
00022  INPUT-OUTPUT SECTION.                                               CL**1
00023  FILE-CONTROL.                                                       CL**1
00024      SELECT  PO-INPUT-FILE                                           CL**5
00025          ASSIGN TO INP01.                                            CL**5
00026                                                                      CL**5
00027      SELECT  PO-STATUS-FILE                                          CL**5
00028          ASSIGN TO OUT01.                                            CL**5
00029                                                                      CL**1
00030  DATA DIVISION.                                                      CL**1
00031  FILE SECTION.                                                       CL**1
00032                                                                      CL**1
00033  FD  PO-INPUT-FILE                                                   CL**5
00034      LABEL RECORDS ARE STANDARD                                      CL**1
00035      RECORDING MODE IS F                                             CL**1
00036      BLOCK CONTAINS 0 RECORDS                                        CL**1
00037      RECORD CONTAINS 350 CHARACTERS                                  CL**5
00038      DATA RECORD IS PO-INPUT-RECORD.                                 CL**5
00039                                                                      CL**1
00040  01  PO-INPUT-RECORD             PIC X(350).                         CL**5
00041                                                                      CL**1
00042  FD  PO-STATUS-FILE                                                  CL**5
00043      LABEL RECORDS ARE STANDARD                                      CL**5
00044      RECORDING MODE IS F                                             CL**5
00045      BLOCK CONTAINS 0 RECORDS                                        CL**5
00046      RECORD CONTAINS 350 CHARACTERS                                  CL**5
00047      DATA RECORD IS PO-INPUT-RECORD.                                 CL**5
00048                                                                      CL**5
00049  01  PO-STATUS-RECORD             PIC X(350).                        CL**5
00050                                                                      CL**5
00051                                                                      CL**1
00052  WORKING-STORAGE SECTION.                                            CL**1
00053 *                                                                    CL**1
00054  01  WS-WORKING-STORAGE.                                             CL*12
00055      05  WS-NOSKU.                                                   CL*47
00056          10  FILLER                 PIC X(05) VALUE 'NOSKU'.         CL*47
00057          10  WS-NOSKU-CNT           PIC 9(03) VALUE ZERO.            CL*47
00058                                                                      CL*47
00059      05  WS-HEADER-SAVE-AREA        PIC X(350) VALUE SPACES.         CL*74
00060      05  WS-HEADER-RECORD.                                           CL*74
00061          10  WS-PO-NBR              PIC 9(09) VALUE ZERO.            CL*47
00062          10  WS-SORT-SEQ-NBR        PIC 9(06) VALUE ZERO.            CL*81
00063          10  WS-DEPT-NBR            PIC X(03) VALUE SPACES.          CL*13
00064          10  WS-STATUS              PIC X(01) VALUE SPACE.           CL*13
JF0304         10  FILLER                 PIC X(22) VALUE SPACES.          CL*13
JF0304         10  WS-CANCEL-DATE         PIC X(06) VALUE SPACES.               
00066          10  WS-SHIP-DATE           PIC X(06) VALUE SPACES.          CL*12
00067          10  WS-TP-ID-QUAL          PIC X(02) VALUE SPACES.          CL*65
00068          10  WS-TP-ID               PIC X(15) VALUE SPACES.          CL*65
00069          10  FILLER                 PIC X(01) VALUE SPACE.           CL*13
00068          10  WS-ISA-CNTL-NBR        PIC 9(09) VALUE ZEROS.                
00068          10  WS-GS-CNTL-NBR         PIC 9(09) VALUE ZEROS.                
00068          10  WS-ST-CNTL-NBR         PIC X(09) VALUE SPACES.               
00068          10  WS-TIMESTAMP           PIC X(26) VALUE SPACES.               
00068          10  WS-SESSION-NBR         PIC 9(06) VALUE ZEROS.                
SP0408         10  WS-LC-IND              PIC X(02) VALUE SPACES.               
WG0610         10  WS-PARENT-ID           PIC 9(10).                            
WG0610         10  WS-FACTORY-ID          PIC 9(10).                            
WG0610         10  FILLER                 PIC X(198) VALUE SPACES.         CL*13
00070      05  WS-PO002-H-PO-NBR          PIC S9(9) COMP.                  CL*53
00071      05  WS-PO002-I-UPC             PIC S9(15)V COMP-3.              CL*54
JF0403     05  WS-PO002-I-SKU             PIC S9(08)V COMP-3.                   
JF0403     05  WS-SKU                     PIC X(08) VALUE SPACES.               
JF0403     05  WS-SAVE-DEPT               PIC X(03) VALUE SPACES           CL*13
JF0403                                              JUSTIFIED RIGHT.            
00072 *                                                                    CL*12
00073  01  PV-PROGRAM-VARIABLES.                                           CL*40
00074      05  PV-PARAGRAPH-NAME       PIC  X(30)   VALUE SPACES.          CL*40
00075      05  PV-DB2-OPER-ATTEMPTED   PIC  X(30)   VALUE SPACES.          CL*40
00076      05  PV-SQL-SUB              PIC S9(03)   VALUE +0               CL*83
00077                                               COMP.                  CL*82
00078      05  PV-CURRENT-REC-NBR      PIC  9(06)   VALUE ZERO.            CL*87
00079      05  PV-RECS-THIS-PO         PIC  9(06)   VALUE ZERO.            CL*87
00080      05  PV-SORT-SEQ-NBR         PIC  9(06)   VALUE ZERO.            CL*88
00081 *                                                                    CL*40
00082  01  PC-PROGRAM-CONSTANTS.                                           CL*40
00083      05  PC-MAX-RETRIES          PIC S9(03)   VALUE +03              CL*40
00084                                               COMP.                  CL*82
00085 *                                                                    CL**1
00086  01  PS-PROGRAM-SWITCHES.                                            CL*15
00087      05  PS-END-OF-FILE             PIC X     VALUE 'N'.             CL**9
00088          88  PS-EOF                           VALUE 'Y'.             CL**1
00089      05  PS-FIRST-HEADER            PIC X     VALUE 'Y'.             CL*20
00090          88  FIRST-HEADER                     VALUE 'Y'.             CL*12
00091          88  PAST-FIRST-HEADER                VALUE 'N'.             CL*12
00092      05  PS-UPC-CATALOG-NOT-NEEDED  PIC X     VALUE 'Y'.             CL*19
00093          88  UPC-CATALOG-NOT-NEEDED           VALUE 'Y'.             CL*20
00094          88  UPC-CATALOG-NEEDED               VALUE 'N'.             CL*20
00095      05  PS-ONE-DEPARTMENT          PIC X     VALUE 'Y'.             CL*20
00096          88  ONE-DEPT                         VALUE 'Y'.             CL*15
00097          88  MULTIPLE-DEPTS                   VALUE 'N'.             CL*20
00098      05  PS-ALL-GOOD-UPCS           PIC X     VALUE 'Y'.             CL*20
00099          88  ALL-GOOD-UPCS                    VALUE 'Y'.             CL*15
00100          88  NOT-ALL-GOOD-UPCS                VALUE 'N'.             CL*20
00101 *                                                                    CL**1
00102 *****************************************************************    CL**1
00103 *****  TO BE DELETED FILE RECORD LAYOUT.                             CL**1
00104 *****************************************************************    CL**1
00105 *                                                                    CL**1
00106      COPY PORD002.                                                   CL*91
00107      EJECT                                                           CL**1
00108 *                                                                    CL**1
00109 ******************************************************************   CL**1
00110 *  SQL ABEND WORKING STORAGE                                         CL**1
00111 ******************************************************************   CL**1
00112      COPY DPWS004.                                                   CL**1
00113      EJECT                                                           CL**1
00114 *                                                                    CL**1
JF0403*****************************************************************    CL**1
JF0403*****  DB2 SKU CROSS REFERENCE TABLE DCLGEN                          CL**4
JF0403*****************************************************************    CL**1
JF0403*                                                                    CL**1
JF0403     EXEC SQL                                                        CL**1
JF0403         INCLUDE TSKXREF                                             CL**4
JF0403     END-EXEC.                                                       CL**1
JF0403*                                                                    CL**1
00123 *****************************************************************    CL**1
00124 *****  DB2 PO TABLE DCLGEN                                           CL*47
00125 *****************************************************************    CL**1
00126 *                                                                    CL**1
00127      EXEC SQL                                                        CL**1
00128          INCLUDE TPURCHS                                             CL*47
00129      END-EXEC.                                                       CL**1
00130 *                                                                    CL*47
00131 *****************************************************************    CL*47
00132 *****  DB2 UPC / SKU TABLE DCLGEN                                    CL*47
00133 *****************************************************************    CL*47
00134 *                                                                    CL*47
00135      EXEC SQL                                                        CL*47
00136          INCLUDE TUPCSKU                                             CL*47
00137      END-EXEC.                                                       CL*47
00138 *                                                                    CL**1
00139 *****************************************************************    CL**1
00140 *****  DB2 EDI UPC LOOKUP TABLE DCLGEN                               CL*27
00141 *****************************************************************    CL**1
00142 *                                                                    CL**1
00143      EXEC SQL                                                        CL**1
00144          INCLUDE TUNUPCS                                             CL*27
00145      END-EXEC.                                                       CL**1
00146 *                                                                    CL*27
00147 *****************************************************************    CL*27
00148 *****  DB2 EDI NETWORK / VENDOR TABLE DCLGEN                         CL*27
00149 *****************************************************************    CL*27
00150 *                                                                    CL*27
00151      EXEC SQL                                                        CL*27
00152          INCLUDE TNETVND                                             CL*27
00153      END-EXEC.                                                       CL*27
00154 *                                                                    CL**1
00155 *****************************************************************    CL**1
00156 *****  DB2 EDI TRADING PARTNER TABLE DCLGEN                          CL**4
00157 *****************************************************************    CL**1
00158 *                                                                    CL**1
00159      EXEC SQL                                                        CL**1
00160          INCLUDE TTRDPRT                                             CL**4
00161      END-EXEC.                                                       CL**1
00162 *                                                                    CL**1
00163 *****************************************************************    CL**1
00164 * DB2 COMMUNICATIONS AREA                                            CL**1
00165 *****************************************************************    CL**1
00166                                                                      CL**1
00167      EXEC SQL                                                        CL**1
00168          INCLUDE SQLCA                                               CL**1
00169      END-EXEC.                                                       CL**1
00170 *                                                                    CL**1
00171      COPY SQLCA2.                                                    CL**1
00172                                                                      CL**1
00173  01  ABEND-CODE               PIC S9(04)  VALUE ZERO                 CL**1
00174                                           COMP SYNC.                 CL**1
00175      EJECT                                                           CL**1
00176  LINKAGE SECTION.                                                    CL**1
00177 *                                                                    CL**1
00178  PROCEDURE DIVISION.                                                 CL**1
00179 *                                                                    CL**1
00180 ******************************************************************   CL**1
00181 * MAIN-LINE LOGIC.                                                   CL**1
00182 ******************************************************************   CL**1
00183  A100-MAIN-MODULE.                                                   CL**1
00184 *                                                                    CL**1
00185      OPEN INPUT  PO-INPUT-FILE.                                      CL*63
00186      OPEN OUTPUT PO-STATUS-FILE.                                     CL*63
00187 *                                                                    CL**1
00188      PERFORM Z100-READ-INPUT-FILE.                                   CL**8
00189 *                                                                    CL**8
00190      SET UPC-CATALOG-NOT-NEEDED TO TRUE.                             CL*20
00191      SET FIRST-HEADER TO TRUE.                                       CL*12
00192 *                                                                    CL*79
00193      PERFORM B100-PROCESS-PO-FILE                                    CL**6
00194              UNTIL PS-EOF.                                           CL**1
00195 *                                                                    CL**1
00196      PERFORM B200-WRAPUP-PO-FILE.                                    CL*76
00197 *                                                                    CL*76
00198      IF UPC-CATALOG-NEEDED                                           CL*32
00199         MOVE 4060 TO RETURN-CODE                                     CL*20
00200      END-IF.                                                         CL*20
00201 *                                                                    CL*20
00202      CLOSE PO-INPUT-FILE                                             CL*62
00203            PO-STATUS-FILE.                                           CL*62
00204      STOP RUN.                                                       CL**1
00205 *                                                                    CL**1
00206 ******************************************************************   CL**1
00207 *  PROCESS ALL PO RECORDS FROM INPUT FILE.                           CL**3
00208 ******************************************************************   CL**1
00209  B100-PROCESS-PO-FILE.                                               CL**6
00210 *                                                                    CL**1
00211       IF PO002-HEADER-RECORD                                         CL*12
00212                                                                      CL*75
00213 * MOVE NEW HEADER TO SAVE AREA                                       CL*75
00214          MOVE PO002-EDI855-HEADER-RECORD                             CL*75
00215               TO WS-HEADER-SAVE-AREA                                 CL*75
00216                                                                      CL*75
00217          IF FIRST-HEADER                                             CL*59
00218             SET PAST-FIRST-HEADER TO TRUE                            CL*59
00219          ELSE                                                        CL*59
00220 * MOVE OLD HEADER TO PO002 HEADER TO WRAP UP THAT PO                 CL*74
00221             MOVE WS-HEADER-RECORD                                    CL*59
00222                  TO PO002-EDI855-HEADER-RECORD                       CL*59
00223                                                                      CL*79
00224             SET PO002-HEADER-RECORD TO TRUE                          CL*59
00225             MOVE SPACES TO PO002-H-DEPT-NBR                          CL*59
00226 *                                                                    CL*14
00227             IF WS-DEPT-NBR = SPACES                                  CL*59
00228                SET PO002-NO-DEPT TO TRUE                             CL*59
00229             ELSE                                                     CL*59
00230                IF ONE-DEPT                                           CL*59
00231                   MOVE WS-DEPT-NBR TO PO002-H-DEPT-NBR               CL*59
00232                   SET PO002-ONE-DEPT TO TRUE                         CL*59
00233                ELSE                                                  CL*59
00234                   SET PO002-MULTIPLE-DEPTS TO TRUE                   CL*59
00235                END-IF                                                CL*59
00236             END-IF                                                   CL*59
00237                                                                      CL*47
00238             IF ALL-GOOD-UPCS                                         CL*47
00239                SET PO002-GOOD-UPCS TO TRUE                           CL*47
00240             ELSE                                                     CL*47
00241                SET PO002-BAD-UPCS TO TRUE                            CL*47
00242             END-IF                                                   CL*47
00243             MOVE WS-PO-NBR TO WS-PO002-H-PO-NBR                      CL*68
00244             PERFORM C100-VERIFY-PO-NBR                               CL*47
00245             IF (SQLCODE = +000 OR -811) AND WS-LC-IND NOT = 'LC'     CL*47
00246                SET PO002-DUPLICATE-PO TO TRUE                        CL*47
00247             ELSE                                                     CL*47
00248                IF PO002-ONE-DEPT AND                                 CL*47
00249                   PO002-GOOD-UPCS                                    CL*47
00250                   SET PO002-NEW-PO-GOOD TO TRUE                      CL*47
00251                ELSE                                                  CL*47
00252                   SET PO002-NEW-PO-BAD TO TRUE                       CL*47
00253                END-IF                                                CL*47
00254             END-IF                                                   CL*47
00255                                                                      CL*47
00256             COMPUTE PV-SORT-SEQ-NBR =                                CL*88
00257                 PV-CURRENT-REC-NBR - PV-RECS-THIS-PO                 CL*87
00258                                                                      CL*88
00259             MOVE PV-SORT-SEQ-NBR TO PO002-H-SORT-SEQ-NBR             CL*88
00260                                                                      CL*86
00261             PERFORM Z200-WRITE-OUTPUT-FILE                           CL*16
00262                                                                      CL*47
00263          END-IF                                                      CL*16
00264                                                                      CL*74
00265 * MOVE NEW SAVED HEADER BACK INTO PO002                              CL*74
00266          MOVE WS-HEADER-SAVE-AREA                                    CL*74
00267                  TO PO002-EDI855-HEADER-RECORD                       CL*74
00268                                                                      CL*74
00269          MOVE SPACES TO WS-DEPT-NBR                                  CL*16
00270          SET ALL-GOOD-UPCS     TO TRUE                               CL*65
00271          SET ONE-DEPT          TO TRUE                               CL*65
00272          MOVE 0                TO WS-NOSKU-CNT                       CL*65
00273          MOVE 0                TO PV-RECS-THIS-PO                    CL*87
00274          ADD  1                TO PV-CURRENT-REC-NBR                 CL*87
00275          MOVE PO002-H-PO-NBR   TO WS-PO-NBR                          CL*65
00276          MOVE PO002-SHIP-DATE  TO WS-SHIP-DATE                       CL*65
JF0304         MOVE PO002-CANCEL-DATE  TO WS-CANCEL-DATE                   CL*65
00277          MOVE PO002-H-INTERCHANGE-ID-QUAL TO WS-TP-ID-QUAL           CL*65
00278          MOVE PO002-H-INTERCHANGE-ID      TO WS-TP-ID                CL*65
00278          MOVE PO002-H-ISA-CNTL-NBR        TO WS-ISA-CNTL-NBR              
00278          MOVE PO002-H-GS-CNTL-NBR         TO WS-GS-CNTL-NBR               
00278          MOVE PO002-H-ST-CNTL-NBR         TO WS-ST-CNTL-NBR               
00278          MOVE PO002-H-TIMESTAMP           TO WS-TIMESTAMP                 
00278          MOVE PO002-H-SESSION-NBR         TO WS-SESSION-NBR               
SP0408         MOVE PO002-H-LC-IND              TO WS-LC-IND                    
WG0610         MOVE PO002-H-PARENT-ENT-ID       TO WS-PARENT-ID                 
WG0610         MOVE PO002-H-FACTORY-ENT-ID      TO WS-FACTORY-ID                
00279       END-IF.                                                        CL*59
00280 *                                                                    CL*66
00281       IF PO002-ITEM-RECORD                                           CL*16
00282          IF PO002-I-UPC NOT = SPACES                                 CL*47
00283 *                                                                    CL*55
00284             MOVE PO002-I-UPC TO WS-PO002-I-UPC                       CL*54
00285             PERFORM C150-VERIFY-UPC-SKU                              CL*54
00286 *                                                                    CL*55
00287             IF SQLCODE = +000                                        CL*47
JF0403               MOVE SKXREF-SKU TO WS-SKU                             CL*47
JF0403               MOVE WS-SKU TO PO002-I-SKU                            CL*47
JF0403               MOVE SKXREF-DEPT TO WS-SAVE-DEPT                           
00289                IF WS-DEPT-NBR = SPACES                               CL*47
JF0403                  MOVE WS-SAVE-DEPT TO WS-DEPT-NBR                   CL*47
00291                ELSE                                                  CL*47
JF0403                  IF WS-SAVE-DEPT = WS-DEPT-NBR                      CL*47
00293                      CONTINUE                                        CL*47
00294                   ELSE                                               CL*47
00295                      SET MULTIPLE-DEPTS TO TRUE                      CL*47
00296                   END-IF                                             CL*47
00297                END-IF                                                CL*47
00298             ELSE                                                     CL*47
00299                IF SQLCODE = +100                                     CL*47
00300                   SET NOT-ALL-GOOD-UPCS TO TRUE                      CL*47
00301                   ADD +1 TO WS-NOSKU-CNT                             CL*47
00302                   MOVE WS-NOSKU TO PO002-I-SKU                       CL*57
00303                                                                      CL*47
00304                   MOVE PO002-I-UPC TO WS-PO002-I-UPC                 CL*54
00305                   PERFORM C200-VERIFY-UPC-ONLY                       CL*47
00306                                                                      CL*47
00307                   IF SQLCODE = +000                                  CL*47
00308                      CONTINUE                                        CL*47
00309                   ELSE                                               CL*47
00310                      SET UPC-CATALOG-NEEDED TO TRUE                  CL*77
00311                      PERFORM C300-FIND-WHICH-NETWORK                 CL*47
00312                      IF SQLCODE = +000                               CL*47
00313                         CONTINUE                                     CL*47
00314                      ELSE                                            CL*47
00315                         IF SQLCODE = +100 OR -811                    CL*47
00316                            MOVE SPACES TO NETVND-NETWK-ID            CL*47
00317                         ELSE                                         CL*47
00318                            DISPLAY 'SQLCODE: ' SQLCODE               CL*47
00319                            MOVE 'B100-PROCESS-PO-FILE'               CL*47
00320                               TO PV-PARAGRAPH-NAME                   CL*47
00321                            MOVE 'FIND WHICH NETOWRK FOR UPC'         CL*47
00322                               TO PV-DB2-OPER-ATTEMPTED               CL*47
00323                            PERFORM Z900-SQL-ABEND                    CL*47
00324                         END-IF                                       CL*47
00325                      END-IF                                          CL*47
00326                                                                      CL*47
00327                      MOVE PO002-I-UPC TO WS-PO002-I-UPC              CL*54
00328                      PERFORM C400-INSERT-UPC                         CL*47
00329                      IF SQLCODE = +000                               CL*47
00330                         EXEC SQL                                     CL*47
00331                            COMMIT                                    CL*47
00332                         END-EXEC                                     CL*47
00333                      ELSE                                            CL*47
00334                         DISPLAY 'SQLCODE: ' SQLCODE                  CL*47
00335                         MOVE 'B100-PROCESS-PO-FILE'                  CL*47
00336                            TO PV-PARAGRAPH-NAME                      CL*47
00337                         MOVE 'INSERT UPC THEN COMMIT'                CL*47
00338                            TO PV-DB2-OPER-ATTEMPTED                  CL*47
00339                         PERFORM Z900-SQL-ABEND                       CL*47
00340                      END-IF                                          CL*47
00341                   END-IF                                             CL*47
00342                ELSE                                                  CL*47
00343                   DISPLAY 'SQLCODE: ' SQLCODE                        CL*47
00344                   MOVE 'B100-PROCESS-PO-FILE'                        CL*47
00345                      TO PV-PARAGRAPH-NAME                            CL*47
00346                   MOVE 'FIND UPC AND SKU'                            CL*47
00347                      TO PV-DB2-OPER-ATTEMPTED                        CL*47
00348                   PERFORM Z900-SQL-ABEND                             CL*47
00349                END-IF                                                CL*47
00350             END-IF                                                   CL*47
00351          ELSE                                                        CL*90
JF0403            MOVE PO002-I-SKU TO WS-PO002-I-SKU                            
JF0403            PERFORM C500-GET-DEPT-FOR-SKU                                 
JF0403            IF SQLCODE = 0                                                
JF0403               MOVE SKXREF-DEPT TO WS-SAVE-DEPT                           
JF0403               IF WS-DEPT-NBR = SPACES                                  CL
JF0403                  MOVE WS-SAVE-DEPT TO WS-DEPT-NBR                      CL
JF0403               ELSE                                                     CL
JF0403                  IF WS-SAVE-DEPT = WS-DEPT-NBR                         CL
JF0403                     CONTINUE                                           CL
JF0403                  ELSE                                                  CL
JF0403                     SET MULTIPLE-DEPTS TO TRUE                         CL
JF0403                  END-IF                                                CL
JF0403               END-IF                                                   CL
JF0403            END-IF                                                        
JF0403         END-IF                                                      CL*47
00362 *                                                                    CL*85
00363          ADD 1 TO PV-RECS-THIS-PO                                    CL*87
00364          ADD 1 TO PV-CURRENT-REC-NBR                                 CL*87
00365          MOVE PV-CURRENT-REC-NBR TO PO002-I-SORT-SEQ-NBR             CL*87
00366          PERFORM Z200-WRITE-OUTPUT-FILE                              CL*66
00367       END-IF.                                                        CL*47
00368 *                                                                    CL*20
00369       IF PO002-ALLOC-RECORD                                          CL*20
00370          ADD 1 TO PV-RECS-THIS-PO                                    CL*87
00371          ADD 1 TO PV-CURRENT-REC-NBR                                 CL*87
00372          MOVE PV-CURRENT-REC-NBR TO PO002-A-SORT-SEQ-NBR             CL*87
00373          PERFORM Z200-WRITE-OUTPUT-FILE                              CL*20
00374       END-IF.                                                        CL*20
00375 *                                                                    CL*20
00376       PERFORM Z100-READ-INPUT-FILE.                                  CL*72
00377 *                                                                    CL*20
00378 ******************************************************************   CL*76
00379 *  PROCESS ALL PO RECORDS FROM INPUT FILE.                           CL*76
00380 ******************************************************************   CL*76
00381  B200-WRAPUP-PO-FILE.                                                CL*76
00382 *                                                                    CL*76
00383 * MOVE OLD HEADER TO PO002 HEADER TO WRAP UP THAT PO                 CL*76
00384      MOVE WS-HEADER-RECORD                                           CL*76
00385           TO PO002-EDI855-HEADER-RECORD.                             CL*76
00386                                                                      CL*79
00387      SET PO002-HEADER-RECORD TO TRUE.                                CL*76
00388      MOVE SPACES TO PO002-H-DEPT-NBR.                                CL*76
00389                                                                      CL*76
00390      IF WS-DEPT-NBR = SPACES                                         CL*76
00391         SET PO002-NO-DEPT TO TRUE                                    CL*76
00392      ELSE                                                            CL*76
00393         IF ONE-DEPT                                                  CL*76
00394            MOVE WS-DEPT-NBR TO PO002-H-DEPT-NBR                      CL*76
00395            SET PO002-ONE-DEPT TO TRUE                                CL*76
00396         ELSE                                                         CL*76
00397            SET PO002-MULTIPLE-DEPTS TO TRUE                          CL*76
00398         END-IF                                                       CL*76
00399      END-IF.                                                         CL*76
00400                                                                      CL*76
00401      IF ALL-GOOD-UPCS                                                CL*76
00402         SET PO002-GOOD-UPCS TO TRUE                                  CL*76
00403      ELSE                                                            CL*76
00404         SET PO002-BAD-UPCS TO TRUE                                   CL*76
00405      END-IF.                                                         CL*76
00406      MOVE WS-PO-NBR TO WS-PO002-H-PO-NBR                             CL*76
00407      PERFORM C100-VERIFY-PO-NBR.                                     CL*76
00408      IF (SQLCODE = +000 OR -811) AND WS-LC-IND NOT = 'LC'            CL*76
00409         SET PO002-DUPLICATE-PO TO TRUE                               CL*76
00410      ELSE                                                            CL*76
00411         IF PO002-ONE-DEPT AND                                        CL*76
00412            PO002-GOOD-UPCS                                           CL*76
00413            SET PO002-NEW-PO-GOOD TO TRUE                             CL*76
00414         ELSE                                                         CL*76
00415            SET PO002-NEW-PO-BAD TO TRUE                              CL*76
00416         END-IF                                                       CL*76
00417      END-IF.                                                         CL*76
00418                                                                      CL*88
00419      COMPUTE PV-SORT-SEQ-NBR =                                       CL*88
00420          PV-CURRENT-REC-NBR - PV-RECS-THIS-PO.                       CL*88
00421                                                                      CL*88
00422      MOVE PV-SORT-SEQ-NBR TO PO002-H-SORT-SEQ-NBR.                   CL*88
00423                                                                      CL*76
00424      PERFORM Z200-WRITE-OUTPUT-FILE.                                 CL*76
00425 *                                                                    CL*76
00426 ******************************************************************   CL**1
00427 *  VERIFY THAT THE UPC AND SKU ARE ON THE APPROPRIATE TABLES         CL*17
00428 ******************************************************************   CL**1
00429  C100-VERIFY-PO-NBR.                                                 CL*47
00430 *                                                                    CL**1
00431      PERFORM                                                         CL**1
00432          WITH TEST AFTER                                             CL**1
00433          VARYING PV-SQL-SUB                                          CL**1
00434          FROM 1 BY 1                                                 CL**1
00435          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**1
00436                    OR SQLCODE = 0                                    CL**1
00437                    OR SQLCODE = +100                                 CL*47
00438                    OR SQLCODE = -811)                                CL*47
00439 *                                                                    CL**1
00440          EXEC SQL                                                    CL*47
00441              SELECT A.PO_NBR                                         CL*47
00442              INTO :PURCHS-PO-NBR                                     CL*47
00443              FROM TPURCHS A                                          CL*47
00444              WHERE A.PO_NBR = :WS-PO002-H-PO-NBR                     CL*53
00445          END-EXEC                                                    CL*47
00446 *                                                                    CL*47
00447          EVALUATE TRUE                                               CL*47
00448              WHEN SQLCODE = -904                                     CL*47
00449              WHEN SQLCODE = -911                                     CL*47
00450              WHEN SQLCODE = -811                                     CL*47
00451              WHEN SQLCODE = 100                                      CL*47
00452              WHEN SQLCODE = 0                                        CL*47
00453                  CONTINUE                                            CL*47
00454              WHEN SQLWARN NOT = SPACES                               CL*47
00455              WHEN SQLCODE NOT = ZERO                                 CL*47
00456                  MOVE 'C100-VERIFY-PO-NBR'                           CL*47
00457                                TO  PV-PARAGRAPH-NAME                 CL*47
00458                  MOVE 'VERIFY PO NUMBER'                             CL*47
00459                                TO  PV-DB2-OPER-ATTEMPTED             CL*47
00460                  PERFORM Z900-SQL-ABEND                              CL*47
00461          END-EVALUATE                                                CL*47
00462      END-PERFORM.                                                    CL*47
00463 *                                                                    CL*47
00464      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*47
00465          MOVE 'C100-VERIFY-PO-NBR'                                   CL*47
00466                                TO  PV-PARAGRAPH-NAME                 CL*47
00467          MOVE 'VERIFY RETRIES EXCEEDED'                              CL*47
00468                                TO  PV-DB2-OPER-ATTEMPTED             CL*47
00469          PERFORM Z900-SQL-ABEND                                      CL*47
00470      END-IF.                                                         CL*47
00471 *                                                                    CL*47
00472 ******************************************************************   CL*47
00473 *  VERIFY THAT THE UPC AND SKU ARE ON THE APPROPRIATE TABLES         CL*47
00474 ******************************************************************   CL*47
00475  C150-VERIFY-UPC-SKU.                                                CL*47
00476 *                                                                    CL*47
00477      PERFORM                                                         CL*47
00478          WITH TEST AFTER                                             CL*47
00479          VARYING PV-SQL-SUB                                          CL*47
00480          FROM 1 BY 1                                                 CL*47
00481          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*47
00482                    OR SQLCODE = 0                                    CL*47
00483                    OR SQLCODE = +100)                                CL*47
00484 *                                                                    CL*47
00485          EXEC SQL                                                    CL**1
JF0403             SELECT A.SKU                                            CL*17
JF0403                  , A.DEPT                                           CL*28
JF0403             INTO :SKXREF-SKU                                        CL*21
JF0403                 ,:SKXREF-DEPT                                       CL*21
JF0403             FROM TSKXREF A                                          CL*17
JF0403                 ,TUPCSKU B                                               
JF0403             WHERE B.UPC_NBR  = :WS-PO002-I-UPC                      CL*54
JF0403               AND A.ITM_NBR = B.ITEM_NBR                            CL*28
00495          END-EXEC                                                    CL**1
00496 *                                                                    CL**1
00497          EVALUATE TRUE                                               CL**1
00498              WHEN SQLCODE = -904                                     CL**1
00499              WHEN SQLCODE = -911                                     CL**1
00500              WHEN SQLCODE = 100                                      CL**1
00501              WHEN SQLCODE = 0                                        CL**1
00502                  CONTINUE                                            CL**1
00503              WHEN SQLWARN NOT = SPACES                               CL**1
00504              WHEN SQLCODE NOT = ZERO                                 CL**1
00505                  MOVE 'C150-VERIFY-UPC-SKU'                          CL*47
00506                                TO  PV-PARAGRAPH-NAME                 CL**1
00507                  MOVE 'VERIFY UPCS AND SKUS'                         CL*17
00508                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00509                  PERFORM Z900-SQL-ABEND                              CL**1
00510          END-EVALUATE                                                CL**1
00511      END-PERFORM.                                                    CL**1
00512 *                                                                    CL**1
00513      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**1
00514          MOVE 'C150-VERIFY-UPC-SKU'                                  CL*47
00515                                TO  PV-PARAGRAPH-NAME                 CL**1
00516          MOVE 'VERIFY RETRIES EXCEEDED'                              CL*17
00517                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00518          PERFORM Z900-SQL-ABEND                                      CL**1
00519      END-IF.                                                         CL**1
00520 *                                                                    CL*20
00521 ******************************************************************   CL*20
00522 *  VERIFY THAT THE UPC IS ON THE UPC TABLE                           CL*20
00523 ******************************************************************   CL*20
00524  C200-VERIFY-UPC-ONLY.                                               CL*30
00525 *                                                                    CL*20
00526      PERFORM                                                         CL*20
00527          WITH TEST AFTER                                             CL*20
00528          VARYING PV-SQL-SUB                                          CL*20
00529          FROM 1 BY 1                                                 CL*20
00530          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*20
00531                    OR SQLCODE = 0                                    CL*20
00532                    OR SQLCODE = +100)                                CL*20
00533 *                                                                    CL*20
00534          EXEC SQL                                                    CL*20
00535              SELECT A.UPC_CDE                                        CL*20
00536              INTO :UNUPCS-UPC-CDE                                    CL*22
00537              FROM TUNUPCS A                                          CL*20
00538              WHERE A.UPC_CDE  = :WS-PO002-I-UPC                      CL*54
00539          END-EXEC                                                    CL*20
00540 *                                                                    CL*20
00541          EVALUATE TRUE                                               CL*20
00542              WHEN SQLCODE = -904                                     CL*20
00543              WHEN SQLCODE = -911                                     CL*20
00544              WHEN SQLCODE = 100                                      CL*20
00545              WHEN SQLCODE = 0                                        CL*20
00546                  CONTINUE                                            CL*20
00547              WHEN SQLWARN NOT = SPACES                               CL*20
00548              WHEN SQLCODE NOT = ZERO                                 CL*20
00549                  MOVE 'C200-VERIFY-UPC-ONLY'                         CL*20
00550                                TO  PV-PARAGRAPH-NAME                 CL*20
00551                  MOVE 'VERIFY UPC ON TUNUPCS'                        CL*20
00552                                TO  PV-DB2-OPER-ATTEMPTED             CL*20
00553                  PERFORM Z900-SQL-ABEND                              CL*20
00554          END-EVALUATE                                                CL*20
00555      END-PERFORM.                                                    CL*20
00556 *                                                                    CL*20
00557      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*20
00558          MOVE 'C200-VERIFY-UPC-ONLY'                                 CL*20
00559                                TO  PV-PARAGRAPH-NAME                 CL*20
00560          MOVE 'VERIFY RETRIES EXCEEDED'                              CL*20
00561                                TO  PV-DB2-OPER-ATTEMPTED             CL*20
00562          PERFORM Z900-SQL-ABEND                                      CL*20
00563      END-IF.                                                         CL*20
00564 *                                                                    CL**1
00565 ******************************************************************   CL*20
00566 *  FIND WHICH NETWORK THE UPC SHOULD BE LOOKED UP ON                 CL*20
00567 ******************************************************************   CL*20
00568  C300-FIND-WHICH-NETWORK.                                            CL*30
00569 *                                                                    CL*20
00570      PERFORM                                                         CL*20
00571          WITH TEST AFTER                                             CL*20
00572          VARYING PV-SQL-SUB                                          CL*20
00573          FROM 1 BY 1                                                 CL*20
00574          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*20
00575                    OR SQLCODE = 0                                    CL*20
00576                    OR SQLCODE = +100                                 CL*20
00577                    OR SQLCODE = -811)                                CL*20
00578 *                                                                    CL*20
00579          EXEC SQL                                                    CL*20
00580              SELECT A.NETWK_ID                                       CL*20
00581              INTO :NETVND-NETWK-ID                                   CL*22
00582              FROM TNETVND A,                                         CL*20
00583                   TTRDPRT B                                          CL*20
00584              WHERE B.VEND_INCHG_ID_DESC = :WS-TP-ID-QUAL AND         CL*44
00585                    B.VEND_INCHG_ID      = :WS-TP-ID      AND         CL*44
00586                    A.ENT_ID             = B.ENT_ID       AND         CL*44
00587                    A.PREF_NETWK_SW_IND   = 'Y'                       CL*20
00588          END-EXEC                                                    CL*20
00589 *                                                                    CL*20
00590          EVALUATE TRUE                                               CL*20
00591              WHEN SQLCODE = -904                                     CL*20
00592              WHEN SQLCODE = -911                                     CL*20
00593              WHEN SQLCODE = -811                                     CL*20
00594              WHEN SQLCODE = 100                                      CL*20
00595              WHEN SQLCODE = 0                                        CL*20
00596                  CONTINUE                                            CL*20
00597              WHEN SQLWARN NOT = SPACES                               CL*20
00598              WHEN SQLCODE NOT = ZERO                                 CL*20
00599                  MOVE 'C300-FIND-WHICH-NETWORK'                      CL*20
00600                                TO  PV-PARAGRAPH-NAME                 CL*20
00601                  MOVE 'FIND WHICH NETWORK FOR UPC'                   CL*20
00602                                TO  PV-DB2-OPER-ATTEMPTED             CL*20
00603                  PERFORM Z900-SQL-ABEND                              CL*20
00604          END-EVALUATE                                                CL*20
00605      END-PERFORM.                                                    CL*20
00606 *                                                                    CL*20
00607      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*20
00608          MOVE 'C300-FIND-WHICH-NETWORK'                              CL*30
00609                                TO  PV-PARAGRAPH-NAME                 CL*20
00610          MOVE 'VERIFY RETRIES EXCEEDED'                              CL*20
00611                                TO  PV-DB2-OPER-ATTEMPTED             CL*20
00612          PERFORM Z900-SQL-ABEND                                      CL*20
00613      END-IF.                                                         CL*20
00614 *                                                                    CL*20
00615 ******************************************************************   CL*20
00616 *  INSERT UPC INTO THE TUNUPCS TABLE                                 CL*20
00617 ******************************************************************   CL*20
00618  C400-INSERT-UPC.                                                    CL*30
00619                                                                      CL*20
00620      PERFORM                                                         CL*20
00621          WITH TEST AFTER                                             CL*20
00622          VARYING PV-SQL-SUB                                          CL*20
00623          FROM 1 BY 1                                                 CL*20
00624          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*20
00625                    OR SQLCODE = 0)                                   CL*50
00626                                                                      CL*20
00627          EXEC SQL                                                    CL*20
00628              INSERT INTO TUNUPCS                                     CL*20
00629                     (UPC_CDE,                                        CL*23
00630                      CRE_DTE,                                        CL*23
00631                      PROD_ID,                                        CL*23
00632                      PROD_DESC,                                      CL*23
00633                      SIZE_CDE,                                       CL*23
00634                      SIZE_DESC,                                      CL*23
00635                      COLOR_CDE,                                      CL*23
00636                      COLOR_DESC,                                     CL*23
00637                      TRANS_IND,                                      CL*23
00638                      UPC_CAT_VEND_ID,                                CL*23
00639                      UPC_CAT_VEND_NME,                               CL*23
00640                      CAT_UNMATCH_DTE,                                CL*23
00641                      NETWK_ID,                                       CL*23
00642                      SYST_ID,                                        CL*23
00643                      PO_NBR,                                         CL*23
00644                      TP_REQ_IND)                                     CL*23
00645              VALUES (:WS-PO002-I-UPC,                                CL*54
00646                      CURRENT DATE,                                   CL*23
00647                      '                    ',                         CL*26
00648                      '                              ',               CL*26
00649                      '     ',                                        CL*26
00650                      '                    ',                         CL*26
00651                      '   ',                                          CL*26
00652                      '                    ',                         CL*26
00653                      'Y',                                            CL*23
00654                      '               ',                              CL*26
00655                      '                                   ',          CL*26
00656                      '9999-09-09',                                   CL*23
00657                      :NETVND-NETWK-ID,                               CL*23
00658                      'RE',                                           CL*78
00659                      +0,                                             CL*23
00660                      'N')                                            CL*23
00661          END-EXEC                                                    CL*20
00662                                                                      CL*20
00663          EVALUATE TRUE                                               CL*20
00664              WHEN SQLCODE = -904                                     CL*20
00665              WHEN SQLCODE = -911                                     CL*20
00666              WHEN SQLCODE = -811                                     CL*20
00667              WHEN SQLCODE = 0                                        CL*20
00668                  CONTINUE                                            CL*20
00669              WHEN SQLWARN NOT = SPACES                               CL*20
00670              WHEN SQLCODE NOT = ZERO                                 CL*20
00671                  MOVE 'C400-INSERT-UPC'                              CL*20
00672                                TO  PV-PARAGRAPH-NAME                 CL*20
00673                  MOVE 'INSERT UPC INTO TUNUPCS'                      CL*20
00674                                TO  PV-DB2-OPER-ATTEMPTED             CL*20
00675                  PERFORM Z900-SQL-ABEND                              CL*20
00676          END-EVALUATE                                                CL*20
00677      END-PERFORM.                                                    CL*20
00678                                                                      CL*20
00679      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*20
00680          MOVE 'C400-INSERT-UPC'                                      CL*20
00681                                TO  PV-PARAGRAPH-NAME                 CL*20
00682          MOVE 'VERIFY RETRIES EXCEEDED'                              CL*20
00683                                TO  PV-DB2-OPER-ATTEMPTED             CL*20
00684          PERFORM Z900-SQL-ABEND                                      CL*20
00685      END-IF.                                                         CL*20
00686 *                                                                    CL*20
JF0403******************************************************************   CL*47
JF0403*  GET DEPARTMENT FOR THE SPECIFIED SKU                              CL*47
JF0403******************************************************************   CL*47
JF0403 C500-GET-DEPT-FOR-SKU.                                              CL*47
JF0403*                                                                    CL*47
JF0403     PERFORM                                                         CL*47
JF0403         WITH TEST AFTER                                             CL*47
JF0403         VARYING PV-SQL-SUB                                          CL*47
JF0403         FROM 1 BY 1                                                 CL*47
JF0403         UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*47
JF0403                   OR SQLCODE = 0                                    CL*47
JF0403                   OR SQLCODE = +100)                                CL*47
JF0403*                                                                    CL*47
JF0403         EXEC SQL                                                    CL**1
JF0403             SELECT A.DEPT                                           CL*28
JF0403             INTO :SKXREF-DEPT                                       CL*21
JF0403             FROM TSKXREF A                                          CL*17
JF0403             WHERE SKU = :WS-PO002-I-SKU                             CL*54
JF0403         END-EXEC                                                    CL**1
JF0403*                                                                    CL**1
JF0403         EVALUATE TRUE                                               CL**1
JF0403             WHEN SQLCODE = -904                                     CL**1
JF0403             WHEN SQLCODE = -911                                     CL**1
JF0403             WHEN SQLCODE = 100                                      CL**1
JF0403             WHEN SQLCODE = 0                                        CL**1
JF0403                 CONTINUE                                            CL**1
JF0403             WHEN SQLWARN NOT = SPACES                               CL**1
JF0403             WHEN SQLCODE NOT = ZERO                                 CL**1
JF0403                 MOVE 'C500-GET-DEPT-FOR-SKU'                        CL*47
JF0403                               TO  PV-PARAGRAPH-NAME                 CL**1
JF0403                 MOVE 'GET DEPT FOR SKU'                             CL*17
JF0403                               TO  PV-DB2-OPER-ATTEMPTED             CL**1
JF0403                 PERFORM Z900-SQL-ABEND                              CL**1
JF0403         END-EVALUATE                                                CL**1
JF0403     END-PERFORM.                                                    CL**1
JF0403*                                                                    CL**1
JF0403     IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**1
JF0403         MOVE 'C500-GET-DEPT-FOR-SKU'                                CL*47
JF0403                               TO  PV-PARAGRAPH-NAME                 CL**1
JF0403         MOVE 'RETRIES EXCEEDED'                                     CL*17
JF0403                               TO  PV-DB2-OPER-ATTEMPTED             CL**1
JF0403         PERFORM Z900-SQL-ABEND                                      CL**1
JF0403     END-IF.                                                         CL**1
JF0403*                                                                    CL*20
00687 ******************************************************************   CL**1
00688 * Z100-READ-INPUT-FILE                                               CL**1
00689 ******************************************************************   CL**1
00690  Z100-READ-INPUT-FILE.                                               CL*30
00691 *                                                                    CL**1
00692      READ PO-INPUT-FILE INTO                                         CL**5
00693           PO002-EDI855-RECORD                                        CL**9
00694             AT END SET PS-EOF TO TRUE.                               CL**1
00695 *                                                                    CL*15
00696 ******************************************************************   CL*15
00697 * Z200-WRITE-OUTPUT-FILE                                             CL*15
00698 ******************************************************************   CL*15
00699  Z200-WRITE-OUTPUT-FILE.                                             CL*30
00700 *                                                                    CL*15
00701      WRITE PO-STATUS-RECORD FROM                                     CL*15
00702           PO002-EDI855-RECORD.                                       CL*15
00703 *                                                                    CL**1
00704 ******************************************************************   CL**1
00705 *  STANDARD DB2 ERROR ABEND ROUTINE                                  CL**1
00706 *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                CL**1
00707 ******************************************************************   CL**1
00708  Z900-SQL-ABEND.                                                     CL*30
00709                                                                      CL**1
00710      MOVE +4013                 TO ABEND-CODE.                       CL**1
00711      DISPLAY '**  ABEND     **'.                                     CL**1
00712      DISPLAY '**  PROGRAM   **  =  ETKUP032'.                        CL*23
00713      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.               CL**1
00714      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.           CL**1
00715                                                                      CL**1
00716 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE            CL**1
00717 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.           CL**1
00718                                                                      CL**1
00719      COPY DPPD004.                                                   CL**1
00720                                                                      CL**1
00721      CALL 'ILBOABN0'  USING ABEND-CODE.                              CL**1
