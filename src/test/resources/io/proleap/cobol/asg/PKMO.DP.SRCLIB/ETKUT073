       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         ETKUT073.                                            
       AUTHOR.             SUE PARI.                                            
       DATE-WRITTEN.       10/29/98.                                            
       DATE-COMPILED.                                                           
                                                                                
************************************************************************        
******* THIS PROGRAM WILL TAKE AN UNWRAPPED OUTBOUND FILE AND   ********        
******* PUT IT IN A FORMAT THAT CAN BE USED TO LOAD THE         ********        
******* DOCUMENT TRACKING TABLES                                ********        
************************************************************************        
99999 *                                                                         
99999 *CHANGE HISTORY:                                                          
99999 *    WR/PITS#    DATE      DESCRIPTION                                    
99999 *    WR99999   08/28/2000 ROD JANSSEN                                     
99999 *                         DOCUMENT TRACKING 864S AND MULTIPLE             
99999 *                         MIT SEGMENT PROBLEM....                         
99999 *                                                                         
************************************************************************        
0601JF* 06/19/2001 JON FIEDLER   0601JF                                         
0601JF* CHANGED TRDPRT_CURSOR FIELD NAME TO TRDPRT_CSR TO AVOID POSSIBLE        
0601JF* CONTENTION WITH ANOTHER EDI PROGRAM.                                    
************************************************************************        
JF0102* 01/10/2002 JON FIEDLER  WR22776                                         
JF0102* REMOVED CONTROL CARD AND LOGIC TO IMPLEMENT IPNET DOCUMENT              
JF0102* TRACKING ENHANCEMENT.                                                   
************************************************************************        
W22778* WR22778   06/21/2002 Colleen Wysocki                                    
W22778*                      Add logic for 816 document. Will track 816         
W22778*                      at BHT level (address and relationship)            
************************************************************************        
JF1102* 11-21-2002 JON FIEDLER  PITS# PM00509248                                
JF1102* INCREASED THE SIZE OF THE TPDKEY INTERNAL TABLE TO 13 IN ORDER          
JF1102* TO ACCOMADATE THE GROWTH IN VENDORS WHO SHARE THE SAME ID.              
************************************************************************        
JF0505* 05-27-2005 JON FIEDLER  PITS# P000766450                                
JF0505* INCREASED THE SIZE OF THE TPDKEY INTERNAL TABLE TO 25 IN ORDER          
JF0505* TO ACCOMADATE THE GROWTH IN VENDORS WHO SHARE THE SAME ID.              
************************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS INPUT-RECORD.                                         
                                                                                
       01  INPUT-RECORD.                                                        
           05  IN-RECORD-TYPE          PIC X(3).                                
           05  IN-SHORT-RECORD-TYPE REDEFINES IN-RECORD-TYPE.                   
               10  IN-SHORT-REC-TYPE   PIC X(2).                                
               10  FILLER              PIC X(1).                                
           05  FILLER                  PIC X(147).                              
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
JF0505     RECORD CONTAINS 200 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD.                                        
                                                                                
           COPY ETWS020.                                                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-ISA-RECORD.                                                       
           05  FILLER                  PIC X(3).                                
           05  WS-ELEMENT-SEPERATOR    PIC X(1).                                
           05  FILLER                  PIC X(47).                               
           05  WS-ISA-07               PIC X(2).                                
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-08               PIC X(15).                               
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-09               PIC 9(6).                                
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-10               PIC 9(4).                                
           05  FILLER                  PIC X(9).                                
           05  WS-ISA-13               PIC 9(9).                                
           05  FILLER                  PIC X(3).                                
           05  WS-ISA-15               PIC X(1).                                
           05  FILLER                  PIC X(2).                                
           05  WS-SEGMENT-TERMINATOR   PIC X(1).                                
           05  FILLER                  PIC X(44).                               
                                                                                
       01  WS-GS-RECORD.                                                        
           05  WS-GS-RECORD-TYPE       PIC X(2).                                
           05  WS-GS-01                PIC X(2).                                
           05  WS-GS-02                PIC X(15).                               
           05  WS-GS-03                PIC X(15).                               
           05  WS-GS-04                PIC X(6).                                
           05  WS-GS-05                PIC X(8).                                
           05  WS-GS-06                PIC 9(9).                                
           05  WS-GS-07                PIC X(1).                                
           05  WS-GS-08                PIC X(12).                               
                                                                                
       01  WS-ST-RECORD.                                                        
           05  WS-ST-RECORD-TYPE       PIC X(2).                                
           05  WS-ST-01                PIC X(3).                                
           05  WS-ST-02                PIC 9(9).                                
                                                                                
       01  WS-BEG-RECORD.                                                       
           05  WS-BEG-RECORD-TYPE      PIC X(3).                                
           05  WS-BEG-01               PIC X(2).                                
           05  WS-BEG-02               PIC X(2).                                
           05  WS-BEG-03               PIC X(30).                               
           05  WS-BEG-04               PIC X(30).                               
           05  WS-BEG-05               PIC X(8).                                
                                                                                
       01  WS-AK1-RECORD.                                                       
           05  WS-AK1-RECORD-TYPE      PIC X(3).                                
           05  WS-AK1-DETAIL.                                                   
               10  WS-AK1-01           PIC X(2).                                
               10  WS-AK1-02           PIC 9(9).                                
                                                                                
       01  WS-XQ-RECORD.                                                        
           05  WS-XQ-RECORD-TYPE       PIC X(2).                                
           05  WS-XQ-01                PIC X(2).                                
           05  WS-XQ-02                PIC X(8).                                
           05  WS-XQ-03                PIC X(8).                                
                                                                                
       01  WS-MIT-RECORD.                                                       
           05  WS-MIT-RECORD-TYPE      PIC X(3).                                
           05  WS-MIT-01               PIC X(30).                               
           05  WS-MIT-02               PIC X(80).                               
           05  WS-MIT-03               PIC X(3).                                
           05  WS-MIT-04               PIC X(3).                                
                                                                                
       01  WS-TRN-RECORD.                                                       
           05  WS-TRN-RECORD-TYPE      PIC X(3).                                
           05  WS-TRN-01               PIC X(2).                                
           05  WS-TRN-02               PIC X(30).                               
           05  WS-TRN-03               PIC X(10).                               
           05  WS-TRN-04               PIC X(30).                               
                                                                                
W22778 01  WS-BHT-RECORD.                                                       
W22778     05  WS-BHT-RECORD-TYPE      PIC X(03).                               
W22778     05  WS-BHT-01               PIC X(04).                               
W22778     05  WS-BHT-02               PIC X(02).                               
W22778     05  WS-BHT-03               PIC X(30).                               
W22778     05  WS-BHT-04               PIC X(08).                               
                                                                                
W22778 01  WS-BHT-DETAIL-MESSAGE.                                               
W22778     05  WS-BHT-DETAIL-TYPE      PIC X(03).                               
W22778     05  WS-BHT-DETAIL-DATE      PIC X(08).                               
W22778     05  FILLER                  PIC X(19).                               
                                                                                
       01  WS-NETWK-ID                 PIC X(1).                                
                                                                                
       01  WS-MESSAGE.                                                          
           05  WS-MESSAGE-TYPE         PIC X(2).                                
           05  WS-MESSAGE-NBR          PIC X(28).                               
                                                                                
       01  WS-CRE-TME.                                                          
           05  WS-CRE-TME-01           PIC 9(4).                                
           05  WS-CRE-TME-02           PIC 9(2).                                
                                                                                
       01  WS-CURRENT-DATE             PIC X(10).                               
       01  WS-CURR-YYMMDD              PIC 9(6).                                
       01  WS-CURR-YYMMDD-RDF REDEFINES WS-CURR-YYMMDD.                         
           05  WS-CURR-YY              PIC X(2).                                
           05  WS-CURR-MM              PIC X(2).                                
           05  WS-CURR-DD              PIC X(2).                                
                                                                                
       01  WS-CURRENT-TIME             PIC X(8).                                
       01  WS-CURR-HHMMSS              PIC 9(6).                                
       01  WS-CURR-HHMMSS-RDF REDEFINES WS-CURR-HHMMSS.                         
           05  WS-CURR-HH              PIC X(2).                                
           05  WS-CURR-MIN             PIC X(2).                                
           05  WS-CURR-SS              PIC X(2).                                
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FILE-SW          PIC X(1)  VALUE 'N'.                     
               88  END-OF-FILE                   VALUE 'Y'.                     
           05  END-OF-TRDPRT-SW        PIC X(1)  VALUE 'N'.                     
               88  END-OF-TRDPRT                 VALUE 'Y'.                     
99999      05  WS-FIRST-MIT-SW         PIC X(1)  VALUE 'N'.                     
99999          88  FIRST-MIT-FOUND               VALUE 'Y'.                     
                                                                                
       01  WS-TP-DKEY-TABLE.                                                    
JF0505     05  WS-TP-DKEY-INFO OCCURS 25 TIMES.                                 
                15 WS-TP-DKEY          PIC S9(9) COMP.                          
        01  WS-SUB                     PIC S9(4) COMP.                          
                                                                                
       01  PV-PARAGRAPH-NAME         PIC X(30).                                 
       01  PV-DB2-OPER-ATTEMPTED     PIC X(30).                                 
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
           COPY DPWS004.                                                        
                                                                                
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
0601JF         DECLARE TRDPRT_CSR CURSOR FOR                                    
                  SELECT TP_DKEY,                                               
                         NETWK_ID                                               
                  FROM   TTRDPRT                                                
                  WHERE  VEND_INCHG_ID_DESC = :WS-ISA-07                        
                    AND  VEND_INCHG_ID      = :WS-ISA-08                        
DUPREC              AND  INACTV_DTE         = '9999-09-09'                      
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE                                               
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           EXEC SQL                                                             
              SET :WS-CURRENT-DATE = CURRENT DATE                               
           END-EXEC.                                                            
           MOVE WS-CURRENT-DATE(3:2) TO WS-CURR-YY.                             
           MOVE WS-CURRENT-DATE(6:2) TO WS-CURR-MM.                             
           MOVE WS-CURRENT-DATE(9:2) TO WS-CURR-DD.                             
                                                                                
           EXEC SQL                                                             
              SET :WS-CURRENT-TIME = CURRENT TIME                               
           END-EXEC.                                                            
           MOVE WS-CURRENT-TIME(1:2) TO WS-CURR-HH.                             
           MOVE WS-CURRENT-TIME(4:2) TO WS-CURR-MIN.                            
           MOVE WS-CURRENT-TIME(7:2) TO WS-CURR-SS.                             
                                                                                
99999      MOVE 'N'  TO WS-FIRST-MIT-SW.                                        
99999                                                                           
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-PROCESS-INPUT-FILE                                      
              THRU 1000-PROCESS-INPUT-FILE-EXIT                                 
                 UNTIL END-OF-FILE.                                             
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-INPUT-FILE.                                                 
                                                                                
           IF IN-RECORD-TYPE = 'ISA'                                            
              PERFORM 1200-PROCESS-ISA                                          
                 THRU 1200-PROCESS-ISA-EXIT.                                    
                                                                                
           IF IN-SHORT-REC-TYPE = 'GS'                                          
              UNSTRING INPUT-RECORD DELIMITED BY                                
                 WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                  
                    INTO WS-GS-RECORD-TYPE, WS-GS-01, WS-GS-02,                 
                         WS-GS-03, WS-GS-04, WS-GS-05,                          
                         WS-GS-06, WS-GS-07, WS-GS-08.                          
                                                                                
           IF IN-SHORT-REC-TYPE = 'ST'                                          
              UNSTRING INPUT-RECORD DELIMITED BY                                
                 WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                  
                    INTO WS-ST-RECORD-TYPE, WS-ST-01, WS-ST-02                  
              MOVE SPACES TO ET020-TSET-DETAIL-RECORD                           
              SET ET020-DETAIL TO TRUE                                          
              MOVE WS-GS-06  TO ET020-FG-CTL-NBR                                
              MOVE WS-ST-02  TO ET020-TSET-CTL-NBR                              
              MOVE WS-ST-01  TO ET020-TRAN-SET-CDE.                             
                                                                                
           IF IN-RECORD-TYPE = 'BEG'                                            
              UNSTRING INPUT-RECORD DELIMITED BY                                
                 WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                  
                    INTO WS-BEG-RECORD-TYPE, WS-BEG-01, WS-BEG-02,              
                         WS-BEG-03, WS-BEG-04, WS-BEG-05                        
              MOVE WS-BEG-03 TO ET020-TRAN-SET-DTL                              
              WRITE ET020-TSET-DETAIL-RECORD.                                   
                                                                                
           IF IN-RECORD-TYPE = 'AK1'                                            
              UNSTRING INPUT-RECORD DELIMITED BY                                
                 WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                  
                    INTO WS-AK1-RECORD-TYPE, WS-AK1-01, WS-AK1-02               
              MOVE WS-AK1-DETAIL TO ET020-TRAN-SET-DTL                          
              WRITE ET020-TSET-DETAIL-RECORD.                                   
                                                                                
           IF IN-SHORT-REC-TYPE = 'XQ'                                          
              UNSTRING INPUT-RECORD DELIMITED BY                                
                 WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                  
                    INTO WS-XQ-RECORD-TYPE, WS-XQ-01, WS-XQ-02,                 
                         WS-XQ-03                                               
              MOVE WS-XQ-02 TO ET020-TRAN-SET-DTL                               
              WRITE ET020-TSET-DETAIL-RECORD.                                   
                                                                                
           IF IN-RECORD-TYPE = 'MIT'                                            
99999        IF (NOT FIRST-MIT-FOUND)                                           
              UNSTRING INPUT-RECORD DELIMITED BY                                
                 WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                  
                    INTO WS-MIT-RECORD-TYPE, WS-MIT-01, WS-MIT-02,              
                         WS-MIT-03, WS-MIT-04                                   
              IF WS-MIT-02 = 'GS Control Number - Missing FA'                   
                 MOVE 'FA' TO WS-MESSAGE-TYPE                                   
                 MOVE WS-MIT-01 TO WS-MESSAGE-NBR                               
                 MOVE WS-MESSAGE TO ET020-TRAN-SET-DTL                          
              ELSE                                                              
                 MOVE 'TX' TO WS-MESSAGE-TYPE                                   
                 MOVE WS-MIT-01 TO WS-MESSAGE-NBR                               
                 MOVE WS-MESSAGE TO ET020-TRAN-SET-DTL                          
              END-IF                                                            
              WRITE ET020-TSET-DETAIL-RECORD                                    
99999         MOVE 'Y'  TO WS-FIRST-MIT-SW                                      
99999        END-IF                                                             
99999      END-IF.                                                              
                                                                                
           IF IN-RECORD-TYPE = 'TRN'                                            
              UNSTRING INPUT-RECORD DELIMITED BY                                
                 WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                  
                    INTO WS-TRN-RECORD-TYPE, WS-TRN-01, WS-TRN-02,              
                         WS-TRN-03, WS-TRN-04                                   
              MOVE WS-TRN-02 TO ET020-TRAN-SET-DTL                              
              WRITE ET020-TSET-DETAIL-RECORD.                                   
                                                                                
W22778     IF IN-RECORD-TYPE = 'BHT'                                            
W22778        UNSTRING INPUT-RECORD DELIMITED BY                                
W22778           WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                  
W22778              INTO WS-BHT-RECORD-TYPE, WS-BHT-01, WS-BHT-02,              
W22778                   WS-BHT-03, WS-BHT-04                                   
W22778        MOVE SPACES TO WS-BHT-DETAIL-MESSAGE                              
W22778        MOVE WS-BHT-04 TO WS-BHT-DETAIL-DATE                              
W22778        IF WS-BHT-01 = '0055' OR WS-BHT-01 = '0057'                       
W22778           MOVE 'REL' TO WS-BHT-DETAIL-TYPE                               
W22778        ELSE                                                              
W22778           MOVE 'ADD' TO WS-BHT-DETAIL-TYPE                               
W22778        END-IF                                                            
W22778        MOVE WS-BHT-DETAIL-MESSAGE TO ET020-TRAN-SET-DTL                  
W22778        WRITE ET020-TSET-DETAIL-RECORD.                                   
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-INPUT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
                                                                                
       1100-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-FILE TO TRUE.                                       
                                                                                
       1100-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       1200-PROCESS-ISA.                                                        
                                                                                
           MOVE INPUT-RECORD TO WS-ISA-RECORD.                                  
                                                                                
           MOVE SPACES TO ET020-TSET-DETAIL-RECORD.                             
                                                                                
           SET ET020-HEADER TO TRUE.                                            
                                                                                
           EXEC SQL                                                             
0601JF        OPEN TRDPRT_CSR                                                   
           END-EXEC.                                                            
           IF SQLCODE NOT = +000                                                
              MOVE '1200-PROCESS-ISA' TO PV-PARAGRAPH-NAME                      
              MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED                       
              PERFORM 1400-SQL-ERROR.                                           
                                                                                
           INITIALIZE WS-TP-DKEY-TABLE.                                         
           MOVE ZERO TO WS-SUB.                                                 
           MOVE 'N' TO END-OF-TRDPRT-SW.                                        
           PERFORM 1300-GET-TP-DKEY                                             
              THRU 1300-GET-TP-DKEY-EXIT                                        
                 UNTIL END-OF-TRDPRT.                                           
                                                                                
           EXEC SQL                                                             
0601JF        CLOSE TRDPRT_CSR                                                  
           END-EXEC.                                                            
           IF SQLCODE NOT = +000                                                
              MOVE '1200-PROCESS-ISA' TO PV-PARAGRAPH-NAME                      
              MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED                      
              PERFORM 1400-SQL-ERROR.                                           
                                                                                
JF0505     PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 25                 
              MOVE WS-TP-DKEY (WS-SUB) TO ET020-TP-DKEY (WS-SUB)                
           END-PERFORM.                                                         
                                                                                
           MOVE WS-ISA-07   TO ET020-VEND-INCHG-ID-QUAL.                        
           MOVE WS-ISA-08   TO ET020-VEND-INCHG-ID.                             
           MOVE WS-NETWK-ID TO ET020-NETWK-ID.                                  
           MOVE 'S'         TO ET020-DIR-IND.                                   
           MOVE WS-ISA-09   TO ET020-CRE-DTE.                                   
                                                                                
           MOVE WS-ISA-10   TO WS-CRE-TME-01.                                   
           MOVE ZERO        TO WS-CRE-TME-02.                                   
           MOVE WS-CRE-TME  TO ET020-CRE-TME.                                   
                                                                                
           IF WS-NETWK-ID = '3'                                                 
              MOVE WS-CURR-YYMMDD TO ET020-RCPT-DTE                             
              MOVE WS-CURR-HHMMSS TO ET020-RCPT-TME                             
              MOVE WS-CURR-YYMMDD TO ET020-PKUP-DTE                             
              MOVE WS-CURR-HHMMSS TO ET020-PKUP-TME                             
           ELSE                                                                 
              MOVE ZERO TO ET020-RCPT-DTE                                       
              MOVE ZERO TO ET020-RCPT-TME                                       
              MOVE ZERO TO ET020-PKUP-DTE                                       
              MOVE ZERO TO ET020-PKUP-TME.                                      
                                                                                
           MOVE WS-ISA-13 TO ET020-INCHG-CTL-NBR.                               
           MOVE 'N'       TO ET020-ERROR-IND.                                   
           MOVE WS-ISA-15 TO ET020-PROD-TEST-IND.                               
                                                                                
           WRITE ET020-TSET-DETAIL-RECORD.                                      
                                                                                
99999      MOVE 'N'  TO WS-FIRST-MIT-SW.                                        
99999                                                                           
       1200-PROCESS-ISA-EXIT.                                                   
           EXIT.                                                                
                                                                                
                                                                                
       1300-GET-TP-DKEY.                                                        
                                                                                
           EXEC SQL                                                             
0601JF         FETCH TRDPRT_CSR                                                 
               INTO :TRDPRT-TP-DKEY,                                            
                    :TRDPRT-NETWK-ID                                            
           END-EXEC.                                                            
                                                                                
           ADD 1 TO WS-SUB.                                                     
                                                                                
JF0505     IF SQLCODE NOT = +000 OR WS-SUB > 25                                 
              MOVE 'Y' TO END-OF-TRDPRT-SW                                      
              GO TO 1300-GET-TP-DKEY-EXIT.                                      
                                                                                
           MOVE TRDPRT-TP-DKEY  TO WS-TP-DKEY (WS-SUB).                         
           MOVE TRDPRT-NETWK-ID TO WS-NETWK-ID.                                 
                                                                                
       1300-GET-TP-DKEY-EXIT.                                                   
           EXIT.                                                                
                                                                                
                                                                                
       1400-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
