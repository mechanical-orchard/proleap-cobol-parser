       IDENTIFICATION DIVISION.                                         00010002
       PROGRAM-ID.    ICKUT013.                                         00020003
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030002
       DATE-WRITTEN.  06-24-2008.                                       00040002
       DATE-COMPILED.                                                   00050002
                                                                        00060002
      ******************************************************************00070002
      *PURPOSE:   THE PROGRAM IS DESIGNED TO FORMAT THE ICT_LOC_CNT_STAT00080002
      *           UNLOAD FROM THE USA INVENTORY COUNTS SYSTEM TO EDW    00090002
      *           LOAD STANDARDS.                                       00100002
      *                (SEE THE FLOW SECTION FOR MORE DETAILS)          00110002
      *                                                                 00120002
      *CHANGE HISTORY:                                                  00130002
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00140002
      * ------- ---------- ----------- -------------------------------- 00150002
      * 38888   12-14-2010 J HELING    CREATION.....                    00160004
      ******************************************************************00170002
                                                                        00180002
       ENVIRONMENT DIVISION.                                            00190002
       CONFIGURATION SECTION.                                           00200002
       SOURCE-COMPUTER. IBM-3090.                                       00210002
       OBJECT-COMPUTER. IBM-3090.                                       00220002
       INPUT-OUTPUT SECTION.                                            00230002
       FILE-CONTROL.                                                    00240002
                                                                        00250002
           SELECT LOC-CNT-STAT-INPUT                                    00260002
               ASSIGN TO INP01.                                         00270002
                                                                        00280002
           SELECT LOC-CNT-STAT-OUTPUT                                   00290002
               ASSIGN TO OUT01.                                         00300002
                                                                        00310002
       DATA DIVISION.                                                   00320002
                                                                        00330002
       FILE SECTION.                                                    00340002
                                                                        00350002
       FD  LOC-CNT-STAT-INPUT                                           00360002
           RECORDING MODE IS F                                          00370002
           LABEL RECORDS ARE STANDARD.                                  00380002
       01  LOC-CNT-STAT-INPUT-RECORD            PIC X(099).             00390002
                                                                        00400002
       FD  LOC-CNT-STAT-OUTPUT                                          00410002
           RECORDING MODE IS F                                          00420002
           LABEL RECORDS ARE STANDARD.                                  00430002
       01  LOC-CNT-STAT-OUTPUT-RECORD           PIC X(117).             00440002
                                                                        00450002
      ******************************************************************00460002
       WORKING-STORAGE SECTION.                                         00470002
      ******************************************************************00480002
       01  PC-PROGRAM-CONSTANTS.                                        00490002
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'ICKUT013'.00500002
                                                                        00510002
       01  PV-PROGRAM-VARIABLES.                                        00520002
           05  PV-INPUT-RECS                  PIC 9(09)   VALUE ZERO.   00530002
           05  PV-OUTPUT-RECS                 PIC 9(09)   VALUE ZERO.   00540002
           05  PV-PARA-NAME                   PIC X(30)   VALUE ZERO.   00550002
                                                                        00560002
       01  PS-PROGRAM-SWITCHES.                                         00570002
           05  PS-LOC-CNT-STAT-INPUT-SW        PIC X(01)   VALUE 'N'.   00580002
               88  PS-LOC-CNT-STAT-INPUT-EOF               VALUE 'Y'.   00590002
                                                                        00600002
       01  ABEND-CODE                          PIC S9(04)  VALUE +0     00610002
                                                           COMP SYNC.   00620002
                                                                        00630002
      ******************************************************************00640005
           COPY ICRD151.                                                00650002
                                                                        00660002
      ******************************************************************00670005
           COPY ICRD071.                                                00680002
                                                                        00690002
      ******************************************************************00700002
      * WORKING-STORAGE FOR CALENDAR ROUTINE                            00710002
      ******************************************************************00720002
           COPY DPWS500.                                                00730002
                                                                        00740002
      ******************************************************************00790002
      ******************************************************************00800002
       PROCEDURE DIVISION.                                              00810002
                                                                        00820002
       0000-MAINLINE.                                                   00830002
                                                                        00840002
           PERFORM 1000-INITIALIZE.                                     00850002
                                                                        00860002
           PERFORM 3000-MAIN-PROCESS                                    00870002
               UNTIL PS-LOC-CNT-STAT-INPUT-EOF.                         00880002
                                                                        00890002
           PERFORM 9000-END-OF-JOB-ROUTINE.                             00900002
                                                                        00910002
           STOP RUN.                                                    00920002
                                                                        00930002
      ******************************************************************00940002
      ******************************************************************00950002
                                                                        00960002
       1000-INITIALIZE.                                                 00970002
           DISPLAY '   '.                                               00980002
           DISPLAY '****************************************'.          00990002
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                      01000002
                                                                        01010002
           OPEN INPUT  LOC-CNT-STAT-INPUT                               01020002
               OUTPUT  LOC-CNT-STAT-OUTPUT.                             01030002
                                                                        01040002
           PERFORM 6100-READ-LOC-CNT-STAT-INPUT.                        01050002
                                                                        01060002
      ***************************************************************** 01070002
       3000-MAIN-PROCESS.                                               01080002
      ******************************************************************01090002
                                                                        01100002
           INITIALIZE IC071-OUTPUT-RECORD.                              01110002
                                                                        01120002
           MOVE ICRD151-CNT-STAT-CDE     TO IC071-CNT-STAT-CDE.         01130002
           MOVE ICRD151-LOC-NBR          TO IC071-LOC-NBR.              01140002
           MOVE ICRD151-CNT-STUP-ID      TO IC071-CNT-STUP-ID.          01150002
           MOVE ICRD151-CNT-SEQ-NBR      TO IC071-CNT-SEQ-NBR.          01160002
           MOVE ICRD151-LAST-UPD-TMST    TO IC071-LAST-UPD-TMST.        01170002
           MOVE ICRD151-LAST-UPD-USR-ID  TO IC071-LAST-UPD-USR-ID.      01180002
           MOVE ICRD151-LAST-ACTN-CDE    TO IC071-LAST-ACTN-CDE.        01190002
                                                                        01200002
           MOVE ICRD151-USR-CMNT-TXT-LEN TO IC071-USR-CMNT-TXT-LEN.     01210002
                                                                        01220002
           IF USR-CMNT-TXT-IS-NULL OR                                   01230002
              ICRD151-USR-CMNT-TXT-LEN IS EQUAL TO ZEROS                01240002
               MOVE SPACES               TO IC071-USR-CMNT-TXT          01250002
           ELSE                                                         01260002
               MOVE ICRD151-USR-CMNT-TXT (1:ICRD151-USR-CMNT-TXT-LEN)   01270002
                                         TO IC071-USR-CMNT-TXT          01280002
           END-IF.                                                      01290002
                                                                        01300002
           PERFORM 5000-WRITE-LOC-CNT-STAT-OUTPUT                       01310002
                                                                        01320002
           PERFORM 6100-READ-LOC-CNT-STAT-INPUT.                        01330002
                                                                        01340002
      ******************************************************************01350002
       5000-WRITE-LOC-CNT-STAT-OUTPUT.                                  01360002
      ******************************************************************01370002
                                                                        01380002
           WRITE LOC-CNT-STAT-OUTPUT-RECORD                             01390002
                 FROM IC071-OUTPUT-RECORD.                              01400002
                                                                        01410002
           ADD +1 TO PV-OUTPUT-RECS.                                    01420002
                                                                        01430002
      ******************************************************************01440002
       6100-READ-LOC-CNT-STAT-INPUT.                                    01450002
      ******************************************************************01460002
           READ LOC-CNT-STAT-INPUT                                      01470002
               INTO ICRD051-IC-COUNTS-FILE                              01480002
                 AT END SET PS-LOC-CNT-STAT-INPUT-EOF TO TRUE           01490002
                 NOT AT END ADD +1 TO PV-INPUT-RECS                     01500002
           END-READ.                                                    01510002
                                                                        01520002
      ******************************************************************01530002
       9000-END-OF-JOB-ROUTINE.                                         01540002
      ******************************************************************01550002
                                                                        01560002
           CLOSE LOC-CNT-STAT-INPUT                                     01570002
                 LOC-CNT-STAT-OUTPUT.                                   01580002
                                                                        01590002
           DISPLAY '****************************************'.          01600002
           DISPLAY '*** SUMMARY FOR '             PC-PROGRAM-NAME.      01610002
           DISPLAY '*** INPUT RECORDS READ      = ' PV-INPUT-RECS.      01620002
           DISPLAY '*** OUTPUT RECORDS WRITTEN  = ' PV-OUTPUT-RECS.     01630002
           DISPLAY '****************************************'.          01640002
                                                                        01650002
      ***************************************************************** 01660002
      ***************************************************************** 01670002
