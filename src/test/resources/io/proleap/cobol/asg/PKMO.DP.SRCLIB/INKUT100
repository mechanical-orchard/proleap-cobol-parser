       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT100.                                       
       AUTHOR.                  SCOTT JACKSON.                                  
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            03/20/2009.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *              INVENTORY LEDGER DATA INTEGRITY                            
      *                                                                         
      * THIS PROGRAM EXTRACTS INVENTORY LEDGER DATA FROM THE VA AND IN          
      * SYSTEMS AND COMPARES THE EXTRACTS TO ENSURE FINANCIALS BALANCE.         
      *                                                                         
      * THIS PROGRAM WILL READ IN A FISCAL DATE FROM CONTROL CARD INPUT         
      * IF PROVIDED AND WILL PERFORM DATA INTEGRITY FOR SPECIFIC MONTH.         
      * IF NO INPUT DATE IS PROVIDED, THE PROCESS WILL USES TINCNTL             
      * AND EXTRACT ALL INFORMATION THAT EXISTS FOR ANY 'ACTIVE' CYCLE.         
      *                                                                         
      * PROCESS BUILDS UNLOAD CONTROL CARD PARAMETERS THAT WILL BE FED          
      * TO THE SUBSEQUENT DATA EXTRACTION PROCESS                               
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-CARD-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT EXTRACT-MONTH-NBR                                             
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT EXTRACT-MONTH-DATE                                            
               ASSIGN TO OUT02.                                                 
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CONTROL-CARD-RECORD              PIC X(80).                          
                                                                                
       FD  EXTRACT-MONTH-NBR                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  EXTRACT-MONTH-RECORD       PIC X(80).                                
                                                                                
       FD  EXTRACT-MONTH-DATE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  EXTRACT-DATE-RECORD       PIC X(80).                                 
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
                                                                                
           COPY DPWS004.                                                        
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  FILLER                       PIC X(01).                          
           05  CC-EXTRACT-DTE               PIC X(10).                          
           05  FILLER                       PIC X(69).                          
       01  CC-CONTROL-MN.                                                       
           05  FILLER                   PIC X(01)    VALUE '('.                 
           05  FILLER                   PIC X(01)    VALUE "'".                 
           05  CC-CONTROL-MONTH         PIC X(02)    VALUE SPACE.               
           05  FILLER                   PIC X(01)    VALUE "'".                 
           05  FILLER                   PIC X(01)    VALUE ')'.                 
           05  FILLER                   PIC X(66)    VALUE SPACE.               
       01  CC-CONTROL-DTE.                                                      
           05  FILLER                   PIC X(01)    VALUE '('.                 
           05  FILLER                   PIC X(01)    VALUE "'".                 
           05  CC-CONTROL-DATE          PIC X(10)    VALUE SPACE.               
           05  FILLER                   PIC X(01)    VALUE "'".                 
           05  FILLER                   PIC X(01)    VALUE ')'.                 
           05  FILLER                   PIC X(66)    VALUE SPACE.               
                                                                                
      *01  CC-OUTPUT-MONTH-NBRS.                                                
      *    05  CC-OUTPUT-MN-MORE            PIC X(01)   VALUE SPACE.            
      *    05  FILLER                       PIC X(01)    VALUE "'".             
      *    05  CC-OUTPUT-MN-NBR             PIC X(02).                          
      *    05  FILLER                       PIC X(01)    VALUE "'".             
      *    05  FILLER                       PIC X(75).                          
      *                                                                         
      *01  CC-OUTPUT-MONTH-DATES.                                               
      *    05  CC-OUTPUT-DTE-MORE           PIC X(01)   VALUE SPACE.            
      *    05  FILLER                       PIC X(01)    VALUE "'".             
      *    05  CC-OUTPUT-MN-END-DTE         PIC X(10).                          
      *    05  FILLER                       PIC X(01)    VALUE "'".             
      *    05  FILLER                       PIC X(67).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-COMMA                     PIC X(01)   VALUE ','.              
           05  PC-RIGHT                     PIC X(01)   VALUE ')'.              
           05  PC-END                       PIC X(01)   VALUE ';'.              
           05  PC-DEFAULT-MN                PIC X(02)   VALUE '12'.             
           05  PC-DEFAULT-MN-DTE       PIC X(10)   VALUE '9999-12-31'.          
       01  PC-FINAL1.                                                           
           05  PC-END-SELECT1               PIC X(01)   VALUE ')'.              
           05  FILLER                       PIC X(79)  VALUE SPACES.            
       01  PC-FINAL2.                                                           
           05  PC-END-SELECT2               PIC X(02)   VALUE ');'.             
           05  FILLER                       PIC X(78)   VALUE SPACES.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-CONTROL-CARDS-SW   PIC X      VALUE 'N'.               
               88  PS-END-OF-CONTROL-CARDS             VALUE 'Y'.               
           05  PS-DATE-FOUND-SW             PIC X      VALUE 'N'.               
               88  PS-DATE-FOUND                       VALUE 'Y'.               
               88  PS-DATE-NOT-FOUND                   VALUE 'N'.               
           05  PS-FIRST-SW                  PIC X      VALUE 'N'.               
               88  PS-FIRST-DATE                       VALUE 'Y'.               
           05  PS-DATE-CSR-SW               PIC X      VALUE 'N'.               
               88  PS-END-OF-DATE-CSR                  VALUE 'Y'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
           05  PV-PARAGRAPH-NAME            PIC X(30).                          
                                                                                
           05  PV-CONTROL-MN.                                                   
               10  CC-OUTPUT-MN-MORE        PIC X(01)    VALUE '('.             
               10  FILLER                   PIC X(01)    VALUE "'".             
               10  PV-CONTROL-MONTH         PIC X(02)    VALUE SPACE.           
               10  FILLER                   PIC X(01)    VALUE "'".             
           05  PV-CONTROL-DTE.                                                  
               10  CC-OUTPUT-DTE-MORE       PIC X(01)    VALUE '('.             
               10  FILLER                   PIC X(01)    VALUE "'".             
               10  PV-CONTROL-DATE          PIC X(10)    VALUE SPACE.           
               10  FILLER                   PIC X(01)    VALUE "'".             
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP                     
                                                       VALUE ZERO.              
      ****************************************************************          
      * MERCHANDISE LEDGER CONTROL TABLE                                        
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
              INCLUDE TINCNTL                                                   
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * DATE ATTRIBUTE TABLE                                                    
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                                
      ****************************************************************          
      *  INVENTORY MONTH DATE CURSOR                                            
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
              DECLARE DATE-CSR CURSOR FOR                                       
                  SELECT                                                        
                      OPN_FSCL_MN_NBR                                           
                     ,OPN_FSCL_MN_DTE                                           
                  FROM                                                          
                      TINCNTL                                                   
                  WHERE ACTV_IND = 'Y'                                          
              FOR FETCH ONLY                                                    
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           OPEN INPUT  CONTROL-CARD-FILE.                                       
           OPEN OUTPUT EXTRACT-MONTH-NBR                                        
                OUTPUT EXTRACT-MONTH-DATE.                                      
                                                                                
           PERFORM 1500-DETERMINE-DI-MONTHS.                                    
                                                                                
           CLOSE CONTROL-CARD-FILE                                              
                 EXTRACT-MONTH-NBR                                              
                 EXTRACT-MONTH-DATE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1500-DETERMINE-DI-MONTHS.                                                
                                                                                
           PERFORM 7000-READ-CONTROL-CARD-FILE.                                 
                                                                                
           DISPLAY 'SELECTION CRITERIA FOR INV LEDGER EXTRACT '                 
           DISPLAY 'FSCL_MN_NBR / FSCL_MN_END_DTE '                             
                                                                                
           IF PS-END-OF-CONTROL-CARDS                                           
           OR CC-EXTRACT-DTE = SPACE                                            
               PERFORM 3000-FIND-ACTIVE-INV-CYCLE                               
           ELSE                                                                 
               DISPLAY '** USING INPUT CONTROL CARD: ' CC-CONTROL-CARD          
               PERFORM 2500-USE-CONTROL-CARD-DTE                                
           END-IF.                                                              
                                                                                
                                                                                
       2500-USE-CONTROL-CARD-DTE.                                               
                                                                                
           MOVE CC-EXTRACT-DTE TO DTATTR-KY-DTE.                                
                                                                                
           PERFORM 8000-GET-DATE-ATTRIBUTES.                                    
                                                                                
           IF  PS-DATE-FOUND                                                    
               MOVE DTATTR-FSCL-MN-NBR     TO CC-CONTROL-MONTH                  
               MOVE DTATTR-FSCL-MN-END-DTE TO CC-CONTROL-DATE                   
               PERFORM 7300-WRITE-OUTPUTS                                       
           ELSE                                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM INKUT100 **'                                 
               DISPLAY '** PARAGRAPH 2500-USE-CONTROL-CARD-DTE'                 
               DISPLAY '** CONTROL CARD DATE NOT VALID'                         
               MOVE +4003 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
                                                                                
       3000-FIND-ACTIVE-INV-CYCLE.                                              
                                                                                
           PERFORM 4000-OPEN-DATE-CSR.                                          
           PERFORM 4100-FETCH-DATE-CSR.                                         
                                                                                
           IF PS-END-OF-DATE-CSR                                                
              PERFORM 4200-WRITE-DEFAULT-DATE                                   
           ELSE                                                                 
              SET PS-FIRST-DATE TO TRUE                                         
                                                                                
              PERFORM 4300-BUILD-RECORDS                                        
                  UNTIL PS-END-OF-DATE-CSR                                      
           END-IF.                                                              
                                                                                
           PERFORM 4400-FINALIZE.                                               
           PERFORM 4500-CLOSE-DATE-CSR.                                         
                                                                                
                                                                                
       4000-OPEN-DATE-CSR.                                                      
                                                                                
           EXEC SQL                                                             
                OPEN DATE-CSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '4000-OPEN-DATE-CSR'                                    
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'OPEN DATE-CSR'   TO PV-DB2-OPER-ATTEMPTED              
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4100-FETCH-DATE-CSR.                                                     
                                                                                
           EXEC SQL                                                             
               FETCH                                                            
                   DATE-CSR                                                     
               INTO                                                             
                  :INCNTL-OPN-FSCL-MN-NBR                                       
                 ,:INCNTL-OPN-FSCL-MN-DTE                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-DATE-CSR TO TRUE                               
               WHEN OTHER                                                       
                   MOVE '4100-FETCH-DATE-CSR' TO PV-PARAGRAPH-NAME              
                   MOVE 'FETCH DATE-CSR'      TO PV-DB2-OPER-ATTEMPTED          
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4200-WRITE-DEFAULT-DATE.                                                 
                                                                                
           MOVE  PC-DEFAULT-MN             TO    PV-CONTROL-MONTH.              
           MOVE  PC-DEFAULT-MN-DTE         TO    PV-CONTROL-DATE.               
                                                                                
           PERFORM 7100-WRITE-OUTPUTS.                                          
                                                                                
                                                                                
       4300-BUILD-RECORDS.                                                      
                                                                                
           IF PS-FIRST-DATE                                                     
      *        MOVE SPACE TO    CC-OUTPUT-MN-MORE                               
      *                         CC-OUTPUT-DTE-MORE                              
               MOVE 'N'   TO    PS-FIRST-SW                                     
           ELSE                                                                 
               MOVE PC-COMMA TO CC-OUTPUT-MN-MORE                               
                                CC-OUTPUT-DTE-MORE                              
           END-IF.                                                              
                                                                                
           MOVE INCNTL-OPN-FSCL-MN-NBR TO PV-CONTROL-MONTH.                     
           MOVE INCNTL-OPN-FSCL-MN-DTE TO PV-CONTROL-DATE.                      
                                                                                
           PERFORM 7100-WRITE-OUTPUTS                                           
                                                                                
           PERFORM 4100-FETCH-DATE-CSR.                                         
                                                                                
       4400-FINALIZE.                                                           
                                                                                
      *    MOVE SPACES   TO CC-OUTPUT-MONTH-NBRS                                
      *                     CC-OUTPUT-MONTH-DATES.                              
      *                                                                         
      *    MOVE PC-RIGHT TO CC-OUTPUT-MN-MORE                                   
      *                     CC-OUTPUT-DTE-MORE.                                 
      *                                                                         
      *    PERFORM 7100-WRITE-OUTPUTS.                                          
      *                                                                         
      *     MOVE PC-END   TO CC-OUTPUT-DTE-MORE.                                
                                                                                
           PERFORM 7200-WRITE-LAST.                                             
                                                                                
                                                                                
       4500-CLOSE-DATE-CSR.                                                     
                                                                                
           EXEC SQL                                                             
               CLOSE DATE-CSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '4200-CLOSE-DATE-CSR' TO PV-PARAGRAPH-NAME              
                   MOVE 'CLOSE DATE-CSR'      TO PV-DB2-OPER-ATTEMPTED          
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       7000-READ-CONTROL-CARD-FILE.                                             
                                                                                
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
               AT END SET PS-END-OF-CONTROL-CARDS TO TRUE.                      
                                                                                
                                                                                
      *7100-WRITE-OUTPUTS.                                                      
      * TWO OUTPUTS ARE WRITTEN (AS UNLOAD PARAMETER CONTOL CARDS)              
      *  1) EXTRACT-MONTH-RECORD CONTAINS ALL ACTIVE INV MONTH NBRS             
      *  2) EXTRACT-DATE-RECORD  CONTAINS ALL ACTIVE INV_MN_END_DTES            
      *                                                                         
       7100-WRITE-OUTPUTS.                                                      
                                                                                
           WRITE EXTRACT-MONTH-RECORD                                           
               FROM PV-CONTROL-MN.                                              
                                                                                
           WRITE EXTRACT-DATE-RECORD                                            
               FROM PV-CONTROL-DTE.                                             
                                                                                
           DISPLAY  PV-CONTROL-MN, ' / ',  PV-CONTROL-DTE.                      
                                                                                
       7200-WRITE-LAST.                                                         
                                                                                
           WRITE EXTRACT-MONTH-RECORD                                           
               FROM PC-FINAL1.                                                  
                                                                                
           WRITE EXTRACT-DATE-RECORD                                            
               FROM PC-FINAL2.                                                  
                                                                                
       7300-WRITE-OUTPUTS.                                                      
                                                                                
           WRITE EXTRACT-MONTH-RECORD                                           
               FROM CC-CONTROL-MN.                                              
                                                                                
           WRITE EXTRACT-DATE-RECORD                                            
               FROM CC-CONTROL-DTE.                                             
                                                                                
           DISPLAY  CC-CONTROL-MN, ' / ',  CC-CONTROL-DTE.                      
                                                                                
                                                                                
       8000-GET-DATE-ATTRIBUTES.                                                
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                    FSCL_MN_NBR                                                 
                   ,FSCL_MN_END_DTE                                             
                INTO                                                            
                    :DTATTR-FSCL-MN-NBR                                         
                   ,:DTATTR-FSCL-MN-END-DTE                                     
                FROM                                                            
                    TDTATTR                                                     
                WHERE                                                           
                    KY_DTE = :DTATTR-KY-DTE                                     
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-DATE-FOUND     TO TRUE                                
               WHEN SQLCODE = +100                                              
                   SET PS-DATE-NOT-FOUND TO TRUE                                
               WHEN OTHER                                                       
                   DISPLAY '** DTATTR-KY-DTE: ' DTATTR-KY-DTE                   
                   MOVE    '8050-GET-DATE-ATTRIBUTES'                           
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT ROW FROM TDTATTR'                            
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       9998-SQL-ABEND.                                                          
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY ' '.                                                         
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = INKUT100'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING MOPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
