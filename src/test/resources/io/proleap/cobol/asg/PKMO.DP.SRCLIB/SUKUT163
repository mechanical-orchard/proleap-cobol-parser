      ******************************************************************00010006
       IDENTIFICATION DIVISION.                                         00020006
      ******************************************************************00030006
       PROGRAM-ID.    SUKUT163.                                         00040006
       AUTHOR.        JIM HUNTER                                        00050006
       INSTALLATION.  KOHLS.                                            00060006
       DATE-WRITTEN   MARCH, 2017.                                      00070006
       DATE-COMPILED.                                                   00080006
      ******************************************************************00090006
      *  THIS PROGRAM WILL READ THE INPUT FILE CREATED BY SUKUT161      00100006
      *  AFTER IT HAS BEEN SORTED IN TRIP ID SEQUENCE AND THE DUPLICATES00110006
      *  HAVE BEEN REMOVED.                                             00120006
      *  IT WILL LOOK FOR CARTON DATA ON THE SUT_OTBND_CART TABLE THAT  00130006
      *  IS NOT ON THE SUT_OTBND_STR_SCN TABLE.  DETAILS WILL BE        00140006
      *  WRITTEN TO A MISSING CARTON OUTPUT FILE.                       00150006
      *  THIS PROGRAM WAS COPIED FROM SUKUT162 AND THEN CONVERTED TO    00160006
      *  LOOK FOR MISSING CARTONS.                                      00170006
      ******************************************************************00180006
      ******************************************************************00190006
      *  CHANGE LOG                                                     00200006
      *  DATE       CHANGED BY    DESC                                  00210006
      *  ---------- ------------- --------------------------------------00220006
      *  03-09-2017 JIM HUNTER    INITIAL PROGRAM FOR STORE RECEIVING   00230006
      *                           PROJECT.                              00240006
      ******************************************************************00250006
       ENVIRONMENT DIVISION.                                            00260006
       CONFIGURATION SECTION.                                           00270006
                                                                        00280006
       SOURCE-COMPUTER.        IBM-3090.                                00290006
       OBJECT-COMPUTER.        IBM-3090.                                00300006
                                                                        00310006
       INPUT-OUTPUT SECTION.                                            00320006
       FILE-CONTROL.                                                    00330006
                                                                        00340006
           SELECT TRIP-ID-FILE                                          00350006
               ASSIGN TO IN01.                                          00360006
                                                                        00370006
           SELECT MISSING-CARTON-DETAIL-FILE                            00380006
               ASSIGN TO OUT01.                                         00390006
                                                                        00400006
      ******************************************************************00410006
       DATA DIVISION.                                                   00420006
       FILE SECTION.                                                    00430006
                                                                        00440006
       FD  TRIP-ID-FILE                                                 00450006
           RECORDING MODE IS F                                          00460006
           LABEL RECORDS ARE STANDARD                                   00470006
           BLOCK CONTAINS 0 RECORDS.                                    00480006
                                                                        00490006
       01  TRIP-ID-RECORD                 PIC X(100).                   00500006
      *                                                                 00510006
       FD  MISSING-CARTON-DETAIL-FILE                                   00520006
           RECORDING MODE IS F                                          00530006
           LABEL RECORDS ARE STANDARD                                   00540006
           BLOCK CONTAINS 0 RECORDS.                                    00550006
                                                                        00560006
       01  MISSING-CARTON-DETAIL-RECORD   PIC X(100).                   00570006
      *                                                                 00580006
      ******************************************************************00590006
       WORKING-STORAGE SECTION.                                         00600006
      ******************************************************************00610006
      ******************************************************************00620006
      *  TRIP ID INPUT FILE LAYOUT                                      00630006
      ******************************************************************00640006
           COPY SURD150.                                                00650006
                                                                        00660006
      *                                                                 00670006
      ******************************************************************00680006
      *  CARTON OUTPUT FILE LAYOUT                                      00690006
      ******************************************************************00700006
           COPY PCRD010.                                                00710006
                                                                        00720006
      ******************************************************************00730006
      *  DB2 FORMATTED MESSAGE AREA                                     00740006
      ******************************************************************00750006
           COPY DPWS004.                                                00760006
      *                                                                 00770006
      ******************************************************************00780006
      *  COMMUNICATIONS AREA FOR DB2                                    00790006
      ******************************************************************00800006
           EXEC SQL                                                     00810006
                INCLUDE SQLCA                                           00820006
           END-EXEC.                                                    00830006
      *                                                                 00840006
      *---------------------------------------------------------------* 00850006
      *    SUT_OTBND_STR_SCN TABLE                                      00860006
      *---------------------------------------------------------------* 00870006
           EXEC SQL                                                     00880006
                INCLUDE SUT00121                                        00890006
           END-EXEC.                                                    00900006
                                                                        00910006
      *---------------------------------------------------------------* 00920006
      *    SUT_OTBND_UPC UPC TABLE                                      00930006
      *---------------------------------------------------------------* 00940006
           EXEC SQL                                                     00950006
                INCLUDE SUT00028                                        00960006
           END-EXEC.                                                    00970006
                                                                        00980006
      *---------------------------------------------------------------* 00990006
      *    XLT_UPC  TABLE                                               01000006
      *---------------------------------------------------------------* 01010006
           EXEC SQL                                                     01020006
                INCLUDE XLT00006                                        01030006
           END-EXEC.                                                    01040006
                                                                        01050006
      *---------------------------------------------------------------* 01060006
      *    XLT_SKU  TABLE                                               01070006
      *---------------------------------------------------------------* 01080006
           EXEC SQL                                                     01090006
                INCLUDE XLT00003                                        01100006
           END-EXEC.                                                    01110006
                                                                        01120006
      *---------------------------------------------------------------* 01130006
      *     SUT_OTBND_VND  TABLE                                        01140006
      *---------------------------------------------------------------* 01150006
           EXEC SQL                                                     01160006
                INCLUDE SUT00026                                        01170006
           END-EXEC.                                                    01180006
                                                                        01190006
      *---------------------------------------------------------------* 01200006
      *     TPKSLIP  TABLE                                              01210006
      *---------------------------------------------------------------* 01220006
           EXEC SQL                                                     01230006
                INCLUDE TPKSLIP                                         01240006
           END-EXEC.                                                    01250006
                                                                        01260006
      *---------------------------------------------------------------* 01270006
      *     SUT_OTBND_CART  TABLE                                       01280006
      *---------------------------------------------------------------* 01290006
           EXEC SQL                                                     01300006
                INCLUDE SUT00024                                        01310006
           END-EXEC.                                                    01320006
                                                                        01330006
      *---------------------------------------------------------------* 01331014
      *     TSUOBTS - OUTBOUND TRIP STATUS                              01332014
      *---------------------------------------------------------------* 01333014
           EXEC SQL                                                     01334014
                INCLUDE TSUOBTS                                         01335014
           END-EXEC.                                                    01336014
                                                                        01337014
      ******************************************************************01340006
       01  ABEND-CODE                      PIC S9(04)    VALUE +0       01350006
                                                         COMP SYNC.     01360006
                                                                        01370006
       01  PS-PROGRAM-SWITCHES.                                         01380006
           05  PS-END-OF-FILE-SW           PIC  X(01)    VALUE 'N'.     01390006
               88  PS-END-OF-FILE                        VALUE 'Y'.     01400006
               88  PS-NOT-END-OF-FILE                    VALUE 'N'.     01410006
           05  PS-END-OF-SCAN-CTN-CSR-SW   PIC  X(01)    VALUE 'N'.     01420006
               88  PS-END-OF-SCAN-CTN-CSR                VALUE 'Y'.     01430006
               88  PS-NOT-END-OF-SCAN-CTN-CSR            VALUE 'N'.     01440006
           05  PS-END-OF-PO-RCVR-CSR-SW    PIC  X(01)    VALUE 'N'.     01450006
               88  PS-END-OF-PO-RCVR-CSR                 VALUE 'Y'.     01460006
               88  PS-NOT-END-OF-PO-RCVR-CSR             VALUE 'N'.     01470006
                                                                        01480006
       01  PC-PROGRAM-CONSTANTS.                                        01490006
           05  PC-A                        PIC  X(01)    VALUE 'A'.     01500006
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08) VALUE 'SUKUT163'. 01510006
           05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.  01520006
                                                                        01530006
       01  PV-PROGRAM-VARIABLES.                                        01540006
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.  01550006
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.  01560006
           05  PV-PO-NBR                   PIC S9(9) COMP VALUE ZEROES. 01570006
           05  PV-RCVR-SEQ-NBR             PIC S9(9) COMP VALUE ZEROES. 01580006
           05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.             01590006
                                                                        01600006
       01  COUNTERS.                                                    01610006
           05  PV-READ-COUNTER             PIC  S9(9) COMP-3.           01620006
           05  PV-UPC-COUNT                PIC  S9(9) COMP-3.           01630006
           05  PV-CARTON-COUNT             PIC  S9(9) COMP-3.           01640006
                                                                        01650006
      *---------------------------------------------------------------* 01660006
      *    SCAN-CTN CURSOR TO FIND MISSING CARTONS                      01670006
      *---------------------------------------------------------------* 01680006
           EXEC SQL                                                     01690006
               DECLARE SCAN-CTN-CSR CURSOR FOR                          01700006
               SELECT  CART.SHIPT_UNT_ID                                01710006
                    ,  CART.DEST_LOC_NBR                                01720006
                 FROM  SUT_OTBND_CART CART                              01730006
                    ,  TSUOBTS B                                        01731014
                WHERE  CART.OTBND_TRIP_ID  = :SUT00024-OTBND-TRIP-ID    01740006
                  AND NOT EXISTS                                        01750006
                      (SELECT *                                         01760006
                         FROM SUT_OTBND_STR_SCN SCAN                    01770006
                        WHERE SCAN.SHIPT_UNT_ID      =                  01780006
                              CART.SHIPT_UNT_ID                         01790006
                          AND SCAN.SCN_OTBND_TRIP_ID =                  01800006
                              CART.OTBND_TRIP_ID)                       01810006
                  AND  CART.OTBND_TRIP_ID  = B.OTBND_TRIP_ID            01811014
                  AND  B.TRIP_STAT_CDE     = 'SU'                       01812014
                  AND  B.FINCL_POST_TMST                                01813014
                                    = '9999-09-09-09.09.09.999999'      01814014
             ORDER BY  CART.SHIPT_UNT_ID                                01820006
              WITH UR                                                   01830006
           END-EXEC.                                                    01840006
                                                                        01850006
      *---------------------------------------------------------------* 01860006
      *    PO-RCVR CURSOR                                               01870006
      *---------------------------------------------------------------* 01880006
           EXEC SQL                                                     01890006
               DECLARE PO-RCVR-CSR CURSOR FOR                           01900006
               SELECT  SUM(A.SHPD_UNT_QTY)                              01910006
                      ,A.UPC_NBR                                        01920006
                      ,A.TRN_LNE_NBR                                    01930006
                      ,A.TRN_NBR                                        01940006
                      ,A.RCVR_SEQ_NBR                                   01950006
                      ,A.TRN_TYP_CDE                                    01960006
                      ,C.SKU_NBR                                        01970006
                      ,C.INTR_UPC_ID                                    01980006
                FROM  SUT_OTBND_UPC A                                   01990006
                     ,XLT_UPC B                                         02000006
                     ,XLT_SKU C                                         02010006
               WHERE  A.SHIPT_UNT_ID  =  :SUT00024-SHIPT-UNT-ID         02020006
                 AND  A.UPC_NBR       =  B.UPC_NBR                      02030006
                 AND  B.INTR_UPC_ID   =  C.INTR_UPC_ID                  02040006
                GROUP BY A.UPC_NBR, A.TRN_LNE_NBR, A.TRN_NBR,           02050006
                         A.RCVR_SEQ_NBR, A.TRN_TYP_CDE,                 02060006
                         C.SKU_NBR, C.INTR_UPC_ID                       02070006
                ORDER BY A.TRN_NBR, A.RCVR_SEQ_NBR                      02080006
                WITH UR                                                 02090006
           END-EXEC.                                                    02100006
                                                                        02110006
      ******************************************************************02120006
       PROCEDURE DIVISION.                                              02130006
      ******************************************************************02140006
      ******************************************************************02150006
      * 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE     02160006
      *      PROGRAM.                                                   02170006
      ******************************************************************02180006
       0000-PROGRAM-MAINLINE.                                           02190006
                                                                        02200006
           DISPLAY '***** SUKUT163 STARTED *****'.                      02210006
                                                                        02220006
           PERFORM 1000-INITIALIZE.                                     02230006
           IF PS-NOT-END-OF-FILE                                        02240006
               PERFORM 2000-PROCESS-TRIP-ID-FILE                        02250006
                  UNTIL PS-END-OF-FILE                                  02260006
           END-IF.                                                      02270006
           PERFORM 9000-EOJ-ROUTINE.                                    02280006
                                                                        02290006
           STOP RUN.                                                    02300006
                                                                        02310006
      ******************************************************************02320006
      *  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                    02330006
      *    -- OPEN INPUT FILE                                           02340006
      *    -- OPEN OUTPUT FILE                                          02350006
      *    -- READ INPUT FILE                                           02360006
      ******************************************************************02370006
       1000-INITIALIZE.                                                 02380006
                                                                        02390006
           INITIALIZE COUNTERS.                                         02400006
           OPEN INPUT  TRIP-ID-FILE.                                    02410006
           OPEN OUTPUT MISSING-CARTON-DETAIL-FILE.                      02420006
                                                                        02430006
           PERFORM 7000-READ-TRIP-ID-FILE.                              02440006
                                                                        02450006
      ******************************************************************02460006
      *  BEGIN RETRIEVING THE NECESSARY DATA                            02470006
      ******************************************************************02480006
       2000-PROCESS-TRIP-ID-FILE.                                       02490006
                                                                        02500006
           INITIALIZE PC010-PCT-INTRANSIT-REC.                          02510006
                                                                        02520006
           DISPLAY 'TRIP ID BEING PROCESSED IS ' SU150-TRIP-ID.         02530006
                                                                        02540006
           SET PS-NOT-END-OF-SCAN-CTN-CSR TO TRUE.                      02550006
           PERFORM 2100-OPEN-SCAN-CTN-CSR.                              02560006
           PERFORM 2200-FETCH-SCAN-CTN-CSR.                             02570006
           PERFORM 2300-PROCESS-SCAN-CTN-CSR UNTIL                      02580006
                   PS-END-OF-SCAN-CTN-CSR.                              02590006
           PERFORM 2400-CLOSE-SCAN-CTN-CSR.                             02600006
                                                                        02610006
           PERFORM 7000-READ-TRIP-ID-FILE.                              02620006
                                                                        02630006
                                                                        02640006
      ******************************************************************02650006
      *  OPEN SCANNED CARTON CURSOR.                                    02660006
      ******************************************************************02670006
       2100-OPEN-SCAN-CTN-CSR.                                          02680006
                                                                        02690006
           MOVE SU150-TRIP-ID    TO SUT00024-OTBND-TRIP-ID.             02700006
                                                                        02710006
           EXEC SQL                                                     02720006
               OPEN SCAN-CTN-CSR                                        02730006
           END-EXEC.                                                    02740006
                                                                        02750006
           EVALUATE TRUE                                                02760006
               WHEN SQLCODE = ZERO                                      02770006
                   CONTINUE                                             02780006
               WHEN SQLWARN0 NOT = SPACES                               02790006
               WHEN SQLCODE < ZERO                                      02800006
               WHEN SQLCODE > ZERO                                      02810006
                   MOVE '2100-OPEN-SCAN-CTN-CSRCSR' TO PV-PARAGRAPH-NAME02820006
                   MOVE 'SELECT SUT_OTBND_STR_SCN '                     02830006
                                          TO PV-DB2-OPERATION-ATTEMPTED 02840006
                   PERFORM 9100-SQL-ABEND                               02850006
           END-EVALUATE.                                                02860006
                                                                        02870006
                                                                        02880006
      ******************************************************************02890006
      *  FETCH SCANNED CARTON CURSOR ROW.                               02900006
      ******************************************************************02910006
       2200-FETCH-SCAN-CTN-CSR.                                         02920006
           EXEC SQL                                                     02930006
              FETCH SCAN-CTN-CSR                                        02940006
               INTO  :SUT00024-SHIPT-UNT-ID                             02950006
                   , :SUT00024-DEST-LOC-NBR                             02960006
           END-EXEC.                                                    02970006
                                                                        02980006
           EVALUATE TRUE                                                02990006
               WHEN SQLCODE = ZERO                                      03000006
                   MOVE SUT00024-DEST-LOC-NBR    TO  PC010-STR-NBR      03010006
               WHEN SQLCODE = +100                                      03020006
                   SET PS-END-OF-SCAN-CTN-CSR    TO  TRUE               03030006
               WHEN SQLWARN0 NOT = SPACES                               03040006
               WHEN SQLCODE < ZERO                                      03050006
               WHEN SQLCODE > ZERO                                      03060006
                   MOVE '2200-FETCH-SCAN-CTN-CSR' TO PV-PARAGRAPH-NAME  03070006
                   MOVE 'SELECT SUT_OTBND_STR_SCN '                     03080006
                                         TO PV-DB2-OPERATION-ATTEMPTED  03090006
                   PERFORM 9100-SQL-ABEND                               03100006
           END-EVALUATE.                                                03110006
                                                                        03120006
      ******************************************************************03130006
      *  PROCESS SCANNED CARTON CURSOR ROW.                             03140006
      ******************************************************************03150006
       2300-PROCESS-SCAN-CTN-CSR.                                       03160006
                                                                        03170006
           SET PS-NOT-END-OF-PO-RCVR-CSR TO TRUE.                       03180006
           PERFORM 5100-OPEN-PO-RCVR-CSR.                               03190006
           PERFORM 5250-FETCH-PO-RCVR-CSR.                              03200006
           IF PS-END-OF-PO-RCVR-CSR                                     03210006
               CONTINUE                                                 03220006
           ELSE                                                         03230006
               PERFORM 5200-PROCESS-PO-RCVR-CSR UNTIL                   03240006
                       PS-END-OF-PO-RCVR-CSR                            03250006
           END-IF.                                                      03260006
           PERFORM 5300-CLOSE-PO-RCVR-CSR.                              03270006
                                                                        03280007
           PERFORM 2200-FETCH-SCAN-CTN-CSR.                             03290007
                                                                        03300006
      ******************************************************************03310006
      *  CLOSE SCANNED CARTON CURSOR.                                   03320006
      ******************************************************************03330006
       2400-CLOSE-SCAN-CTN-CSR.                                         03340006
           EXEC SQL                                                     03350006
              CLOSE SCAN-CTN-CSR                                        03360006
           END-EXEC.                                                    03370006
                                                                        03380006
           EVALUATE TRUE                                                03390006
               WHEN SQLCODE = ZERO                                      03400006
                   CONTINUE                                             03410006
               WHEN SQLWARN0 NOT = SPACES                               03420006
               WHEN SQLCODE < ZERO                                      03430006
               WHEN SQLCODE > ZERO                                      03440006
                   MOVE '2400-CLOSE-SCAN-CTN-CSR' TO PV-PARAGRAPH-NAME  03450006
                   MOVE 'SELECT SUT_OTBND_STR_SCN'                      03460006
                                         TO PV-DB2-OPERATION-ATTEMPTED  03470006
                   PERFORM 9100-SQL-ABEND                               03480006
           END-EVALUATE.                                                03490006
                                                                        03500006
                                                                        03510006
      *----------------------------------------------------------------*03520006
      * OPEN CURSOR                                                     03530006
      *----------------------------------------------------------------*03540006
       5100-OPEN-PO-RCVR-CSR.                                           03550006
           INITIALIZE DCLSUT00028.                                      03560006
           EXEC SQL                                                     03570006
               OPEN PO-RCVR-CSR                                         03580006
           END-EXEC.                                                    03590006
                                                                        03600006
           EVALUATE TRUE                                                03610006
               WHEN SQLCODE = ZERO                                      03620006
                   CONTINUE                                             03630006
               WHEN SQLWARN0 NOT = SPACES                               03640006
               WHEN SQLCODE < ZERO                                      03650006
               WHEN SQLCODE > ZERO                                      03660006
                   MOVE '5100-OPEN-PO-RCVR-CSR' TO PV-PARAGRAPH-NAME    03670006
                   MOVE 'SELECT SUT_OTBND_UPC '                         03680006
                                          TO PV-DB2-OPERATION-ATTEMPTED 03690006
                   PERFORM 9100-SQL-ABEND                               03700006
           END-EVALUATE.                                                03710006
                                                                        03720006
      *----------------------------------------------------------------*03730006
      * PROCESS PO-RCVR CURSOR                                          03740006
      *----------------------------------------------------------------*03750006
       5200-PROCESS-PO-RCVR-CSR.                                        03760006
           IF SUT00028-TRN-TYP-CDE = 'CS'                               03770006
               CONTINUE                                                 03780006
           ELSE                                                         03790006
               IF SUT00028-TRN-TYP-CDE = 'FX'                           03800006
      *            SET PS-END-OF-PO-RCVR-CSR TO TRUE                    03810012
                   CONTINUE                                             03820012
               ELSE                                                     03830006
                   IF SUT00028-TRN-NBR    = PV-PO-NBR AND               03840006
                       SUT00028-RCVR-SEQ-NBR = PV-RCVR-SEQ-NBR          03850006
                       MOVE SUT00028-RCVR-SEQ-NBR TO PC010-RCVR-SEQ-NBR 03860006
                   ELSE                                                 03870006
                       IF SUT00028-TRN-NBR = ZERO                       03880006
                         MOVE SUT00028-RCVR-SEQ-NBR                     03890006
                                                  TO PC010-RCVR-SEQ-NBR 03900006
                       ELSE                                             03910006
                           IF SUT00028-RCVR-SEQ-NBR = ZERO              03920006
                               PERFORM 5400-GET-REC-SEQ-NBR             03930006
                           ELSE                                         03940006
                               MOVE SUT00028-RCVR-SEQ-NBR TO            03950006
                                                 PC010-RCVR-SEQ-NBR     03960006
                           END-IF                                       03970006
                       END-IF                                           03980006
                   END-IF                                               03990006
               END-IF                                                   04000006
           END-IF.                                                      04010006
                                                                        04020006
           IF SUT00028-TRN-TYP-CDE = 'FX'                               04030009
               CONTINUE                                                 04040009
           ELSE                                                         04050009
               PERFORM 8000-WRITE-OUTPUT                                04060009
               MOVE SUT00028-TRN-NBR      TO PV-PO-NBR                  04070009
               MOVE SUT00028-RCVR-SEQ-NBR TO PV-RCVR-SEQ-NBR            04080009
           END-IF.                                                      04090009
           PERFORM 5250-FETCH-PO-RCVR-CSR.                              04100012
                                                                        04110006
      *----------------------------------------------------------------*04120006
      * FETCH PO-RCVR CURSOR                                            04130006
      *----------------------------------------------------------------*04140006
       5250-FETCH-PO-RCVR-CSR.                                          04150006
           EXEC SQL                                                     04160006
              FETCH PO-RCVR-CSR                                         04170006
               INTO   :SUT00028-SHPD-UNT-QTY                            04180006
                     ,:SUT00028-UPC-NBR                                 04190006
                     ,:SUT00028-TRN-LNE-NBR                             04200006
                     ,:SUT00028-TRN-NBR                                 04210006
                     ,:SUT00028-RCVR-SEQ-NBR                            04220006
                     ,:SUT00028-TRN-TYP-CDE                             04230006
                     ,:XLT00003-SKU-NBR                                 04240006
                     ,:XLT00003-INTR-UPC-ID                             04250006
           END-EXEC.                                                    04260006
                                                                        04270006
           EVALUATE TRUE                                                04280006
               WHEN SQLCODE = ZERO                                      04290006
                   COMPUTE PC010-IN-TRNST-QTY =                         04300011
                           SUT00028-SHPD-UNT-QTY * -1                   04310011
                   END-COMPUTE                                          04320011
                   MOVE SUT00028-UPC-NBR      TO PC010-UPC-NBR          04330006
                   MOVE SUT00028-TRN-NBR      TO PC010-PO-NBR           04340006
                   MOVE SUT00028-TRN-LNE-NBR  TO PC010-TRN-LNE-NBR      04350006
                   MOVE SUT00028-TRN-TYP-CDE  TO PC010-OTB-TYP-CDE      04360006
                   MOVE XLT00003-INTR-UPC-ID  TO PC010-INTR-UPC-ID      04370008
                   ADD  +1                    TO PV-CARTON-COUNT        04380006
                                                                        04390006
               WHEN SQLCODE = +100                                      04400006
                   MOVE ZEROES                TO PC010-IN-TRNST-QTY     04410006
                                                 PC010-UPC-NBR          04420006
                                                 PC010-PO-NBR           04430006
                                                 PC010-RCVR-SEQ-NBR     04440006
                                                 PC010-TRN-LNE-NBR      04450006
                   SET PS-END-OF-PO-RCVR-CSR     TO  TRUE               04460006
               WHEN SQLWARN0 NOT = SPACES                               04470006
               WHEN SQLCODE < ZERO                                      04480006
               WHEN SQLCODE > ZERO                                      04490006
                   MOVE '5200-FETCH-PO-RCVR-CSR' TO PV-PARAGRAPH-NAME   04500006
                   MOVE 'SELECT SUT_OTBND_UPC'                          04510006
                                         TO PV-DB2-OPERATION-ATTEMPTED  04520006
                   PERFORM 9100-SQL-ABEND                               04530006
           END-EVALUATE.                                                04540006
                                                                        04550006
      *----------------------------------------------------------------*04560006
      * CLOSE PO-RCVR CURSOR                                            04570006
      *----------------------------------------------------------------*04580006
       5300-CLOSE-PO-RCVR-CSR.                                          04590006
           EXEC SQL                                                     04600006
              CLOSE PO-RCVR-CSR                                         04610006
           END-EXEC.                                                    04620006
                                                                        04630006
           EVALUATE TRUE                                                04640006
               WHEN SQLCODE = ZERO                                      04650006
                   CONTINUE                                             04660006
               WHEN SQLWARN0 NOT = SPACES                               04670006
               WHEN SQLCODE < ZERO                                      04680006
               WHEN SQLCODE > ZERO                                      04690006
                   MOVE '5300-CLOSE-PO-RCVR-CSR' TO PV-PARAGRAPH-NAME   04700006
                   MOVE 'SELECT SUT_OTBND_UPC'                          04710006
                                         TO PV-DB2-OPERATION-ATTEMPTED  04720006
                   PERFORM 9100-SQL-ABEND                               04730006
           END-EVALUATE.                                                04740006
                                                                        04750006
      *----------------------------------------------------------------*04760006
      * GET REC SEQ NBR                                                 04770006
      *----------------------------------------------------------------*04780006
       5400-GET-REC-SEQ-NBR.                                            04790006
           EXEC SQL                                                     04800006
               SELECT  B.REC_SEQ_NBR                                    04810006
                INTO   :PKSLIP-REC-SEQ-NBR                              04820006
                FROM  SUT_OTBND_VND A                                   04830006
                     ,TPKSLIP B                                         04840006
               WHERE  A.SHIPT_UNT_ID  =  :SUT00024-SHIPT-UNT-ID         04850006
                 AND  A.PO_NBR        =  B.PO_NBR                       04860006
                 AND  A.PKSL_SEQ_NBR  =  B.SEQ_NBR                      04870006
                 AND  B.VER_STAT_CDE  =  :PC-A                          04880006
                 WITH UR                                                04890006
           END-EXEC.                                                    04900006
                                                                        04910006
           EVALUATE TRUE                                                04920006
               WHEN SQLCODE = ZERO                                      04930006
                   MOVE PKSLIP-REC-SEQ-NBR  TO  PC010-RCVR-SEQ-NBR      04940006
                                                SUT00028-RCVR-SEQ-NBR   04950006
               WHEN SQLCODE = +100                                      04960006
                   MOVE ZERO                TO  PC010-RCVR-SEQ-NBR      04970006
                                                SUT00028-RCVR-SEQ-NBR   04980006
               WHEN SQLWARN0 NOT = SPACES                               04990006
               WHEN SQLCODE < ZERO                                      05000006
               WHEN SQLCODE > ZERO                                      05010006
                   MOVE '5400-GET-REC-SEQ-NBR ' TO PV-PARAGRAPH-NAME    05020006
                   MOVE 'SELECT TPKSLIP ' TO PV-DB2-OPERATION-ATTEMPTED 05030006
                   PERFORM 9100-SQL-ABEND                               05040006
           END-EVALUATE.                                                05050006
                                                                        05060006
                                                                        05070006
      ******************************************************************05080006
      *  READ INPUT FILE.                                               05090006
      ******************************************************************05100006
       7000-READ-TRIP-ID-FILE.                                          05110006
                                                                        05120006
            INITIALIZE SU150-SCANNED-TRIP-ID-REC.                       05130006
                                                                        05140006
            READ TRIP-ID-FILE INTO                                      05150006
                SU150-SCANNED-TRIP-ID-REC                               05160006
                  AT END SET PS-END-OF-FILE TO TRUE.                    05170006
                                                                        05180006
            IF PS-END-OF-FILE                                           05190006
               CONTINUE                                                 05200006
            ELSE                                                        05210006
               ADD +1 TO PV-READ-COUNTER                                05220006
            END-IF.                                                     05230006
                                                                        05240006
      ******************************************************************05250006
      *  WRITE OUTPUT FILE.                                             05260006
      ******************************************************************05270006
       8000-WRITE-OUTPUT.                                               05280006
                                                                        05290006
           MOVE SU150-TRIP-ID               TO PC010-TRIP-ID.           05300008
           MOVE PC-CURRENT-PROGRAM-NAME     TO PC010-UPD-USR-ID.        05310008
           MOVE SU150-EVENT-TMST            TO PC010-EVENT-TMST.        05320006
           SET PC010-NOT-SCANNED-BY-STORE   TO TRUE.                    05330006
           SET PC010-TRN-STR-EXPECTED       TO TRUE.                    05340006
           SET PC010-NOT-SHIPPED-FROM-DC    TO TRUE.                    05350006
           SET PC010-UNLOADED-AT-STORE      TO TRUE.                    05360006
                                                                        05370006
           WRITE  MISSING-CARTON-DETAIL-RECORD                          05380006
            FROM  PC010-PCT-INTRANSIT-REC.                              05390006
           ADD    +1   TO PV-UPC-COUNT.                                 05400006
                                                                        05410006
                                                                        05420006
      ******************************************************************05430006
      *  CLOSE INPUT FILE.  DISPLAY COUNTERS.                           05440006
      ******************************************************************05450006
       9000-EOJ-ROUTINE.                                                05460006
           CLOSE TRIP-ID-FILE                                           05470006
                 MISSING-CARTON-DETAIL-FILE.                            05480006
                                                                        05490006
           MOVE PV-READ-COUNTER TO PV-DISPLAY-COUNT.                    05500006
           DISPLAY 'TOTAL RECORDS READ.: '  PV-DISPLAY-COUNT.           05510006
           MOVE PV-UPC-COUNT    TO PV-DISPLAY-COUNT.                    05520006
           DISPLAY 'TOTAL RECORDS OUT..: '  PV-DISPLAY-COUNT.           05530006
           MOVE PV-CARTON-COUNT TO PV-DISPLAY-COUNT.                    05540006
           DISPLAY 'TOTAL TRIP CARTONS.: '  PV-DISPLAY-COUNT.           05550006
                                                                        05560006
           DISPLAY '*****  SUKUT163 ENDED  *****'.                      05570006
                                                                        05580006
      ******************************************************************05590006
      *  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL    05600006
      *  ERROR OR WARNING CODE OCCURS.                                  05610006
      ******************************************************************05620006
       9100-SQL-ABEND.                                                  05630006
           DISPLAY '9100-SQL-ABEND'.                                    05640006
           DISPLAY '** ABEND     **'.                                   05650006
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        05660006
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              05670006
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.     05680006
           DISPLAY '** TRIP ID   ** = ' SU150-TRIP-ID.                  05690006
                                                                        05700006
      ******************************************************************05710006
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    05720006
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         05730006
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     05740006
      * FORMAT.                                                         05750006
      ******************************************************************05760006
           COPY DPPD004.                                                05770006
                                                                        05780006
           MOVE +4013                  TO ABEND-CODE.                   05790006
           CALL 'ILBOABN0' USING ABEND-CODE.                            05800006
                                                                        05810006
