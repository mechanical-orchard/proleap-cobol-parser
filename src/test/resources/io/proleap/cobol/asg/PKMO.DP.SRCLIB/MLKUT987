       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT987.                                                 
       AUTHOR.        JILL LIN.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  09-15-10.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      * MLKUT987 -                                                              
      * THIS PROGRAM WILL COMPARE EXTRACTS OF WKLYSUM AND                       
      * AUDIT DATA EXTRACT FILES FROM TABLES:                                   
      *     MLT_WKLY_SUM        (MLT00005)                                      
      *     MLT_INV_ADJ_TRN_DTL (MLT00037)                                      
      *     MLT_PRCHG_TRN_DTL   (MLT00038)                                      
      *     MLT_PADJ_TRN_DTL    (MLT00039)                                      
      *     MLT_SLS_TRN_DTL     (MLT00040)                                      
      * AND CREATE FOUR EXTRACT FILES FOR REPORT PROGRAM TO SHOW THE            
      * DIFFERENCES.                                                            
      *                                                                         
      ******************************************************************        
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
P10681*                                                                         
P10681* P10681* 03/02/12  COGNIZANT   FIX TO REMOVE JUNK VALUE                  
P10681*                               POPULATION TO                             
P10681*                               ML987-WKSUM-PADJ-AMT                      
      *                                                                         
P46161* P46161* 06/20/14  COGNIZANT   UNCOMMENT THE NON-ZERO VALUE              
P46161*                               CHECK FOR OTH-COST & OTH-RTL AMT          
P46161*                               IN 7220-PROCESS-NO-PURCHADJ-ERR           
P46161*                               PARA TO AVOID JOB FAILURE.                
      * ------- ---------- ----------- --------------------------------         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT WKLYSUM-EXTR-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT INVADJ-EXTR-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT SALE-EXTR-FILE                                                
               ASSIGN TO INP03.                                                 
                                                                                
           SELECT PURCHADJ-EXTR-FILE                                            
               ASSIGN TO INP04.                                                 
                                                                                
           SELECT PRCHG-EXTR-FILE                                               
               ASSIGN TO INP05.                                                 
                                                                                
           SELECT INVADJ-RPT-FILE                                               
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT SALE-RPT-FILE                                                 
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT PURCHADJ-RPT-FILE                                             
               ASSIGN TO OUT03.                                                 
                                                                                
           SELECT PRCHG-RPT-FILE                                                
               ASSIGN TO OUT04.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  WKLYSUM-EXTR-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  WKLYSUM-EXTR-REC             PIC X(352).                             
                                                                                
       FD  INVADJ-EXTR-FILE                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INVADJ-EXTR-REC              PIC X(37).                              
                                                                                
       FD  SALE-EXTR-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SALE-EXTR-REC                PIC X(38).                              
                                                                                
       FD  PURCHADJ-EXTR-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PURCHADJ-EXTR-REC            PIC X(74).                              
                                                                                
       FD  PRCHG-EXTR-FILE                                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PRCHG-EXTR-REC               PIC X(43).                              
                                                                                
       FD  INVADJ-RPT-FILE                                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INVADJ-RPT-REC               PIC X(80).                              
                                                                                
       FD  SALE-RPT-FILE                                                        
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SALE-RPT-REC                 PIC X(80).                              
                                                                                
       FD  PURCHADJ-RPT-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PURCHADJ-RPT-REC             PIC X(80).                              
                                                                                
       FD  PRCHG-RPT-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PRCHG-RPT-REC                PIC X(80).                              
                                                                                
      *                                                                         
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MORE-WKLYSUM-RECS-SW            PIC X(01)   VALUE 'Y'.        
               88  PS-MORE-WKLYSUM-RECS                       VALUE 'Y'.        
               88  PS-NO-MORE-WKLYSUM-RECS                    VALUE 'N'.        
           05  PS-MORE-INVADJ-RECS-SW             PIC X(01)   VALUE 'Y'.        
               88  PS-MORE-INVADJ-RECS                        VALUE 'Y'.        
               88  PS-NO-MORE-INVADJ-RECS                     VALUE 'N'.        
           05  PS-MORE-SALE-RECS-SW               PIC X(01)   VALUE 'Y'.        
               88  PS-MORE-SALE-RECS                          VALUE 'Y'.        
               88  PS-NO-MORE-SALE-RECS                       VALUE 'N'.        
           05  PS-MORE-PURCHADJ-RECS-SW           PIC X(01)   VALUE 'Y'.        
               88  PS-MORE-PURCHADJ-RECS                      VALUE 'Y'.        
               88  PS-NO-MORE-PURCHADJ-RECS                   VALUE 'N'.        
           05  PS-MORE-PRCHG-RECS-SW              PIC X(01)   VALUE 'Y'.        
               88  PS-MORE-PRCHG-RECS                         VALUE 'Y'.        
               88  PS-NO-MORE-PRCHG-RECS                      VALUE 'N'.        
           05  PS-BYPASS-0-DOLLARS-SW             PIC X(01)   VALUE 'Y'.        
               88  PS-BYPASS-0-DOLLARS                        VALUE 'Y'.        
               88  PS-NO-BYPASS-DOLLARS                       VALUE 'N'.        
           05  PS-FILE-KEY-MATCH-SW               PIC X(01)   VALUE 'Y'.        
               88  PS-FILE-KEY-MATCH                          VALUE 'Y'.        
               88  PS-FILE-KEY-NOT-MATCH                      VALUE 'N'.        
                                                                                
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
           05  PA-WKLYSUM-RECS-READ              PIC S9(13)  VALUE ZERO.        
           05  PA-INVADJ-RECS-READ               PIC S9(13)  VALUE ZERO.        
           05  PA-SALE-RECS-READ                 PIC S9(13)  VALUE ZERO.        
           05  PA-PURCHADJ-RECS-READ             PIC S9(13)  VALUE ZERO.        
           05  PA-PRCHG-RECS-READ                PIC S9(13)  VALUE ZERO.        
           05  PA-INVADJ-KEY-MATCHES             PIC S9(13)  VALUE ZERO.        
           05  PA-SALE-KEY-MATCHES               PIC S9(13)  VALUE ZERO.        
           05  PA-PURCHADJ-KEY-MATCHES           PIC S9(13)  VALUE ZERO.        
           05  PA-PRCHG-KEY-MATCHES              PIC S9(13)  VALUE ZERO.        
           05  PA-INVADJ-RECS-WRITTEN            PIC S9(13)  VALUE ZERO.        
           05  PA-SALE-RECS-WRITTEN              PIC S9(13)  VALUE ZERO.        
           05  PA-PURCHADJ-RECS-WRITTEN          PIC S9(13)  VALUE ZERO.        
           05  PA-PRCHG-RECS-WRITTEN             PIC S9(13)  VALUE ZERO.        
                                                                                
       01  CD-COUNTER-DISPLAY.                                                  
           05  CD-COUNT-HOLD1                    PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.         
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-WKLYSUM-FILE-KEY.                                             
               10  PV-WKLYSUM-FSCL-WK-END-DTE    PIC  X(10).                    
               10  PV-WKLYSUM-DEPT-NBR           PIC S9(4)V COMP-3.             
               10  PV-WKLYSUM-MAJ-CL-NBR         PIC S9(4)V COMP-3.             
               10  PV-WKLYSUM-SUB-CL-NBR         PIC S9(4)V COMP-3.             
               10  PV-WKLYSUM-LOC-NBR            PIC S9(4) COMP.                
               10  PV-WKLYSUM-ML-LO-MDSE-LVL-ID  PIC  S9(9) COMP.               
           05  PV-INVADJ-FILE-KEY.                                              
               10  PV-INVADJ-FSCL-WK-END-DTE     PIC  X(10).                    
               10  PV-INVADJ-DEPT-NBR            PIC S9(4)V COMP-3.             
               10  PV-INVADJ-MAJ-CL-NBR          PIC S9(4)V COMP-3.             
               10  PV-INVADJ-SUB-CL-NBR          PIC S9(4)V COMP-3.             
               10  PV-INVADJ-LOC-NBR             PIC S9(4) COMP.                
               10  PV-INVADJ-ML-LO-MDSE-LVL-ID   PIC  S9(9) COMP.               
           05  PV-SALE-FILE-KEY.                                                
               10  PV-SALE-FSCL-WK-END-DTE       PIC  X(10).                    
               10  PV-SALE-DEPT-NBR              PIC S9(4)V COMP-3.             
               10  PV-SALE-MAJ-CL-NBR            PIC S9(4)V COMP-3.             
               10  PV-SALE-SUB-CL-NBR            PIC S9(4)V COMP-3.             
               10  PV-SALE-LOC-NBR               PIC S9(4) COMP.                
               10  PV-SALE-ML-LO-MDSE-LVL-ID     PIC  S9(9) COMP.               
           05  PV-PURCHADJ-FILE-KEY.                                            
               10  PV-PURCHADJ-FSCL-WK-END-DTE   PIC  X(10).                    
               10  PV-PURCHADJ-DEPT-NBR          PIC S9(4)V COMP-3.             
               10  PV-PURCHADJ-MAJ-CL-NBR        PIC S9(4)V COMP-3.             
               10  PV-PURCHADJ-SUB-CL-NBR        PIC S9(4)V COMP-3.             
               10  PV-PURCHADJ-LOC-NBR           PIC S9(4) COMP.                
               10  PV-PURCHADJ-ML-LO-MDSE-LVL-ID PIC  S9(9) COMP.               
           05  PV-PRCHG-FILE-KEY.                                               
               10  PV-PRCHG-FSCL-WK-END-DTE      PIC  X(10).                    
               10  PV-PRCHG-DEPT-NBR             PIC S9(4)V COMP-3.             
               10  PV-PRCHG-MAJ-CL-NBR           PIC S9(4)V COMP-3.             
               10  PV-PRCHG-SUB-CL-NBR           PIC S9(4)V COMP-3.             
               10  PV-PRCHG-LOC-NBR              PIC S9(4) COMP.                
               10  PV-PRCHG-ML-LO-MDSE-LVL-ID    PIC  S9(9) COMP.               
           05  PV-WORK-ADJ-DTE.                                                 
               10  PV-WORK-ADJ-MM      PIC  X(2) VALUE '00'.                    
               10  PV-WORK-ADJ-DD      PIC  X(2) VALUE '00'.                    
               10  PV-WORK-ADJ-YY      PIC  X(2) VALUE '00'.                    
           05  PV-WORK-CLASS.                                                   
               10  PV-WORK-CLASS1      PIC  X(1) VALUE '0'.                     
               10  FILLER              PIC  X(1) VALUE '0'.                     
           05  PV-WRITE-MAJOR-CLASS.                                            
               10  PV-WRITE-MAJ-CLASS1 PIC  X(1) VALUE '0'.                     
               10  FILLER              PIC  X(1) VALUE '0'.                     
           05  PV-AP-TRN-CDE           PIC  X(2) VALUE SPACES.                  
               88  PV-RTV-ADJ-AP-TRN-CDE         VALUE                          
                   '20' '21' '22' '55' '54' '0A'.                               
               88  PV-DA-ADJ-AP-TRN-CDE          VALUE                          
                   '24' '2F' '2H' '2I'.                                         
                                                                                
       01 ABEND-AREAS.                                                          
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO                   
                                                   COMP SYNC.                   
               88  AC-NO-WKLYSUM-DATA              VALUE +4001.                 
               88  AC-NO-SALE-DATA                 VALUE +4002.                 
               88  AC-NO-PURCHADJ-DATA             VALUE +4003.                 
               88  AC-NO-PRCHG-DATA                VALUE +4004.                 
               88  AC-DB2-ERROR                    VALUE +4013.                 
           05  AA-ABEND-LIT            PIC X(40)   VALUE                        
                 '*****       ABEND'.                                           
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
                   '*****   PROGRAM: MLKUT987'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER              PIC X(17)   VALUE                        
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER              PIC X(06)   VALUE '*****'.               
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER              PIC X(17)   VALUE                        
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.                
                                                                                
       EJECT                                                                    
                                                                                
      *** DATA INTEGRITY BV WEEKLY SUMMARY INPUT FILE                           
           COPY MLRD980.                                                        
                                                                                
      *** DATA INTEGRITY INVENTORY ADJUSTMENT INPUT FILE                        
           COPY MLRD981.                                                        
                                                                                
      *** DATA INTEGRITY SALE DIFFERENCE INPUT FILE                             
           COPY MLRD982.                                                        
                                                                                
      *** DATA INTEGRITY PURCHASE ADJUSTMENT INPUT FILE                         
           COPY MLRD983.                                                        
                                                                                
      *** DATA INTEGRITY PRICE CHANG ADJUSTMENT INPUT FILE                      
           COPY MLRD984.                                                        
                                                                                
      *** DATA INTEGRITY INVENTORY ADJUSTMENT DIFFERENCE REPORT FILE            
           COPY MLRD985.                                                        
                                                                                
      *** DATA INTEGRITY SALE DIFFERENCE REPORT FILE                            
           COPY MLRD986.                                                        
                                                                                
      *** DATA INTEGRITY PURCHASE ADJUSTMENT DIFFERENCE REPORT FILE             
           COPY MLRD987.                                                        
                                                                                
      *** DATA INTEGRITY PRICE CHANG ADJUSTMENT DIFFERENCE REPORT FILE          
           COPY MLRD988.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAINLINE-MLKUT987.                                                  
      ******************************************************************        
      *                                                                         
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS.                                           
                                                                                
           PERFORM 9000-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      ******************************************************************        
      *                                                                         
           OPEN INPUT  WKLYSUM-EXTR-FILE                                        
                       INVADJ-EXTR-FILE                                         
                       SALE-EXTR-FILE                                           
                       PURCHADJ-EXTR-FILE                                       
                       PRCHG-EXTR-FILE.                                         
                                                                                
           OPEN OUTPUT INVADJ-RPT-FILE                                          
                       SALE-RPT-FILE                                            
                       PURCHADJ-RPT-FILE                                        
                       PRCHG-RPT-FILE.                                          
                                                                                
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
      *                                                                         
           PERFORM 4000-READ-WKLYSUM-EXTR-FILE.                                 
           IF PS-NO-MORE-WKLYSUM-RECS                                           
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY WKLYSUM EXTRACT FILE'                                
                                       TO AA-MESSAGE                            
               MOVE +4001              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM 4100-READ-INVADJ-EXTR-FILE.                                  
           IF PS-NO-MORE-INVADJ-RECS                                            
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM                                                          
                 UNTIL PS-NO-MORE-WKLYSUM-RECS                                  
                   AND PS-NO-MORE-INVADJ-RECS                                   
                   PERFORM 2100-COMPARE-INVADJ-FILE                             
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
           CLOSE WKLYSUM-EXTR-FILE.                                             
           SET PS-MORE-WKLYSUM-RECS TO TRUE.                                    
           OPEN INPUT WKLYSUM-EXTR-FILE.                                        
           PERFORM 4000-READ-WKLYSUM-EXTR-FILE.                                 
           IF PS-NO-MORE-WKLYSUM-RECS                                           
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY WKLYSUM EXTRACT FILE'                                
                                       TO AA-MESSAGE                            
               MOVE +4001              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM 4200-READ-SALE-EXTR-FILE.                                    
           IF PS-NO-MORE-SALE-RECS                                              
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY SALE EXTRACT FILE'                                   
                                       TO AA-MESSAGE                            
               MOVE +4002              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM                                                              
             UNTIL PS-NO-MORE-WKLYSUM-RECS                                      
               AND PS-NO-MORE-SALE-RECS                                         
               PERFORM 2200-COMPARE-SALE-FILE                                   
           END-PERFORM.                                                         
                                                                                
           CLOSE WKLYSUM-EXTR-FILE.                                             
           SET PS-MORE-WKLYSUM-RECS TO TRUE.                                    
           OPEN INPUT WKLYSUM-EXTR-FILE.                                        
           PERFORM 4000-READ-WKLYSUM-EXTR-FILE.                                 
           IF PS-NO-MORE-WKLYSUM-RECS                                           
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY WKLYSUM EXTRACT FILE'                                
                                       TO AA-MESSAGE                            
               MOVE +4001              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM 4300-READ-PURCHADJ-EXTR-FILE.                                
           IF PS-NO-MORE-PURCHADJ-RECS                                          
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY PURCHADJ EXTRACT FILE'                               
                                       TO AA-MESSAGE                            
               MOVE +4003              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM                                                              
             UNTIL PS-NO-MORE-WKLYSUM-RECS                                      
               AND PS-NO-MORE-PURCHADJ-RECS                                     
               PERFORM 2300-COMPARE-PURCHADJ-FILE                               
           END-PERFORM.                                                         
                                                                                
           CLOSE WKLYSUM-EXTR-FILE.                                             
           SET PS-MORE-WKLYSUM-RECS TO TRUE.                                    
           OPEN INPUT WKLYSUM-EXTR-FILE.                                        
           PERFORM 4000-READ-WKLYSUM-EXTR-FILE.                                 
           IF PS-NO-MORE-WKLYSUM-RECS                                           
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY WKLYSUM EXTRACT FILE'                                
                                       TO AA-MESSAGE                            
               MOVE +4001              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM 4400-READ-PRCHG-EXTR-FILE.                                   
           IF PS-NO-MORE-PRCHG-RECS                                             
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY PRCHG EXTRACT FILE'                                  
                                       TO AA-MESSAGE                            
               MOVE +4004              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM                                                              
             UNTIL PS-NO-MORE-WKLYSUM-RECS                                      
               AND PS-NO-MORE-PRCHG-RECS                                        
               PERFORM 2400-COMPARE-PRCHG-FILE                                  
           END-PERFORM.                                                         
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2100-COMPARE-INVADJ-FILE.                                                
      ******************************************************************        
      *                                                                         
           PERFORM 3000-BUILD-WKLYSUM-FILE-KEY.                                 
           PERFORM 3100-BUILD-INVADJ-FILE-KEY.                                  
           IF PV-WKLYSUM-FILE-KEY = PV-INVADJ-FILE-KEY                          
               ADD 1 TO PA-INVADJ-KEY-MATCHES                                   
               PERFORM 5100-COMPARE-INVADJ-FILE-AMTS                            
               PERFORM 4000-READ-WKLYSUM-EXTR-FILE                              
               PERFORM 4100-READ-INVADJ-EXTR-FILE                               
           ELSE                                                                 
               IF PV-WKLYSUM-FILE-KEY < PV-INVADJ-FILE-KEY                      
                   PERFORM 5200-PROCESS-ONLY-WKLYSUM-FILE                       
                   PERFORM 4000-READ-WKLYSUM-EXTR-FILE                          
               ELSE                                                             
                   PERFORM 5300-PROCESS-ONLY-INVADJ-FILE                        
                   PERFORM 4100-READ-INVADJ-EXTR-FILE                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2200-COMPARE-SALE-FILE.                                                  
      ******************************************************************        
      *                                                                         
           PERFORM 3000-BUILD-WKLYSUM-FILE-KEY.                                 
           PERFORM 3200-BUILD-SALE-FILE-KEY.                                    
           IF PV-WKLYSUM-FILE-KEY = PV-SALE-FILE-KEY                            
               ADD 1 TO PA-SALE-KEY-MATCHES                                     
               SET PS-FILE-KEY-MATCH TO TRUE                                    
               PERFORM 6100-COMPARE-SALE-FILE-AMTS                              
                   UNTIL PS-FILE-KEY-NOT-MATCH                                  
           ELSE                                                                 
               IF PV-WKLYSUM-FILE-KEY < PV-SALE-FILE-KEY                        
                   PERFORM 6200-PROCESS-ONLY-WKLYSUM-FILE                       
                   PERFORM 4000-READ-WKLYSUM-EXTR-FILE                          
               ELSE                                                             
                   PERFORM 6300-PROCESS-ONLY-SALE-FILE                          
                   PERFORM 4200-READ-SALE-EXTR-FILE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
       2300-COMPARE-PURCHADJ-FILE.                                              
      ******************************************************************        
      *                                                                         
           PERFORM 3000-BUILD-WKLYSUM-FILE-KEY.                                 
           PERFORM 3300-BUILD-PURCHADJ-FILE-KEY.                                
           IF PV-WKLYSUM-FILE-KEY = PV-PURCHADJ-FILE-KEY                        
               ADD 1 TO PA-PURCHADJ-KEY-MATCHES                                 
               SET PS-FILE-KEY-MATCH TO TRUE                                    
               PERFORM 7100-COMPARE-PADJ-FILE-AMTS                              
                   UNTIL PS-FILE-KEY-NOT-MATCH                                  
           ELSE                                                                 
               IF PV-WKLYSUM-FILE-KEY < PV-PURCHADJ-FILE-KEY                    
                   PERFORM 7200-PROCESS-ONLY-WKLYSUM-FILE                       
                   PERFORM 4000-READ-WKLYSUM-EXTR-FILE                          
               ELSE                                                             
                   PERFORM 7300-PROCESS-ONLY-PADJ-FILE                          
                   PERFORM 4300-READ-PURCHADJ-EXTR-FILE                         
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2400-COMPARE-PRCHG-FILE.                                                 
      ******************************************************************        
      *                                                                         
           PERFORM 3000-BUILD-WKLYSUM-FILE-KEY.                                 
           PERFORM 3400-BUILD-PRCHG-FILE-KEY.                                   
           IF PV-WKLYSUM-FILE-KEY = PV-PRCHG-FILE-KEY                           
               ADD 1 TO PA-PRCHG-KEY-MATCHES                                    
               SET PS-FILE-KEY-MATCH TO TRUE                                    
               PERFORM 8100-COMPARE-PRCHG-FILE-AMTS                             
                   UNTIL PS-FILE-KEY-NOT-MATCH                                  
           ELSE                                                                 
               IF PV-WKLYSUM-FILE-KEY < PV-PRCHG-FILE-KEY                       
                   PERFORM 8200-PROCESS-ONLY-WKLYSUM-FILE                       
                   PERFORM 4000-READ-WKLYSUM-EXTR-FILE                          
               ELSE                                                             
                   PERFORM 8300-PROCESS-ONLY-PRCHG-FILE                         
                   PERFORM 4400-READ-PRCHG-EXTR-FILE                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       3000-BUILD-WKLYSUM-FILE-KEY.                                             
      ******************************************************************        
           INITIALIZE PV-WKLYSUM-FILE-KEY.                                      
                                                                                
           IF ML980-WKLY-SUM-EXTRACT-RECS = HIGH-VALUES                         
               MOVE HIGH-VALUES TO PV-WKLYSUM-FILE-KEY                          
           ELSE                                                                 
               MOVE ML980-FSCL-WK-END-DTE TO PV-WKLYSUM-FSCL-WK-END-DTE         
               MOVE ML980-DEPT-NBR        TO PV-WKLYSUM-DEPT-NBR                
               MOVE ML980-MAJ-CL-NBR      TO PV-WKLYSUM-MAJ-CL-NBR              
               MOVE ML980-SUB-CL-NBR      TO PV-WKLYSUM-SUB-CL-NBR              
               MOVE ML980-LOC-NBR         TO PV-WKLYSUM-LOC-NBR                 
               MOVE ML980-ML-LO-MDSE-LVL-ID                                     
                   TO PV-WKLYSUM-ML-LO-MDSE-LVL-ID                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       3100-BUILD-INVADJ-FILE-KEY.                                              
      ******************************************************************        
           INITIALIZE PV-INVADJ-FILE-KEY.                                       
                                                                                
           IF ML981-INV-ADJ-EXTRACT-RECS = HIGH-VALUES                          
               MOVE HIGH-VALUES        TO PV-INVADJ-FILE-KEY                    
           ELSE                                                                 
               MOVE ML981-FSCL-WK-END-DTE TO PV-INVADJ-FSCL-WK-END-DTE          
               MOVE ML981-DEPT-NBR        TO PV-INVADJ-DEPT-NBR                 
               MOVE ML981-MAJ-CL-NBR      TO PV-INVADJ-MAJ-CL-NBR               
               MOVE ML981-SUB-CL-NBR      TO PV-INVADJ-SUB-CL-NBR               
               MOVE ML981-LOC-NBR         TO PV-INVADJ-LOC-NBR                  
               MOVE ML981-ML-LO-MDSE-LVL-ID                                     
                   TO PV-INVADJ-ML-LO-MDSE-LVL-ID                               
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       3200-BUILD-SALE-FILE-KEY.                                                
      ******************************************************************        
           INITIALIZE PV-SALE-FILE-KEY.                                         
                                                                                
           IF ML982-SALE-EXTRACT-RECS = HIGH-VALUES                             
               MOVE HIGH-VALUES           TO PV-SALE-FILE-KEY                   
           ELSE                                                                 
               MOVE ML982-FSCL-WK-END-DTE TO PV-SALE-FSCL-WK-END-DTE            
               MOVE ML982-DEPT-NBR        TO PV-SALE-DEPT-NBR                   
               MOVE ML982-MAJ-CL-NBR      TO PV-SALE-MAJ-CL-NBR                 
               MOVE ML982-SUB-CL-NBR      TO PV-SALE-SUB-CL-NBR                 
               MOVE ML982-LOC-NBR         TO PV-SALE-LOC-NBR                    
               MOVE ML982-ML-LO-MDSE-LVL-ID                                     
                   TO PV-SALE-ML-LO-MDSE-LVL-ID                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       3300-BUILD-PURCHADJ-FILE-KEY.                                            
      ******************************************************************        
           INITIALIZE PV-PURCHADJ-FILE-KEY.                                     
                                                                                
           IF ML983-PURCHASE-ADJ-RECS = HIGH-VALUES                             
               MOVE HIGH-VALUES           TO PV-PURCHADJ-FILE-KEY               
           ELSE                                                                 
               MOVE ML983-FSCL-WK-END-DTE TO PV-PURCHADJ-FSCL-WK-END-DTE        
               MOVE ML983-DEPT-NBR        TO PV-PURCHADJ-DEPT-NBR               
               MOVE ML983-MAJ-CL-NBR      TO PV-PURCHADJ-MAJ-CL-NBR             
               MOVE ML983-SUB-CL-NBR      TO PV-PURCHADJ-SUB-CL-NBR             
               MOVE ML983-LOC-NBR         TO PV-PURCHADJ-LOC-NBR                
               MOVE ML983-ML-LO-MDSE-LVL-ID                                     
                   TO PV-PURCHADJ-ML-LO-MDSE-LVL-ID                             
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       3400-BUILD-PRCHG-FILE-KEY.                                               
      ******************************************************************        
           INITIALIZE PV-PRCHG-FILE-KEY.                                        
                                                                                
           IF ML984-PRCHG-EXTRACT-RECS = HIGH-VALUES                            
               MOVE HIGH-VALUES           TO PV-PRCHG-FILE-KEY                  
           ELSE                                                                 
               MOVE ML984-FSCL-WK-END-DTE TO PV-PRCHG-FSCL-WK-END-DTE           
               MOVE ML984-DEPT-NBR        TO PV-PRCHG-DEPT-NBR                  
               MOVE ML984-MAJ-CL-NBR      TO PV-PRCHG-MAJ-CL-NBR                
               MOVE ML984-SUB-CL-NBR      TO PV-PRCHG-SUB-CL-NBR                
               MOVE ML984-LOC-NBR         TO PV-PRCHG-LOC-NBR                   
               MOVE ML984-ML-LO-MDSE-LVL-ID                                     
                   TO PV-PRCHG-ML-LO-MDSE-LVL-ID                                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4000-READ-WKLYSUM-EXTR-FILE.                                             
      ******************************************************************        
      *                                                                         
           INITIALIZE ML980-WKLY-SUM-EXTRACT-RECS.                              
                                                                                
           IF PS-MORE-WKLYSUM-RECS                                              
               READ WKLYSUM-EXTR-FILE                                           
                 INTO ML980-WKLY-SUM-EXTRACT-RECS                               
                   AT END                                                       
                       SET PS-NO-MORE-WKLYSUM-RECS                              
                           TO TRUE                                              
                       MOVE HIGH-VALUES                                         
                           TO ML980-WKLY-SUM-EXTRACT-RECS                       
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-WKLYSUM-RECS                                              
               ADD 1 TO PA-WKLYSUM-RECS-READ                                    
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4100-READ-INVADJ-EXTR-FILE.                                              
      ******************************************************************        
      *                                                                         
           INITIALIZE ML981-INV-ADJ-EXTRACT-RECS.                               
                                                                                
           IF PS-MORE-INVADJ-RECS                                               
               READ INVADJ-EXTR-FILE                                            
                 INTO ML981-INV-ADJ-EXTRACT-RECS                                
                   AT END                                                       
                       SET PS-NO-MORE-INVADJ-RECS                               
                           TO TRUE                                              
                       MOVE HIGH-VALUES                                         
                           TO ML981-INV-ADJ-EXTRACT-RECS                        
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-INVADJ-RECS                                               
               ADD 1 TO PA-INVADJ-RECS-READ                                     
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4200-READ-SALE-EXTR-FILE.                                                
      ******************************************************************        
      *                                                                         
           INITIALIZE ML982-SALE-EXTRACT-RECS.                                  
                                                                                
           IF PS-MORE-SALE-RECS                                                 
               READ SALE-EXTR-FILE                                              
                 INTO ML982-SALE-EXTRACT-RECS                                   
                   AT END                                                       
                       SET PS-NO-MORE-SALE-RECS                                 
                           TO TRUE                                              
                       MOVE HIGH-VALUES                                         
                           TO ML982-SALE-EXTRACT-RECS                           
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-SALE-RECS                                                 
               ADD 1 TO PA-SALE-RECS-READ                                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4300-READ-PURCHADJ-EXTR-FILE.                                            
      ******************************************************************        
      *                                                                         
           INITIALIZE ML983-PURCHASE-ADJ-RECS.                                  
                                                                                
           IF PS-MORE-PURCHADJ-RECS                                             
               READ PURCHADJ-EXTR-FILE                                          
                 INTO ML983-PURCHASE-ADJ-RECS                                   
                   AT END                                                       
                       SET PS-NO-MORE-PURCHADJ-RECS                             
                           TO TRUE                                              
                       MOVE HIGH-VALUES                                         
                           TO ML983-PURCHASE-ADJ-RECS                           
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-PURCHADJ-RECS                                             
               ADD 1 TO PA-PURCHADJ-RECS-READ                                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4400-READ-PRCHG-EXTR-FILE.                                               
      ******************************************************************        
      *                                                                         
           INITIALIZE ML984-PRCHG-EXTRACT-RECS.                                 
                                                                                
           IF PS-MORE-PRCHG-RECS                                                
               READ PRCHG-EXTR-FILE                                             
                 INTO ML984-PRCHG-EXTRACT-RECS                                  
                   AT END                                                       
                       SET PS-NO-MORE-PRCHG-RECS                                
                           TO TRUE                                              
                       MOVE HIGH-VALUES                                         
                           TO ML984-PRCHG-EXTRACT-RECS                          
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-PRCHG-RECS                                                
               ADD 1 TO PA-PRCHG-RECS-READ                                      
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5100-COMPARE-INVADJ-FILE-AMTS.                                           
      ******************************************************************        
      *                                                                         
           MOVE ML981-FSCL-WK-END-DTE   TO ML985-FSCL-WK-END-DTE.               
           MOVE ML981-DEPT-NBR          TO ML985-DEPT-NBR.                      
           MOVE ML981-MAJ-CL-NBR        TO ML985-MAJ-CL-NBR.                    
           MOVE ML981-SUB-CL-NBR        TO ML985-SUB-CL-NBR.                    
           MOVE ML981-LOC-NBR           TO ML985-LOC-NBR.                       
           MOVE ML981-ML-LO-MDSE-LVL-ID TO ML985-ML-LO-MDSE-LVL-ID.             
           MOVE ML981-TRN-TYP-CDE       TO ML985-TRN-TYP-CDE.                   
                                                                                
           IF ML981-INV-ADJ-RTL-AMT     NOT = ML980-INV-ADJ-RTL-AMT             
               MOVE ML980-INV-ADJ-RTL-AMT TO ML985-WKSUM-INVADJ-AMT             
               MOVE ML981-INV-ADJ-RTL-AMT TO ML985-AUDIT-INVADJ-AMT             
               MOVE 'INV-ADJ-RTL-AMT' TO ML985-COMPARE-FIELD-NAME               
               WRITE INVADJ-RPT-REC                                             
                   FROM ML985-INV-ADJ-REPORT-RECS                               
               ADD 1 TO PA-INVADJ-RECS-WRITTEN                                  
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5200-PROCESS-ONLY-WKLYSUM-FILE.                                          
      ******************************************************************        
      *                                                                         
           PERFORM 5210-BYPASS-WKLYSUM-0-DLRS-CHK.                              
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 5220-PROCESS-NO-INVADJ-ERR                               
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5210-BYPASS-WKLYSUM-0-DLRS-CHK.                                          
      ******************************************************************        
      *                                                                         
           IF ML980-INV-ADJ-RTL-AMT = 0                                         
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5220-PROCESS-NO-INVADJ-ERR.                                              
      ******************************************************************        
      *                                                                         
           MOVE ML980-FSCL-WK-END-DTE   TO ML985-FSCL-WK-END-DTE.               
           MOVE ML980-DEPT-NBR          TO ML985-DEPT-NBR.                      
           MOVE ML980-MAJ-CL-NBR        TO ML985-MAJ-CL-NBR.                    
           MOVE ML980-SUB-CL-NBR        TO ML985-SUB-CL-NBR.                    
           MOVE ML980-LOC-NBR           TO ML985-LOC-NBR.                       
           MOVE ML980-ML-LO-MDSE-LVL-ID TO ML985-ML-LO-MDSE-LVL-ID.             
           MOVE '080'                   TO ML985-TRN-TYP-CDE.                   
           MOVE ML980-INV-ADJ-RTL-AMT TO ML985-WKSUM-INVADJ-AMT.                
           MOVE 0                     TO ML985-AUDIT-INVADJ-AMT.                
           MOVE 'INV-ADJ-RTL-AMT' TO ML985-COMPARE-FIELD-NAME.                  
           WRITE INVADJ-RPT-REC                                                 
               FROM ML985-INV-ADJ-REPORT-RECS.                                  
           ADD 1 TO PA-INVADJ-RECS-WRITTEN.                                     
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5300-PROCESS-ONLY-INVADJ-FILE.                                           
      ******************************************************************        
      *                                                                         
           PERFORM 5310-BYPASS-INVADJ-0-DLRS-CHK.                               
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 5320-PROCESS-NO-WKSUM-RECD-ERR                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5310-BYPASS-INVADJ-0-DLRS-CHK.                                           
      ******************************************************************        
      *                                                                         
           IF ML981-INV-ADJ-RTL-AMT = 0                                         
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5320-PROCESS-NO-WKSUM-RECD-ERR.                                          
      ******************************************************************        
      *                                                                         
           MOVE ML981-FSCL-WK-END-DTE   TO ML985-FSCL-WK-END-DTE.               
           MOVE ML981-DEPT-NBR          TO ML985-DEPT-NBR.                      
           MOVE ML981-MAJ-CL-NBR        TO ML985-MAJ-CL-NBR.                    
           MOVE ML981-SUB-CL-NBR        TO ML985-SUB-CL-NBR.                    
           MOVE ML981-LOC-NBR           TO ML985-LOC-NBR.                       
           MOVE ML981-ML-LO-MDSE-LVL-ID TO ML985-ML-LO-MDSE-LVL-ID.             
           MOVE ML981-TRN-TYP-CDE       TO ML985-TRN-TYP-CDE.                   
           MOVE 0                     TO ML985-WKSUM-INVADJ-AMT.                
           MOVE ML981-INV-ADJ-RTL-AMT TO ML985-AUDIT-INVADJ-AMT.                
           MOVE 'INV-ADJ-RTL-AMT' TO ML985-COMPARE-FIELD-NAME.                  
           WRITE INVADJ-RPT-REC                                                 
               FROM ML985-INV-ADJ-REPORT-RECS.                                  
           ADD 1 TO PA-INVADJ-RECS-WRITTEN.                                     
                                                                                
      ******************************************************************        
       6100-COMPARE-SALE-FILE-AMTS.                                             
      ******************************************************************        
      *                                                                         
           MOVE ML982-FSCL-WK-END-DTE   TO ML986-FSCL-WK-END-DTE.               
           MOVE ML982-DEPT-NBR          TO ML986-DEPT-NBR.                      
           MOVE ML982-MAJ-CL-NBR        TO ML986-MAJ-CL-NBR.                    
           MOVE ML982-SUB-CL-NBR        TO ML986-SUB-CL-NBR.                    
           MOVE ML982-LOC-NBR           TO ML986-LOC-NBR.                       
           MOVE ML982-ML-LO-MDSE-LVL-ID TO ML986-ML-LO-MDSE-LVL-ID.             
           MOVE ML982-TRN-TYP-CDE       TO ML986-TRN-TYP-CDE.                   
           MOVE ML982-SLS-TYP-CDE       TO ML986-SLS-TYP-CDE.                   
                                                                                
           EVALUATE ML982-SLS-TYP-CDE                                           
               WHEN 'C'                                                         
                   IF ML982-SLS-RTL-AMT NOT = ML980-SLS-CLR-RTL-AMT             
                       MOVE ML980-SLS-CLR-RTL-AMT                               
                           TO ML986-WKSUM-SLS-AMT                               
                       MOVE ML982-SLS-RTL-AMT TO ML986-AUDIT-SLS-AMT            
                       MOVE 'SLS-CLR-RTL-AMT'                                   
                           TO ML986-COMPARE-FIELD-NAME                          
                       WRITE SALE-RPT-REC                                       
                           FROM ML986-SALE-REPORT-RECS                          
                       ADD 1 TO PA-SALE-RECS-WRITTEN                            
                   END-IF                                                       
               WHEN 'G'                                                         
                   IF ML982-SLS-RTL-AMT NOT = ML980-SLS-PROCLR-RTL-AMT          
                       MOVE ML980-SLS-PROCLR-RTL-AMT                            
                           TO ML986-WKSUM-SLS-AMT                               
                       MOVE ML982-SLS-RTL-AMT TO ML986-AUDIT-SLS-AMT            
                       MOVE 'SLS-PROCLR-RTL-AMT'                                
                           TO ML986-COMPARE-FIELD-NAME                          
                       WRITE SALE-RPT-REC                                       
                           FROM ML986-SALE-REPORT-RECS                          
                       ADD 1 TO PA-SALE-RECS-WRITTEN                            
                   END-IF                                                       
               WHEN 'P'                                                         
                   IF ML982-SLS-RTL-AMT NOT = ML980-SLS-PROMO-RTL-AMT           
                       MOVE ML980-SLS-PROMO-RTL-AMT                             
                           TO ML986-WKSUM-SLS-AMT                               
                       MOVE ML982-SLS-RTL-AMT TO ML986-AUDIT-SLS-AMT            
                       MOVE 'SLS-PROMO-RTL-AMT'                                 
                           TO ML986-COMPARE-FIELD-NAME                          
                       WRITE SALE-RPT-REC                                       
                           FROM ML986-SALE-REPORT-RECS                          
                       ADD 1 TO PA-SALE-RECS-WRITTEN                            
                   END-IF                                                       
               WHEN 'R'                                                         
                   IF ML982-SLS-RTL-AMT NOT = ML980-SLS-REG-RTL-AMT             
                       MOVE ML980-SLS-REG-RTL-AMT                               
                           TO ML986-WKSUM-SLS-AMT                               
                       MOVE ML982-SLS-RTL-AMT TO ML986-AUDIT-SLS-AMT            
                       MOVE 'SLS-REG-RTL-AMT'                                   
                           TO ML986-COMPARE-FIELD-NAME                          
                       WRITE SALE-RPT-REC                                       
                           FROM ML986-SALE-REPORT-RECS                          
                       ADD 1 TO PA-SALE-RECS-WRITTEN                            
                   END-IF                                                       
               WHEN OTHER                                                       
                   IF ML982-SLS-RTL-AMT NOT = ML980-SLS-OTH-RTL-AMT             
                       MOVE ML980-SLS-OTH-RTL-AMT                               
                           TO ML986-WKSUM-SLS-AMT                               
                       MOVE ML982-SLS-RTL-AMT TO ML986-AUDIT-SLS-AMT            
                       MOVE 'SLS-OTH-RTL-AMT'                                   
                           TO ML986-COMPARE-FIELD-NAME                          
                       WRITE SALE-RPT-REC                                       
                           FROM ML986-SALE-REPORT-RECS                          
                       ADD 1 TO PA-SALE-RECS-WRITTEN                            
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4200-READ-SALE-EXTR-FILE.                                    
           PERFORM 3200-BUILD-SALE-FILE-KEY.                                    
           IF PV-WKLYSUM-FILE-KEY NOT = PV-SALE-FILE-KEY                        
               PERFORM 4000-READ-WKLYSUM-EXTR-FILE                              
               SET PS-FILE-KEY-NOT-MATCH TO TRUE                                
           END-IF.                                                              
       EJECT                                                                    
                                                                                
      ******************************************************************        
       6200-PROCESS-ONLY-WKLYSUM-FILE.                                          
      ******************************************************************        
      *                                                                         
           PERFORM 6210-BYPASS-WKLYSUM-0-DLRS-CHK.                              
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 6220-PROCESS-NO-SALE-ERR                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       6210-BYPASS-WKLYSUM-0-DLRS-CHK.                                          
      ******************************************************************        
      *                                                                         
           IF (ML980-SLS-REG-RTL-AMT    = 0 AND                                 
               ML980-SLS-PROMO-RTL-AMT  = 0 AND                                 
               ML980-SLS-PROCLR-RTL-AMT = 0 AND                                 
               ML980-SLS-CLR-RTL-AMT    = 0 AND                                 
               ML980-SLS-OTH-RTL-AMT    = 0)                                    
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       6220-PROCESS-NO-SALE-ERR.                                                
      ******************************************************************        
      *                                                                         
           MOVE ML980-FSCL-WK-END-DTE   TO ML986-FSCL-WK-END-DTE.               
           MOVE ML980-DEPT-NBR          TO ML986-DEPT-NBR.                      
           MOVE ML980-MAJ-CL-NBR        TO ML986-MAJ-CL-NBR.                    
           MOVE ML980-SUB-CL-NBR        TO ML986-SUB-CL-NBR.                    
           MOVE ML980-LOC-NBR           TO ML986-LOC-NBR.                       
           MOVE ML980-ML-LO-MDSE-LVL-ID TO ML986-ML-LO-MDSE-LVL-ID.             
           MOVE '020'                   TO ML986-TRN-TYP-CDE.                   
           MOVE SPACES                  TO ML986-SLS-TYP-CDE.                   
           EVALUATE TRUE                                                        
               WHEN ML980-SLS-REG-RTL-AMT NOT = 0                               
                   MOVE ML980-SLS-REG-RTL-AMT TO ML986-WKSUM-SLS-AMT            
                   MOVE 'SLS-REG-RTL-AMT' TO ML986-COMPARE-FIELD-NAME           
               WHEN ML980-SLS-PROMO-RTL-AMT NOT = 0                             
                   MOVE ML980-SLS-PROMO-RTL-AMT TO ML986-WKSUM-SLS-AMT          
                   MOVE 'SLS-PROMO-RTL-AMT' TO ML986-COMPARE-FIELD-NAME         
               WHEN ML980-SLS-PROCLR-RTL-AMT NOT = 0                            
                   MOVE ML980-SLS-PROCLR-RTL-AMT TO ML986-WKSUM-SLS-AMT         
                   MOVE 'SLS-PROCLR-RTL-AMT' TO ML986-COMPARE-FIELD-NAME        
               WHEN ML980-SLS-CLR-RTL-AMT NOT = 0                               
                   MOVE ML980-SLS-CLR-RTL-AMT TO ML986-WKSUM-SLS-AMT            
                   MOVE 'SLS-CLR-RTL-AMT' TO ML986-COMPARE-FIELD-NAME           
               WHEN ML980-SLS-OTH-RTL-AMT NOT = 0                               
                   MOVE ML980-SLS-OTH-RTL-AMT TO ML986-WKSUM-SLS-AMT            
                   MOVE 'SLS-OTH-RTL-AMT' TO ML986-COMPARE-FIELD-NAME           
           END-EVALUATE.                                                        
           MOVE 0                     TO ML986-AUDIT-SLS-AMT.                   
           WRITE SALE-RPT-REC                                                   
               FROM ML986-SALE-REPORT-RECS.                                     
           ADD 1 TO PA-SALE-RECS-WRITTEN.                                       
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       6300-PROCESS-ONLY-SALE-FILE.                                             
      ******************************************************************        
      *                                                                         
           PERFORM 6310-BYPASS-SALE-0-DLRS-CHK.                                 
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 6320-PROCESS-NO-WKSUM-RECD-ERR                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       6310-BYPASS-SALE-0-DLRS-CHK.                                             
      ******************************************************************        
      *                                                                         
           IF ML982-SLS-RTL-AMT = 0                                             
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       6320-PROCESS-NO-WKSUM-RECD-ERR.                                          
      ******************************************************************        
      *                                                                         
           MOVE ML982-FSCL-WK-END-DTE   TO ML986-FSCL-WK-END-DTE.               
           MOVE ML982-DEPT-NBR          TO ML986-DEPT-NBR.                      
           MOVE ML982-MAJ-CL-NBR        TO ML986-MAJ-CL-NBR.                    
           MOVE ML982-SUB-CL-NBR        TO ML986-SUB-CL-NBR.                    
           MOVE ML982-LOC-NBR           TO ML986-LOC-NBR.                       
           MOVE ML982-ML-LO-MDSE-LVL-ID TO ML986-ML-LO-MDSE-LVL-ID.             
           MOVE ML982-TRN-TYP-CDE       TO ML986-TRN-TYP-CDE.                   
           MOVE ML982-SLS-TYP-CDE       TO ML986-SLS-TYP-CDE.                   
           MOVE 0                     TO ML986-WKSUM-SLS-AMT.                   
           MOVE ML982-SLS-RTL-AMT     TO ML986-AUDIT-SLS-AMT.                   
           EVALUATE ML982-SLS-TYP-CDE                                           
               WHEN 'C'                                                         
                   MOVE 'SLS-CLR-RTL-AMT' TO ML986-COMPARE-FIELD-NAME           
               WHEN 'G'                                                         
                   MOVE 'SLS-PROCLR-RTL-AMT'                                    
                       TO ML986-COMPARE-FIELD-NAME                              
               WHEN 'P'                                                         
                   MOVE 'SLS-PROMO-RTL-AMT' TO ML986-COMPARE-FIELD-NAME         
               WHEN 'R'                                                         
                   MOVE 'SLS-REG-RTL-AMT' TO ML986-COMPARE-FIELD-NAME           
               WHEN 'N'                                                         
               WHEN 'O'                                                         
                   MOVE 'SLS-OTH-RTL-AMT' TO ML986-COMPARE-FIELD-NAME           
           END-EVALUATE.                                                        
           WRITE SALE-RPT-REC                                                   
               FROM ML986-SALE-REPORT-RECS.                                     
           ADD 1 TO PA-SALE-RECS-WRITTEN.                                       
                                                                                
      ******************************************************************        
       7100-COMPARE-PADJ-FILE-AMTS.                                             
      ******************************************************************        
      *                                                                         
           MOVE ML983-FSCL-WK-END-DTE   TO ML987-FSCL-WK-END-DTE.               
           MOVE ML983-DEPT-NBR          TO ML987-DEPT-NBR.                      
           MOVE ML983-MAJ-CL-NBR        TO ML987-MAJ-CL-NBR.                    
           MOVE ML983-SUB-CL-NBR        TO ML987-SUB-CL-NBR.                    
           MOVE ML983-LOC-NBR           TO ML987-LOC-NBR.                       
           MOVE ML983-ML-LO-MDSE-LVL-ID TO ML987-ML-LO-MDSE-LVL-ID.             
           MOVE ML983-TRN-TYP-CDE       TO ML987-TRN-TYP-CDE.                   
           MOVE ML983-FINCL-PRC-GP-CDE  TO ML987-FINCL-PRC-GP-CDE.              
           MOVE ML983-AP-TRN-CDE        TO ML987-AP-TRN-CDE                     
                                           PV-AP-TRN-CDE.                       
           MOVE ML983-DRCR-CDE          TO ML987-DRCR-CDE.                      
                                                                                
           IF ML983-TRN-TYP-CDE = '010'                                         
               IF ML983-PADJ-GRO-COST-AMT                                       
                   NOT = ML980-RCPT-GRO-COST-AMT                                
                   MOVE ML980-RCPT-GRO-COST-AMT                                 
                       TO ML987-WKSUM-PADJ-AMT                                  
                   MOVE ML983-PADJ-GRO-COST-AMT                                 
                       TO ML987-AUDIT-PADJ-AMT                                  
                   MOVE 'RCPT-GRO-COST-AMT'                                     
                       TO ML987-COMPARE-FIELD-NAME                              
                   WRITE PURCHADJ-RPT-REC                                       
                       FROM ML987-PADJ-REPORT-RECS                              
                   ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                            
               END-IF                                                           
               IF ML983-PADJ-NET-COST-AMT                                       
                   NOT = ML980-RCPT-NET-COST-AMT                                
                   MOVE ML980-RCPT-NET-COST-AMT                                 
                       TO ML987-WKSUM-PADJ-AMT                                  
                   MOVE ML983-PADJ-NET-COST-AMT                                 
                       TO ML987-AUDIT-PADJ-AMT                                  
                   MOVE 'RCPT-NET-COST-AMT'                                     
                       TO ML987-COMPARE-FIELD-NAME                              
                   WRITE PURCHADJ-RPT-REC                                       
                       FROM ML987-PADJ-REPORT-RECS                              
                   ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                            
               END-IF                                                           
               IF ML983-PADJ-RTL-AMT NOT = ML980-RCPT-RTL-AMT                   
                   MOVE ML980-RCPT-RTL-AMT                                      
                       TO ML987-WKSUM-PADJ-AMT                                  
                   MOVE ML983-PADJ-RTL-AMT                                      
                       TO ML987-AUDIT-PADJ-AMT                                  
                   MOVE 'RCPT-RTL-AMT'                                          
                       TO ML987-COMPARE-FIELD-NAME                              
                   WRITE PURCHADJ-RPT-REC                                       
                       FROM ML987-PADJ-REPORT-RECS                              
                   ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                            
               END-IF                                                           
           ELSE                                                                 
               EVALUATE TRUE                                                    
                   WHEN PV-AP-TRN-CDE = SPACES                                  
                       IF ML983-FINCL-PRC-GP-CDE = SPACES                       
                           AND ML983-DRCR-CDE = SPACES                          
                           IF ML983-PADJ-NET-COST-AMT                           
                               NOT = ML980-PADJ-NET-COST-AMT                    
                               MOVE ML980-PADJ-NET-COST-AMT                     
                                   TO ML987-WKSUM-PADJ-AMT                      
                               MOVE ML983-PADJ-NET-COST-AMT                     
                                   TO ML987-AUDIT-PADJ-AMT                      
                               MOVE 'PADJ-NET-COST-AMT'                         
                                   TO ML987-COMPARE-FIELD-NAME                  
                               WRITE PURCHADJ-RPT-REC                           
                                   FROM ML987-PADJ-REPORT-RECS                  
                               ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                
                           END-IF                                               
                           IF ML983-PADJ-RTL-AMT                                
                               NOT = ML980-PADJ-RTL-AMT                         
                               MOVE ML980-PADJ-RTL-AMT                          
                                   TO ML987-WKSUM-PADJ-AMT                      
                               MOVE ML983-PADJ-RTL-AMT                          
                                   TO ML987-AUDIT-PADJ-AMT                      
                               MOVE 'PADJ-RTL-AMT'                              
                                   TO ML987-COMPARE-FIELD-NAME                  
                               WRITE PURCHADJ-RPT-REC                           
                                   FROM ML987-PADJ-REPORT-RECS                  
                               ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                
                           END-IF                                               
                           IF ML983-PADJ-DISC-COST-AMT                          
                               NOT = ML980-PADJ-DISC-COST-AMT                   
                               MOVE ML980-PADJ-DISC-COST-AMT                    
                                   TO ML987-WKSUM-PADJ-AMT                      
                               MOVE ML983-PADJ-DISC-COST-AMT                    
                                   TO ML987-AUDIT-PADJ-AMT                      
                               MOVE 'PADJ-DISC-COST-AMT'                        
                                   TO ML987-COMPARE-FIELD-NAME                  
                               WRITE PURCHADJ-RPT-REC                           
                                   FROM ML987-PADJ-REPORT-RECS                  
                               ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                
                           END-IF                                               
                       END-IF                                                   
                   WHEN PV-RTV-ADJ-AP-TRN-CDE                                   
                       IF ML983-PADJ-NET-COST-AMT                               
                           NOT = ML980-PADJ-RTV-COST-AMT                        
                           MOVE ML980-PADJ-RTV-COST-AMT                         
                               TO ML987-WKSUM-PADJ-AMT                          
                           MOVE ML983-PADJ-NET-COST-AMT                         
                               TO ML987-AUDIT-PADJ-AMT                          
                           MOVE 'PADJ-RTV-COST-AMT'                             
                               TO ML987-COMPARE-FIELD-NAME                      
                           WRITE PURCHADJ-RPT-REC                               
                               FROM ML987-PADJ-REPORT-RECS                      
                           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
                       END-IF                                                   
                       IF ML983-PADJ-RTL-AMT                                    
                           NOT = ML980-PADJ-RTV-RTL-AMT                         
                           MOVE ML980-PADJ-RTV-RTL-AMT                          
                               TO ML987-WKSUM-PADJ-AMT                          
                           MOVE ML983-PADJ-RTL-AMT                              
                               TO ML987-AUDIT-PADJ-AMT                          
                           MOVE 'PADJ-RTV-RTL-AMT'                              
                               TO ML987-COMPARE-FIELD-NAME                      
                           WRITE PURCHADJ-RPT-REC                               
                               FROM ML987-PADJ-REPORT-RECS                      
                           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
                       END-IF                                                   
                   WHEN PV-DA-ADJ-AP-TRN-CDE                                    
                       IF ML983-PADJ-NET-COST-AMT                               
                           NOT = ML980-PADJ-DA-COST-AMT                         
                           MOVE ML980-PADJ-DA-COST-AMT                          
                               TO ML987-WKSUM-PADJ-AMT                          
                           MOVE ML983-PADJ-NET-COST-AMT                         
                               TO ML987-AUDIT-PADJ-AMT                          
                           MOVE 'PADJ-DA-COST-AMT'                              
                               TO ML987-COMPARE-FIELD-NAME                      
                           WRITE PURCHADJ-RPT-REC                               
                               FROM ML987-PADJ-REPORT-RECS                      
                           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
                       END-IF                                                   
                       IF ML983-PADJ-RTL-AMT                                    
                           NOT = ML980-PADJ-DA-RTL-AMT                          
                           MOVE ML980-PADJ-DA-RTL-AMT                           
                               TO ML987-WKSUM-PADJ-AMT                          
                           MOVE ML983-PADJ-RTL-AMT                              
                               TO ML987-AUDIT-PADJ-AMT                          
                           MOVE 'PADJ-DA-RTL-AMT'                               
                               TO ML987-COMPARE-FIELD-NAME                      
                           WRITE PURCHADJ-RPT-REC                               
                               FROM ML987-PADJ-REPORT-RECS                      
                           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
                       END-IF                                                   
      *            WHEN OTHER                                                   
      *                MOVE ML983-PADJ-GRO-COST-AMT                             
      *                    TO ML987-AUDIT-PADJ-AMT                              
      *                ADD ML983-PADJ-DISC-COST-AMT                             
      *                    TO ML987-AUDIT-PADJ-AMT                              
      *                IF ML987-AUDIT-PADJ-AMT                                  
      *                    NOT = ML980-PADJ-OTH-COST-AMT                        
      *                    MOVE ML980-PADJ-OTH-COST-AMT                         
      *                        TO ML987-WKSUM-PADJ-AMT                          
      *                    MOVE 'PADJ-OTH-COST-AMT'                             
      *                        TO ML987-COMPARE-FIELD-NAME                      
      *                    WRITE PURCHADJ-RPT-REC                               
      *                        FROM ML987-PADJ-REPORT-RECS                      
      *                    ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
      *                END-IF                                                   
      *                IF ML983-PADJ-RTL-AMT                                    
      *                    NOT = ML980-PADJ-OTH-RTL-AMT                         
      *                    MOVE ML980-PADJ-OTH-RTL-AMT                          
      *                        TO ML987-WKSUM-PADJ-AMT                          
      *                    MOVE ML983-PADJ-RTL-AMT                              
      *                        TO ML987-AUDIT-PADJ-AMT                          
      *                    MOVE 'PADJ-OTH-RTL-AMT'                              
      *                        TO ML987-COMPARE-FIELD-NAME                      
      *                    WRITE PURCHADJ-RPT-REC                               
      *                        FROM ML987-PADJ-REPORT-RECS                      
      *                    ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
      *                END-IF                                                   
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
           PERFORM 4300-READ-PURCHADJ-EXTR-FILE.                                
           PERFORM 3300-BUILD-PURCHADJ-FILE-KEY.                                
           IF PV-WKLYSUM-FILE-KEY NOT = PV-PURCHADJ-FILE-KEY                    
               PERFORM 4000-READ-WKLYSUM-EXTR-FILE                              
               SET PS-FILE-KEY-NOT-MATCH TO TRUE                                
           END-IF.                                                              
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7200-PROCESS-ONLY-WKLYSUM-FILE.                                          
      ******************************************************************        
      *                                                                         
           PERFORM 7210-BYPASS-WKLYSUM-0-DLRS-CHK.                              
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 7220-PROCESS-NO-PURCHADJ-ERR                             
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7210-BYPASS-WKLYSUM-0-DLRS-CHK.                                          
      ******************************************************************        
      *                                                                         
           IF (ML980-RCPT-RTL-AMT       = 0 AND                                 
               ML980-RCPT-NET-COST-AMT  = 0 AND                                 
               ML980-RCPT-GRO-COST-AMT  = 0 AND                                 
               ML980-PADJ-RTL-AMT       = 0 AND                                 
               ML980-PADJ-NET-COST-AMT  = 0 AND                                 
               ML980-PADJ-DISC-COST-AMT = 0 AND                                 
               ML980-PADJ-RTV-COST-AMT  = 0 AND                                 
               ML980-PADJ-RTV-RTL-AMT   = 0 AND                                 
               ML980-PADJ-DA-COST-AMT   = 0 AND                                 
               ML980-PADJ-DA-RTL-AMT    = 0 AND                                 
               ML980-PADJ-OTH-COST-AMT  = 0 AND                                 
               ML980-PADJ-OTH-RTL-AMT   = 0)                                    
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7220-PROCESS-NO-PURCHADJ-ERR.                                            
      ******************************************************************        
      *                                                                         
                                                                                
P10681     INITIALIZE   ML987-PADJ-REPORT-RECS.                                 
                                                                                
           MOVE ML980-FSCL-WK-END-DTE   TO ML987-FSCL-WK-END-DTE.               
           MOVE ML980-DEPT-NBR          TO ML987-DEPT-NBR.                      
           MOVE ML980-MAJ-CL-NBR        TO ML987-MAJ-CL-NBR.                    
           MOVE ML980-SUB-CL-NBR        TO ML987-SUB-CL-NBR.                    
           MOVE ML980-LOC-NBR           TO ML987-LOC-NBR.                       
           MOVE ML980-ML-LO-MDSE-LVL-ID TO ML987-ML-LO-MDSE-LVL-ID.             
           MOVE SPACES                  TO ML987-FINCL-PRC-GP-CDE               
                                           ML987-AP-TRN-CDE                     
                                           ML987-DRCR-CDE.                      
           EVALUATE TRUE                                                        
               WHEN ML980-RCPT-RTL-AMT NOT = 0                                  
                   MOVE '010' TO ML987-TRN-TYP-CDE                              
                   MOVE ML980-RCPT-RTL-AMT TO ML987-WKSUM-PADJ-AMT              
                   MOVE 'RCPT-RTL-AMT' TO ML987-COMPARE-FIELD-NAME              
               WHEN ML980-RCPT-NET-COST-AMT NOT = 0                             
                   MOVE '010' TO ML987-TRN-TYP-CDE                              
                   MOVE ML980-RCPT-NET-COST-AMT TO ML987-WKSUM-PADJ-AMT         
                   MOVE 'RCPT-NET-COST-AMT' TO ML987-COMPARE-FIELD-NAME         
               WHEN ML980-RCPT-GRO-COST-AMT NOT = 0                             
                   MOVE '010' TO ML987-TRN-TYP-CDE                              
                   MOVE ML980-RCPT-GRO-COST-AMT TO ML987-WKSUM-PADJ-AMT         
                   MOVE 'RCPT-GRO-COST-AMT' TO ML987-COMPARE-FIELD-NAME         
               WHEN ML980-PADJ-RTL-AMT NOT = 0                                  
                   MOVE '015' TO ML987-TRN-TYP-CDE                              
                   MOVE ML980-PADJ-RTL-AMT TO ML987-WKSUM-PADJ-AMT              
                   MOVE 'PADJ-RTL-AMT' TO ML987-COMPARE-FIELD-NAME              
               WHEN ML980-PADJ-NET-COST-AMT NOT = 0                             
                   MOVE '015' TO ML987-TRN-TYP-CDE                              
                   MOVE ML980-PADJ-NET-COST-AMT TO ML987-WKSUM-PADJ-AMT         
                   MOVE 'PADJ-NET-COST-AMT' TO ML987-COMPARE-FIELD-NAME         
               WHEN ML980-PADJ-DISC-COST-AMT NOT = 0                            
                   MOVE '015' TO ML987-TRN-TYP-CDE                              
                   MOVE ML980-PADJ-DISC-COST-AMT TO ML987-WKSUM-PADJ-AMT        
                   MOVE 'PADJ-DISC-COST-AMT' TO ML987-COMPARE-FIELD-NAME        
               WHEN ML980-PADJ-RTV-COST-AMT NOT = 0                             
                   MOVE '015' TO ML987-TRN-TYP-CDE                              
                   MOVE ML980-PADJ-RTV-COST-AMT TO ML987-WKSUM-PADJ-AMT         
                   MOVE 'PADJ-RTV-COST-AMT' TO ML987-COMPARE-FIELD-NAME         
               WHEN ML980-PADJ-RTV-RTL-AMT NOT = 0                              
                   MOVE '015' TO ML987-TRN-TYP-CDE                              
                   MOVE ML980-PADJ-RTV-RTL-AMT TO ML987-WKSUM-PADJ-AMT          
                   MOVE 'PADJ-RTV-RTL-AMT' TO ML987-COMPARE-FIELD-NAME          
               WHEN ML980-PADJ-DA-COST-AMT NOT = 0                              
                   MOVE '015' TO ML987-TRN-TYP-CDE                              
                   MOVE ML980-PADJ-DA-COST-AMT TO ML987-WKSUM-PADJ-AMT          
                   MOVE 'PADJ-DA-COST-AMT' TO ML987-COMPARE-FIELD-NAME          
               WHEN ML980-PADJ-DA-RTL-AMT NOT = 0                               
                   MOVE '015' TO ML987-TRN-TYP-CDE                              
                   MOVE ML980-PADJ-DA-RTL-AMT TO ML987-WKSUM-PADJ-AMT           
                   MOVE 'PADJ-DA-RTL-AMT' TO ML987-COMPARE-FIELD-NAME           
P46161         WHEN ML980-PADJ-OTH-COST-AMT NOT = 0                             
P46161             MOVE '015' TO ML987-TRN-TYP-CDE                              
P46161             MOVE ML980-PADJ-OTH-COST-AMT TO ML987-WKSUM-PADJ-AMT         
P46161             MOVE 'PADJ-OTH-COST-AMT' TO ML987-COMPARE-FIELD-NAME         
P46161         WHEN ML980-PADJ-OTH-RTL-AMT NOT = 0                              
P46161             MOVE '015' TO ML987-TRN-TYP-CDE                              
P46161             MOVE ML980-PADJ-OTH-RTL-AMT TO ML987-WKSUM-PADJ-AMT          
P46161             MOVE 'PADJ-OTH-RTL-AMT' TO ML987-COMPARE-FIELD-NAME          
           END-EVALUATE.                                                        
           MOVE 0                     TO ML987-AUDIT-PADJ-AMT.                  
           WRITE PURCHADJ-RPT-REC                                               
               FROM ML987-PADJ-REPORT-RECS.                                     
           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN.                                   
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7300-PROCESS-ONLY-PADJ-FILE.                                             
      ******************************************************************        
      *                                                                         
           PERFORM 7310-BYPASS-PADJ-0-DLRS-CHK.                                 
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 7320-PROCESS-NO-WKSUM-RECD-ERR                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7310-BYPASS-PADJ-0-DLRS-CHK.                                             
      ******************************************************************        
      *                                                                         
           IF (ML983-PADJ-RTL-AMT = 0 AND                                       
               ML983-PADJ-GRO-COST-AMT = 0 AND                                  
               ML983-PADJ-NET-COST-AMT = 0 AND                                  
               ML983-PADJ-DISC-COST-AMT = 0)                                    
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7320-PROCESS-NO-WKSUM-RECD-ERR.                                          
      ******************************************************************        
      *                                                                         
           MOVE ML983-FSCL-WK-END-DTE   TO ML987-FSCL-WK-END-DTE.               
           MOVE ML983-DEPT-NBR          TO ML987-DEPT-NBR.                      
           MOVE ML983-MAJ-CL-NBR        TO ML987-MAJ-CL-NBR.                    
           MOVE ML983-SUB-CL-NBR        TO ML987-SUB-CL-NBR.                    
           MOVE ML983-LOC-NBR           TO ML987-LOC-NBR.                       
           MOVE ML983-ML-LO-MDSE-LVL-ID TO ML987-ML-LO-MDSE-LVL-ID.             
           MOVE ML983-TRN-TYP-CDE       TO ML987-TRN-TYP-CDE.                   
           MOVE ML983-FINCL-PRC-GP-CDE  TO ML987-FINCL-PRC-GP-CDE.              
           MOVE ML983-AP-TRN-CDE        TO ML987-AP-TRN-CDE                     
                                           PV-AP-TRN-CDE.                       
           MOVE ML983-DRCR-CDE          TO ML987-DRCR-CDE.                      
           MOVE 0                     TO ML987-WKSUM-PADJ-AMT.                  
           EVALUATE TRUE                                                        
               WHEN PV-AP-TRN-CDE = SPACES                                      
                   IF ML983-TRN-TYP-CDE = '010'                                 
                       IF ML983-PADJ-RTL-AMT NOT = 0                            
                           MOVE 'RCPT-RTL-AMT'                                  
                               TO ML987-COMPARE-FIELD-NAME                      
                           MOVE ML983-PADJ-RTL-AMT                              
                               TO ML987-AUDIT-PADJ-AMT                          
                           WRITE PURCHADJ-RPT-REC                               
                               FROM ML987-PADJ-REPORT-RECS                      
                           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
                       END-IF                                                   
                       IF ML983-PADJ-NET-COST-AMT NOT = 0                       
                           MOVE 'RCPT-NET-COST-AMT'                             
                               TO ML987-COMPARE-FIELD-NAME                      
                           MOVE ML983-PADJ-NET-COST-AMT                         
                               TO ML987-AUDIT-PADJ-AMT                          
                           WRITE PURCHADJ-RPT-REC                               
                               FROM ML987-PADJ-REPORT-RECS                      
                           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
                       END-IF                                                   
                       IF ML983-PADJ-GRO-COST-AMT NOT = 0                       
                           MOVE 'RCPT-GRO-COST-AMT'                             
                               TO ML987-COMPARE-FIELD-NAME                      
                           MOVE ML983-PADJ-GRO-COST-AMT                         
                               TO ML987-AUDIT-PADJ-AMT                          
                           WRITE PURCHADJ-RPT-REC                               
                               FROM ML987-PADJ-REPORT-RECS                      
                           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
                       END-IF                                                   
                   ELSE                                                         
                       IF ML983-PADJ-RTL-AMT NOT = 0                            
                           MOVE 'PADJ-RTL-AMT'                                  
                               TO ML987-COMPARE-FIELD-NAME                      
                           MOVE ML983-PADJ-RTL-AMT                              
                               TO ML987-AUDIT-PADJ-AMT                          
                           WRITE PURCHADJ-RPT-REC                               
                               FROM ML987-PADJ-REPORT-RECS                      
                           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
                       END-IF                                                   
                       IF ML983-PADJ-NET-COST-AMT NOT = 0                       
                           MOVE 'PADJ-NET-COST-AMT'                             
                               TO ML987-COMPARE-FIELD-NAME                      
                           MOVE ML983-PADJ-NET-COST-AMT                         
                               TO ML987-AUDIT-PADJ-AMT                          
                           WRITE PURCHADJ-RPT-REC                               
                               FROM ML987-PADJ-REPORT-RECS                      
                           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
                       END-IF                                                   
                       IF ML983-PADJ-DISC-COST-AMT NOT = 0                      
                           MOVE 'PADJ-DISC-COST-AMT'                            
                               TO ML987-COMPARE-FIELD-NAME                      
                           MOVE ML983-PADJ-DISC-COST-AMT                        
                               TO ML987-AUDIT-PADJ-AMT                          
                           WRITE PURCHADJ-RPT-REC                               
                               FROM ML987-PADJ-REPORT-RECS                      
                           ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                    
                       END-IF                                                   
                   END-IF                                                       
               WHEN PV-RTV-ADJ-AP-TRN-CDE                                       
                   IF ML983-PADJ-RTL-AMT NOT = 0                                
                       MOVE 'PADJ-RTV-RTL-AMT'                                  
                           TO ML987-COMPARE-FIELD-NAME                          
                       MOVE ML983-PADJ-RTL-AMT                                  
                           TO ML987-AUDIT-PADJ-AMT                              
                       WRITE PURCHADJ-RPT-REC                                   
                           FROM ML987-PADJ-REPORT-RECS                          
                       ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                        
                   END-IF                                                       
                   IF ML983-PADJ-NET-COST-AMT NOT = 0                           
                       MOVE 'PADJ-RTV-COST-AMT'                                 
                           TO ML987-COMPARE-FIELD-NAME                          
                       MOVE ML983-PADJ-NET-COST-AMT                             
                           TO ML987-AUDIT-PADJ-AMT                              
                       WRITE PURCHADJ-RPT-REC                                   
                           FROM ML987-PADJ-REPORT-RECS                          
                       ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                        
                   END-IF                                                       
               WHEN PV-DA-ADJ-AP-TRN-CDE                                        
                   IF ML983-PADJ-RTL-AMT NOT = 0                                
                       MOVE 'PADJ-DA-RTL-AMT'                                   
                           TO ML987-COMPARE-FIELD-NAME                          
                       MOVE ML983-PADJ-RTL-AMT                                  
                           TO ML987-AUDIT-PADJ-AMT                              
                       WRITE PURCHADJ-RPT-REC                                   
                           FROM ML987-PADJ-REPORT-RECS                          
                       ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                        
                   END-IF                                                       
                   IF ML983-PADJ-NET-COST-AMT NOT = 0                           
                       MOVE 'PADJ-DA-COST-AMT'                                  
                           TO ML987-COMPARE-FIELD-NAME                          
                       MOVE ML983-PADJ-NET-COST-AMT                             
                           TO ML987-AUDIT-PADJ-AMT                              
                       WRITE PURCHADJ-RPT-REC                                   
                           FROM ML987-PADJ-REPORT-RECS                          
                       ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                        
                   END-IF                                                       
      *        WHEN OTHER                                                       
      *            IF ML983-PADJ-RTL-AMT NOT = 0                                
      *                MOVE 'PADJ-OTH-RTL-AMT'                                  
      *                    TO ML987-COMPARE-FIELD-NAME                          
      *                MOVE ML983-PADJ-RTL-AMT                                  
      *                    TO ML987-AUDIT-PADJ-AMT                              
      *                WRITE PURCHADJ-RPT-REC                                   
      *                    FROM ML987-PADJ-REPORT-RECS                          
      *                ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                        
      *            END-IF                                                       
      *            IF ML983-PADJ-NET-COST-AMT NOT = 0                           
      *                MOVE 'PADJ-OTH-COST-AMT'                                 
      *                    TO ML987-COMPARE-FIELD-NAME                          
      *                MOVE ML983-PADJ-GRO-COST-AMT                             
      *                    TO ML987-AUDIT-PADJ-AMT                              
      *                ADD ML983-PADJ-DISC-COST-AMT                             
      *                    TO ML987-AUDIT-PADJ-AMT                              
      *                WRITE PURCHADJ-RPT-REC                                   
      *                    FROM ML987-PADJ-REPORT-RECS                          
      *                ADD 1 TO PA-PURCHADJ-RECS-WRITTEN                        
      *            END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
       8100-COMPARE-PRCHG-FILE-AMTS.                                            
      ******************************************************************        
      *                                                                         
           MOVE ML984-FSCL-WK-END-DTE   TO ML988-FSCL-WK-END-DTE.               
           MOVE ML984-DEPT-NBR          TO ML988-DEPT-NBR.                      
           MOVE ML984-MAJ-CL-NBR        TO ML988-MAJ-CL-NBR.                    
           MOVE ML984-SUB-CL-NBR        TO ML988-SUB-CL-NBR.                    
           MOVE ML984-LOC-NBR           TO ML988-LOC-NBR.                       
           MOVE ML984-ML-LO-MDSE-LVL-ID TO ML988-ML-LO-MDSE-LVL-ID.             
           MOVE ML984-TRN-TYP-CDE       TO ML988-TRN-TYP-CDE.                   
           MOVE ML984-PRCHG-RSN-CDE     TO ML988-PRCHG-RSN-CDE.                 
           MOVE ML984-PRCHG-CTG-CDE     TO ML988-PRCHG-CTG-CDE.                 
                                                                                
           EVALUATE ML984-TRN-TYP-CDE                                           
               WHEN '050'                                                       
                   IF ML984-PRCHG-RTL-AMT                                       
                       NOT = ML980-MKUP-RTL-AMT                                 
                       MOVE ML980-MKUP-RTL-AMT                                  
                           TO ML988-WKSUM-PRCHG-AMT                             
                       MOVE ML984-PRCHG-RTL-AMT                                 
                           TO ML988-AUDIT-PRCHG-AMT                             
                       MOVE 'MKUP-RTL-AMT'                                      
                           TO ML988-COMPARE-FIELD-NAME                          
                       WRITE PRCHG-RPT-REC                                      
                           FROM ML988-PRCHG-REPORT-RECS                         
                       ADD 1 TO PA-PRCHG-RECS-WRITTEN                           
                   END-IF                                                       
               WHEN '040'                                                       
                   EVALUATE ML984-FIELD-TYPE                                    
                       WHEN SPACES                                              
                           IF ML984-PRCHG-RTL-AMT                               
                               NOT = ML980-MKDN-RTL-AMT                         
                               MOVE ML980-MKDN-RTL-AMT                          
                                   TO ML988-WKSUM-PRCHG-AMT                     
                               MOVE ML984-PRCHG-RTL-AMT                         
                                   TO ML988-AUDIT-PRCHG-AMT                     
                               MOVE 'MKDN-RTL-AMT'                              
                                   TO ML988-COMPARE-FIELD-NAME                  
                               WRITE PRCHG-RPT-REC                              
                                   FROM ML988-PRCHG-REPORT-RECS                 
                               ADD 1 TO PA-PRCHG-RECS-WRITTEN                   
                           END-IF                                               
                       WHEN '1'                                                 
                           IF ML984-PRCHG-RTL-AMT                               
                               NOT = ML980-MKDN-PLU-RTL-AMT                     
                               MOVE ML980-MKDN-PLU-RTL-AMT                      
                                   TO ML988-WKSUM-PRCHG-AMT                     
                               MOVE ML984-PRCHG-RTL-AMT                         
                                   TO ML988-AUDIT-PRCHG-AMT                     
                               MOVE 'MKDN-PLU-RTL-AMT'                          
                                   TO ML988-COMPARE-FIELD-NAME                  
                               WRITE PRCHG-RPT-REC                              
                                   FROM ML988-PRCHG-REPORT-RECS                 
                               ADD 1 TO PA-PRCHG-RECS-WRITTEN                   
                           END-IF                                               
                       WHEN '2'                                                 
                           IF ML984-PRCHG-RTL-AMT                               
                               NOT = ML980-MKDN-PROMO-RTL-AMT                   
                               MOVE ML980-MKDN-PROMO-RTL-AMT                    
                                   TO ML988-WKSUM-PRCHG-AMT                     
                               MOVE ML984-PRCHG-RTL-AMT                         
                                   TO ML988-AUDIT-PRCHG-AMT                     
                               MOVE 'MKDN-PROMO-RTL-AMT'                        
                                   TO ML988-COMPARE-FIELD-NAME                  
                               WRITE PRCHG-RPT-REC                              
                                   FROM ML988-PRCHG-REPORT-RECS                 
                               ADD 1 TO PA-PRCHG-RECS-WRITTEN                   
                           END-IF                                               
                       WHEN '3'                                                 
                           IF ML984-PRCHG-RTL-AMT                               
                               NOT = ML980-MKDN-RGST-RTL-AMT                    
                               MOVE ML980-MKDN-RGST-RTL-AMT                     
                                   TO ML988-WKSUM-PRCHG-AMT                     
                               MOVE ML984-PRCHG-RTL-AMT                         
                                   TO ML988-AUDIT-PRCHG-AMT                     
                               MOVE 'MKDN-RGST-RTL-AMT'                         
                                   TO ML988-COMPARE-FIELD-NAME                  
                               WRITE PRCHG-RPT-REC                              
                                   FROM ML988-PRCHG-REPORT-RECS                 
                               ADD 1 TO PA-PRCHG-RECS-WRITTEN                   
                           END-IF                                               
                       WHEN '4'                                                 
                           IF ML984-PRCHG-RTL-AMT                               
                               NOT = ML980-MKDN-GLDST-RTL-AMT                   
                               MOVE ML980-MKDN-GLDST-RTL-AMT                    
                                   TO ML988-WKSUM-PRCHG-AMT                     
                               MOVE ML984-PRCHG-RTL-AMT                         
                                   TO ML988-AUDIT-PRCHG-AMT                     
                               MOVE 'MKDN-GLDST-RTL-AMT'                        
                                   TO ML988-COMPARE-FIELD-NAME                  
                               WRITE PRCHG-RPT-REC                              
                                   FROM ML988-PRCHG-REPORT-RECS                 
                               ADD 1 TO PA-PRCHG-RECS-WRITTEN                   
                           END-IF                                               
                       WHEN '5'                                                 
                           IF ML984-PRCHG-RTL-AMT                               
                               NOT = ML980-MKDN-UMTCH-RTL-AMT                   
                               MOVE ML980-MKDN-UMTCH-RTL-AMT                    
                                   TO ML988-WKSUM-PRCHG-AMT                     
                               MOVE ML984-PRCHG-RTL-AMT                         
                                   TO ML988-AUDIT-PRCHG-AMT                     
                               MOVE 'MKDN-UMTCH-RTL-AMT'                        
                                   TO ML988-COMPARE-FIELD-NAME                  
                               WRITE PRCHG-RPT-REC                              
                                   FROM ML988-PRCHG-REPORT-RECS                 
                               ADD 1 TO PA-PRCHG-RECS-WRITTEN                   
                           END-IF                                               
                       WHEN '6'                                                 
                           IF ML984-PRCHG-RTL-AMT                               
                               NOT = ML980-MKDN-BYR-RTL-AMT                     
                               MOVE ML980-MKDN-BYR-RTL-AMT                      
                                   TO ML988-WKSUM-PRCHG-AMT                     
                               MOVE ML984-PRCHG-RTL-AMT                         
                                   TO ML988-AUDIT-PRCHG-AMT                     
                               MOVE 'MKDN-BYR-RTL-AMT'                          
                                   TO ML988-COMPARE-FIELD-NAME                  
                               WRITE PRCHG-RPT-REC                              
                                   FROM ML988-PRCHG-REPORT-RECS                 
                               ADD 1 TO PA-PRCHG-RECS-WRITTEN                   
                           END-IF                                               
                       WHEN '7'                                                 
                           IF ML984-PRCHG-RTL-AMT                               
                               NOT = ML980-MKDN-STR-RTL-AMT                     
                               MOVE ML980-MKDN-STR-RTL-AMT                      
                                   TO ML988-WKSUM-PRCHG-AMT                     
                               MOVE ML984-PRCHG-RTL-AMT                         
                                   TO ML988-AUDIT-PRCHG-AMT                     
                               MOVE 'MKDN-STR-RTL-AMT'                          
                                   TO ML988-COMPARE-FIELD-NAME                  
                               WRITE PRCHG-RPT-REC                              
                                   FROM ML988-PRCHG-REPORT-RECS                 
                               ADD 1 TO PA-PRCHG-RECS-WRITTEN                   
                           END-IF                                               
                       WHEN '8'                                                 
                           IF ML984-PRCHG-RTL-AMT                               
                               NOT = ML980-MKDN-PRPT-RTL-AMT                    
                               MOVE ML980-MKDN-PRPT-RTL-AMT                     
                                   TO ML988-WKSUM-PRCHG-AMT                     
                               MOVE ML984-PRCHG-RTL-AMT                         
                                   TO ML988-AUDIT-PRCHG-AMT                     
                               MOVE 'MKDN-PRPT-RTL-AMT'                         
                                   TO ML988-COMPARE-FIELD-NAME                  
                               WRITE PRCHG-RPT-REC                              
                                   FROM ML988-PRCHG-REPORT-RECS                 
                               ADD 1 TO PA-PRCHG-RECS-WRITTEN                   
                           END-IF                                               
                       WHEN '9'                                                 
                           IF ML984-PRCHG-RTL-AMT                               
                               NOT = ML980-MKDN-OTH-RTL-AMT                     
                               MOVE ML980-MKDN-OTH-RTL-AMT                      
                                   TO ML988-WKSUM-PRCHG-AMT                     
                               MOVE ML984-PRCHG-RTL-AMT                         
                                   TO ML988-AUDIT-PRCHG-AMT                     
                               MOVE 'MKDN-OTH-RTL-AMT'                          
                                   TO ML988-COMPARE-FIELD-NAME                  
                               WRITE PRCHG-RPT-REC                              
                                   FROM ML988-PRCHG-REPORT-RECS                 
                               ADD 1 TO PA-PRCHG-RECS-WRITTEN                   
                           END-IF                                               
                   END-EVALUATE                                                 
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4400-READ-PRCHG-EXTR-FILE.                                   
           PERFORM 3400-BUILD-PRCHG-FILE-KEY.                                   
           IF PV-WKLYSUM-FILE-KEY NOT = PV-PRCHG-FILE-KEY                       
               PERFORM 4000-READ-WKLYSUM-EXTR-FILE                              
               SET PS-FILE-KEY-NOT-MATCH TO TRUE                                
           END-IF.                                                              
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8200-PROCESS-ONLY-WKLYSUM-FILE.                                          
      ******************************************************************        
      *                                                                         
           PERFORM 8210-BYPASS-WKLYSUM-0-DLRS-CHK.                              
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 8220-PROCESS-NO-PRCHG-ERR                                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8210-BYPASS-WKLYSUM-0-DLRS-CHK.                                          
      ******************************************************************        
      *                                                                         
           IF (ML980-MKDN-PLU-RTL-AMT   = 0 AND                                 
               ML980-MKDN-PROMO-RTL-AMT = 0 AND                                 
               ML980-MKDN-RGST-RTL-AMT  = 0 AND                                 
               ML980-MKDN-GLDST-RTL-AMT = 0 AND                                 
               ML980-MKDN-UMTCH-RTL-AMT = 0 AND                                 
               ML980-MKDN-BYR-RTL-AMT   = 0 AND                                 
               ML980-MKDN-STR-RTL-AMT   = 0 AND                                 
               ML980-MKDN-PRPT-RTL-AMT  = 0 AND                                 
               ML980-MKDN-OTH-RTL-AMT   = 0 AND                                 
               ML980-MKDN-RTL-AMT       = 0 AND                                 
               ML980-MKUP-RTL-AMT       = 0)                                    
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8220-PROCESS-NO-PRCHG-ERR.                                               
      ******************************************************************        
      *                                                                         
           MOVE ML980-FSCL-WK-END-DTE   TO ML988-FSCL-WK-END-DTE.               
           MOVE ML980-DEPT-NBR          TO ML988-DEPT-NBR.                      
           MOVE ML980-MAJ-CL-NBR        TO ML988-MAJ-CL-NBR.                    
           MOVE ML980-SUB-CL-NBR        TO ML988-SUB-CL-NBR.                    
           MOVE ML980-LOC-NBR           TO ML988-LOC-NBR.                       
           MOVE ML980-ML-LO-MDSE-LVL-ID TO ML988-ML-LO-MDSE-LVL-ID.             
           MOVE SPACES                  TO ML988-PRCHG-RSN-CDE                  
                                           ML988-PRCHG-CTG-CDE.                 
           EVALUATE TRUE                                                        
               WHEN ML980-MKUP-RTL-AMT NOT = 0                                  
                   MOVE '050' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-RCPT-RTL-AMT TO ML988-WKSUM-PRCHG-AMT             
                   MOVE 'RCPT-RTL-AMT' TO ML988-COMPARE-FIELD-NAME              
               WHEN ML980-MKDN-RTL-AMT NOT = 0                                  
                   MOVE '040' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-MKDN-RTL-AMT TO ML988-WKSUM-PRCHG-AMT             
                   MOVE 'MKDN-RTL-AMT' TO ML988-COMPARE-FIELD-NAME              
               WHEN ML980-MKDN-PLU-RTL-AMT NOT = 0                              
                   MOVE '040' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-MKDN-PLU-RTL-AMT TO ML988-WKSUM-PRCHG-AMT         
                   MOVE 'MKDN-PLU-RTL-AMT' TO ML988-COMPARE-FIELD-NAME          
               WHEN ML980-MKDN-PROMO-RTL-AMT NOT = 0                            
                   MOVE '040' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-MKDN-PROMO-RTL-AMT                                
                       TO ML988-WKSUM-PRCHG-AMT                                 
                   MOVE 'MKDN-PROMO-RTL-AMT' TO ML988-COMPARE-FIELD-NAME        
               WHEN ML980-MKDN-RGST-RTL-AMT NOT = 0                             
                   MOVE '040' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-MKDN-RGST-RTL-AMT TO ML988-WKSUM-PRCHG-AMT        
                   MOVE 'MKDN-RGST-RTL-AMT' TO ML988-COMPARE-FIELD-NAME         
               WHEN ML980-MKDN-GLDST-RTL-AMT NOT = 0                            
                   MOVE '040' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-MKDN-GLDST-RTL-AMT                                
                       TO ML988-WKSUM-PRCHG-AMT                                 
                   MOVE 'MKDN-GLDST-RTL-AMT' TO ML988-COMPARE-FIELD-NAME        
               WHEN ML980-MKDN-UMTCH-RTL-AMT NOT = 0                            
                   MOVE '040' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-MKDN-UMTCH-RTL-AMT                                
                       TO ML988-WKSUM-PRCHG-AMT                                 
                   MOVE 'MKDN-UMTCH-RTL-AMT' TO ML988-COMPARE-FIELD-NAME        
               WHEN ML980-MKDN-BYR-RTL-AMT NOT = 0                              
                   MOVE '040' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-MKDN-BYR-RTL-AMT TO ML988-WKSUM-PRCHG-AMT         
                   MOVE 'MKDN-BYR-RTL-AMT' TO ML988-COMPARE-FIELD-NAME          
               WHEN ML980-MKDN-STR-RTL-AMT NOT = 0                              
                   MOVE '040' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-MKDN-STR-RTL-AMT TO ML988-WKSUM-PRCHG-AMT         
                   MOVE 'MKDN-STR-RTL-AMT' TO ML988-COMPARE-FIELD-NAME          
               WHEN ML980-MKDN-PRPT-RTL-AMT NOT = 0                             
                   MOVE '040' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-MKDN-PRPT-RTL-AMT TO ML988-WKSUM-PRCHG-AMT        
                   MOVE 'MKDN-PRPT-RTL-AMT' TO ML988-COMPARE-FIELD-NAME         
               WHEN ML980-MKDN-OTH-RTL-AMT NOT = 0                              
                   MOVE '040' TO ML988-TRN-TYP-CDE                              
                   MOVE ML980-MKDN-OTH-RTL-AMT TO ML988-WKSUM-PRCHG-AMT         
                   MOVE 'MKDN-OTH-RTL-AMT' TO ML988-COMPARE-FIELD-NAME          
           END-EVALUATE.                                                        
           MOVE 0                     TO ML988-AUDIT-PRCHG-AMT.                 
           WRITE PRCHG-RPT-REC                                                  
               FROM ML988-PRCHG-REPORT-RECS.                                    
           ADD 1 TO PA-PRCHG-RECS-WRITTEN.                                      
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8300-PROCESS-ONLY-PRCHG-FILE.                                            
      ******************************************************************        
      *                                                                         
           PERFORM 8310-BYPASS-PRCHG-0-DLRS-CHK.                                
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 8320-PROCESS-NO-WKSUM-RECD-ERR                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8310-BYPASS-PRCHG-0-DLRS-CHK.                                            
      ******************************************************************        
      *                                                                         
           IF ML984-PRCHG-RTL-AMT = 0                                           
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8320-PROCESS-NO-WKSUM-RECD-ERR.                                          
      ******************************************************************        
      *                                                                         
           MOVE ML984-FSCL-WK-END-DTE   TO ML988-FSCL-WK-END-DTE.               
           MOVE ML984-DEPT-NBR          TO ML988-DEPT-NBR.                      
           MOVE ML984-MAJ-CL-NBR        TO ML988-MAJ-CL-NBR.                    
           MOVE ML984-SUB-CL-NBR        TO ML988-SUB-CL-NBR.                    
           MOVE ML984-LOC-NBR           TO ML988-LOC-NBR.                       
           MOVE ML984-ML-LO-MDSE-LVL-ID TO ML988-ML-LO-MDSE-LVL-ID.             
           MOVE ML984-TRN-TYP-CDE       TO ML988-TRN-TYP-CDE.                   
           MOVE ML984-PRCHG-RSN-CDE     TO ML988-PRCHG-RSN-CDE.                 
           MOVE ML984-PRCHG-CTG-CDE     TO ML988-PRCHG-CTG-CDE.                 
           MOVE 0                       TO ML988-WKSUM-PRCHG-AMT.               
           MOVE ML984-PRCHG-RTL-AMT     TO ML988-AUDIT-PRCHG-AMT.               
                                                                                
           EVALUATE ML984-FIELD-TYPE                                            
               WHEN SPACES                                                      
                   IF ML984-TRN-TYP-CDE = '040'                                 
                       MOVE 'MKDN-RTL-AMT' TO ML988-COMPARE-FIELD-NAME          
                   ELSE                                                         
                       MOVE 'MKUP-RTL-AMT' TO ML988-COMPARE-FIELD-NAME          
                   END-IF                                                       
               WHEN '1'                                                         
                   MOVE 'MKDN-PLU-RTL-AMT' TO ML988-COMPARE-FIELD-NAME          
               WHEN '2'                                                         
                   MOVE 'MKDN-PROMO-RTL-AMT' TO ML988-COMPARE-FIELD-NAME        
               WHEN '3'                                                         
                   MOVE 'MKDN-RGST-RTL-AMT' TO ML988-COMPARE-FIELD-NAME         
               WHEN '4'                                                         
                   MOVE 'MKDN-GLDST-RTL-AMT' TO ML988-COMPARE-FIELD-NAME        
               WHEN '5'                                                         
                   MOVE 'MKDN-UMTCH-RTL-AMT' TO ML988-COMPARE-FIELD-NAME        
               WHEN '6'                                                         
                   MOVE 'MKDN-BYR-RTL-AMT' TO ML988-COMPARE-FIELD-NAME          
               WHEN '7'                                                         
                   MOVE 'MKDN-STR-RTL-AMT' TO ML988-COMPARE-FIELD-NAME          
               WHEN '8'                                                         
                   MOVE 'MKDN-PRPT-RTL-AMT' TO ML988-COMPARE-FIELD-NAME         
               WHEN '9'                                                         
                   MOVE 'MKDN-OTH-RTL-AMT' TO ML988-COMPARE-FIELD-NAME          
           END-EVALUATE.                                                        
                                                                                
           WRITE PRCHG-RPT-REC FROM ML988-PRCHG-REPORT-RECS.                    
           ADD 1 TO PA-PRCHG-RECS-WRITTEN.                                      
                                                                                
      ******************************************************************        
       9000-EOJ.                                                                
      ******************************************************************        
      *                                                                         
           DISPLAY ' '.                                                         
           DISPLAY '  PROGRAM MLKUT987 PROCESSING SUMMARY'.                     
           DISPLAY '  -----------------------------------'.                     
           DISPLAY ' '.                                                         
      *                                                                         
           MOVE PA-WKLYSUM-RECS-READ     TO CD-COUNT-HOLD1.                     
           DISPLAY 'IN RECORDS READ =           ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-INVADJ-RECS-READ      TO CD-COUNT-HOLD1.                     
           DISPLAY 'INVADJ RECORDS READ =       ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-SALE-RECS-READ        TO CD-COUNT-HOLD1.                     
           DISPLAY 'SALE RECORDS READ =         ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-PURCHADJ-RECS-READ    TO CD-COUNT-HOLD1.                     
           DISPLAY 'PURCHADJ RECORDS READ =     ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-PRCHG-RECS-READ       TO CD-COUNT-HOLD1.                     
           DISPLAY 'PRCHG RECORDS READ =        ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-INVADJ-KEY-MATCHES    TO CD-COUNT-HOLD1.                     
           DISPLAY 'INVADJ KEY MATCHES =        ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-SALE-KEY-MATCHES      TO CD-COUNT-HOLD1.                     
           DISPLAY 'SALE KEY MATCHES =          ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-PURCHADJ-KEY-MATCHES  TO CD-COUNT-HOLD1.                     
           DISPLAY 'PURCHADJ KEY MATCHES =      ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-PRCHG-KEY-MATCHES     TO CD-COUNT-HOLD1.                     
           DISPLAY 'PRCHG KEY MATCHES =         ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-INVADJ-RECS-WRITTEN   TO CD-COUNT-HOLD1.                     
           DISPLAY 'INVADJ RPT RECS WRITTEN =   ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-SALE-RECS-WRITTEN     TO CD-COUNT-HOLD1.                     
           DISPLAY 'SALE RPT RECS WRITTEN =     ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-PURCHADJ-RECS-WRITTEN TO CD-COUNT-HOLD1.                     
           DISPLAY 'PURCHADJ RPT RECS WRITTEN = ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-PRCHG-RECS-WRITTEN    TO CD-COUNT-HOLD1.                     
           DISPLAY 'PRCHG RPT RECS WRITTEN =    ' CD-COUNT-HOLD1.               
      *                                                                         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE WKLYSUM-EXTR-FILE                                              
                 INVADJ-EXTR-FILE                                               
                 SALE-EXTR-FILE                                                 
                 PURCHADJ-EXTR-FILE                                             
                 PRCHG-EXTR-FILE                                                
                 INVADJ-RPT-FILE                                                
                 SALE-RPT-FILE                                                  
                 PURCHADJ-RPT-FILE                                              
                 PRCHG-RPT-FILE.                                                
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9900-COMMON-ABEND-ROUTINE.                                               
      ******************************************************************        
      *                                                                         
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY ABEND-CODE.                                                  
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
