       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP057.                                         00030000
       AUTHOR.        SURENDAR BALAKRISHNAN.                            00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  03/18/2010.                                       00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP057 - SALES TRANSACTION UPDATE                  *00100000
      *                      TABLE UPDATE (MLT_SLS_TRN_DTL)            *00110000
      ******************************************************************00120000
      ******************************************************************00130000
      *    WR/PITS#     DATE        DESCRIPTION                        *00140000
      *    --------   --------     ---------------------------------   *00150000
      *    WR37965    03/18/10     INITIAL CREATION                    *00160002
      ******************************************************************00170000
                                                                        00180000
       ENVIRONMENT DIVISION.                                            00190000
                                                                        00200000
       CONFIGURATION SECTION.                                           00210000
       SOURCE-COMPUTER.        IBM-3090.                                00220000
       OBJECT-COMPUTER.        IBM-3090.                                00230000
       INPUT-OUTPUT SECTION.                                            00240000
       FILE-CONTROL.                                                    00250000
           SELECT SLS-TRN-DTL-FILE                                      00260000
               ASSIGN TO INP01.                                         00270000
                                                                        00280000
           SELECT CNTL-DATE-FILE                                        00290000
               ASSIGN TO INP02.                                         00300000
                                                                        00310000
       DATA DIVISION.                                                   00320000
       FILE SECTION.                                                    00330000
                                                                        00340000
       FD  SLS-TRN-DTL-FILE                                             00350000
           LABEL RECORDS ARE STANDARD                                   00360000
           RECORDING MODE IS F                                          00370000
           BLOCK CONTAINS 0 RECORDS.                                    00380000
       01  SLS-TRN-DTL-REC            PIC X(200).                       00390000
                                                                        00400000
       FD  CNTL-DATE-FILE                                               00410000
           LABEL RECORDS ARE STANDARD                                   00420000
           RECORDING MODE IS F                                          00430000
           BLOCK CONTAINS 0 RECORDS.                                    00440000
       01  CNTL-DATE-REC               PIC X(80).                       00450000
      *                                                                 00460000
                                                                        00470000
       WORKING-STORAGE SECTION.                                         00480000
                                                                        00490000
       01  FILLER                      PIC X(28)   VALUE                00500000
                'BEGINNING OF WORKING STORAGE'.                         00510000
                                                                        00520000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00530000
                                                                        00540000
       01  PC-PROGRAM-CONSTANTS.                                        00550000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK057'.    00560000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP057'.  00570000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00580000
                                                                        00590000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00600000
           05  PE-MSG-01                       PIC X(50) VALUE          00610000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00620000
           05  PE-MSG-02                       PIC X(50) VALUE          00630000
                'ATTEMPT TO UPDATE ROW ON MLT_SLS_TRN_DTL FAILED'.      00640000
           05  PE-MSG-03                       PIC X(50) VALUE          00650000
                'ATTEMPT TO INSERT ROW ON MLT_SLS_TRN_DTL FAILED'.      00660000
           05  PE-MSG-04                       PIC X(50) VALUE          00670000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00680000
           05  PE-MSG-05                       PIC X(50) VALUE          00690000
                'UPDATE TO TCKRSTINF FAILED                      '.     00700000
           05  PE-MSG-06                       PIC X(50) VALUE          00710000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00720000
           05  PE-MSG-07                       PIC X(50) VALUE          00730000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00740000
           05  PE-MSG-08                       PIC X(50) VALUE          00750000
                'ERROR WHILE VALIDATING CONTROL DATE'.                  00760000
      *                                                                 00770000
       01  PS-PROGRAM-SWITCHES.                                         00780000
           05  PS-EOF-SLS-DTL-FILE-SW       PIC X(01)     VALUE 'N'.    00790000
               88  PS-EOF-SLS-DTL-FILE                    VALUE 'Y'.    00800000
           05  PS-MLT00040-CURSOR-EOF-SW    PIC X(01)     VALUE 'N'.    00810000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00820000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00830000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00840000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00850000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00860000
           05  PS-CNTL-DATE-FILE-SW         PIC X(01)     VALUE 'N'.    00870000
               88  PS-END-OF-CNTL-DTE-FILE                VALUE 'Y'.    00880000
      *                                                                 00890000
       01  CC-CONTROL-CARD.                                             00900000
           05  CC-CNTL-DATE                 PIC X(10)     VALUE SPACES. 00910000
           05  FILLER                       PIC X(70)     VALUE SPACES. 00920000
      *                                                                 00930000
       01  PROGRAM-VARIABLES.                                           00990000
           05  PV-RETRY-COUNT            PIC S9(04) VALUE +0.           01000000
           05  PV-DEADLOCK-COUNT         PIC S9(04) VALUE +0.           01010000
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       01020000
           05  PV-ABEND-MESSAGE          PIC  X(75) VALUE SPACES.       01030000
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       01040000
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.       01050000
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.       01060000
           05  PV-DISPLAY-FIELD          PIC ZZZZZZZZ9  VALUE ZERO.     01070000
           05  PV-DB2-DATE-EXISTS        PIC   X(1) VALUE SPACES.       01080000
      *                                                                 01090000
       01  PA-PROGRAM-ACCUMULATORS.                                     01100000
           05  PA-RECORD-COUNTS.                                        01110000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      01120000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      01130000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      01140000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      01150000
      *                                                                 01160000
       01  PD-PROGRAM-DISPLAYS.                                         01170000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01180000
      *                                                                 01220000
      ******************************************************************01230000
      * MLRD090 - AUDIT DETAIL TRANSACTIONS                             01240000
      ******************************************************************01250000
           COPY MLRD090.                                                01260011
      *                                                                 01270000
      ******************************************************************01280000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01290000
      ******************************************************************01300000
           COPY DPWS004.                                                01310000
      *                                                                 01320000
      ******************************************************************01330000
      *  USED FOR DB2 ERRORS                                            01340000
      ******************************************************************01350000
           EXEC SQL                                                     01360000
               INCLUDE SQLCA                                            01370000
           END-EXEC.                                                    01380000
      *                                                                 01390000
      ******************************************************************01400000
      * DATE TABLE                                                      01410000
      ******************************************************************01420000
           EXEC SQL                                                     01430000
             INCLUDE TDTATTR                                            01440000
           END-EXEC.                                                    01450000
      *                                                                 01460000
      ******************************************************************01470000
      *  MERCHANDISE LEDGER SALES TRANSACTION AUDIT DETAIL              01480000
      ******************************************************************01490000
           EXEC SQL                                                     01500000
               INCLUDE MLT00040                                         01510000
           END-EXEC.                                                    01520000
      *                                                                 01530000
      ***************************************************************** 01540000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01550000
      ***************************************************************** 01560000
           EXEC SQL                                                     01570000
               INCLUDE TCKRSTCN                                         01580000
           END-EXEC.                                                    01590000
      *                                                                 01600000
           EXEC SQL                                                     01610000
               INCLUDE TCKRSTIN                                         01620000
           END-EXEC.                                                    01630000
      *                                                                 01640000
      ******************************************************************01650000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01660000
      ******************************************************************01670000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01680000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +76 COMP.  01690000
           05  CR-CHECKPOINT-RESTART-VAR.                               01700000
               10  CR-PREV-DTL-KEY.                                     01710000
                   15  CR-FSCL-WK-END-DTE   PIC X(10)  VALUE SPACES.    01720000
                   15  CR-PRCG-DTE          PIC X(10)  VALUE SPACES.    01730000
                   15  CR-DEPT-NBR          PIC S9(4)V COMP-3 VALUE +0. 01740000
                   15  CR-MAJ-CL-NBR        PIC S9(4)V COMP-3 VALUE +0. 01750000
                   15  CR-SUB-CL-NBR        PIC S9(4)V COMP-3 VALUE +0. 01760000
                   15  CR-LOC-NBR           PIC S9(4)  COMP   VALUE +0. 01770000
                   15  CR-ML-LO-MDSE-LVL-ID PIC S9(9)  COMP   VALUE +0. 01780000
                   15  CR-ORIG-ENT-ID       PIC S9(9)  COMP   VALUE +0. 01790000
                   15  CR-ORIG-LBL-SEQ-NBR  PIC S9(9)  COMP   VALUE +0. 01800000
                   15  CR-DUMMY-SKU-IND     PIC X(01) VALUE SPACES.     01810000
                   15  CR-FINCL-PRC-GP-CDE  PIC X(06) VALUE SPACES.     01820000
                   15  CR-SRC-SYS-ID        PIC S9(4)  COMP   VALUE +0. 01830000
                   15  CR-SLS-TYP-CDE       PIC X(01) VALUE SPACES.     01840000
                   15  CR-TRN-TYP-CDE       PIC X(03) VALUE SPACES.     01850000
                   15  CR-ENT-ID            PIC S9(9)  COMP   VALUE +0. 01860000
                   15  CR-LBL-SEQ-NBR       PIC S9(4)  COMP   VALUE +0. 01870000
               10  CR-INPUT-READ-COUNT      PIC 9(09) VALUE ZEROES.     01880000
      *                                                                 01890000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01900000
           05  CK-SAVE-PREV-KEY.                                        01910000
               10  CK-FSCL-WK-END-DTE     PIC X(10)  VALUE SPACES.      01920000
               10  CK-PRCG-DTE            PIC X(10)  VALUE SPACES.      01930000
               10  CK-DEPT-NBR            PIC S9(4)V COMP-3 VALUE +0.   01940000
               10  CK-MAJ-CL-NBR          PIC S9(4)V COMP-3 VALUE +0.   01950000
               10  CK-SUB-CL-NBR          PIC S9(4)V COMP-3 VALUE +0.   01960000
               10  CK-LOC-NBR             PIC S9(4)  COMP   VALUE +0.   01970000
               10  CK-ML-LO-MDSE-LVL-ID   PIC S9(9)  COMP   VALUE +0.   01980000
               10  CK-ORIG-ENT-ID         PIC S9(9)  COMP   VALUE +0.   01990000
               10  CK-ORIG-LBL-SEQ-NBR    PIC S9(9)  COMP   VALUE +0.   02000000
               10  CK-DUMMY-SKU-IND       PIC X(01) VALUE SPACES.       02010000
               10  CK-FINCL-PRC-GP-CDE    PIC X(06) VALUE SPACES.       02020000
               10  CK-SRC-SYS-ID          PIC S9(4)  COMP   VALUE +0.   02030000
               10  CK-SLS-TYP-CDE         PIC X(01) VALUE SPACES.       02040000
               10  CK-TRN-TYP-CDE         PIC X(03) VALUE SPACES.       02050000
               10  CK-ENT-ID              PIC S9(9)  COMP   VALUE +0.   02060000
               10  CK-LBL-SEQ-NBR         PIC S9(4)  COMP   VALUE +0.   02070000
                                                                        02080000
      *-------------------*                                             02090000
       PROCEDURE DIVISION.                                              02100000
      *-------------------*                                             02110000
                                                                        02120000
       0000-MLKUP057.                                                   02130005
                                                                        02140000
           PERFORM 1000-INITIALIZATION.                                 02150000
      *                                                                 02160000
           PERFORM 2000-PROCESS-INPUT                                   02170000
                UNTIL PS-EOF-SLS-DTL-FILE.                              02180000
      *                                                                 02190000
           PERFORM 8000-EOJ-ROUTINE.                                    02200000
                                                                        02210000
           STOP RUN.                                                    02220000
                                                                        02230000
      ******************************************************************02240000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02250000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02260000
      ******************************************************************02270000
      *                                                                 02280000
       1000-INITIALIZATION.                                             02290000
      *                                                                 02310000
           OPEN INPUT SLS-TRN-DTL-FILE                                  02320000
                      CNTL-DATE-FILE.                                   02330000
                                                                        02340000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02350000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02380000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02390000
               PERFORM 7500-SELECT-RESTART-INFO                         02400000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02410000
                                            CR-CHECKPOINT-RESTART-VAR   02420000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02430000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02450000
               DISPLAY 'FSCL WK END DTE ' CR-FSCL-WK-END-DTE            02460000
               DISPLAY 'PRCG DATE       ' CR-PRCG-DTE                   02470000
               MOVE CR-DEPT-NBR           TO PV-DISPLAY-FIELD           02480000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              02490000
               MOVE CR-MAJ-CL-NBR         TO PV-DISPLAY-FIELD           02500000
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              02510000
               MOVE CR-SUB-CL-NBR         TO PV-DISPLAY-FIELD           02520000
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              02530000
               MOVE CR-LOC-NBR            TO PV-DISPLAY-FIELD           02540000
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              02550000
               MOVE CR-ML-LO-MDSE-LVL-ID  TO PV-DISPLAY-FIELD           02560000
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              02570000
               MOVE CR-ORIG-ENT-ID        TO PV-DISPLAY-FIELD           02580000
               DISPLAY 'ORIG ENT ID     ' PV-DISPLAY-FIELD              02590000
               MOVE CR-ORIG-LBL-SEQ-NBR   TO PV-DISPLAY-FIELD           02600000
               DISPLAY 'ORIG LBL SEQ    ' PV-DISPLAY-FIELD              02610000
               DISPLAY 'DUMMY SKU IND   ' CR-DUMMY-SKU-IND              02620000
               DISPLAY 'FINCL PRC CDE   ' CR-FINCL-PRC-GP-CDE           02630000
               MOVE CR-SRC-SYS-ID         TO PV-DISPLAY-FIELD           02640000
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              02650000
               DISPLAY 'SLS TYP CDE     ' CR-SLS-TYP-CDE                02660000
               MOVE CR-ENT-ID             TO PV-DISPLAY-FIELD           02670000
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              02680000
               MOVE CR-LBL-SEQ-NBR        TO PV-DISPLAY-FIELD           02690000
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              02700000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02710000
           ELSE                                                         02720000
               SET PS-FIRST-COMMIT TO TRUE                              02730000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02740000
           END-IF.                                                      02750000
      *                                                                 02760000
           PERFORM 5000-READ-DATE-CNTL-FILE                             02770000
           PERFORM 4000-READ-SLS-DTL-FILE UNTIL                         02780000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02790000
            OR PS-EOF-SLS-DTL-FILE.                                     02800000
      *                                                                 02810000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02840000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02850000
                        PA-INPUT-COUNT                                  02860000
               DISPLAY 'FSCL WK END DTE ' ML090-FSCL-WK-END-DTE         02870000
               DISPLAY 'PRCG DATE       ' ML090-PRCG-DTE                02880000
               MOVE ML090-DEPT-NBR        TO PV-DISPLAY-FIELD           02890000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              02900000
               MOVE ML090-MAJ-CL-NBR      TO PV-DISPLAY-FIELD           02910000
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              02920000
               MOVE ML090-SUB-CL-NBR      TO PV-DISPLAY-FIELD           02930000
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              02940000
               MOVE ML090-LOC-NBR         TO PV-DISPLAY-FIELD           02950000
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              02960000
               MOVE ML090-ML-LO-MDSE-LVL-ID TO PV-DISPLAY-FIELD         02970000
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              02980000
               MOVE ML090-ORIG-ENT-ID     TO PV-DISPLAY-FIELD           02990000
               DISPLAY 'ORIG ENT ID     ' PV-DISPLAY-FIELD              03000000
               MOVE ML090-ORIG-LBL-SEQ-NBR TO PV-DISPLAY-FIELD          03010000
               DISPLAY 'ORIG LBL SEQ    ' PV-DISPLAY-FIELD              03020000
               DISPLAY 'DUMMY SKU IND   ' ML090-DUMMY-SKU-IND           03030000
               DISPLAY 'FINCL PRC CDE   ' ML090-FINCL-PRC-GP-CDE        03040000
               MOVE ML090-SRC-SYS-ID      TO PV-DISPLAY-FIELD           03050000
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              03060000
               DISPLAY 'SLS TYP CDE     ' ML090-SLS-TYP-CDE             03070000
               DISPLAY 'TRN TYP CDE     ' ML090-TRN-TYP-CDE             03080000
               MOVE ML090-ENT-ID          TO PV-DISPLAY-FIELD           03090000
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              03100000
               MOVE ML090-LBL-SEQ-NBR     TO PV-DISPLAY-FIELD           03110000
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              03120000
           END-IF.                                                      03130000
                                                                        03140000
           IF PS-EOF-SLS-DTL-FILE                                       03150000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       03160000
                   CONTINUE                                             03170000
               ELSE                                                     03180000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             03190000
               END-IF                                                   03200000
           ELSE                                                         03210000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        03220000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  03230000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             03240000
           END-IF.                                                      03250000
                                                                        03260000
      ******************************************************************03270000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  03280000
      ******************************************************************03290000
       2000-PROCESS-INPUT.                                              03300000
      *                                                                 03320000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          03330000
      *                                                                 03340000
           IF CC-CNTL-DATE = SPACES                                     03370000
             MOVE ML090-PRCG-DTE        TO MLT00040-PRCG-DTE            03390000
           ELSE                                                         03400000
             MOVE CC-CNTL-DATE          TO MLT00040-PRCG-DTE            03420000
           END-IF                                                       03430000
                                                                        03440003
           PERFORM 7200-INSERT-MLT00040-ROW.                            03450005
      *                                                                 03630000
           IF PS-PROGRAM-DEADLOCKED                                     03640000
               CONTINUE                                                 03650000
           ELSE                                                         03660000
               PERFORM 7700-COMMIT-PROCESSING                           03670000
               PERFORM 4000-READ-SLS-DTL-FILE                           03680000
           END-IF.                                                      03690000
      *                                                                 03700000
      *                                                                 03710000
      ******************************************************************03810000
      * READS THE CONDENSED FILE INTO THE MLT_SLS_TRN_DTL LAYOUT        03820000
      ******************************************************************03830000
       4000-READ-SLS-DTL-FILE.                                          03840000
           READ SLS-TRN-DTL-FILE INTO ML090-AUDIT-REC                   03860000
              AT END                                                    03870000
                MOVE 'Y' TO PS-EOF-SLS-DTL-FILE-SW.                     03880000
      *                                                                 03890000
           IF PS-EOF-SLS-DTL-FILE                                       03900000
               IF PA-INPUT-COUNT=0                                      03901010
                  INITIALIZE ML090-AUDIT-REC                            03902010
               END-IF                                                   03903010
               MOVE ML090-FSCL-WK-END-DTE     TO CR-FSCL-WK-END-DTE     03910000
               MOVE ML090-PRCG-DTE            TO CR-PRCG-DTE            03920000
               MOVE ML090-DEPT-NBR            TO CR-DEPT-NBR            03930000
               MOVE ML090-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03940000
               MOVE ML090-SUB-CL-NBR          TO CR-SUB-CL-NBR          03950000
               MOVE ML090-LOC-NBR             TO CR-LOC-NBR             03960000
               MOVE ML090-ML-LO-MDSE-LVL-ID   TO CR-ML-LO-MDSE-LVL-ID   03970000
               MOVE ML090-ORIG-ENT-ID         TO CR-ORIG-ENT-ID         03980000
               MOVE ML090-ORIG-LBL-SEQ-NBR    TO CR-ORIG-LBL-SEQ-NBR    03990000
               MOVE ML090-DUMMY-SKU-IND       TO CR-DUMMY-SKU-IND       04010000
               MOVE ML090-FINCL-PRC-GP-CDE    TO CR-FINCL-PRC-GP-CDE    04030000
               MOVE ML090-SRC-SYS-ID          TO CR-SRC-SYS-ID          04040000
               MOVE ML090-SLS-TYP-CDE         TO CR-SLS-TYP-CDE         04050000
               MOVE ML090-TRN-TYP-CDE         TO CR-TRN-TYP-CDE         04060000
               MOVE ML090-ENT-ID              TO CR-ENT-ID              04070000
               MOVE ML090-LBL-SEQ-NBR         TO CR-LBL-SEQ-NBR         04080000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    04090000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  04100000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       04110000
                                             TCKRSTINF-WORK-STRG-DESC   04120000
               EXEC SQL                                                 04130000
                 UPDATE TCKRSTINF                                       04140000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      04150000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       04160000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   04170000
               END-EXEC                                                 04180000
               IF SQLCODE = 0                                           04190000
                   CONTINUE                                             04200000
               ELSE                                                     04210000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    04220000
                   MOVE '* 4000-READ-SLS-DTL  *' TO PV-PARAGRAPH-NAME   04230000
                   PERFORM 9999-SQL-ABEND                               04240000
               END-IF                                                   04250000
            ELSE                                                        04260000
                ADD 1                        TO PA-INPUT-COUNT          04270000
            END-IF.                                                     04280000
      *                                                                 04290000
      ******************************************************************04300000
      * READ DATE CONTROL FILE                                          04310000
      ******************************************************************04320000
       5000-READ-DATE-CNTL-FILE.                                        04330000
           READ CNTL-DATE-FILE INTO CC-CONTROL-CARD                     04340000
               AT END                                                   04350000
                   MOVE SPACES TO CC-CONTROL-CARD                       04360000
                   SET PS-END-OF-CNTL-DTE-FILE TO TRUE                  04370000
                   DISPLAY  'PROCESSING DATE CONTROL CARD EMPTY'.       04380003
           IF NOT PS-END-OF-CNTL-DTE-FILE                               04390000
               PERFORM 5001-VALIDATE-CNTL-DATE                          04400000
           END-IF.                                                      04410000
      *                                                                 04420000
      ******************************************************************04430000
      * VALIDATE CONTROL DATE                                           04440000
      ******************************************************************04450000
       5001-VALIDATE-CNTL-DATE.                                         04460000
                  EXEC SQL                                              04470000
                     SELECT 'Y'                                         04480000
                       INTO :PV-DB2-DATE-EXISTS                         04490000
                       FROM TDTATTR                                     04500000
                      WHERE KY_DTE = :CC-CNTL-DATE                      04510000
                  END-EXEC                                              04520000
                                                                        04530000
                  EVALUATE TRUE                                         04540000
                      WHEN SQLCODE = 0                                  04550000
                           CONTINUE                                     04560000
                      WHEN SQLCODE = 100                                04570000
                           DISPLAY 'INVALID DATE IN CONTROL CARD'       04580000
                           MOVE SPACES TO CC-CNTL-DATE                  04590000
                      WHEN OTHER                                        04600000
                           CONTINUE                                     04610000
                           MOVE '5001-VALIDATE-CNTL-DATE' TO            04620000
                                              PV-PARAGRAPH-NAME         04630000
                           MOVE PE-MSG-08 TO PV-OPERATION-ATTEMPTED     04640000
                           PERFORM 9999-SQL-ABEND                       04650000
                  END-EVALUATE.                                         04660000
      *                                                                 05460000
      ***************************************************************** 05461008
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON KEY. CURRENT ROW 05462008
      * AMOUNT FIELDS UPDATED TO ADD VALUES FROM INPUT REC.             05463009
      ***************************************************************** 05464008
       7100-UPDATE-MLT00040-ROW.                                        05520000
      *                                                                 05530000
           EXEC SQL                                                     05560000
               UPDATE MLT_SLS_TRN_DTL                                   05570000
                   SET SLS_RTL_AMT        = SLS_RTL_AMT +               05581005
                                            :ML090-SLS-RTL-AMT          05582005
              WHERE   FSCL_WK_END_DTE     = :ML090-FSCL-WK-END-DTE      05590000
                AND   PRCG_DTE            = :MLT00040-PRCG-DTE          05600000
                AND   DEPT_NBR            = :ML090-DEPT-NBR             05610000
                AND   MAJ_CL_NBR          = :ML090-MAJ-CL-NBR           05620000
                AND   SUB_CL_NBR          = :ML090-SUB-CL-NBR           05630000
                AND   LOC_NBR             = :ML090-LOC-NBR              05640000
                AND   ML_LO_MDSE_LVL_ID   = :ML090-ML-LO-MDSE-LVL-ID    05650000
                AND   ORIG_ENT_ID         = :ML090-ORIG-ENT-ID          05660000
                AND   ORIG_LBL_SEQ_NBR    = :ML090-ORIG-LBL-SEQ-NBR     05670000
                AND   DMY_SKU_IND         = :ML090-DUMMY-SKU-IND        05680000
                AND   FINCL_PRC_GP_CDE    = :ML090-FINCL-PRC-GP-CDE     05690000
                AND   SRC_SYS_ID          = :ML090-SRC-SYS-ID           05700000
                AND   SLS_TYP_CDE         = :ML090-SLS-TYP-CDE          05710000
                AND   TRN_TYP_CDE         = :ML090-TRN-TYP-CDE          05720000
                AND   ENT_ID              = :ML090-ENT-ID               05730000
                AND   LBL_SEQ_NBR         = :ML090-LBL-SEQ-NBR          05740000
           END-EXEC                                                     05750000
      *                                                                 05760000
           EVALUATE SQLCODE                                             05770000
               WHEN 0                                                   05780000
                   ADD 1 TO PA-UPDATE-COUNT                             05790005
               WHEN -911                                                05800000
                   ADD +1             TO PV-DEADLOCK-COUNT              05810000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            05820000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05830000
                       MOVE '7100-UPDATE-MLT00040-ROW' TO               05840000
                                                    PV-PARAGRAPH-NAME   05850000
                       PERFORM 9000-DEADLOCK-ERROR                      05860000
                   ELSE                                                 05870000
                       PERFORM 7999-RESTART-PROCESS                     05880000
                   END-IF                                               05890000
               WHEN OTHER                                               05900000
                   MOVE '7100-UPDATE-MLT00040-ROW' TO PV-PARAGRAPH-NAME 05910000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED05920000
                   PERFORM 9999-SQL-ABEND                               05930000
           END-EVALUATE.                                                05940000
      *                                                                 05970000
      ***************************************************************** 05980000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY DTL TABLE. INITIAL   * 05990000
      * ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW.             * 06000000
      ***************************************************************** 06020000
       7200-INSERT-MLT00040-ROW.                                        06030000
      *                                                                 06040000
           EXEC SQL                                                     06090000
               INSERT INTO MLT_SLS_TRN_DTL                              06100000
                    (  FSCL_WK_END_DTE                                  06110000
                     , PRCG_DTE                                         06120000
                     , DEPT_NBR                                         06130000
                     , MAJ_CL_NBR                                       06140000
                     , SUB_CL_NBR                                       06150000
                     , LOC_NBR                                          06160000
                     , ML_LO_MDSE_LVL_ID                                06170000
                     , ORIG_ENT_ID                                      06180000
                     , ORIG_LBL_SEQ_NBR                                 06190000
                     , DMY_SKU_IND                                      06200000
                     , FINCL_PRC_GP_CDE                                 06210000
                     , SRC_SYS_ID                                       06220000
                     , SLS_TYP_CDE                                      06230000
                     , TRN_TYP_CDE                                      06240000
                     , ENT_ID                                           06250000
                     , LBL_SEQ_NBR                                      06260000
                     , SLS_RTL_AMT)                                     06270000
               VALUES                                                   06280000
                   (   :ML090-FSCL-WK-END-DTE                           06290000
                     , :MLT00040-PRCG-DTE                               06300000
                     , :ML090-DEPT-NBR                                  06310000
                     , :ML090-MAJ-CL-NBR                                06320000
                     , :ML090-SUB-CL-NBR                                06330000
                     , :ML090-LOC-NBR                                   06340000
                     , :ML090-ML-LO-MDSE-LVL-ID                         06350000
                     , :ML090-ORIG-ENT-ID                               06360000
                     , :ML090-ORIG-LBL-SEQ-NBR                          06370000
                     , :ML090-DUMMY-SKU-IND                             06380000
                     , :ML090-FINCL-PRC-GP-CDE                          06390000
                     , :ML090-SRC-SYS-ID                                06400000
                     , :ML090-SLS-TYP-CDE                               06410000
                     , :ML090-TRN-TYP-CDE                               06420000
                     , :ML090-ENT-ID                                    06430000
                     , :ML090-LBL-SEQ-NBR                               06440000
                     , :ML090-SLS-RTL-AMT  )                            06450000
           END-EXEC                                                     06460000
      *                                                                 06470000
           EVALUATE SQLCODE                                             06480000
               WHEN 0                                                   06490000
                   ADD 1 TO PA-INSERT-COUNT                             06500005
               WHEN -803                                                06501005
                   PERFORM 7100-UPDATE-MLT00040-ROW                     06503005
               WHEN -911                                                06510000
                   ADD +1             TO PV-DEADLOCK-COUNT              06520000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            06530000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              06540000
                       MOVE '7200-INSERT-MLT00040-ROW' TO               06550000
                                                    PV-PARAGRAPH-NAME   06560000
                       PERFORM 9000-DEADLOCK-ERROR                      06570000
                   ELSE                                                 06580000
                       PERFORM 7999-RESTART-PROCESS                     06590000
                   END-IF                                               06600000
               WHEN OTHER                                               06610000
                   MOVE '7200-INSERT-MLT00040-ROW' TO PV-PARAGRAPH-NAME 06620000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED06630000
                   PERFORM 9999-SQL-ABEND                               06640000
           END-EVALUATE.                                                06650000
                                                                        06660000
      ******************************************************************06690000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *06700000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *06710000
      *            CONTROL TABLE                                       *06720000
      ******************************************************************06730000
       7300-GET-COMMIT-TIMESTAMP.                                       06740000
                                                                        06750000
           EXEC SQL                                                     06760000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        06770000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       06780000
               INTO :PV-COMMIT-TIMESTAMP                                06790000
               FROM TCKRSTCNTL                                          06800000
              WHERE JOB_NAME  = :PC-JOB-NAME                            06810000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        06820000
           END-EXEC.                                                    06830000
                                                                        06840000
           EVALUATE TRUE                                                06850000
               WHEN SQLCODE = 0                                         06860000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  06870000
                   CONTINUE                                             06880000
               WHEN SQLCODE NOT EQUAL ZERO                              06890000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME06900000
                   PERFORM 9999-SQL-ABEND                               06910000
           END-EVALUATE.                                                06920000
      *                                                                 06930000
      *                                                                 06940000
      ******************************************************************06950000
      * 7400-INIT-CHECKPOINT-SELECT                                    *06960000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *06970000
      *            IF WE ARE IN A RESTART CONDITION.                   *06980000
      ******************************************************************06990000
       7400-INIT-CHECKPOINT-SELECT.                                     07000000
           EXEC SQL                                                     07020000
             SELECT  CKPT_STAT_CODE                                     07030000
                    ,CKPT_FRQNCY_QTY                                    07040000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         07050000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        07060000
               FROM  TCKRSTCNTL                                         07070000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07080000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07090000
           END-EXEC.                                                    07100000
                                                                        07110000
           IF SQLCODE = 0                                               07120000
               CONTINUE                                                 07130000
           ELSE                                                         07140000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  07150000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     07160000
               PERFORM 9999-SQL-ABEND                                   07170000
           END-IF.                                                      07180000
      *                                                                 07190000
      *                                                                 07200000
      ******************************************************************07210000
      * 7500-SELECT-RESTART-INFO                                       *07220000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *07230000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *07240000
      ******************************************************************07250000
       7500-SELECT-RESTART-INFO.                                        07260000
           EXEC SQL                                                     07280000
             SELECT  WORK_STRG_DESC                                     07290000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          07300000
               FROM  TCKRSTINF                                          07310000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07320000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07330000
           END-EXEC.                                                    07340000
                                                                        07350000
           IF SQLCODE = 0                                               07360000
               CONTINUE                                                 07370000
           ELSE                                                         07380000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   07390000
               PERFORM 9999-SQL-ABEND                                   07400000
           END-IF.                                                      07410000
                                                                        07420000
      ******************************************************************07430000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *07440000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *07450000
      ******************************************************************07460000
       7600-SET-CURRENT-TIMESTAMP.                                      07470000
                                                                        07480000
           EXEC SQL                                                     07490000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           07500000
           END-EXEC.                                                    07510000
                                                                        07520000
           IF SQLCODE = 0                                               07530000
               CONTINUE                                                 07540000
           ELSE                                                         07550000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 07560000
               PERFORM 9999-SQL-ABEND                                   07570000
           END-IF.                                                      07580000
      *                                                                 07590000
      *                                                                 07600000
      ******************************************************************07610000
      * 7700-COMMIT-PROCESSING.                                        *07620000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *07630000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*07640000
      ******************************************************************07650000
       7700-COMMIT-PROCESSING.                                          07660000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     07670000
                                                                        07680000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          07690000
                                                                        07700000
      * PREPARE COMMIT RECORD FOR UPDATE                                07710000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                07720000
               MOVE ML090-FSCL-WK-END-DTE     TO CR-FSCL-WK-END-DTE     07730000
               MOVE ML090-PRCG-DTE            TO CR-PRCG-DTE            07740000
               MOVE ML090-DEPT-NBR            TO CR-DEPT-NBR            07750000
               MOVE ML090-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          07760000
               MOVE ML090-SUB-CL-NBR          TO CR-SUB-CL-NBR          07770000
               MOVE ML090-LOC-NBR             TO CR-LOC-NBR             07780000
               MOVE ML090-ML-LO-MDSE-LVL-ID   TO CR-ML-LO-MDSE-LVL-ID   07790000
               MOVE ML090-ORIG-ENT-ID         TO CR-ORIG-ENT-ID         07800000
               MOVE ML090-ORIG-LBL-SEQ-NBR    TO CR-ORIG-LBL-SEQ-NBR    07810000
               MOVE ML090-DUMMY-SKU-IND       TO CR-DUMMY-SKU-IND       07820000
               MOVE ML090-FINCL-PRC-GP-CDE    TO CR-FINCL-PRC-GP-CDE    07830000
               MOVE ML090-SRC-SYS-ID          TO CR-SRC-SYS-ID          07840000
               MOVE ML090-SLS-TYP-CDE         TO CR-SLS-TYP-CDE         07850000
               MOVE ML090-TRN-TYP-CDE         TO CR-TRN-TYP-CDE         07860000
               MOVE ML090-ENT-ID              TO CR-ENT-ID              07870000
               MOVE ML090-LBL-SEQ-NBR         TO CR-LBL-SEQ-NBR         07880000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    07890000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  07900000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       07910000
                                             TCKRSTINF-WORK-STRG-DESC   07920000
               IF PS-FIRST-COMMIT                                       07930000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                07940000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                07950000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       07960000
               END-IF                                                   07970000
               PERFORM 7800-COMMIT-CHANGES                              07980000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        07990000
           END-IF.                                                      08000000
      *                                                                 08010000
      *                                                                 08020000
      ******************************************************************08030000
      * 7800-COMMIT-CHANGES.                                            08040000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      08050000
      *            TAKE A COMMIT.                                       08060000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    08070000
      ******************************************************************08080000
       7800-COMMIT-CHANGES.                                             08090000
                                                                        08100000
           EXEC SQL                                                     08110000
             UPDATE TCKRSTINF                                           08120000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          08130000
              WHERE JOB_NAME       = :PC-JOB-NAME                       08140000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   08150000
           END-EXEC.                                                    08160000
                                                                        08170000
           IF SQLCODE = 0                                               08180000
               CONTINUE                                                 08190000
           ELSE                                                         08200000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED08210000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     08220000
               PERFORM 9999-SQL-ABEND                                   08230000
           END-IF.                                                      08240000
                                                                        08250000
           EXEC SQL                                                     08260000
               COMMIT                                                   08270000
           END-EXEC.                                                    08280000
                                                                        08290000
           IF SQLCODE = 0                                               08300000
               ADD 1 TO PA-COMMIT-COUNT                                 08310000
           ELSE                                                         08320000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED08330000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     08340000
               PERFORM 9999-SQL-ABEND                                   08350000
           END-IF.                                                      08360000
      *                                                                 08370000
      ******************************************************************08380000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   08390000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   08400000
      ******************************************************************08410000
       7900-UPDATE-CHECKPOINT-STATUS.                                   08420000
                                                                        08430000
           EXEC SQL                                                     08440000
             UPDATE  TCKRSTCNTL                                         08450000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        08460000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08470000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08480000
           END-EXEC.                                                    08490000
                                                                        08500000
           IF SQLCODE = 0                                               08510000
               CONTINUE                                                 08520000
           ELSE                                                         08530000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME08540000
               PERFORM 9999-SQL-ABEND                                   08550000
           END-IF.                                                      08560000
                                                                        08570000
           EJECT                                                        08580000
      *                                                                 08590000
      *                                                                 08600000
      ******************************************************************08610000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST MLT_SLS_TRN_DTL       08620000
      * ROW FOR UPDATE.  FIND RESTART RECORD AND CKPT RESTART AT POINT  08630000
      * OF LAST COMMIT.                                                 08640000
      ******************************************************************08650000
       7999-RESTART-PROCESS.                                            08660000
      *                                                                 08670000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           08680000
      *                                                                 08690000
           CLOSE SLS-TRN-DTL-FILE.                                      08700000
      *                                                                 08710000
           PERFORM 7500-SELECT-RESTART-INFO.                            08720000
      *                                                                 08730000
           DISPLAY '**** RESTART **** '.                                08740000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 08750000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   08760000
                                                                        08770000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      08780000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        08790000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       08800000
           IF PS-FIRST-COMMIT                                           08810000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:76)               08820000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     08830000
                      ' BEGINNING'                                      08840000
           ELSE                                                         08850000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           08860000
                        TCKRSTINF-WORK-STRG-DESC (1:76)                 08870000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 08880000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         08890000
                    CR-CHECKPOINT-RESTART-AREA                          08900000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                08910000
               DISPLAY 'FSCL WK END DTE ' CR-FSCL-WK-END-DTE            08920000
               DISPLAY 'PRCG DATE       ' CR-PRCG-DTE                   08930000
               MOVE CR-DEPT-NBR           TO PV-DISPLAY-FIELD           08940000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              08950000
               MOVE CR-MAJ-CL-NBR         TO PV-DISPLAY-FIELD           08960000
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              08970000
               MOVE CR-SUB-CL-NBR         TO PV-DISPLAY-FIELD           08980000
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              08990000
               MOVE CR-LOC-NBR            TO PV-DISPLAY-FIELD           09000000
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              09010000
               MOVE CR-ML-LO-MDSE-LVL-ID  TO PV-DISPLAY-FIELD           09020000
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              09030000
               MOVE CR-ORIG-ENT-ID        TO PV-DISPLAY-FIELD           09040000
               DISPLAY 'ORIG ENT ID     ' PV-DISPLAY-FIELD              09050000
               MOVE CR-ORIG-LBL-SEQ-NBR   TO PV-DISPLAY-FIELD           09060000
               DISPLAY 'ORIG LBL SEQ    ' PV-DISPLAY-FIELD              09070000
               DISPLAY 'DUMMY SKU IND   ' CR-DUMMY-SKU-IND              09080000
               DISPLAY 'FINCL PRC CDE   ' CR-FINCL-PRC-GP-CDE           09090000
               MOVE CR-SRC-SYS-ID         TO PV-DISPLAY-FIELD           09100000
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              09110000
               DISPLAY 'SLS TYP CDE     ' CR-SLS-TYP-CDE                09120000
               MOVE CR-ENT-ID             TO PV-DISPLAY-FIELD           09130000
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              09140000
               MOVE CR-LBL-SEQ-NBR        TO PV-DISPLAY-FIELD           09150000
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              09160000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           09170000
           END-IF.                                                      09180000
                                                                        09190000
           OPEN INPUT SLS-TRN-DTL-FILE.                                 09200000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      09210000
      *                                                                 09220000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          09230000
           PERFORM 4000-READ-SLS-DTL-FILE                               09240000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              09250000
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  09260000
                    PA-INPUT-COUNT.                                     09270000
           DISPLAY 'FSCL WK END DTE '     ML090-FSCL-WK-END-DTE.        09280000
           DISPLAY 'PRCG DATE       '     ML090-PRCG-DTE.               09290000
           MOVE ML090-DEPT-NBR          TO PV-DISPLAY-FIELD.            09300000
           DISPLAY 'DEPARTMENT NBR  '      PV-DISPLAY-FIELD.            09310000
           MOVE ML090-MAJ-CL-NBR        TO PV-DISPLAY-FIELD.            09320000
           DISPLAY 'MAJOR CLASS     '      PV-DISPLAY-FIELD.            09330000
           MOVE ML090-SUB-CL-NBR        TO PV-DISPLAY-FIELD.            09340000
           DISPLAY 'SUB CLASS       '      PV-DISPLAY-FIELD.            09350000
           MOVE ML090-LOC-NBR           TO PV-DISPLAY-FIELD.            09360000
           DISPLAY 'LOC NBR         '      PV-DISPLAY-FIELD.            09370000
           MOVE ML090-ML-LO-MDSE-LVL-ID TO PV-DISPLAY-FIELD.            09380000
           DISPLAY 'ML LWST ID      '      PV-DISPLAY-FIELD.            09390000
           MOVE ML090-ORIG-ENT-ID       TO PV-DISPLAY-FIELD.            09400000
           DISPLAY 'ORIG ENT ID     '      PV-DISPLAY-FIELD.            09410000
           MOVE ML090-ORIG-LBL-SEQ-NBR  TO PV-DISPLAY-FIELD.            09420000
           DISPLAY 'ORIG LBL SEQ    '      PV-DISPLAY-FIELD.            09430000
           DISPLAY 'DUMMY SKU IND   '      ML090-DUMMY-SKU-IND.         09440000
           DISPLAY 'FINCL PRC CDE   '      ML090-FINCL-PRC-GP-CDE.      09450000
           MOVE ML090-SRC-SYS-ID        TO PV-DISPLAY-FIELD.            09460000
           DISPLAY 'SRC SYS ID      '      PV-DISPLAY-FIELD.            09470000
           DISPLAY 'SLS TYP CDE     '      ML090-SLS-TYP-CDE.           09480000
           DISPLAY 'TRN TYP CDE     '      ML090-TRN-TYP-CDE.           09490000
           MOVE ML090-ENT-ID            TO PV-DISPLAY-FIELD.            09500000
           DISPLAY 'ENT-ID          '      PV-DISPLAY-FIELD.            09510000
           MOVE ML090-LBL-SEQ-NBR       TO PV-DISPLAY-FIELD.            09520000
           DISPLAY 'LBL SEQ NBR     '      PV-DISPLAY-FIELD.            09530000
      ******************************************************************09540000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *09550000
      ******************************************************************09560000
       8000-EOJ-ROUTINE.                                                09570000
      *                                                                 09580000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       09590000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       09600000
                                                                        09610000
      *                                                                 09620000
           DISPLAY SPACES.                                              09630000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         09640000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        09650000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        09660000
           DISPLAY SPACES.                                              09670000
      *                                                                 09680000
           CLOSE SLS-TRN-DTL-FILE.                                      09690000
      *                                                                 09700000
      *                                                                 09710000
      ******************************************************************09720000
      *  PERFORMED BY 7100-UPDATE AND 7200-INSERT                       09730000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   09740000
      ******************************************************************09750000
       9000-DEADLOCK-ERROR.                                             09760000
      *                                                                 09770000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     09780000
               PERFORM 9999-SQL-ABEND.                                  09790000
      *                                                                 09800000
      ******************************************************************09810000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               09820000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             09830000
      ******************************************************************09840000
       9999-SQL-ABEND.                                                  09850000
                                                                        09860000
           MOVE +4013                 TO ABEND-CODE.                    09870000
           DISPLAY '**  ABEND     **'.                                  09880000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              09890000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            09900000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       09910000
                                                                        09920000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         09930000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        09940000
                                                                        09950000
           COPY DPPD004.                                                09960000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           09970000
