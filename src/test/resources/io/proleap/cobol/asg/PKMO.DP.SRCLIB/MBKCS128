00001 *************************                                         02/05/92
00002  IDENTIFICATION DIVISION.                                         MBKCS128
00003 *************************                                            LV028
00004  PROGRAM-ID.    MBKCS128.                                            CL**1
00005  AUTHOR.        MIKE FURLICK.                                        CL**1
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**1
00007  DATE-WRITTEN.  9-06-91.                                             CL**1
00008  DATE-COMPILED.                                                      CL**1
00009                                                                      CL**1
00010 *****************************************************************    CL**1
00011 *    R128 - MERCHANDISE LEVEL UPDATE.                                CL**1
00012 *                                                                    CL**1
00013 *    THIS PROGRAM PERFORMS ALLOWS CHANGES TO THE MERCHANDISE LEVEL   CL**1
00014 *    DESCRIPTION COLUMN (TMDSELV).  IT IS THE ONLY DATA THAT MAY     CL**1
00015 *    BE CHANGED.                                                     CL**1
00016 *                                                                    CL**1
00017 *****************************************************************    CL**1
00018                                                                      CL**1
00019 *                                                                    CL**1
00020 *                                                                    CL**1
00021 *                                                                    CL**1
00022  ENVIRONMENT DIVISION.                                               CL**1
00023 *                                                                    CL**1
00024 *                                                                    CL**1
00025 *                                                                    CL**1
00026  DATA DIVISION.                                                      CL**1
00027 *                                                                    CL**1
00028 *                                                                    CL**1
00029  WORKING-STORAGE SECTION.                                            CL**1
00030 *                                                                    CL**1
00031  01  PC-PROGRAM-CONSTANTS.                                           CL**1
00032      05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.   CL**1
00033      05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072     CL**1
00034                                                      COMP SYNC.      CL**1
00035      05  PC-CURRENT-MAP-NAME            PIC X(08)    VALUE           CL**1
00036      'MB128A  '.                                                     CL**1
00037      05  PC-CURRENT-MAPSET-NAME         PIC X(08)    VALUE           CL**1
00038      'MBKM128 '.                                                     CL**1
00039      05  PC-CURRENT-PROGRAM-NAME        PIC X(08)    VALUE           CL**1
00040      'MBKCS128'.                                                     CL**1
00041      05  PC-ERASE-EOF-COMP              PIC S9(04)   VALUE +128      CL**1
00042                                                      COMP SYNC.      CL**1
00043      05  FILLER REDEFINES PC-ERASE-EOF-COMP.                         CL**1
00044          10  FILLER                     PIC X(01).                   CL**1
00045          10  PC-ERASE-EOF               PIC X(01).                   CL**1
00046      05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3         CL**1
00047                                                      COMP-3.         CL**1
00048      05  PC-COMPANY                     PIC XX       VALUE '03'.     CL**2
00049 *                                                                    CL**1
00050  01  PC-TSYMSG-NUMBERS.                                              CL**1
00051      05  PC-TSYMSG-00005                PIC 9(5)    VALUE 00005.     CL**1
00052      05  PC-TSYMSG-00007                PIC 9(5)    VALUE 00007.     CL**1
00053      05  PC-TSYMSG-00008                PIC 9(5)    VALUE 00008.     CL**1
00054      05  PC-TSYMSG-00034                PIC 9(5)    VALUE 00034.     CL**1
00055      05  PC-TSYMSG-00045                PIC 9(5)    VALUE 00045.     CL**9
00056      05  PC-TSYMSG-00050                PIC 9(5)    VALUE 00050.     CL**1
00057      05  PC-TSYMSG-00051                PIC 9(5)    VALUE 00051.     CL**1
00058      05  PC-TSYMSG-00052                PIC 9(5)    VALUE 00052.     CL**1
00059      05  PC-TSYMSG-00053                PIC 9(5)    VALUE 00053.     CL**1
00060      05  PC-TSYMSG-00054                PIC 9(5)    VALUE 00054.     CL**1
00061      05  PC-TSYMSG-00055                PIC 9(5)    VALUE 00055.     CL**1
00062      05  PC-TSYMSG-00057                PIC 9(5)    VALUE 00057.     CL**1
00063      05  PC-TSYMSG-00059                PIC 9(5)    VALUE 00059.     CL**1
00064      05  PC-TSYMSG-00061                PIC 9(5)    VALUE 00061.     CL**1
00065      05  PC-TSYMSG-00062                PIC 9(5)    VALUE 00062.     CL**1
00066      05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.     CL**1
00067      05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.     CL**1
00068      05  PC-TSYMSG-00148                PIC 9(5)    VALUE 00148.     CL*13
00069      05  PC-TSYMSG-99901                PIC 9(5)    VALUE 99901.     CL*24
00070 *                                                                    CL**1
00071  01  PM-PROGRAM-MESSAGES.                                            CL**1
00072      05  PM-PROGRAM-MESSAGE-12          PIC X(78)    VALUE           CL**1
00073      'INVALID NEXT APPLICATION ACTIVITY RECEIVED'.                   CL**1
00074      05  PM-PROGRAM-MESSAGE-22          PIC X(78)    VALUE           CL**1
00075      'LOGIC ERROR IN TRYING TO READ APLSYS QUEUE - RESP = '.         CL**1
00076      05  FILLER REDEFINES PM-PROGRAM-MESSAGE-22.                     CL**1
00077          10  FILLER                     PIC X(61).                   CL**1
00078          10  PM-RESP-22                 PIC 9999.                    CL**1
00079          10  FILLER                     PIC X(12).                   CL**1
00080 *                                                                    CL**1
00081  01  PS-PROGRAM-SUBSCRIPTS.                                          CL**1
00082      05  PS-SUB                         PIC S9(04)   VALUE +0        CL**1
00083                                                      COMP SYNC.      CL**1
00084      05  PS-ITEM-SUB                    PIC S9(09)   VALUE +0        CL**1
00085                                                      COMP-3.         CL**1
00086                                                                      CL**1
00087  01  PS-PROGRAM-SWITCHES.                                            CL**1
00088      05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.      CL**1
00089          88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.      CL**1
00090          88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.      CL**1
00091          88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.      CL**1
00092          88  PS-SEND-DATA-ONLY                       VALUE '4'.      CL**1
00093          88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.      CL**1
00094          88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.      CL**1
00095      05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.      CL**1
00096          88  PS-ERROR                                VALUE 'Y'.      CL**1
00097          88  PS-NO-ERROR                             VALUE 'N'.      CL**1
00098      05  PS-INTER-APPL-VALID-SW         PIC X(01)    VALUE 'N'.      CL**1
00099          88  PS-INTER-APPL-VALID                     VALUE 'Y'.      CL**1
00100          88  PS-INTER-APPL-NOT-VALID                 VALUE 'N'.      CL**1
00101      05  PS-WARNING-SW                  PIC X(01)    VALUE 'N'.      CL**1
00102          88  PS-WARNING                              VALUE 'Y'.      CL**1
00103          88  PS-NO-WARNING                           VALUE 'N'.      CL**1
00104      05  PS-INFORMATIONAL-SW            PIC X(01)    VALUE 'N'.      CL**1
00105          88  PS-INFORMATIONAL                        VALUE 'Y'.      CL**1
00106          88  PS-NOT-INFORMATIONAL                    VALUE 'N'.      CL**1
00107      05  PS-PROCESSING-SW               PIC X(01)    VALUE 'N'.      CL**1
00108          88  CONTINUE-PROCESSING                     VALUE 'Y'.      CL**1
00109          88  STOP-PROCESSING                         VALUE 'N'.      CL**1
00110      05  PS-SAVE-SW                     PIC X(01)    VALUE 'N'.      CL**1
00111          88  SAVE-MDSELV-DATA                        VALUE 'Y'.      CL**1
00112          88  DONT-SAVE-MDSELV-DATA                   VALUE 'N'.      CL**1
00113      EJECT                                                           CL**1
00114  01  PV-PROGRAM-VARIABLES.                                           CL**1
00115      05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0        CL**1
00116                                                      COMP SYNC.      CL**1
00117      05  PV-RESP                        PIC S9(04)   VALUE +0        CL**1
00118                                                      COMP SYNC.      CL**1
00119      05  PV-ITEM                        PIC S9(04)   VALUE +0        CL**1
00120                                                      COMP SYNC.      CL**1
00121      05  PV-AS-ITEM                     PIC S9(04)   VALUE +0        CL**1
00122                                                      COMP SYNC.      CL**1
00123      05  PV-MESSAGE                     PIC X(78)    VALUE SPACES.   CL**1
00124      05  PV-SQL-SUB                     PIC S9(4)    COMP.           CL**1
00125      05  PV-RETRY-COUNTER               PIC S9(3)    COMP-3.         CL**1
00126 *                                                                    CL**1
00127  01  PV-DB2-COUNT                       PIC S9(9)    VALUE +0        CL**1
00128                                                      COMP-3.         CL**1
00129  01  PV-DB2-DATE                        PIC X(10).                   CL**1
00130  01  FILLER  REDEFINES PV-DB2-DATE.                                  CL**1
00131      05  PV-DD-CCYY                     PIC X(4).                    CL**1
00132      05  FILLER                         PIC X.                       CL**1
00133          88  PV-DD-DASH1                             VALUE '-'.      CL**1
00134      05  PV-DD-MM                       PIC XX.                      CL**1
00135      05  FILLER                         PIC X.                       CL**1
00136          88  PV-DD-DASH2                             VALUE '-'.      CL**1
00137      05  PV-DD-ML                       PIC XX.                      CL**1
00138 *                                                                    CL**1
00139  EJECT                                                               CL**1
00140 *****************************************************************    CL**1
00141 *    STANDARD COMMAREA.                                              CL**1
00142 *****************************************************************    CL**1
00143                                                                      CL**1
00144      COPY DPWS020.                                                   CL*28
00145      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                   CL**1
00146                                                                      CL**1
00147 *****************************************************************    CL**1
00148 *    INTER-APPLICATION COMMAREA.                                     CL**1
00149 *****************************************************************    CL**1
00150                                                                      CL**1
00151          10  INTER-APPL-COMM-AREA       PIC X(200).                  CL**1
00152      COPY MBWS005.                                                   CL*28
00153 *****************************************************************    CL**1
00154 *    SPECIFIC COMMMAREA FOR MBKCS128.                                CL**1
00155 *****************************************************************    CL**1
00156          10  MB128-SPECIFIC-COMMAREA.                                CL**1
00157              15  MB128-APPL-CURSOR-POSITION          PIC S9(04)      CL**1
00158                                                      COMP.           CL**1
00159              15  MB128-ML-SUB                        PIC S9(4)       CL**1
00160                                                          COMP.       CL**1
00161              15  MB128-ML-ITEM                       PIC S9(4)       CL**1
00162                                                          COMP.       CL**1
00163              15  MB128-LAST-MLEVEL                   PIC X(3).       CL**1
00164              15  MB128-LAST-MDESC                    PIC X(25).      CL**1
00165              15  MB128-LAST-PLEVEL                   PIC X(3).       CL**1
00166              15  MB128-LAST-PDESC                    PIC X(25).      CL**1
00167              15  MB128-TEMP-QUEUE-NAME.                              CL**1
00168                  20  MB128-TERM-ID                   PIC X(4).       CL**1
00169                  20  MB128-TASK-NUMBER               PIC S9(7)       CL**1
00170                                                          COMP-3.     CL**1
00171              15  MB128-SAVE-INTER-APPL-COMMAREA      PIC X(200).     CL**1
00172              15  MB128-SAVE-TS-QUEUE                 PIC X(1700).    CL*16
00173 *                                                                    CL**1
00174      EJECT                                                           CL**1
00175 *****************************************************************    CL**1
00176 *    APPL SYS LIST QUEUE RECORD LAYOUT.                              CL**1
00177 *****************************************************************    CL**1
00178  01 MDSELV-LIST-RECORD.                                              CL**1
00179      COPY MBWS127.                                                   CL*28
00180                                                                      CL**1
00181 *                                                                    CL**1
00182      EJECT                                                           CL**1
00183 *****************************************************************    CL**1
00184 *    ABEND PROCESSING COPYBOOK.                                      CL**1
00185 *****************************************************************    CL**1
00186                                                                      CL**1
00187      COPY DPWS013.                                                   CL**1
00188                                                                      CL**1
00189      EJECT                                                           CL**1
00190 *****************************************************************    CL**1
00191 *    ATTRIBUTE SETTINGS COPYBOOK.                                    CL**1
00192 *****************************************************************    CL**1
00193                                                                      CL**1
00194      COPY DPWS015.                                                   CL**1
00195                                                                      CL**1
00196 *****************************************************************    CL**1
00197 *    CURRENT DATE AND TIME COPYBOOK.                                 CL**1
00198 *****************************************************************    CL**1
00199                                                                      CL**1
00200      COPY DPWS017.                                                   CL**1
00201                                                                      CL**1
00202 *****************************************************************    CL**1
00203 *    TOGGLE FUNCTION KEY LINES COPYBOOK.                             CL**1
00204 *****************************************************************    CL**1
00205                                                                      CL**1
00206      COPY DPWS009.                                                   CL**1
                                                                        MBKCS101
      *****************************************************************         
      *    SYSTEM MESSAGE FIELDS                                                
      *****************************************************************         
                                                                                
           COPY DPWS006.                                                        
                                                                                
00208 *****************************************************************    CL**1
00209 *    FUNCTION KEYS COPYBOOK.                                         CL**1
00210 *****************************************************************    CL**1
00211                                                                      CL**1
00212      COPY DPWS016.                                                   CL**1
00213                                                                      CL**1
00214 *****************************************************************    CL**1
00215 *    DPKUT045 COPYBOOK.                                              CL**1
00216 *****************************************************************    CL**1
00217                                                                      CL**1
00218      COPY DPWS045.                                                   CL**1
00219                                                                      CL**1
00220 *****************************************************************    CL**1
00221 *    NUMERIC EDIT LAYOUT.                                            CL**1
00222 *****************************************************************    CL**1
00223                                                                      CL**1
00224      COPY DPWS010I.                                                  CL**1
00225                                                                      CL**1
00226 *****************************************************************    CL**1
00227 *    MESSAGE RECORD LAYOUT                                           CL**1
00228 *****************************************************************    CL**1
00229                                                                      CL**1
00230      COPY DPRD507I.                                                  CL**1
00231                                                                      CL**1
00232 *****************************************************************    CL**1
00233 *    STANDARD ATTENTION IDENTIFIER LIST                              CL**1
00234 *****************************************************************    CL**1
00235                                                                      CL**1
00236      COPY DFHAID.                                                    CL**1
00237                                                                      CL**1
00238 *****************************************************************    CL**1
00239 *    STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST           CL**1
00240 *****************************************************************    CL**1
00241                                                                      CL**1
00242      COPY DFHBMSCA.                                                  CL**1
00243                                                                      CL**1
00244 *****************************************************************    CL**1
00245 *    MAP LAYOUT                                                      CL**1
00246 *****************************************************************    CL**1
00247                                                                      CL**1
00248      COPY MBKM128.                                                   CL**1
00249  01  MD-MAP-DATA-AREA  REDEFINES MB128AO PIC X(1000).                CL**1
00250 *                                                                    CL**1
00251  EJECT                                                               CL**1
00252 *                                                                    CL**1
00253 *    DB2 AREA FOR TMDSELV                                            CL**1
00254 *                                                                    CL**1
00255      EXEC SQL                                                        CL**1
00256           INCLUDE TMDSELV                                            CL**1
00257      END-EXEC.                                                       CL**1
00258 *                                                                    CL**1
00259 *    DB2 AREA FOR COMMUNICATIONS                                     CL**1
00260 *                                                                    CL**1
00261      EXEC SQL                                                        CL**1
00262           INCLUDE SQLCA                                              CL**1
00263      END-EXEC.                                                       CL**1
00264 *                                                                    CL**1
00265  EJECT                                                               CL**1
00266  LINKAGE SECTION.                                                    CL**1
00267                                                                      CL**1
00268  01  DFHCOMMAREA.                                                    CL**1
00269      05  FILLER                         OCCURS  1 TO 4072 TIMES      CL**1
00270                                         DEPENDING ON EIBCALEN.       CL**1
00271          10  FILLER                     PIC X.                       CL**1
00272                                                                      CL**1
00273 *                                                                    CL**1
00274 *                                                                    CL**1
00275 *                                                                    CL**1
00276  PROCEDURE DIVISION.                                                 CL**1
00277                                                                      CL**1
00278                                                                      CL**1
00279  0000-MAIN-MODULE.                                                   CL**5
00280                                                                      CL**1
00281 ******************************************************************   CL**1
00282 *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.              CL**1
00283 ******************************************************************   CL**1
00284                                                                      CL**1
00285      PERFORM DP015-0000-CHECK-COMMAREA.                              CL**1
00286                                                                      CL**1
00287      IF DP020-SRC-RETURN-W-TRAN-ID                                   CL**1
00288          PERFORM 1000-CONTROL-PROCESSING                             CL**5
00289      END-IF.                                                         CL**1
00290                                                                      CL**1
00291      PERFORM DP003-0000-RETURN-TO-CICS.                              CL**1
00292  EJECT                                                               CL**1
00293 ******************************************************************   CL**1
00294 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT       CL**1
00295 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                       CL**1
00296 *                                                                    CL**1
00297 *    PERFORMED BY 0000-MAIN-MODULE.                                  CL**5
00298 ******************************************************************   CL**1
00299 *                                                                    CL**1
00300  1000-CONTROL-PROCESSING.                                            CL**5
00301 *                                                                    CL**1
00302      SET CONTINUE-PROCESSING   TO  TRUE.                             CL**1
00303 *                                                                    CL**1
00304      EVALUATE TRUE                                                   CL**1
00305          WHEN DP020-NEXT-ACT-INITIAL                                 CL**1
00306              PERFORM 4100-VALIDATE-COMMAREA                          CL*23
00307              IF  PS-INTER-APPL-NOT-VALID                             CL*23
00308                  MOVE DP016-PF3                                      CL*23
00309                                TO  DP020-SRC-AID                     CL*23
00310                  SET DP020-MSG-FATAL                                 CL*25
00311                                TO  TRUE                              CL*25
00312                  MOVE PC-TSYMSG-99901                                CL*24
00313                                TO  DP020-MSG-NUMBER                  CL*24
00314                  PERFORM DP015-1000-START-NAVIGATOR                  CL*23
00315              ELSE                                                    CL*23
00316                  INITIALIZE MB128-SPECIFIC-COMMAREA                  CL*23
00317                  SET PS-SEND-MAP-N-DATA-FRSET                        CL*23
00318                                TO  TRUE                              CL*23
00319                  SET DP020-NEXT-ACT-READ-MAP                         CL*23
00320                                TO  TRUE                              CL*23
00321                  PERFORM 2000-BUILD-INITIAL-PANEL                    CL*23
00322              END-IF                                                  CL*23
00323 *                                                                    CL**1
00324          WHEN DP020-NEXT-ACT-READ-MAP                                CL**1
00325              PERFORM 3000-PROCESS-PANEL                              CL**5
00326 *                                                                    CL**1
00327          WHEN DP020-NEXT-ACT-RETURN                                  CL**1
00328              SET PS-SEND-MAP-N-DATA-FRSET                            CL**1
00329                                TO  TRUE                              CL**1
00330              MOVE MB128-SAVE-INTER-APPL-COMMAREA                     CL**1
00331                                TO  MB005-INTER-APPL-COMMAREA         CL**1
00332              PERFORM 3400-MOVE-COMMAREA-TO-SCREEN                    CL**5
00333              PERFORM 3300-EDIT-DATA-IN-COMMAREA                      CL**5
00334              PERFORM 2200-DISPLAY-PANEL                              CL**5
00335 *                                                                    CL**1
00336          WHEN DP020-NEXT-ACT-ERROR                                   CL**1
00337              MOVE DP020-MSG-MESSAGE TO PV-MESSAGE                    CL**1
00338              IF DP020-NAV-MNEMONIC = SPACES                          CL**1
00339                  MOVE SPACES   TO  ANAVCMDO                          CL**1
00340                  MOVE DP015-UNDERLINE                                CL**1
00341                                TO  ANAVCMDH                          CL**1
00342                  MOVE DP015-GREEN                                    CL**1
00343                                TO  ANAVCMDC                          CL**1
00344              ELSE                                                    CL**1
00345                  MOVE DP015-REVERSE                                  CL**1
00346                                TO  ANAVCMDH                          CL**1
00347                  MOVE DP015-RED                                      CL**1
00348                                TO  ANAVCMDC                          CL**1
00349              END-IF                                                  CL**1
00350 *                                                                    CL**1
00351              MOVE PV-MESSAGE   TO  DP020-MSG-MESSAGE                 CL**1
00352                                                                      CL**1
00353              SET DP020-MSG-FATAL                                     CL**1
00354                                TO  TRUE                              CL**1
00355              SET PS-SEND-DATA-ONLY-FRSET                             CL**1
00356                                TO  TRUE                              CL**1
00357              PERFORM 2200-DISPLAY-PANEL                              CL**5
00358 *                                                                    CL**1
00359          WHEN OTHER                                                  CL**1
00360              SET DP013-LOGIC-ABEND TO TRUE                           CL**1
00361              SET DP013-NO-ROLLBACK TO TRUE                           CL**1
00362              MOVE '1000-CONTROL-PROCESSING'                          CL**5
00363                                TO  DP013-PARAGRAPH                   CL**1
00364              MOVE PM-PROGRAM-MESSAGE-12                              CL**1
00365                                TO  DP013-MESSAGE-TEXT(1)             CL**1
00366              PERFORM DP013-0000-PROCESS-ABEND                        CL**1
00367      END-EVALUATE.                                                   CL**1
00368 *                                                                    CL**1
00369  EJECT                                                               CL**1
00370 ******************************************************************   CL**1
00371 *    ENSURE THE QUEUE WAS PASSED WITH DATA.  STOP PROCESSING IF      CL**1
00372 *    NOT!                                                            CL**1
00373 *    PERFORMED BY 1000-CONTROL-PROCESSING.                           CL**5
00374 ******************************************************************   CL**1
00375 *                                                                    CL**1
00376  2000-BUILD-INITIAL-PANEL.                                           CL**5
00377 *                                                                    CL**1
00378      IF  MB005-LIST-QUEUE-NAME GREATER THAN SPACES                   CL**1
00379          MOVE 1                TO  MB128-ML-ITEM                     CL**1
00380          PERFORM 2130-READ-LIST-QUEUE                                CL**5
00381          MOVE MDSELV-LIST-RECORD                                     CL*16
00382                                TO  MB128-SAVE-TS-QUEUE               CL*16
00383          MOVE 1                TO  MB128-ML-SUB                      CL**1
00384          MOVE SPACES           TO  DCLTMDSELV                        CL*20
00385          MOVE MBWS127-MLEVEL (1)                                     CL**2
00386                                TO  MDSELV-CDE                        CL**1
00387      ELSE                                                            CL**1
00388          MOVE '2000-BUILD-INITIAL-PANEL'                             CL*13
00389                                TO  DP013-PARAGRAPH                   CL*13
00390          MOVE 'INVALID TSQUEUE NAME - CANNOT CONTINUE PROCESSING'    CL*13
00391                                TO  DP013-MESSAGE-TEXT(1)             CL*13
00392          PERFORM DP013-0000-PROCESS-ABEND                            CL*14
00393      END-IF.                                                         CL**1
00394 *                                                                    CL**5
00395      IF  CONTINUE-PROCESSING                                         CL**5
00396          PERFORM 2100-RETRIEVE-DATA-FOR-PANEL                        CL**5
00397      END-IF.                                                         CL**5
00398 *                                                                    CL**5
00399      MOVE PC-COMPANY           TO  AOPERCOO.                         CL*12
00400      PERFORM 2200-DISPLAY-PANEL.                                     CL**5
00401 *                                                                    CL**5
00402  EJECT                                                               CL**5
00403 ******************************************************************   CL**5
00404 *    WE MAY NEED TO READ THE MDSELV TABLE TWICE, THEREFORE, THE      CL**5
00405 *    ACTUAL SQL TO OBTAIN DATA IS FOUND IN 2110.  IF THE PARENT      CL**5
00406 *    LEVEL CODE IS THE SAME, ONLY ONE READ IS REQUIRED.              CL**5
00407 *                                                                    CL**5
00408 *    PERFORMED BY 2000, 2140.                                        CL**5
00409 ******************************************************************   CL**5
00410 *                                                                    CL**5
00411  2100-RETRIEVE-DATA-FOR-PANEL.                                       CL**5
00412 *                                                                    CL**5
00413      PERFORM 2110-READ-MDSELV.                                       CL**5
00414 *                                                                    CL**5
00415      MOVE MDSELV-CDE           TO  MB128-LAST-MLEVEL.                CL*20
00416      MOVE MDSELV-PRNT-MDSELV-CDE                                     CL*20
00417                                TO  MB128-LAST-PLEVEL                 CL*20
00418      MOVE MDSELV-DESC          TO  MB128-LAST-MDESC                  CL*20
00419 *                                                                    CL*20
00420      IF  SQLCODE EQUAL ZERO                                          CL**5
00421          IF  MDSELV-CDE NOT EQUAL MDSELV-PRNT-MDSELV-CDE             CL**5
00422              MOVE MDSELV-PRNT-MDSELV-CDE                             CL**5
00423                                TO  MDSELV-CDE                        CL**5
00424              PERFORM 2110-READ-MDSELV                                CL**5
00425          END-IF                                                      CL**5
00426          MOVE MDSELV-DESC      TO  MB128-LAST-PDESC                  CL**5
00427 *                                                                    CL**5
00428      ELSE                                                            CL**5
00429          MOVE PC-TSYMSG-00148  TO  DP020-MSG-NUMBER                  CL*13
00430          SET STOP-PROCESSING   TO  TRUE                              CL**5
00431          SET DP020-MSG-FATAL   TO  TRUE                              CL**5
00432      END-IF.                                                         CL**5
00433      PERFORM 3400-MOVE-COMMAREA-TO-SCREEN.                           CL*20
00434 *                                                                    CL**5
00435  EJECT                                                               CL**5
00436 ******************************************************************   CL**5
00437 *    READ THE MDSELV TABLE.                                          CL**5
00438 *                                                                    CL**5
00439 *    PERFORMED BY 2100                                               CL**5
00440 ******************************************************************   CL**5
00441 *                                                                    CL**5
00442  2110-READ-MDSELV.                                                   CL**5
00443 *                                                                    CL**5
00444      PERFORM                                                         CL**5
00445          WITH TEST AFTER                                             CL**5
00446          VARYING PV-SQL-SUB                                          CL**5
00447          FROM 1 BY 1                                                 CL**5
00448          UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                CL**5
00449             OR SQLCODE = 0                                           CL**5
00450             OR SQLCODE = +100                                        CL**5
00451 *                                                                    CL**5
00452          EXEC SQL                                                    CL**5
00453               SELECT                                                 CL**5
00454                      MDSELV_CDE,                                     CL**5
00455                      PRNT_MDSELV_CDE,                                CL**5
00456                      MDSELV_DESC                                     CL**5
00457               INTO                                                   CL**5
00458                      :MDSELV-CDE,                                    CL**5
00459                      :MDSELV-PRNT-MDSELV-CDE,                        CL**5
00460                      :MDSELV-DESC                                    CL**5
00461               FROM                                                   CL**5
00462                      TMDSELV                                         CL**5
00463               WHERE                                                  CL**5
00464                      MDSELV_CDE     = :MDSELV-CDE                    CL**5
00465          END-EXEC                                                    CL**5
00466 *                                                                    CL**5
00467          EVALUATE TRUE                                               CL**5
00468              WHEN SQLCODE = -904                                     CL**5
00469              WHEN SQLCODE = -913                                     CL**5
00470              WHEN SQLCODE = ZERO                                     CL**5
00471              WHEN SQLCODE = +100                                     CL**5
00472                   CONTINUE                                           CL**5
00473              WHEN SQLWARN0 NOT EQUAL SPACE                           CL**5
00474              WHEN SQLCODE NOT EQUAL ZERO                             CL**5
00475                   MOVE '2110-READ-MDSELV'                            CL**5
00476                                TO  DP013-PARAGRAPH                   CL**5
00477                   MOVE 'READ MDSELV'                                 CL**5
00478                                TO  DP013-MESSAGE-TEXT (1)            CL**5
00479                   MOVE SQLCA   TO  DP013-SQLCA                       CL**5
00480                   SET DP013-DB2-ABEND                                CL**5
00481                                TO  TRUE                              CL**5
00482                   PERFORM DP013-0000-PROCESS-ABEND                   CL**5
00483          END-EVALUATE                                                CL**5
00484      END-PERFORM.                                                    CL**5
00485 *                                                                    CL**5
00486      IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                      CL**5
00487          MOVE '2110-READ-MDSELV'                                     CL**5
00488                                TO  DP013-PARAGRAPH                   CL**5
00489          MOVE 'MAX RETRIES EXCEEDED FOR READ MDSELV'                 CL**5
00490                                TO  DP013-MESSAGE-TEXT (1)            CL**5
00491          MOVE SQLCA            TO  DP013-SQLCA                       CL**5
00492          SET DP013-DB2-ABEND   TO  TRUE                              CL**5
00493          PERFORM DP013-0000-PROCESS-ABEND                            CL**5
00494      END-IF.                                                         CL**5
00495 *                                                                    CL**5
00496      IF  SQLCODE = +100                                              CL**5
00497          MOVE 'NOT FOUND'      TO  MDSELV-DESC                       CL**5
00498      END-IF.                                                         CL**5
00499 *                                                                    CL**5
00500  EJECT                                                               CL**5
00501 ******************************************************************   CL**5
00502 *    VALIDATION HAS BEEN PERFORMED.  THE CURRENT APPLICATION         CL**5
00503 *    SYSTEM CODE CAN BE UPDATED.                                     CL**5
00504 *                                                                    CL**5
00505 *    PERFORMED BY 3000.                                              CL**5
00506 ******************************************************************   CL**5
00507 *                                                                    CL**5
00508  2120-UPDATE-MDSELV.                                                 CL**5
00509 *                                                                    CL**5
00510      PERFORM                                                         CL**5
00511          WITH TEST AFTER                                             CL**5
00512          VARYING PV-SQL-SUB                                          CL**5
00513          FROM 1 BY 1                                                 CL**5
00514          UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                CL**5
00515             OR SQLCODE = 0                                           CL**5
00516             OR SQLCODE = +100                                        CL**5
00517 *                                                                    CL**5
00518          EXEC SQL                                                    CL**5
00519               UPDATE                                                 CL**5
00520                     TMDSELV                                          CL**5
00521                 SET                                                  CL**5
00522                     MDSELV_DESC      =  :MB128-LAST-MDESC            CL*21
00523               WHERE                                                  CL**5
00524                     MDSELV_CDE       =  :MB128-LAST-MLEVEL           CL*21
00525          END-EXEC                                                    CL**5
00526 *                                                                    CL**5
00527          EVALUATE TRUE                                               CL**5
00528              WHEN SQLCODE = -904                                     CL**5
00529              WHEN SQLCODE = -913                                     CL**5
00530              WHEN SQLCODE = +100                                     CL**5
00531              WHEN SQLCODE = ZERO                                     CL**5
00532                   CONTINUE                                           CL**5
00533              WHEN SQLWARN0 NOT EQUAL SPACE                           CL**5
00534              WHEN SQLCODE NOT EQUAL ZERO                             CL**5
00535                   MOVE '2120-UPDATE-MDSELV'                          CL**5
00536                                TO  DP013-PARAGRAPH                   CL**5
00537                   MOVE 'UPDATE MDSELV'                               CL**5
00538                                TO  DP013-MESSAGE-TEXT (1)            CL**5
00539                   MOVE SQLCA   TO  DP013-SQLCA                       CL**5
00540                   SET DP013-DB2-ABEND                                CL**5
00541                                TO  TRUE                              CL**5
00542                   PERFORM DP013-0000-PROCESS-ABEND                   CL**5
00543          END-EVALUATE                                                CL**5
00544      END-PERFORM.                                                    CL**5
00545 *                                                                    CL**5
00546      IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                      CL**5
00547          MOVE '2120-UPDATE-MDSELV'                                   CL**5
00548                                TO  DP013-PARAGRAPH                   CL**5
00549          MOVE 'MAX RETRIES EXCEEDED FOR UPDATE MDSELV'               CL**5
00550                                TO  DP013-MESSAGE-TEXT (1)            CL**5
00551          MOVE SQLCA            TO  DP013-SQLCA                       CL**5
00552          SET DP013-DB2-ABEND   TO  TRUE                              CL**5
00553          PERFORM DP013-0000-PROCESS-ABEND                            CL**5
00554      END-IF.                                                         CL**5
00555 *                                                                    CL**5
00556      IF  SQLCODE EQUAL ZERO                                          CL**5
00557          MOVE PC-TSYMSG-00059  TO  DP020-MSG-NUMBER                  CL**5
00558      ELSE                                                            CL**5
00559          MOVE PC-TSYMSG-00148  TO  DP020-MSG-NUMBER                  CL*13
00560      END-IF.                                                         CL**5
00561 *                                                                    CL**5
00562      SET DP020-MSG-INFORMATIONAL                                     CL**5
00563                                TO  TRUE.                             CL**5
00564 *                                                                    CL**5
00565  EJECT                                                               CL**5
00566 ******************************************************************   CL**5
00567 *    READ THE TEMPORARY STORAGE QUEUE THAT CONTAINS THE SELECTED     CL**5
00568 *    BUSINESS GROUPS FROM MBKCS120 (BUSINESS GROUP LIST).            CL**5
00569 *                                                                    CL**5
00570 *    PERFORMED BY 2000, 2140                                         CL**5
00571 ******************************************************************   CL**5
00572 *                                                                    CL**5
00573  2130-READ-LIST-QUEUE.                                               CL**5
00574 *                                                                    CL**5
00575      EXEC CICS                                                       CL**5
00576          READQ TS                                                    CL**5
00577          QUEUE(MB005-LIST-QUEUE-NAME)                                CL**5
00578          INTO(MDSELV-LIST-RECORD)                                    CL**5
00579          LENGTH(MB005-LIST-QUEUE-LENGTH)                             CL**5
00580          ITEM(MB128-ML-ITEM)                                         CL**5
00581          RESP(PV-RESP)                                               CL**5
00582      END-EXEC.                                                       CL**5
00583 *                                                                    CL**5
00584      IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                           CL**5
00585          MOVE PV-RESP          TO  PM-RESP-22                        CL**5
00586          SET DP013-LOGIC-ABEND TO  TRUE                              CL**5
00587          SET DP013-NO-ROLLBACK TO  TRUE                              CL**5
00588          MOVE '2130-READ-LIST-QUEUE'                                 CL**5
00589                                TO  DP013-PARAGRAPH                   CL**5
00590          MOVE PM-PROGRAM-MESSAGE-22                                  CL**5
00591                                TO  DP013-MESSAGE-TEXT (1)            CL**5
00592          PERFORM DP013-0000-PROCESS-ABEND                            CL**5
00593      END-IF.                                                         CL**5
00594 *                                                                    CL**1
00595  EJECT                                                               CL**1
00596 ******************************************************************   CL**1
00597 *    POSITION TO THE NEXT MERCHANDISE LEVEL CODE.                    CL**5
00598 *    IF AT END, ISSUE MESSAGE, OTHERWISE CONTINUE PROCESSING.        CL**1
00599 *                                                                    CL**1
00600 *    PERFORMED BY 3000                                               CL**5
00601 ******************************************************************   CL**1
00602 *                                                                    CL**1
00603  2140-GET-NEXT-MDSELV.                                               CL**5
00604 *                                                                    CL**1
00605      ADD 1                     TO  MB128-ML-SUB.                     CL**1
00606      IF  MB128-ML-SUB GREATER THAN MBWS127-MAX-MLEVELS               CL**2
00607          ADD 1                 TO  MB128-ML-ITEM                     CL**1
00608          MOVE 1                TO  MB128-ML-SUB                      CL**1
00609          PERFORM 2130-READ-LIST-QUEUE                                CL*16
00610          MOVE MDSELV-LIST-RECORD                                     CL*16
00611                                TO  MB128-SAVE-TS-QUEUE               CL*16
00612      ELSE                                                            CL*16
00613          MOVE MB128-SAVE-TS-QUEUE                                    CL*16
00614                                TO  MDSELV-LIST-RECORD                CL*16
00615      END-IF.                                                         CL**1
00616 *                                                                    CL**1
00617      IF  MBWS127-MLEVEL (MB128-ML-SUB) GREATER THAN SPACES           CL**2
00618          MOVE SPACES           TO  DCLTMDSELV                        CL*20
00619          MOVE MBWS127-MLEVEL (MB128-ML-SUB)                          CL**2
00620                                TO  MDSELV-CDE                        CL**1
00621          PERFORM 2100-RETRIEVE-DATA-FOR-PANEL                        CL**5
00622          MOVE DP015-GREEN      TO  ALVLDESC                          CL*22
00623          MOVE DP015-UNDERLINE  TO  ALVLDESH                          CL*22
00624      ELSE                                                            CL**1
00625          SUBTRACT 1          FROM  MB128-ML-SUB                      CL*17
00626          MOVE PC-TSYMSG-00070  TO  DP020-MSG-NUMBER                  CL*18
00627          SET DP020-MSG-INFORMATIONAL                                 CL*18
00628                                TO  TRUE                              CL*18
00629      END-IF.                                                         CL**1
00630 *                                                                    CL**1
00631  EJECT                                                               CL**1
00632 ******************************************************************   CL**1
00633 *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.          CL**1
00634 *                                                                    CL**1
00635 *    PERFORMED BY 1000-CONTROL-PROCESSING.                           CL**5
00636 *    PERFORMED BY 3000-PROCESS-PANEL.                                CL**5
00637 ******************************************************************   CL**1
00638 *                                                                    CL**1
00639  2200-DISPLAY-PANEL.                                                 CL**5
00640 *                                                                    CL**4
00641      PERFORM DP017-0000-GET-CURR-DATE-TIME.                          CL**1
00642      MOVE DP017-EDITED-TIME-HMS                                      CL**5
00643                                TO  ATIMEO.                           CL**5
00644 *                                                                    CL**1
00645      IF  NOT DP020-MSG-FATAL                                         CL**4
00646          MOVE -1               TO  ALVLDESL                          CL*12
00647      END-IF.                                                         CL**4
00648 *                                                                    CL**1
00649      IF  PS-SEND-MAP-N-DATA-FRSET                                    CL**1
00650          PERFORM DP002-0000-FORMAT-TITLE-LINES                       CL**1
00651          PERFORM DP009-0000-LOAD-FK-LINES                            CL**1
00652      END-IF.                                                         CL**1
00653 *                                                                    CL**1
00654      PERFORM DP005-0000-FORMAT-MSG-LINES.                            CL**1
00655                                                                      CL**1
00656      PERFORM DP008-0000-WRITE-PANEL.                                 CL**1
00657 *                                                                    CL**5
00658  EJECT                                                               CL**5
00659 ******************************************************************   CL**5
00660 *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED     CL**5
00661 *    ON WHAT FUNCTION KEY WAS SELECTED.                              CL**5
00662 *                                                                    CL**5
00663 *    PERFORMED BY 1000-CONTROL-PROCESSING.                           CL**5
00664 ******************************************************************   CL**5
00665 *                                                                    CL**5
00666  3000-PROCESS-PANEL.                                                 CL**5
00667 *                                                                    CL**5
00668      PERFORM DP007-0000-GET-PANEL.                                   CL**5
00669      SET PS-SEND-DATA-ONLY-FRSET                                     CL**5
00670                                TO TRUE.                              CL**5
00671 *                                                                    CL**8
00672      IF  (DP020-SRC-AID NOT EQUAL DP016-PF5                          CL**8
00673      AND  DP020-SRC-AID NOT EQUAL DP016-CLEAR)                       CL**8
00674          PERFORM 3200-MOVE-SCREEN-TO-COMMAREA                        CL**8
00675      END-IF.                                                         CL**8
00676 *                                                                    CL**8
00677      PERFORM 3100-CHECK-LOCAL-FKEYS.                                 CL**8
00678 *                                                                    CL**8
00679  EJECT                                                               CL**8
00680 ******************************************************************   CL**8
00681 *   DETERMINE THE PROCESSING PATH HERE.                              CL**8
00682 *                                                                    CL**8
00683 *   PERFORMED BY 3000.                                               CL**8
00684 ******************************************************************   CL**8
00685 *                                                                    CL**8
00686  3100-CHECK-LOCAL-FKEYS.                                             CL**8
00687 *                                                                    CL**8
00688      EVALUATE TRUE                                                   CL**5
00689 *                                                                    CL**5
00690          WHEN DP020-SRC-AID = DP016-CLEAR                            CL**5
00691              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                    CL**5
00692              PERFORM 3400-MOVE-COMMAREA-TO-SCREEN                    CL**5
00693              PERFORM 3300-EDIT-DATA-IN-COMMAREA                      CL**5
00694              PERFORM 2200-DISPLAY-PANEL                              CL**5
00695 *                                                                    CL**5
00696          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                        CL**5
00697              MOVE SPACES       TO  ANAVCMDO                          CL*27
00698                                    ANAVKEYO                          CL*27
00699              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                    CL*10
00700              MOVE PC-COMPANY   TO  AOPERCOO                          CL*12
00701              MOVE MB128-LAST-MLEVEL                                  CL**5
00702                                TO  MDSELV-CDE                        CL**5
00703              PERFORM 2100-RETRIEVE-DATA-FOR-PANEL                    CL**5
00704              PERFORM 2200-DISPLAY-PANEL                              CL**5
00705 *                                                                    CL**5
00706 *    "NEXT" PROCESSING - POSITION TO NEXT DEPARTMENT DISCOUNT CODE   CL**5
00707 *                                                                    CL**5
00708          WHEN DP020-FK-NEXT-SELECTION(DP020-SRC-AID)                 CL*19
00709              PERFORM 2140-GET-NEXT-MDSELV                            CL**5
00710              PERFORM 2200-DISPLAY-PANEL                              CL*18
00711 *                                                                    CL**5
00712          WHEN DP020-SRC-AID = DP016-PA2                              CL**5
00713              PERFORM DP009-0000-LOAD-FK-LINES                        CL**5
00714              PERFORM DP017-0000-GET-CURR-DATE-TIME                   CL**5
00715              SET PS-SEND-DATA-ONLY                                   CL**5
00716                                TO  TRUE                              CL**5
00717              MOVE -1           TO  ANAVCMDL                          CL**5
00718              PERFORM DP008-0000-WRITE-PANEL                          CL**5
00719 *                                                                    CL**5
00720          WHEN DP020-FK-RETURN(DP020-SRC-AID)                         CL**5
00721               PERFORM 4000-NAVIGATE                                  CL*16
00722 *                                                                    CL*16
00723          WHEN DP020-FK-GO-TO-TRAN(DP020-SRC-AID)                     CL*13
00724               PERFORM 4000-NAVIGATE                                  CL**5
00725 *                                                                    CL**5
00726          WHEN DP020-SRC-AID = DP016-ENTER                            CL**5
00727              IF  DP020-NAV-MNEMONIC GREATER THAN SPACES              CL**5
00728                  PERFORM 4000-NAVIGATE                               CL**5
00729              ELSE                                                    CL**5
00730                  PERFORM 3300-EDIT-DATA-IN-COMMAREA                  CL**8
00731                  PERFORM 2200-DISPLAY-PANEL                          CL**5
00732              END-IF                                                  CL**5
00733 *                                                                    CL**5
00734 *    UPDATE FUNCTION                                                 CL**5
00735 *                                                                    CL**5
00736          WHEN DP020-SRC-AID = DP016-PF15                             CL**5
00737              PERFORM 3300-EDIT-DATA-IN-COMMAREA                      CL**8
00738              IF  CONTINUE-PROCESSING                                 CL**5
00739                  PERFORM 2120-UPDATE-MDSELV                          CL**5
00740              END-IF                                                  CL**5
00741              PERFORM 2200-DISPLAY-PANEL                              CL**5
00742 *                                                                    CL**5
00743          WHEN OTHER                                                  CL**5
00744              PERFORM 3300-EDIT-DATA-IN-COMMAREA                      CL*26
00745              MOVE PC-TSYMSG-00045                                    CL**9
00746                                TO  DP020-MSG-NUMBER                  CL**9
00747              SET DP020-MSG-FATAL                                     CL**9
00748                                TO  TRUE                              CL**9
00749              MOVE -1           TO  ANAVCMDL                          CL*10
00750              PERFORM 2200-DISPLAY-PANEL                              CL**5
00751      END-EVALUATE.                                                   CL**5
00752 *                                                                    CL**5
00753  EJECT                                                               CL**5
00754 ******************************************************************   CL**5
00755 *    HERE WE SIMPLY MOVE THE SCREEN DATA TO THE COMM AREA.           CL**8
00756 *                                                                    CL**5
00757 *    PERFORMED BY 3000                                               CL**5
00758 ******************************************************************   CL**5
00759 *                                                                    CL**5
00760  3200-MOVE-SCREEN-TO-COMMAREA.                                       CL**5
00761 *                                                                    CL**5
00762      IF (ALVLDESL GREATER THAN ZERO                                  CL*12
00763      OR  ALVLDESA EQUAL PC-ERASE-EOF)                                CL*12
00764          MOVE ALVLDESI         TO  MB128-LAST-MDESC                  CL*12
00765      END-IF.                                                         CL**5
00766 *                                                                    CL**5
00767  EJECT                                                               CL**5
00768 ******************************************************************   CL**5
00769 *    ENSURE THE DESCRIPTION IS NOT SPACES                            CL**8
00770 *                                                                    CL**5
00771 *    PERFORMED BY 1000, 3000.                                        CL*10
00772 ******************************************************************   CL**5
00773 *                                                                    CL**5
00774  3300-EDIT-DATA-IN-COMMAREA.                                         CL**5
00775 *                                                                    CL**5
00776      IF  MB128-LAST-MDESC GREATER THAN SPACES                        CL**5
00777          MOVE DP015-GREEN      TO  ALVLDESC                          CL*12
00778          MOVE DP015-UNDERLINE  TO  ALVLDESH                          CL*12
00779      ELSE                                                            CL**5
00780          MOVE DP015-RED        TO  ALVLDESC                          CL*12
00781          MOVE DP015-REVERSE    TO  ALVLDESH                          CL*12
00782          MOVE -1               TO  ALVLDESL                          CL*15
00783          SET DP020-MSG-FATAL   TO  TRUE                              CL**5
00784          SET STOP-PROCESSING   TO  TRUE                              CL**5
00785          MOVE PC-TSYMSG-00005  TO  DP020-MSG-NUMBER                  CL**5
00786      END-IF.                                                         CL**5
00787 *                                                                    CL**5
00788  EJECT                                                               CL**5
00789 ******************************************************************   CL**5
00790 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.              CL**5
00791 *                                                                    CL**5
00792 *    PERFORMED BY 2000, 2140.                                        CL**5
00793 ******************************************************************   CL**5
00794 *                                                                    CL**5
00795  3400-MOVE-COMMAREA-TO-SCREEN.                                       CL**5
00796 *                                                                    CL**5
00797      MOVE MB128-LAST-MDESC     TO  ALVLDESO.                         CL*12
00798      MOVE MB128-LAST-PDESC     TO  APRTDESO.                         CL*12
00799      MOVE MB128-LAST-MLEVEL    TO  AMLEVELO.                         CL*12
00800      MOVE MB128-LAST-PLEVEL    TO  ALVLPRTO.                         CL*12
00801      MOVE PC-COMPANY           TO  AOPERCOO.                         CL*12
00802 *                                                                    CL**5
00803  EJECT                                                               CL**5
00804 ******************************************************************   CL**5
00805 *    IF THE USER HITS A KEY THAT REQUIRES US TO GO TO ANOTHER        CL**5
00806 *    TRANSACTION, SAVE THE INTER APPLICATION COMMUNICATIONS AREA     CL**5
00807 *    AND START THE NAVIGATOR.                                        CL**5
00808 *                                                                    CL**5
00809 *    PERFORMED BY 3000                                               CL**5
00810 ******************************************************************   CL**5
00811 *                                                                    CL**5
00812  4000-NAVIGATE.                                                      CL**5
00813 *                                                                    CL**5
00814      COMPUTE MB005-NUMBER-PROCESSED-ITEMS =                          CL*17
00815            ((MB128-ML-ITEM - 1) * MBWS127-MAX-MLEVELS) +             CL*17
00816              MB128-ML-SUB.                                           CL*17
00817 *                                                                    CL*17
00818      MOVE MB005-INTER-APPL-COMMAREA                                  CL**5
00819                                TO  MB128-SAVE-INTER-APPL-COMMAREA.   CL**5
00820      PERFORM DP015-1000-START-NAVIGATOR.                             CL**5
00821 *                                                                    CL*23
00822  EJECT                                                               CL*23
00823 ******************************************************************   CL*23
00824 *    ENSURE PROPER ENTRY TO THIS PROGRAM.                            CL*23
00825 *                                                                    CL*23
00826 *    PERFORMED BY 1000                                               CL*23
00827 ******************************************************************   CL*23
00828 *                                                                    CL*23
00829  4100-VALIDATE-COMMAREA.                                             CL*23
00830 *                                                                    CL*23
00831      IF  DP020-MB-MERCH-LEVEL-LIST                                   CL*23
00832          SET PS-INTER-APPL-VALID                                     CL*23
00833                                TO  TRUE                              CL*23
00834      ELSE                                                            CL*23
00835          SET PS-INTER-APPL-NOT-VALID                                 CL*23
00836                                TO  TRUE                              CL*23
00837      END-IF.                                                         CL*23
00838 *                                                                    CL**1
00839  EJECT                                                               CL**1
00840 ******************************************************************   CL**1
00841 *    FILL IN SCREEN HEADER MODULE                                    CL**1
00842 ******************************************************************   CL**1
00843                                                                      CL**1
00844      COPY DPPD002.                                                   CL**1
00845  EJECT                                                               CL**1
00846 ******************************************************************   CL**1
00847 *    RETURN TO CICS MODULE                                           CL**1
00848 ******************************************************************   CL**1
00849                                                                      CL**1
00850      COPY DPPD003.                                                   CL**1
00851  EJECT                                                               CL**1
00852 ******************************************************************   CL**1
00853 *    FORMAT MESSAGE LINES MODULE                                     CL**1
00854 ******************************************************************   CL**1
00855                                                                      CL**1
00856      COPY DPPD005.                                                   CL**1
00857  EJECT                                                               CL**1
00858 ******************************************************************   CL**1
00859 *    GET PANEL MODULE                                                CL**1
00860 ******************************************************************   CL**1
00861                                                                      CL**1
00862      COPY DPPD007.                                                   CL**1
00863  EJECT                                                               CL**1
00864 ******************************************************************   CL**1
00865 *    WRITE PANEL MODULE                                              CL**1
00866 ******************************************************************   CL**1
00867                                                                      CL**1
00868      COPY DPPD008.                                                   CL**1
00869  EJECT                                                               CL**1
00870 ******************************************************************   CL**1
00871 *    TOGGLE THE FUNCTION KEY LINE MODULE                             CL**1
00872 ******************************************************************   CL**1
00873                                                                      CL**1
00874      COPY DPPD009.                                                   CL**1
00875  EJECT                                                               CL**1
00876 ******************************************************************   CL**1
00877 *    ABEND PROCESSOR MODULE                                          CL**1
00878 ******************************************************************   CL**1
00879                                                                      CL**1
00880      COPY DPPD013.                                                   CL**1
00881  EJECT                                                               CL**1
00882 ******************************************************************   CL**1
00883 *    CHECK COMMUNICATION AREA MODULE                                 CL**1
00884 ******************************************************************   CL**1
00885                                                                      CL**1
00886      COPY DPPD015.                                                   CL**1
00887  EJECT                                                               CL**1
00888 ******************************************************************   CL**1
00889 *    UPDATE TIME AND DATE MODULE                                     CL**1
00890 ******************************************************************   CL**1
00891                                                                      CL**1
00892      COPY DPPD017.                                                   CL**1
00893  EJECT                                                               CL**1
