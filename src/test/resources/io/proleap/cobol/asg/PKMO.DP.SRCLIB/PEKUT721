       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PEKUT721.                                        
       AUTHOR.                 COGNIZANT.                                       
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           26/12/2010.                                      
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * Remarks                                                        *        
      * -------                                                        *        
      *                                                                *        
      * Title        - Perm clearance report -Email job creation       *        
      *                Program                                         *        
      *                                                                *        
      * Description  - This Program will create and submit jobs that   *        
      *                will be send exception details to Byer group    *        
      *                                                                *        
      * WARNING: This program should be kept in lower case due to the  *        
      * need for lower case text in emails.                            *        
      *                                                                *        
      * Change History                                                 *        
      ******************************************************************        
      * WR Number     Programmer       Date                            *        
      * ---------------------------------------------------------------*        
      * WR34795       COGNIZANT        26/12/2010                               
      * Original Version                                               *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PERM-RPT-BYR-FILE ASSIGN TO INP01                             
                                    FILE STATUS IS FV-BYR-FILE-STATUS.          
                                                                                
           SELECT EMAIL-MSG-FILE    ASSIGN TO OUT02.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  PERM-RPT-BYR-FILE                                                    
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS PERM-RPT-BYR-REC.                                     
       01  PERM-RPT-BYR-REC                 PIC X(80).                          
                                                                                
       FD  EMAIL-MSG-FILE                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           DATA RECORD IS EMAIL-MSG-FILE-RECORD.                                
       01  EMAIL-MSG-FILE-RECORD           PIC X(80).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                    PIC S9(04) COMP VALUE +0.              
           88  ABEND-4001-WRITE-ERROR                VALUE +4001.               
           88  ABEND-4002-READ-ERROR                 VALUE +4002.               
           88  ABEND-4003-CTRL-CARD-ERROR            VALUE +4003.               
           88  ABEND-4004-TABLE-ERROR                VALUE +4004.               
           88  ABEND-4005-OPEN-ERROR                 VALUE +4005.               
           88  ABEND-4006-CLOSE-ERROR                VALUE +4006.               
           88  ABEND-4007-SEQUENCE-ERROR             VALUE +4007.               
           88  ABEND-4008-DATA-ERROR                 VALUE +4008.               
           88  ABEND-4009-KEY-DATA-ERROR             VALUE +4009.               
           88  ABEND-4013-SQL-ERROR                  VALUE +4013.               
           88  ABEND-4014-ORACLE-LOGON-ERROR         VALUE +4014.               
           88  ABEND-4019-DATE-ROUTINE-ERROR         VALUE +4019.               
           88  ABEND-4444-FORCED-ABEND               VALUE +4444.               
                                                                                
      *****************************************************************         
      *  File Varialble declaration                                   *         
      *****************************************************************         
       01  FV-FILE-VARIABLES.                                                   
           05  FV-BYR-FILE-STATUS           PIC  X(02).                         
                                                                                
      *****************************************************************         
      *  Switches                                                      *        
      *****************************************************************         
       01  PS-RVOGRAM-SWITCHES.                                                 
           05  PS-PERM-BYR-FILE-SW          PIC  X(01) VALUE 'N'.               
               88  EOF-PERM-BYR-FILE                   VALUE 'Y'.               
               88  NOT-EOF-PERM-BYR-FILE               VALUE 'N'.               
                                                                                
      *****************************************************************         
      *  Program variable                                              *        
      *****************************************************************         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08) VALUE                    
                                                       'PEKUT721'.              
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.            
           05  PV-DPT-CNT                   PIC  9(04) VALUE ZEROES.            
           05  PV-SS-SUB1                   PIC  9(04) VALUE ZEROES.            
           05  PV-PREV-BYR-NBR              PIC  X(03) VALUE SPACES.            
           05  PV-REC-READ-CNT              PIC S9(04) VALUE +0 COMP.           
           05  PV-OPERATION-MSG-1           PIC  X(40) VALUE SPACES.            
           05  PV-OPERATION-MSG-2           PIC  X(40) VALUE SPACES.            
                                                                                
           05  PV-PERM-RPT-FILE.                                                
               10  PV-BYR-NBR               PIC  X(03) VALUE SPACES.            
               10  PV-DEPT-NBR              PIC  X(03) VALUE SPACES.            
                                                                                
           05  PV-DEPT-ARRAY.                                                   
               10  PV-TB-DEPT-NBR           PIC  X(03)                          
                                                 OCCURS 1000 TIMES.             
      *----------------------------------------------------------               
      * EM - EMAIL MESSAGE DATA LINES                                           
      *----------------------------------------------------------               
       01  EM-EMAIL-MESSAGE-DATA-LINES.                                         
           05  EM-DATA-LINE-1               PIC X(80)  VALUE                    
               'HELO KOHLS@KOHLS.COM'.                                          
           05  EM-DATA-LINE-2               PIC X(80)  VALUE                    
               'MAIL FROM: <MIO-PermClearanceReport@KOHLS.COM>'.                
           05  EM-DATA-LINE-3.                                                  
               10  EM-DATA-LINE-3-PT1       PIC X(10)  VALUE                    
               'RCPT TO: <'.                                                    
               10  EM-DATA-LINE-3-PT2       PIC X(06)  VALUE                    
               'BUYER-'.                                                        
               10  EM-DATA-LINE-3-BYR       PIC 9(04)  VALUE ZEROES.            
               10  EM-DATA-LINE-3-PT3       PIC X(11)  VALUE                    
               '@KOHLS.COM>'.                                                   
               10  FILLER                   PIC X(49) VALUE SPACES.             
                                                                                
           05  EM-DATA-LINE-4               PIC X(80)  VALUE                    
               'DATA'.                                                          
           05  EM-DATA-LINE-5.                                                  
               10  EM-DATA-LINE-5-PT1       PIC X(04)  VALUE                    
               'TO: '.                                                          
               10  EM-DATA-LINE-5-PT2       PIC X(06)  VALUE                    
               'BUYER-'.                                                        
               10  EM-DATA-LINE-5-BYR       PIC 9(04)  VALUE ZEROES.            
               10  FILLER                   PIC X(66) VALUE SPACES.             
           05  EM-DATA-LINE-6               PIC X(80)  VALUE                    
               'SUBJECT: Clearance Exception Report - ACTION REQUIRED'.         
           05  EM-DATA-LINE-7.                                                  
               10  FILLER                   PIC  X(48) VALUE                    
               'The following department(s) have one or more SKU'.              
               10  FILLER                   PIC  X(01) VALUE QUOTE.             
               10  FILLER                   PIC  X(05) VALUE                    
               's on '.                                                         
               10  FILLER                   PIC  X(14) VALUE                    
               'the Clearance '.                                                
               10  FILLER                   PIC  X(12) VALUE SPACES.            
           05  EM-DATA-LINE-8.                                                  
               10  FILLER                   PIC  X(18) VALUE                    
               'Exception Report :'.                                            
               10  FILLER                   PIC  X(62) VALUE SPACES.            
           05  EM-DATA-LINE-9.                                                  
               10  EM-DATA-DEPT-NBR         PIC  9(03) VALUE ZEROES.            
               10  FILLER                   PIC  X(77) VALUE SPACES.            
           05  EM-DATA-LINE-10.                                                 
               10  FILLER                   PIC  X(46) VALUE                    
               'There are three reasons why SKUs could appear:'.                
               10  FILLER                   PIC  X(34) VALUE SPACES.            
           05  EM-DATA-LINE-11.                                                 
               10  FILLER                   PIC  X(49) VALUE                    
               '1)  Reason Code CE1: SKUs are on a Clearance MK1 '.             
               10  FILLER                   PIC  X(18) VALUE                    
               'document within 30'.                                            
               10  FILLER                   PIC  X(13) VALUE SPACES.            
           05  EM-DATA-LINE-12.                                                 
               10  FILLER                   PIC  X(15) VALUE                    
               'days of the SKU'.                                               
               10  FILLER                   PIC  X(01) VALUE QUOTE.             
               10  FILLER                   PIC  X(15) VALUE                    
               's last receipt.'.                                               
               10  FILLER                   PIC  X(51) VALUE SPACES.            
           05  EM-DATA-LINE-13.                                                 
               10  FILLER                   PIC  X(50) VALUE                    
               '2)  Reason Code CE2: SKUs have on order after the '.            
               10  FILLER                   PIC  X(13) VALUE                    
               'Clearance MK1'.                                                 
               10  FILLER                   PIC  X(13) VALUE SPACES.            
           05  EM-DATA-LINE-14.                                                 
               10  FILLER                   PIC  X(15) VALUE                    
               'effective date.'.                                               
               10  FILLER                   PIC  X(65) VALUE SPACES.            
           05  EM-DATA-LINE-15.                                                 
               10  FILLER                   PIC  X(50) VALUE                    
               '3)  Reason Code CE3: SKUs are clearanced, have on '.            
               10  FILLER                   PIC  X(24) VALUE                    
               'Order and are not on an '.                                      
               10  FILLER                   PIC  X(06) VALUE SPACES.            
           05  EM-DATA-LINE-16.                                                 
               10  FILLER                   PIC  X(44) VALUE                    
               'approved CTA (Clearance To Active) document '.                  
               10  FILLER                   PIC  X(28) VALUE                    
               'prior to on order ship date.'.                                  
               10  FILLER                   PIC  X(08) VALUE SPACES.            
           05  EM-DATA-LINE-17.                                                 
               10  FILLER                   PIC  X(54) VALUE                    
               'Please open report number PEK11505 in Document Direct '.        
               10  FILLER                   PIC  X(16) VALUE                    
               'for the details.'.                                              
               10  FILLER                   PIC  X(10) VALUE SPACES.            
           05  EM-DATA-LINE-18.                                                 
               10  FILLER                   PIC  X(50) VALUE                    
               'Please direct all questions regarding this report '.            
               10  FILLER                   PIC  X(17) VALUE                    
               'to the mail group'.                                             
               10  FILLER                   PIC  X(13) VALUE SPACES.            
           05  EM-DATA-LINE-19.                                                 
               10  FILLER                   PIC  X(26) VALUE                    
               'MIO-PermClearanceReport.'.                                      
               10  FILLER                   PIC  X(54) VALUE SPACES.            
           05  EM-DATA-LINE-20.                                                 
               10  FILLER                   PIC  X(17) VALUE                    
               '                 '.                                             
               10  FILLER                   PIC  X(63) VALUE SPACES.            
           05  EM-DATA-LINE-21.                                                 
               10  FILLER                   PIC  X(17) VALUE                    
               '                 '.                                             
               10  FILLER                   PIC  X(63) VALUE SPACES.            
           05  EM-NULL-LINE                 PIC  X(80) VALUE SPACES.            
                                                                                
      *----------------------------------------------------------               
      *  PARAMETER LIST FOR THE DYNAMIC ALLOCATION ROUTINE                      
      *----------------------------------------------------------               
           COPY DPWS023.                                                        
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      * Description     : Control main processing flow                *         
      *****************************************************************         
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 0500-INITIALISATION.                                         
                                                                                
           PERFORM 1000-MAIN-PROCESS                                            
             UNTIL EOF-PERM-BYR-FILE.                                           
                                                                                
           PERFORM 9000-TERMINATE.                                              
           STOP RUN.                                                            
                                                                                
       0500-INITIALISATION.                                                     
      *****************************************************************         
      * Description     : Initialise fields and working storage       *         
      *****************************************************************         
                                                                                
           INITIALIZE                       PV-PREV-BYR-NBR                     
                                            PV-BYR-NBR                          
                                                                                
           OPEN INPUT  PERM-RPT-BYR-FILE                                        
                                                                                
           PERFORM 2000-READ-PERM-RPT-BYR-FILE.                                 
                                                                                
       1000-MAIN-PROCESS.                                                       
      *****************************************************************         
      * Description     : Main Processing                             *         
      *****************************************************************         
                                                                                
           MOVE "*1000-MAIN-PROCESS *"     TO PV-PARAGRAPH-NAME                 
                                                                                
           IF  PV-BYR-NBR      NOT = PV-PREV-BYR-NBR                            
           AND PV-REC-READ-CNT NOT = 1                                          
               PERFORM 1100-CREATE-BYR-EMAIL-STEP                               
               INITIALIZE                     PV-DEPT-ARRAY                     
               MOVE ZEROES                 TO PV-DPT-CNT                        
           END-IF                                                               
                                                                                
           ADD +1                          TO PV-DPT-CNT                        
           MOVE PV-DEPT-NBR                TO PV-TB-DEPT-NBR(PV-DPT-CNT)        
           MOVE PV-BYR-NBR                 TO PV-PREV-BYR-NBR                   
           INITIALIZE                         PV-BYR-NBR                        
                                              PV-DEPT-NBR                       
           PERFORM 2000-READ-PERM-RPT-BYR-FILE.                                 
                                                                                
      ******************************************************************        
      * Description     : Create buyer Email to list of buyers        *         
      ******************************************************************        
       1100-CREATE-BYR-EMAIL-STEP.                                              
                                                                                
           MOVE PV-PREV-BYR-NBR            TO EM-DATA-LINE-3-BYR                
                                              EM-DATA-LINE-5-BYR                
           INITIALIZE                         DP023-DYNALC-PARMS                
                                              DP023-RECOUNT.                    
           MOVE 'OUT02'                    TO DP023-DDNAME.                     
           SET DP023-DSNAME-SYSOUT         TO TRUE.                             
           SET DP023-FREE-CLOSE-YES        TO TRUE.                             
           SET DP023-RECFM-FIXED-BLOCK     TO TRUE.                             
           MOVE 80                         TO DP023-RECSZ.                      
           COMPUTE DP023-RECOUNT = PV-DPT-CNT + 26                              
           END-COMPUTE.                                                         
           MOVE 'B'                        TO DP023-PRINT-CLASS.                
           MOVE 'SMTP'                     TO DP023-PRINT-WRITER.               
           PERFORM DP023-0000-DYNALC.                                           
                                                                                
           OPEN OUTPUT EMAIL-MSG-FILE.                                          
                                                                                
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-1.                   
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-2.                   
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-3.                   
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-4.                   
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-5.                   
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-6.                   
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-7.                   
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-8.                   
                                                                                
           PERFORM VARYING PV-SS-SUB1                                           
                      FROM 1 By 1                                               
                     UNTIL PV-SS-SUB1 > PV-DPT-CNT                              
                        OR PV-SS-SUB1 > 1000                                    
               INITIALIZE                     EM-DATA-DEPT-NBR                  
               MOVE PV-TB-DEPT-NBR(PV-SS-SUB1)                                  
                                           TO EM-DATA-DEPT-NBR                  
               WRITE EMAIL-MSG-FILE-RECORD FROM EM-DATA-LINE-9                  
           END-PERFORM                                                          
                                                                                
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-NULL-LINE.                     
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-10.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-NULL-LINE.                     
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-11.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-12.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-13.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-14.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-15.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-16.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-NULL-LINE.                     
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-17.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-NULL-LINE.                     
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-18.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-19.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-20.                  
           WRITE EMAIL-MSG-FILE-RECORD   FROM EM-DATA-LINE-21.                  
                                                                                
           CLOSE EMAIL-MSG-FILE.                                                
                                                                                
      ******************************************************************        
      * Description     : Read PERM RPT Buyer file                    *         
      ******************************************************************        
       2000-READ-PERM-RPT-BYR-FILE.                                             
                                                                                
           MOVE '*2000-READ-PERM-RPT-BYR-FILE'                                  
                                           TO PV-PARAGRAPH-NAME.                
           READ PERM-RPT-BYR-FILE        INTO PV-PERM-RPT-FILE                  
             AT END SET EOF-PERM-BYR-FILE  TO TRUE                              
           END-READ.                                                            
                                                                                
           EVALUATE FV-BYR-FILE-STATUS                                          
               WHEN '00'                                                        
               WHEN '10'                                                        
                    IF  NOT-EOF-PERM-BYR-FILE                                   
                        ADD +1             TO PV-REC-READ-CNT                   
                    END-IF                                                      
               WHEN OTHER                                                       
                    DISPLAY '** INVALID INPUT FILE **'                          
                    MOVE 'PEKUT721'        TO PC-CURRENT-PROGRAM-NAME           
                    MOVE 'READING FILE PERM-RPT-BYR-FILE'                       
                                           TO PV-OPERATION-MSG-1                
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *-----------------------------------------------------------------        
      * 9000-TERMINATE - CLOSES THE FILES ASSOCIATED WITH THE JOB.              
      *-----------------------------------------------------------------        
       9000-TERMINATE.                                                          
                                                                                
           IF  EOF-PERM-BYR-FILE                                                
           AND PV-REC-READ-CNT NOT = 0                                          
               PERFORM 1100-CREATE-BYR-EMAIL-STEP                               
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '============================================='.             
           DISPLAY ' '.                                                         
           DISPLAY ' ******  PEKUT721 PROCESSING SUMMARY  ******  '.            
           PERFORM 9100-DISPLAY-PROCESSING-INFO.                                
           DISPLAY '============================================='.             
                                                                                
           CLOSE                               PERM-RPT-BYR-FILE.               
                                                                                
      *-----------------------------------------------------------------        
      * DISPLAY PROCESSED RECORDS COUNTS                                        
      *-----------------------------------------------------------------        
       9100-DISPLAY-PROCESSING-INFO.                                            
                                                                                
            DISPLAY ' '.                                                        
            DISPLAY ' INPUT RECORD COUNT     = ' PV-REC-READ-CNT.               
            DISPLAY ' '.                                                        
                                                                                
      ************************************************************              
      * Z100-ABEND.                                                             
      *     PERFORMS A NON-SQL-ABEND.                                           
      ************************************************************              
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
      *----------------------------------------------------------               
      *  DYNAMIC ALLOCATION ROUTINE                                             
      *----------------------------------------------------------               
           COPY DPPD023.                                                        
      * This contains DP023-0000-DYNALC para.                                   
                                                                                
