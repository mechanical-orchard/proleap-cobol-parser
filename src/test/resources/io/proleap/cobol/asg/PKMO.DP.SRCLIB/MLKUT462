                                                                                
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.     MLKUT462.                                                
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   08/13/2002.                                              
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * NOTE:  QUICK AND DIRTY CHANGE WAS MADE TO THIS FOR A REVERSAL           
      *        THAT WAS NEEDED.  THIS HAS NOT BEEN FULLY TESTED.                
      *                                                                         
      ******************************************************************        
      *PURPOSE:                                                                 
      *  THE MODULE WILL TAKE AN INPUT FILE IN THE MLRD017 LAYOUT AND           
      *  PRODUCES ANOTHER FILE FOR REVERSAL OF OUR MERCHANDISE LEDGER           
      *                                                                         
      * CHANGE HISTORY:                                                         
      * CHG ID/                                                                 
      * WR/PROJ DATE       PGMR             DESCRIPTION OF CHANGES              
      * ------- ---------- -------------    ------------------                  
      * ??????  04-18-2002                                                      
      *                MAKE THIS MEMBER JUST A DYLE SCRIPT ONLY                 
      *                TAKE OUT THE SURROUNDING JCL.                            
      *                USE THE COPYBOOK MLRD012O INSTEAD OF HARD-CODING         
      *                THE POSITION OF THE FIELDS.                              
      *                EXPAND THE TALLYING FIELDS TO 11 DIGITS TO               
      *                ELIMINATE TRUNCATION.                                    
      *                ADD CODE TO CHECK FOR ML012O-MKDN-MKON-CATEGORY          
      *                OF '1' OR '2' AND NOT EXTEND THE PRICE CHANGES,          
      *                BECAUSE THESE TRANSACTIONS ALREADY EXTENDED PRICE        
      * 999999  08-13-2002 R. JANSSEN TURN THE DYL280 MODULE DYLPRICE           
      *                               INTO A COBOL PGM                          
W26603* W26603  10-11-2004 R. JANSSEN MERCH LEDGER STORE NMBR EXPANSION         
W29145* W29145  08-25-2006 L. YANG EXPAND FOR SAS                               
RONNA * W29145  07-25-2012 R. MCDONALD - ML017-PC-TYP-CDE IS NOT                
RONNA *                GETTING REVERSED.  FIXING IT SO IT DOES.                 
RONNA1*                PRICE EXPANSION FROM 9999.99 TO 99999.99                 
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT LEDGER-INPUT-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT LEDGER-OUTPUT-FILE                                            
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  LEDGER-INPUT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
W29145 01  LEDGER-INPUT-RECORD      PIC X(200).                                 
                                                                                
       FD  LEDGER-OUTPUT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
W29145 01  LEDGER-OUTPUT-RECORD     PIC X(200).                                 
                                                                                
      /***************************************************************          
       WORKING-STORAGE SECTION.                                                 
      ****************************************************************          
                                                                                
W29145     COPY MLRD017.                                                        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW    PIC X(01)  VALUE 'N'.                       
               88  PS-END-OF-FILE              VALUE 'Y'.                       
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RECCNT            PIC S9(15) COMP-3 VALUE ZEROS.              
           05  PV-WRITECNT          PIC S9(15) COMP-3 VALUE ZEROS.              
RONNA1     05  PV-WSOLDAMT          PIC 9(5)V99 VALUE ZEROS.                    
RONNA1     05  PV-WSEXTAMT          PIC 9(5)V99 VALUE ZEROS.                    
RONNA1     05  PV-DIFF              PIC 9(5)V99 VALUE ZEROS.                    
                                                                                
      /***************************************************************          
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1200-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS-LOOP                                       
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM 9000-EOJ-TASKS.                                              
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /***************************************************************          
      *1200-INITIALIZATION.                                                     
      ****************************************************************          
       1200-INITIALIZATION.                                                     
                                                                                
           OPEN  INPUT LEDGER-INPUT-FILE                                        
                OUTPUT LEDGER-OUTPUT-FILE.                                      
                                                                                
           MOVE 'N'                 TO PS-END-OF-FILE-SW.                       
           MOVE ZEROS               TO PV-RECCNT.                               
           MOVE ZEROS               TO PV-WRITECNT.                             
           MOVE ZEROS               TO PV-WSOLDAMT.                             
           MOVE ZEROS               TO PV-WSEXTAMT.                             
           MOVE ZEROS               TO PV-DIFF.                                 
                                                                                
           PERFORM 8000-READ-PC-FILE.                                           
                                                                                
      /***************************************************************          
      *2000-MAIN-PROCESS-LOOP.                                                  
      ****************************************************************          
       2000-MAIN-PROCESS-LOOP.                                                  
                                                                                
           ADD 1                    TO PV-RECCNT.                               
                                                                                
      ***  MARKDOWN IS CODE 4 ; MARKUP IS CODE 5                                
           IF ML017-MKDOWN                                                      
               COMPUTE PV-WSEXTAMT  = ML017-PC-OLD-PR-AMT                       
                                    - ML017-PC-NEW-PR-AMT                       
           ELSE                                                                 
               COMPUTE PV-WSEXTAMT  = ML017-PC-NEW-PR-AMT                       
                                    - ML017-PC-OLD-PR-AMT                       
           END-IF.                                                              
                                                                                
      ***  EXTEND THE PRICE, IF CATEGORY IS NOT '1' AND IS NOT '2'              
      ***  TRANSACTIONS WITH CATEGORY OF '1' OR '2' ARE ALREADY                 
      ***  EXTENDED BY POS/COSA.                                                
                                                                                
           IF ML017-UNMATCHED-MKDN-MKON OR                                      
              ML017-BUYER-MKDN-MKON     OR                                      
              ML017-STORE-MKDN-MKON                                             
               COMPUTE PV-WSEXTAMT  = PV-WSEXTAMT                               
                                    * ML017-PC-QTY                              
           END-IF.                                                              
                                                                                
           COMPUTE PV-DIFF          = PV-DIFF + PV-WSEXTAMT.                    
                                                                                
      ***  SWITCH THE PRICES AROUND                                             
           MOVE ML017-PC-OLD-PR-AMT TO PV-WSOLDAMT.                             
           MOVE ML017-PC-NEW-PR-AMT TO ML017-PC-OLD-PR-AMT.                     
           MOVE PV-WSOLDAMT         TO ML017-PC-NEW-PR-AMT.                     
                                                                                
      ***  REVERSE THE ML017-PC-TYP-CDE                                         
RONNA      IF ML017-PC-TYP-CDE = '1'                                            
RONNA         MOVE '2'              TO ML017-PC-TYP-CDE                         
RONNA      ELSE                                                                 
RONNA      IF ML017-PC-TYP-CDE = '2'                                            
RONNA         MOVE '1'              TO ML017-PC-TYP-CDE                         
RONNA      ELSE                                                                 
RONNA      IF ML017-PC-TYP-CDE = '3'                                            
RONNA         MOVE '4'              TO ML017-PC-TYP-CDE                         
RONNA      ELSE                                                                 
RONNA      IF ML017-PC-TYP-CDE = '4'                                            
RONNA         MOVE '3'              TO ML017-PC-TYP-CDE                         
RONNA      END-IF.                                                              
                                                                                
      ***  CREATE THE REVERSAL....                                              
           WRITE LEDGER-OUTPUT-RECORD                                           
                                FROM ML017-PRICE-CHANGE-RECORD.                 
           ADD 1                TO PV-WRITECNT.                                 
                                                                                
      ***  READ NEXT RECORD....                                                 
           PERFORM 8000-READ-PC-FILE.                                           
                                                                                
      ******************************************************************        
      *8000-READ-PC-FILE.                                                       
      * GET THE MBS INFO FROM THE MOST RECENT ROW FOR THE DEPARTMENT.           
      ******************************************************************        
       8000-READ-PC-FILE.                                                       
                                                                                
           READ LEDGER-INPUT-FILE NEXT                                          
               INTO ML017-PRICE-CHANGE-RECORD                                   
                   AT END  MOVE 'Y'     TO PS-END-OF-FILE-SW.                   
                                                                                
      ******************************************************************        
      *9000-EOJ-TASKS.                                                          
      ******************************************************************        
       9000-EOJ-TASKS.                                                          
                                                                                
           DISPLAY '*********************************'                          
           DISPLAY '**  CONTROL SUMMARY - MLKUT462 **'                          
           DISPLAY '* REVERSAL RECS READS   = ', PV-RECCNT                      
           DISPLAY '*               WRITTEN = ', PV-WRITECNT                    
           DISPLAY '*               DIFF    = ', PV-DIFF                        
           DISPLAY '*********************************'                          
                                                                                
           CLOSE LEDGER-INPUT-FILE                                              
                 LEDGER-OUTPUT-FILE.                                            
                                                                                
