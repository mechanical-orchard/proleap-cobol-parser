       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKRP142.                                                 
       AUTHOR.        JEFF HARTIG/COMPUWARE.                                    
       INSTALLATION.  KOHL CORPORATION.                                         
       DATE-WRITTEN.  06/17/98.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *            INVENTORY LEDGER CUTOFF REPORT                               
      * THE REPORT WILL BE PRODUCED AFTER THE LEDGER CUTOFF HAS TAKEN           
      * PLACE.  IT WILL REPORT, BY STORE, THE BOOK VALUE OF                     
      * INVENTORY AT THE TIME THE CUTOFF WAS TAKEN                              
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
W26600* W26600  04-05-2004 ROD JANSSEN INV LEDGER STORE EXPANSION               
W20445* W20445  01-10-2005 PAUL KEBER  DETAILED INVENTORY ADJUSTMENT            
      *                                PROJECT CHANGE FOR PERFORMANCE           
W33304* W33304  10-14-2008 D. THEEL    HILO PROJECT                             
W33304*         THIS REPORT HAS BEEN MODIFIED TO ALLOW FOR PROCESSING           
W33304*         MULTIPLE CYCLES IN THE SAME RUN.  IT WILL TOTAL AND             
W33304*         BREAK BY CYCLE. THE INPUT FILES AND STORE CURSOR ARE            
W33304*         NOW READ INTO WORKING STORAGE TABLES. THE INV ID HAS            
W33304*         ADDED TO THE INVENTORY SUBCLASS FILE WHICH IS NOW               
W33304*         SORTED/SUMMED BY INV ID/STORE.  THE CYCLE NAME AND              
W33304*         CYCLE ID HAVE BEEN ADDED TO THE HEADING.                        
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
W20445     SELECT MNLYSUM-UNLOAD-FILE                                           
W20445         ASSIGN INP01.                                                    
                                                                                
W33304     SELECT INV-SCL-UNLOAD-FILE                                           
W20445         ASSIGN INP02.                                                    
                                                                                
           SELECT LEDGER-CUTOFF-RPT                                             
               ASSIGN RPT01.                                                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
W20445 FD  MNLYSUM-UNLOAD-FILE                                                  
W20445     RECORDING MODE IS F                                                  
W20445     LABEL RECORDS ARE STANDARD                                           
W20445     BLOCK CONTAINS 0 CHARACTERS.                                         
W20445 01  MNLYSUM-UNLOAD-REC    PIC X(9).                                      
                                                                                
W33304 FD  INV-SCL-UNLOAD-FILE                                                  
W20445     RECORDING MODE IS F                                                  
W20445     LABEL RECORDS ARE STANDARD                                           
W20445     BLOCK CONTAINS 0 CHARACTERS.                                         
W33304 01  INV-SCL-UNLOAD-REC    PIC X(13).                                     
                                                                                
       FD  LEDGER-CUTOFF-RPT                                                    
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING  MODE IS F                                                 
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE           PIC X(132).                                    
       EJECT                                                                    
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ***************************************************************           
      *  ERROR MESSAGE AREA FOR DB2                                             
      ***************************************************************           
           COPY DPWS004.                                                        
                                                                                
       01  FT-FINAL-TOTALS.                                                     
           05  FT-LEDGER-TOTAL              PIC S9(11)V99 VALUE ZERO.           
           05  FT-INVENTORY-TOTAL           PIC S9(11)V99 VALUE ZERO.           
                                                                                
       01  PC-PRINT-CONTROL.                                                    
           05  PC-PAGE-COUNT           PIC S9(05)  VALUE ZERO.                  
           05  PC-LINE-COUNT           PIC S9(03)  VALUE +99  COMP-3.           
                                                                                
       01  PC-PROGRAM-COUNTERS.                                                 
           05  PC-MAX-RETRIES                  PIC S9(4)                        
                                               COMP SYNC VALUE +2.              
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
W26600     05  PS-XMTLOC-EOF-SW             PIC X(01)     VALUE 'N'.            
W26600         88  PS-XMTLOC-EOF                          VALUE 'Y'.            
W20445     05  PS-MNLYSUM-UNLOAD-EOF-SW     PIC X(01)     VALUE 'N'.            
W20445         88  PS-MNLYSUM-UNLOAD-NOT-EOF              VALUE 'N'.            
W20445         88  PS-MNLYSUM-UNLOAD-EOF                  VALUE 'Y'.            
W33304     05  PS-INV-SCL-UNLOAD-EOF-SW     PIC X(01)     VALUE 'N'.            
W33304         88  PS-INV-SCL-UNLOAD-NOT-EOF              VALUE 'N'.            
W33304         88  PS-INV-SCL-UNLOAD-EOF                  VALUE 'Y'.            
           05  PS-OPEN-FSCL-MN-EOF-SW       PIC X(01)     VALUE 'N'.            
               88  PS-OPEN-FSCL-MN-EOF                    VALUE 'Y'.            
           05  PS-DONE-WITH-PAGE-SW         PIC X(01)     VALUE 'N'.            
               88  PS-START-PAGE                          VALUE 'N'.            
               88  PS-DONE-WITH-PAGE                      VALUE 'Y'.            
                                                                                
       01  PROGRAM-VARIABLES.                                                   
W20445     05  PV-MNLYSUM-UNLOAD-RECS-READ     PIC S9(09)  VALUE +0             
W20445                                                     COMP.                
W33304     05  PV-INV-SCL-UNLOAD-RECS-READ     PIC S9(09)  VALUE +0             
W20445                                                     COMP.                
W20445     05  PV-DISPLAY-RECS-READ            PIC Z(4)9   VALUE ZERO.          
           05  PV-RETRY-COUNT                  PIC S9(4)                        
                                               COMP SYNC   VALUE +0.            
           05  PV-END-INV-RTL-AMT-SUM       PIC S9(11)V99 COMP-3                
                                                           VALUE +0.            
           05  PV-CTOFF-END-INV-AMT-SUM     PIC S9(11)V99 COMP-3                
                                                           VALUE +0.            
           05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE.         
           05  PV-DB2-OPER-ATTEMPTED           PIC X(30)   VALUE SPACE.         
           05  PV-PRT-CC                       PIC  X(1)   VALUE SPACES.        
W33304     05  PV-TEMP-NBR-10-COMP     PIC S9(10) COMP VALUE ZERO.              
W33304     05  PV-TEMP-NBR-10-NUM      PIC 9(10)   VALUE ZERO.                  
W33304     05  FILLER REDEFINES  PV-TEMP-NBR-10-NUM.                            
W33304         10  FILLER        PIC 9(05).                                     
W33304         10  PV-REPORT-INV-ID                                             
W33304                           PIC 9(05).                                     
W33304     05  PV-HOLD-CYCLE             PIC S9(9)     VALUE +0 COMP.           
W33304     05  PV-PREV-CYCLE             PIC S9(9)     VALUE +0 COMP.           
W33304     05  PV-STR-IDX                       PIC S9(04) VALUE ZERO.          
W33304     05  PV-MNLYSUM-IDX                   PIC S9(04) VALUE ZERO.          
W33304     05  PV-INV-SCL-IDX                   PIC S9(04) VALUE ZERO.          
W33304     05  PV-STORE-COUNT                   PIC S9(04) VALUE ZERO.          
W33304     05  PV-CYCLE-COUNT                   PIC 9(04) VALUE ZERO.           
W33304     05  PV-CYCLES-PROCESSED              PIC 9(04) VALUE ZERO.           
                                                                                
       01  SD-SYSTEM-DATE.                                                      
           05  SD-YY                        PIC 9(02)     VALUE ZEROES.         
           05  SD-MM                        PIC 9(02)     VALUE ZEROES.         
           05  SD-DD                        PIC 9(02)     VALUE ZEROES.         
                                                                                
       01  ST-SYSTEM-TIME.                                                      
           05  ST-HR                        PIC 9(02)     VALUE ZEROES.         
           05  ST-MM                        PIC 9(02)     VALUE ZEROES.         
           05  ST-SS                        PIC 9(02)     VALUE ZEROES.         
           05  FILLER                       PIC 9(02)     VALUE ZEROES.         
                                                                                
W20445******************************************************************        
W20445* RECORD LAYOUT FOR MLT_MNLY_SUM TABLE SORTED UNLOAD FILE                 
W20445******************************************************************        
W20445 01  WS-MNLYSUM-UNLOAD-RECORD.                                            
W20445     05  WS-MNLYSUM-LOC-NBR           PIC S9(04)    VALUE +0 COMP.        
W20445     05  WS-MNLYSUM-END-INV-RTL-AMT   PIC S9(11)V99 VALUE +0              
W20445                                                    COMP-3.               
                                                                                
W20445******************************************************************        
W33304* RECORD LAYOUT FOR INT_SUB_CL_INV_LDGR TABLE SORTED UNLOAD FILE          
W20445******************************************************************        
W33304 01  WS-INV-SCL-UNLOAD-RECORD.                                            
W33304     05  WS-INV-SCL-STR-NBR           PIC S9(04)    VALUE +0 COMP.        
W33304     05  WS-INV-SCL-CTOFF-END-INV-AMT PIC S9(11)V99 VALUE +0              
W20445                                                    COMP-3.               
W33304     05  WS-INV-SCL-INV-ID            PIC S9(9)     VALUE +0 COMP.        
                                                                                
       01  ABEND-CODE                       PIC S9(04)    VALUE +0              
                                                          COMP SYNC.            
       01  H1-HEADING.                                                          
W33304     05  FILLER              PIC X(06)   VALUE ' CYCLE'.                  
W33304     05  FILLER              PIC X(01)   VALUE SPACE.                     
W33304     05  H1-INV-ID           PIC 9(05)   VALUE ZERO.                      
W33304     05  FILLER              PIC X(23)   VALUE SPACE.                     
           05  FILLER              PIC X(39)   VALUE                            
              'LEDGER CUTOFF REPORT FOR FISCAL MTH:  '.                         
           05  H1-OPEN-FSCL-MN-NBR PIC X(02).                                   
           05  FILLER              PIC X(01)   VALUE '-'.                       
           05  H1-OPEN-FSCL-MN-NM  PIC X(09).                                   
W33304     05  FILLER              PIC X(05)   VALUE SPACES.                    
W33304     05  FILLER              PIC X(07)   VALUE ' CYCLE:'.                 
W33304     05  FILLER              PIC X(03)   VALUE SPACES.                    
W33304     05  H1-INV-NAME-TEXT    PIC X(20)   VALUE SPACE.                     
W33304     05  FILLER              PIC X(11)   VALUE SPACES.                    
                                                                                
       01  H2-HEADING.                                                          
           05  FILLER              PIC X(47)   VALUE SPACES.                    
           05  FILLER              PIC X(85)   VALUE                            
               'MERCHANDISE LEDGER      INVENTORY LEDGER'.                      
                                                                                
       01  H3-HEADING.                                                          
           05  FILLER              PIC X(01)   VALUE SPACE.                     
           05  FILLER              PIC X(05)   VALUE 'STORE'.                   
           05  FILLER              PIC X(41)   VALUE SPACES.                    
           05  FILLER              PIC X(85)   VALUE                            
               'ENDING INVENTORY        CUTOFF INVENTORY'.                      
           EJECT                                                                
       01  D1-DETAIL-LINE.                                                      
           05  D1-DETAIL-DATA.                                                  
W26600         10  D1-STORE-NBR         PIC Z9999        VALUE SPACES.          
W26600         10  FILLER               PIC X(39)        VALUE SPACES.          
               10  D1-LEDGER-INV        PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                 
               10  FILLER               PIC X(04)        VALUE SPACES.          
               10  D1-INVENTORY-LEDGER  PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                 
               10  FILLER               PIC X(52)        VALUE SPACES.          
                                                                                
       01  T1-TOTAL-LINE.                                                       
           05  T1-TOTAL-DATA.                                                   
               10  FILLER               PIC X(44)        VALUE                  
                      ' TOTAL'.                                                 
               10  T1-LEDGER-TOT        PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                 
               10  FILLER               PIC X(04)        VALUE SPACES.          
               10  T1-INVENTORY-TOT     PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                 
               10  FILLER               PIC X(52)        VALUE SPACES.          
                                                                                
      * COPY FOR STANDARD HEADING                                               
           COPY DPWS132O.                                                       
W33304 01 STORES-TABLE.                                                         
W33304     05  STORES-REC OCCURS 9999 TIMES INDEXED BY STR-IDX.                 
W33304         10  STORE-NBR                PIC S9(04) VALUE +0 COMP.           
W33304 01  MNLYSUM-TABLE.                                                       
W33304     05  MNLYSUM-REC OCCURS 9999 TIMES INDEXED BY MNLYSUM-IDX.            
W33304         10  MNLYSUM-LOC-NBR         PIC S9(04)    VALUE +0 COMP.         
W33304         10  MNLYSUM-END-INV-RTL-AMT   PIC S9(11)V99 VALUE +0             
                                                         COMP-3.                
W33304 01  INV-SCL-TABLE.                                                       
W33304     05  INV-SCL-REC OCCURS 9999 TIMES INDEXED BY INV-SCL-IDX.            
W33304         10 INV-SCL-STR-NBR           PIC S9(04)    VALUE +0 COMP.        
W33304         10 INV-SCL-CTOFF-END-INV-AMT PIC S9(11)V99 VALUE +0              
W33304                                                    COMP-3.               
W33304         10 INV-SCL-INV-ID            PIC S9(9)     VALUE +0 COMP.        
       EJECT                                                                    
      *                                                                         
      ***************************************************************           
      *    USED FOR DB2 ERRORS.                                                 
      ***************************************************************           
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      ***************************************************************           
      *  OPEN FISCAL MONTH CONTROL TABLE                                        
      ***************************************************************           
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *    LAYOUT USED FOR DB2 DATE ATTRIBUTE TABLE                             
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *    LAYOUT USED FOR DB2 STORE TABLE.                                     
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
W33304***************************************************************           
W33304*    LAYOUT USED FOR DB2 MERCHANDISE LEDGER CONTROL                       
W33304***************************************************************           
                                                                                
W33304     EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
W33304     END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *    USED FOR DB2 ERRORS.                                                 
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *    CURSOR DECLARATION USED FOR ACCESSING DB2 STORE TABLE                
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
               DECLARE STORE_CURSOR CURSOR FOR                                  
W26600             SELECT LOC_NBR                                               
W26600             FROM XMT_LOC                                                 
W26600             WHERE LOC_TYP_CDE IN ('DC','DS','RV','CS','DA')              
W26600               AND (FINCL_CLO_DTE > :MLCNTL-OPN-FSCL-MN-DTE               
W26600                OR  FINCL_CLO_DTE IS NULL)                                
W26600             ORDER BY LOC_NBR                                             
W20445             WITH UR                                                      
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
W20445     OPEN INPUT  MNLYSUM-UNLOAD-FILE                                      
W33304                 INV-SCL-UNLOAD-FILE.                                     
           OPEN OUTPUT LEDGER-CUTOFF-RPT.                                       
                                                                                
           PERFORM B100-INITIALIZE.                                             
W33304****LOAD STORE CURSOR INTO WORKING STORAGE TABLE                          
W33304     PERFORM C500-LOAD-STORE-TABLE.                                       
W33304     PERFORM B300-PROCESS-RECS.                                           
W20445     MOVE PV-MNLYSUM-UNLOAD-RECS-READ TO PV-DISPLAY-RECS-READ.            
W20445     DISPLAY 'MNLYSUM-UNLOAD-FILE RECS READ: '                            
W20445             PV-DISPLAY-RECS-READ.                                        
W33304     MOVE PV-INV-SCL-UNLOAD-RECS-READ TO PV-DISPLAY-RECS-READ.            
W33304     DISPLAY 'INV-SCL-UNLOAD-FILE RECS READ: '                            
W20445             PV-DISPLAY-RECS-READ.                                        
W33304     MOVE PV-STORE-COUNT TO PV-DISPLAY-RECS-READ.                         
W33304     DISPLAY 'NUMBER OF STORES: ' PV-DISPLAY-RECS-READ.                   
W33304     DISPLAY 'NUMBER OF CYCLES READ: ' PV-CYCLE-COUNT.                    
W33304     DISPLAY 'NUMBER OF CYCLES PROCESSED: ' PV-CYCLES-PROCESSED.          
                                                                                
W20445     CLOSE  MNLYSUM-UNLOAD-FILE                                           
W33304            INV-SCL-UNLOAD-FILE                                           
                  LEDGER-CUTOFF-RPT.                                            
                                                                                
           STOP RUN.                                                            
       EJECT                                                                    
       B100-INITIALIZE.                                                         
                                                                                
           ACCEPT SD-SYSTEM-DATE                                                
               FROM DATE.                                                       
           ACCEPT ST-SYSTEM-TIME                                                
               FROM TIME.                                                       
                                                                                
           MOVE SD-MM      TO DP132O-RUN-MONTH.                                 
           MOVE SD-DD      TO DP132O-RUN-DAY.                                   
           MOVE SD-YY      TO DP132O-RUN-YEAR.                                  
           MOVE ST-HR      TO DP132O-RUN-HOUR.                                  
           MOVE ST-MM      TO DP132O-RUN-MINUTE.                                
           MOVE 'INKRP142' TO DP132O-PROGRAM-NAME.                              
           MOVE 1          TO DP132O-REPORT-NUMBER.                             
                                                                                
           PERFORM Z375-GET-OPN-FSCL-MN.                                        
           DISPLAY 'OPEN FISCAL MONTH: ' MLCNTL-OPN-FSCL-MN-DTE                 
                   '     OPEN FISCAL NBR: ' MLCNTL-OPN-FSCL-MN-NBR.             
                                                                                
           PERFORM B200-GET-DATE-ATTRIBUTES.                                    
                                                                                
           MOVE MLCNTL-OPN-FSCL-MN-NBR TO H1-OPEN-FSCL-MN-NBR.                  
           MOVE DTATTR-FSCL-MN-NM TO H1-OPEN-FSCL-MN-NM.                        
W33304****READ THE MONTHLY SUM FILE INTO A WORKING STORAGE TABLE                
W33304     INITIALIZE MNLYSUM-TABLE.                                            
W33304     MOVE +0 TO PV-MNLYSUM-IDX.                                           
W20445     SET PS-MNLYSUM-UNLOAD-NOT-EOF TO TRUE.                               
W33304     PERFORM C100-READ-MNLYSUM-UNLOAD-FILE                                
W33304        UNTIL PS-MNLYSUM-UNLOAD-EOF.                              4       
W33304****READ THE INV SCL SUM FILE INTO A WORKING STORAGE TABLE                
W33304****IT IS SORTED BY CYCLE ID / STORE                                      
W33304     INITIALIZE INV-SCL-TABLE.                                            
W33304     MOVE +0 TO PV-INV-SCL-IDX.                                           
W20445     SET PS-INV-SCL-UNLOAD-NOT-EOF TO TRUE.                               
W33304     PERFORM C200-READ-INV-SCL-UNLOAD-FILE                                
W33304        UNTIL PS-INV-SCL-UNLOAD-EOF.                                      
                                                                                
       B200-GET-DATE-ATTRIBUTES.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                       FSCL_MN_NM                                               
                    INTO                                                        
                       :DTATTR-FSCL-MN-NM                                       
                    FROM                                                        
                        TDTATTR                                                 
                    WHERE                                                       
                        KY_DTE = :MLCNTL-OPN-FSCL-MN-DTE                        
W20445              WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                   WHEN SQLCODE        = -911                                   
W33304             WHEN SQLCODE        = -913                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY 'MLCNTL-OPN-FSCL-MN-DTE: '                       
                                MLCNTL-OPN-FSCL-MN-DTE                          
                       MOVE    'B200-GET-DATE-ATTRIBUTES'                       
                                                TO PV-PARAGRAPH-NAME            
                       MOVE    'SELECT ROW FROM TDTATTR'                        
                                           TO PV-DB2-OPER-ATTEMPTED             
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
       EJECT                                                                    
W33304 B300-PROCESS-RECS.                                                       
W33304     MOVE ZERO TO PV-HOLD-CYCLE.                                          
W33304     PERFORM C600-PROCESS-CYCLE                                           
W33304        UNTIL PV-CYCLES-PROCESSED = PV-CYCLE-COUNT.                       
                                                                                
       B400-OPEN-CURSOR.                                                        
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                                                                                
               EXEC SQL                                                         
                    OPEN STORE_CURSOR                                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -911                                          
W33304             WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE ' B400-OPEN-CURSOR'                                 
                                                TO PV-PARAGRAPH-NAME            
                       MOVE 'OPEN STORE CURSOR'                                 
                                                TO PV-DB2-OPER-ATTEMPTED        
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '** MAX RETRIES EXCEEDED **'                             
               MOVE ' B400-OPEN-CURSOR' TO PV-PARAGRAPH-NAME                    
               MOVE 'OPEN STORE CURSOR'    TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       B500-FETCH-STORE.                                                        
                                                                                
      ***************************************************************           
      *    FETCH A ROW FROM THE STORE TABLE.                                    
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      *    PERFORMED BY B500-LOAD-STORES.                                       
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
               FETCH STORE_CURSOR                                               
W26600             INTO :XMT00001-LOC-NBR                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
W33304             ADD +1 TO PV-STR-IDX                                         
W33304             MOVE XMT00001-LOC-NBR TO STORE-NBR(PV-STR-IDX)               
               WHEN SQLCODE = +100                                              
W33304             MOVE PV-STR-IDX TO PV-STORE-COUNT                            
W26600             SET PS-XMTLOC-EOF TO TRUE                                    
               WHEN OTHER                                                       
                  MOVE    'B500-FETCH-STORE     '                               
                                                TO PV-PARAGRAPH-NAME            
                  MOVE    'FETCH STORE '                                        
                                                TO PV-DB2-OPER-ATTEMPTED        
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
       B550-CLOSE-CURSOR.                                                       
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                                                                                
               EXEC SQL                                                         
                    CLOSE STORE_CURSOR                                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -911                                          
W33304             WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE ' B550-CLOSE-CURSOR'                                
                                                TO PV-PARAGRAPH-NAME            
                       MOVE 'CLOSE STORE CURSOR'                                
                                                TO PV-DB2-OPER-ATTEMPTED        
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '** MAX RETRIES EXCEEDED **'                             
               MOVE ' B400-OPEN-CURSOR' TO PV-PARAGRAPH-NAME                    
               MOVE 'OPEN STORE CURSOR'    TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       B800-PRINT-DETAIL-LINE.                                                  
           ADD PV-END-INV-RTL-AMT-SUM   TO FT-LEDGER-TOTAL.                     
           ADD PV-CTOFF-END-INV-AMT-SUM TO FT-INVENTORY-TOTAL.                  
                                                                                
           MOVE SPACES                   TO D1-DETAIL-DATA.                     
                                                                                
W33304     MOVE STORE-NBR(STR-IDX)       TO D1-STORE-NBR.                       
           MOVE PV-END-INV-RTL-AMT-SUM   TO D1-LEDGER-INV.                      
           MOVE PV-CTOFF-END-INV-AMT-SUM TO D1-INVENTORY-LEDGER.                
                                                                                
           IF PC-LINE-COUNT > +59                                               
               PERFORM Z800-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           MOVE D1-DETAIL-LINE TO REPORT-LINE.                                  
           WRITE REPORT-LINE AFTER ADVANCING 1.                                 
           ADD +1 TO  PC-LINE-COUNT.                                            
                                                                                
       B900-PRINT-TOTALS.                                                       
                                                                                
           MOVE FT-LEDGER-TOTAL    TO T1-LEDGER-TOT.                            
           MOVE FT-INVENTORY-TOTAL TO T1-INVENTORY-TOT.                         
                                                                                
           MOVE T1-TOTAL-LINE TO REPORT-LINE.                                   
           WRITE REPORT-LINE AFTER ADVANCING 2.                                 
           ADD +2 TO  PC-LINE-COUNT.                                            
W33304     MOVE ZERO               TO T1-LEDGER-TOT                             
W33304                                FT-LEDGER-TOTAL.                          
W33304     MOVE ZERO               TO T1-INVENTORY-TOT                          
W33304                                FT-INVENTORY-TOTAL.                       
W20445******************************************************************07160094
W20445* READ MLT_MNLY_SUM TABLE SORTED UNLOAD FILE.                    *07170094
W20445******************************************************************07180094
W20445 C100-READ-MNLYSUM-UNLOAD-FILE.                                   07140094
W20445                                                                  07210094
W20445     READ MNLYSUM-UNLOAD-FILE INTO WS-MNLYSUM-UNLOAD-RECORD       07190094
W20445         AT END                                                           
W20445             SET PS-MNLYSUM-UNLOAD-EOF TO TRUE                    4       
W20445         NOT AT END                                               07200094
W20445             ADD +1 TO PV-MNLYSUM-UNLOAD-RECS-READ                07210094
W33304             ADD +1 TO PV-MNLYSUM-IDX                                     
W33304             MOVE WS-MNLYSUM-LOC-NBR                                      
W33304                              TO MNLYSUM-LOC-NBR(PV-MNLYSUM-IDX)          
W33304             MOVE WS-MNLYSUM-END-INV-RTL-AMT                              
W33304                        TO MNLYSUM-END-INV-RTL-AMT(PV-MNLYSUM-IDX)        
W20445     END-READ.                                                    07280094
                                                                        07290094
W20445******************************************************************07160094
W33304* READ INT_SUB_CL_INV_LDGR TABLE SORTED UNLOAD FILE              *07170094
W20445******************************************************************07180094
W33304 C200-READ-INV-SCL-UNLOAD-FILE.                                   07140094
W33304     READ INV-SCL-UNLOAD-FILE INTO WS-INV-SCL-UNLOAD-RECORD       07190094
W20445         AT END                                                           
W33304             SET PS-INV-SCL-UNLOAD-EOF TO TRUE                    4       
W33304             MOVE ZERO TO PV-HOLD-CYCLE                                   
W20445         NOT AT END                                               07200094
W33304             ADD +1 TO PV-INV-SCL-UNLOAD-RECS-READ                07210094
W33304             ADD +1 TO PV-INV-SCL-IDX                                     
W33304             MOVE WS-INV-SCL-STR-NBR                                      
W33304                      TO INV-SCL-STR-NBR(PV-INV-SCL-IDX)                  
W33304             MOVE WS-INV-SCL-CTOFF-END-INV-AMT                            
W33304                   TO INV-SCL-CTOFF-END-INV-AMT(PV-INV-SCL-IDX)           
W33304             MOVE WS-INV-SCL-INV-ID                                       
W33304                  TO INV-SCL-INV-ID(PV-INV-SCL-IDX)                       
W33304             IF PV-INV-SCL-UNLOAD-RECS-READ = +1                  07210094
W33304               OR WS-INV-SCL-INV-ID > PV-HOLD-CYCLE                       
W33304                 MOVE WS-INV-SCL-INV-ID TO PV-HOLD-CYCLE                  
W33304                 ADD 1 TO PV-CYCLE-COUNT                                  
W33304             END-IF                                                       
W20445     END-READ.                                                    07280094
                                                                        07290094
W33304******************************************************************        
W33304* INITIALIZE STORE TABLE AND INDEX                                        
W33304* LOAD STORE TABLE                                                        
W33304*                                                                         
W33304* PERFORMED BY B100-PROCESS-PA-UPCIN-FILE                                 
W33304******************************************************************        
W33304 C500-LOAD-STORE-TABLE.                                                   
041900                                                                          
W33304     INITIALIZE STORES-TABLE.                                             
W33304     MOVE +0 TO PV-STR-IDX.                                               
W33304     PERFORM B400-OPEN-CURSOR.                                            
W33304     PERFORM B500-FETCH-STORE                                             
W33304        UNTIL PS-XMTLOC-EOF.                                              
W33304     PERFORM B550-CLOSE-CURSOR.                                           
W33304 C600-PROCESS-CYCLE.                                                      
W33304     PERFORM VARYING INV-SCL-IDX                                          
W33304         FROM 1 BY 1 UNTIL                                                
W33304           INV-SCL-INV-ID(INV-SCL-IDX) > PV-HOLD-CYCLE                    
W33304           OR INV-SCL-INV-ID(INV-SCL-IDX) = ZERO                          
W33304     END-PERFORM.                                                         
W33304     IF INV-SCL-INV-ID(INV-SCL-IDX) > ZERO                                
W33304         MOVE +99 TO PC-LINE-COUNT                                        
W33304         MOVE INV-SCL-INV-ID(INV-SCL-IDX) TO PV-HOLD-CYCLE                
W33304         MOVE INV-SCL-INV-ID(INV-SCL-IDX) TO INCNTL-INV-ID                
W33304         PERFORM Z400-GET-CYCLE-NAME                                      
W33304         MOVE INCNTL-INV-ID          TO PV-TEMP-NBR-10-COMP               
W33304         MOVE PV-TEMP-NBR-10-COMP    TO PV-TEMP-NBR-10-NUM                
W33304         MOVE PV-REPORT-INV-ID       TO H1-INV-ID                         
W33304         MOVE INCNTL-INV-NM-TEXT (1:INCNTL-INV-NM-LEN)                    
W33304                                     TO H1-INV-NAME-TEXT                  
W33304         PERFORM C700-PROCESS-STORES VARYING STR-IDX                      
W33304             FROM 1 BY 1 UNTIL                                            
W33304               STORE-NBR(STR-IDX) = ZERO                                  
W33304         PERFORM B900-PRINT-TOTALS                                        
W33304         MOVE PV-HOLD-CYCLE TO PV-PREV-CYCLE                              
W33304         ADD 1 TO PV-CYCLES-PROCESSED                                     
W33304     END-IF.                                                              
                                                                                
W33304 C700-PROCESS-STORES.                                                     
W33304     PERFORM VARYING MNLYSUM-IDX                                          
W33304         FROM 1 BY 1 UNTIL                                                
W33304         STORE-NBR(STR-IDX) = MNLYSUM-LOC-NBR(MNLYSUM-IDX)                
W33304         OR STORE-NBR(STR-IDX) < MNLYSUM-LOC-NBR(MNLYSUM-IDX)             
W33304         OR MNLYSUM-LOC-NBR(MNLYSUM-IDX) = ZERO                           
W33304     END-PERFORM.                                                         
W33304     IF STORE-NBR(STR-IDX) = MNLYSUM-LOC-NBR(MNLYSUM-IDX)                 
W33304         MOVE MNLYSUM-END-INV-RTL-AMT(MNLYSUM-IDX)                4       
W33304                                 TO PV-END-INV-RTL-AMT-SUM        4       
W33304     ELSE                                                                 
W33304         MOVE ZERO TO PV-END-INV-RTL-AMT-SUM                              
W33304     END-IF.                                                              
W33304     PERFORM VARYING INV-SCL-IDX                                          
W33304         FROM 1 BY 1 UNTIL                                                
W33304           INV-SCL-INV-ID(INV-SCL-IDX) = PV-HOLD-CYCLE                    
W33304           AND (STORE-NBR(STR-IDX) = INV-SCL-STR-NBR(INV-SCL-IDX)         
W33304              OR STORE-NBR(STR-IDX) < INV-SCL-STR-NBR(INV-SCL-IDX)        
W33304              OR INV-SCL-STR-NBR(INV-SCL-IDX) = ZERO)                     
W33304           OR INV-SCL-INV-ID(INV-SCL-IDX) > PV-HOLD-CYCLE                 
W33304           OR INV-SCL-INV-ID(INV-SCL-IDX) = ZERO                          
W33304     END-PERFORM.                                                         
W33304     IF STORE-NBR(STR-IDX) = INV-SCL-STR-NBR(INV-SCL-IDX)                 
W33304       AND INV-SCL-INV-ID(INV-SCL-IDX) = PV-HOLD-CYCLE                    
W33304         MOVE INV-SCL-CTOFF-END-INV-AMT(INV-SCL-IDX)              4       
W33304                                 TO PV-CTOFF-END-INV-AMT-SUM      4       
W33304     ELSE                                                                 
W33304         MOVE ZERO TO PV-CTOFF-END-INV-AMT-SUM                            
W33304     END-IF.                                                              
W33304     PERFORM B800-PRINT-DETAIL-LINE.                                      
       Z375-GET-OPN-FSCL-MN.                                                    
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                        OPN_FSCL_MN_DTE                                         
                       ,OPN_FSCL_MN_NBR                                         
                    INTO                                                        
                       :MLCNTL-OPN-FSCL-MN-DTE                                  
                      ,:MLCNTL-OPN-FSCL-MN-NBR                                  
                    FROM                                                        
                        TMLCNTL                                                 
W20445              WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                   WHEN SQLCODE        = -911                                   
W33304             WHEN SQLCODE        = -913                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE    'Z375-GET-OPN-FSCL-MN '                          
                                                TO PV-PARAGRAPH-NAME            
                       MOVE    'SELECT ROW FROM TMLCNTL'                        
                                                TO PV-DB2-OPER-ATTEMPTED        
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '** MAX RETRIES EXCEEDED ON TMLCNTL **'                  
               MOVE 'Z375-GET-OPN-FSCL-MN '                                     
                                                TO PV-PARAGRAPH-NAME            
               MOVE 'SELECT ROW FROM TMLCNTL'                                   
                                                TO PV-DB2-OPER-ATTEMPTED        
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
W33304 Z400-GET-CYCLE-NAME.                                                     
                                                                                
W33304     PERFORM                                                              
W33304         WITH TEST AFTER                                                  
W33304         VARYING PV-RETRY-COUNT FROM 1 BY 1                               
W33304           UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
W33304              OR (SQLCODE         = +000)                                 
W33304              OR (SQLCODE         = +100)                                 
                                                                                
W33304         EXEC SQL                                                         
W33304          SELECT INV_NM                                                   
W33304            INTO :INCNTL-INV-NM                                           
W33304            FROM TINCNTL                                                  
W33304           WHERE INV_ID = :INCNTL-INV-ID                                  
W33304         END-EXEC                                                         
                                                                                
W33304         EVALUATE TRUE                                                    
W33304             WHEN SQLCODE        = +000                                   
W33304             WHEN SQLCODE        = -911                                   
W33304             WHEN SQLCODE        = -913                                   
W33304                 CONTINUE                                                 
W33304             WHEN OTHER                                                   
W33304                 MOVE    'Z400-GET-CYCLE-NAME '                           
W33304                                          TO PV-PARAGRAPH-NAME            
W33304                 MOVE    'SELECT ROW FROM TINCNTL'                        
W33304                                          TO PV-DB2-OPER-ATTEMPTED        
W33304                 PERFORM 9999-SQL-ABEND                                   
W33304         END-EVALUATE                                                     
                                                                                
W33304     END-PERFORM.                                                         
                                                                                
W33304     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
W33304         DISPLAY '** MAX RETRIES EXCEEDED ON TINCNTL **'                  
W33304         MOVE    'Z400-GET-CYCLE-NAME '                                   
W33304                                          TO PV-PARAGRAPH-NAME            
W33304         MOVE    'SELECT ROW FROM TINCNTL'                                
W33304                                          TO PV-DB2-OPER-ATTEMPTED        
W33304         PERFORM 9999-SQL-ABEND                                           
W33304     END-IF.                                                              
                                                                                
      ***************************************************************           
      *    READ THE OPEN FISCAL MONTH FILE                                      
      ***************************************************************           
                                                                                
                                                                                
       Z800-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1              TO PC-PAGE-COUNT.                                 
                                                                                
           MOVE PC-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
           MOVE 1             TO DP132O-REPORT-NUMBER.                          
           MOVE DP132O-STANDRD-HEADER-132-COLS TO REPORT-LINE.                  
           WRITE REPORT-LINE AFTER ADVANCING PAGE.                              
           MOVE +1 TO  PC-LINE-COUNT.                                           
                                                                                
           MOVE H1-HEADING TO REPORT-LINE.                                      
           WRITE REPORT-LINE AFTER ADVANCING 2.                                 
           MOVE +2 TO  PC-LINE-COUNT.                                           
                                                                                
           MOVE H2-HEADING TO REPORT-LINE.                                      
           WRITE REPORT-LINE AFTER ADVANCING 2.                                 
           MOVE +2 TO  PC-LINE-COUNT.                                           
                                                                                
           MOVE H3-HEADING TO REPORT-LINE.                                      
           WRITE REPORT-LINE AFTER ADVANCING 1.                                 
           MOVE +1 TO  PC-LINE-COUNT.                                           
                                                                                
           MOVE +6         TO PC-LINE-COUNT.                                    
                                                                                
       9999-SQL-ABEND.                                                          
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = INKRP142'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = '                                  
                                         PV-DB2-OPER-ATTEMPTED.                 
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
