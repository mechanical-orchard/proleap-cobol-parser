       IDENTIFICATION DIVISION.                                         00010099
       PROGRAM-ID.              INKRP197.                               00020099
       AUTHOR.                  JIM ARENDT.                             00030099
       INSTALLATION.            KOHLS DEPARTMENT STORES.                00040099
       DATE-WRITTEN.            07/30/2013.                             00050099
       DATE-COMPILED.                                                   00060099
      ******************************************************************00070099
      *                                                                *00080099
      *  INVENTORY STORE-PARTIAL SHIP ADJUSTMENT DETAIL/SUMMARY REPORT *00090099
      *                                                                *00100099
      *  THIS PROGRAM WILL GENERATE A DETAIL LISTING DOWN TO SKU LEVEL *00110099
      *  OF THE STORE PARTIAL SHIP TRANSACTIONS FROM RECEIVING         *00120099
      *  MANIFEST PROCESSING.                                          *00130099
      *                                                                *00140099
      *  THE REPORT WILL BE GENERATED BY STORE THEN DEPT.              *00150099
      *                                                                *00160099
      ******************************************************************00170099
      *CHANGE HISTORY:                                                 *00180099
      * WR/PROJ   DATE      PGMR        DESCRIPTION OF CHANGES         *00190099
      * -------- --------- ----------- --------------------------------*00200099
      * CHG61608  08/13     JIM ARENDT  INITIAL IMPLEMENTATION.         00210099
      ******************************************************************00220099
C63789* CHG 63789 09/26/13 - MOVE SHIP DATE INSTEAD OF RUN DATE         00230099
      ******************************************************************00240099
C63961* CHG 63961 09/27/13 - CHANGED LOCATION NOT FOUND MESSAGE         00250099
      ******************************************************************00260099
P48331* CHG 89593 09/19/14 - MODIFIED ORDER NUMBER TO BE AS 10 DIGIT    00270099
P48331*                      ONE INSTEAD OF 9 TO AVOID TRUNCATION.      00280099
P48331******************************************************************00290099
                                                                        00300099
       ENVIRONMENT DIVISION.                                            00310099
       CONFIGURATION SECTION.                                           00320099
       INPUT-OUTPUT SECTION.                                            00330099
       FILE-CONTROL.                                                    00340099
                                                                        00350099
           SELECT RPT-CONTROL-FILE                                      00360099
               ASSIGN TO INP01.                                         00370099
                                                                        00380099
           SELECT STORE-PARTIAL-ADJ-FILE                                00390099
               ASSIGN TO INP02.                                         00400099
                                                                        00410099
           SELECT STORE-PARTIAL-DTL-RPT                                 00420099
               ASSIGN TO RPT01.                                         00430099
                                                                        00440099
           SELECT STORE-PARTIAL-SUM-1-RPT                               00450099
               ASSIGN TO RPT02.                                         00460099
                                                                        00470099
           SELECT STORE-PARTIAL-SUM-2-RPT                               00480099
               ASSIGN TO RPT03.                                         00490099
                                                                        00500099
           SELECT STORE-PARTIAL-STATUS-RPT                              00510099
               ASSIGN TO RPT04.                                         00520099
                                                                        00530099
      ******************************************************************00540099
       DATA DIVISION.                                                   00550099
       FILE SECTION.                                                    00560099
                                                                        00570099
       FD  RPT-CONTROL-FILE                                             00580099
           RECORDING MODE IS F                                          00590099
           LABEL RECORDS ARE STANDARD                                   00600099
           BLOCK CONTAINS 0 RECORDS.                                    00610099
       01  RPT-CONTROL-REC                 PIC X(80).                   00620099
                                                                        00630099
       FD  STORE-PARTIAL-ADJ-FILE                                       00640099
           RECORDING MODE IS F                                          00650099
           LABEL RECORDS ARE STANDARD                                   00660099
           BLOCK CONTAINS 0 RECORDS.                                    00670099
       01  STORE-PARTIAL-ADJ-REC           PIC X(129).                  00680099
                                                                        00690099
       FD  STORE-PARTIAL-DTL-RPT                                        00700099
           RECORDING MODE IS F                                          00710099
           LABEL RECORDS ARE OMITTED                                    00720099
           RECORD CONTAINS 132 CHARACTERS                               00730099
           DATA RECORD IS REPORT-LINE-1.                                00740099
       01  REPORT-LINE-1                   PIC X(132).                  00750099
                                                                        00760099
       FD  STORE-PARTIAL-SUM-1-RPT                                      00770099
           RECORDING MODE IS F                                          00780099
           LABEL RECORDS ARE OMITTED                                    00790099
           RECORD CONTAINS 132 CHARACTERS                               00800099
           DATA RECORD IS REPORT-LINE-2.                                00810099
       01  REPORT-LINE-2                   PIC X(132).                  00820099
                                                                        00830099
       FD  STORE-PARTIAL-SUM-2-RPT                                      00840099
           RECORDING MODE IS F                                          00850099
           LABEL RECORDS ARE OMITTED                                    00860099
           RECORD CONTAINS 132 CHARACTERS                               00870099
           DATA RECORD IS REPORT-LINE-3.                                00880099
       01  REPORT-LINE-3                   PIC X(132).                  00890099
                                                                        00900099
       FD  STORE-PARTIAL-STATUS-RPT                                     00910099
           RECORDING MODE IS F                                          00920099
           LABEL RECORDS ARE OMITTED                                    00930099
           RECORD CONTAINS 132 CHARACTERS                               00940099
           DATA RECORD IS REPORT-LINE-4.                                00950099
       01  REPORT-LINE-4                   PIC X(132).                  00960099
                                                                        00970099
      ******************************************************************00980099
       WORKING-STORAGE SECTION.                                         00990099
                                                                        01000099
      ******************************************************************01010099
       01  PC-PROGRAM-CONSTANTS-AREA.                                   01020099
           05  PC-PROGRAM-NAME             PIC  X(08)      VALUE        01030099
                                                           'INKRP197'.  01040099
           05  PC-PRINT-LINE-MAX           PIC S9(04)      VALUE +57    01050099
                                                           COMP.        01060099
                                                                        01070099
      ******************************************************************01080099
       01  PV-PROGRAM-VARIABLES-AREA.                                   01090099
           05  PV-PAGE-COUNT-A             PIC S9(05)      VALUE ZEROES 01100099
                                                           COMP.        01110099
           05  PV-PAGE-COUNT-B             PIC S9(05)      VALUE ZEROES 01120099
                                                           COMP.        01130099
           05  PV-PAGE-COUNT-C             PIC S9(05)      VALUE ZEROES 01140099
                                                           COMP.        01150099
           05  PV-PAGE-COUNT-S             PIC S9(05)      VALUE ZEROES 01160099
                                                           COMP.        01170099
           05  PV-PRINT-LINE-COUNT-A       PIC S9(04)      VALUE ZEROES 01180099
                                                           COMP.        01190099
           05  PV-PRINT-LINE-COUNT-B       PIC S9(04)      VALUE ZEROES 01200099
                                                           COMP.        01210099
           05  PV-PRINT-LINE-COUNT-C       PIC S9(04)      VALUE ZEROES 01220099
                                                           COMP.        01230099
           05  PV-PRINT-LINE-COUNT-S       PIC S9(04)      VALUE ZEROES 01240099
                                                           COMP.        01250099
           05  PV-ADVANCE-LINE-A           PIC S9(04)      VALUE ZEROES 01260099
                                                           COMP.        01270099
           05  PV-ADVANCE-LINE-B           PIC S9(04)      VALUE ZEROES 01280099
                                                           COMP.        01290099
           05  PV-ADVANCE-LINE-C           PIC S9(04)      VALUE ZEROES 01300099
                                                           COMP.        01310099
           05  PV-ADVANCE-LINE-S           PIC S9(04)      VALUE ZEROES 01320099
                                                           COMP.        01330099
           05  PV-OPER-ATTEMPTED           PIC  X(25)      VALUE SPACES.01340099
           05  PV-PROCESSING-DATE          PIC  X(10)      VALUE SPACES.01350099
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.     01360099
               10  PV-PROCESSING-CC        PIC  X(02).                  01370099
               10  PV-PROCESSING-YY        PIC  X(02).                  01380099
               10  PV-PROC-DSH1            PIC  X(01).                  01390099
               10  PV-PROCESSING-MM        PIC  X(02).                  01400099
               10  PV-PROC-DSH2            PIC  X(01).                  01410099
               10  PV-PROCESSING-DD        PIC  X(02).                  01420099
           05  PV-DATE-HOLD-CCYY-MM-DD     PIC  X(10)      VALUE SPACES.01430099
           05  FILLER REDEFINES PV-DATE-HOLD-CCYY-MM-DD.                01440099
               10  PV-DATE-HOLD-CCYY       PIC  X(04).                  01450099
               10  FILLER REDEFINES PV-DATE-HOLD-CCYY.                  01460099
                   15  PV-DATE-HOLD-CC     PIC  X(02).                  01470099
                   15  PV-DATE-HOLD-YY     PIC  X(02).                  01480099
               10  PV-DATE-HOLD-DASH-1     PIC  X(01).                  01490099
               10  PV-DATE-HOLD-MM         PIC  X(02).                  01500099
               10  PV-DATE-HOLD-DASH-2     PIC  X(01).                  01510099
               10  PV-DATE-HOLD-DD         PIC  X(02).                  01520099
           05  PV-DATE-RFMT-MM-DD-CCYY     PIC  X(10)      VALUE SPACES.01530099
           05  FILLER REDEFINES PV-DATE-RFMT-MM-DD-CCYY.                01540099
               10  PV-DATE-RFMT-MM         PIC  X(02).                  01550099
               10  PV-DATE-RFMT-DASH-1     PIC  X(01).                  01560099
               10  PV-DATE-RFMT-DD         PIC  X(02).                  01570099
               10  PV-DATE-RFMT-DASH-2     PIC  X(01).                  01580099
               10  PV-DATE-RFMT-CCYY       PIC  X(04).                  01590099
               10  FILLER REDEFINES PV-DATE-RFMT-CCYY.                  01600099
                   15  PV-DATE-RFMT-CC     PIC  X(02).                  01610099
                   15  PV-DATE-RFMT-YY     PIC  X(02).                  01620099
           05  PV-DATE-RFMTX-CCYY-MM-DD    PIC  X(10)     VALUE SPACES. 01630099
           05  FILLER REDEFINES PV-DATE-RFMTX-CCYY-MM-DD.               01640099
               10  PV-DATE-RFMTX-CCYY      PIC  X(04).                  01650099
               10  PV-DATE-RFMTX-DASH-1    PIC  X(01).                  01660099
               10  PV-DATE-RFMTX-MM        PIC  X(02).                  01670099
               10  PV-DATE-RFMTX-DASH-2    PIC  X(01).                  01680099
               10  PV-DATE-RFMTX-DD        PIC  X(02).                  01690099
           05  PV-TODAYS-DATE.                                          01700099
               10  PV-TODAYS-DATE-YY       PIC  9(02)      VALUE ZEROES.01710099
               10  PV-TODAYS-DATE-MM       PIC  9(02)      VALUE ZEROES.01720099
               10  PV-TODAYS-DATE-DD       PIC  9(02)      VALUE ZEROES.01730099
           05  PV-TODAYS-TIME.                                          01740099
               10  PV-TODAYS-TIME-HR       PIC  9(02)      VALUE ZEROES.01750099
               10  PV-TODAYS-TIME-MIN      PIC  9(02)      VALUE ZEROES.01760099
               10  PV-TODAYS-TIME-SEC      PIC  9(02)      VALUE ZEROES.01770099
               10  FILLER                  PIC  9(02)      VALUE ZEROES.01780099
C63789     05  PV-SHIP-DATE                PIC  X(10)      VALUE SPACES.01790099
           05  PV-CURR-LOC-NBR             PIC  X(05)      VALUE SPACES.01800099
           05  PV-CURR-LOC-NBR-9 REDEFINES PV-CURR-LOC-NBR              01810099
                                           PIC  9(05).                  01820099
           05  PV-CURR-DEPT                PIC  X(04)      VALUE SPACES.01830099
           05  PV-CURR-DEPT-9 REDEFINES PV-CURR-DEPT                    01840099
                                           PIC  9(04).                  01850099
           05  FILLER REDEFINES PV-CURR-DEPT.                           01860099
               10  FILLER                  PIC  X(01).                  01870099
               10  PV-CURR-DEPT-3X         PIC  X(03).                  01880099
           05  PV-SUB-TTL-SHIP-QTY         PIC S9(11)      VALUE ZEROES 01890099
                                                           COMP-3.      01900099
           05  PV-GRP-TTL-SHIP-QTY         PIC S9(11)      VALUE ZEROES 01910099
                                                           COMP-3.      01920099
           05  PV-GRD-TTL-SHIP-QTY         PIC S9(11)      VALUE ZEROES 01930099
                                                           COMP-3.      01940099
           05  PV-SUB-TTL-EXTD-RTL-AMT     PIC S9(13)V9(2) VALUE ZEROES 01950099
                                                           COMP-3.      01960099
           05  PV-GRP-TTL-EXTD-RTL-AMT     PIC S9(13)V9(2) VALUE ZEROES 01970099
                                                           COMP-3.      01980099
           05  PV-GRD-TTL-EXTD-RTL-AMT     PIC S9(13)V9(2) VALUE ZEROES 01990099
                                                           COMP-3.      02000099
           05  PV-RECORD-COUNT             PIC S9(11)      VALUE ZEROES 02010099
                                                           COMP-3.      02020099
           05  PV-DISP-NUMBER              PIC  Z,ZZZ,ZZ9.              02030099
                                                                        02040099
      ******************************************************************02050099
       01  PS-SWITCHES-AREA.                                            02060099
           05  PS-INVENTORY-STORE-STATUS   PIC  X(01)      VALUE SPACES.02070099
               88  VALID-MANIFESTED-INV-STORE              VALUE 'Y'.   02080099
               88  INVALID-MANIFESTED-INV-STORE            VALUE 'N'.   02090099
           05  PS-END-OF-ADJ-FILE          PIC  X(01)      VALUE 'N'.   02100099
               88  END-OF-ADJ-FILE                         VALUE 'Y'.   02110099
               88  NOT-END-OF-ADJ-FILE                     VALUE 'N'.   02120099
           05  PS-END-OF-CNTL-FILE         PIC  X(01)      VALUE 'N'.   02130099
               88  END-OF-CNTL-FILE                        VALUE 'Y'.   02140099
               88  NOT-END-OF-CNTL-FILE                    VALUE 'N'.   02150099
           05  PS-END-OF-INV-STR           PIC  X(01)      VALUE 'N'.   02160099
               88  END-OF-INV-STR                          VALUE 'Y'.   02170099
               88  NOT-END-OF-INV-STR                      VALUE 'N'.   02180099
                                                                        02190099
      ******************************************************************02200099
       01  PA-PROGRAM-ACCUMULATORS.                                     02210099
           05  PA-INPUT-READ-CTR            PIC S9(07)  COMP-3          02220099
                                                        VALUE ZERO.     02230099
                                                                        02240099
      ******************************************************************02250099
      *  HEADER LINE                                                    02260099
      ******************************************************************02270099
           COPY DPWS132O.                                               02280099
                                                                        02290099
       01  RP-REPORT-TITLE-1A.                                          02300099
           05  FILLER                      PIC X(01)       VALUE SPACES.02310099
           05  FILLER                      PIC X(38)       VALUE SPACES.02320099
           05  FILLER                      PIC X(28)       VALUE        02330099
               'INVENTORY STORE-PARTIAL SHIP'.                          02340099
           05  FILLER                      PIC X(18)       VALUE        02350099
               ' ADJ DETAIL REPORT'.                                    02360099
           05  FILLER                      PIC X(10)       VALUE        02370099
               ' BY STORE '.                                            02380099
           05  FILLER                      PIC X(39)       VALUE SPACES.02390099
                                                                        02400099
       01  RP-REPORT-TITLE-2.                                           02410099
           05  FILLER                      PIC X(01)       VALUE SPACES.02420099
           05  FILLER                      PIC X(08)       VALUE        02430099
               'STORE:  '.                                              02440099
           05  RP-REPORT-LOC-NBR           PIC X(04)       VALUE SPACES.02450099
           05  RP-REPORT-LOC-NBR-9 REDEFINES                            02460099
               RP-REPORT-LOC-NBR           PIC 9(04).                   02470099
           05  RP-REPORT-DEPT REDEFINES                                 02480099
               RP-REPORT-LOC-NBR           PIC X(04).                   02490099
           05  RP-REPORT-DEPT-9 REDEFINES                               02500099
               RP-REPORT-LOC-NBR           PIC 9(04).                   02510099
           05  FILLER                      PIC X(01)       VALUE SPACES.02520099
           05  RP-REPORT-TITLE-DESC        PIC X(40)       VALUE SPACES.02530099
                                                                        02540099
       01  RP-REPORT-HEADER-1A.                                         02550099
           05  FILLER                      PIC X(10)       VALUE SPACES.02560099
           05  FILLER                      PIC X(04)       VALUE        02570099
               'DEPT'.                                                  02580099
           05  FILLER                      PIC X(02)       VALUE SPACES.02590099
           05  FILLER                      PIC X(04)       VALUE        02600099
               'SCLS'.                                                  02610099
           05  FILLER                      PIC X(04)       VALUE SPACES.02620099
           05  FILLER                      PIC X(03)       VALUE        02630099
               'SKU'.                                                   02640099
           05  FILLER                      PIC X(05)       VALUE SPACES.02650099
P48331     05  FILLER                      PIC X(10)       VALUE        02660099
P48331         'ORDER NBR '.                                            02670099
           05  FILLER                      PIC X(02)       VALUE SPACES.02680099
           05  FILLER                      PIC X(11)       VALUE        02690099
               'CREATE DATE'.                                           02700099
           05  FILLER                      PIC X(03)       VALUE SPACES.02710099
           05  FILLER                      PIC X(09)       VALUE        02720099
               'SHIP DATE'.                                             02730099
           05  FILLER                      PIC X(08)       VALUE SPACES.02740099
           05  FILLER                      PIC X(08)       VALUE        02750099
               'QUANTITY'.                                              02760099
           05  FILLER                      PIC X(06)       VALUE SPACES.02770099
           05  FILLER                      PIC X(11)       VALUE        02780099
               'UNIT RETAIL'.                                           02790099
           05  FILLER                      PIC X(08)       VALUE SPACES.02800099
           05  FILLER                      PIC X(15)       VALUE        02810099
               'EXTENDED RETAIL'.                                       02820099
P48331     05  FILLER                      PIC X(01)       VALUE SPACES.02830099
                                                                        02840099
       01  RP-REPORT-HEADER-2A.                                         02850099
           05  FILLER                      PIC X(10)      VALUE SPACES. 02860099
           05  FILLER                      PIC X(04)      VALUE ALL '-'.02870099
           05  FILLER                      PIC X(02)      VALUE SPACES. 02880099
           05  FILLER                      PIC X(04)      VALUE ALL '-'.02890099
           05  FILLER                      PIC X(02)      VALUE SPACES. 02900099
           05  FILLER                      PIC X(08)      VALUE ALL '-'.02910099
           05  FILLER                      PIC X(02)      VALUE SPACES. 02920099
P48331     05  FILLER                      PIC X(10)      VALUE ALL '-'.02930099
           05  FILLER                      PIC X(02)      VALUE SPACES. 02940099
           05  FILLER                      PIC X(11)      VALUE ALL '-'.02950099
           05  FILLER                      PIC X(02)      VALUE SPACES. 02960099
           05  FILLER                      PIC X(10)      VALUE ALL '-'.02970099
           05  FILLER                      PIC X(06)      VALUE SPACES. 02980099
           05  FILLER                      PIC X(10)      VALUE ALL '-'.02990099
           05  FILLER                      PIC X(06)      VALUE SPACES. 03000099
           05  FILLER                      PIC X(11)      VALUE ALL '-'.03010099
           05  FILLER                      PIC X(08)      VALUE SPACES. 03020099
           05  FILLER                      PIC X(15)      VALUE ALL '-'.03030099
P48331     05  FILLER                      PIC X(01)      VALUE SPACES. 03040099
                                                                        03050099
       01  RP-REPORT-DETAIL-A.                                          03060099
           05  FILLER                      PIC X(10)       VALUE SPACES.03070099
           05  RP-DEPT-A                   PIC X(04)       VALUE SPACES.03080099
           05  RP-DEPT-A-9 REDEFINES RP-DEPT-A                          03090099
                                           PIC 9(04).                   03100099
           05  RP-LOC-NBR-A REDEFINES RP-DEPT-A                         03110099
                                           PIC X(04).                   03120099
           05  RP-LOC-NBR-A-9 REDEFINES RP-DEPT-A                       03130099
                                           PIC 9(04).                   03140099
           05  FILLER                      PIC X(02)       VALUE SPACES.03150099
           05  RP-CLASS-NBR-A              PIC X(04)       VALUE SPACES.03160099
           05  RP-CLASS-NBR-A-9 REDEFINES RP-CLASS-NBR-A                03170099
                                           PIC 9(04).                   03180099
           05  FILLER                      PIC X(02)       VALUE SPACES.03190099
           05  RP-SKU-NBR-A                PIC X(08)       VALUE SPACES.03200099
           05  FILLER                      PIC X(02)       VALUE SPACES.03210099
P48331     05  RP-ORDER-NBR-A              PIC 9(10)       VALUE ZEROES.03220099
           05  FILLER                      PIC X(03)       VALUE SPACES.03230099
           05  RP-CREATE-DATE-A            PIC X(10)       VALUE SPACES.03240099
           05  FILLER                      PIC X(02)       VALUE SPACES.03250099
           05  RP-SHIP-DATE-A              PIC X(10)       VALUE SPACES.03260099
           05  FILLER                      PIC X(02)       VALUE SPACES.03270099
           05  RP-SHIP-QTY-A               PIC ZZ,ZZZ,ZZZ,ZZ9-          03280099
                                                           VALUE ZEROES.03290099
           05  FILLER                      PIC X(02)       VALUE SPACES.03300099
           05  RP-ADJ-RTL-AMT-A            PIC ZZZ,ZZZ,ZZ9.99-          03310099
                                                           VALUE ZEROES.03320099
           05  FILLER                      PIC X(02)       VALUE SPACES.03330099
           05  RP-EXTD-RTL-AMT-A           PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-    03340099
                                                           VALUE ZEROES.03350099
P48331     05  FILLER                      PIC X(01)       VALUE SPACES.03360099
                                                                        03370099
       01  RP-REPORT-TOTALS-A.                                          03380099
           05  FILLER                      PIC X(08)       VALUE SPACES.03390099
           05  RP-TTL-TYPE-A               PIC X(10)       VALUE SPACES.03400099
               88  RP-DEPT-TOTALS-A                        VALUE        03410099
                   ' DEPT     '.                                        03420099
               88  RP-STORE-TOTALS-A                       VALUE        03430099
                   'STORE     '.                                        03440099
               88  RP-GRAND-TOTALS-A                       VALUE        03450099
                   '     GRAND'.                                        03460099
           05  FILLER REDEFINES RP-TTL-TYPE-A.                          03470099
               10  FILLER                  PIC X(06).                   03480099
               10  RP-TTL-DEPT-A           PIC X(04).                   03490099
               10  RP-TTL-DEPT-A-9 REDEFINES RP-TTL-DEPT-A              03500099
                                           PIC 9(04).                   03510099
               10  RP-TTL-LOC-NBR-A REDEFINES RP-TTL-DEPT-A             03520099
                                           PIC X(04).                   03530099
               10  RP-TTL-LOC-NBR-A-9 REDEFINES RP-TTL-DEPT-A           03540099
                                           PIC 9(04).                   03550099
           05  FILLER                      PIC X(01)       VALUE SPACES.03560099
           05  FILLER                      PIC X(06)       VALUE        03570099
               'TOTAL:'.                                                03580099
P48331     05  FILLER                      PIC X(44)       VALUE SPACES.03590099
           05  RP-TTL-SHIP-QTY-A           PIC ZZ,ZZZ,ZZZ,ZZ9-          03600099
                                                           VALUE ZEROES.03610099
           05  FILLER                      PIC X(19)       VALUE SPACES.03620099
           05  RP-TTL-EXTD-RTL-AMT-A       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-    03630099
                                                           VALUE ZEROES.03640099
P48331     05  FILLER                      PIC X(01)       VALUE SPACES.03650099
                                                                        03660099
       01  RP-REPORT-TITLE-1B.                                          03670099
           05  FILLER                      PIC X(36)       VALUE SPACES.03680099
           05  FILLER                      PIC X(28)       VALUE        03690099
               'INVENTORY STORE-PARTIAL SHIP'.                          03700099
           05  FILLER                      PIC X(04)       VALUE        03710099
               ' ADJ'.                                                  03720099
           05  FILLER                      PIC X(29)       VALUE        03730099
               ' DEPT SUMMARY REPORT BY STORE'.                         03740099
           05  FILLER                      PIC X(37)       VALUE SPACES.03750099
                                                                        03760099
       01  RP-REPORT-HEADER-1B.                                         03770099
           05  FILLER                      PIC X(25)       VALUE SPACES.03780099
           05  FILLER                      PIC X(10)       VALUE        03790099
               'DEPARTMENT'.                                            03800099
           05  FILLER                      PIC X(23)       VALUE SPACES.03810099
           05  FILLER                      PIC X(09)       VALUE        03820099
               'SHIP DATE'.                                             03830099
           05  FILLER                      PIC X(09)       VALUE SPACES.03840099
           05  FILLER                      PIC X(08)       VALUE        03850099
               'QUANTITY'.                                              03860099
           05  FILLER                      PIC X(08)       VALUE SPACES.03870099
           05  FILLER                      PIC X(15)       VALUE        03880099
               'EXTENDED RETAIL'.                                       03890099
           05  FILLER                      PIC X(26)       VALUE SPACES.03900099
                                                                        03910099
       01  RP-REPORT-HEADER-2B.                                         03920099
           05  FILLER                      PIC X(25)      VALUE SPACES. 03930099
           05  FILLER                      PIC X(30)      VALUE ALL '-'.03940099
           05  FILLER                      PIC X(02)      VALUE SPACES. 03950099
           05  FILLER                      PIC X(10)      VALUE ALL '-'.03960099
           05  FILLER                      PIC X(02)      VALUE SPACES. 03970099
           05  FILLER                      PIC X(15)      VALUE ALL '-'.03980099
           05  FILLER                      PIC X(02)      VALUE SPACES. 03990099
           05  FILLER                      PIC X(21)      VALUE ALL '-'.04000099
           05  FILLER                      PIC X(25)      VALUE SPACES. 04010099
                                                                        04020099
       01  RP-REPORT-DETAIL-B.                                          04030099
           05  FILLER                      PIC X(25)       VALUE SPACES.04040099
           05  RP-DEPT-B                   PIC X(04)       VALUE SPACES.04050099
           05  RP-DEPT-B-9 REDEFINES RP-DEPT-B                          04060099
                                           PIC 9(04).                   04070099
           05  RP-LOC-NBR-B REDEFINES RP-DEPT-B                         04080099
                                           PIC X(04).                   04090099
           05  RP-LOC-NBR-B-9 REDEFINES RP-DEPT-B                       04100099
                                           PIC 9(04).                   04110099
           05  FILLER                      PIC X(01)       VALUE SPACES.04120099
           05  RP-DESCRIPTION-B            PIC X(25)       VALUE SPACES.04130099
           05  FILLER                      PIC X(02)       VALUE SPACES.04140099
           05  RP-SHIP-DATE-B              PIC X(10)       VALUE SPACES.04150099
           05  FILLER                      PIC X(03)       VALUE SPACES.04160099
           05  RP-SHIP-QTY-B               PIC ZZ,ZZZ,ZZZ,ZZ9-          04170099
                                                           VALUE ZEROES.04180099
           05  FILLER                      PIC X(02)       VALUE SPACES.04190099
           05  RP-EXTD-RTL-AMT-B           PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-    04200099
                                                           VALUE ZEROES.04210099
           05  FILLER                      PIC X(25)       VALUE SPACES.04220099
                                                                        04230099
       01  RP-REPORT-TOTALS-B.                                          04240099
           05  FILLER                      PIC X(26)       VALUE SPACES.04250099
           05  RP-TTL-TYPE-B               PIC X(10)       VALUE SPACES.04260099
               88  RP-DEPT-TOTALS-B                        VALUE        04270099
                   ' DEPT     '.                                        04280099
               88  RP-STORE-TOTALS-B                       VALUE        04290099
                   'STORE     '.                                        04300099
               88  RP-GRAND-TOTALS-B                       VALUE        04310099
                   '     GRAND'.                                        04320099
           05  FILLER REDEFINES RP-TTL-TYPE-B.                          04330099
               10  FILLER                  PIC X(06).                   04340099
               10  RP-TTL-DEPT-B           PIC X(04).                   04350099
               10  RP-TTL-DEPT-B-9 REDEFINES RP-TTL-DEPT-B              04360099
                                           PIC 9(04).                   04370099
               10  RP-TTL-LOC-NBR-B REDEFINES RP-TTL-DEPT-B             04380099
                                           PIC X(04).                   04390099
               10  RP-TTL-LOC-NBR-B-9 REDEFINES RP-TTL-DEPT-B           04400099
                                           PIC 9(04).                   04410099
           05  FILLER                      PIC X(07)       VALUE        04420099
               ' TOTAL:'.                                               04430099
           05  FILLER                      PIC X(27)       VALUE SPACES.04440099
           05  RP-TTL-SHIP-QTY-B           PIC ZZ,ZZZ,ZZZ,ZZ9-          04450099
                                                           VALUE ZEROES.04460099
           05  FILLER                      PIC X(02)       VALUE SPACES.04470099
           05  RP-TTL-EXTD-RTL-AMT-B       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-    04480099
                                                           VALUE ZEROES.04490099
           05  FILLER                      PIC X(25)       VALUE SPACES.04500099
                                                                        04510099
       01  RP-REPORT-TITLE-1C.                                          04520099
           05  FILLER                      PIC X(36)       VALUE SPACES.04530099
           05  FILLER                      PIC X(28)       VALUE        04540099
               'INVENTORY STORE-PARTIAL SHIP'.                          04550099
           05  FILLER                      PIC X(11)       VALUE        04560099
               ' ADJUSTMENT'.                                           04570099
           05  FILLER                      PIC X(24)       VALUE        04580099
               ' SUMMARY REPORT BY STORE'.                              04590099
           05  FILLER                      PIC X(35)       VALUE SPACES.04600099
                                                                        04610099
       01  RP-REPORT-HEADER-1C.                                         04620099
           05  FILLER                      PIC X(25)       VALUE SPACES.04630099
           05  FILLER                      PIC X(08)       VALUE        04640099
               'LOCATION'.                                              04650099
           05  FILLER                      PIC X(25)       VALUE SPACES.04660099
           05  FILLER                      PIC X(09)       VALUE        04670099
               'SHIP DATE'.                                             04680099
           05  FILLER                      PIC X(09)       VALUE SPACES.04690099
           05  FILLER                      PIC X(08)       VALUE        04700099
               'QUANTITY'.                                              04710099
           05  FILLER                      PIC X(08)       VALUE SPACES.04720099
           05  FILLER                      PIC X(15)       VALUE        04730099
               'EXTENDED RETAIL'.                                       04740099
           05  FILLER                      PIC X(26)       VALUE SPACES.04750099
                                                                        04760099
       01  RP-REPORT-HEADER-2C.                                         04770099
           05  FILLER                      PIC X(25)      VALUE SPACES. 04780099
           05  FILLER                      PIC X(30)      VALUE ALL '-'.04790099
           05  FILLER                      PIC X(02)      VALUE SPACES. 04800099
           05  FILLER                      PIC X(10)      VALUE ALL '-'.04810099
           05  FILLER                      PIC X(02)      VALUE SPACES. 04820099
           05  FILLER                      PIC X(15)      VALUE ALL '-'.04830099
           05  FILLER                      PIC X(02)      VALUE SPACES. 04840099
           05  FILLER                      PIC X(21)      VALUE ALL '-'.04850099
           05  FILLER                      PIC X(25)      VALUE SPACES. 04860099
                                                                        04870099
       01  RP-REPORT-DETAIL-C.                                          04880099
           05  FILLER                      PIC X(25)       VALUE SPACES.04890099
           05  RP-DEPT-C                   PIC X(04)       VALUE SPACES.04900099
           05  RP-DEPT-C-9 REDEFINES RP-DEPT-C                          04910099
                                           PIC 9(04).                   04920099
           05  RP-LOC-NBR-C REDEFINES RP-DEPT-C                         04930099
                                           PIC X(04).                   04940099
           05  RP-LOC-NBR-C-9 REDEFINES RP-DEPT-C                       04950099
                                           PIC 9(04).                   04960099
           05  FILLER                      PIC X(01)       VALUE SPACES.04970099
           05  RP-DESCRIPTION-C            PIC X(25)       VALUE SPACES.04980099
           05  FILLER                      PIC X(02)       VALUE SPACES.04990099
           05  RP-SHIP-DATE-C              PIC X(10)       VALUE SPACES.05000099
           05  FILLER                      PIC X(03)       VALUE SPACES.05010099
           05  RP-SHIP-QTY-C               PIC ZZ,ZZZ,ZZZ,ZZ9-          05020099
                                                           VALUE ZEROES.05030099
           05  FILLER                      PIC X(02)       VALUE SPACES.05040099
           05  RP-EXTD-RTL-AMT-C           PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-    05050099
                                                           VALUE ZEROES.05060099
           05  FILLER                      PIC X(25)       VALUE SPACES.05070099
                                                                        05080099
       01  RP-REPORT-TOTALS-C.                                          05090099
           05  FILLER                      PIC X(31)       VALUE SPACES.05100099
           05  RP-TTL-TYPE-C               PIC X(12)       VALUE        05110099
               'GRAND TOTAL:'.                                          05120099
           05  FILLER                      PIC X(27)       VALUE SPACES.05130099
           05  RP-TTL-SHIP-QTY-C           PIC ZZ,ZZZ,ZZZ,ZZ9-          05140099
                                                           VALUE ZEROES.05150099
           05  FILLER                      PIC X(02)       VALUE SPACES.05160099
           05  RP-TTL-EXTD-RTL-AMT-C       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-    05170099
                                                           VALUE ZEROES.05180099
           05  FILLER                      PIC X(25)       VALUE SPACES.05190099
                                                                        05200099
       01  RP-REPORT-TITLE-1S.                                          05210099
           05  FILLER                      PIC X(01)       VALUE SPACES.05220099
           05  FILLER                      PIC X(40)       VALUE SPACES.05230099
           05  FILLER                      PIC X(28)       VALUE        05240099
               'INVENTORY STORE-PARTIAL SHIP'.                          05250099
           05  FILLER                      PIC X(23)       VALUE        05260099
               ' ADJ DAILY STORE STATUS'.                               05270099
           05  FILLER                      PIC X(42)       VALUE SPACES.05280099
                                                                        05290099
       01  RP-REPORT-HEADER-1S.                                         05300099
           05  FILLER                      PIC X(25)       VALUE SPACES.05310099
           05  FILLER                      PIC X(08)       VALUE        05320099
               'LOCATION'.                                              05330099
C63961     05  FILLER                      PIC X(34)       VALUE SPACES.05340099
           05  FILLER                      PIC X(12)       VALUE        05350099
               'RECORD COUNT'.                                          05360099
           05  FILLER                      PIC X(07)       VALUE SPACES.05370099
           05  FILLER                      PIC X(15)       VALUE        05380099
               'COMMENTS/STATUS'.                                       05390099
           05  FILLER                      PIC X(36)       VALUE SPACES.05400099
                                                                        05410099
       01  RP-REPORT-HEADER-2S.                                         05420099
           05  FILLER                      PIC X(25)      VALUE SPACES. 05430099
           05  FILLER                      PIC X(30)      VALUE ALL '-'.05440099
C63961     05  FILLER                      PIC X(11)      VALUE SPACES. 05450099
           05  FILLER                      PIC X(14)      VALUE ALL '-'.05460099
           05  FILLER                      PIC X(06)      VALUE SPACES. 05470099
           05  FILLER                      PIC X(41)      VALUE ALL '-'.05480099
           05  FILLER                      PIC X(05)      VALUE SPACES. 05490099
                                                                        05500099
       01  RP-REPORT-DETAIL-S.                                          05510099
           05  FILLER                      PIC X(25)       VALUE SPACES.05520099
           05  RP-LOC-NBR-S                PIC X(04)       VALUE SPACES.05530099
           05  RP-LOC-NBR-S-9 REDEFINES RP-LOC-NBR-S                    05540099
                                           PIC 9(04).                   05550099
           05  FILLER                      PIC X(01)       VALUE SPACES.05560099
           05  RP-LOC-NM-S                 PIC X(25)       VALUE SPACES.05570099
C63961     05  FILLER                      PIC X(10)       VALUE SPACES.05580099
           05  RP-RECORD-COUNT-S           PIC ZZ,ZZZ,ZZZ,ZZ9-          05590099
                                                           VALUE ZEROES.05600099
           05  FILLER                      PIC X(06)       VALUE SPACES.05610099
           05  RP-STATUS-S                 PIC X(41)       VALUE SPACES.05620099
               88  RP-MISSING-LOCATION-S                   VALUE        05630099
C63961             '*** NO PARTIAL SHIPS FOUND FOR THIS STORE'.         05640099
               88  RP-ADDITION-LOCATION-S                  VALUE        05650099
                   'LOCATION IS UNEXPECTED; WILL BE IGNORED. '.         05660099
           05  FILLER                      PIC X(05)       VALUE SPACES.05670099
                                                                        05680099
       01  RP-REPORT-TOTALS-S.                                          05690099
           05  FILLER                      PIC X(31)       VALUE SPACES.05700099
           05  RP-TTL-TYPE-S               PIC X(12)       VALUE        05710099
               'GRAND TOTAL:'.                                          05720099
           05  FILLER                      PIC X(22)       VALUE SPACES.05730099
           05  RP-TTL-RECORD-COUNT-S       PIC ZZ,ZZZ,ZZZ,ZZ9-          05740099
                                                           VALUE ZEROES.05750099
           05  FILLER                      PIC X(52)       VALUE SPACES.05760099
                                                                        05770099
       01  RP-REPORT-END.                                               05780099
           05  FILLER                      PIC X(46)       VALUE SPACES.05790099
           05  FILLER                      PIC X(39)       VALUE        05800099
               '*****  E N D   O F   R E P O R T  *****'.               05810099
                                                                        05820099
      ******************************************************************05830099
       01  IN-RPT-CNTL-AREA.                                            05840099
           05  IN-RPT-DATE                 PIC X(10)       VALUE SPACES.05850099
           05  FILLER                      PIC X(70)       VALUE SPACES.05860099
                                                                        05870099
      ******************************************************************05880099
       01  PT-PROGRAM-TABLE-AREA.                                       05890099
           05  PT-STORE-TABLE.                                          05900099
               10  PT-STORE-TBL-INDX       PIC S9(05)      VALUE ZEROES 05910099
                               COMP-3.                                  05920099
               10  PT-STORE-TBL-MAX        PIC S9(05)      VALUE +99999 05930099
                               COMP-3.                                  05940099
               10  PT-STORE-TBL-COUNT      PIC S9(05)      VALUE ZEROES 05950099
                               COMP-3.                                  05960099
               10  PT-STORE-TBL OCCURS 99999 TIMES.                     05970099
                   15  PT-STORE-TBL-LOC    PIC S9(04)      VALUE ZEROES 05980099
                               COMP.                                    05990099
                   15  PT-STORE-TBL-LOC-TYP                             06000099
                                           PIC X(02)       VALUE SPACES.06010099
                   15  PT-STORE-TBL-LOC-NM PIC X(40)       VALUE SPACES.06020099
                   15  PT-STORE-TBL-SOURCE PIC X(02)       VALUE SPACES.06030099
                       88  PT-STORE-TBL-INVENTORY          VALUE 'IN'.  06040099
                       88  PT-STORE-TBL-RECEIVING          VALUE 'RC'.  06050099
                   15  PT-STORE-TBL-RECS   PIC S9(11)      VALUE ZEROES 06060099
                                                           COMP-3.      06070099
                                                                        06080099
      ***************************************************************** 06090099
      *  INPUT FILE                                                     06100099
      ******************************************************************06110099
           COPY INRD157.                                                06120099
                                                                        06130099
      ***************************************************************** 06140099
      *  SQL ABEND ROUTINE WORKING STORAGE                              06150099
      ***************************************************************** 06160099
      *01  DP004-DB2-ERROR-FIELDS.                                      06170099
           COPY DPWS004.                                                06180099
                                                                        06190099
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES 06200099
                                                           COMP SYNC.   06210099
           88  TABLE-EXCEEDED-ABEND-4003                   VALUE +4003. 06220099
           88  REQUEST-CNTL-ABEND-4003                     VALUE +4003. 06230099
           88  SQL-ABEND-4013                              VALUE +4013. 06240099
                                                                        06250099
           EXEC SQL                                                     06260099
               INCLUDE SQLCA                                            06270099
           END-EXEC.                                                    06280099
                                                                        06290099
      *---------------------------------------------------------------* 06300099
      *    TINVPAR - PHYSICAL INVENTORY PARAMETER TABLE               * 06310099
      *---------------------------------------------------------------* 06320099
           EXEC SQL                                                     06330099
               INCLUDE TINVPAR                                          06340099
           END-EXEC.                                                    06350099
                                                                        06360099
      *---------------------------------------------------------------* 06370099
      *    TINCNTL - PHYSICAL INVENTORY CONTROL MASTER TABLE          * 06380099
      *---------------------------------------------------------------* 06390099
           EXEC SQL                                                     06400099
               INCLUDE TINCNTL                                          06410099
           END-EXEC.                                                    06420099
                                                                        06430099
      *---------------------------------------------------------------* 06440099
      *    XMT_LOC - STORE LOCATION TABLE                             * 06450099
      *---------------------------------------------------------------* 06460099
           EXEC SQL                                                     06470099
               INCLUDE XMT00001                                         06480099
           END-EXEC.                                                    06490099
                                                                        06500099
      *---------------------------------------------------------------* 06510099
      *    TMDSEAR - MERCH. HEIRARCHY                                 * 06520099
      *---------------------------------------------------------------* 06530099
           EXEC SQL                                                     06540099
               INCLUDE TMDSEAR                                          06550099
           END-EXEC.                                                    06560099
                                                                        06570099
      *---------------------------------------------------------------* 06580099
      *    TINVPAR - INVENTORYING LOCATIONS CURSOR                    * 06590099
      *---------------------------------------------------------------* 06600099
           EXEC SQL DECLARE INV-STR-CSR CURSOR FOR                      06610099
                SELECT A.LOC_NBR                                        06620099
                      ,A.ACTL_INV_DTE                                   06630099
                  FROM TINVPAR A ,                                      06640099
                       TINCNTL B                                        06650099
                 WHERE B.ACTV_IND         = 'Y'                         06660099
                   AND A.INV_ID           = B.INV_ID                    06670099
                   AND PLND_INV_DTE       = :IN-RPT-DATE                06680099
                   AND A.LOC_INV_STAT_CDE = 'IN'                        06690099
                 ORDER BY                                               06700099
                       LOC_NBR                                          06710099
           END-EXEC.                                                    06720099
                                                                        06730099
       PROCEDURE DIVISION.                                              06740099
                                                                        06750099
      ******************************************************************06760099
      *  MAIN RPOCESSING.                                               06770099
      ******************************************************************06780099
       A100-MAIN-MODULE.                                                06790099
                                                                        06800099
           PERFORM B100-HOUSEKEEPING.                                   06810099
                                                                        06820099
           PERFORM B200-PROCESS-ADJ-BY-STORE                            06830099
              UNTIL END-OF-ADJ-FILE.                                    06840099
                                                                        06850099
           PERFORM B900-FINALIZE.                                       06860099
                                                                        06870099
           STOP RUN.                                                    06880099
                                                                        06890099
      ******************************************************************06900099
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET      06910099
      * THE REPORT NAME AND NUMBER.                                     06920099
      ******************************************************************06930099
       B100-HOUSEKEEPING.                                               06940099
                                                                        06950099
           ACCEPT PV-TODAYS-DATE FROM DATE.                             06960099
           ACCEPT PV-TODAYS-TIME FROM TIME.                             06970099
                                                                        06980099
           MOVE PV-TODAYS-DATE-YY  TO PV-PROCESSING-YY.                 06990099
           MOVE PV-TODAYS-DATE-MM  TO PV-PROCESSING-MM.                 07000099
           MOVE PV-TODAYS-DATE-DD  TO PV-PROCESSING-DD.                 07010099
                                                                        07020099
           MOVE '-'                TO PV-PROC-DSH1.                     07030099
           MOVE '-'                TO PV-PROC-DSH2.                     07040099
                                                                        07050099
           IF PV-TODAYS-DATE-YY > '80'                                  07060099
              MOVE '19'            TO PV-PROCESSING-CC                  07070099
           ELSE                                                         07080099
              MOVE '20'            TO PV-PROCESSING-CC                  07090099
           END-IF.                                                      07100099
                                                                        07110099
           MOVE PV-TODAYS-DATE-MM  TO DP132O-RUN-MONTH.                 07120099
           MOVE PV-TODAYS-DATE-DD  TO DP132O-RUN-DAY.                   07130099
           MOVE PV-TODAYS-DATE-YY  TO DP132O-RUN-YEAR.                  07140099
           MOVE PV-TODAYS-TIME-HR  TO DP132O-RUN-HOUR.                  07150099
           MOVE PV-TODAYS-TIME-MIN TO DP132O-RUN-MINUTE.                07160099
           MOVE PC-PROGRAM-NAME    TO DP132O-PROGRAM-NAME.              07170099
                                                                        07180099
           MOVE ZEROES TO PV-GRD-TTL-SHIP-QTY                           07190099
                          PV-GRD-TTL-EXTD-RTL-AMT.                      07200099
                                                                        07210099
           OPEN INPUT  RPT-CONTROL-FILE                                 07220099
                INPUT  STORE-PARTIAL-ADJ-FILE                           07230099
                OUTPUT STORE-PARTIAL-DTL-RPT                            07240099
                       STORE-PARTIAL-SUM-1-RPT                          07250099
                       STORE-PARTIAL-SUM-2-RPT                          07260099
                       STORE-PARTIAL-STATUS-RPT.                        07270099
                                                                        07280099
           DISPLAY ' '.                                                 07290099
           DISPLAY '***************************************'.           07300099
           DISPLAY '*** PROGRAM NAME: '         PC-PROGRAM-NAME.        07310099
           DISPLAY ' '.                                                 07320099
                                                                        07330099
           PERFORM Z200-READ-CNTL-FILE.                                 07340099
                                                                        07350099
           PERFORM Z100-READ-ADJ-FILE.                                  07360099
                                                                        07370099
           MOVE ZEROES             TO PT-STORE-TBL-COUNT.               07380099
           PERFORM C100-LOAD-INV-STR.                                   07390099
           DISPLAY '   ' PT-STORE-TBL-COUNT ' STORES LOADED'            07400099
                   ' FOR DATE: '                                        07410099
                   IN-RPT-DATE.                                         07420099
                                                                        07430099
      ******************************************************************07440099
      *    REPORT STORE-PARTIAL SHIP ADJUSTMENT BY STORE/DEPT           07450099
      ******************************************************************07460099
       B200-PROCESS-ADJ-BY-STORE.                                       07470099
                                                                        07480099
           IF IN157-STORE-NBR NOT = PV-CURR-LOC-NBR                     07490099
              MOVE IN157-STORE-NBR         TO PV-CURR-LOC-NBR           07500099
              PERFORM C200-VALIDATE-INV-STR                             07510099
           END-IF.                                                      07520099
                                                                        07530099
           IF VALID-MANIFESTED-INV-STORE                                07540099
               IF IN157-STORE-NBR-9 = RP-REPORT-LOC-NBR-9               07550099
                   CONTINUE                                             07560099
               ELSE                                                     07570099
                   MOVE IN157-STORE-NBR-9  TO RP-REPORT-LOC-NBR-9       07580099
                   MOVE XMT00001-LOC-NM    TO RP-REPORT-TITLE-DESC      07590099
                   MOVE ZEROES             TO PV-GRP-TTL-SHIP-QTY       07600099
                                              PV-GRP-TTL-EXTD-RTL-AMT   07610099
                                              PV-PRINT-LINE-COUNT-A     07620099
                                              PV-PRINT-LINE-COUNT-B     07630099
               END-IF                                                   07640099
                                                                        07650099
               MOVE IN157-DEPT         TO PV-CURR-DEPT                  07660099
                                                                        07670099
               MOVE ZEROES TO PV-SUB-TTL-SHIP-QTY                       07680099
                              PV-SUB-TTL-EXTD-RTL-AMT                   07690099
                                                                        07700099
               PERFORM B300-PRODUCE-REPORT                              07710099
                  UNTIL END-OF-ADJ-FILE                                 07720099
                     OR IN157-STORE-NBR NOT = PV-CURR-LOC-NBR           07730099
                     OR IN157-DEPT      NOT = PV-CURR-DEPT              07740099
                                                                        07750099
               PERFORM B400-PRODUCE-TOTALS                              07760099
           ELSE                                                         07770099
      ***************************************************************   07780099
      *    THE READ LOGIC IS PRESENTED HERE FOR TEST PURPOSE ONLY.  *   07790099
      *    THERE SHOULD BE NO DATA PASSED INTO THIS PROGRAM FOR     *   07800099
      *    THE NON-MANIFESTED STORE, OR NON-INVENTORYING STORE.     *   07810099
      ***************************************************************   07820099
               DISPLAY ' *** SKIPPING NON-MANIFESTED STORE: '           07830099
                       PV-CURR-LOC-NBR ' ***'                           07840099
               PERFORM Z100-READ-ADJ-FILE                               07850099
                   UNTIL END-OF-ADJ-FILE                                07860099
                       OR IN157-STORE-NBR NOT = PV-CURR-LOC-NBR         07870099
           END-IF.                                                      07880099
                                                                        07890099
           IF END-OF-ADJ-FILE                                           07900099
               MOVE PV-RECORD-COUNT                                     07910099
                   TO PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)              07920099
           ELSE                                                         07930099
               COMPUTE PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)             07940099
                   = PV-RECORD-COUNT - 1                                07950099
           END-IF.                                                      07960099
                                                                        07970099
      ******************************************************************07980099
      *    REPORT STORE-PARTIAL SHIP ADJUSTMENT BY DEPT/STORE           07990099
      ******************************************************************08000099
       B250-PROCESS-ADJ-BY-DEPT.                                        08010099
                                                                        08020099
           IF IN157-STORE-NBR NOT = PV-CURR-LOC-NBR                     08030099
              MOVE IN157-STORE-NBR         TO PV-CURR-LOC-NBR           08040099
              PERFORM C200-VALIDATE-INV-STR                             08050099
           END-IF.                                                      08060099
                                                                        08070099
           IF VALID-MANIFESTED-INV-STORE                                08080099
               IF IN157-DEPT-9 = RP-REPORT-DEPT-9                       08090099
                   CONTINUE                                             08100099
               ELSE                                                     08110099
                   MOVE IN157-DEPT-9       TO RP-REPORT-DEPT-9          08120099
                                              PV-CURR-DEPT-9            08130099
                   MOVE PV-CURR-DEPT-3X    TO MDSEAR-DEPT-NBR           08140099
                   PERFORM Z400-GET-DEPT-INFO                           08150099
                   MOVE MDSEAR-DESC        TO RP-REPORT-TITLE-DESC      08160099
                   MOVE ZEROES             TO PV-GRP-TTL-SHIP-QTY       08170099
                                              PV-GRP-TTL-EXTD-RTL-AMT   08180099
                                              PV-PRINT-LINE-COUNT-A     08190099
                                              PV-PRINT-LINE-COUNT-B     08200099
               END-IF                                                   08210099
                                                                        08220099
               MOVE ZEROES TO PV-SUB-TTL-SHIP-QTY                       08230099
                              PV-SUB-TTL-EXTD-RTL-AMT                   08240099
                                                                        08250099
               PERFORM B300-PRODUCE-REPORT                              08260099
                  UNTIL END-OF-ADJ-FILE                                 08270099
                     OR IN157-STORE-NBR NOT = PV-CURR-LOC-NBR           08280099
                     OR IN157-DEPT      NOT = PV-CURR-DEPT              08290099
                                                                        08300099
               PERFORM B400-PRODUCE-TOTALS                              08310099
           ELSE                                                         08320099
      ***************************************************************   08330099
      *    THE READ LOGIC IS PRESENTED HERE FOR TEST PURPOSE ONLY.  *   08340099
      *    THERE SHOULD BE NO DATA PASSED INTO THIS PROGREAM FOR    *   08350099
      *    THE NON-MANIFESTED STORE, OR NON-INVENTORYING STORE.     *   08360099
      ***************************************************************   08370099
               PERFORM Z100-READ-ADJ-FILE                               08380099
                  UNTIL END-OF-ADJ-FILE                                 08390099
                     OR IN157-STORE-NBR NOT = PV-CURR-LOC-NBR           08400099
           END-IF.                                                      08410099
                                                                        08420099
           IF END-OF-ADJ-FILE                                           08430099
              MOVE PV-RECORD-COUNT                                      08440099
                              TO PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)   08450099
           ELSE                                                         08460099
              COMPUTE PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)              08470099
                                = PV-RECORD-COUNT - 1                   08480099
           END-IF.                                                      08490099
                                                                        08500099
      ******************************************************************08510099
      *    PRODUCE THE DETAIL REPORT                                    08520099
      ******************************************************************08530099
       B300-PRODUCE-REPORT.                                             08540099
                                                                        08550099
           PERFORM D100-FORMAT-DETAIL-RPT.                              08560099
                                                                        08570099
           ADD IN157-ADJ-QTY            TO PV-SUB-TTL-SHIP-QTY          08580099
                                           PV-GRP-TTL-SHIP-QTY          08590099
                                           PV-GRD-TTL-SHIP-QTY.         08600099
                                                                        08610099
           ADD IN157-EXTD-RTL-AMT       TO PV-SUB-TTL-EXTD-RTL-AMT      08620099
                                           PV-GRP-TTL-EXTD-RTL-AMT      08630099
                                           PV-GRD-TTL-EXTD-RTL-AMT.     08640099
                                                                        08650099
           PERFORM Z600-PRINT-DETAILS-A.                                08660099
                                                                        08670099
           PERFORM Z100-READ-ADJ-FILE.                                  08680099
                                                                        08690099
      ******************************************************************08700099
      *    PRODUCE THE TOTALS FOR DEPARTMENTS, STORES, AND GRAND TOTAL  08710099
      ******************************************************************08720099
       B400-PRODUCE-TOTALS.                                             08730099
                                                                        08740099
      * PRODUCE SUB-TOTAL FOR DETAIL RPT AND DETAIL FOR SUMMARY 1 RPT   08750099
           SET RP-DEPT-TOTALS-A             TO TRUE.                    08760099
           MOVE PV-CURR-DEPT-9              TO RP-TTL-DEPT-A-9.         08770099
                                                                        08780099
           MOVE PV-SUB-TTL-SHIP-QTY         TO RP-TTL-SHIP-QTY-A.       08790099
           MOVE PV-SUB-TTL-EXTD-RTL-AMT     TO RP-TTL-EXTD-RTL-AMT-A.   08800099
                                                                        08810099
           MOVE RP-REPORT-TOTALS-A          TO RP-REPORT-DETAIL-A.      08820099
           MOVE 2                           TO PV-ADVANCE-LINE-A.       08830099
           PERFORM Z600-PRINT-DETAILS-A.                                08840099
                                                                        08850099
           PERFORM D200-FORMAT-SUM-1-RPT.                               08860099
           PERFORM Z625-PRINT-DETAILS-B.                                08870099
                                                                        08880099
      * PRODUCE GROUP-TOTAL FOR DETAIL RPT, SUB-TOT FOR SUMMARY 1 RPT   08890099
      * AND DETAIL FOR SUMMARY 2 RPT                                    08900099
           IF END-OF-ADJ-FILE                                           08910099
           OR IN157-STORE-NBR NOT = PV-CURR-LOC-NBR                     08920099
               SET RP-STORE-TOTALS-A                                    08930099
                   RP-STORE-TOTALS-B        TO TRUE                     08940099
               MOVE PV-CURR-LOC-NBR-9       TO RP-TTL-LOC-NBR-A-9       08950099
                                               RP-TTL-LOC-NBR-B-9       08960099
               MOVE PV-GRP-TTL-SHIP-QTY     TO RP-TTL-SHIP-QTY-A        08970099
                                               RP-TTL-SHIP-QTY-B        08980099
               MOVE PV-GRP-TTL-EXTD-RTL-AMT TO RP-TTL-EXTD-RTL-AMT-A    08990099
                                               RP-TTL-EXTD-RTL-AMT-B    09000099
               MOVE RP-REPORT-TOTALS-A      TO RP-REPORT-DETAIL-A       09010099
               MOVE 2 TO PV-ADVANCE-LINE-A                              09020099
               PERFORM Z600-PRINT-DETAILS-A                             09030099
                                                                        09040099
               MOVE RP-REPORT-TOTALS-B      TO RP-REPORT-DETAIL-B       09050099
               MOVE 2 TO PV-ADVANCE-LINE-B                              09060099
               PERFORM Z625-PRINT-DETAILS-B                             09070099
                                                                        09080099
               PERFORM D300-FORMAT-SUM-2-RPT                            09090099
               PERFORM Z650-PRINT-DETAILS-C                             09100099
                                                                        09110099
           END-IF.                                                      09120099
                                                                        09130099
           MOVE 2 TO PV-ADVANCE-LINE-A.                                 09140099
                                                                        09150099
      ******************************************************************09160099
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                      09170099
      ******************************************************************09180099
       B900-FINALIZE.                                                   09190099
                                                                        09200099
           IF PV-PAGE-COUNT-A > 0                                       09210099
               SET RP-GRAND-TOTALS-A                                    09220099
                   RP-GRAND-TOTALS-B        TO TRUE                     09230099
               MOVE PV-GRD-TTL-SHIP-QTY     TO RP-TTL-SHIP-QTY-A        09240099
                                               RP-TTL-SHIP-QTY-B        09250099
                                               RP-TTL-SHIP-QTY-C        09260099
               MOVE PV-GRD-TTL-EXTD-RTL-AMT TO RP-TTL-EXTD-RTL-AMT-A    09270099
                                               RP-TTL-EXTD-RTL-AMT-B    09280099
                                               RP-TTL-EXTD-RTL-AMT-C    09290099
                                                                        09300099
               MOVE RP-REPORT-TOTALS-A      TO RP-REPORT-DETAIL-A       09310099
               MOVE 2 TO PV-ADVANCE-LINE-A                              09320099
               PERFORM Z600-PRINT-DETAILS-A                             09330099
                                                                        09340099
               MOVE RP-REPORT-END TO RP-REPORT-DETAIL-A                 09350099
               MOVE 3 TO PV-ADVANCE-LINE-A                              09360099
               PERFORM Z600-PRINT-DETAILS-A                             09370099
                                                                        09380099
               MOVE RP-REPORT-TOTALS-B      TO RP-REPORT-DETAIL-B       09390099
               MOVE 2 TO PV-ADVANCE-LINE-B                              09400099
               PERFORM Z625-PRINT-DETAILS-B                             09410099
                                                                        09420099
               MOVE RP-REPORT-END TO RP-REPORT-DETAIL-B                 09430099
               MOVE 3 TO PV-ADVANCE-LINE-B                              09440099
               PERFORM Z625-PRINT-DETAILS-B                             09450099
                                                                        09460099
               MOVE RP-REPORT-TOTALS-C      TO RP-REPORT-DETAIL-C       09470099
               MOVE 2 TO PV-ADVANCE-LINE-C                              09480099
               PERFORM Z650-PRINT-DETAILS-C                             09490099
                                                                        09500099
               MOVE RP-REPORT-END TO RP-REPORT-DETAIL-C                 09510099
               MOVE 3 TO PV-ADVANCE-LINE-C                              09520099
               PERFORM Z650-PRINT-DETAILS-C                             09530099
           END-IF.                                                      09540099
                                                                        09550099
           MOVE ZEROES        TO PV-RECORD-COUNT.                       09560099
           PERFORM                                                      09570099
             VARYING PT-STORE-TBL-INDX FROM 1 BY 1                      09580099
               UNTIL PT-STORE-TBL-INDX > PT-STORE-TBL-COUNT             09590099
                                                                        09600099
               MOVE PT-STORE-TBL-LOC(PT-STORE-TBL-INDX)                 09610099
                                                   TO RP-LOC-NBR-S-9    09620099
               MOVE PT-STORE-TBL-LOC-NM(PT-STORE-TBL-INDX)              09630099
                                                   TO RP-LOC-NM-S       09640099
               MOVE PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)                09650099
                                                   TO RP-RECORD-COUNT-S 09660099
               ADD PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)                 09670099
                                                   TO PV-RECORD-COUNT   09680099
               IF PT-STORE-TBL-INVENTORY(PT-STORE-TBL-INDX)             09690099
                  IF PT-STORE-TBL-RECS(PT-STORE-TBL-INDX) > ZEROES      09700099
                     CONTINUE                                           09710099
                  ELSE                                                  09720099
                     SET RP-MISSING-LOCATION-S   TO TRUE                09730099
                  END-IF                                                09740099
               ELSE                                                     09750099
                  SET RP-ADDITION-LOCATION-S     TO TRUE                09760099
               END-IF                                                   09770099
                                                                        09780099
               PERFORM Z675-PRINT-DETAILS-S                             09790099
           END-PERFORM.                                                 09800099
                                                                        09810099
           IF PV-PAGE-COUNT-S > 0                                       09820099
               MOVE PV-RECORD-COUNT         TO RP-TTL-RECORD-COUNT-S    09830099
                                                                        09840099
               MOVE RP-REPORT-TOTALS-S      TO RP-REPORT-DETAIL-S       09850099
               MOVE 2                       TO PV-ADVANCE-LINE-S        09860099
               PERFORM Z675-PRINT-DETAILS-S                             09870099
                                                                        09880099
               MOVE RP-REPORT-END           TO RP-REPORT-DETAIL-S       09890099
               MOVE 3                       TO PV-ADVANCE-LINE-S        09900099
               PERFORM Z675-PRINT-DETAILS-S                             09910099
           END-IF.                                                      09920099
                                                                        09930099
           CLOSE RPT-CONTROL-FILE                                       09940099
                 STORE-PARTIAL-ADJ-FILE                                 09950099
                 STORE-PARTIAL-DTL-RPT                                  09960099
                 STORE-PARTIAL-SUM-1-RPT                                09970099
                 STORE-PARTIAL-SUM-2-RPT                                09980099
                 STORE-PARTIAL-STATUS-RPT.                              09990099
                                                                        10000099
           DISPLAY ' '.                                                 10010099
           MOVE PA-INPUT-READ-CTR           TO PV-DISP-NUMBER.          10020099
           DISPLAY ' INPUT RECORDS READ = '    PV-DISP-NUMBER.          10030099
           DISPLAY ' '.                                                 10040099
                                                                        10050099
      ******************************************************************10060099
      * LOAD STORES INTO WORKING STORAGE TABLE                          10070099
      ******************************************************************10080099
       C100-LOAD-INV-STR.                                               10090099
                                                                        10100099
           PERFORM Z500-OPEN-INV-STR-CSR.                               10110099
                                                                        10120099
           PERFORM Z550-FETCH-INV-STR-CSR.                              10130099
           PERFORM                                                      10140099
             UNTIL END-OF-INV-STR                                       10150099
               ADD +1                  TO PT-STORE-TBL-COUNT            10160099
               IF PT-STORE-TBL-COUNT > PT-STORE-TBL-MAX                 10170099
                   SET TABLE-EXCEEDED-ABEND-4003 TO TRUE                10180099
                   DISPLAY ' *** STORE TABLE EXCEEDED'                  10190099
                           ' IN C100-LOAD-INV-STR ***'                  10200099
                   PERFORM Z999-ABEND                                   10210099
               END-IF                                                   10220099
                                                                        10230099
               MOVE INVPAR-LOC-NBR                                      10240099
                                TO PT-STORE-TBL-LOC(PT-STORE-TBL-COUNT) 10250099
                                   XMT00001-LOC-NBR                     10260099
                                                                        10270099
               PERFORM Z300-GET-LOC-INFO                                10280099
               MOVE XMT00001-LOC-TYP-CDE                                10290099
                   TO PT-STORE-TBL-LOC-TYP(PT-STORE-TBL-COUNT)          10300099
               MOVE XMT00001-LOC-NM                                     10310099
                   TO PT-STORE-TBL-LOC-NM(PT-STORE-TBL-COUNT)           10320099
                                                                        10330099
               SET PT-STORE-TBL-INVENTORY(PT-STORE-TBL-COUNT) TO TRUE   10340099
               MOVE ZEROES                                              10350099
                   TO PT-STORE-TBL-RECS(PT-STORE-TBL-COUNT)             10360099
                                                                        10370099
               PERFORM Z550-FETCH-INV-STR-CSR                           10380099
           END-PERFORM.                                                 10390099
                                                                        10400099
           PERFORM Z575-CLOSE-INV-STR-CSR.                              10410099
                                                                        10420099
      ******************************************************************10430099
      * SEARCH LOADED STORE WORKING STORAGE TABLE                       10440099
      ******************************************************************10450099
       C200-VALIDATE-INV-STR.                                           10460099
                                                                        10470099
           MOVE PV-CURR-LOC-NBR-9 TO XMT00001-LOC-NBR.                  10480099
           PERFORM C250-SEARCH-INV-STR.                                 10490099
                                                                        10500099
      * ADD TO WORKING STORAGE TABLE AND SET AS RECEIVING/UNEXPECTED    10510099
           IF PT-STORE-TBL-INDX > PT-STORE-TBL-COUNT                    10520099
               ADD +1               TO PT-STORE-TBL-COUNT               10530099
               IF PT-STORE-TBL-COUNT > PT-STORE-TBL-MAX                 10540099
                   SET TABLE-EXCEEDED-ABEND-4003 TO TRUE                10550099
                   DISPLAY ' *** STORE TABLE EXCEEDED'                  10560099
                           ' IN C200-VALIDATE-INV-STR ***'              10570099
                   PERFORM Z999-ABEND                                   10580099
               END-IF                                                   10590099
                                                                        10600099
               MOVE PT-STORE-TBL-COUNT TO PT-STORE-TBL-INDX             10610099
               MOVE XMT00001-LOC-NBR                                    10620099
                              TO PT-STORE-TBL-LOC(PT-STORE-TBL-INDX)    10630099
                                 INVPAR-LOC-NBR                         10640099
                                                                        10650099
               PERFORM Z300-GET-LOC-INFO                                10660099
               MOVE XMT00001-LOC-TYP-CDE                                10670099
                              TO PT-STORE-TBL-LOC-TYP(PT-STORE-TBL-INDX)10680099
               MOVE XMT00001-LOC-NM                                     10690099
                              TO PT-STORE-TBL-LOC-NM(PT-STORE-TBL-INDX) 10700099
               SET PT-STORE-TBL-RECEIVING(PT-STORE-TBL-INDX) TO TRUE    10710099
               MOVE ZEROES    TO PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)   10720099
                                 PV-RECORD-COUNT                        10730099
           ELSE                                                         10740099
               MOVE PT-STORE-TBL-LOC(PT-STORE-TBL-INDX)                 10750099
                                                TO INVPAR-LOC-NBR       10760099
               MOVE PT-STORE-TBL-LOC-TYP(PT-STORE-TBL-INDX)             10770099
                                                TO XMT00001-LOC-TYP-CDE 10780099
               MOVE PT-STORE-TBL-LOC-NM(PT-STORE-TBL-INDX)              10790099
                                                TO XMT00001-LOC-NM      10800099
               MOVE PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)                10810099
                                                TO PV-RECORD-COUNT      10820099
           END-IF.                                                      10830099
                                                                        10840099
           IF PT-STORE-TBL-INVENTORY(PT-STORE-TBL-INDX)                 10850099
              SET VALID-MANIFESTED-INV-STORE   TO TRUE                  10860099
           ELSE                                                         10870099
              SET INVALID-MANIFESTED-INV-STORE TO TRUE                  10880099
           END-IF.                                                      10890099
                                                                        10900099
      * ACCOUNT FOR RECORD ALREADY READ                                 10910099
           COMPUTE PV-RECORD-COUNT = PV-RECORD-COUNT + 1.               10920099
                                                                        10930099
      ******************************************************************10940099
      * SEARCH LOADED STORE WORKING STORAGE TABLE                       10950099
      ******************************************************************10960099
       C250-SEARCH-INV-STR.                                             10970099
                                                                        10980099
           PERFORM                                                      10990099
               VARYING PT-STORE-TBL-INDX FROM 1 BY 1                    11000099
               UNTIL PT-STORE-TBL-INDX > PT-STORE-TBL-COUNT             11010099
                   OR (PT-STORE-TBL-LOC(PT-STORE-TBL-INDX)              11020099
                       = XMT00001-LOC-NBR)                              11030099
               CONTINUE                                                 11040099
           END-PERFORM.                                                 11050099
                                                                        11060099
      ******************************************************************11070099
      * FORMAT DETAIL REPORT                                            11080099
      ******************************************************************11090099
       D100-FORMAT-DETAIL-RPT.                                          11100099
                                                                        11110099
           MOVE IN157-DEPT-9            TO RP-DEPT-A-9.                 11120099
           MOVE IN157-CLASS-9           TO RP-CLASS-NBR-A-9.            11130099
           MOVE IN157-SKU               TO RP-SKU-NBR-A.                11140099
           MOVE IN157-ORDER-NBR         TO RP-ORDER-NBR-A.              11150099
                                                                        11160099
           MOVE IN157-CREATE-DATE       TO PV-DATE-RFMT-MM-DD-CCYY.     11170099
           MOVE '-'                     TO PV-DATE-RFMT-DASH-1          11180099
                                           PV-DATE-RFMT-DASH-2.         11190099
           MOVE PV-DATE-RFMT-MM-DD-CCYY TO RP-CREATE-DATE-A.            11200099
                                                                        11210099
C63789     MOVE IN157-SHIP-DATE         TO PV-DATE-RFMT-MM-DD-CCYY.     11220099
           MOVE '-'                     TO PV-DATE-RFMT-DASH-1          11230099
                                           PV-DATE-RFMT-DASH-2.         11240099
           MOVE PV-DATE-RFMT-MM-DD-CCYY TO RP-SHIP-DATE-A               11250099
C63789                                     PV-SHIP-DATE.                11260099
                                                                        11270099
           MOVE IN157-RTL-AMT           TO RP-ADJ-RTL-AMT-A.            11280099
           MOVE IN157-ADJ-QTY           TO RP-SHIP-QTY-A.               11290099
           MOVE IN157-EXTD-RTL-AMT      TO RP-EXTD-RTL-AMT-A.           11300099
                                                                        11310099
      ******************************************************************11320099
      * FORMAT SUMMARY 1 REPORT                                         11330099
      ******************************************************************11340099
       D200-FORMAT-SUM-1-RPT.                                           11350099
                                                                        11360099
           MOVE PV-CURR-DEPT-9          TO RP-DEPT-B-9.                 11370099
           MOVE PV-CURR-DEPT-3X         TO MDSEAR-DEPT-NBR.             11380099
           PERFORM Z400-GET-DEPT-INFO.                                  11390099
           MOVE MDSEAR-DESC             TO RP-DESCRIPTION-B.            11400099
                                                                        11410099
C63789     MOVE PV-SHIP-DATE            TO RP-SHIP-DATE-B.              11420099
                                                                        11430099
           MOVE PV-SUB-TTL-SHIP-QTY     TO RP-SHIP-QTY-B.               11440099
           MOVE PV-SUB-TTL-EXTD-RTL-AMT TO RP-EXTD-RTL-AMT-B.           11450099
                                                                        11460099
      ******************************************************************11470099
      * FORMAT SUMMARY 2 REPORT                                         11480099
      ******************************************************************11490099
       D300-FORMAT-SUM-2-RPT.                                           11500099
                                                                        11510099
           MOVE PV-CURR-LOC-NBR-9       TO RP-LOC-NBR-C-9.              11520099
           MOVE XMT00001-LOC-NM         TO RP-DESCRIPTION-C.            11530099
                                                                        11540099
C63789     MOVE PV-SHIP-DATE            TO RP-SHIP-DATE-C.              11550099
                                                                        11560099
           MOVE PV-GRP-TTL-SHIP-QTY     TO RP-SHIP-QTY-C.               11570099
           MOVE PV-GRP-TTL-EXTD-RTL-AMT TO RP-EXTD-RTL-AMT-C.           11580099
                                                                        11590099
      ******************************************************************11600099
      *    READ THE NEXT RECORD FROM STORE-PARTIAL-ADJ-FILE             11610099
      ******************************************************************11620099
       Z100-READ-ADJ-FILE.                                              11630099
                                                                        11640099
           READ STORE-PARTIAL-ADJ-FILE                                  11650099
               INTO IN157-STR-EXPTED-RECORD                             11660099
                 AT END                                                 11670099
                    SET END-OF-ADJ-FILE   TO TRUE                       11680099
               NOT AT END                                               11690099
                    ADD +1                TO PV-RECORD-COUNT            11700099
                                             PA-INPUT-READ-CTR.         11710099
                                                                        11720099
      ******************************************************************11730099
      *    READ THE CONTROL INPUT RECORD                                11740099
      ******************************************************************11750099
       Z200-READ-CNTL-FILE.                                             11760099
                                                                        11770099
           READ RPT-CONTROL-FILE                                        11780099
               INTO IN-RPT-CNTL-AREA                                    11790099
                 AT END                                                 11800099
                     SET END-OF-CNTL-FILE TO TRUE.                      11810099
                                                                        11820099
           IF END-OF-CNTL-FILE                                          11830099
              DISPLAY '   CONTROL CARD NOT FOUND'                       11840099
              DISPLAY ' '                                               11850099
           ELSE                                                         11860099
              DISPLAY '   CONTROL CARD DATE = ' IN-RPT-DATE             11870099
              DISPLAY ' '                                               11880099
           END-IF.                                                      11890099
                                                                        11900099
      ******************************************************************11910099
      *    GET LOCATION INFORMATION                                     11920099
      ******************************************************************11930099
       Z300-GET-LOC-INFO.                                               11940099
                                                                        11950099
           EXEC SQL                                                     11960099
                SELECT LOC_TYP_CDE                                      11970099
                      ,LOC_NM                                           11980099
                  INTO :XMT00001-LOC-TYP-CDE                            11990099
                      ,:XMT00001-LOC-NM                                 12000099
                  FROM XMT_LOC                                          12010099
                 WHERE LOC_NBR = :XMT00001-LOC-NBR                      12020099
           END-EXEC.                                                    12030099
                                                                        12040099
           EVALUATE SQLCODE                                             12050099
               WHEN +0                                                  12060099
                   CONTINUE                                             12070099
                                                                        12080099
               WHEN OTHER                                               12090099
                   MOVE XMT00001-LOC-NBR         TO PV-CURR-LOC-NBR-9   12100099
                   DISPLAY ' *** INVALID STORE: '   PV-CURR-LOC-NBR     12110099
                   MOVE 'Z300-GET-LOC-INFO'      TO PV-OPER-ATTEMPTED   12120099
                   PERFORM Z900-SQL-ABEND                               12130099
           END-EVALUATE.                                                12140099
                                                                        12150099
      ******************************************************************12160099
      *    GET DEPT INFORMATION                                         12170099
      ******************************************************************12180099
       Z400-GET-DEPT-INFO.                                              12190099
                                                                        12200099
           EXEC SQL                                                     12210099
                SELECT  MDSEAR_DESC                                     12220099
                  INTO :MDSEAR-DESC                                     12230099
                  FROM  TMDSEAR                                         12240099
                 WHERE OPERCO_NBR  = '03'                               12250099
                   AND DEPT_NBR    = :MDSEAR-DEPT-NBR                   12260099
                   AND MDSELV_CDE  = 'DPT'                              12270099
                   AND :IN-RPT-DATE BETWEEN STRT_DTE                    12280099
                                        AND END_DTE                     12290099
           END-EXEC.                                                    12300099
                                                                        12310099
           EVALUATE SQLCODE                                             12320099
               WHEN +0                                                  12330099
                   CONTINUE                                             12340099
                                                                        12350099
               WHEN OTHER                                               12360099
                   DISPLAY ' *** INVALID DEPT: ' PV-CURR-DEPT           12370099
                   MOVE 'GET TMDSEAR/DEPT INFO' TO  PV-OPER-ATTEMPTED   12380099
                   PERFORM Z900-SQL-ABEND                               12390099
           END-EVALUATE.                                                12400099
                                                                        12410099
      ******************************************************************12420099
      *    OPEN/SETUP INVENTORYING STORE CURSOR                         12430099
      ******************************************************************12440099
       Z500-OPEN-INV-STR-CSR.                                           12450099
                                                                        12460099
           EXEC SQL                                                     12470099
               OPEN INV-STR-CSR                                         12480099
           END-EXEC.                                                    12490099
                                                                        12500099
           EVALUATE SQLCODE                                             12510099
               WHEN +0                                                  12520099
                  SET NOT-END-OF-INV-STR  TO TRUE                       12530099
                                                                        12540099
               WHEN OTHER                                               12550099
                  DISPLAY ' DATE '           IN-RPT-DATE                12560099
                  MOVE 'OPEN INV-STR-CSR' TO PV-OPER-ATTEMPTED          12570099
                  PERFORM Z900-SQL-ABEND                                12580099
           END-EVALUATE.                                                12590099
                                                                        12600099
      ******************************************************************12610099
      *    FETCH INVENTORYING STORE CURSOR                              12620099
      ******************************************************************12630099
       Z550-FETCH-INV-STR-CSR.                                          12640099
                                                                        12650099
           EXEC SQL FETCH INV-STR-CSR                                   12660099
              INTO                                                      12670099
                   :INVPAR-LOC-NBR                                      12680099
                  ,:INVPAR-ACTL-INV-DTE                                 12690099
           END-EXEC.                                                    12700099
                                                                        12710099
           EVALUATE SQLCODE                                             12720099
               WHEN +0                                                  12730099
                   CONTINUE                                             12740099
                                                                        12750099
               WHEN +100                                                12760099
                   SET END-OF-INV-STR       TO TRUE                     12770099
                                                                        12780099
               WHEN OTHER                                               12790099
                   MOVE 'FETCH INV-STR-CSR' TO  PV-OPER-ATTEMPTED       12800099
                   PERFORM Z900-SQL-ABEND                               12810099
           END-EVALUATE.                                                12820099
                                                                        12830099
      ******************************************************************12840099
      *    CLOSE/TERMINATE INVENTORYING STORE CURSOR                    12850099
      ******************************************************************12860099
       Z575-CLOSE-INV-STR-CSR.                                          12870099
                                                                        12880099
           EXEC SQL                                                     12890099
                CLOSE INV-STR-CSR                                       12900099
           END-EXEC.                                                    12910099
                                                                        12920099
           EVALUATE SQLCODE                                             12930099
               WHEN +0                                                  12940099
                   CONTINUE                                             12950099
                                                                        12960099
               WHEN OTHER                                               12970099
                   MOVE 'CLOSE INV-STR-CSR' TO PV-OPER-ATTEMPTED        12980099
                   PERFORM Z900-SQL-ABEND                               12990099
           END-EVALUATE.                                                13000099
                                                                        13010099
      ******************************************************************13020099
      *    PRINT THE DETAIL REPORT                                      13030099
      ******************************************************************13040099
       Z600-PRINT-DETAILS-A.                                            13050099
                                                                        13060099
           IF (PV-PRINT-LINE-COUNT-A + PV-ADVANCE-LINE-A                13070099
                                                     > PC-PRINT-LINE-MAX13080099
           OR PV-PRINT-LINE-COUNT-A = 0)                                13090099
           AND IN157-DEPT = PV-CURR-DEPT                                13100099
               PERFORM Z700-PRINT-HEADINGS-A                            13110099
           END-IF.                                                      13120099
                                                                        13130099
           WRITE REPORT-LINE-1 FROM RP-REPORT-DETAIL-A                  13140099
               AFTER ADVANCING PV-ADVANCE-LINE-A LINES.                 13150099
                                                                        13160099
           ADD  PV-ADVANCE-LINE-A TO PV-PRINT-LINE-COUNT-A.             13170099
           MOVE 1                 TO PV-ADVANCE-LINE-A.                 13180099
           MOVE SPACES            TO RP-REPORT-DETAIL-A.                13190099
                                                                        13200099
      ******************************************************************13210099
      *    PRINT THE SUMMARY 1 REPORT                                   13220099
      ******************************************************************13230099
       Z625-PRINT-DETAILS-B.                                            13240099
                                                                        13250099
           IF (PV-PRINT-LINE-COUNT-B + PV-ADVANCE-LINE-B                13260099
                                                    > PC-PRINT-LINE-MAX 13270099
           OR PV-PRINT-LINE-COUNT-B = 0)                                13280099
              PERFORM Z725-PRINT-HEADINGS-B                             13290099
           END-IF.                                                      13300099
                                                                        13310099
           WRITE REPORT-LINE-2 FROM RP-REPORT-DETAIL-B                  13320099
               AFTER ADVANCING PV-ADVANCE-LINE-B LINES.                 13330099
                                                                        13340099
           ADD  PV-ADVANCE-LINE-B TO PV-PRINT-LINE-COUNT-B.             13350099
           MOVE 1                 TO PV-ADVANCE-LINE-B.                 13360099
           MOVE SPACES            TO RP-REPORT-DETAIL-B.                13370099
                                                                        13380099
      ******************************************************************13390099
      *    PRINT THE SUMMARY 2 REPORT                                   13400099
      ******************************************************************13410099
       Z650-PRINT-DETAILS-C.                                            13420099
                                                                        13430099
           IF (PV-PRINT-LINE-COUNT-C + PV-ADVANCE-LINE-C                13440099
                                                     > PC-PRINT-LINE-MAX13450099
           OR PV-PRINT-LINE-COUNT-C = 0)                                13460099
              PERFORM Z750-PRINT-HEADINGS-C                             13470099
           END-IF.                                                      13480099
                                                                        13490099
           WRITE REPORT-LINE-3 FROM RP-REPORT-DETAIL-C                  13500099
               AFTER ADVANCING PV-ADVANCE-LINE-C LINES.                 13510099
                                                                        13520099
           ADD  PV-ADVANCE-LINE-C TO PV-PRINT-LINE-COUNT-C.             13530099
           MOVE 1                 TO PV-ADVANCE-LINE-C.                 13540099
           MOVE SPACES            TO RP-REPORT-DETAIL-C.                13550099
                                                                        13560099
      ******************************************************************13570099
      *    PRINT THE STORE STATUS REPORT                                13580099
      ******************************************************************13590099
       Z675-PRINT-DETAILS-S.                                            13600099
                                                                        13610099
           IF (PV-PRINT-LINE-COUNT-S + PV-ADVANCE-LINE-S                13620099
                                                    > PC-PRINT-LINE-MAX 13630099
           OR PV-PRINT-LINE-COUNT-S = 0)                                13640099
              PERFORM Z775-PRINT-HEADINGS-S                             13650099
           END-IF.                                                      13660099
                                                                        13670099
           WRITE REPORT-LINE-4 FROM RP-REPORT-DETAIL-S                  13680099
               AFTER ADVANCING PV-ADVANCE-LINE-S LINES.                 13690099
                                                                        13700099
           ADD  PV-ADVANCE-LINE-S TO PV-PRINT-LINE-COUNT-S.             13710099
           MOVE 1                 TO PV-ADVANCE-LINE-S.                 13720099
           MOVE SPACES            TO RP-REPORT-DETAIL-S.                13730099
                                                                        13740099
      ******************************************************************13750099
      *    PRINT THE DETAIL REPORT HEADINGS                             13760099
      ******************************************************************13770099
       Z700-PRINT-HEADINGS-A.                                           13780099
                                                                        13790099
           ADD 1                    TO PV-PAGE-COUNT-A.                 13800099
           MOVE PV-PAGE-COUNT-A     TO DP132O-PAGE-NUMBER.              13810099
           MOVE 1                   TO DP132O-REPORT-NUMBER             13820099
                                                                        13830099
           WRITE REPORT-LINE-1 FROM DP132O-STANDRD-HEADER-132-COLS      13840099
               AFTER ADVANCING PAGE.                                    13850099
                                                                        13860099
           WRITE REPORT-LINE-1 FROM RP-REPORT-TITLE-1A.                 13870099
           WRITE REPORT-LINE-1 FROM RP-REPORT-TITLE-2.                  13880099
                                                                        13890099
           WRITE REPORT-LINE-1 FROM RP-REPORT-HEADER-1A                 13900099
               AFTER ADVANCING 2 LINES.                                 13910099
           WRITE REPORT-LINE-1 FROM RP-REPORT-HEADER-2A.                13920099
                                                                        13930099
           MOVE 7 TO PV-PRINT-LINE-COUNT-A.                             13940099
           MOVE 2 TO PV-ADVANCE-LINE-A.                                 13950099
                                                                        13960099
      ******************************************************************13970099
      *    PRINT THE SUMMARY 1 REPORT HEADINGS                          13980099
      ******************************************************************13990099
       Z725-PRINT-HEADINGS-B.                                           14000099
                                                                        14010099
           ADD 1                TO PV-PAGE-COUNT-B.                     14020099
           MOVE PV-PAGE-COUNT-B TO DP132O-PAGE-NUMBER.                  14030099
           MOVE 2               TO DP132O-REPORT-NUMBER                 14040099
                                                                        14050099
           WRITE REPORT-LINE-2 FROM DP132O-STANDRD-HEADER-132-COLS      14060099
               AFTER ADVANCING PAGE.                                    14070099
                                                                        14080099
           WRITE REPORT-LINE-2 FROM RP-REPORT-TITLE-1B.                 14090099
           WRITE REPORT-LINE-2 FROM RP-REPORT-TITLE-2.                  14100099
                                                                        14110099
           WRITE REPORT-LINE-2 FROM RP-REPORT-HEADER-1B                 14120099
               AFTER ADVANCING 2 LINES.                                 14130099
           WRITE REPORT-LINE-2 FROM RP-REPORT-HEADER-2B.                14140099
                                                                        14150099
           MOVE 7 TO PV-PRINT-LINE-COUNT-B.                             14160099
           MOVE 2 TO PV-ADVANCE-LINE-B.                                 14170099
                                                                        14180099
      ******************************************************************14190099
      *    PRINT THE SUMMARY 2 REPORT HEADINGS                          14200099
      ******************************************************************14210099
       Z750-PRINT-HEADINGS-C.                                           14220099
                                                                        14230099
           ADD 1                TO PV-PAGE-COUNT-C.                     14240099
           MOVE PV-PAGE-COUNT-C TO DP132O-PAGE-NUMBER.                  14250099
           MOVE 3               TO DP132O-REPORT-NUMBER                 14260099
                                                                        14270099
           WRITE REPORT-LINE-3 FROM DP132O-STANDRD-HEADER-132-COLS      14280099
               AFTER ADVANCING PAGE.                                    14290099
                                                                        14300099
           WRITE REPORT-LINE-3 FROM RP-REPORT-TITLE-1C.                 14310099
                                                                        14320099
           WRITE REPORT-LINE-3 FROM RP-REPORT-HEADER-1C                 14330099
               AFTER ADVANCING 2 LINES.                                 14340099
           WRITE REPORT-LINE-3 FROM RP-REPORT-HEADER-2C.                14350099
                                                                        14360099
           MOVE 6 TO PV-PRINT-LINE-COUNT-C.                             14370099
           MOVE 2 TO PV-ADVANCE-LINE-C.                                 14380099
                                                                        14390099
      ******************************************************************14400099
      *    PRINT THE STORE STATUS RPT HEADINGS                          14410099
      ******************************************************************14420099
       Z775-PRINT-HEADINGS-S.                                           14430099
                                                                        14440099
           ADD 1                TO PV-PAGE-COUNT-S.                     14450099
           MOVE PV-PAGE-COUNT-S TO DP132O-PAGE-NUMBER.                  14460099
           MOVE 4               TO DP132O-REPORT-NUMBER                 14470099
                                                                        14480099
           WRITE REPORT-LINE-4 FROM DP132O-STANDRD-HEADER-132-COLS      14490099
               AFTER ADVANCING PAGE.                                    14500099
                                                                        14510099
           WRITE REPORT-LINE-4 FROM RP-REPORT-TITLE-1S.                 14520099
                                                                        14530099
           WRITE REPORT-LINE-4 FROM RP-REPORT-HEADER-1S                 14540099
               AFTER ADVANCING 2 LINES.                                 14550099
           WRITE REPORT-LINE-4 FROM RP-REPORT-HEADER-2S.                14560099
                                                                        14570099
           MOVE 6 TO PV-PRINT-LINE-COUNT-S.                             14580099
           MOVE 2 TO PV-ADVANCE-LINE-S.                                 14590099
                                                                        14600099
      ******************************************************************14610099
      *    SQL ERROR - ABEND                                            14620099
      ******************************************************************14630099
       Z900-SQL-ABEND.                                                  14640099
                                                                        14650099
           SET SQL-ABEND-4013         TO TRUE.                          14660099
           DISPLAY '**  ABEND     **'.                                  14670099
           DISPLAY '**  PROGRAM   **  = '  PC-PROGRAM-NAME              14680099
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED            14690099
                                                                        14700099
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         14710099
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        14720099
                                                                        14730099
           COPY DPPD004.                                                14740099
                                                                        14750099
           PERFORM Z999-ABEND.                                          14760099
                                                                        14770099
      ******************************************************************14780099
      *    FORCE ABEND                                                  14790099
      ******************************************************************14800099
       Z999-ABEND.                                                      14810099
                                                                        14820099
           CALL 'ILBOABN0'  USING ABEND-CODE.                           14830099
