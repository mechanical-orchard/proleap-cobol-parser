      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
                                                                                
       PROGRAM-ID.             MRKUT138.                                        
       AUTHOR.                 KRISTIN PETERSON.                                
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            06-30-05.                                        
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  CREATE THE MQ REQUEST MESSAGES TO CHECK ELIGIBILITY RULES              
      *  FOR TONIGHT'S REPLENISHMENT ORDERS.                                    
      *****************************************************************         
      *  CHANGE LOG:                                                            
      *  06/30/05 - KRISTIN PETERSON                                            
      *             (WR# 26351) NEW PROGRAM FOR MR REVIEW & ADJUST              
      *             PO SUBMIT PROJECT.                                          
      *  08/08/05 - KRISTIN PETERSON                                            
      *             CHANGED STORE CURSOR TO INCLUDE E-COMMERCE STORES.          
      *  08/16/05 - KRISTIN PETERSON                                            
      *             FIX TRUNCATION OF 10-DIGIT INTERNAL UPC IDS.                
      *  05/30/06 - NICK SCHULTZ                                                
      *             UPDATED FOR NEW ISMQ                                        
PG1008*  10/29/08 - PATI GREEN                                                  
PG1008*             UPDATED LOGIC TO ALLOW FOR QUANTUM RT_DAY 301.              
      *  02/21/11 - JACK KRAMER                                                 
      *             MULTI-LOCATION ORDER FULFILLMENT - SEND A MESSAGE           
      *             FOR ALL 'DS' E_LOCATIONS IF E_BUS_IND = 'Y' FOR A           
      *             STORE RETURNED IN THE STORE CURSOR                          
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-CARD-1   ASSIGN TO INP01.                             
           SELECT CONTROL-CARD-2   ASSIGN TO INP02.                             
           SELECT CONTROL-CARD-3   ASSIGN TO INP03.                             
           SELECT CONTROL-CARD-4   ASSIGN TO INP04.                             
           SELECT MESSAGE-ID-FILE  ASSIGN TO OUT01.                             
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-1                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-LINE                PIC X(80).                          
                                                                                
       FD  CONTROL-CARD-2                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-LINE-2              PIC X(80).                          
                                                                                
       FD  CONTROL-CARD-3                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-LINE-3              PIC X(80).                          
                                                                                
       FD  CONTROL-CARD-4                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-LINE-4              PIC X(80).                          
                                                                                
       FD  MESSAGE-ID-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 60 CHARACTERS.                                       
                                                                                
       01  MESSAGE-ID-RECORD                PIC X(60).                          
      *                                                                         
      ******************************************                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************                                
                                                                                
      ******************************************                                
      * COPYBOOKS FOR THE CALENDAR ROUTINE                                      
      ******************************************                                
           COPY DPWS024.                                                        
           COPY DPWS500.                                                        
      ******************************************                                
       01  FILLER.                                                              
           05  FILLER                      PIC  X(36)    VALUE                  
               ' ***** WORKING STORAGE PGM=MRKUT138 '.                          
           05  FILLER                      PIC  X(10)    VALUE                  
               ' LOCATION='.                                                    
           05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.           
           05  FILLER                      PIC  X(06)    VALUE                  
               ' *****'.                                                        
                                                                                
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                 PIC S9(04)  VALUE +18  COMP.         
                                                                                
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-ITEM-NBR             PIC S9(15)  VALUE +0 COMP-3.         
               10  CR-TOT-NBR-PUTS-CNT     PIC S9(09)  VALUE +0 COMP-3.         
               10  CR-TOT-OUTPUT-RECS-CNT  PIC S9(09)  VALUE +0 COMP-3.         
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  PS-CONTROL-CARD-1-SW        PIC X(01)     VALUE 'N'.             
               88  PS-CONTROL-CARD-1-END                 VALUE 'Y'.             
               88  PS-CONTROL-CARD-1-START               VALUE 'N'.             
                                                                                
           05  PS-CONTROL-CARD-2-SW        PIC X(01)     VALUE 'N'.             
               88  PS-CONTROL-CARD-2-END                 VALUE 'Y'.             
               88  PS-CONTROL-CARD-2-START               VALUE 'N'.             
                                                                                
           05  PS-CONTROL-CARD-3-SW        PIC X(01)     VALUE 'N'.             
               88  PS-CONTROL-CARD-3-END                 VALUE 'Y'.             
               88  PS-CONTROL-CARD-3-START               VALUE 'N'.             
                                                                                
           05  PS-CONTROL-CARD-4-SW        PIC X(01)     VALUE 'N'.             
               88  PS-CONTROL-CARD-4-END                 VALUE 'Y'.             
               88  PS-CONTROL-CARD-4-START               VALUE 'N'.             
                                                                                
           05  PS-EOC-ITEM-CSR             PIC X(01)     VALUE 'N'.             
               88  EOC-ITEM-CSR                          VALUE 'Y'.             
                                                                                
           05  PS-EOC-STORE-CSR            PIC X(01)     VALUE 'N'.             
               88  EOC-STORE-CSR                         VALUE 'Y'.             
                                                                                
           05  PS-FIRST-COMMIT-SW          PIC X(01)     VALUE 'N'.             
               88  PS-FIRST-COMMIT                       VALUE 'Y'.             
                                                                                
           05  PS-FIRST-STORE-SW           PIC X(01)     VALUE 'N'.             
               88  PS-FIRST-STORE                        VALUE 'Y'.             
                                                                                
           05  PS-E-LOCS-CSR               PIC X(01)     VALUE 'N'.             
               88  EOC-E-LOCS-CSR                        VALUE 'Y'.             
                                                                                
       01  PC-CONSTANTS.                                                        
           05  PC-PROGRAM-NAME             PIC X(08)     VALUE                  
                                                         'MRKUT138'.            
           05  PC-JOB-NAME                 PIC X(08)     VALUE                  
                                                         'MRK016  '.            
           05  PC-PGM-DPKUT500             PIC X(08)     VALUE                  
                                                         'DPKUT500'.            
           05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
                                                         COMP SYNC.             
           05  PC-100                      PIC S9(3)     VALUE +100             
                                                         COMP-3.                
           05  PC-200                      PIC S9(3)     VALUE +200             
                                                         COMP-3.                
PG1008     05  PC-300                      PIC S9(3)     VALUE +300             
PG1008                                                   COMP-3.                
           05  PC-400                      PIC S9(3)     VALUE +400             
                                                         COMP-3.                
           05  PC-FIFTY-STARS              PIC X(50)     VALUE ALL '*'.         
                                                                                
           05  PC-QMGR-NAME-PUT            PIC X(48)     VALUE SPACES.          
               88  PC-PROD-QMGR-PUT               VALUE 'ISMQI03P'.             
               88  PC-QA-QMGR-PUT                 VALUE 'ISMQI01Q'.             
               88  PC-TEST-QMGR-PUT               VALUE 'ISMQI03T'.             
                                                                                
           05  PC-QMGR-NAME-CONNECT        PIC X(48)     VALUE SPACES.          
               88  PC-PROD-QMGR-CONNECT           VALUE 'QKSP'.                 
               88  PC-QA-QMGR-CONNECT             VALUE 'QKSQ'.                 
               88  PC-TEST-QMGR-CONNECT           VALUE 'QKST'.                 
                                                                                
           05  PC-MQCONN                PIC X(8)  VALUE 'CSQBCONN'.             
           05  PC-MQCMIT                PIC X(8)  VALUE 'CSQBCOMM'.             
           05  PC-MQOPEN                PIC X(8)  VALUE 'CSQBOPEN'.             
           05  PC-MQPUT                 PIC X(8)  VALUE 'CSQBPUT '.             
           05  PC-MQBACK                PIC X(8)  VALUE 'CSQBBACK'.             
           05  PC-MQCLOSE               PIC X(8)  VALUE 'CSQBCLOS'.             
           05  PC-MQDISC                PIC X(8)  VALUE 'CSQBDISC'.             
           05  PC-MQCHECK               PIC X(8)  VALUE 'MQR2C   '.             
           05  PC-PIPE                  PIC X(1)  VALUE '|'.                    
           05  PC-MAX-REQUEST-LENGTH    PIC S9(09) BINARY VALUE +12143.         
           05  PC-MAX-ELOC-NBR          PIC  S9(3)    VALUE +99                 
                                                       COMP-3.                  
                                                                                
       01  PV-SCHED-DATE.                                                       
           05  PV-SCHED-CCYY.                                                   
               10  PV-SCHED-CC             PIC 9(2)      VALUE ZEROS.           
               10  PV-SCHED-YY             PIC 9(2)      VALUE ZEROS.           
           05  PV-SCHED-FILLER             PIC X(1)      VALUE SPACE.           
           05  PV-SCHED-MM                 PIC 9(2)      VALUE ZEROS.           
           05  PV-SCHED-FILLER             PIC X(1)      VALUE SPACE.           
           05  PV-SCHED-DD                 PIC 9(2)      VALUE ZEROS.           
                                                                                
       01  CT-CPU-TIME.                                                         
           05  CT-CPU-HR                   PIC 9(2)      VALUE ZEROS.           
           05  CT-CPU-MIN                  PIC 9(2)      VALUE ZEROS.           
           05  CT-CPU-SEC                  PIC 9(2)      VALUE ZEROS.           
           05  CT-CPU-HSEC                 PIC 9(2)      VALUE ZEROS.           
                                                                                
       01  RT-INFO.                                                             
           05  RT-DAY-NO                   PIC 9(1)      VALUE ZEROS.           
           05  FILLER                      PIC X(79)     VALUE SPACES.          
                                                                                
       01  PV-MISC-FIELDS.                                                      
           05  PV-ERROR-MESSAGE            PIC X(48)     VALUE SPACES.          
           05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
                                                         COMP SYNC.             
           05  PV-USER-ERROR               PIC S9(04)    VALUE +4003            
                                                         COMP SYNC.             
           05  PV-MQ-ERROR                 PIC S9(04)    VALUE +4020            
                                                         COMP SYNC.             
           05  PV-ABEND-PARAGRAPH          PIC X(30)     VALUE SPACES.          
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                           PIC X(30)     VALUE SPACES.          
                                                                                
           05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9                      
                                                         VALUE ZERO.            
           05  PV-FISCAL-SPLIT.                                                 
               10  PV-FISCAL-DIGIT1        PIC  9(01)    VALUE ZERO.            
               10  PV-FISCAL-DIGIT2        PIC  9(01)    VALUE ZERO.            
                   88  PV-ODD                    VALUE 1, 3, 5, 7, 9.           
           05  PV-F2                       PIC  X(02)    VALUE 'F2'.            
                                                                                
           05  PV-RT-DAY-NBR               PIC S9(03)V   VALUE ZERO             
                                                              COMP-3.           
           05  PV-RT-DAY-NBR-7             PIC S9(03)V   VALUE ZERO             
                                                              COMP-3.           
           05  PV-RT-DAY-NBR-7B            PIC S9(03)V   VALUE ZERO             
                                                              COMP-3.           
           05  PV-RT-DAY-NBR-7C            PIC S9(03)V   VALUE ZERO             
                                                              COMP-3.           
           05  PV-RT-DAY-NBR-7D            PIC S9(03)V   VALUE ZERO             
                                                              COMP-3.           
PG1008     05  PV-RT-DAY-NBR-7Q            PIC S9(03)V   VALUE ZERO             
PG1008                                                        COMP-3.           
           05  PV-RT-DAY-NBR-14            PIC S9(03)V   VALUE ZERO             
                                                              COMP-3.           
           05  PV-RT-DAY-CAL               PIC 9(01)     VALUE ZERO.            
                                                                                
           05  PV-STR-SUB                  PIC S9(04)    VALUE +0               
                                                         COMP SYNC.             
           05  PV-ELOC-SUB                 PIC S9(04)    VALUE +0               
                                                         COMP SYNC.             
           05  PV-ELOC-COUNT               PIC S9(04)    VALUE +0               
                                                         COMP SYNC.             
           05  PV-SAVE-UPC-ID              PIC S9(10)    COMP-3                 
                                                         VALUE +0.              
           05  PV-MSGBUFFER                PIC X(12143)  VALUE SPACES.          
           05  PV-HCONN                    PIC S9(09)    BINARY VALUE 0.        
           05  PV-PUTQ-HANDLE              PIC S9(09)    BINARY VALUE 0.        
           05  PV-OPEN-OPTIONS             PIC S9(09)    BINARY.                
           05  PV-COMPLETION-CODE          PIC S9(09)    BINARY.                
           05  PV-REASON                   PIC S9(09)    BINARY.                
           05  PV-CON-REASON               PIC S9(9)     BINARY.                
           05  PV-MQ-REASON-TEXT           PIC X(30)     VALUE SPACES.          
           05  PV-MSGLENGTH                PIC S9(09)    BINARY VALUE 0.        
                                                                                
      *----------------------------------------------------------------*        
      *    CONTROL CARD-1                                                       
      *----------------------------------------------------------------*        
       01  CC-CONTROL-CARD-1.                                                   
           05  CC-QMGR-PUT                 PIC  X(48)    VALUE SPACES.          
           05  FILLER                      PIC  X(32).                          
                                                                                
      *----------------------------------------------------------------*        
      *    CONTROL CARD-2                                                       
      *----------------------------------------------------------------*        
       01  CC-CONTROL-CARD-2.                                                   
           05  CC-QMGR-CONNECT             PIC  X(48)    VALUE SPACES.          
           05  FILLER                      PIC  X(32).                          
                                                                                
      *----------------------------------------------------------------*        
      *    CONTROL CARD-3                                                       
      *----------------------------------------------------------------*        
       01  CC-CONTROL-CARD-3.                                                   
           05  CC-QNAME-PUT                PIC  X(48)    VALUE SPACES.          
           05  FILLER                      PIC  X(32).                          
                                                                                
      *----------------------------------------------------------------*        
      *    CONTROL CARD-4                                                       
      *----------------------------------------------------------------*        
       01  CC-CONTROL-CARD-4.                                                   
           05  CC-QNAME-CONNECT            PIC  X(48)    VALUE SPACES.          
           05  FILLER                      PIC  X(32).                          
                                                                                
      *----------------------------------------------------------------*        
      *    LAYOUT OF THE MESSAGE ID OUTPUT RECORD                      *        
      *----------------------------------------------------------------*        
           COPY MRRD138.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    LAYOUTS USED TO SEND ELIGIBILITY REQUESTS THROUGH MQSERIES  *        
      *----------------------------------------------------------------*        
           COPY MRRD120.                                                        
                                                                                
           COPY MRRD121.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    MQSERIES COPYBOOKS                                          *        
      *----------------------------------------------------------------*        
       01  MQM-OBJECT-DESCRIPTOR.                                               
           COPY CMQODV.                                                         
                                                                                
       01  MQM-MESSAGE-DESCRIPTOR.                                              
           COPY CMQMDV.                                                         
                                                                                
       01  MQM-GET-MESSAGE-OPTIONS.                                             
           COPY CMQGMOV.                                                        
                                                                                
       01  MQM-PUT-MESSAGE-OPTIONS.                                             
           COPY CMQPMOV.                                                        
                                                                                
      *    MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)           
      *    AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)                  
                                                                                
       01  MQM-CONSTANTS.                                                       
           COPY CMQV.                                                           
      *                                                                         
           EJECT                                                                
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TMRITEM                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TMRITST                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      * DB2 COMMUNICATION AREA                                                  
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      * DB2 COMMUNICATION AREA2                                                 
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      ******************************************                                
           EXEC SQL                                                             
             DECLARE ITEM_CSR CURSOR WITH HOLD FOR                              
           SELECT DISTINCT A.ITEM_NBR                                           
                          ,B.DEPT                                               
                          ,B.CLASS                                              
                          ,B.SUBCLASS                                           
                          ,B.SKU                                                
                          ,B.INTR_UPC_ID                                        
             FROM TMRITEM A,                                                    
                  TSKXREF B                                                     
            WHERE A.ITEM_NBR > :CR-ITEM-NBR                                     
              AND A.ITEM_NBR = B.ITM_NBR                                        
              AND ((A.RT_NBR = 7 AND A.RT_DAY_NBR = :PV-RT-DAY-NBR-7)           
               OR  (A.RT_NBR = 14 AND A.RT_DAY_NBR = :PV-RT-DAY-NBR-14)         
               OR  (A.RT_NBR = 7 AND A.RT_DAY_NBR = :PV-RT-DAY-NBR-7B)          
               OR  (A.RT_NBR = 7 AND A.RT_DAY_NBR = :PV-RT-DAY-NBR-7C)          
               OR  (A.RT_NBR = 7 AND A.RT_DAY_NBR = :PV-RT-DAY-NBR-7D)          
PG1008         OR  (A.RT_NBR = 7 AND A.RT_DAY_NBR = :PV-RT-DAY-NBR-7Q))         
            ORDER BY A.ITEM_NBR                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************                                
           EXEC SQL                                                             
             DECLARE STORE_CSR CURSOR WITH HOLD FOR                             
           SELECT DISTINCT  C.STR_NBR                                           
                           ,D.E_BUS_IND                                         
           FROM TMRITST C,                                                      
                XMT_LOC D                                                       
           WHERE C.ITEM_NBR     = :MRITEM-ITEM-NBR                              
              AND C.STR_NBR     = D.LOC_NBR                                     
              AND NOT C.FT_CDE  = 'X'                                           
              AND D.LOC_TYP_CDE = 'DS'                                          
           ORDER BY D.E_BUS_IND DESC                                            
           END-EXEC.                                                            
                                                                                
      ******************************************                                
           EXEC SQL                                                             
             DECLARE E_LOCS_CSR CURSOR WITH HOLD FOR                            
           SELECT LOC_NBR                                                       
           FROM XMT_LOC                                                         
           WHERE LOC_TYP_CDE = 'DS'                                             
             AND E_BUS_IND = 'Y'                                                
           ORDER BY LOC_NBR                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  TABLE FOR LIST OF STORES (PIPE DELIMITED) TO BE USED WHEN              
      *  CREATING THE ER REQUEST MESSAGE.                                       
      ******************************************************************        
       01  SL-STORE-LIST-TABLE.                                                 
           05  SL-STORE-LIST    OCCURS 2000 TIMES.                              
               10  SL-STORE-NMBR           PIC 9(05).                           
               10  SL-PIPE                 PIC X(01).                           
                                                                                
      ******************************************************************        
      *  TABLE TO HOLD ALL E-BUSINESS-LOCS                                      
      ******************************************************************        
       01  EL-EFC-LOC-TABLE.                                                    
           05  EL-EFC-LIST      OCCURS 99 TIMES.                                
               10  EL-EFC-NMBR              PIC S9(04) COMP SYNC.               
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       A000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM A100-INITIALIZATION.                                         
                                                                                
           IF  RT-DAY-NO NOT = ZERO                                             
      ****     CALCULATE RT DAY NBR FOR THE ITEM CURSOR                         
               ADD PC-100 TO RT-DAY-NO                                          
                      GIVING PV-RT-DAY-NBR-7B                                   
               ADD PC-200 TO RT-DAY-NO                                          
                      GIVING PV-RT-DAY-NBR-7C                                   
PG1008         ADD PC-300 TO RT-DAY-NO                                          
PG1008                GIVING PV-RT-DAY-NBR-7Q                                   
               ADD PC-400 TO RT-DAY-NO                                          
                      GIVING PV-RT-DAY-NBR-7D                                   
               IF  PV-ODD                                                       
                   MOVE RT-DAY-NO                                               
                     TO PV-RT-DAY-NBR-7                                         
                        PV-RT-DAY-NBR-14                                        
               ELSE                                                             
                   MOVE RT-DAY-NO                                               
                     TO PV-RT-DAY-NBR-7                                         
                   ADD 7 TO RT-DAY-NO                                           
                     GIVING PV-RT-DAY-NBR-14                                    
               END-IF                                                           
               PERFORM A200-GET-ALL-E-LOCS                                      
      ****     PROCESS THE ITEM CURSOR TO BUILD THE MQ REQUEST                  
      ****     MESSAGES FOR ER                                                  
               PERFORM S100-OPEN-ITEM-CSR                                       
               PERFORM B100-PROCESS-ITEM-CSR                                    
                 UNTIL EOC-ITEM-CSR                                             
           END-IF.                                                              
                                                                                
      **** END-OF-JOB PROCESSING                                                
                                                                                
           PERFORM Q400-MQ-CLOSE-PUT-ER.                                        
                                                                                
           PERFORM Q600-DISCONNECT-QMGR.                                        
                                                                                
           PERFORM Z995-END-OF-JOB.                                             
                                                                                
           CLOSE MESSAGE-ID-FILE.                                               
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      ** PROGRAM INITIALIZATION                                                 
      **   - PROCESS CONTROL CARDS                                              
      **   - CALL CALENDAR ROUTINES TO RETRIEVE VALUES NEEDED IN PGM            
      ******************************************************************        
       A100-INITIALIZATION.                                                     
                                                                                
           DISPLAY PC-FIFTY-STARS.                                              
           DISPLAY '***** START OF PROGRAM MRKUT138 *****'.                     
                                                                                
           ACCEPT PV-SCHED-DATE.                                                
                                                                                
           OPEN INPUT CONTROL-CARD-1                                            
                      CONTROL-CARD-2                                            
                      CONTROL-CARD-3                                            
                      CONTROL-CARD-4                                            
               OUTPUT MESSAGE-ID-FILE.                                          
                                                                                
           SET PS-CONTROL-CARD-1-START TO TRUE.                                 
           SET PS-CONTROL-CARD-2-START TO TRUE.                                 
           SET PS-CONTROL-CARD-3-START TO TRUE.                                 
           SET PS-CONTROL-CARD-4-START TO TRUE.                                 
                                                                                
      **** READ THE FIRST CONTROL CARD FOR THE PUT QUEUE MGR NAME.              
                                                                                
           PERFORM S010-READ-CONTROL-CARD-1.                                    
           IF  PS-CONTROL-CARD-1-END                                            
               DISPLAY PC-FIFTY-STARS                                           
               DISPLAY PC-PROGRAM-NAME                                          
                       ' CONTROL CARD MISSING - FOR PUT QMGR NAME'              
           ELSE                                                                 
               IF  CC-QMGR-PUT > SPACES                                         
                   DISPLAY PC-FIFTY-STARS                                       
                   DISPLAY 'PUT QMGR:       ' CC-CONTROL-CARD-1                 
               ELSE                                                             
                   SET PS-CONTROL-CARD-1-END TO TRUE                            
                   DISPLAY PC-PROGRAM-NAME ' INVALID PUT QMGR NAME '            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           CLOSE CONTROL-CARD-1.                                                
                                                                                
      **** READ THE SECOND CONTROL CARD FOR THE CONNECT QUEUE MGR NAME.         
                                                                                
           PERFORM S020-READ-CONTROL-CARD-2.                                    
           IF  PS-CONTROL-CARD-2-END                                            
               DISPLAY PC-FIFTY-STARS                                           
               DISPLAY PC-PROGRAM-NAME                                          
                       ' CONTROL CARD MISSING - FOR CONNECT QMGR NAME'          
           ELSE                                                                 
               IF  CC-QMGR-CONNECT > SPACES                                     
                   DISPLAY 'CONNECT QMGR:   ' CC-CONTROL-CARD-2                 
               ELSE                                                             
                   SET PS-CONTROL-CARD-2-END TO TRUE                            
                   DISPLAY PC-PROGRAM-NAME ' INVALID CONNECT QMGR NAME '        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           CLOSE CONTROL-CARD-2.                                                
                                                                                
      **** READ THE THIRD CONTROL CARD FOR THE PUT QUEUE NAME                   
                                                                                
           PERFORM S030-READ-CONTROL-CARD-3.                                    
           IF  PS-CONTROL-CARD-3-END                                            
               DISPLAY PC-PROGRAM-NAME                                          
                      ' CONTROL CARD MISSING - PUT Q-NAME '                     
            ELSE                                                                
                IF  CC-QNAME-PUT > SPACES                                       
                    DISPLAY 'PUT Q-NAME:     ' CC-CONTROL-CARD-3                
                ELSE                                                            
                    SET PS-CONTROL-CARD-3-END TO TRUE                           
                    DISPLAY PC-PROGRAM-NAME                                     
                            ' INVALID PUT Q-NAME'                               
                END-IF                                                          
           END-IF.                                                              
                                                                                
           CLOSE CONTROL-CARD-3.                                                
                                                                                
      **** READ THE FOURTH CONTROL CARD FOR THE CONNECT QUEUE NAME              
                                                                                
           PERFORM S040-READ-CONTROL-CARD-4.                                    
           IF  PS-CONTROL-CARD-4-END                                            
               DISPLAY PC-PROGRAM-NAME                                          
                      ' CONTROL CARD MISSING - CONNECT Q-NAME '                 
            ELSE                                                                
                IF  CC-QNAME-CONNECT > SPACES                                   
                    DISPLAY 'CONNECT Q-NAME: ' CC-CONTROL-CARD-4                
                    DISPLAY PC-FIFTY-STARS                                      
                ELSE                                                            
                    SET PS-CONTROL-CARD-4-END TO TRUE                           
                    DISPLAY PC-PROGRAM-NAME                                     
                            ' INVALID CONNECT Q-NAME'                           
                END-IF                                                          
           END-IF.                                                              
                                                                                
           CLOSE CONTROL-CARD-4.                                                
                                                                                
           IF  PS-CONTROL-CARD-1-END                                            
           OR  PS-CONTROL-CARD-2-END                                            
           OR  PS-CONTROL-CARD-3-END                                            
           OR  PS-CONTROL-CARD-4-END                                            
               CLOSE MESSAGE-ID-FILE                                            
               CALL 'ILBOABN0' USING PV-USER-ERROR                              
           ELSE                                                                 
               PERFORM Q100-MQ-CONNECT                                          
               PERFORM Q200-MQ-OPEN-PUT-ER                                      
           END-IF.                                                              
                                                                                
      **** INITIALIZE THE DCLGEN FIELDS FOR TABLES ACCESSED                     
           INITIALIZE DCLTSKXREF                                                
                      DCLTMRITEM                                                
                      DCLTMRITST                                                
                      DCLTCKRSTCNTL                                             
                      DCLTCKRSTINF                                              
                      DCLXMT00001.                                              
                                                                                
           PERFORM X100-INIT-CHECKPOINT-SELECT.                                 
                                                                                
      **** IF THIS IS THE INITIAL RUN OF THE PROGRAM (THE PROGRAM IS            
      **** NOT IN RESTART STATUS), SET THE CHECKPOINT STATUS CODE TO            
      **** 'IN PROCESS'.                                                        
           IF  TCKRSTCNTL-CKPT-STAT-CODE = '0'                                  
               MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                            
               PERFORM X300-UPDATE-CHECKPOINT-STATUS                            
           ELSE                                                                 
      ****     IF THE PROGRAM IS IN A RESTART STATUS, SELECT SAVED              
      ****     DATE FROM THE RESTART INFO TABLE.                                
               IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                              
                   PERFORM X200-SELECT-RESTART-INFO                             
                   MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                           
                     TO CR-CHECKPOINT-RESTART-VAR                               
                   DISPLAY SPACES                                               
                   DISPLAY '** MRKUT138 IS BEING RESTARTED'                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM R100-FIND-FISCAL-WEEK.                                       
                                                                                
      ******************************************************************        
      **   LOAD ALL E-BUS-LOCATIONS INTO AN INTERNAL TABLE                      
      ******************************************************************        
       A200-GET-ALL-E-LOCS.                                                     
           PERFORM S300-OPEN-E-LOCS-CURSOR.                                     
           MOVE ZERO TO PV-ELOC-SUB.                                            
           INITIALIZE EL-EFC-LOC-TABLE.                                         
           PERFORM B300-GET-E-LOC-STORES                                        
               UNTIL EOC-E-LOCS-CSR.                                            
           PERFORM U300-CLOSE-E-LOCS-CSR.                                       
                                                                                
      ******************************************************************        
      *  PROCESS ITEMS UNTIL END-OF-CURSOR                             *        
      ******************************************************************        
       B100-PROCESS-ITEM-CSR.                                                   
                                                                                
           PERFORM D100-FETCH-ITEM-CURSOR.                                      
                                                                                
           MOVE 'Y'  TO PS-FIRST-STORE-SW.                                      
           MOVE ZERO TO PV-STR-SUB.                                             
           INITIALIZE SL-STORE-LIST-TABLE                                       
                      MR120-STORE-LIST.                                         
                                                                                
           PERFORM S200-OPEN-STORE-CSR.                                         
                                                                                
           PERFORM B200-LOAD-STORE-TABLE                                        
               UNTIL EOC-STORE-CSR.                                             
                                                                                
           IF  PV-STR-SUB > 0                                                   
               PERFORM E100-ELIGIBILITY-ROUTINE                                 
           END-IF.                                                              
                                                                                
      **** AT THIS POINT A LOGICAL UNIT OF WORK HAS BEEN COMPLETED              
      **** (COMPLETED PROCESSING OF ONE ITEM / CREATION OF ONE REQUEST          
      **** MESSAGE).  THEREFORE, PREFORM CHECKPOINT PROCESSING.                 
           PERFORM X400-COMMIT-DB2.                                             
                                                                                
      *****************************************************************         
      *  LOAD TABLE WITH STORES FROM TMRITST                                    
      *****************************************************************         
       B200-LOAD-STORE-TABLE.                                                   
                                                                                
           PERFORM D200-FETCH-STORE-CURSOR.                                     
                                                                                
           IF  EOC-STORE-CSR                                                    
               CONTINUE                                                         
           ELSE                                                                 
               IF PS-FIRST-STORE                                                
                   MOVE 'N' TO PS-FIRST-STORE-SW                                
                   IF XMT00001-E-BUS-IND = 'Y'                                  
                       PERFORM C100-LOAD-ALL-E-BUS-LOCS                         
                   ELSE                                                         
                       ADD +1   TO PV-STR-SUB                                   
                       MOVE MRITST-STR-NBR TO SL-STORE-NMBR (PV-STR-SUB)        
                       MOVE PC-PIPE        TO SL-PIPE (PV-STR-SUB)              
                   END-IF                                                       
               ELSE                                                             
                   ADD +1   TO PV-STR-SUB                                       
                   MOVE MRITST-STR-NBR TO SL-STORE-NMBR (PV-STR-SUB)            
                   MOVE PC-PIPE        TO SL-PIPE (PV-STR-SUB)                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *  LOAD INTERNAL TABLE WITH E_BUS_LOCS FROM XMT_LOC                       
      *****************************************************************         
       B300-GET-E-LOC-STORES.                                                   
      *                                                                         
           PERFORM D300-FETCH-E-LOCS-CSR.                                       
                                                                                
           IF EOC-E-LOCS-CSR                                                    
              MOVE PV-ELOC-SUB        TO PV-ELOC-COUNT                          
           ELSE                                                                 
              ADD +1   TO PV-ELOC-SUB                                           
              IF PV-ELOC-SUB > PC-MAX-ELOC-NBR                                  
                  DISPLAY '** ABEND ** IN PROGRAM MRKUT138 '                    
                  DISPLAY 'MRKUT138 - EXCEEDED TABLE LIMIT'                     
                       ' OF E-LOCATION TABLE'                                   
                  CALL 'ILBOABN0' USING PV-USER-ERROR                           
              END-IF                                                            
              MOVE XMT00001-LOC-NBR   TO EL-EFC-NMBR(PV-ELOC-SUB)               
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * LOAD ALL E-BUS LOCS FROM THE INTERNAL TABLE TO THE STORE TABLE          
      ******************************************************************        
       C100-LOAD-ALL-E-BUS-LOCS.                                                
                                                                                
           MOVE 'C100'                   TO PGM-LOCATION.                       
           MOVE 'C100-LOAD-ALL-E-BUS-LOCS'                                      
                                         TO PV-ABEND-PARAGRAPH.                 
           MOVE ZERO TO PV-ELOC-SUB.                                            
           PERFORM UNTIL  PV-ELOC-SUB = PV-ELOC-COUNT                           
                        ADD +1         TO PV-STR-SUB                            
                                          PV-ELOC-SUB                           
                        MOVE EL-EFC-NMBR (PV-ELOC-SUB)                          
                                       TO SL-STORE-NMBR (PV-STR-SUB)            
                        MOVE PC-PIPE   TO SL-PIPE (PV-STR-SUB)                  
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH FETCHES THE ITEM CURSOR FOR CHANGE RECORDS      *        
      ******************************************************************        
       D100-FETCH-ITEM-CURSOR.                                                  
                                                                                
           MOVE 'D100'                   TO PGM-LOCATION.                       
           MOVE 'D100-FETCH-ITEM-CURSOR'                                        
                                         TO PV-ABEND-PARAGRAPH.                 
           EXEC SQL                                                             
             FETCH ITEM_CSR                                                     
              INTO                                                              
                   :MRITEM-ITEM-NBR,                                            
                   :SKXREF-DEPT,                                                
                   :SKXREF-CLASS,                                               
                   :SKXREF-SUBCLASS,                                            
                   :SKXREF-SKU,                                                 
                   :SKXREF-INTR-UPC-ID                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
               MOVE MRITEM-ITEM-NBR TO CR-ITEM-NBR                              
               MOVE SKXREF-INTR-UPC-ID TO PV-SAVE-UPC-ID                        
             WHEN SQLCODE = PC-ROW-NOT-FOUND                                    
               PERFORM U100-CLOSE-CHG-ITEM-CSR                                  
               SET EOC-ITEM-CSR TO TRUE                                         
             WHEN OTHER                                                         
               PERFORM Z999-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH FETCHES THE STORE CURSOR FOR STORE NUMBER, CITY *        
      ******************************************************************        
       D200-FETCH-STORE-CURSOR.                                                 
                                                                                
           MOVE 'D200'                   TO PGM-LOCATION.                       
           MOVE 'D200-FETCH-STORE-CURSOR'                                       
                                         TO PV-ABEND-PARAGRAPH.                 
           EXEC SQL                                                             
             FETCH STORE_CSR                                                    
              INTO                                                              
                   :MRITST-STR-NBR,                                             
                   :XMT00001-E-BUS-IND                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
               CONTINUE                                                         
             WHEN SQLCODE = PC-ROW-NOT-FOUND                                    
               PERFORM U200-CLOSE-STORE-CSR                                     
               SET EOC-STORE-CSR TO TRUE                                        
             WHEN OTHER                                                         
               PERFORM Z999-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH FETCHES THE E-LOCATIONS CURSOR                  *        
      ******************************************************************        
       D300-FETCH-E-LOCS-CSR.                                                   
                                                                                
           MOVE 'D300'                   TO PGM-LOCATION.                       
           MOVE 'D300-FETCH-E-LOCS-CSR'  TO PV-ABEND-PARAGRAPH.                 
           EXEC SQL                                                             
             FETCH E_LOCS_CSR                                                   
              INTO :XMT00001-LOC-NBR                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
               CONTINUE                                                         
             WHEN SQLCODE = PC-ROW-NOT-FOUND                                    
               SET EOC-E-LOCS-CSR TO TRUE                                       
             WHEN OTHER                                                         
               PERFORM Z999-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    SEND A REQUEST TO THE ELIGIBILITY ROUTINE, USING MQSERIES   *        
      *----------------------------------------------------------------*        
       E100-ELIGIBILITY-ROUTINE.                                                
                                                                                
           MOVE PV-SAVE-UPC-ID TO MR120-INTERNAL-UPC-ID.                        
           MOVE SPACE          TO SL-PIPE (PV-STR-SUB).                         
                                                                                
           MOVE PV-SCHED-MM    TO MR120-SHIP-MM.                                
           MOVE PV-SCHED-DD    TO MR120-SHIP-DD.                                
           MOVE PV-SCHED-CC    TO MR120-SHIP-CC.                                
           MOVE PV-SCHED-YY    TO MR120-SHIP-YY.                                
                                                                                
           STRING SL-STORE-LIST-TABLE                                           
                  MR121-TRAILER                                                 
                  DELIMITED BY SPACE                                            
                  INTO MR120-STORE-LIST.                                        
                                                                                
           MOVE MR120-ELIGIBILITY-INPUT-LAYOUT TO PV-MSGBUFFER.                 
           MOVE PC-MAX-REQUEST-LENGTH          TO PV-MSGLENGTH.                 
                                                                                
           COMPUTE MQPMO-OPTIONS = MQPMO-NO-SYNCPOINT                           
                                 + MQPMO-FAIL-IF-QUIESCING.                     
                                                                                
           MOVE CC-QMGR-PUT          TO PC-QMGR-NAME-PUT.                       
           MOVE PC-QMGR-NAME-PUT     TO MQOD-OBJECTQMGRNAME.                    
                                                                                
           MOVE CC-QNAME-PUT         TO MQOD-OBJECTNAME.                        
           MOVE CC-QNAME-CONNECT     TO MQMD-REPLYTOQ.                          
                                                                                
           MOVE CC-QMGR-CONNECT      TO PC-QMGR-NAME-CONNECT.                   
           MOVE PC-QMGR-NAME-CONNECT TO MQMD-REPLYTOQMGR.                       
                                                                                
           MOVE MQMI-NONE            TO MQMD-MSGID.                             
           MOVE MQCI-NONE            TO MQMD-CORRELID.                          
           MOVE MQFMT-STRING         TO MQMD-FORMAT.                            
      **** EXPIRY TIME IS IN TENTHS OF A SECOND                                 
      **** EXPIRY TIME OF 180000 = 5 HOURS                                      
           MOVE 180000               TO MQMD-EXPIRY.                            
           MOVE 3                    TO MQMD-PRIORITY.                          
                                                                                
           CALL PC-MQPUT   USING PV-HCONN                                       
                                 PV-PUTQ-HANDLE                                 
                                 MQM-MESSAGE-DESCRIPTOR                         
                                 MQM-PUT-MESSAGE-OPTIONS                        
                                 PV-MSGLENGTH                                   
                                 PV-MSGBUFFER                                   
                                 PV-COMPLETION-CODE                             
                                 PV-REASON.                                     
                                                                                
           IF  PV-COMPLETION-CODE = MQCC-OK                                     
               ADD +1  TO CR-TOT-NBR-PUTS-CNT                                   
               PERFORM F100-WRITE-OUTPUT-REC                                    
           ELSE                                                                 
      *        ----  MQPUT FAILED  ----                                         
               MOVE 'MQPUT ERROR'    TO PV-ERROR-MESSAGE                        
               MOVE 'E100-ELIGIBILITY-ROUTINE' TO PV-ABEND-PARAGRAPH            
               PERFORM Z998-DISPLAY-MQ-ERROR                                    
               PERFORM Q600-DISCONNECT-QMGR                                     
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE A RECORD TO THE MESSAGE ID OUTPUT FILE.                          
      ******************************************************************        
       F100-WRITE-OUTPUT-REC.                                                   
                                                                                
           MOVE MRITEM-ITEM-NBR    TO MR138-ITEM-NBR.                           
           MOVE SKXREF-SKU         TO MR138-SKU.                                
           MOVE SKXREF-DEPT        TO MR138-DEPT.                               
           MOVE SKXREF-CLASS       TO MR138-MCL.                                
           MOVE SKXREF-SUBCLASS    TO MR138-SCL.                                
           MOVE SKXREF-INTR-UPC-ID TO MR138-INTR-UPC-ID.                        
           MOVE MQMD-MSGID         TO MR138-MESSAGE-ID.                         
                                                                                
           WRITE MESSAGE-ID-RECORD                                              
            FROM MR138-ER-MESSAGE-ID-RECORD.                                    
                                                                                
           ADD  +1                 TO CR-TOT-OUTPUT-RECS-CNT.                   
                                                                                
      ******************************************************************        
      *  MQ - CONNECT TO THE QUEUE MANAGER.                                     
      ******************************************************************        
       Q100-MQ-CONNECT.                                                         
                                                                                
           MOVE CC-QMGR-CONNECT TO PC-QMGR-NAME-CONNECT.                        
                                                                                
           CALL PC-MQCONN USING                                                 
                                PC-QMGR-NAME-CONNECT                            
                                PV-HCONN                                        
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
                                                                                
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE                      
      *    AND EXIT                                                             
                                                                                
           MOVE PV-REASON TO PV-CON-REASON.                                     
           IF  (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                          
               MOVE 'MQCONN ERROR'    TO PV-ERROR-MESSAGE                       
               MOVE 'Q100-MQ-CONNECT' TO  PV-ABEND-PARAGRAPH                    
               PERFORM Z998-DISPLAY-MQ-ERROR                                    
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    OPEN QUEUES FOR CALL TO ELIGIBILITY RULES                            
      *----------------------------------------------------------------*        
       Q200-MQ-OPEN-PUT-ER.                                                     
      *    ---- OPEN ITEM ELIGIBILITY QUEUE FOR PUTTING RESPONSES ----          
                                                                                
           COMPUTE PV-OPEN-OPTIONS = MQOO-OUTPUT +                              
                                     MQOO-FAIL-IF-QUIESCING.                    
                                                                                
           MOVE CC-QNAME-PUT     TO MQOD-OBJECTNAME.                            
                                                                                
           MOVE CC-QMGR-PUT      TO PC-QMGR-NAME-PUT.                           
           MOVE PC-QMGR-NAME-PUT TO MQOD-OBJECTQMGRNAME.                        
                                                                                
           CALL PC-MQOPEN USING PV-HCONN                                        
                                MQM-OBJECT-DESCRIPTOR                           
                                PV-OPEN-OPTIONS                                 
                                PV-PUTQ-HANDLE                                  
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
                                                                                
      *    IF OPEN FAILED, DISPLAY ERROR MESSAGE AND ABEND                      
                                                                                
           IF  PV-COMPLETION-CODE NOT = MQCC-OK                                 
               CALL PC-MQCHECK USING PV-REASON PV-MQ-REASON-TEXT                
                                                                                
               MOVE 'Q200-MQ-OPEN-PUT-ER' TO PV-ABEND-PARAGRAPH                 
               MOVE 'MQOPEN ERROR   '     TO PV-ERROR-MESSAGE                   
               PERFORM Z998-DISPLAY-MQ-ERROR                                    
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    CLOSE MQSERIES QUEUES                                       *        
      *----------------------------------------------------------------*        
       Q400-MQ-CLOSE-PUT-ER.                                                    
                                                                                
      *    ---- CLOSE PUT QUEUE ----                                            
           CALL PC-MQCLOSE USING PV-HCONN                                       
                                 PV-PUTQ-HANDLE                                 
                                 MQCO-NONE                                      
                                 PV-COMPLETION-CODE                             
                                 PV-REASON.                                     
                                                                                
           IF  PV-COMPLETION-CODE NOT = MQCC-OK                                 
      *        IF  PS-ELIGIBILITY-GOOD                                          
                                                                                
                   CALL PC-MQCHECK USING PV-REASON PV-MQ-REASON-TEXT            
                                                                                
                   MOVE 'Q400-MQ-CLOSE-PUT-ER' TO PV-ABEND-PARAGRAPH            
                   MOVE 'MQCLOSE ERROR  '      TO PV-ERROR-MESSAGE              
                   PERFORM Z998-DISPLAY-MQ-ERROR                                
      *        END-IF                                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      *  DISCONNECT QUEUE MANAGER                                               
      *****************************************************************         
       Q600-DISCONNECT-QMGR.                                                    
                                                                                
           IF  PV-CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED             
               CALL PC-MQDISC USING PV-HCONN                                    
                                    PV-COMPLETION-CODE                          
                                    PV-REASON                                   
               IF  (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                      
                   MOVE 'MQDISC ERROR'         TO PV-ERROR-MESSAGE              
                   MOVE 'Q600-DISCONNECT-QMGR' TO PV-ABEND-PARAGRAPH            
                   PERFORM Z998-DISPLAY-MQ-ERROR                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH SETS THE DATE FOR THE CURSOR                    *        
      ******************************************************************        
       R100-FIND-FISCAL-WEEK.                                                   
           INITIALIZE            DPG51 DPG52                                    
                                 DPG53 DPG54                                    
                                 DPG55 DPG56.                                   
                                                                                
           MOVE PV-SCHED-DATE TO DPG52-LK-DATE-INPUT.                           
           SET DPG52-LK-DTE-ISO-FMT TO TRUE.                                    
           MOVE PV-F2   TO DPG51-CALENDAR-TYPE.                                 
                                                                                
           PERFORM R200-CALENDAR-ROUTINE.                                       
                                                                                
           MOVE DPG56-FISCAL-WEEK-NBR    TO PV-FISCAL-SPLIT.                    
           MOVE DPG55-NUMERIC-WEEK-DAY   TO PV-RT-DAY-CAL.                      
           MOVE PV-RT-DAY-CAL            TO RT-DAY-NO.                          
           DISPLAY SPACES.                                                      
           DISPLAY 'RT-DAY = ' RT-DAY-NO.                                       
                                                                                
      ******************************************************************        
      * CALLS THE DATE ROUTINE.                                                 
      ******************************************************************        
       R200-CALENDAR-ROUTINE.                                                   
                                                                                
           CALL PC-PGM-DPKUT500 USING DPG51 DPG52                               
                                      DPG53 DPG54                               
                                      DPG55 DPG56.                              
           EVALUATE TRUE                                                        
           WHEN DPG54-SEVERE-ERROR                                              
               DISPLAY 'SEVERE ERROR IN CALENDAR ROUTINE'                       
               DISPLAY DPG54-ERROR-MESSAGE                                      
               DISPLAY 'INPUT DATE: '  DPG52-LK-DATE-INPUT                      
               CALL 'ILBOABN0'        USING PV-ABEND-CODE                       
           WHEN DPG54-WARNING-ERROR                                             
               DISPLAY 'WARNING ERROR IN CALENDAR ROUTINE'                      
               DISPLAY DPG54-ERROR-MESSAGE                                      
           WHEN DPG54-NO-ERROR                                                  
               CONTINUE                                                         
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * READ CONTROL CARD-1 TO OBTAIN THE PUT QUEUE MGR NAME.                   
      *****************************************************************         
       S010-READ-CONTROL-CARD-1.                                                
                                                                                
            READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1                          
                AT END                                                          
                    SET PS-CONTROL-CARD-1-END TO TRUE.                          
                                                                                
      *****************************************************************         
      * READ CONTROL CARD-2 TO OBTAIN THE CONNECT QUEUE MGR NAME.               
      *****************************************************************         
       S020-READ-CONTROL-CARD-2.                                                
                                                                                
            READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                          
                AT END                                                          
                    SET PS-CONTROL-CARD-2-END TO TRUE.                          
                                                                                
      *****************************************************************         
      * READ CONTROL CARD-3 TO OBTAIN THE PUT QUEUE NAME.                       
      *****************************************************************         
       S030-READ-CONTROL-CARD-3.                                                
                                                                                
            READ CONTROL-CARD-3 INTO CC-CONTROL-CARD-3                          
                AT END                                                          
                    SET PS-CONTROL-CARD-3-END   TO TRUE.                        
                                                                                
      *****************************************************************         
      * READ CONTROL CARD-4 TO OBTAIN THE CONNECT QUEUE NAME.                   
      *****************************************************************         
       S040-READ-CONTROL-CARD-4.                                                
                                                                                
            READ CONTROL-CARD-4 INTO CC-CONTROL-CARD-4                          
                AT END                                                          
                    SET PS-CONTROL-CARD-4-END   TO TRUE.                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH OPENS THE CHG CURSOR                                     
      ******************************************************************        
       S100-OPEN-ITEM-CSR.                                                      
                                                                                
           MOVE 'S100'                TO PGM-LOCATION.                          
           MOVE 'S100-OPEN-ITEM-CSR'  TO PV-ABEND-PARAGRAPH.                    
                                                                                
           EXEC SQL                                                             
               OPEN ITEM_CSR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
             WHEN SQLCODE = PC-ROW-NOT-FOUND                                    
               CONTINUE                                                         
             WHEN OTHER                                                         
               PERFORM Z999-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH OPENS THE STORE CURSOR                                   
      ******************************************************************        
       S200-OPEN-STORE-CSR.                                                     
                                                                                
           MOVE 'S200'                TO PGM-LOCATION.                          
           MOVE 'S200-OPEN-STORE-CSR' TO PV-ABEND-PARAGRAPH.                    
                                                                                
           EXEC SQL                                                             
               OPEN STORE_CSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
               MOVE 'N' TO PS-EOC-STORE-CSR                                     
             WHEN SQLCODE = PC-ROW-NOT-FOUND                                    
               CONTINUE                                                         
             WHEN OTHER                                                         
               PERFORM Z999-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH OPENS THE E-LOCATIONS CURSOR                             
      ******************************************************************        
       S300-OPEN-E-LOCS-CURSOR.                                                 
                                                                                
           MOVE 'S300'                TO PGM-LOCATION.                          
           MOVE 'S300-OPEN-E-LOCS-CURSOR' TO PV-ABEND-PARAGRAPH.                
                                                                                
           EXEC SQL                                                             
               OPEN E_LOCS_CSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
               MOVE 'N' TO PS-E-LOCS-CSR                                        
             WHEN SQLCODE = PC-ROW-NOT-FOUND                                    
               CONTINUE                                                         
             WHEN OTHER                                                         
               PERFORM Z999-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  CLOSE CHANGE CURSOR                                           *        
      ******************************************************************        
       U100-CLOSE-CHG-ITEM-CSR.                                                 
                                                                                
           MOVE 'U100'                    TO PGM-LOCATION.                      
           MOVE 'U100-CLOSE-CHG-ITEM-CSR' TO PV-ABEND-PARAGRAPH.                
           EXEC SQL                                                             
               CLOSE ITEM_CSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
             WHEN SQLCODE = PC-ROW-NOT-FOUND                                    
               CONTINUE                                                         
             WHEN OTHER                                                         
               PERFORM Z999-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  CLOSE STORE CURSOR                                            *        
      ******************************************************************        
       U200-CLOSE-STORE-CSR.                                                    
                                                                                
           MOVE 'U200'                 TO PGM-LOCATION.                         
           MOVE 'U200-CLOSE-STORE-CSR' TO PV-ABEND-PARAGRAPH.                   
           EXEC SQL                                                             
               CLOSE STORE_CSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
             WHEN SQLCODE = PC-ROW-NOT-FOUND                                    
               CONTINUE                                                         
             WHEN OTHER                                                         
               PERFORM Z999-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  CLOSE E-LOCS CURSOR                                           *        
      ******************************************************************        
       U300-CLOSE-E-LOCS-CSR.                                                   
                                                                                
           MOVE 'U300'                  TO PGM-LOCATION.                        
           MOVE 'U300-CLOSE-E-LOCS-CSR' TO PV-ABEND-PARAGRAPH.                  
           EXEC SQL                                                             
               CLOSE E_LOCS_CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
             WHEN SQLCODE = PC-ROW-NOT-FOUND                                    
               CONTINUE                                                         
             WHEN OTHER                                                         
               PERFORM Z999-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  SELECT FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE IF             
      *  THE PROGRAM IS IN A RESTART CONDITION.                                 
      ******************************************************************        
       X100-INIT-CHECKPOINT-SELECT.                                             
                                                                                
           EXEC SQL                                                             
             SELECT                                                             
                  CKPT_STAT_CODE                                                
               INTO                                                             
                 :TCKRSTCNTL-CKPT-STAT-CODE                                     
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'X100-INIT-CHECKPOINT-SELECT '                          
                     TO  PV-ABEND-PARAGRAPH                                     
                   MOVE 'ERROR ON SELECT OF TCKRSTCNTL '                        
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM Z999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  SELECT FROM THE CHECKPOINT RESTART INFORMATION TABLE TO                
      *  OBTAIN THE PROGRAMS SAVED VARIABLES.                                   
      ******************************************************************        
       X200-SELECT-RESTART-INFO.                                                
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                  WORK_STRG_DESC                                                
               INTO                                                             
                 :TCKRSTINF-WORK-STRG-DESC                                      
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'X200-SELECT-RESTART-INFO '                             
                     TO  PV-ABEND-PARAGRAPH                                     
                   MOVE 'ERROR ON SELECT OF TCKRSTINF '                         
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM Z999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  UPDATE THE STATUS ON THE CHECKPOINT CONTROL TABLE                      
      ******************************************************************        
       X300-UPDATE-CHECKPOINT-STATUS.                                           
                                                                                
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'X300-UPDATE-CHECKPOINT-STATUS '                        
                     TO  PV-ABEND-PARAGRAPH                                     
                   MOVE 'ERROR ON UPDATE OF TCKRSTCNTL '                        
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM Z999-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE AND                     
      *  TAKE A COMMIT.                                                         
      ******************************************************************        
       X400-COMMIT-DB2.                                                         
                                                                                
           MOVE CR-CHECKPOINT-RESTART-AREA TO TCKRSTINF-WORK-STRG-DESC.         
                                                                                
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'X400-COMMIT-DB2 '                                          
                 TO  PV-ABEND-PARAGRAPH                                         
               MOVE 'ERROR ON UPDATE OF TCKRSTINF '                             
                 TO  PV-DB2-OPERATION-ATTEMPTED                                 
               PERFORM Z999-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'X400-COMMIT-DB2 '                                          
                 TO  PV-ABEND-PARAGRAPH                                         
               MOVE 'ERROR ON COMMIT '                                          
                 TO  PV-DB2-OPERATION-ATTEMPTED                                 
               PERFORM Z999-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
           EJECT                                                                
                                                                                
      ******************************************************************        
      *  DISPLAY TOTAL COUNTS & UPDATE CKPT STATUS                              
      ******************************************************************        
       Z995-END-OF-JOB.                                                         
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*********************************************'.             
           DISPLAY '***** TOTAL COUNTS FOR PROGRAM MRKUT138 *****'.             
           DISPLAY '*********************************************'.             
                                                                                
           MOVE CR-TOT-NBR-PUTS-CNT     TO PV-DISPLAY-COUNT                     
           DISPLAY 'TOT NBR OF SUCCESSFUL MQ PUTS  = '                          
                                                     PV-DISPLAY-COUNT.          
                                                                                
           MOVE CR-TOT-OUTPUT-RECS-CNT  TO PV-DISPLAY-COUNT                     
           DISPLAY 'TOT NBR OF OUTPUT RECS WRITTEN = '                          
                                                     PV-DISPLAY-COUNT.          
           DISPLAY SPACES.                                                      
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                               
           PERFORM X300-UPDATE-CHECKPOINT-STATUS.                               
                                                                                
      *****************************************************************         
      * DISPLAY THE VALUES OF A FAILED MQ API COMMAND                           
      *****************************************************************         
       Z998-DISPLAY-MQ-ERROR.                                                   
                                                                                
           DISPLAY '************************************************'.          
           DISPLAY '**** ABEND *************************************'.          
           DISPLAY '************************************************'.          
           DISPLAY '** MQSERIES ERROR **'                                       
           DISPLAY '** PROGRAM:    ', PC-PROGRAM-NAME.                          
           DISPLAY '** PARAGRAPH:  ', PV-ABEND-PARAGRAPH.                       
           DISPLAY '** MESSAGE:    ', PV-ERROR-MESSAGE.                         
           DISPLAY '** COMP CODE:  ', PV-COMPLETION-CODE.                       
           DISPLAY '** RSN CODE:   ', PV-REASON.                                
           DISPLAY '************************************************'.          
           DISPLAY '************************************************'.          
           DISPLAY '************************************************'.          
      *    DISPLAY '************************************************'.          
      *    DISPLAY '**** PROGRAM COUNTERS AT TIME OF ABEND *********'.          
      *    DISPLAY '************************************************'.          
      *    DISPLAY '** NUMBER OF Q MESSAGES PROCESSED: ', PA-PUTS.              
      *    DISPLAY '************************************************'.          
           MOVE PV-MQ-ERROR TO PV-ABEND-CODE.                                   
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
      *                                                                         
      ******************************************************************        
      *   ERROR MESSAGE ROUTINE                                                 
      ******************************************************************        
       Z999-SQL-ERROR.                                                          
                                                                                
           MOVE SQLCODE    TO SQLCODE2.                                         
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
           DISPLAY '** OPERATION      ** = ' PV-DB2-OPERATION-ATTEMPTED.        
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           MOVE +4013 TO PV-ABEND-CODE.                                         
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
      ******************************************************************        
