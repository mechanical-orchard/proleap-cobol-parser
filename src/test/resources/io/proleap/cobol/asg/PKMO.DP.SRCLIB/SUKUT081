000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000500 PROGRAM-ID.    SUKUT081.                                                 
000600 AUTHOR.        DEANNA BRANTNER.                                          
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   DECEMBER, 2002.                                           
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES AN EXTRACT FILE OF OUTBOUND TRAILERS THAT         
001300*  WERE OPENED FOR THE BATCH PROCESS DAY & THAT WERE OPENED USING         
001300*  A DIFFERENT SEAL # THAN THE ONE THE TRAILER WAS CLOSED WITH.           
001300*  THIS PRGM CREATE OUTPUT FILE PKMO.SU.DLY.TRLR.SEAL.SURD042.EXT.        
001300*  THIS FILE WILL BE USED BY PROGRAM SUKRP037 TO CREATE THE TRLR          
001300*  SEAL DISCREPANCY REPORT ON RDS AS SUK01025 IN JOB SUK010.              
001400******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
003300     SELECT TRLR-SEAL-DATA-EXT                                            
003400         ASSIGN TO OUT01.                                                 
003410                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
004800 FD  TRLR-SEAL-DATA-EXT                                                   
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005200                                                                          
005300 01  TRLR-SEAL-DATA-REC         PIC  X(200).                              
005470                                                                          
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
006700*-----------------------------------------------------------------        
006800*  RECORD LAYOUT FOR THE TRLR SEAL OUTPUT FILE.                           
006810*-----------------------------------------------------------------        
007000     COPY SURD042.                                                        
007100                                                                          
007200*-----------------------------------------------------------------        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007510*-----------------------------------------------------------------        
007700     COPY DPWS004.                                                        
007800                                                                          
007801*-----------------------------------------------------------------        
007802*  COMMUNICATIONS AREA FOR DB2                                            
007803*-----------------------------------------------------------------        
007804     EXEC SQL                                                             
007805          INCLUDE SQLCA                                                   
007806     END-EXEC.                                                            
007807                                                                          
007810*-----------------------------------------------------------------        
007820*  DCLGEN MEMBER FOR THE CONTROL TABLE                                    
007840*-----------------------------------------------------------------        
007850     EXEC SQL                                                             
007860          INCLUDE TCOCNTL                                                 
007870     END-EXEC.                                                            
007880                                                                          
007810*-----------------------------------------------------------------        
007820*  DCLGEN MEMBER FOR THE OUTBOUND TRIP STATUS TABLE                       
007840*-----------------------------------------------------------------        
007850     EXEC SQL                                                             
007860          INCLUDE TSUOBTS                                                 
007870     END-EXEC.                                                            
007880                                                                          
007810*-----------------------------------------------------------------        
007820*  DCLGEN MEMBER FOR THE OUTBOUND TRIP TABLE                              
007840*-----------------------------------------------------------------        
007850     EXEC SQL                                                             
007860          INCLUDE TSUOBTR                                                 
007870     END-EXEC.                                                            
007880                                                                          
007810*-----------------------------------------------------------------        
007820*  DCLGEN MEMBER FOR THE CONTROL TRAILER ID TABLE                         
007840*-----------------------------------------------------------------        
007850     EXEC SQL                                                             
007860          INCLUDE TCNTRID                                                 
007870     END-EXEC.                                                            
007880                                                                          
007810*-----------------------------------------------------------------        
007820*  DCLGEN MEMBER FOR THE OUTBOUND STATUS CODE TABLE                       
007840*-----------------------------------------------------------------        
007850     EXEC SQL                                                             
007860          INCLUDE TSUOBSC                                                 
007870     END-EXEC.                                                            
007880                                                                          
010320*-----------------------------------------------------------------        
010340*-----------------------------------------------------------------        
011200 01  ABEND-CODE                    PIC S9(04) VALUE +0                    
011300                                              COMP SYNC.                  
011400                                                                          
012100 01  DK-DB2-CONSTANTS.                                                    
012400     05  DK-PROCESS-DATE           PIC  X(10) VALUE SPACES.               
013100                                                                          
011500 01  PS-PROGRAM-SWITCHES.                                                 
011600     05  PS-EOF-OPEN-TRLR-CSR-SW   PIC  X(01) VALUE 'N'.                  
011700         88  PS-EOF-OPEN-TRLR-CSR             VALUE 'Y'.                  
011800         88  PS-MORE-OPEN-TRLR-CSR            VALUE 'N'.                  
011600     05  PS-EOF-CLOSE-TRLR-CSR-SW  PIC  X(01) VALUE 'N'.                  
011700         88  PS-EOF-CLOSE-TRLR-CSR            VALUE 'Y'.                  
011800         88  PS-MORE-CLOSE-TRLR-CSR           VALUE 'N'.                  
011900                                                                          
012100 01  PC-PROGRAM-CONSTANTS.                                                
012400     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08) VALUE 'SUKUT081'.           
013000     05  PC-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.               
013100                                                                          
013200 01  PV-PROGRAM-VARIABLES.                                                
013400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
013500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
014200     05  PV-DISPLAY-COUNT            PIC S9(09)    VALUE ZERO             
014300                                                   COMP SYNC.             
014400     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
014500                                                   COMP SYNC.             
014400     05  PV-SELECT-COUNTER           PIC S9(09)    VALUE ZERO             
014500                                                   COMP SYNC.             
015900                                                                          
015910*-----------------------------------------------------------------        
015920*  DETERMINE WHICH TRAILERS WHERE OPENED TODAY BUT ARE NOT THE            
015920*  FIRST OPEN FOR THE TRIP.                                               
015930*-----------------------------------------------------------------        
016000     EXEC SQL                                                             
016100          DECLARE OPEN-TRLR-CSR CURSOR FOR                                
016200           SELECT C.ORGN_OPER_UNT_ID                                      
016201                 ,C.DEST_OPER_UNT_ID                                      
016202                 ,D.SCAC_CDE                                              
016210                 ,D.TRLR_ID                                               
016220                 ,C.TRLR_SRL_NBR                                          
016201                 ,A.OTBND_TRIP_ID                                         
016202                 ,A.OTBND_STAT_TMST                                       
016210                 ,A.TRIP_STAT_CDE                                         
016220                 ,E.STAT_SHRT_DESC                                        
016202                 ,A.STAT_OPER_UNT_ID                                      
016210                 ,A.OTBND_STAT_USR_ID                                     
016220                 ,A.TRLR_SEAL_ID                                          
017700             FROM TSUOBTS A                                               
017700                 ,TSUOBTR C                                               
017700                 ,TCNTRID D                                               
017700                 ,TSUOBSC E                                               
018000            WHERE A.TRIP_STAT_CDE  IN  ('DO', 'AO', 'SO')                 
018000              AND DATE(A.OTBND_STAT_TMST)  =  :DK-PROCESS-DATE            
018000              AND A.OTBND_TRIP_ID          =  C.OTBND_TRIP_ID             
018000              AND C.TRLR_SRL_NBR           =  D.TRLR_SRL_NBR              
018000              AND A.TRIP_STAT_CDE          =  E.TRIP_STAT_CDE             
018000              AND A.OTBND_STAT_TMST NOT IN                                
018000                  (SELECT MIN(B.OTBND_STAT_TMST)                          
017700                    FROM TSUOBTS B                                        
018000               WHERE B.OTBND_TRIP_ID       =  A.OTBND_TRIP_ID)            
                  ORDER BY C.ORGN_OPER_UNT_ID                                   
                          ,C.DEST_OPER_UNT_ID                                   
                          ,D.SCAC_CDE                                           
                          ,D.TRLR_ID                                            
                          ,A.OTBND_STAT_TMST                                    
018200     END-EXEC.                                                            
018400                                                                          
015910*-----------------------------------------------------------------        
015920*  FOR EACH ROW RETRIEVED WITH THE ABOVE CURSOR, USE THE CURSOR           
015920*  BELOW TO SEE IF THE SEAL # USED TO CLOSE THE TRAILER PRIOR             
015920*  TO THE OPEN IS THE SAME.  YOU WILL ONLY NEED TO FETCH THIS             
015920*  CURSOR ONE TIME FOR EACH ROW FROM THE OPEN CURSOR.                     
015930*-----------------------------------------------------------------        
016000     EXEC SQL                                                             
016100          DECLARE CLOSE-TRLR-CSR CURSOR FOR                               
016200           SELECT OTBND_STAT_TMST                                         
016201                 ,OTBND_STAT_USR_ID                                       
016202                 ,TRLR_SEAL_ID                                            
017700             FROM TSUOBTS                                                 
018000            WHERE OTBND_TRIP_ID    =  :SU042-TRIP-ID                      
018000              AND OTBND_STAT_TMST  <  :SU042-OPEN-TMST                    
018000              AND TRIP_STAT_CDE    =  'DC'                                
                  ORDER BY OTBND_STAT_TMST DESC                                 
018200     END-EXEC.                                                            
018400                                                                          
018500******************************************************************        
018600 PROCEDURE DIVISION.                                                      
018700******************************************************************        
018801                                                                          
018810*-----------------------------------------------------------------        
018820* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
018830*      PROGRAM.                                                           
018840*-----------------------------------------------------------------        
018900 0000-PROGRAM-MAINLINE.                                                   
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019600     IF  PS-MORE-OPEN-TRLR-CSR                                            
019700         PERFORM 2000-PROCESS-TRLR-DATA                                   
019800           UNTIL PS-EOF-OPEN-TRLR-CSR                                     
019900     END-IF.                                                              
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020100                                                                          
020200     STOP RUN.                                                            
020310                                                                          
020320*-----------------------------------------------------------------        
020330*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020340*    -- OPEN OUTPUT FILE                                                  
020350*    -- OPEN CURSOR                                                       
020360*    -- FETCH CURSOR                                                      
020370*-----------------------------------------------------------------        
020400 1000-INITIALIZE.                                                         
021100                                                                          
021400     OPEN OUTPUT TRLR-SEAL-DATA-EXT.                                      
021600                                                                          
021700     PERFORM 1100-GET-BATCH-PROC-DATE.                                    
021710                                                                          
021800     PERFORM 3000-OPEN-TRLR-OPEN-CURSOR.                                  
021900     PERFORM 3100-FETCH-TRLR-OPEN-CURSOR.                                 
022000                                                                          
022010                                                                          
022100*-----------------------------------------------------------------        
022200* RETRIEVE BATCH PROCESS DATE FROM TCOCNTL                                
022300*-----------------------------------------------------------------        
025400 1100-GET-BATCH-PROC-DATE.                                                
025401                                                                          
025402     EXEC SQL                                                             
025403         SELECT BTCH_PRC_DTE                                              
025404           INTO :DK-PROCESS-DATE                                          
025405           FROM TCOCNTL                                                   
025406          WHERE OPER_UNT_ID  = 90                                         
025402     END-EXEC.                                                            
025407                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
032700             CONTINUE                                                     
032800         WHEN SQLCODE = +100                                              
032900             MOVE ZEROES TO DK-PROCESS-DATE                               
033100         WHEN SQLWARN0 NOT = SPACES                                       
033200         WHEN SQLCODE < ZERO                                              
033300         WHEN SQLCODE > ZERO                                              
033400             MOVE '1100-GET-BATCH-PROC-DATE ' TO PV-PARAGRAPH-NAME        
033600             MOVE 'RETRIEVING BATCH PROCESS DATE '                        
033700                                     TO PV-DB2-OPERATION-ATTEMPTED        
033800             PERFORM 9100-SQL-ABEND                                       
033900     END-EVALUATE.                                                        
034000                                                                          
025409*-----------------------------------------------------------------        
025410*  LOOP THROUGH THE RESULTS TABLE.                                        
025411*  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT MEET CRITERIA.       
025413*-----------------------------------------------------------------        
025420 2000-PROCESS-TRLR-DATA.                                                  
026000                                                                          
026200     PERFORM 3100-FETCH-TRLR-OPEN-CURSOR.                                 
026300                                                                          
026320*-----------------------------------------------------------------        
026330*  OPEN THE FIRST CURSOR.                                                 
026340*-----------------------------------------------------------------        
026400 3000-OPEN-TRLR-OPEN-CURSOR.                                              
027300                                                                          
027400     EXEC SQL                                                             
027500          OPEN OPEN-TRLR-CSR                                              
027600     END-EXEC                                                             
027700                                                                          
027800     EVALUATE TRUE                                                        
027810         WHEN SQLCODE = ZERO                                              
027820              CONTINUE                                                    
027900         WHEN SQLCODE = -904                                              
028000         WHEN SQLCODE = -911                                              
028100              CONTINUE                                                    
028200         WHEN SQLWARN0 NOT = SPACES                                       
028300         WHEN SQLCODE < ZERO                                              
028400         WHEN SQLCODE > ZERO                                              
028500             MOVE '3000-OPEN-TRLR-OPEN-CURSOR '                           
028600                                              TO PV-PARAGRAPH-NAME        
028700             MOVE 'OPEN THE OPEN-TRLR-CSR     '                           
028800                                     TO PV-DB2-OPERATION-ATTEMPTED        
028900             PERFORM 9100-SQL-ABEND                                       
029200     END-EVALUATE.                                                        
029400                                                                          
030210*-----------------------------------------------------------------        
030220*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
030230*-----------------------------------------------------------------        
030300 3100-FETCH-TRLR-OPEN-CURSOR.                                             
030500                                                                          
034150     INITIALIZE TRLR-SEAL-DATA-REC.                                       
030700     EXEC SQL                                                             
030800          FETCH OPEN-TRLR-CSR                                             
031000           INTO :SU042-OPER-UNIT                                          
031100               ,:SU042-DEST-NBR                                           
031200               ,:SU042-SCAC                                               
031300               ,:SU042-TRLR-ID                                            
031100               ,:SU042-TRLR-SRL-NBR                                       
031200               ,:SU042-TRIP-ID                                            
031300               ,:SU042-OPEN-TMST                                          
031200               ,:SU042-OPEN-STAT-CDE                                      
031300               ,:SU042-OPEN-FUNCTION                                      
031100               ,:SU042-OPEN-OPER-UNT                                      
031200               ,:SU042-OPEN-USER-ID                                       
031300               ,:SU042-OPEN-SEAL-ID                                       
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
034800             ADD +1 TO PV-SELECT-COUNTER                                  
021800             PERFORM 4000-OPEN-TRLR-CLOSE-CURSOR                          
021900             PERFORM 4100-FETCH-TRLR-CLOSE-CURSOR                         
021900             PERFORM 4200-CLOSE-TRLR-CLOSE-CURSOR                         
032800         WHEN SQLCODE = +100                                              
032900             SET PS-EOF-OPEN-TRLR-CSR  TO  TRUE                           
033000                                                                          
033100         WHEN SQLWARN0 NOT = SPACES                                       
033200         WHEN SQLCODE < ZERO                                              
033300         WHEN SQLCODE > ZERO                                              
033400             MOVE '3100-FETCH-TRLR-OPEN-CURSOR  '                         
033500                                 TO PV-PARAGRAPH-NAME                     
033600             MOVE 'FETCH THE OPEN-TRLR-CSR   '                            
033700                                 TO PV-DB2-OPERATION-ATTEMPTED            
033800             PERFORM 9100-SQL-ABEND                                       
033900     END-EVALUATE.                                                        
034000                                                                          
034120                                                                          
026320*-----------------------------------------------------------------        
026330*  OPEN THE SECOND CURSOR.                                                
026340*-----------------------------------------------------------------        
026400 4000-OPEN-TRLR-CLOSE-CURSOR.                                             
027300                                                                          
027400     EXEC SQL                                                             
027500          OPEN CLOSE-TRLR-CSR                                             
027600     END-EXEC                                                             
027700                                                                          
027800     EVALUATE TRUE                                                        
027810         WHEN SQLCODE = ZERO                                              
027820              CONTINUE                                                    
027900         WHEN SQLCODE = -904                                              
028000         WHEN SQLCODE = -911                                              
028100              CONTINUE                                                    
028200         WHEN SQLWARN0 NOT = SPACES                                       
028300         WHEN SQLCODE < ZERO                                              
028400         WHEN SQLCODE > ZERO                                              
028500             MOVE '4000-OPEN-TRLR-CLOSE-CURSOR '                          
028600                                              TO PV-PARAGRAPH-NAME        
028700             MOVE 'OPEN THE CLOSE-TRLR-CSR     '                          
028800                                     TO PV-DB2-OPERATION-ATTEMPTED        
028900             PERFORM 9100-SQL-ABEND                                       
029200     END-EVALUATE.                                                        
029400                                                                          
030210*-----------------------------------------------------------------        
030220*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
030230*-----------------------------------------------------------------        
030300 4100-FETCH-TRLR-CLOSE-CURSOR.                                            
030500                                                                          
030700     EXEC SQL                                                             
030800          FETCH CLOSE-TRLR-CSR                                            
031000           INTO :SU042-CLOSE-TMST                                         
031100               ,:SU042-CLOSE-USER-ID                                      
031200               ,:SU042-CLOSE-SEAL-ID                                      
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
021800             PERFORM 8000-WRITE-TRLR-SEAL-REC                             
032800         WHEN SQLCODE = +100                                              
032900             SET PS-EOF-CLOSE-TRLR-CSR  TO  TRUE                          
033100         WHEN SQLWARN0 NOT = SPACES                                       
033200         WHEN SQLCODE < ZERO                                              
033300         WHEN SQLCODE > ZERO                                              
033400             MOVE '4100-FETCH-TRLR-CLOSE-CURSOR  '                        
033500                                 TO PV-PARAGRAPH-NAME                     
033600             MOVE 'FETCH THE CLOSE-TRLR-CSR   '                           
033700                                 TO PV-DB2-OPERATION-ATTEMPTED            
033800             PERFORM 9100-SQL-ABEND                                       
033900     END-EVALUATE.                                                        
034000                                                                          
030210*-----------------------------------------------------------------        
030220*  CLOSE THE SECOND CURSOR.                                               
030230*-----------------------------------------------------------------        
030300 4200-CLOSE-TRLR-CLOSE-CURSOR.                                            
030500                                                                          
037400     EXEC SQL                                                             
037500          CLOSE CLOSE-TRLR-CSR                                            
037600     END-EXEC.                                                            
037700                                                                          
037800     EVALUATE TRUE                                                        
037810         WHEN SQLCODE = ZERO                                              
037820              CONTINUE                                                    
037900         WHEN SQLCODE = -904                                              
038000         WHEN SQLCODE = -911                                              
038100              CONTINUE                                                    
038200         WHEN SQLWARN0 NOT = SPACES                                       
038300         WHEN SQLCODE < ZERO                                              
038400         WHEN SQLCODE > ZERO                                              
038500              MOVE '4200-CLOSE-TRLR-CLOSE-CURSOR   '                      
038600                                 TO PV-PARAGRAPH-NAME                     
038700              MOVE 'CLOSE THE CLOSE-TRLR-CSR     '                        
038800                                 TO PV-DB2-OPERATION-ATTEMPTED            
038900              PERFORM 9100-SQL-ABEND                                      
039200     END-EVALUATE.                                                        
                                                                                
034121*-----------------------------------------------------------------        
034122*  WRITE THE TRAILER SEAL RECORD                                          
034123*-----------------------------------------------------------------        
034130 8000-WRITE-TRLR-SEAL-REC.                                                
034140                                                                          
034160     IF SU042-OPEN-SEAL-ID  NOT = SU042-CLOSE-SEAL-ID                     
034600         WRITE TRLR-SEAL-DATA-REC FROM SU042-EXTRACT-RECORD               
034800         ADD +1 TO PV-WRITE-COUNTER                                       
035690     END-IF.                                                              
035700                                                                          
035710*-----------------------------------------------------------------        
035711*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
035720*-----------------------------------------------------------------        
035800 9000-EOJ-ROUTINE.                                                        
036200     CLOSE TRLR-SEAL-DATA-EXT.                                            
036400                                                                          
036600     DISPLAY 'BATCH PROCESS DATE ' DK-PROCESS-DATE                .       
036500     MOVE PV-SELECT-COUNTER TO PV-DISPLAY-COUNT.                          
036600     DISPLAY 'TOTAL TRLR RECORDS SELECTED ' PV-DISPLAY-COUNT      .       
036500     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                           
036600     DISPLAY 'TOTAL TRLR SEAL RECORDS WRITTEN ' PV-DISPLAY-COUNT  .       
036700                                                                          
037400     EXEC SQL                                                             
037500          CLOSE OPEN-TRLR-CSR                                             
037600     END-EXEC.                                                            
037700                                                                          
037800     EVALUATE TRUE                                                        
037810         WHEN SQLCODE = ZERO                                              
037820              CONTINUE                                                    
037900         WHEN SQLCODE = -904                                              
038000         WHEN SQLCODE = -911                                              
038100              CONTINUE                                                    
038200         WHEN SQLWARN0 NOT = SPACES                                       
038300         WHEN SQLCODE < ZERO                                              
038400         WHEN SQLCODE > ZERO                                              
038500              MOVE '9000-EOJ-ROUTINE               '                      
038600                                 TO PV-PARAGRAPH-NAME                     
038700              MOVE 'CLOSE THE LABEL_DATA_CURSOR     '                     
038800                                 TO PV-DB2-OPERATION-ATTEMPTED            
038900              PERFORM 9100-SQL-ABEND                                      
039200     END-EVALUATE.                                                        
039400                                                                          
040200                                                                          
040210*-----------------------------------------------------------------        
040220*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040230*  ERROR OR WARNING CODE OCCURS.                                          
040240*-----------------------------------------------------------------        
040300 9100-SQL-ABEND.                                                          
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
