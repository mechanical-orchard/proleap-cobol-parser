      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.   XLKUP090.                                                  
       AUTHOR.       SUBASH T.                                                  
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. SEP, 2014.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PROGRAM TO UPDATE PACK DESCRIPTION IN TPOPRPK TABLE             *        
      *WITH THE VALUE PRESENT IN XLT_PK TABLE IF THERE IS A DIFFERENCE *        
      *FOUND IN DESCRIPTION BETWEEN XLT_PK AND TPOPRPK TABLES          *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      ******************************************************************        
      *  COMMUNICATIONS AREA FOR DB2                                   *        
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 TABLE DECLARATION - TPOPRPK                               *        
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE TPOPRPK                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 TABLE DECLARATION - XLT_PK                                *        
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE XLT00007                                                
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    CURSOR TO SELECT SKU'S WHICH HAS DIFFERENCES IN SKU         *        
      *    DESCRIPTION BETWEEN XLT_PK AND TPOPRPK TABLE                *        
      ******************************************************************        
           EXEC SQL                                                             
              DECLARE PK_DESC_CSR CURSOR WITH HOLD FOR                          
                  SELECT                                                        
                         A.PK_DESC                                              
                        ,B.PO_NBR                                               
                        ,B.PK_SKU_NBR                                           
                  FROM                                                          
                         XLT_PK A                                               
                        ,TPOPRPK B                                              
                  WHERE                                                         
                         B.PK_SKU_NBR = A.PK_SKU_NBR                            
                     AND NOT UPPER(B.PREPK_DESC) = UPPER(A.PK_DESC)             
                     AND A.LAST_UPD_TMST > '2014-07-17-00.00.00.000000'         
                  ORDER BY B.PK_SKU_NBR                                         
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  WORK AREA FOR SQL ERROR ROUTINE 'DSNTIAR'                     *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                    PIC S9(04)  VALUE +0                   
                                                         COMP SYNC.             
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-SKU_DESC-CSR-SW PIC X(01)   VALUE 'N'.                 
               88  PS-END-OF-PK-DESC-CSR             VALUE 'Y'.                 
               88  PS-END-OF-PK-DESC-CSR-NO          VALUE 'N'.                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME               PIC X(08)   VALUE 'XLKUP090'.          
           05  PC-COMMIT-HOLD            PIC 9(3)    VALUE 100.                 
                                                                                
       01 PV-PROGRAM-VARIABLES.                                                 
           05  PV-ABEND-PARAGRAPH        PIC X(35)   VALUE SPACES.              
           05  PV-DB2-OPN-ATTEMPTED      PIC X(35)   VALUE SPACES.              
           05  PV-FETCH-CNT              PIC 9(13)   VALUE ZEROES.              
           05  PV-UPDATE-CNT             PIC 9(13)   VALUE ZEROES.              
           05  PV-UPD-COUNT-TOT          PIC 9(13)   VALUE ZEROES.              
           05  PV-COMMIT-CNT             PIC 9(13)   VALUE ZEROES.              
           05  PV-DISPLAY-COUNT          PIC Z(12)9.                            
                                                                                
       LINKAGE SECTION.                                                         
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM.  *        
      ******************************************************************        
       0000-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM 1000-INITIALIZE.                                             
           PERFORM 2000-PROCESS-PK-DESC-DIFF.                                   
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  PERFORMS INITIALIZATION.                                      *        
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           DISPLAY 'START OF PROGRAM : ' PC-PGM-NAME.                           
                                                                                
           INITIALIZE DCLTPOPRPK                                                
                      DCLXLT00007                                               
                      PV-PROGRAM-VARIABLES.                                     
                                                                                
           SET PS-END-OF-PK-DESC-CSR-NO TO TRUE.                                
                                                                                
      *                                                                         
      ******************************************************************        
      * PERFORMS MAIN PROCESSING                                       *        
      ******************************************************************        
       2000-PROCESS-PK-DESC-DIFF.                                               
                                                                                
           PERFORM 2100-OPEN-PK-DESC-DIFF-CSR.                                  
                                                                                
           PERFORM 2200-FETCH-PK-DESC-DIFF-CSR                                  
             UNTIL PS-END-OF-PK-DESC-CSR.                                       
                                                                                
           PERFORM 2300-CLOSE-PK-DESC-DIFF-CSR.                                 
                                                                                
      *                                                                         
      ******************************************************************        
      * OPENS PACK DESCRIPTION DIFERENCE CURSOR                        *        
      ******************************************************************        
       2100-OPEN-PK-DESC-DIFF-CSR.                                              
                                                                                
           EXEC SQL                                                             
                OPEN PK_DESC_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '2100-OPEN-PK-DESC-DIFF-CSR'                            
                                           TO PV-ABEND-PARAGRAPH                
                   MOVE 'OPEN PK_DESC_CSR'                                      
                                           TO PV-DB2-OPN-ATTEMPTED              
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      ******************************************************************        
      * FETCHES PACK DESCRIPTION DIFFERENCE CURSOR                     *        
      ******************************************************************        
       2200-FETCH-PK-DESC-DIFF-CSR.                                             
                                                                                
           INITIALIZE DCLTPOPRPK                                                
                      DCLXLT00007.                                              
                                                                                
           EXEC SQL                                                             
                FETCH PK_DESC_CSR                                               
                 INTO :XLT00007-PK-DESC                                         
                     ,:POPRPK-PO-NBR                                            
                     ,:POPRPK-PK-SKU-NBR                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
                   ADD 1                 TO PV-FETCH-CNT                        
                   PERFORM 3000-UPDATE-TPOPRPK-PK-DESC                          
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-PK-DESC-CSR                                    
                                         TO TRUE                                
               WHEN OTHER                                                       
                   MOVE '2200-FETCH-PK-DESC-DIFF-CSR'                           
                                         TO PV-ABEND-PARAGRAPH                  
                   MOVE 'OPEN PK_DESC_CSR'                                      
                                         TO PV-DB2-OPN-ATTEMPTED                
                 PERFORM 9100-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      ******************************************************************        
      * CLOSES PACK DESCRIPTION DIFFERENCE CURSOR                      *        
      ******************************************************************        
       2300-CLOSE-PK-DESC-DIFF-CSR.                                             
                                                                                
           EXEC SQL                                                             
                CLOSE PK_DESC_CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   IF PV-UPDATE-CNT > 0                                         
                     PERFORM 4000-COMMIT                                        
                   END-IF                                                       
               WHEN OTHER                                                       
                   MOVE '2300-CLOSE-PK-DESC-DIFF-CSR'                           
                                         TO PV-ABEND-PARAGRAPH                  
                   MOVE 'CLOSE PK_DESC_CSR'                                     
                                         TO PV-DB2-OPN-ATTEMPTED                
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      ******************************************************************        
      * UPDATES SKU DESCRIPTION IN TPOPRPK WITH THAT OF XLT_PK         *        
      ******************************************************************        
       3000-UPDATE-TPOPRPK-PK-DESC.                                             
                                                                                
           EXEC SQL                                                             
               UPDATE TPOPRPK                                                   
                 SET  PREPK_DESC   = :XLT00007-PK-DESC                          
               WHERE                                                            
                      PO_NBR       = :POPRPK-PO-NBR                             
                 AND  PK_SKU_NBR   = :POPRPK-PK-SKU-NBR                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
                   ADD  1                TO  PV-UPDATE-CNT                      
                                             PV-UPD-COUNT-TOT                   
                   DISPLAY 'PO NUMBER       :' POPRPK-PO-NBR                    
                   DISPLAY 'PACK SKU NUMBER :' POPRPK-PK-SKU-NBR                
                   IF PV-UPDATE-CNT = PC-COMMIT-HOLD                            
                      PERFORM 4000-COMMIT                                       
                   END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   MOVE '3000-UPDATE-TPOPRPK-PK-DESC'                           
                                         TO PV-ABEND-PARAGRAPH                  
                   MOVE 'UDPATE TO TPOPRPK'                                     
                                         TO PV-DB2-OPN-ATTEMPTED                
                 PERFORM 9100-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *                                                                         
      ******************************************************************        
      * TAKES COMMIT                                                   *        
      ******************************************************************        
       4000-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
                   MOVE 0                TO PV-UPDATE-CNT                       
                   ADD +1                TO PV-COMMIT-CNT                       
               WHEN OTHER                                                       
                   MOVE '4000-COMMIT'    TO PV-ABEND-PARAGRAPH                  
                   MOVE 'COMMIT'         TO PV-DB2-OPN-ATTEMPTED                
                 PERFORM 9100-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      ******************************************************************        
      * DISPLAYS PROGRAM STATISTICS                                    *        
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '*************************************************'.         
           DISPLAY '**             XLKUP090 SUMMARY                **'.         
           DISPLAY '*************************************************'.         
           MOVE  PV-FETCH-CNT              TO PV-DISPLAY-COUNT.                 
           DISPLAY 'TOTAL RECORDS FETCHED  = ' PV-DISPLAY-COUNT.                
           MOVE  PV-UPD-COUNT-TOT          TO PV-DISPLAY-COUNT.                 
           DISPLAY 'TOTAL RECORDS UPDATED  = ' PV-DISPLAY-COUNT.                
           MOVE  PV-COMMIT-CNT             TO PV-DISPLAY-COUNT.                 
           DISPLAY 'TOTAL COMMITS TAKEN    = ' PV-DISPLAY-COUNT.                
           DISPLAY '*************************************************'.         
           DISPLAY 'END OF PROGRAM   : ' PC-PGM-NAME.                           
                                                                                
       EJECT                                                                    
      *                                                                         
      ******************************************************************        
      * PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL    *        
      * ERROR OR WARNING CODE OCCURS.                                  *        
      ******************************************************************        
       9100-SQL-ABEND.                                                          
                                                                                
           DISPLAY '9100-SQL-ABEND'.                                            
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME.                            
           DISPLAY '** PARAGRAPH ** = ' PV-ABEND-PARAGRAPH.                     
           DISPLAY '** OPERATION ** = ' PV-DB2-OPN-ATTEMPTED.                   
           COPY DPPD004.                                                12410000
           MOVE +4013                      TO ABEND-CODE.               12570000
           CALL 'ILBOABN0' USING ABEND-CODE.                            12580000
                                                                                
