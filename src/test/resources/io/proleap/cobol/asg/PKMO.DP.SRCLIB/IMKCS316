      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    IMKCS316.                                                 
       AUTHOR.        JACK KRAMER.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  NOVEMBER 2005.                                            
       DATE-COMPILED.                                                           
      *----------------------------------------------------------------*        
      *    I316 - UPDATE BRAND VENDOR RELATIONSHIP                     *        
      *                                                                *        
      *    THIS PROGRAM ALLOWS THE USER TO UPDATE A BRAND VENDOR       *        
      *    RELATIONSHIP.                                               *        
      *----------------------------------------------------------------*        
      *    CHANGE LOG:                                                 *        
      *    DATE  01/16/2023                                            *        
      *               - CHANGE CALL OF 'DPKCS030' TO MAKE THE CALL     *        
      *                 DYNAMIC VS STATIC                              *        
      *        MADE BY: JEAN KURK               WR/IR #: CHG0276953    *        
      *----------------------------------------------------------------*        
                                                                                
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DK-DB2-KEYS.                                                         
           05  DK-DEPT                        PIC S9(4)V  COMP-3.               
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'IM316A  '.          
           05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'IMKM316 '.          
           05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'IMKCS316'.          
           05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3                   
                                                    COMP.                       
           05  PC-BV-ADD-FORMAT               PIC  X(01)  VALUE '1'.            
           05  PC-LIST-FORMAT                 PIC  X(01)  VALUE '2'.            
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00007      PIC  9(05)  VALUE  00007.               
               10  PC-TSYMSG-00009      PIC  9(05)  VALUE  00009.               
               10  PC-TSYMSG-00046      PIC  9(05)  VALUE  00046.               
               10  PC-TSYMSG-00069      PIC  9(05)  VALUE  00069.               
               10  PC-TSYMSG-00070      PIC  9(05)  VALUE  00070.               
               10  PC-TSYMSG-00115      PIC  9(05)  VALUE  00115.               
               10  PC-TSYMSG-00148      PIC  9(05)  VALUE  00148.               
               10  PC-TSYMSG-01657      PIC  9(05)  VALUE  01657.               
               10  PC-TSYMSG-01305      PIC  9(05)  VALUE  01305.               
               10  PC-TSYMSG-02429      PIC  9(05)  VALUE  02429.               
               10  PC-TSYMSG-03131      PIC  9(05)  VALUE  03131.               
               10  PC-TSYMSG-03445      PIC  9(05)  VALUE  03445.               
               10  PC-TSYMSG-03455      PIC  9(05)  VALUE  03455.               
               10  PC-TSYMSG-03457      PIC  9(05)  VALUE  03457.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.                 
               88  PS-ERROR                         VALUE  'Y'.                 
               88  PS-NO-ERROR                      VALUE  'N'.                 
           05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-COMMAREA-VALID                       VALUE 'Y'.           
               88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.           
           05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-LIST-END                             VALUE 'Y'.           
               88  PS-NO-LIST-END                          VALUE 'N'.           
           05  PS-RLTNSHP-FOUND-SW            PIC X(01)    VALUE 'N'.           
               88  PS-RLTNSHP-FOUND                        VALUE 'Y'.           
               88  PS-RLTNSHP-NOT-FOUND                    VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-BEG-DATE                    PIC X(10).                        
           05  PV-END-DATE                    PIC X(10).                        
           05  PV-CURRENT-DATE                PIC X(10).                        
           05  PV-ISO-DATE REDEFINES PV-CURRENT-DATE.                           
               10  PV-ISO-CENTURY             PIC X(02).                        
               10  PV-ISO-YEAR                PIC X(02).                        
               10  PV-ISO-DASH1               PIC X(01).                        
               10  PV-ISO-MONTH               PIC X(02).                        
               10  PV-ISO-DASH2               PIC X(01).                        
               10  PV-ISO-DAY                 PIC X(02).                        
           05  PV-MMDDYY-DATE.                                                  
               10  PV-MMDDYY-MONTH            PIC X(02).                        
               10  PV-MMDDYY-DAY              PIC X(02).                        
               10  PV-MMDDYY-YEAR             PIC X(02).                        
           05  PV-NULL-IND                    PIC S9(04)   COMP.                
           05  PV-CICS-RESP             PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
      *----------------------------------------------------------------*        
      *    THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT   *        
      *    SUB-ROUTINE (DPKUT100).                                     *        
      *----------------------------------------------------------------*        
           COPY DPWS010I.                                                       
                                                                                
      *                                                                         
      *  COPY BOOK FOR THE CALENDAR ROUTINE                                     
      *                                                                         
           COPY DPWS500.                                                        
                                                                                
      **--------------------------------------------------------------**        
      ** THE LAYOUT FOR THE TEMPORARY STORAGE QUEUE CONTAINING        **        
      ** SELECTED LIST ITEMS TO BE PROCESSED BY THIS PROGRAM.         **        
      **--------------------------------------------------------------**        
           COPY IMWS009.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    MAP LAYOUT                                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY IMKM316.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    FUNCTION KEYS COPYBOOK                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *****************************************************************         
      * PARAMETERS FOR CALLING CICS ARCHITECTURE API                            
      *****************************************************************         
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
                                                                                
      *****************************************************************         
      *    STANDARD COMMAREA.                                                   
      *****************************************************************         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *****************************************************************         
      *    INTER-APPLICATION COMMAREA.                                          
      *****************************************************************         
                                                                                
               10  INTER-APPL-COMM-AREA PIC X(200).                             
           COPY IMWS008.                                                        
      *****************************************************************         
      *    SPECIFIC COMMMAREA FOR IMKCS311.                                     
      *****************************************************************         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-CENTURY                 PIC  X(02).                  
                   15  ASC-CURRENT-DATE            PIC  X(10).                  
                   15  ASC-DISPLAY-DATE            PIC  X(06).                  
                   15  ASC-DEPT                    PIC  X(03).                  
                   15  ASC-DEPT-N REDEFINES ASC-DEPT                            
                                                   PIC  9(03).                  
                   15  ASC-DEPT-DESC               PIC  X(25).                  
                   15  ASC-BRAND                   PIC  X(20).                  
                   15  ASC-VENDOR                  PIC  X(30).                  
                   15  ASC-INITIAL-ISO-BEG-DATE    PIC  X(10).                  
                   15  ASC-INITIAL-ISO-END-DATE    PIC  X(10).                  
                   15  ASC-INITIAL-BEG-DATE        PIC  X(06).                  
                   15  ASC-INITIAL-END-DATE        PIC  X(06).                  
                   15  ASC-BEG-DATE                PIC  X(06).                  
                   15  ASC-END-DATE                PIC  X(06).                  
                   15  ASC-RLTNSHP-ID              PIC S9(09)  COMP.            
                   15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)           
                                                               COMP-3.          
                   15  ASC-SEL-QUEUE-NAME.                                      
                       20  ASC-SEL-TERM-ID                 PIC  X(04).          
                       20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-SEL-SEQ                     PIC  9(01).          
                   15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).          
                       88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
                   15  ASC-BEG-DATE-PAST-IND               PIC  X(01).          
                       88  ASC-BEG-DATE-PAST                 VALUE 'Y'.         
                       88  ASC-BEG-DATE-NOT-PAST             VALUE 'N'.         
                   15  ASC-END-DATE-PAST-IND               PIC  X(01).          
                       88  ASC-END-DATE-PAST                 VALUE 'Y'.         
                       88  ASC-END-DATE-NOT-PAST             VALUE 'N'.         
                   15  ASC-ACTION-SW                       PIC  X(01).     CL**1
                       88  ASC-DELETE-REQUESTED              VALUE 'D'.    CL**1
                   15  ASC-RLTNSHP-DELETE-SW               PIC  X(01).     CL**1
                       88  ASC-RLTNSHP-DELETED               VALUE 'D'.    CL**1
                   15  ASC-SEL-SUB               PIC S9(04)  COMP.              
                   15  ASC-SEL-ITEM              PIC S9(04)  COMP.              
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSING COPYBOOK.                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS013.                                                        
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR BRAND VENDOR RELATIONSHIP TABLE               *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE IMT00017                                                
           END-EXEC.                                                            
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC  X(01).                       
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
      **                                                             **         
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
      *****************************************************************         
       0000-MAIN-MODULE.                                                        
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF IM316AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING                                      
                                                                                
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
       0001-CALL-CICS-ARCH-API.                                                 
      *    CALL 'DPKCS030' USING DFHEIBLK                                       
           CALL DP930-CICS-ARCH-API                                             
                           USING DFHEIBLK                                       
                                 DFHCOMMAREA                                    
                                 DP030-CICS-API-FIELDS                          
                                 DP020-STANDARD-COMMAREA                        
                                 IM316AI.                                       
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE 'BEFORE 0000-MAIN-MODULE'                                   
                                         TO DP013-PARAGRAPH                     
      *        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEX        
      *             'T LINE'             TO DP013-MESSAGE-TEXT (2)              
               MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
      -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                           
                                         TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   EXEC SQL                                                     
                       SET  :PV-CURRENT-DATE = CURRENT DATE                     
                   END-EXEC                                                     
                   MOVE PV-CURRENT-DATE   TO ASC-CURRENT-DATE                   
                   MOVE PV-ISO-YEAR       TO PV-MMDDYY-YEAR                     
                   MOVE PV-ISO-MONTH      TO PV-MMDDYY-MONTH                    
                   MOVE PV-ISO-DAY        TO PV-MMDDYY-DAY                      
                   MOVE PV-ISO-CENTURY    TO ASC-CENTURY                        
                   MOVE PV-MMDDYY-DATE    TO ASC-DISPLAY-DATE                   
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                         
                   IF  PS-COMMAREA-NOT-VALID                                    
                       SET DP020-NEXT-ACT-APPL-ERROR                            
                                     TO  TRUE                                   
                   ELSE                                                         
                       PERFORM 4000-BUILD-INITIAL-PANEL                         
                   END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM 2000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   MOVE SPACE            TO ASC-ACTION-SW                  CL**1
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND                                        
                       DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
                                         TO DP013-MESSAGE-TEXT(1)               
                   MOVE DP020-NEXT-APPL-ACTIVITY                                
                                         TO DP013-MESSAGE-TEXT(2)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
      * IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
      *----------------------------------------------------------------*        
                                                                                
       1100-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           SET PS-COMMAREA-VALID              TO TRUE.                          
           MOVE IM008-DEPT                    TO ASC-DEPT.                      
           MOVE IM008-DEPT-DESC               TO ASC-DEPT-DESC.                 
           EVALUATE DP020-INT-APPL-FORMAT-IND                                   
               WHEN PC-BV-ADD-FORMAT                                            
                   MOVE IM008-BRAND       TO ASC-BRAND                          
                   MOVE IM008-VENDOR      TO ASC-VENDOR                         
                   MOVE IM008-RLTNSHP-ID  TO ASC-RLTNSHP-ID                     
               WHEN PC-LIST-FORMAT                                              
                   SET ASC-USE-SELECTION-QUEUE TO TRUE                          
                   MOVE IM008-NUMBER-OF-TS-ITEMS                                
                                     TO ASC-NUMBER-OF-TS-ITEMS                  
                   MOVE IM008-SEL-QUEUE-NAME                                    
                                     TO ASC-SEL-QUEUE-NAME                      
                   PERFORM 1120-PREPARE-LIST-ENTRY                              
               WHEN OTHER                                                       
                   MOVE PC-TSYMSG-00148  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-FATAL TO TRUE                                  
                   SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *        
      *----------------------------------------------------------------*        
                                                                                
       1120-PREPARE-LIST-ENTRY.                                                 
                                                                                
           MOVE IM009-MAX-ITEMS TO ASC-SEL-SUB.                                 
           MOVE ZERO            TO ASC-SEL-ITEM.                                
           PERFORM 2140-GET-NEXT-BV.                                            
      *----------------------------------------------------------------*        
      * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
      * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
      * EDITS.                                                         *        
      *----------------------------------------------------------------*        
       2000-PROCESS-PANEL.                                                      
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                                                                                
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
                   IF  ASC-USE-SELECTION-QUEUE                                  
                       MOVE SPACE                TO ASC-ACTION-SW               
                       PERFORM 2140-GET-NEXT-BV                                 
                       IF PS-LIST-END                                           
                           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
                           SET PS-NO-LIST-END TO TRUE                           
                       ELSE                                                     
                           PERFORM 4000-BUILD-INITIAL-PANEL                     
                       END-IF                                                   
                       PERFORM 3200-RESET-ATTRIBUTES                            
                   ELSE                                                         
                       MOVE PC-TSYMSG-00070                                     
                                        TO DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
                   IF  ASC-USE-SELECTION-QUEUE                                  
                       MOVE SPACE                TO ASC-ACTION-SW               
                       PERFORM 2150-GET-PREV-BV                                 
                       IF PS-LIST-END                                           
                           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
                           SET PS-NO-LIST-END TO TRUE                           
                       ELSE                                                     
                           PERFORM 4000-BUILD-INITIAL-PANEL                     
                       END-IF                                                   
                       PERFORM 3200-RESET-ATTRIBUTES                            
                   ELSE                                                         
                       MOVE PC-TSYMSG-00069                                     
                                        TO DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   IF  PS-NO-ERROR                                              
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                       SET DP030-CONTINUE-GO TO TRUE                            
                   ELSE                                                         
                       SET DP030-OVERRIDE-GO TO TRUE                            
                   END-IF                                                       
           END-EVALUATE.                                                        
      ******************************************************************        
      ** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
      ** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
      ** FROM THE CICS ARCHITECTURE API.                              **        
      ******************************************************************        
       2100-CHECK-FUNCTION-KEY.                                                 
           EVALUATE TRUE                                                        
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
                 IF ASC-RLTNSHP-DELETED                                         
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3200-RESET-ATTRIBUTES                                
                 ELSE                                                           
                   IF ASC-BEG-DATE NOT = ASC-END-DATE                           
                       MOVE SPACE                TO ASC-ACTION-SW               
                   END-IF                                                       
                   IF ASC-DELETE-REQUESTED                                      
                       PERFORM 5500-DELETE-RELATIONSHIP                         
                       IF  ASC-USE-SELECTION-QUEUE                              
                           PERFORM 5550-UPDATE-SELECTION-QUEUE                  
                       END-IF                                                   
                       SET ASC-RLTNSHP-DELETED      TO TRUE                     
                       SET DP020-MSG-INFORMATIONAL  TO TRUE                     
                       MOVE PC-TSYMSG-00115         TO DP020-MSG-NUMBER         
                       MOVE -1                      TO ABRANDL                  
                   ELSE                                                         
*                      IF ASC-END-DATE-PAST                                     
*                          SET DP020-MSG-INFORMATIONAL  TO TRUE                 
*                          MOVE PC-TSYMSG-03455     TO DP020-MSG-NUMBER         
*                          MOVE -1                      TO AENDDTL              
*                      ELSE                                                     
                           IF ASC-BEG-DATE NOT = ASC-INITIAL-BEG-DATE           
                           OR ASC-END-DATE NOT = ASC-INITIAL-END-DATE           
                               IF ASC-BEG-DATE = ASC-END-DATE                   
                                   SET ASC-DELETE-REQUESTED                     
                                       PS-ERROR                                 
                                       DP030-OVERRIDE-GO                20010002
                                       DP020-MSG-WARNING TO TRUE                
      *                    -- PRESS F15 AGAIN TO CONFIRM DELETE --              
                                   MOVE PC-TSYMSG-03457                         
                                                    TO DP020-MSG-NUMBER         
                               ELSE                                             
                                   PERFORM 5300-UPDATE-RELATIONSHIP             
      *            -- UPDATE SUCCESSFUL --                                      
                                   MOVE PC-TSYMSG-01305                         
                                                     TO DP020-MSG-NUMBER        
                                   SET DP020-MSG-INFORMATIONAL                  
                                                     TO TRUE                    
                               END-IF                                           
                           ELSE                                                 
      *            -- DATA NOT CHANGED - NO UPDATE PERFORMED --                 
                               MOVE PC-TSYMSG-00046  TO DP020-MSG-NUMBER        
                               SET DP020-MSG-WARNING TO TRUE                    
                           END-IF                                               
*                      END-IF                                                   
                   END-IF                                                       
                 END-IF                                                         
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   MOVE SPACE            TO ASC-ACTION-SW                  CL**1
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    GET THE NEXT MESSAGE IF A LIST OF MESSAGES IS BEING         *        
      *    PROCESSED.                                                  *        
      *----------------------------------------------------------------*        
                                                                                
       2140-GET-NEXT-BV.                                                        
                                                                                
           ADD +1                       TO ASC-SEL-SUB.                         
           IF  ASC-SEL-SUB > IM009-MAX-ITEMS                                    
               ADD +1                   TO ASC-SEL-ITEM                         
               MOVE +1                  TO ASC-SEL-SUB                          
           END-IF.                                                              
           IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
               PERFORM 9100-READ-SEL-QUEUE                                      
               IF  IM009-ITEM (ASC-SEL-SUB) > ZERO                              
                   MOVE IM009-BRAND (ASC-SEL-SUB)   TO ASC-BRAND                
                   MOVE IM009-VENDOR (ASC-SEL-SUB)  TO ASC-VENDOR               
                   MOVE IM009-RLTNSHP-ID(ASC-SEL-SUB) TO ASC-RLTNSHP-ID         
                   SET IM009-INQ (ASC-SEL-SUB) TO TRUE                          
                   PERFORM 9200-REWRITE-SEL-QUEUE                               
               ELSE                                                             
                   SUBTRACT +1 FROM ASC-SEL-SUB                                 
                   MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                     
                   SET DP020-MSG-INFORMATIONAL TO TRUE                          
                   SET PS-LIST-END             TO TRUE                          
               END-IF                                                           
           ELSE                                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE IM009-MAX-ITEMS     TO ASC-SEL-SUB                          
               MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER                     
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
               SET PS-LIST-END             TO TRUE                              
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    GET THE PREVIOUS MESSAGE IF A LIST OF MESSAGES IS BEING     *        
      *    PROCESSED.                                                  *        
      *----------------------------------------------------------------*        
                                                                                
       2150-GET-PREV-BV.                                                        
                                                                                
           SUBTRACT +1 FROM ASC-SEL-SUB.                                        
           IF  ASC-SEL-SUB < +1                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE IM009-MAX-ITEMS     TO ASC-SEL-SUB                          
           END-IF.                                                              
                                                                                
           IF  ASC-SEL-ITEM > ZERO                                              
               PERFORM 9100-READ-SEL-QUEUE                                      
               MOVE IM009-BRAND (ASC-SEL-SUB)   TO ASC-BRAND                    
               MOVE IM009-VENDOR (ASC-SEL-SUB)  TO ASC-VENDOR                   
               MOVE IM009-RLTNSHP-ID(ASC-SEL-SUB) TO ASC-RLTNSHP-ID             
           ELSE                                                                 
               MOVE +1                  TO ASC-SEL-ITEM                         
                                           ASC-SEL-SUB                          
               MOVE PC-TSYMSG-00069     TO DP020-MSG-NUMBER                     
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
               SET PS-LIST-END             TO TRUE                              
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
      * THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
      * APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
      *----------------------------------------------------------------*        
                                                                                
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
           IF  ABEGDTL > ZERO                                                   
               MOVE ABEGDTI              TO ASC-BEG-DATE                        
           ELSE                                                                 
               IF  ABEGDTF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-BEG-DATE                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  AENDDTL > ZERO                                                   
               MOVE AENDDTI              TO ASC-END-DATE                        
           ELSE                                                                 
               IF  AENDDTF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-END-DATE                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON EACH FIELD ENTERED BY THE USER.  IF AN ERROR  *        
      * IS FOUND THE CURSOR IS POSITIONED AT THAT FIELD AND AN         *        
      * APPROPRIATE ERROR MESSAGE IS DISPLAYED.                        *        
      *----------------------------------------------------------------*        
                                                                                
       3000-EDIT-DATA-IN-COMMAREA.                                              
           PERFORM 3200-RESET-ATTRIBUTES.                                       
                                                                                
           IF ASC-BEG-DATE NOT = ASC-INITIAL-BEG-DATE                           
               IF ASC-BEG-DATE = SPACES                                         
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-00007      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                             TO ABEGDTA                         
                   MOVE DP015-RED            TO ABEGDTC                         
                   MOVE DP015-REVERSE        TO ABEGDTH                         
                   MOVE -1                   TO ABEGDTL                         
               ELSE                                                             
                   PERFORM 3020-VALIDATE-BEGIN-DATE                             
               END-IF                                                           
           END-IF.                                                              
           IF  PS-NO-ERROR                                                      
               IF ASC-BEG-DATE NOT = ASC-INITIAL-BEG-DATE                       
               OR ASC-END-DATE NOT = ASC-INITIAL-END-DATE                       
                   IF ASC-END-DATE NOT = SPACES                                 
                       PERFORM 3030-VALIDATE-END-DATE                           
                   ELSE                                                         
                       MOVE SPACES       TO PV-END-DATE                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  PS-NO-ERROR                                                      
               MOVE -1                   TO AENDDTL                             
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * VALIDATE THAT THE BEGIN DATE IS VALID AND THAT IT IS GREATER   *        
      * THAN CURRENT DATE.                                             *        
      *----------------------------------------------------------------*        
       3020-VALIDATE-BEGIN-DATE.                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           MOVE SPACES                    TO PV-BEG-DATE.                       
           MOVE ASC-BEG-DATE              TO  DPG52-LK-DATE-INPUT.              
           MOVE ASC-DISPLAY-DATE          TO  DPG53-LK-CMPR-DATE-INPUT.         
           PERFORM 3100-COMPARE-DATES.                                          
           EVALUATE TRUE                                                        
               WHEN DPG54-NO-ERROR                                              
                   IF DPG55-DAYS-DIFFERENCE IS NEGATIVE                         
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL                                      
                           DP030-SET-CURSOR-APPL-1 TO TRUE                      
                       MOVE PC-TSYMSG-03131      TO DP020-MSG-NUMBER            
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                                 TO ABEGDTA                     
                       MOVE DP015-RED            TO ABEGDTC                     
                       MOVE DP015-REVERSE        TO ABEGDTH                     
                       MOVE -1                   TO ABEGDTL                     
                   ELSE                                                         
                       MOVE DPG55-DB2-ISO-DATE-FMT                              
                                                 TO PV-BEG-DATE                 
                   END-IF                                                       
               WHEN OTHER                                                       
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-00009      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                             TO ABEGDTA                         
                   MOVE DP015-RED            TO ABEGDTC                         
                   MOVE DP015-REVERSE        TO ABEGDTH                         
                   MOVE -1                   TO ABEGDTL                         
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * VALIDATE THAT THE END DATE IS VALID AND THAT IT IS GREATER     *        
      * THAN THE BEGIN DATE (IF END DATE HAS BEEN ENTERED)             *        
      *----------------------------------------------------------------*        
       3030-VALIDATE-END-DATE.                                                  
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           MOVE SPACES                    TO PV-END-DATE.                       
           MOVE ASC-END-DATE              TO  DPG52-LK-DATE-INPUT.              
           IF ASC-BEG-DATE > ASC-DISPLAY-DATE                                   
               MOVE ASC-BEG-DATE          TO  DPG53-LK-CMPR-DATE-INPUT          
           ELSE                                                                 
               MOVE ASC-DISPLAY-DATE      TO  DPG53-LK-CMPR-DATE-INPUT          
           END-IF.                                                              
           PERFORM 3100-COMPARE-DATES.                                          
           EVALUATE TRUE                                                        
               WHEN DPG54-NO-ERROR                                              
                   IF DPG55-DAYS-DIFFERENCE IS NEGATIVE                         
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL                                      
                           DP030-SET-CURSOR-APPL-1 TO TRUE                      
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                                 TO AENDDTA                     
                       MOVE DP015-RED            TO AENDDTC                     
                       MOVE DP015-REVERSE        TO AENDDTH                     
                       MOVE -1                   TO AENDDTL                     
                       IF ASC-BEG-DATE > ASC-DISPLAY-DATE                       
                           MOVE PC-TSYMSG-03445  TO DP020-MSG-NUMBER            
                       ELSE                                                     
                           MOVE PC-TSYMSG-02429  TO DP020-MSG-NUMBER            
                       END-IF                                                   
                   ELSE                                                         
                       MOVE DPG55-DB2-ISO-DATE-FMT                              
                                                 TO PV-END-DATE                 
                   END-IF                                                       
               WHEN OTHER                                                       
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-00009      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                             TO AENDDTA                         
                   MOVE DP015-RED            TO AENDDTC                         
                   MOVE DP015-REVERSE        TO AENDDTH                         
                   MOVE -1                   TO AENDDTL                         
           END-EVALUATE.                                                        
                                                                                
       3100-COMPARE-DATES.                                                      
           SET  DPG51-ACTUAL-CALENDAR-ONLY                                      
                DPG52-LK-DTE-GREG                                               
                DPG53-LK-CMPR-DTE-GREG    TO  TRUE.                             
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
                     DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
                                                                                
       3200-RESET-ATTRIBUTES.                                                   
           IF ASC-BEG-DATE-PAST                                                 
              OR ASC-RLTNSHP-DELETED                                            
               MOVE DP015-ASK-NOR-OFF        TO ABEGDTA                         
               MOVE DP015-BLUE               TO ABEGDTC                         
               MOVE DP015-HL-OFF             TO ABEGDTH                         
           ELSE                                                                 
               MOVE DP015-UNP-ALP-NOR-OFF    TO ABEGDTA                         
               MOVE DP015-GREEN              TO ABEGDTC                         
               MOVE DP015-UNDERLINE          TO ABEGDTH                         
           END-IF.                                                              
           IF ASC-END-DATE-PAST                                                 
              OR ASC-RLTNSHP-DELETED                                            
               MOVE DP015-ASK-NOR-OFF        TO AENDDTA                         
               MOVE DP015-BLUE               TO AENDDTC                         
               MOVE DP015-HL-OFF             TO AENDDTH                         
           ELSE                                                                 
               MOVE DP015-UNP-ALP-NOR-OFF    TO AENDDTA                         
               MOVE DP015-GREEN              TO AENDDTC                         
               MOVE DP015-UNDERLINE          TO AENDDTH                         
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * GET THE NEXT AVAILABLE MESSAGE NUMBER, MOVE IT TO THE SCREEN   *        
      * AND POSITION THE CURSOR FOR THE INITIAL PANEL.                 *        
      *----------------------------------------------------------------*        
                                                                                
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           SET DP030-SET-CURSOR-APPL-1      TO TRUE.                            
           PERFORM 5000-READ-RELATIONSHIP.                                      
           IF PS-RLTNSHP-FOUND                                                  
               MOVE SPACE         TO ASC-RLTNSHP-DELETE-SW                      
               IF IMT00017-EFF-BEG-DTE LESS THAN ASC-CURRENT-DATE               
                   SET ASC-BEG-DATE-PAST  TO TRUE                               
               ELSE                                                             
                   SET ASC-BEG-DATE-NOT-PAST  TO TRUE                           
               END-IF                                                           
      *        IF IMT00017-EFF-END-DTE LESS THAN ASC-CURRENT-DATE AND           
      *           IMT00017-EFF-END-DTE NOT = SPACES                             
      *            SET ASC-END-DATE-PAST  TO TRUE                               
      *        ELSE                                                             
                   SET ASC-END-DATE-NOT-PAST  TO TRUE                           
      *        END-IF                                                           
               MOVE IMT00017-EFF-BEG-DTE TO PV-ISO-DATE                         
                                         ASC-INITIAL-ISO-BEG-DATE               
               MOVE PV-ISO-MONTH      TO PV-MMDDYY-MONTH                        
               MOVE PV-ISO-DAY        TO PV-MMDDYY-DAY                          
               MOVE PV-ISO-YEAR       TO PV-MMDDYY-YEAR                         
               MOVE PV-MMDDYY-DATE    TO ASC-BEG-DATE                           
                                         ASC-INITIAL-BEG-DATE                   
               MOVE IMT00017-EFF-END-DTE TO PV-ISO-DATE                         
                                         ASC-INITIAL-ISO-END-DATE               
               MOVE PV-ISO-MONTH      TO PV-MMDDYY-MONTH                        
               MOVE PV-ISO-DAY        TO PV-MMDDYY-DAY                          
               MOVE PV-ISO-YEAR       TO PV-MMDDYY-YEAR                         
               MOVE PV-MMDDYY-DATE    TO ASC-END-DATE                           
                                             ASC-INITIAL-END-DATE               
           ELSE                                                                 
               SET ASC-RLTNSHP-DELETED      TO TRUE                             
               SET DP020-MSG-WARNING        TO TRUE                             
               MOVE PC-TSYMSG-01657         TO DP020-MSG-NUMBER                 
           END-IF.                                                              
      *    IF ASC-END-DATE-PAST                                                 
      *        SET DP020-MSG-INFORMATIONAL  TO TRUE                             
      *        MOVE PC-TSYMSG-03455         TO DP020-MSG-NUMBER                 
      *    END-IF.                                                              
           MOVE -1                          TO AENDDTL.                         
           PERFORM 3200-RESET-ATTRIBUTES.                                       
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
      *----------------------------------------------------------------*        
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
           MOVE ASC-DEPT                 TO ADEPTO.                             
           MOVE ASC-DEPT-DESC            TO ADESCO.                             
           MOVE ASC-BRAND                TO ABRANDO.                            
           MOVE ASC-VENDOR               TO AVENDRO.                            
           MOVE ASC-BEG-DATE             TO ABEGDTO.                            
           MOVE ASC-END-DATE             TO AENDDTO.                            
                                                                                
      *----------------------------------------------------------------*        
      * READ A ROW FROM IMT_DEPT_BRND_VND                              *        
      *----------------------------------------------------------------*        
       5000-READ-RELATIONSHIP.                                                  
           SET PS-RLTNSHP-NOT-FOUND    TO TRUE.                                 
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = +100))                                   
               EXEC SQL                                                         
                   SELECT EFF_BEG_DTE                                           
                         ,EFF_END_DTE                                           
                     INTO :IMT00017-EFF-BEG-DTE                                 
                         ,:IMT00017-EFF-END-DTE :PV-NULL-IND                    
                    FROM IMT_DEPT_BRND_VND                                      
                   WHERE DEPT_BRND_VND_ID = :ASC-RLTNSHP-ID                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-RLTNSHP-FOUND                                     
                           ASC-RLTNSHP-DELETED TO TRUE                          
                   WHEN SQLCODE = +100                                          
                       MOVE SPACES TO ASC-BEG-DATE                              
                                      ASC-END-DATE                              
                                      ASC-INITIAL-BEG-DATE                      
                                      ASC-INITIAL-END-DATE                      
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5000-READ-RELATIONSHIP'                            
                                         TO DP013-PARAGRAPH                     
                       MOVE 'READ A ROW FROM THE BRAND VENDOR TABLE'            
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'IMT_DEPT_BRND_VND'                                 
                                         TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5000-READ-RELATIONSHIP'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED READING A BRAND/VENDOR'               
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'IMT_DEPT_BRND_VND'  TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
           IF PV-NULL-IND < 0                                                   
               MOVE SPACES         TO IMT00017-EFF-END-DTE                      
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * CREATE THE BRAND VENDOR RELATIONSHIP                           *        
      *----------------------------------------------------------------*        
       5300-UPDATE-RELATIONSHIP.                                                
           INITIALIZE DCLIMT00017.                                              
           IF ASC-BEG-DATE NOT = ASC-INITIAL-BEG-DATE                           
               MOVE PV-BEG-DATE      TO IMT00017-EFF-BEG-DTE                    
           ELSE                                                                 
               MOVE ASC-INITIAL-ISO-BEG-DATE TO IMT00017-EFF-BEG-DTE            
           END-IF.                                                              
           IF ASC-END-DATE NOT = ASC-INITIAL-END-DATE   OR                      
              ASC-END-DATE = SPACES                                             
               IF PV-END-DATE NOT = SPACES                                      
                   MOVE PV-END-DATE      TO IMT00017-EFF-END-DTE                
                   MOVE +1               TO PV-NULL-IND                         
               ELSE                                                             
                   MOVE -1                   TO PV-NULL-IND                     
               END-IF                                                           
           ELSE                                                                 
               MOVE ASC-INITIAL-ISO-END-DATE TO IMT00017-EFF-END-DTE            
           END-IF.                                                              
           PERFORM 5400-UPDATE-RELATIONSHIP.                                    
           IF  ASC-USE-SELECTION-QUEUE                                          
               PERFORM 5350-UPDATE-SELECTION-QUEUE                              
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * RECORD IN THE SELECTION TEMP STORAGE QUEUE THE FACT THAT THIS  *        
      * OBJECT WAS UPDATED.                                            *        
      *----------------------------------------------------------------*        
       5350-UPDATE-SELECTION-QUEUE.                                             
           PERFORM 9100-READ-SEL-QUEUE.                                         
           SET IM009-UPD (ASC-SEL-SUB) TO TRUE.                                 
           PERFORM 9200-REWRITE-SEL-QUEUE.                                      
      *----------------------------------------------------------------*        
      * UPDATE A ROW IN IMT_DEPT_BRND_VND                              *        
      *----------------------------------------------------------------*        
       5400-UPDATE-RELATIONSHIP.                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
                   UPDATE IMT_DEPT_BRND_VND                                     
                       SET EFF_BEG_DTE = :IMT00017-EFF-BEG-DTE                  
                          ,EFF_END_DTE =                                        
                                 :IMT00017-EFF-END-DTE :PV-NULL-IND             
                   WHERE DEPT_BRND_VND_ID = :ASC-RLTNSHP-ID                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5400-UPDATE-RELATIONSHIP'                          
                                         TO DP013-PARAGRAPH                     
                       MOVE 'UPDATE A ROW IN THE BRAND VENDOR TABLE'            
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'IMT_DEPT_BRND_VND'                                 
                                         TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5400-UPDATE-RELATIONSHIP'                                  
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED UPDATING A BRAND/VENDOR'              
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'IMT_DEPT_BRND_VND'  TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * DELETE A ROW IN IMT_DEPT_BRND_VND                              *        
      *----------------------------------------------------------------*        
       5500-DELETE-RELATIONSHIP.                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
                   DELETE FROM IMT_DEPT_BRND_VND                                
                   WHERE (DEPT_BRND_VND_ID = :ASC-RLTNSHP-ID)                   
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5500-DELETE-RELATIONSHIP'                          
                                         TO DP013-PARAGRAPH                     
                       MOVE 'DELETE A ROW IN THE BRAND VENDOR TABLE'            
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'IMT_DEPT_BRND_VND'                                 
                                         TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5500-DELETE-RELATIONSHIP'                                  
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAX RETRIES EXCEEDED DELETING A BRAND/VENDOR'              
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'IMT_DEPT_BRND_VND'  TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * RECORD IN THE SELECTION TEMP STORAGE QUEUE THE FACT THAT THIS  *        
      * OBJECT WAS UPDATED.                                            *        
      *----------------------------------------------------------------*        
       5550-UPDATE-SELECTION-QUEUE.                                             
           PERFORM 9100-READ-SEL-QUEUE.                                         
           SET IM009-DEL (ASC-SEL-SUB) TO TRUE.                                 
           PERFORM 9200-REWRITE-SEL-QUEUE.                                      
      *----------------------------------------------------------------*        
      * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
                                                                                
       9100-READ-SEL-QUEUE.                                                     
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   INTO  (IM009-BRAND-VENDOR-REC)                               
                   ITEM  (ASC-SEL-ITEM)                                         
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-LOGIC-ABEND                                            
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9100-READ-SEL-QUEUE'                                       
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
       9200-REWRITE-SEL-QUEUE.                                                  
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE  (ASC-SEL-QUEUE-NAME)                                 
                    FROM   (IM009-BRAND-VENDOR-REC)                             
                    ITEM   (ASC-SEL-ITEM)                                       
                    RESP   (PV-CICS-RESP)                                       
                    REWRITE                                                     
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                           
               SET DP013-CICS-ABEND                                             
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9200-REWRITE-SEL-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
