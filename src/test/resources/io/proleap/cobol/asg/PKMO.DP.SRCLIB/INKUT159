      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.             INKUT159.                                        
       AUTHOR.                 JULIE SEIDLER.                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            04-23-98.                                        
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *       THIS PROGRAM READS THE STORE SHORTAGE RANKING FILE                
      *       AND ADDS THE STORE'S PRIME DC TO EACH RECORD.                     
      *                                                                         
      *  CHANGES:                                                               
      *     02/22/01 WR# 99999 ROD JANSSEN                                      
W02221*              FORMAT AND ADD THE PRIMARY DC LOCATION IN THE SHORT        
W02221*           SHORTAGE FILE.                                                
      *                                                                         
W26600*     04/  /04  W26600  M JOHNSON                                         
W26600*        STORE EXPANSION                                                  
W26600*                                                                         
S26600*     09/13/04  W26600  S JACKSON.                                        
S26600*        STORE EXPANSION. UPDATE SUBSCRIPT FIELDS AND PROGRAM             
S26600*        VARIABLES TO 9(04). PC-SUB, PRIME-DC-SUB, STORE-SUB              
S26600*        REMOVE ALL REFERENCES TO STR 000 DB2 TABLE                       
 28545* W28545  08-10-2006 B. GRAHAM    FIX BAD TABLE NAME DISPLAY              
 28545*                                 TO TINVPAR                              
W33304* W33304  12-12-2008 SATHISH/     HOLDING INVENTORY LEDGER OPEN           
W33304*                    INFOSYS      (HILO) CHANGES.                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT IN-DEPT-STR-RANK                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUT-DEPT-STR-RANK-DC                                          
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  IN-DEPT-STR-RANK                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W26600*    RECORD CONTAINS 94 CHARACTERS                                        
           DATA RECORD IS DEPT-STR-RANK-RECORD.                                 
                                                                                
W33304 01  IN-DEPT-STR-RANK-RECORD        PIC X(126).                           
                                                                                
       FD  OUT-DEPT-STR-RANK-DC                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W26600*    RECORD CONTAINS 96 CHARACTERS                                        
           DATA RECORD IS DEPT-STR-RANK-DC-RECORD.                              
                                                                                
W33304 01  OUT-DEPT-STR-RANK-DC-RECORD     PIC X(128).                          
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
W26600*01  IN-DEPT-STORE-LAYOUT.                                                
W26600*    05  FILLER                 PIC X(25).                                
W26600*    05  IN-STORE               PIC X(03).                                
W26600*    05  IN-REST-OF-RECORD      PIC X(95).                                
                                                                                
W26600*01  OUT-DEPT-STORE-LAYOUT.                                               
W26600*    05  OUT-DEPT-STR-RANK-RECORD PIC X(113).                             
W26600*    05  OUT-REST-OF-RECORD.                                              
W26600*        10  FILLER              PIC X(01)    VALUE ','.                  
W26600*        10  OUT-PRIME-DC        PIC X(02).                               
                                                                                
       01  ABEND-CODE                  PIC S9(4)    VALUE ZERO                  
                                       COMP SYNC.                               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-DEPTST-FILE-IND                                        
                                       PIC X(1)     VALUE 'N'.                  
               88  PS-NOT-END-OF-DEPTST-FILE        VALUE 'N'.                  
               88  PS-END-OF-DEPTST-FILE            VALUE 'Y'.                  
           05  PS-END-OF-XMTLOC-CURSOR-IND                                      
                                       PIC X(1)     VALUE 'N'.                  
               88  PS-END-OF-XMTLOC-CURSOR          VALUE 'Y'.                  
       01  PC-PROGRAM-CONSTANTS.                                                
S26600*    05  PC-SUB                  PIC 9(3)     VALUE ZERO.                 
S26600     05  PC-SUB                  PIC 9(4)     VALUE ZERO.                 
S26600*    05  PRIME-DC-SUB            PIC 9(3)     VALUE ZERO.                 
S26600     05  PRIME-DC-SUB            PIC 9(4)     VALUE ZERO.                 
S26600*    05  STORE-SUB               PIC 9(3)     VALUE ZERO.                 
S26600     05  STORE-SUB               PIC 9(4)     VALUE ZERO.                 
       01  PV-PROGRAM-VARIABLES.                                                
S26600     05  PV-PREV-STORE           PIC X(04)    VALUE SPACES.               
W02221*    05  PV-PREV-STORE           PIC X(03)    VALUE SPACES.               
W02221**** 05  PV-PREV-STORE           PIC 9(3)     VALUE ZERO.                 
           05  PV-PARAGRAPH-NAME       PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                       PIC X(30)    VALUE SPACES.               
           05  PV-CURRENT-DATE.                                                 
W26600         10  PV-CD-CCYY.                                                  
W26600             15  PC-CD-CC        PIC 9(02)    VALUE ZERO.                 
W26600             15  PV-CD-YY        PIC 9(02)    VALUE ZERO.                 
               10  PV-CD-MM            PIC 9(02)    VALUE ZERO.                 
               10  PV-CD-DD            PIC 9(02)    VALUE ZERO.                 
           05  PV-CURRENT-DATE-DB2.                                             
W26600         10  PV-CD-DB2-CCYY.                                              
W26600             15  PV-CD-DB2-CC    PIC 9(02)    VALUE ZERO.                 
W26600             15  PV-CD-DB2-YY    PIC 9(02)    VALUE ZERO.                 
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  PV-CD-DB2-MM        PIC 9(02)    VALUE ZERO.                 
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  PV-CD-DB2-DD        PIC 9(02)    VALUE ZERO.                 
W26600                                                                          
       01  WS-STORE-TABLE.                                                      
           05  WS-TABLE OCCURS 9999 TIMES.                                      
W26600         10  TBL-STORE-NMBR          PIC X(4).                            
               10  TBL-CHAR5-NAME.                                              
                   15 FILLER               PIC X(2).                            
                   15 TBL-NAME-DASH        PIC X(1).                            
                   15 TBL-CHAR2-NAME       PIC X(2).                            
               10  FILLER REDEFINES TBL-CHAR5-NAME.                             
                   15 FILLER               PIC X(2).                            
                   15 TBL-CHAR2-NAME-OTH   PIC X(2).                            
                   15 FILLER               PIC X(1).                            
W26600         10  TBL-DC-NMBR             PIC X(4).                            
                                                                                
      ******************************************************************        
      *  INVENTORY STORE SHORTAGE FILE....                                      
      ******************************************************************        
           COPY INRD003I.                                                       
                                                                                
      ******************************************************************        
      *  INVENTORY STORE SHORTAGE FILE WITH THE PRIMARY DC LOCATION....         
      ******************************************************************        
           COPY INRD003O.                                                       
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
     *******************************************************************        
     **  USED FOR DB2 ERRORS                                                    
     *******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
W26600******************************************************************        
W26600*  LAYOUT USED FOR XMT LOC TABLE                                          
W26600******************************************************************        
W26600     EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
W26600     END-EXEC.                                                            
                                                                                
W26600******************************************************************        
W26600*  LAYOUT USED FOR XMT DC LOC ASGMT TABLE                                 
W26600******************************************************************        
W26600     EXEC SQL                                                             
W26600         INCLUDE XMT00002                                                 
W26600     END-EXEC.                                                            
                                                                                
     *******************************************************************        
     **  SQL DB2 XMT_LOC CURSOR DECLARATION                                     
     *******************************************************************        
           EXEC SQL                                                             
W26600       DECLARE XMT_LOC_CSR CURSOR FOR                                     
W26600          SELECT X.LOC_NBR,                                               
W26600                 X.LOC_5_ABBR_NM,                                         
W26600                 X.CLO_DTE,                                               
W26600                 DC_ASGMNT.DC_NBR,                                        
W26600                 DC_ASGMNT.PO_STRT_DTE                                    
W26600          FROM XMT_LOC X,                                                 
W26600          (SELECT A.LOC_NBR, A.DC_NBR, TEMP.PO_STRT_DTE                   
W26600              FROM XMT_DC_LOC_ASGMT A,                                    
W26600              (SELECT B.LOC_NBR, MAX(B.PO_STRT_DTE) AS PO_STRT_DTE        
W26600              FROM XMT_DC_LOC_ASGMT B                                     
W26600              GROUP BY B.LOC_NBR) TEMP                                    
W26600              WHERE A.LOC_NBR = TEMP.LOC_NBR                              
W26600              AND A.PO_STRT_DTE = TEMP.PO_STRT_DTE) DC_ASGMNT             
W26600          WHERE X.LOC_NBR = DC_ASGMNT.LOC_NBR                             
W26600          AND X.LOC_TYP_CDE <> 'OP'                                       
W26600          ORDER BY LOC_NBR                                                
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-EXTRACT THRU 2000-EXIT                          
               UNTIL PS-END-OF-DEPTST-FILE.                                     
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  IN-DEPT-STR-RANK                                         
                OUTPUT OUT-DEPT-STR-RANK-DC.                                    
                                                                                
           SET PS-NOT-END-OF-DEPTST-FILE TO TRUE.                               
                                                                                
W26600     ACCEPT PV-CURRENT-DATE FROM DATE YYYYMMDD.                           
W26600     MOVE PV-CD-CCYY TO PV-CD-DB2-CCYY.                                   
           MOVE PV-CD-MM TO PV-CD-DB2-MM.                                       
           MOVE PV-CD-DD TO PV-CD-DB2-DD.                                       
                                                                                
           DISPLAY 'CURRENT DATE: ' PV-CURRENT-DATE-DB2                         
           INITIALIZE WS-STORE-TABLE.                                           
           PERFORM 6300-PROCESS-XMT-LOC-CSR.                                    
                                                                                
           PERFORM 7100-READ-DEPTST-RANK-FILE.                                  
           IF PS-END-OF-DEPTST-FILE                                             
               DISPLAY '* * * * * * * * * * * * * * * * * * * *'                
               DISPLAY 'INPUT DEPT STORE RANKING FILE IS EMPTY'                 
               DISPLAY '* * * * * * * * * * * * * * * * * * * *'.               
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
                                                                                
       2000-PROCESS-EXTRACT.                                                    
                                                                                
           MOVE IN003I-STORE-NUMB                                       INRD003I
                               TO IN003O-STORE-NUMB.                            
           MOVE IN003I-DIVISION-NUMB                                       LV001
                               TO IN003O-DIVISION-NUMB.                         
           MOVE IN003I-DEPARTMENT-NUMB                                  INRD003I
                               TO IN003O-DEPARTMENT-NUMB.                       
           MOVE IN003I-CLASS-NUMB                                       INRD003I
                               TO IN003O-CLASS-NUMB.                            
           MOVE IN003I-BUYER-NUMB                                       INRD003I
                               TO IN003O-BUYER-NUMB.                            
           MOVE IN003I-STORE-NAME                                       INRD003I
                               TO IN003O-STORE-NAME.                            
           MOVE IN003I-DEPARTMENT-NAME                                  INRD003I
                               TO IN003O-DEPARTMENT-NAME.                       
           MOVE IN003I-FINANCE-CORP-NMBR                                INRD003I
                               TO IN003O-FINANCE-CORP-NMBR.                     
           MOVE IN003I-BOOK-INV                                         INRD003I
                               TO IN003O-BOOK-INV.                              
           MOVE IN003I-SHRINK-ALLOWANCE                                 INRD003I
                               TO IN003O-SHRINK-ALLOWANCE.                      
           MOVE IN003I-TOTAL-RETAIL                                     INRD003I
                               TO IN003O-TOTAL-RETAIL.                          
           MOVE IN003I-TOTAL-COST                                       INRD003I
                               TO IN003O-TOTAL-COST.                            
           MOVE IN003I-PHY-INV-RETAIL                                   INRD003I
                               TO IN003O-PHY-INV-RETAIL.                        
           MOVE IN003I-SHORT-RETAIL                                     INRD003I
                               TO IN003O-SHORT-RETAIL.                          
           MOVE IN003I-SHORT-COST                                       INRD003I
                               TO IN003O-SHORT-COST.                            
           MOVE IN003I-TOTAL-SALES                                      INRD003I
                               TO IN003O-TOTAL-SALES.                           
W33304     MOVE IN003I-OPN-FSCL-MN-DTE                                  INRD003I
W33304                         TO IN003O-OPN-FSCL-MN-DTE.                       
W33304     MOVE IN003I-INV-ID                                           INRD003I
W33304                         TO IN003O-INV-ID.                                
W33304     MOVE IN003I-FINCL-INV-STAT-CDE                               INRD003I
W33304                         TO IN003O-FINCL-INV-STAT-CDE.                    
                                                                                
           IF IN003I-STORE-NUMB NUMERIC                                         
               MOVE IN003I-STORE-NUMB TO STORE-SUB                              
               IF TBL-STORE-NMBR(STORE-SUB) = SPACES                            
                   MOVE 'NA' TO IN003O-PRIME-DC                                 
               ELSE                                                             
               IF TBL-DC-NMBR(STORE-SUB) NUMERIC                                
                   MOVE TBL-DC-NMBR(STORE-SUB) TO PRIME-DC-SUB                  
                   IF TBL-NAME-DASH(PRIME-DC-SUB) EQUAL '-'                     
                       MOVE TBL-CHAR2-NAME(PRIME-DC-SUB)                        
                                   TO IN003O-PRIME-DC                           
                   ELSE                                                         
                       MOVE TBL-CHAR2-NAME-OTH(PRIME-DC-SUB)                    
                                   TO IN003O-PRIME-DC                           
                   END-IF                                                       
               ELSE                                                             
                   MOVE 'NA' TO IN003O-PRIME-DC                                 
               END-IF                                                           
           ELSE                                                                 
               MOVE 'NA' TO IN003O-PRIME-DC                                     
           END-IF.                                                              
                                                                                
           WRITE OUT-DEPT-STR-RANK-DC-RECORD                                    
                       FROM IN003O-STORE-SHORTAGE-REC.                          
                                                                                
           PERFORM 7100-READ-DEPTST-RANK-FILE.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6300-PROCESS-XMT-LOC-CSR.                                                
           EXEC SQL                                                             
               OPEN XMT_LOC_CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'OPEN XMT_LOC CURSOR' TO                                
                       PV-DB2-OPERATION-ATTEMPTED                               
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
           PERFORM 6400-FETCH-XMT-LOC-TABLE.                                    
                                                                                
           PERFORM 6500-LOAD-WS-STORE-TABLE                                     
                  UNTIL PS-END-OF-XMTLOC-CURSOR.                                
                                                                                
           EXEC SQL                                                             
               CLOSE XMT_LOC_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
28545              MOVE 'CLOSE XMTLOC  CURSOR' TO                               
                       PV-DB2-OPERATION-ATTEMPTED                               
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  FETCH THE DB2 XMT_LOC TABLE INTO THE DCLGEN                            
      ******************************************************************        
                                                                                
       6400-FETCH-XMT-LOC-TABLE.                                                
                                                                                
W26600     EXEC SQL                                                             
W26600         FETCH XMT_LOC_CSR                                                
W26600         INTO :XMT00001-LOC-NBR,                                          
W26600              :XMT00001-LOC-5-ABBR-NM,                                    
W26600              :XMT00001-CLO-DTE,                                          
W26600              :XMT00002-DC-NBR,                                           
W26600              :XMT00002-PO-STRT-DTE                                       
W26600     END-EXEC.                                                            
                                                                                
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET PS-END-OF-XMTLOC-CURSOR TO TRUE                          
               WHEN OTHER                                                       
                   MOVE '6400-FETCH-XMT-LOC-TABLE'                              
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'FETCH XMT_LOC RECORD' TO                               
                       PV-DB2-OPERATION-ATTEMPTED                               
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  LOAD THE STORE DATA INTO THE WORKING STORAGE TABLE                     
      *  WHEN DETERMINE WHICH DC TO USES (DC1 OR DC2), MUST LOCK                
      *  AT THE PO-START-DATE.                                                  
      ******************************************************************        
                                                                                
       6500-LOAD-WS-STORE-TABLE.                                                
W26600     MOVE XMT00001-LOC-NBR          TO PC-SUB.                            
                                                                                
W26600     MOVE XMT00001-LOC-NBR         TO TBL-STORE-NMBR(PC-SUB).             
W26600     MOVE XMT00001-LOC-5-ABBR-NM   TO TBL-CHAR5-NAME(PC-SUB).             
                                                                                
W26600     IF XMT00002-DC-NBR > ZERO                                            
W26000         IF PV-CURRENT-DATE-DB2 > XMT00002-PO-STRT-DTE                    
W26600           MOVE XMT00002-DC-NBR    TO TBL-DC-NMBR(PC-SUB)                 
W26600         ELSE                                                             
W26600           DISPLAY '************************************'                 
W26600           DISPLAY 'FOR STORE ' XMT00001-LOC-NBR                          
W26600           DISPLAY 'STRT DTE ' XMT00002-PO-STRT-DTE                       
W26600           DISPLAY 'CLOSED DATE ' XMT00001-CLO-DTE                        
W26600           DISPLAY '************************************'                 
W26600           MOVE SPACE              TO TBL-DC-NMBR(PC-SUB)                 
W26600         END-IF                                                           
W26600     ELSE                                                                 
W26600         MOVE XMT00002-DC-NBR      TO TBL-DC-NMBR(PC-SUB)                 
W26600     END-IF.                                                              
           PERFORM 6400-FETCH-XMT-LOC-TABLE.                                    
                                                                                
      ******************************************************************        
      *  READ A STORE EXTRACT RECORD.                                           
      ******************************************************************        
                                                                                
       7100-READ-DEPTST-RANK-FILE.                                              
           READ IN-DEPT-STR-RANK                                                
W02221         INTO IN003I-STORE-SHORTAGE-REC                                   
W02221****     INTO IN-DEPT-STORE-LAYOUT                                        
               AT END                                                           
                   SET PS-END-OF-DEPTST-FILE TO TRUE                            
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *  CLOSE ALL FILES.                                              *        
      ******************************************************************        
                                                                                
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE IN-DEPT-STR-RANK                                               
                 OUT-DEPT-STR-RANK-DC.                                          
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      ******************************************************************        
      *                                                                         
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = INKUT159'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
