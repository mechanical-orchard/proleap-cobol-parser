000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             SUKUT036.                                        
000600 AUTHOR.                 PATRICK BURKE.                                   
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            JULY, 2000                                       
000900 DATE-COMPILED.                                                           
001000*****************************************************************         
001100*                                                                         
001200*****************************************************************         
001300*                                                               *         
001400*  THIS PROGRAM WILL CREATE A REPORT EXTRACT TO BE USED IN      *         
001500*  SUKRP007 -PO/STORE UNIT ADJUSTMENTS.                         *         
001600*                                                               *         
001700*  INPUT FOR THIS PROGRAM IS CREATED OUT OF SUKUT034.           *         
001800*****************************************************************         
001900*                                                                         
002000*****************************************************************         
002100 ENVIRONMENT DIVISION.                                                    
002200*****************************************************************         
002300                                                                          
002400 CONFIGURATION SECTION.                                                   
002500                                                                          
002600 SOURCE-COMPUTER.        IBM-3090.                                        
002700 OBJECT-COMPUTER.        IBM-3090.                                        
002800                                                                          
002900 INPUT-OUTPUT SECTION.                                                    
003000                                                                          
003100 FILE-CONTROL.                                                            
003200*                                                                         
003300     SELECT EXTRACT-FILE                                                  
003400         ASSIGN TO INP01.                                                 
003500                                                                          
003600     SELECT REPORT-FILE                                                   
003700         ASSIGN TO OUT01.                                                 
003800                                                                          
003900*                                                                         
004000*****************************************************************         
004100 DATA DIVISION.                                                           
004200*****************************************************************         
004300*                                                                         
004400 FILE SECTION.                                                            
004500*                                                                         
004600 FD  EXTRACT-FILE                                                         
004700     RECORDING MODE IS F                                                  
004800     RECORD CONTAINS 200 CHARACTERS                                       
004900     LABEL RECORDS ARE STANDARD                                           
005000     DATA RECORD IS EXTRACT-RECORD.                                       
005100 01  EXTRACT-RECORD                 PIC X(200).                           
005200                                                                          
005300 FD  REPORT-FILE                                                          
005400     RECORDING MODE IS F                                                  
005500     RECORD CONTAINS 200 CHARACTERS                                       
005600     LABEL RECORDS ARE OMITTED                                            
005700     DATA RECORD IS REPORT-RECORD.                                        
005800 01  REPORT-RECORD                  PIC X(200).                           
005900*                                                                         
006000*                                                                         
006100 WORKING-STORAGE SECTION.                                                 
006200*                                                                         
006300**************************************************************            
006400* FORMAT OF THE CONTROL CARD INFORMATION PASSED INTO THE PGM *            
006500**************************************************************            
006600 01  CC-CONTROL-CARD.                                                     
006700     05  CC-PRINTER-ID                PIC  X(08).                         
006800     05  CC-PRTR-CNTL-IND             PIC  X(01).                         
006900     05  CC-TRACK-NBR                 PIC  9(01).                         
007000     05  FILLER                       PIC  X(01).                         
007100     05  CC-CUT-REPORT                PIC  X(01).                         
007200         88 CC-CUT-REPORT-AT-END                   VALUE 'C'.             
007300     05  CC-REQUESTOR-ID              PIC  X(08).                         
007400     05  CC-PRINT-SUKRP002-IND        PIC  X(01).                         
007500         88 CC-PRINT-SUKRP002                      VALUE 'Y'.             
007600     05  CC-PRINT-SUKRP003-IND        PIC  X(01).                         
007700         88 CC-PRINT-SUKRP003                      VALUE 'Y'.             
007800     05  CC-PRINT-SUKRP004-IND        PIC  X(01).                         
007900         88 CC-PRINT-SUKRP004                      VALUE 'Y'.             
008000     05  CC-PRINT-SUKRP005-IND        PIC  X(01).                         
008100         88 CC-PRINT-SUKRP005                      VALUE 'Y'.             
008200     05  CC-PRINT-SUKRP006-IND        PIC  X(01).                         
008300         88 CC-PRINT-SUKRP006                      VALUE 'Y'.             
008400     05  CC-PRINT-SUKRP007-IND        PIC  X(01).                         
008500         88 CC-PRINT-SUKRP007                      VALUE 'Y'.             
008600     05  CC-PRINT-SUKRP008-IND        PIC  X(01).                         
008700         88 CC-PRINT-SUKRP008                      VALUE 'Y'.             
008800     05  CC-PRINT-SUKRP023-IND        PIC  X(01).                         
008900         88 CC-PRINT-SUKRP023                      VALUE 'Y'.             
009000*                                                                         
009100 01 WS-CALC-FIELDS.                                                       
009200    05  WS-RECORDS-READ-QTY          PIC S9(07)    COMP-3                 
009300                                                   VALUE ZEROS.           
009400    05  WS-RECORDS-WRITE-QTY         PIC S9(07)    COMP-3                 
009500                                                   VALUE ZEROS.           
009600*                                                                         
009700 01  PROGRAM-SWITCHES.                                                    
009800     05  PS-END-OF-FILE-SW           PIC X(03)     VALUE 'N'.             
009900         88  PS-END-OF-FILE                        VALUE 'Y'.             
010000     05  PS-FIRST-TIME-SW            PIC X(01)     VALUE 'N'.             
010100         88  PS-FIRST-TIME-YES                     VALUE 'Y'.             
010200         88  PS-FIRST-TIME-NO                      VALUE 'N'.             
010300     05  PS-END-OF-LBVRDA-SW         PIC X(03)     VALUE 'N'.             
010400         88 PS-END-OF-LBVRDA                       VALUE 'Y'.             
010500         88 PS-NOT-END-OF-LBVRDA                   VALUE 'N'.             
010600     05  PS-LBVRDA-DATA-SW           PIC X(03)     VALUE 'N'.             
010700         88 PS-LBVRDA-DATA                         VALUE 'Y'.             
010800         88 PS-LBVRDA-DATA-NO                      VALUE 'N'.             
010900     05  PS-END-OF-SHUUPC-SW         PIC X(03)     VALUE 'N'.             
011000         88 PS-END-OF-SHUUPC                       VALUE 'Y'.             
011100         88 PS-NOT-END-OF-SHUUPC                   VALUE 'N'.             
011200     05  PS-USE-SHUUPC-DATA-SW       PIC X(03)     VALUE 'N'.             
011300         88 PS-USE-SHUUPC-DATA                     VALUE 'Y'.             
011400         88 PS-SHUUPC-DATA-NO                      VALUE 'N'.             
011500     05  PS-END-OF-TABLE-SW          PIC X(03)     VALUE 'N'.             
011600         88 PS-END-OF-TABLE                        VALUE 'Y'.             
011700         88 PS-NOT-END-OF-TABLE                    VALUE 'N'.             
011800     05  PS-SAME-PO-SW               PIC X(01)     VALUE 'N'.             
011900         88  PS-SAME-PO                            VALUE 'Y'.             
012000         88  PS-NOT-SAME-PO                        VALUE 'N'.             
012100*                                                                         
012200 01 PC-PROGRAM-CONSTANTS.                                                 
012300     05  PC-N                        PIC  X(01)    VALUE 'N'.             
012400                                                                          
012500     05  PC-PROGRAM-NAME             PIC  X(08)    VALUE                  
012600         'SUKUT036'.                                                      
012700*                                                                         
012800 01 PV-PROGRAM-VARIABLES.                                                 
012900     05  PV-PO-NBR                   PIC S9(09)    VALUE ZEROS            
013000                                                   COMP.                  
013100     05  PV-REC-SEQ-NBR              PIC S9(09)    VALUE ZEROS            
013200                                                   COMP.                  
013300     05  PV-STORE-NBR                PIC 9(04)     VALUE ZEROS.           
013400     05  PV-LINE-NBR                 PIC 9(09)     VALUE ZEROS.           
013500     05  PV-SHIPT-UNT-ID             PIC S9(18)V   VALUE ZERO             
013600                                                   COMP-3.                
013700     05  PV-EXCEPTION-TYPE           PIC 9(01)     VALUE ZEROS.           
013800     05  PV-SHPD-QTY                 PIC S9(18)    VALUE ZEROS.           
013900     05  PV-PREV-PO-NBR              PIC S9(09)    VALUE ZEROS            
014000                                                   COMP.                  
014100     05  PV-PREV-REC-SEQ-NBR         PIC S9(09)    VALUE ZEROS            
014200                                                   COMP.                  
014300                                                                          
014400     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
014500                                                   COMP SYNC.             
014600     05  PV-ABEND-PARA-NAME          PIC X(30)     VALUE SPACES.          
014700     05  PV-DB2-OPERATION-MSG-1      PIC X(40)     VALUE SPACES.          
014800     05  PV-DB2-OPERATION-MSG-2      PIC X(40)     VALUE SPACES.          
014900     05  PV-DB2-TABLE-NAME           PIC X(12)     VALUE SPACES.          
015000     05  PV-SQLCODE                  PIC Z(8)9-.                          
015100                                                                          
015200                                                                          
015300*RECORD LAYOUT FOR INPUT FILE                                             
015400     COPY SURD001.                                                        
015500                                                                          
015600*RECORD LAYOUT FOR OUTPUT FILE                                            
015700     COPY SURD009.                                                        
015800                                                                          
015900 EJECT                                                                    
016000*                                                                         
016100*****************************************************************         
016200*  DB2 AREA FOR TLBVRDA                                                   
016300*****************************************************************         
016400*                                                                         
016500     EXEC SQL                                                             
016600          INCLUDE TLBVRDA                                                 
016700     END-EXEC.                                                            
016800*                                                                         
016900*****************************************************************         
017000*  DB2 AREA FOR TSHUUPC                                                   
017100*****************************************************************         
017200*                                                                         
017300     EXEC SQL                                                             
017400          INCLUDE TSHUUPC                                                 
017500     END-EXEC.                                                            
017600*                                                                         
017700*****************************************************************         
017800*** DB2 MESSAGE AREAS ****                                                
017900*****************************************************************         
018000*                                                                         
018100     COPY DPWS004.                                                        
018200*                                                                         
018300*****************************************************************         
018400*    DB2 AREA FOR COMMUNICATIONS                                          
018500*****************************************************************         
018600*                                                                         
018700     EXEC SQL                                                             
018800          INCLUDE SQLCA                                                   
018900     END-EXEC.                                                            
019000*                                                                         
019100*****************************************************************         
019200*    DB2 AREA FOR COMMUNICATIONS AREA2                                    
019300*****************************************************************         
019400*                                                                         
019500*    COPY SQLCA2.                                                         
019600*                                                                         
019700*****************************************************************         
019800*    CURSOR DECLARATION FOR LBVRDA CUR                                    
019900*****************************************************************         
020000*                                                                         
020100     EXEC SQL                                                             
020200         DECLARE LBVRDA_CUR CURSOR FOR                                    
020300             SELECT OTPK_UNT_QTY                                          
020400                  , DEST_OPER_UNT_ID                                      
020500                  , PO_LNE_NBR                                            
020600                  , PREPK_ID                                              
020700               FROM TLBVRDA                                               
020800              WHERE PO_NBR           = :PV-PO-NBR                         
020900                AND RCVR_SEQ_NBR     = :PV-REC-SEQ-NBR                    
021000                AND LBL_ASG_IND      = :PC-N                              
021100                AND CART_LBL_TYP_CDE IN ('C','S')                         
021200     END-EXEC.                                                            
021300*                                                                         
021400*****************************************************************         
021500*    CURSOR DECLARATION FOR SHUUPC CUR                                    
021600*****************************************************************         
021700*                                                                         
021800     EXEC SQL                                                             
021900         DECLARE SHUUPC_CUR CURSOR FOR                                    
022000             SELECT TRN_NBR                                               
022100                  , TRN_LNE_NBR                                           
022200                  , SHPD_QTY                                              
022300               FROM TSHUUPC                                               
022400              WHERE SHIPT_UNT_ID = :PV-SHIPT-UNT-ID                       
022500     END-EXEC.                                                            
022600                                                                          
022700******************************************************************        
022800 PROCEDURE DIVISION.                                                      
022900******************************************************************        
023000*                                                                         
023100 0000-MAINLINE-PROCESSING.                                                
023200                                                                          
023300     DISPLAY '**** START OF SUKUT036 ****'.                               
023400     DISPLAY ' '.                                                         
023500                                                                          
023600     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
023700     DISPLAY 'CONTROL CARD = ' CC-CONTROL-CARD.                           
023800                                                                          
023900     IF CC-PRINT-SUKRP007                                                 
024000         PERFORM 1000-INITIALIZE                                          
024100*                                                                         
024200         IF  PS-END-OF-FILE                                               
024300             PERFORM 9000-END-PROCESS                                     
024400         ELSE                                                             
024500             SET PS-FIRST-TIME-NO TO TRUE                                 
024600             PERFORM 2000-MAIN-PROCESS                                    
024700                UNTIL  PS-END-OF-FILE                                     
024800             PERFORM 9000-END-PROCESS                                     
024900         END-IF                                                           
025000*                                                                         
025100     ELSE                                                                 
025200         CONTINUE                                                         
025300     END-IF.                                                              
025400*                                                                         
025500     STOP RUN.                                                            
025600*                                                                         
025700******************************************************************        
025800**  PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.             **        
025900******************************************************************        
026000 1000-INITIALIZE.                                                         
026100*                                                                         
026200     OPEN INPUT  EXTRACT-FILE                                             
026300          OUTPUT REPORT-FILE.                                             
026400*                                                                         
026500*READ FIRST INPUT RECORD.                                                 
026600     PERFORM 7000-READ-EXTRACT.                                           
026700     SET PS-FIRST-TIME-YES TO TRUE.                                       
026800                                                                          
026900     IF PS-END-OF-FILE                                                    
027000         DISPLAY 'EXTRACT FILE INP01 IS EMPTY'                            
027100     ELSE                                                                 
027200        IF SU001-ERROR-RECORD                                             
027300          PERFORM 6100-BUILD-ERROR-RECORD                                 
027400          PERFORM 7200-WRITE-EXTRACT-RECORD                               
027500          SET PS-END-OF-FILE TO TRUE                                      
027600        END-IF                                                            
027700     END-IF.                                                              
027800*                                                                         
027900*                                                                         
028000******************************************************************        
028100*  THIS IS THE MAIN CONTROLLING PARAGRAPH                        *        
028200******************************************************************        
028300 2000-MAIN-PROCESS.                                                       
028400*                                                                         
028500     IF    SU001-PO-NUMBER    =   PV-PREV-PO-NBR AND                      
028600           SU001-REC-SEQ-NBR  =   PV-PREV-REC-SEQ-NBR                     
028700*       IF PS-USE-SHUUPC-DATA                                             
028800*          PERFORM 8000-OPEN-SHUUPC-CSR                                   
028900*          PERFORM 8200-FETCH-SHUUPC-CSR                                  
029000*          PERFORM 2400-PROCESS-SHUUPC                                    
029100*             UNTIL PS-END-OF-SHUUPC                                      
029200*          PERFORM 8100-CLOSE-SHUUPC-CSR                                  
029300*          SET PS-NOT-END-OF-SHUUPC  TO TRUE                              
029400*       END-IF                                                            
029500        CONTINUE                                                          
029600     ELSE                                                                 
029700        SET PS-LBVRDA-DATA-NO                                             
029800            PS-SHUUPC-DATA-NO                                             
029900            PS-NOT-END-OF-LBVRDA TO TRUE                                  
030000        PERFORM 7500-OPEN-LBVRDA-CSR                                      
030100        PERFORM 7700-FETCH-LBVRDA-CSR                                     
030200        PERFORM 2200-PROCESS-ROW                                          
030300           UNTIL PS-END-OF-LBVRDA                                         
030400        PERFORM 7600-CLOSE-LBVRDA-CSR                                     
030500*       IF PS-LBVRDA-DATA-NO                                              
030600*          SET PS-USE-SHUUPC-DATA TO TRUE                                 
030700*          PERFORM 8000-OPEN-SHUUPC-CSR                                   
030800*          PERFORM 8200-FETCH-SHUUPC-CSR                                  
030900*          PERFORM 2400-PROCESS-SHUUPC                                    
031000*             UNTIL PS-END-OF-SHUUPC                                      
031100*          PERFORM 8100-CLOSE-SHUUPC-CSR                                  
031200*          SET PS-NOT-END-OF-SHUUPC  TO TRUE                              
031300*       END-IF                                                            
031400        MOVE SU001-PO-NUMBER     TO PV-PREV-PO-NBR                        
031500        MOVE SU001-REC-SEQ-NBR   TO PV-PREV-REC-SEQ-NBR                   
031600     END-IF.                                                              
031700                                                                          
031800*READ NEXT INPUT RECORD.                                                  
031900                                                                          
032000     PERFORM 7000-READ-EXTRACT.                                           
032100*                                                                         
032200******************************************************************        
032300*  PROCESS A LBVRDA ROW, BUILD AND WRITE A RECORD, IF NOT FOUND           
032400*  SEARCH TSHUUPC.                                                        
032500******************************************************************        
032600 2200-PROCESS-ROW.                                                        
032700*                                                                         
032800      SET PS-LBVRDA-DATA TO TRUE.                                         
032900      MOVE LBVRDA-OTPK-UNT-QTY TO PV-SHPD-QTY.                            
033000      MOVE LBVRDA-DEST-OPER-UNT-ID                                        
033100                                  TO PV-STORE-NBR.                        
033200*     MOVE LBVRDA-PO-LNE-NBR      TO PV-LINE-NBR.                         
033300      PERFORM 2500-PROCESS-INDICATORS.                                    
033400      PERFORM 6000-BUILD-OUTPUT-RECORD.                                   
033500      PERFORM 7200-WRITE-EXTRACT-RECORD.                                  
033600      PERFORM 7700-FETCH-LBVRDA-CSR.                                      
033700*                                                                         
033800******************************************************************        
033900*  SEARCH TSHUUPC TABLE                                                   
034000******************************************************************        
034100 2400-PROCESS-SHUUPC.                                                     
034200*                                                                         
034300     MOVE SHUUPC-SHPD-QTY     TO PV-SHPD-QTY.                             
034400     PERFORM 2500-PROCESS-INDICATORS.                                     
034500     MOVE SHUUPC-TRN-LNE-NBR   TO PV-LINE-NBR.                            
034600     PERFORM 6000-BUILD-OUTPUT-RECORD.                                    
034700     PERFORM 7200-WRITE-EXTRACT-RECORD.                                   
034800                                                                          
034900     PERFORM 8200-FETCH-SHUUPC-CSR.                                       
035000*                                                                         
035100******************************************************************        
035200*  PROCESS ERROR TYPE INDICATORS                                 *        
035300******************************************************************        
035400 2500-PROCESS-INDICATORS.                                                 
035500*                                                                         
035600     EVALUATE TRUE                                                        
035700        WHEN SU001-SCANNED-IND     = 'N' OR ' '                           
035800          MOVE 1                    TO PV-EXCEPTION-TYPE                  
035900          MULTIPLY PV-SHPD-QTY        BY -1                               
036000              GIVING PV-SHPD-QTY                                          
036100                                                                          
036200        WHEN SU001-MISDIRECT-IND   = 'Y'                                  
036300          MOVE 2                   TO PV-EXCEPTION-TYPE                   
036400                                                                          
036500        WHEN SU001-FREE-ASTRAY-IND = 'Y'                                  
036600          MOVE 3                   TO PV-EXCEPTION-TYPE                   
036700                                                                          
036800     END-EVALUATE.                                                        
036900*                                                                         
037000******************************************************************        
037100*   WRITE OUTPUT RECORD                                          *        
037200******************************************************************        
037300 6000-BUILD-OUTPUT-RECORD.                                                
037400*                                                                         
037500     INITIALIZE SU009-EXTRACT-RECORD.                                     
037600*                                                                         
037700     IF LBVRDA-PO-LNE-NBR = 0                                             
037800        IF LBVRDA-PREPK-ID > 0                                            
037900           MOVE LBVRDA-PREPK-ID TO PV-LINE-NBR                            
038000           MOVE 'P'             TO SU009-PREPK-IND                        
038100        ELSE                                                              
038200           MOVE LBVRDA-PO-LNE-NBR TO PV-LINE-NBR                          
038300        END-IF                                                            
038400     ELSE                                                                 
038500        MOVE LBVRDA-PO-LNE-NBR    TO PV-LINE-NBR                          
038600     END-IF.                                                              
038700*                                                                         
038800     MOVE SU001-MANIFEST-NBR     TO SU009-MANIFEST-NBR.                   
038900     MOVE SU001-PO-NUMBER        TO SU009-PO-NUMBER.                      
039000     MOVE SU001-REC-SEQ-NBR      TO SU009-REC-SEQ-NBR.                    
039100     MOVE PV-STORE-NBR           TO SU009-STR-NBR.                        
039200     MOVE PV-LINE-NBR            TO SU009-LINE-NBR.                       
039300     MOVE PV-SHPD-QTY            TO SU009-SHPD-QTY.                       
039400     MOVE PV-EXCEPTION-TYPE      TO SU009-EXCEPTION-TYPE.                 
039500     MOVE SU001-RECORD-TYPE-IND  TO SU009-RECORD-TYPE-IND.                
039600     MOVE SU001-FREE-ASTRAY-IND  TO SU009-FREE-ASTRAY-IND.                
039700     MOVE SU001-AUTO-VE-IND      TO SU009-AUTO-VE-IND.                    
039800*                                                                         
039900******************************************************************        
040000*   BUILD ERROR RECORD                                           *        
040100******************************************************************        
040200 6100-BUILD-ERROR-RECORD.                                                 
040300*                                                                         
040400     INITIALIZE SU009-EXTRACT-RECORD.                                     
040500*                                                                         
040600     MOVE SU001-MANIFEST-NBR     TO SU009-MANIFEST-NBR.                   
040700     MOVE SU001-PO-NUMBER        TO SU009-PO-NUMBER.                      
040800     MOVE SU001-REC-SEQ-NBR      TO SU009-REC-SEQ-NBR.                    
040900     MOVE SU001-STR-NBR          TO SU009-STR-NBR.                        
041000     MOVE SU001-RECORD-TYPE-IND  TO SU009-RECORD-TYPE-IND.                
041100*                                                                         
041200*                                                                         
041300*                                                                         
041400******************************************************************        
041500*  READ EXTRACT FILE                                             *        
041600******************************************************************        
041700 7000-READ-EXTRACT.                                                       
041800     READ EXTRACT-FILE INTO SU001-EXTRACT-RECORD                          
041900        AT END                                                            
042000           SET PS-END-OF-FILE          TO TRUE                            
042100        NOT AT END                                                        
042200            MOVE SU001-PO-NUMBER       TO PV-PO-NBR                       
042300            MOVE SU001-REC-SEQ-NBR     TO PV-REC-SEQ-NBR                  
042400            MOVE SU001-STR-NBR         TO PV-STORE-NBR                    
042500            MOVE SU001-SHIPT-UNT-ID    TO PV-SHIPT-UNT-ID                 
042600            ADD  +1                    TO WS-RECORDS-READ-QTY             
042700*                                                                         
042800     END-READ.                                                            
042900*                                                                         
043000******************************************************************        
043100*  WRITE EXTRACT RECORD                                          *        
043200******************************************************************        
043300 7200-WRITE-EXTRACT-RECORD.                                               
043400*                                                                         
043500     WRITE REPORT-RECORD                                                  
043600         FROM SU009-EXTRACT-RECORD                                        
043700     ADD 1                        TO WS-RECORDS-WRITE-QTY.                
043800*                                                                         
043900******************************************************************        
044000*   OPEN LBVRDA CURSOR.                                                   
044100******************************************************************        
044200 7500-OPEN-LBVRDA-CSR.                                                    
044300*                                                                         
044400     EXEC SQL                                                             
044500         OPEN LBVRDA_CUR                                                  
044600     END-EXEC.                                                            
044700                                                                          
044800     EVALUATE TRUE                                                        
044900         WHEN SQLCODE = ZERO                                              
045000             CONTINUE                                                     
045100         WHEN OTHER                                                       
045200           MOVE '7500-OPEN-LBVRDA-CURSOR' TO PV-ABEND-PARA-NAME           
045300           MOVE 'SQL ERROR OPENING CURSOR' TO                             
045400                            PV-DB2-OPERATION-MSG-1                        
045500             PERFORM 9998-SQL-ABEND                                       
045600     END-EVALUATE.                                                        
045700*                                                                         
045800******************************************************************        
045900*   CLOSE LBVRDA CURSOR.                                                  
046000******************************************************************        
046100 7600-CLOSE-LBVRDA-CSR.                                                   
046200*                                                                         
046300     EXEC SQL                                                             
046400         CLOSE LBVRDA_CUR                                                 
046500     END-EXEC.                                                            
046600                                                                          
046700     EVALUATE TRUE                                                        
046800         WHEN SQLCODE = ZERO                                              
046900             CONTINUE                                                     
047000         WHEN OTHER                                                       
047100           MOVE '7600-CLOSE-LBVRDA-CURSOR' TO PV-ABEND-PARA-NAME          
047200           MOVE 'SQL ERROR CLOSING CURSOR' TO                             
047300                            PV-DB2-OPERATION-MSG-1                        
047400             PERFORM 9998-SQL-ABEND                                       
047500     END-EVALUATE.                                                        
047600*                                                                         
047700******************************************************************        
047800*   FETCH LBVRDA CURSOR.                                                  
047900******************************************************************        
048000*                                                                         
048100 7700-FETCH-LBVRDA-CSR.                                                   
048200     EXEC SQL                                                             
048300         FETCH LBVRDA_CUR                                                 
048400          INTO :LBVRDA-OTPK-UNT-QTY                                       
048500             , :LBVRDA-DEST-OPER-UNT-ID                                   
048600             , :LBVRDA-PO-LNE-NBR                                         
048700             , :LBVRDA-PREPK-ID                                           
048800     END-EXEC.                                                            
048900                                                                          
049000     EVALUATE TRUE                                                        
049100         WHEN SQLCODE = ZERO                                              
049200             CONTINUE                                                     
049300         WHEN SQLCODE = +100                                              
049400             SET PS-END-OF-LBVRDA TO TRUE                                 
049500         WHEN OTHER                                                       
049600           MOVE '7700-FETCH-LBVRDA-CSR' TO PV-ABEND-PARA-NAME             
049700           MOVE 'SQL ERROR FETCHING DATA' TO                              
049800                            PV-DB2-OPERATION-MSG-1                        
049900             PERFORM 9998-SQL-ABEND                                       
050000     END-EVALUATE.                                                        
050100*                                                                         
050200*                                                                         
050300******************************************************************        
050400*   OPEN SHUUPC CURSOR.                                         *         
050500******************************************************************        
050600 8000-OPEN-SHUUPC-CSR.                                                    
050700*                                                                         
050800     EXEC SQL                                                             
050900         OPEN SHUUPC_CUR                                                  
051000     END-EXEC.                                                            
051100                                                                          
051200     EVALUATE TRUE                                                        
051300         WHEN SQLCODE = ZERO                                              
051400             CONTINUE                                                     
051500         WHEN OTHER                                                       
051600           MOVE '8000-OPEN-SHUUPC-CSR' TO PV-ABEND-PARA-NAME              
051700           MOVE 'SQL ERROR OPENING CURSOR' TO                             
051800                            PV-DB2-OPERATION-MSG-1                        
051900             PERFORM 9998-SQL-ABEND                                       
052000     END-EVALUATE.                                                        
052100*                                                                         
052200******************************************************************        
052300*   CLOSE SHUUPC CURSOR.                                        *         
052400******************************************************************        
052500 8100-CLOSE-SHUUPC-CSR.                                                   
052600*                                                                         
052700     EXEC SQL                                                             
052800         CLOSE SHUUPC_CUR                                                 
052900     END-EXEC.                                                            
053000                                                                          
053100     EVALUATE TRUE                                                        
053200         WHEN SQLCODE = ZERO                                              
053300             CONTINUE                                                     
053400         WHEN OTHER                                                       
053500           MOVE '8100-CLOSE-SHUUPC-CSR' TO PV-ABEND-PARA-NAME             
053600           MOVE 'SQL ERROR CLOSING CURSOR' TO                             
053700                            PV-DB2-OPERATION-MSG-1                        
053800             PERFORM 9998-SQL-ABEND                                       
053900     END-EVALUATE.                                                        
054000*                                                                         
054100******************************************************************        
054200*   FETCH SHUUPC CURSOR.                                                  
054300******************************************************************        
054400 8200-FETCH-SHUUPC-CSR.                                                   
054500*                                                                         
054600     EXEC SQL                                                             
054700         FETCH SHUUPC_CUR                                                 
054800          INTO :SHUUPC-TRN-NBR                                            
054900             , :SHUUPC-TRN-LNE-NBR                                        
055000             , :SHUUPC-SHPD-QTY                                           
055100     END-EXEC.                                                            
055200                                                                          
055300     EVALUATE TRUE                                                        
055400         WHEN SQLCODE = ZERO                                              
055500             CONTINUE                                                     
055600         WHEN SQLCODE = +100                                              
055700             SET PS-END-OF-SHUUPC TO TRUE                                 
055800         WHEN OTHER                                                       
055900           MOVE '8200-FETCH-SHUUPC-CSR' TO PV-ABEND-PARA-NAME             
056000           MOVE 'SQL ERROR FETCHING DATA' TO                              
056100                            PV-DB2-OPERATION-MSG-1                        
056200             PERFORM 9998-SQL-ABEND                                       
056300     END-EVALUATE.                                                        
056400*                                                                         
056500******************************************************************        
056600*     CLOSE FILES.                                              **        
056700******************************************************************        
056800 9000-END-PROCESS.                                                        
056900*                                                                         
057000     CLOSE EXTRACT-FILE                                                   
057100           REPORT-FILE.                                                   
057200*                                                                         
057300     DISPLAY '**********************************'.                        
057400     DISPLAY '** SUKUT036 SUMMARY **'.                                    
057500     DISPLAY 'TOTAL INPUT RECS       = ' WS-RECORDS-READ-QTY.             
057600     DISPLAY 'TOTAL RECS WRITTEN     = ' WS-RECORDS-WRITE-QTY.            
057700     DISPLAY '**********************************'.                        
057800*                                                                         
057900******************************************************************        
058000*     SQL ERROR PROCESSING                                      **        
058100******************************************************************        
058200 9998-SQL-ABEND.                                                          
058300     MOVE SQLCODE     TO PV-SQLCODE.                                      
058400     DISPLAY '** ABEND           ** = SQLABEND'.                          
058500     DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
058600     DISPLAY '** PARAGRAPH       ** = ' PV-ABEND-PARA-NAME                
058700     DISPLAY '** TABLE AFFECTED  ** = ' PV-DB2-TABLE-NAME.                
058800     DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MSG-1.           
058900     DISPLAY '**                 ** = ' PV-DB2-OPERATION-MSG-2.           
059000     DISPLAY '** SQLCODE         ** = ' PV-SQLCODE.                       
059100     COPY DPPD004.                                                        
059200                                                                          
059300     MOVE +4013                  TO PV-ABEND-CODE.                        
059400     CALL 'ILBOABN0'   USING PV-ABEND-CODE.                               
059500*                                                                         
059600*                                                                         
