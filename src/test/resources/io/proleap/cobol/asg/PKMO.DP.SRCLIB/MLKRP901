       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             MLKRP901.                                        
       DATE-WRITTEN.           10/31/2000.                                      
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:  THE MODULE IS USED FOR FIRE DAMAGE STORE TRANSFERS. THE        
      *        MODULE SUMMARIZES THE PRICE CHANGE INFORMATION COMING            
      *        FROM THE MOUDLE MLKUT9XX. IT DETAILS THE INFORMATION BY          
      *        DEPARTMENT AND PROVIDES TOTALS AT THE DETAIL, DEPARTMENT,        
      *        AND TOTAL REPORT LEVEL.                                          
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
LY1003* LY1003   10-29-2003  CALIFORNIA STORE TO STORE TRANSFER MODS            
LY1003*                      -ROD JANSSEN                                       
W26600* W26600   07-15-2004  STORE EXPANSION CHANGES                            
W26600*                      -SCOTT JACKSON                                     
      * W26006   09-17-2004  EXPAND STORE NUMBER IN HEADING AND USE             
      *                      ML017-PC-STORE-NBR FOR STORE NUMBER INSTEAD        
      *                      OF ML017-PC-BATCH-NBR.  REPORT WILL NOW            
      *                      BREAK BY BATCH AND/OR STORE.                       
      *                      -V. LEE YANG                                       
      * W29145   11-20-2005  EXPAND MLRD017 COPYBOOK FOR SAS OPTIMIZE           
      *                      AND RELATED CHANGES. INPUT LRECL = 200             
      *                      -S. JACKSON                                        
PM3403*PM3403  04-20-2012    LARGE RETAIL RECOMPILE ONLY(MLRD017)          CL**6
PM3403*                      S. JACKSON                                    CL**6
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  FIRE-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT  REPORT-FILE                                                  
               ASSIGN RPT01.                                                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  FIRE-FILE                                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
W29145     RECORD CONTAINS 200 CHARACTERS                                       
           DATA RECORD IS ML017-PRICE-CHANGE-RECORD.                            
W26600*01  FIRE-RECORD                   PIC X(120).                            
W91450 01  FIRE-RECORD                   PIC X(200).                            
                                                                                
                                                                                
       FD  REPORT-FILE                                                          
           RECORDING MODE F                                                     
           LABEL RECORDS OMITTED                                                
           BLOCK CONTAINS  0 RECORDS.                                           
       01  REPORT-RECORD.                                                       
           05  REPORT-LINE               PIC X(132).                            
                                                                                
      /****************************************************************         
       WORKING-STORAGE SECTION.                                                 
      *****************************************************************         
                                                                                
       01  ABEND-CODE                PIC S9(04) VALUE ZERO COMP SYNC.           
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-LINE-COUNT         PIC S9(03)    COMP-3 VALUE 0.              
           05  PA-LINE-NO            PIC S9(03)    COMP-3 VALUE 0.              
           05  PA-PAGE-COUNT         PIC S9(05)    COMP-3 VALUE 0.              
           05  PA-IN-COUNT           PIC S9(09)    COMP-3 VALUE 0.              
           05  PA-TOTAL-READ         PIC S9(09)    COMP-3 VALUE 0.              
           05  PA-TOTAL-WRITTEN      PIC S9(09)    COMP-3 VALUE 0.              
           05  PA-OLD-PRICE-ACC      PIC S9(15)V99 COMP-3 VALUE 0.              
           05  PA-NEW-PRICE-ACC      PIC S9(15)V99 COMP-3 VALUE 0.              
W26600*    05  PA-QUANTITY-ACC       PIC S9(15)V99 COMP-3 VALUE 0.              
W26600     05  PA-QUANTITY-ACC       PIC S9(15)    COMP-3 VALUE 0.              
           05  PA-TOT-DOLLARS-ACC    PIC S9(15)V99 COMP-3 VALUE 0.              
           05  PA-OLD-PRICE-GR-ACC   PIC S9(15)V99 COMP-3 VALUE 0.              
           05  PA-NEW-PRICE-GR-ACC   PIC S9(15)V99 COMP-3 VALUE 0.              
           05  PA-QUANTITY-GR-ACC    PIC S9(15)V99 COMP-3 VALUE 0.              
           05  PA-TOT-DOLLARS-GR-ACC PIC S9(15)V99 COMP-3 VALUE 0.              
           05  PA-WORK-AMOUNT        PIC S9(15)V99 COMP-3 VALUE 0.              
LY1003     05  SA-OLD-PRICE-ACC      PIC S9(15)V99 COMP-3 VALUE 0.              
LY1003     05  SA-NEW-PRICE-ACC      PIC S9(15)V99 COMP-3 VALUE 0.              
LY1003     05  SA-QUANTITY-ACC       PIC S9(15)V99 COMP-3 VALUE 0.              
LY1003     05  SA-TOT-DOLLARS-ACC    PIC S9(15)V99 COMP-3 VALUE 0.              
LY1003     05  SA-DIFF-AMOUNT-ACC    PIC S9(15)V99 COMP-3 VALUE 0.              
                                                                                
       01  PC-CONSTANTS.                                                        
           05  PC-OPERCO-NBR         PIC  X(02) VALUE '03'.                     
           05  PC-MDSELV-DPT         PIC  X(03) VALUE 'DPT'.                    
LY1003     05  PC-MLK904-JOB         PIC  X(06) VALUE 'MLK904'.                 
           05  PC-MLKRP901-PGM       PIC  X(08) VALUE 'MLKRP901'.               
           05  PC-NO-INPUT-MSG.                                                 
               10  FILLER            PIC X(55)  VALUE SPACES.                   
               10  FILLER            PIC X(26)  VALUE                           
               '   =====> NO INPUT <===='.                                      
           05  PC-END-RPT-MSG.                                                  
               10  FILLER            PIC X(54)  VALUE SPACES.                   
               10  FILLER            PIC X(27)  VALUE                           
               '* * *  END OF REPORT  * * *'.                                   
LY1003     05  PC-MAX-LINES          PIC S9(03)  VALUE 63.                      
LY1003     05  PC-MAX-SUBTOTAL-LINES PIC S9(03)  VALUE 52.                      
LY1003     05  PC-MAX-GRTOTAL-LINES  PIC S9(03)  VALUE 56.                      
                                                                                
       01  PS-SWITCHES.                                                         
           05  PS-EOF-EXTRACT-SWITCH PIC X(01) VALUE 'N'.                       
               88  PS-EOF-EXTRACT              VALUE 'Y'.                       
                                                                                
       01  PV-VARIABLES.                                                        
LY1003     05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACE.          
LY1003     05  PV-DB2-OPER-ATTEMPTED        PIC  X(30)    VALUE SPACES.         
           05  PV-DEPT-NBR           PIC X(03)     VALUE SPACES.                
W26006     05  PV-LOC-NBR            PIC X(05)     VALUE SPACES.                
W26006     05  PV-LOC-NBR-9 REDEFINES PV-LOC-NBR                                
W26006                               PIC 9(05).                                 
LY1003     05  PV-BATCH-NMBR         PIC X(03)     VALUE SPACES.                
LY1003     05  PV-MAJ-CLASS.                                                    
LY1003         10  PV-MAJ-CLASS-P1   PIC X(02)     VALUE SPACES.                
LY1003         10  PV-MAJ-CLASS-P2   PIC X(02)     VALUE SPACES.                
           05  PV-RUN-TIME           PIC X(08)     VALUE SPACES.                
           05  PV-RUN-DATE           PIC X(10)     VALUE SPACES.                
           05  PV-PARAGRAPH          PIC X(30)     VALUE SPACES.                
           05  PV-OPERATION-ATTEMPTED  PIC X(40)   VALUE SPACES.                
           05  PV-ABEND-MESSAGE        PIC X(75)   VALUE SPACES.                
           05  PV-EFF-DTE-TIME       PIC X(26)     VALUE SPACES.                
           05  PV-EFF-DTE-TIME-RED REDEFINES                                    
               PV-EFF-DTE-TIME.                                                 
               10  PV-EFF-DTE        PIC X(10).                                 
               10  PV-EFF-TIME       PIC X(16).                                 
                                                                                
       01  H2-HEADING2.                                                         
           05  FILLER                PIC X(47) VALUE SPACES.                    
           05  FILLER                PIC X(40) VALUE                            
           '       FIRE DAMAGE SUMMARY REPORT    '.                             
           05  FILLER                PIC X(23) VALUE SPACES.                    
           05  FILLER                PIC X(05) VALUE 'JOB: '.                   
           05  H2-JOB-NAME           PIC X(06) VALUE SPACES.                    
                                                                                
LY1003 01  H3-STORE.                                                            
   |       05  FILLER                PIC X(02) VALUE SPACES.                    
LY1003     05  FILLER                PIC X(07) VALUE 'STORE: '.                 
W26006     05  H3-STORE-NMBR         PIC X(05) VALUE SPACES.                    
LY1003     05  FILLER                PIC X(01) VALUE SPACE.                     
   |       05  H3-LOC-NAME           PIC X(40) VALUE SPACES.                    
LY1003                                                                          
       01  H3-HEADING3.                                                         
LY1003     05  FILLER                PIC X(04) VALUE SPACES.                    
           05  FILLER                PIC X(07) VALUE 'DEPT:  '.                 
           05  H3-DEPT-NBR           PIC X(03) VALUE SPACES.                    
LY1003     05  FILLER                PIC X(01) VALUE SPACE.                     
LY1003     05  H3-DEPT-DESCRIP       PIC X(40) VALUE SPACES.                    
                                                                                
       01  H4-HEADING4.                                                         
           05  FILLER                PIC X(02) VALUE SPACES.                    
           05  FILLER                PIC X(05) VALUE 'BATCH'.                   
           05  FILLER                PIC X(03) VALUE SPACES.                    
           05  FILLER                PIC X(04) VALUE 'DEPT'.                    
           05  FILLER                PIC X(04) VALUE SPACES.                    
           05  FILLER                PIC X(05) VALUE 'CLASS'.                   
           05  FILLER                PIC X(02) VALUE SPACES.                    
507721     05  FILLER                PIC X(08) VALUE 'SKU     '.                
           05  FILLER                PIC X(01) VALUE SPACES.                    
           05  FILLER                PIC X(08) VALUE 'QUANTITY'.                
           05  FILLER                PIC X(07) VALUE SPACES.                    
           05  FILLER                PIC X(03) VALUE 'OLD'.                     
           05  FILLER                PIC X(17) VALUE SPACES.                    
           05  FILLER                PIC X(03) VALUE 'NEW'.                     
           05  FILLER                PIC X(06) VALUE SPACES.                    
           05  FILLER                PIC X(08) VALUE 'MRKDN/UP'.                
           05  FILLER                PIC X(02) VALUE SPACES.                    
           05  FILLER                PIC X(08) VALUE 'DOCUMENT'.                
           05  FILLER                PIC X(08) VALUE SPACES.                    
           05  FILLER                PIC X(05) VALUE 'TOTAL'.                   
                                                                                
       01  H5-HEADING5.                                                         
           05  FILLER                PIC X(02) VALUE SPACES.                    
           05  FILLER                PIC X(06) VALUE 'NUMBER'.                  
           05  FILLER                PIC X(17) VALUE SPACES.                    
           05  FILLER                PIC X(06) VALUE 'NUMBER'.                  
           05  FILLER                PIC X(18) VALUE SPACES.                    
           05  FILLER                PIC X(05) VALUE 'PRICE'.                   
           05  FILLER                PIC X(15) VALUE SPACES.                    
           05  FILLER                PIC X(05) VALUE 'PRICE'.                   
           05  FILLER                PIC X(04) VALUE SPACES.                    
           05  FILLER                PIC X(09) VALUE 'REAS CODE'.               
           05  FILLER                PIC X(17) VALUE SPACES.                    
           05  FILLER                PIC X(06) VALUE 'NUMBER'.                  
                                                                                
       01  DL1-DETAIL-LINE-1.                                                   
           05  FILLER                PIC X(02) VALUE SPACES.                    
           05  DL1-BATCH-NUMBER      PIC X(03).                                 
           05  FILLER                PIC X(05) VALUE SPACES.                    
           05  DL1-DEPT-NUMBER       PIC X(03).                                 
           05  FILLER                PIC X(05) VALUE SPACES.                    
           05  DL1-CLASS             PIC X(02).                                 
           05  FILLER                PIC X(05) VALUE SPACES.                    
507721     05  DL1-SKU               PIC X(08).                                 
507721     05  FILLER                PIC X(01) VALUE SPACES.                    
           05  DL1-QUANTITY          PIC ZZZ,ZZ9.                               
           05  FILLER                PIC X(02) VALUE SPACES.                    
           05  DL1-OLD-PRICE         PIC $$$,$$$,$$$.99-.                       
           05  FILLER                PIC X(05) VALUE SPACES.                    
           05  DL1-NEW-PRICE         PIC $$$,$$$,$$$.99-.                       
           05  FILLER                PIC X(03) VALUE SPACES.                    
           05  DL1-REASON-CODE       PIC X(03).                                 
           05  FILLER                PIC X(04) VALUE SPACES.                    
           05  DL1-DOCU-NUMBER       PIC X(07).                                 
           05  FILLER                PIC X(06) VALUE SPACES.                    
           05  DL1-TOTAL             PIC $$$,$$$,$$$.99-.                       
                                                                                
       01  TL1-TOTAL-LINE-1.                                                    
           05  FILLER                PIC X(02) VALUE SPACES.                    
           05  TL1-DESCRIPTION       PIC X(29) VALUE SPACES.                    
           05  FILLER                PIC X(01) VALUE SPACES.                    
           05  TL1-COUNT1            PIC Z,ZZZ,ZZ9.                             
           05  TL1-COUNT2            PIC $,$$$,$$$,$$$.99-.                     
           05  FILLER                PIC X(03) VALUE SPACES.                    
           05  TL1-COUNT3            PIC $,$$$,$$$,$$$.99-.                     
           05  FILLER                PIC X(21) VALUE SPACES.                    
           05  TL1-COUNT4            PIC $,$$$,$$$,$$$.99-.                     
                                                                                
      *****************************************************************         
      *  PRICE CHANGE LAYOUT                                                    
      *****************************************************************         
           COPY MLRD017.                                                        
                                                                                
      *****************************************************************         
      *  KOHL'S STANDARD FIRST HEADING LINE                                     
      *****************************************************************         
           COPY DPWS132O.                                                       
                                                                                
LY1003******************************************************************        
LY1003*  DB2 FORMATTED MESSAGES                                        *        
LY1003******************************************************************        
LY1003     COPY DPWS004.                                                        
LY1003                                                                          
LY1003******************************************************************        
LY1003*  REQUIRED SQL COMMUNICATIONS AREA                                       
LY1003******************************************************************        
LY1003     EXEC SQL                                                             
LY1003         INCLUDE SQLCA                                                    
LY1003     END-EXEC.                                                            
LY1003                                                                          
LY1003*****************************************************************         
LY1003     EXEC SQL                                                             
LY1003         INCLUDE XMT00001                                                 
LY1003     END-EXEC                                                             
LY1003                                                                          
LY1003******************************************************************        
LY1003     EXEC SQL                                                             
LY1003         INCLUDE TMDSEAR                                                  
LY1003     END-EXEC                                                             
LY1003                                                                          
LY1003******************************************************************        
LY1003     COPY SQLCA2.                                                         
LY1003                                                                          
      /****************************************************************         
      *FLOW:    THE MODULE SEQUENTIALLY READS THE FIRE DAMAGE PRICE             
      *      CHANGE FILE USING THE COPYBOOK MLRD017.                            
      *      TOTAL UP AT THE DETAIL, DEPARTMENT, AND CORPROATE LEVEL.           
      *                                                                         
      *****************************************************************         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-MAIN-DRIVER-SECTION.                                                
                                                                                
           PERFORM 1000-INITIALIZE                                              
              THRU 1000-INITIALIZE-EXIT.                                        
                                                                                
           PERFORM 2000-PROCESS-MAIN-LOOP                                       
              THRU 2000-PROCESS-MAIN-LOOP-EXIT                                  
                UNTIL PS-EOF-EXTRACT.                                           
                                                                                
           PERFORM 9000-EOJ-TASKS                                               
              THRU 9000-EOJ-TASKS-EXIT.                                         
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /****************************************************************         
      * 1000-INITIALIZATION ROUTINE.                                            
      *****************************************************************         
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  FIRE-FILE                                                
           OPEN OUTPUT REPORT-FILE.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE (3:2)  TO DP132O-RUN-YEAR.                
           MOVE FUNCTION CURRENT-DATE (5:2)  TO DP132O-RUN-MONTH.               
           MOVE FUNCTION CURRENT-DATE (7:2)  TO DP132O-RUN-DAY.                 
           MOVE FUNCTION CURRENT-DATE (9:2)  TO DP132O-RUN-HOUR.                
           MOVE FUNCTION CURRENT-DATE (11:2) TO DP132O-RUN-MINUTE.              
           MOVE PC-MLKRP901-PGM    TO DP132O-PROGRAM-NAME.                      
           MOVE 1                  TO DP132O-REPORT-NUMBER.                     
                                                                                
           MOVE ZEROS                TO PA-WORK-AMOUNT.                         
           MOVE ZEROS                TO PA-OLD-PRICE-ACC.                       
           MOVE ZEROS                TO PA-NEW-PRICE-ACC.                       
           MOVE ZEROS                TO PA-QUANTITY-ACC.                        
           MOVE ZEROS                TO PA-TOT-DOLLARS-ACC.                     
           MOVE ZEROS                TO PA-OLD-PRICE-GR-ACC.                    
           MOVE ZEROS                TO PA-NEW-PRICE-GR-ACC.                    
           MOVE ZEROS                TO PA-QUANTITY-GR-ACC.                     
           MOVE ZEROS                TO PA-TOT-DOLLARS-GR-ACC.                  
LY1003     MOVE ZEROS                TO SA-OLD-PRICE-ACC.                       
LY1003     MOVE ZEROS                TO SA-NEW-PRICE-ACC.                       
LY1003     MOVE ZEROS                TO SA-QUANTITY-ACC.                        
LY1003     MOVE ZEROS                TO SA-TOT-DOLLARS-ACC.                     
                                                                                
           PERFORM 4000-WRITE-REPORT-HEADING                                    
              THRU 4000-WRITE-REPORT-HEADING-EXIT.                              
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           IF PS-EOF-EXTRACT                                                    
               DISPLAY 'NO INPUT -- REPORT WILL BE EMPTY'                       
           ELSE                                                                 
               MOVE ML017-PC-DEPT-NBR    TO PV-DEPT-NBR                         
W26006         MOVE ML017-PC-STORE-NBR   TO PV-LOC-NBR                          
LY1003         MOVE ML017-PC-BATCH-NBR   TO PV-BATCH-NMBR                       
LY1003         MOVE ML017-PC-MAJ-CLASS   TO PV-MAJ-CLASS                        
               PERFORM 4200-SUB-HEADINGS                                        
                  THRU 4200-SUB-HEADINGS-EXIT                                   
           END-IF.                                                              
                                                                                
       1000-INITIALIZE-EXIT.                                                    
           EXIT.                                                                
                                                                                
      /****************************************************************         
      * 2000-MAIN PROCESS LOOP...                                               
      *****************************************************************         
       2000-PROCESS-MAIN-LOOP.                                                  
                                                                                
LY1003     IF ML017-PC-BATCH-NBR IS EQUAL TO PV-BATCH-NMBR                      
W26006        AND ML017-PC-STORE-NBR IS EQUAL TO PV-LOC-NBR                     
LY1003       IF ML017-PC-DEPT-NBR IS EQUAL TO PV-DEPT-NBR                       
LY1003         NEXT SENTENCE                                                    
LY1003       ELSE                                                               
               PERFORM 3200-DEPT-TOTALS                                         
                  THRU 3200-DEPT-TOTALS-EXIT                                    
               PERFORM 4000-WRITE-REPORT-HEADING                                
                  THRU 4000-WRITE-REPORT-HEADING-EXIT                           
               MOVE ML017-PC-DEPT-NBR    TO PV-DEPT-NBR                         
LY1003         MOVE ML017-PC-MAJ-CLASS   TO PV-MAJ-CLASS                        
               PERFORM 4200-SUB-HEADINGS                                        
                  THRU 4200-SUB-HEADINGS-EXIT                                   
LY1003       END-IF                                                             
LY1003     ELSE                                                                 
LY1003         PERFORM 3200-DEPT-TOTALS                                         
LY1003            THRU 3200-DEPT-TOTALS-EXIT                                    
LY1003         PERFORM 3400-STR-TOTALS                                          
LY1003            THRU 3400-STR-TOTALS-EXIT                                     
LY1003         PERFORM 4000-WRITE-REPORT-HEADING                                
LY1003            THRU 4000-WRITE-REPORT-HEADING-EXIT                           
LY1003         MOVE ML017-PC-DEPT-NBR    TO PV-DEPT-NBR                         
LY1003         MOVE ML017-PC-MAJ-CLASS   TO PV-MAJ-CLASS                        
LY1003         MOVE ML017-PC-BATCH-NBR   TO PV-BATCH-NMBR                       
W26006         MOVE ML017-PC-STORE-NBR   TO PV-LOC-NBR                          
LY1003         PERFORM 4200-SUB-HEADINGS                                        
LY1003            THRU 4200-SUB-HEADINGS-EXIT                                   
           END-IF.                                                              
                                                                                
           MOVE ML017-PC-DEPT-NBR     TO DL1-DEPT-NUMBER.                       
           MOVE ML017-PC-CLASS-NBR    TO DL1-CLASS.                             
507721     MOVE ML017-PC-SKU          TO DL1-SKU.                               
           MOVE ML017-PC-OLD-PR-AMT   TO DL1-OLD-PRICE.                         
           MOVE ML017-PC-NEW-PR-AMT   TO DL1-NEW-PRICE.                         
           MOVE ML017-PC-QTY          TO DL1-QUANTITY.                          
           MOVE ML017-PC-BATCH-NBR    TO DL1-BATCH-NUMBER.                      
           MOVE ML017-PC-MKDN-MKUP-RSN-CDE TO DL1-REASON-CODE.                  
           MOVE ML017-PC-DOC-7-NBR    TO DL1-DOCU-NUMBER.                       
                                                                                
           COMPUTE PA-WORK-AMOUNT ROUNDED                                       
                                      = ((ML017-PC-OLD-PR-AMT                   
                                        - ML017-PC-NEW-PR-AMT)                  
                                        *  ML017-PC-QTY).                       
           MOVE PA-WORK-AMOUNT        TO DL1-TOTAL.                             
                                                                                
           PERFORM 5000-WRITE-REPORT-LINE                                       
              THRU 5000-WRITE-REPORT-LINE-EXIT.                                 
                                                                                
           ADD ML017-PC-OLD-PR-AMT   TO PA-OLD-PRICE-ACC.                       
           ADD ML017-PC-NEW-PR-AMT   TO PA-NEW-PRICE-ACC.                       
           ADD ML017-PC-QTY          TO PA-QUANTITY-ACC.                        
           ADD PA-WORK-AMOUNT        TO PA-TOT-DOLLARS-ACC.                     
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       2000-PROCESS-MAIN-LOOP-EXIT.                                             
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3200-BUYOR SUB TOTAL GENERATION..                                       
      ******************************************************************        
       3200-DEPT-TOTALS.                                                        
                                                                                
           IF PA-LINE-NO IS GREATER THAN PC-MAX-SUBTOTAL-LINES                  
               PERFORM 4000-WRITE-REPORT-HEADING                                
                  THRU 4000-WRITE-REPORT-HEADING-EXIT                           
               PERFORM 4200-SUB-HEADINGS                                        
                  THRU 4200-SUB-HEADINGS-EXIT                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES              TO TL1-DESCRIPTION.                         
LY1003     STRING '  DEPARTMENT TOTAL - ' DELIMITED BY SIZE                     
                  PV-DEPT-NBR DELIMITED BY SIZE                                 
                  ': ' DELIMITED BY SIZE                                        
                                    INTO TL1-DESCRIPTION.                       
                                                                                
           MOVE PA-OLD-PRICE-ACC    TO TL1-COUNT2.                              
           MOVE PA-NEW-PRICE-ACC    TO TL1-COUNT3.                              
           MOVE PA-QUANTITY-ACC     TO TL1-COUNT1.                              
           MOVE PA-TOT-DOLLARS-ACC  TO TL1-COUNT4.                              
                                                                                
           WRITE REPORT-RECORD      FROM TL1-TOTAL-LINE-1                       
               AFTER ADVANCING 2 LINES.                                         
           ADD 2                    TO PA-LINE-NO.                              
                                                                                
           ADD PA-OLD-PRICE-ACC      TO PA-OLD-PRICE-GR-ACC.                    
           ADD PA-NEW-PRICE-ACC      TO PA-NEW-PRICE-GR-ACC.                    
           ADD PA-QUANTITY-ACC       TO PA-QUANTITY-GR-ACC.                     
           ADD PA-TOT-DOLLARS-ACC    TO PA-TOT-DOLLARS-GR-ACC.                  
LY1003                                                                          
LY1003     ADD PA-OLD-PRICE-ACC      TO SA-OLD-PRICE-ACC.                       
LY1003     ADD PA-NEW-PRICE-ACC      TO SA-NEW-PRICE-ACC.                       
LY1003     ADD PA-QUANTITY-ACC       TO SA-QUANTITY-ACC.                        
LY1003     ADD PA-TOT-DOLLARS-ACC    TO SA-TOT-DOLLARS-ACC.                     
LY1003                                                                          
           MOVE ZEROS                TO PA-OLD-PRICE-ACC.                       
           MOVE ZEROS                TO PA-NEW-PRICE-ACC.                       
           MOVE ZEROS                TO PA-QUANTITY-ACC.                        
           MOVE ZEROS                TO PA-TOT-DOLLARS-ACC.                     
                                                                                
       3200-DEPT-TOTALS-EXIT.                                                   
           EXIT.                                                                
                                                                                
      *****************************************************************         
      * 3300-GRAND TOTAL PAGE GENERATION...                                     
      *****************************************************************         
       3300-GRAND-TOTALS.                                                       
                                                                                
           PERFORM 4000-WRITE-REPORT-HEADING                                    
              THRU 4000-WRITE-REPORT-HEADING-EXIT.                              
                                                                                
           MOVE SPACES              TO PV-DEPT-NBR.                             
           PERFORM 4200-SUB-HEADINGS                                            
              THRU 4200-SUB-HEADINGS-EXIT.                                      
                                                                                
           MOVE SPACES              TO TL1-DESCRIPTION.                         
           MOVE 'GRAND TOTAL: '     TO TL1-DESCRIPTION.                         
           MOVE PA-OLD-PRICE-GR-ACC TO TL1-COUNT2.                              
           MOVE PA-NEW-PRICE-GR-ACC TO TL1-COUNT3.                              
           MOVE PA-QUANTITY-GR-ACC  TO TL1-COUNT1.                              
           MOVE PA-TOT-DOLLARS-GR-ACC TO TL1-COUNT4.                            
                                                                                
           WRITE REPORT-RECORD      FROM TL1-TOTAL-LINE-1                       
               AFTER ADVANCING 2 LINES.                                         
           ADD 2                    TO PA-LINE-NO.                              
                                                                                
       3300-GRAND-TOTALS-EXIT.                                                  
           EXIT.                                                                
                                                                                
LY1003******************************************************************        
   |  * 3400-STORE TOTAL GENERATION..                                           
   |  ******************************************************************        
   |   3400-STR-TOTALS.                                                         
   |                                                                            
   |       IF PA-LINE-NO IS GREATER THAN PC-MAX-SUBTOTAL-LINES                  
   |           PERFORM 4000-WRITE-REPORT-HEADING                                
   |              THRU 4000-WRITE-REPORT-HEADING-EXIT                           
   |           PERFORM 4200-SUB-HEADINGS                                        
   |              THRU 4200-SUB-HEADINGS-EXIT                                   
   |       END-IF.                                                              
   |                                                                            
LY1003     MOVE SPACES              TO TL1-DESCRIPTION.                         
W26006     STRING 'STORE/BATCH - '                                              
W26006            PV-LOC-NBR                                                    
W26006            '/'                                                           
LY1003            PV-BATCH-NMBR                                                 
   |              ': ' DELIMITED BY SIZE                                        
   |                                INTO TL1-DESCRIPTION.                       
   |                                                                            
   |       MOVE SA-OLD-PRICE-ACC    TO TL1-COUNT2.                              
   |       MOVE SA-NEW-PRICE-ACC    TO TL1-COUNT3.                              
   |       MOVE SA-QUANTITY-ACC     TO TL1-COUNT1.                              
   |       MOVE SA-TOT-DOLLARS-ACC  TO TL1-COUNT4.                              
   |                                                                            
   |       WRITE REPORT-RECORD      FROM TL1-TOTAL-LINE-1                       
   |           AFTER ADVANCING 2 LINES.                                         
   |       ADD 2                    TO PA-LINE-NO.                              
   |                                                                            
   |       MOVE ZEROS                TO SA-OLD-PRICE-ACC.                       
   |       MOVE ZEROS                TO SA-NEW-PRICE-ACC.                       
   |       MOVE ZEROS                TO SA-QUANTITY-ACC.                        
   |       MOVE ZEROS                TO SA-TOT-DOLLARS-ACC.                     
   |       MOVE ZEROS                TO SA-DIFF-AMOUNT-ACC.                     
   |                                                                            
   |   3400-STR-TOTALS-EXIT.                                                    
   |       EXIT.                                                                
LY1003                                                                          
      /*****************************************************************        
      * 4000-WRITE THE REPORT HEADING LINES.                                    
      ******************************************************************        
       4000-WRITE-REPORT-HEADING.                                               
                                                                                
           ADD 1                    TO PA-PAGE-COUNT.                           
           MOVE PA-PAGE-COUNT       TO DP132O-PAGE-NUMBER.                      
           WRITE REPORT-RECORD  FROM DP132O-STANDRD-HEADER-132-COLS             
                   AFTER ADVANCING PAGE.                                        
                                                                                
LY1003     MOVE PC-MLK904-JOB       TO H2-JOB-NAME.                             
           WRITE REPORT-RECORD      FROM H2-HEADING2                            
                   AFTER ADVANCING 1 LINE.                                      
           MOVE 2                   TO PA-LINE-NO.                              
                                                                                
       4000-WRITE-REPORT-HEADING-EXIT.                                          
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4200-WRITE THE REPORT SUB HEADING LINES.                                
      ******************************************************************        
       4200-SUB-HEADINGS.                                                       
                                                                                
LY1003*    MOVE PV-BATCH-NMBR       TO H3-STORE-NMBR.                           
W26006     MOVE PV-LOC-NBR          TO H3-STORE-NMBR.                           
LY1003     PERFORM 5100-LOCATION-NAME THRU                                      
LY1003             5100-LOCATION-NAME-EXIT.                                     
LY1003     WRITE REPORT-RECORD FROM H3-STORE                                    
LY1003         AFTER ADVANCING 2 LINES.                                         
LY1003                                                                          
           MOVE PV-DEPT-NBR         TO H3-DEPT-NBR.                             
LY1003     PERFORM 5200-DEPT-DESCRIP THRU                                       
LY1003             5200-DEPT-DESCRIP-EXIT.                                      
           WRITE REPORT-RECORD FROM H3-HEADING3                                 
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           WRITE REPORT-RECORD FROM H4-HEADING4                                 
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           WRITE REPORT-RECORD FROM H5-HEADING5                                 
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           MOVE SPACES         TO REPORT-RECORD.                                
           WRITE REPORT-RECORD AFTER ADVANCING 1 LINES.                         
           ADD 6               TO PA-LINE-NO.                                   
                                                                                
       4200-SUB-HEADINGS-EXIT.                                                  
           EXIT.                                                                
                                                                                
      /*****************************************************************        
      * 5000-WRITE THE REPORT DETAIL LINES.                                     
      ******************************************************************        
       5000-WRITE-REPORT-LINE.                                                  
                                                                                
           IF PA-LINE-NO IS GREATER THAN PC-MAX-LINES                           
               PERFORM 4000-WRITE-REPORT-HEADING                                
                  THRU 4000-WRITE-REPORT-HEADING-EXIT                           
               PERFORM 4200-SUB-HEADINGS                                        
                  THRU 4200-SUB-HEADINGS-EXIT                                   
           END-IF.                                                              
                                                                                
           WRITE REPORT-RECORD FROM DL1-DETAIL-LINE-1                           
                  AFTER ADVANCING 1 LINES.                                      
           ADD 1                    TO PA-LINE-NO.                              
                                                                                
       5000-WRITE-REPORT-LINE-EXIT.                                             
           EXIT.                                                                
                                                                                
LY1003******************************************************************        
LY1003* 5100-GET THE STORE LOCATION NAME FROM THE BATCH NUMBER.....             
LY1003******************************************************************        
LY1003 5100-LOCATION-NAME.                                                      
LY1003                                                                          
W26006     MOVE PV-LOC-NBR-9        TO XMT00001-LOC-NBR.                        
LY1003                                                                          
LY1003     EXEC SQL                                                             
LY1003         SELECT LOC_NM,                                                   
LY1003                LOC_NBR                                                   
LY1003         INTO :XMT00001-LOC-NM,                                           
LY1003              :XMT00001-LOC-NBR                                           
LY1003         FROM XMT_LOC                                                     
LY1003         WHERE LOC_NBR = :XMT00001-LOC-NBR                                
LY1003     END-EXEC                                                             
LY1003                                                                          
LY1003     EVALUATE TRUE                                                        
LY1003        WHEN SQLCODE IS EQUAL TO ZEROS                                    
LY1003            MOVE XMT00001-LOC-NM TO H3-LOC-NAME                           
LY1003        WHEN SQLCODE IS EQUAL TO +100                                     
LY1003            MOVE SPACES          TO H3-LOC-NAME                           
LY1003        WHEN OTHER                                                        
W26006             DISPLAY 'STORE      ' PV-LOC-NBR                             
LY1003             DISPLAY 'SQLCODE ' SQLCODE                                   
LY1003             MOVE '5100-LOCATION-NAME' TO PV-PARAGRAPH-NAME               
LY1003             MOVE 'SELECT XMT_LOC'     TO PV-DB2-OPER-ATTEMPTED           
LY1003             PERFORM 9999-SQL-ABEND                                       
LY1003     END-EVALUATE.                                                        
LY1003                                                                          
LY1003 5100-LOCATION-NAME-EXIT.                                                 
LY1003     EXIT.                                                                
LY1003                                                                          
LY1003******************************************************************        
LY1003* 5200-GET THE DEPARTMENT DESCRIPTION.                                    
LY1003******************************************************************        
LY1003 5200-DEPT-DESCRIP.                                                       
LY1003                                                                          
LY1003     MOVE PV-DEPT-NBR         TO MDSEAR-DEPT-NBR.                         
LY1003     MOVE PV-MAJ-CLASS-P2     TO MDSEAR-MAJ-CL-NBR.                       
LY1003                                                                          
LY1003     EXEC SQL                                                             
LY1003         SELECT DISTINCT                                                  
LY1003                DEPT_NBR                                                  
LY1003              , MAJ_CL_NBR                                                
LY1003              , MDSEAR_DESC                                               
LY1003         INTO :MDSEAR-DEPT-NBR                                            
LY1003             ,:MDSEAR-MAJ-CL-NBR                                          
LY1003             ,:MDSEAR-DESC                                                
LY1003         FROM TMDSEAR                                                     
LY1003         WHERE DEPT_NBR   = :MDSEAR-DEPT-NBR                              
LY1003           AND MAJ_CL_NBR = :MDSEAR-MAJ-CL-NBR                            
LY1003           AND MDSELV_CDE = 'MCL'                                         
W26600           AND CURRENT_DATE BETWEEN STRT_DTE AND END_DTE                  
LY1003     END-EXEC.                                                            
LY1003                                                                          
LY1003     EVALUATE TRUE                                                        
LY1003        WHEN SQLCODE IS EQUAL TO ZEROS                                    
LY1003            MOVE MDSEAR-DESC  TO H3-DEPT-DESCRIP                          
LY1003        WHEN SQLCODE IS EQUAL TO +100                                     
LY1003            MOVE SPACES       TO H3-DEPT-DESCRIP                          
LY1003        WHEN OTHER                                                        
LY1003             DISPLAY 'DEPT      ' PV-DEPT-NBR                             
LY1003             DISPLAY 'MAJ CLASS ' PV-MAJ-CLASS-P2                         
LY1003             DISPLAY 'SQLCODE ' SQLCODE                                   
LY1003             MOVE '5200-DEPT-DESCRIP' TO PV-PARAGRAPH-NAME                
LY1003             MOVE 'SELECT TMDSEAR'    TO PV-DB2-OPER-ATTEMPTED            
LY1003             PERFORM 9999-SQL-ABEND                                       
LY1003     END-EVALUATE.                                                        
LY1003                                                                          
LY1003 5200-DEPT-DESCRIP-EXIT.                                                  
LY1003     EXIT.                                                                
LY1003                                                                          
      /****************************************************************         
      * 8000-READ THE INPUT FILE....                                            
      *****************************************************************         
       8000-READ-INPUT-FILE.                                                    
                                                                                
W26600*    READ FIRE-FILE NEXT                                                  
W26600     READ FIRE-FILE INTO ML017-PRICE-CHANGE-RECORD                        
               AT END  SET PS-EOF-EXTRACT   TO TRUE.                            
                                                                                
           ADD 1                    TO PA-TOTAL-READ.                           
                                                                                
       8000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
      /************************************************************             
      * 9000-EOJ-TASKS.                                                         
      *************************************************************             
       9000-EOJ-TASKS.                                                          
                                                                                
           IF PA-TOTAL-READ IS EQUAL TO ZEROS                                   
              WRITE REPORT-RECORD FROM PC-NO-INPUT-MSG                          
                    AFTER ADVANCING 5 LINES                                     
           ELSE                                                                 
               PERFORM 3200-DEPT-TOTALS                                         
                  THRU 3200-DEPT-TOTALS-EXIT                                    
LY1003         PERFORM 3400-STR-TOTALS                                          
LY1003            THRU 3400-STR-TOTALS-EXIT                                     
               PERFORM 3300-GRAND-TOTALS                                        
                  THRU 3300-GRAND-TOTALS-EXIT                                   
           END-IF.                                                              
                                                                                
           WRITE REPORT-RECORD FROM PC-END-RPT-MSG                              
                AFTER ADVANCING 5 LINES.                                        
                                                                                
           CLOSE FIRE-FILE                                                      
                 REPORT-FILE.                                                   
                                                                                
       9000-EOJ-TASKS-EXIT.                                                     
           EXIT.                                                                
LY1003******************************************************************        
LY1003*  STANDARD DB2 ERROR ABEND ROUTINE                                       
LY1003*  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
LY1003******************************************************************        
LY1003 9999-SQL-ABEND.                                                          
LY1003                                                                          
LY1003     MOVE +4013                 TO ABEND-CODE.                            
LY1003     DISPLAY '**  ABEND     **'.                                          
LY1003     DISPLAY '**  PROGRAM   **  =  MLKRP900'.                             
LY1003     DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
LY1003     DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
LY1003                                                                          
LY1003*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
LY1003*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
LY1003                                                                          
LY1003     COPY DPPD004.                                                        
LY1003                                                                          
LY1003     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
