       IDENTIFICATION DIVISION.                                                 
      *=======================*                                                 
                                                                                
       PROGRAM-ID.         PEKUT719.                                            
       AUTHOR.             COGNIZANT.                                           
       INSTALLATION.       KOHL CORPORATION.                                    
       DATE-WRITTEN.       26/12/2010.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * Remarks                                                        *        
      * -------                                                        *        
      *                                                                *        
      * Title        - Get SKU's on Mark1 Clearance document for Perm  *        
      *                Clearance Report (CE2)                          *        
      *                                                                *        
      * Description  - This Program gets the SKU informations on MARK1 *        
      *                clearance document in approved status with      *        
      *                future effective date. The informations will be *        
      *                used to prepare perm Clearance Report For Reason*        
      *                code CE2.                                                
      *                CE2 - On Order After Mark 1                              
      * Change History                                                 *        
      ******************************************************************        
      * WR Number     Programmer       Date                            *        
      * ---------------------------------------------------------------*        
      * WR34795       COGNIZANT        26/12/2010                               
      * Original Version                                               *        
P53282* P53282  05-13-2015 COGNIZANT Union and distinct are removed in          
P53282*                              MARK1 Cursor to improve performance        
      ******************************************************************        
      * CHG0204126    COGNIZANT        07/30/2018                      *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
      *=====================*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PERM-MARK1-SKU-OUTFILE                                        
              ASSIGN                        TO RPT01.                           
                                                                                
           SELECT ORACLE-LOGON-FILE                                             
               ASSIGN                       TO ORALOGON.                        
                                                                                
       DATA DIVISION.                                                           
      *==============*                                                          
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PERM-MARK1-SKU-OUTFILE                                               
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS PERM-MARK1-SKU-REC.                                   
       01  PERM-MARK1-SKU-REC            PIC X(336).                            
                                                                                
       FD  ORACLE-LOGON-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
       01  OL-ORACLE-LOGON-RECORD      PIC X(80).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                    PIC S9(04) COMP VALUE +0.              
           88  ABEND-4001-WRITE-ERROR                VALUE +4001.               
           88  ABEND-4002-READ-ERROR                 VALUE +4002.               
           88  ABEND-4003-CTRL-CARD-ERROR            VALUE +4003.               
           88  ABEND-4004-TABLE-ERROR                VALUE +4004.               
           88  ABEND-4005-OPEN-ERROR                 VALUE +4005.               
           88  ABEND-4006-CLOSE-ERROR                VALUE +4006.               
           88  ABEND-4007-SEQUENCE-ERROR             VALUE +4007.               
           88  ABEND-4008-DATA-ERROR                 VALUE +4008.               
           88  ABEND-4009-KEY-DATA-ERROR             VALUE +4009.               
           88  ABEND-4013-SQL-ERROR                  VALUE +4013.               
           88  ABEND-4014-ORACLE-LOGON-ERROR         VALUE +4014.               
           88  ABEND-4019-DATE-ROUTINE-ERROR         VALUE +4019.               
           88  ABEND-4444-FORCED-ABEND               VALUE +4444.               
                                                                                
      *****************************************************************         
      * Program constants                                                       
      *****************************************************************         
       01  PC-PROGRAM-CONSTANTS.                                                
           03  PC-PROGRAM-NAME           PIC  X(08)  VALUE 'PEKUT719'.          
                                                                                
      *****************************************************************         
      * Program switches                                                        
      *****************************************************************         
       01  PS-PROGRAM-SWITCHES.                                                 
006510     05  PS-END-OF-LOGON-FILE-SW   PIC  X(01)  VALUE 'N'.                 
006530         88  PS-END-OF-LOGON-FILE              VALUE 'Y'.                 
           05  PS-ORA-SRC-CTL-DONE-SW    PIC  X(01)  VALUE 'N'.                 
               88  PS-ORA-SRC-EMPTY                  VALUE 'Y'.                 
           03  PS-MARK1-SKU-CSR-SW       PIC  X(01)  VALUE 'N'.                 
               88  EOF-MAR1-SKU-CSR                  VALUE 'Y'.                 
               88  NOT-EOF-MAR1-SKU-CSR              VALUE 'N'.                 
                                                                                
       01  CC-ORACLE-CONNECTION.                                                
           05  CC-ORACLE-USERID          PIC X(40).                             
               88 CC-AUTO-LOGON                      VALUE '/'.                 
           05  CC-ORACLE-PASSWORD        PIC X(40).                             
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ORACLE-USERID-LEN      PIC S9(04)  VALUE ZEROS                
                                                     COMP SYNC.                 
           05  PV-ORACLE-PASSWORD-LEN    PIC S9(04)  VALUE ZEROS                
                                                     COMP SYNC.                 
           03  PV-PARAGRAPH-NAME         PIC  X(30)  VALUE SPACES.              
           03  PV-TABLE-NAME             PIC  X(30)  VALUE SPACES.              
           03  PV-PREV-SKU-NBR           PIC  9(08)  VALUE 0.                   
           03  PV-OPERATION-MSG          PIC  X(60)  VALUE SPACES.              
           03  PV-MARK1-FILE.                                                   
               05  PV-DMA-NBR            PIC  9(04)  VALUE 0.                   
               05  PV-DMM-FRST-NM        PIC  X(20)  VALUE SPACES.              
               05  PV-DMM-LAST-NM        PIC  X(30)  VALUE SPACES.              
               05  PV-BYR-NBR            PIC  9(04)  VALUE 0.                   
               05  PV-FRST-NM            PIC  X(40)  VALUE SPACES.              
               05  PV-LAST-NM            PIC  X(40)  VALUE SPACES.              
               05  PV-DEPT-NBR           PIC  9(04)  VALUE 0.                   
               05  PV-DEPT-NM            PIC  X(20)  VALUE SPACES.              
               05  PV-SUB-CL-NBR         PIC  9(04)  VALUE 0.                   
               05  PV-VS-NBR             PIC  X(20)  VALUE SPACES.              
               05  PV-VS-DESC            PIC  X(30)  VALUE SPACES.              
               05  PV-SKU-NBR            PIC  9(08)  VALUE 0.                   
               05  PV-SKU-STAT-CDE       PIC  X(02)  VALUE SPACES.              
               05  PV-SKU-DESC           PIC  X(30)  VALUE SPACES.              
               05  PV-CSTM-CLOR-DESC     PIC  X(10)  VALUE SPACES.              
               05  PV-PRCHG-ID           PIC  9(10)  VALUE 0.                   
               05  PV-ALL-RMNG-STR-IND   PIC  X(01)  VALUE SPACES.              
               05  PV-EFF-DTE            PIC  X(10)  VALUE SPACES.              
               05  PV-PRCHG-TYP-CDE      PIC  X(03)  VALUE SPACES.              
               05  PV-PRCHG-ST-CDE       PIC  X(03)  VALUE SPACES.              
               05  PV-ITM-NBR            PIC  9(15)  VALUE 0.                   
               05  PV-STYL-ID            PIC  9(08)  VALUE 0.                   
               05  PV-INTR-UPC-ID        PIC  9(10)  VALUE 0.                   
               05  PV-SKU-STAT-DESC      PIC  X(10)  VALUE SPACES.              
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *----------------------------------------------------------------*        
      *    ORACLE DECLARE SECTION                                               
      *----------------------------------------------------------------*        
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      *****************************************************************         
      *    MARK1 Cursor - To Get SKU's on Mark1 Clearance document    *         
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE MARK1_SKU_CSR CURSOR FOR                                
                 SELECT DISTINCT                                                
                         XDM.DMA_NBR                                            
                        ,XDM.DMM_FRST_NM                                        
                        ,XDM.DMM_LAST_NM                                        
                        ,XDM.BYR_NBR                                            
                        ,XDM.BYR_FRST_NM                                        
                        ,XDM.BYR_LAST_NM                                        
                        ,VT1.DEPT_NBR                                           
                        ,XDM.DEPT_NM                                            
                        ,XVS.SUB_CL_NBR                                         
                        ,XVS.VS_NBR                                             
                        ,XVS.VS_DESC                                            
                        ,VT1.SKU_NBR                                            
                        ,XS.SKU_STAT_CDE                                        
                        ,XS.SKU_DESC                                            
                        ,XS.CSTM_CLOR_DESC                                      
                        ,VT1.PRCHG_ID                                           
                        ,VT1.ALL_RMNG_STR_IND                                   
                        ,VT1.EFF_DTE                                            
                        ,VT1.PRCHG_TYP_CDE                                      
                        ,VT1.PRCHG_ST_CDE                                       
                        ,XS.ITM_NBR                                             
                        ,XS.STYL_ID                                             
                        ,XS.INTR_UPC_ID                                         
                        ,XS.SKU_STAT_DESC                                       
                   FROM                                                         
P53282          (SELECT                                                         
                        E.SKU_NBR                                               
                       ,C.PRCHG_ID                                              
                       ,C.ALL_RMNG_STR_IND                                      
                       ,C.PRCHG_TYP_CDE                                         
                       ,C.PRCHG_ST_CDE                                          
                       ,A.EFF_DTE                                               
                       ,C.DEPT_NBR                                              
                       ,B.STR_SELN_ID                                           
                       ,C.CURR_STR_GP_TYP_CDE                                   
                   FROM PET_PERM_EVNT             A                             
                       ,PET_STR_GP_PRCHG_ASSN     B                             
                       ,PET_PRCHG                 C                             
                       ,PET_STR_GP_MDSE_LNE_PRICG D                             
                       ,PET_PRCHG_MDSE_SELN_LNE   E                             
                       ,PET_PRCHG_MDSE_SELN       F                             
                  WHERE A.PERM_EVNT_ID       = B.PERM_EVNT_ID                   
                    AND B.PRCHG_ID           = C.PRCHG_ID                       
                    AND D.STR_GP_PRCHG_ASSN_ID                                  
                                             = B.STR_GP_PRCHG_ASSN_ID           
                    AND D.PRCHG_MDSE_SELN_LNE_ID                                
                                             = E.PRCHG_MDSE_SELN_LNE_ID         
                    AND F.PRCHG_MDSE_SELN_ID = E.PRCHG_MDSE_SELN_ID             
                    AND F.MDSE_HIER_CDE      = 'SKU'                            
                    AND C.PRCHG_TYP_CDE      = 'RCP'                            
                    AND C.PRCHG_ST_CDE       = 'APR'                            
                    AND TRUNC(A.EFF_DTE)     > TRUNC(SYSDATE)                   
                    AND A.STAT_CDE           = 'S'                              
                    AND B.STAT_CDE           = 'S'                              
                    AND C.STAT_CDE           = 'S'                              
                    AND D.STAT_CDE           = 'S'                              
                    AND E.STAT_CDE           = 'S'                              
P53282              AND F.STAT_CDE           = 'S') VT1,                        
                        XKT_SKU        XS                                       
                       ,XKT_VND_STYL   XVS                                      
                       ,PMV_PRSN_MDSE_HIER XDM                                  
                  WHERE XS.SKU_NBR          = VT1.SKU_NBR                       
                    AND XVS.STYL_ID         = XS.STYL_ID                        
                    AND XDM.DEPT_NBR        = VT1.DEPT_NBR                      
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *==================*                                                      
                                                                                
      *****************************************************************         
      * Main processing flow                                                    
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM C300-PROCESS-MARK1-SKU                                       
             UNTIL EOF-MAR1-SKU-CSR.                                            
                                                                                
           PERFORM D900-CLOSE-PARA.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * Initialise fields and working storage                                   
      *****************************************************************         
       B100-INITIALIZE.                                                         
                                                                                
      *    PERFORM PM700-A000-ORACLE-SETUP.                                     
           PERFORM B110-ORACLE-SETUP.                                           
                                                                                
      ******************************************************************        
      * This copy book member contains the procedure division that     *        
      * logons to oracle.                                              *        
      ******************************************************************        
      *    COPY PMPD700.                                                        
                                                                                
           OPEN OUTPUT PERM-MARK1-SKU-OUTFILE.                                  
                                                                                
           PERFORM X100-OPEN-MARK1-SKU-CSR.                                     
                                                                                
           INITIALIZE                          PV-MARK1-FILE.                   
           PERFORM X200-FETCH-MARK1-SKU-CSR.                                    
           MOVE PV-SKU-NBR                  TO PV-PREV-SKU-NBR.                 
                                                                                
      *****************************************************************         
      *   C300-PROCESS-MARK1-SKU.                                               
      *****************************************************************         
       C300-PROCESS-MARK1-SKU.                                                  
                                                                                
           WRITE PERM-MARK1-SKU-REC       FROM PV-MARK1-FILE.                   
                                                                                
           INITIALIZE                          PV-MARK1-FILE.                   
           PERFORM X200-FETCH-MARK1-SKU-CSR.                                    
           MOVE PV-SKU-NBR                  TO PV-PREV-SKU-NBR.                 
                                                                                
      *****************************************************************         
      * B110-ORACLE-SETUP.                                            *         
      *****************************************************************         
       B110-ORACLE-SETUP.                                                       
                                                                                
           OPEN INPUT ORACLE-LOGON-FILE.                                        
                                                                                
           PERFORM B120-READ-ORACLE-LOGON.                                      
                                                                                
           CLOSE ORACLE-LOGON-FILE.                                             
                                                                                
           IF ((PS-END-OF-LOGON-FILE)                                           
                   OR (CC-ORACLE-CONNECTION = SPACES))                          
               DISPLAY '** ABEND **'                                            
               DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
                         'NOT PROVIDED'                                         
               SET PS-ORA-SRC-EMPTY TO TRUE                                     
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           IF CC-AUTO-LOGON                                                     
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
               EXEC SQL                                                         
                    CONNECT :USERNAME                                           
               END-EXEC                                                         
           ELSE                                                                 
               INITIALIZE PV-ORACLE-USERID-LEN                                  
                          PV-ORACLE-PASSWORD-LEN                                
               INSPECT CC-ORACLE-USERID                                         
                   TALLYING PV-ORACLE-USERID-LEN                                
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            INSPECT CC-ORACLE-PASSWORD                                          
                TALLYING PV-ORACLE-PASSWORD-LEN                                 
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
                      USERNAME-ARR                                              
            MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
            MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
                      PASSWD-ARR                                                
            MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
            DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
                      USERNAME-ARR                                              
            EXEC SQL                                                            
              CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
            END-EXEC                                                            
            INITIALIZE CC-ORACLE-PASSWORD                                       
                       PASSWD                                                   
            END-IF.                                                             
                                                                                
020500     IF (SQLCODE NOT = 0)                                                 
020900         DISPLAY PV-PARAGRAPH-NAME                                        
020910         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
021000*        PERFORM 9900-ORACLE-ABEND                                        
               PERFORM Z100-ABEND                                               
021200     END-IF.                                                              
                                                                                
      *****************************************************************         
      *  B120-READ-ORACLE-LOGON.                                      *         
      *****************************************************************         
       B120-READ-ORACLE-LOGON.                                                  
                                                                                
034510     READ ORACLE-LOGON-FILE                                               
034520         INTO CC-ORACLE-CONNECTION                                        
034530         AT END                                                           
034540             SET PS-END-OF-LOGON-FILE TO TRUE                             
034550     END-READ.                                                            
                                                                                
      *****************************************************************         
      *    CLOSE FILES                                                          
      *****************************************************************         
       D900-CLOSE-PARA.                                                         
                                                                                
           CLOSE                            PERM-MARK1-SKU-OUTFILE.             
                                                                                
           PERFORM X300-CLOSE-MARK1-SKU-CSR.                                    
                                                                                
      ***********************************************************               
      * X100-OPEN-MARK1-SKU-CSR.                                                
      ***********************************************************               
       X100-OPEN-MARK1-SKU-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                OPEN MARK1_SKU_CSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE 'X100-OPEN-MARK1-SKU-CSR'                              
                                            TO PV-PARAGRAPH-NAME                
                    MOVE 'ERROR ON OPEN MARK1_SKU_CSR'                          
                                            TO PV-OPERATION-MSG                 
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      ***********************************************************               
      * X200-FETCH-MARK1-SKU-CSR.                                               
      ***********************************************************               
       X200-FETCH-MARK1-SKU-CSR.                                                
                                                                                
           EXEC SQL                                                             
                FETCH  MARK1_SKU_CSR                                            
                 INTO :PV-DMA-NBR                                               
                     ,:PV-DMM-FRST-NM                                           
                     ,:PV-DMM-LAST-NM                                           
                     ,:PV-BYR-NBR                                               
                     ,:PV-FRST-NM                                               
                     ,:PV-LAST-NM                                               
                     ,:PV-DEPT-NBR                                              
                     ,:PV-DEPT-NM                                               
                     ,:PV-SUB-CL-NBR                                            
                     ,:PV-VS-NBR                                                
                     ,:PV-VS-DESC                                               
                     ,:PV-SKU-NBR                                               
                     ,:PV-SKU-STAT-CDE                                          
                     ,:PV-SKU-DESC                                              
                     ,:PV-CSTM-CLOR-DESC                                        
                     ,:PV-PRCHG-ID                                              
                     ,:PV-ALL-RMNG-STR-IND                                      
                     ,:PV-EFF-DTE                                               
                     ,:PV-PRCHG-TYP-CDE                                         
                     ,:PV-PRCHG-ST-CDE                                          
                     ,:PV-ITM-NBR                                               
                     ,:PV-STYL-ID                                               
                     ,:PV-INTR-UPC-ID                                           
                     ,:PV-SKU-STAT-DESC                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = 1403                                              
                    SET EOF-MAR1-SKU-CSR    TO TRUE                             
               WHEN OTHER                                                       
                    MOVE 'X200-FETCH-MARK1-SKU-CSR'                             
                                            TO PV-PARAGRAPH-NAME                
                    DISPLAY 'LAST SKU_NBR FETCHED = ' PV-PREV-SKU-NBR           
                    MOVE 'ERROR ON FETCH MARK1_SKU_CSR'                         
                                            TO PV-OPERATION-MSG                 
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      ***********************************************************               
      * X300-CLOSE-MARK1-SKU-CSR                                                
      ***********************************************************               
       X300-CLOSE-MARK1-SKU-CSR.                                                
                                                                                
            EXEC SQL                                                            
                 CLOSE MARK1_SKU_CSR                                            
            END-EXEC.                                                           
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE 'X300-CLOSE-MARK1-SKU-CSR'                             
                                            TO PV-PARAGRAPH-NAME                
                    MOVE 'ERROR ON CLOSE MARK1_SKU_CSR'                         
                                            TO PV-OPERATION-MSG                 
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * Z100-ABEND.                                                   *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE            *         
      *****************************************************************         
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
