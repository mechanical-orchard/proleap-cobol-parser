00001 *                                                                 08/22/94
00002  IDENTIFICATION DIVISION.                                         PCKUT004
00003 *                                                                    LV005
00004  PROGRAM-ID.    PCKUT004.                                            CL**3
00005 *                                                                 PCKUT004
00006  AUTHOR.        CAL BUSH.                                         PCKUT004
00007  INSTALLATION.  KOHLS DEPARTMENT STORES.                          PCKUT004
00008  DATE-WRITTEN.  08/17/94.                                         PCKUT004
00009  DATE-COMPILED.                                                   PCKUT004
00010 *                                                                 PCKUT004
00011 ***************************************************************** PCKUT004
00012 **                                                             **    CL**5
00013 ** THIS PROGRAM STRIPS TRAILING BLANKS FROM THE LABEL FILES,   **    CL**5
00014 ** CREATING A VARIABLE LENGTH RECORD FILE.  THAT FILE CAN THEN **    CL**5
00015 ** BE DOWNLOADED THROUGH FTP TO THE LABEL PRINTING PC.         **    CL**5
00016 **                                                             **    CL**5
00017 ***************************************************************** PCKUT004
00018      EJECT                                                        PCKUT004
00019  ENVIRONMENT DIVISION.                                            PCKUT004
00020 *                                                                 PCKUT004
00021  CONFIGURATION SECTION.                                           PCKUT004
00022 *                                                                 PCKUT004
00023  SOURCE-COMPUTER.  IBM-3090.                                      PCKUT004
00024  OBJECT-COMPUTER.  IBM-3090.                                      PCKUT004
00025 *                                                                 PCKUT004
00026  INPUT-OUTPUT SECTION.                                            PCKUT004
00027 *                                                                 PCKUT004
00028  FILE-CONTROL.                                                    PCKUT004
00029      SELECT INPUT-LABEL-FILE          ASSIGN TO INP01.            PCKUT004
00030      SELECT OUTPUT-LABEL-FILE         ASSIGN TO OUT01.            PCKUT004
00031      EJECT                                                        PCKUT004
00032  DATA DIVISION.                                                   PCKUT004
00033 *                                                                 PCKUT004
00034  FILE SECTION.                                                    PCKUT004
00035 *                                                                 PCKUT004
00036  FD  INPUT-LABEL-FILE                                             PCKUT004
00037      RECORDING MODE IS F                                          PCKUT004
00038      LABEL RECORDS ARE STANDARD                                   PCKUT004
00039      RECORD CONTAINS 80 CHARACTERS.                               PCKUT004
00040  01  INPUT-LABEL-RECORD               PIC  X(80).                 PCKUT004
00041 *                                                                 PCKUT004
00042  FD  OUTPUT-LABEL-FILE                                            PCKUT004
00043      RECORDING MODE IS V                                          PCKUT004
00044      LABEL RECORDS ARE STANDARD                                   PCKUT004
00045      RECORD IS VARYING FROM 1 TO 80                               PCKUT004
00046      DEPENDING ON PV-BYTE-COUNT                                   PCKUT004
00047      DATA RECORD IS OUTPUT-LABEL-RECORD.                          PCKUT004
00048  01  OUTPUT-LABEL-RECORD.                                         PCKUT004
00049                                                                   PCKUT004
00050      05  OUTPUT-LABEL-BYTE         PIC  X(01)                     PCKUT004
00051                                    OCCURS 1 TO 80 TIMES           PCKUT004
00052                                    DEPENDING ON PV-BYTE-COUNT.    PCKUT004
00053      EJECT                                                        PCKUT004
00054  WORKING-STORAGE SECTION.                                         PCKUT004
00055  01  ABEND-CODE                    PIC S9(04)  VALUE +0           PCKUT004
00056                                                COMP               PCKUT004
00057                                                SYNC.              PCKUT004
00058                                                                   PCKUT004
00059 *                                                                 PCKUT004
00060  01  PC-PROGRAM-CONSTANTS.                                        PCKUT004
00061      05  PC-BYTE-MAX               PIC S9(04)  VALUE +80          PCKUT004
00062                                                COMP.              PCKUT004
00063 *                                                                 PCKUT004
00064  01  PV-PROGRAM-VARIABLES.                                        PCKUT004
00065      05  PV-BYTE-COUNT             PIC  9(04)  VALUE ZERO         PCKUT004
00066                                                COMP.              PCKUT004
00067      05  PV-TRAILING-SPACE         PIC S9(04)  VALUE ZERO         PCKUT004
00068                                                COMP SYNC.         PCKUT004
00069 *                                                                    CL**5
00070  01  PS-PROGRAM-SWITCHES.                                            CL**5
00071      05  PS-EOF-SW                 PIC  X(01)  VALUE 'N'.            CL**5
00072          88  PS-EOF                            VALUE 'Y'.            CL**5
00073 ****************************************************************     CL**5
00074 ** THIS IS THE RECORD LAYOUT FOR THE INPUT AND OUTPUT RECORDS **     CL**5
00075 ** USED IN THIS PROGRAM.                                      **     CL**5
00076 ****************************************************************     CL**5
00077   01  ILR-INPUT-LABEL-RECORD.                                     PCKUT004
00078       05  ILR-INPUT-LABEL-BYTE     PIC  X(01)  OCCURS 80 TIMES    PCKUT004
00079                                                INDEXED BY ILR-IDX.PCKUT004
00080 *                                                                    CL**5
00081  EJECT                                                            PCKUT004
00082  PROCEDURE DIVISION.                                              PCKUT004
00083 ***************************************************************** PCKUT004
00084 **  THIS MODULE CONTROLS ALL PROCESSING DONE IN THIS PROGRAM.  **    CL**5
00085 ***************************************************************** PCKUT004
00086  0000-MAIN-DRIVER.                                                PCKUT004
00087      PERFORM 1000-INITIALIZATION.                                 PCKUT004
00088      PERFORM 2000-PROCESS-INPUT                                   PCKUT004
00089          UNTIL PS-EOF.                                               CL**5
00090      PERFORM 3000-TERMINATION.                                    PCKUT004
00091                                                                   PCKUT004
00092      GOBACK.                                                      PCKUT004
00093 ****************************************************************  PCKUT004
00094 ** OPEN FILES AND DO THE PRIMING READ.                        **     CL**5
00095 ****************************************************************  PCKUT004
00096  1000-INITIALIZATION.                                             PCKUT004
00097      OPEN INPUT  INPUT-LABEL-FILE                                    CL**2
00098           OUTPUT OUTPUT-LABEL-FILE.                               PCKUT004
00099      PERFORM 2100-READ-INPUT-LABEL.                               PCKUT004
00100 ****************************************************************  PCKUT004
00101 ** SEARCH BACWARDS THROUGH THE INPUT RECORD, LOOKING FOR THE  **     CL**5
00102 ** FIRST NON-SPACE CHARACTER ENCOUNTERED.  WHEN THE NON-SPACE **     CL**5
00103 ** IS FOUND, WRITE THE RECORD TO THE VARIABLE-RECORD OUTPUT   **     CL**5
00104 ** FILE.                                                      **     CL**5
00105 **                                                            **     CL**5
00106 ** IT'S INVALID TO WRITE A RECORD WITH ZERO BYTES, SO THE PER-**     CL**4
00107 ** FORM IS DONE UNTIL IDX IS LESS THAN 2.                     **     CL**4
00108 ****************************************************************  PCKUT004
00109  2000-PROCESS-INPUT.                                                 CL**2
00110      PERFORM                                                      PCKUT004
00111          VARYING ILR-IDX                                          PCKUT004
00112          FROM PC-BYTE-MAX                                         PCKUT004
00113          BY -1                                                    PCKUT004
00114          UNTIL ILR-INPUT-LABEL-BYTE (ILR-IDX) NOT = SPACE         PCKUT004
00115             OR ILR-IDX < 2                                           CL**4
00116      END-PERFORM.                                                 PCKUT004
00117 *                                                                 PCKUT004
00118      SET PV-BYTE-COUNT TO ILR-IDX.                                PCKUT004
00119      MOVE ILR-INPUT-LABEL-RECORD(1:PV-BYTE-COUNT)                 PCKUT004
00120                               TO OUTPUT-LABEL-RECORD.             PCKUT004
00121      PERFORM 2200-WRITE-OUTPUT-LABEL.                             PCKUT004
00122                                                                   PCKUT004
00123      PERFORM 2100-READ-INPUT-LABEL.                               PCKUT004
00124                                                                   PCKUT004
00125 ****************************************************************     CL**3
00126 ** READ A RECORD FROM THE INPUT FILE.                         **     CL**5
00127 ****************************************************************     CL**3
00128  2100-READ-INPUT-LABEL.                                           PCKUT004
00129      READ INPUT-LABEL-FILE INTO ILR-INPUT-LABEL-RECORD            PCKUT004
00130          AT END                                                   PCKUT004
00131              SET PS-EOF TO TRUE.                                     CL**5
00132 ****************************************************************     CL**3
00133 ** WRITE THE RECORD TO THE OUTPUT FILE.                       **     CL**5
00134 ****************************************************************     CL**3
00135  2200-WRITE-OUTPUT-LABEL.                                         PCKUT004
00136      WRITE OUTPUT-LABEL-RECORD.                                   PCKUT004
00137                                                                      CL**3
00138 ****************************************************************     CL**3
00139 ** CLOSE FILES                                                **     CL**5
00140 ****************************************************************     CL**3
00141  3000-TERMINATION.                                                   CL**3
00142      CLOSE INPUT-LABEL-FILE                                          CL**3
00143            OUTPUT-LABEL-FILE.                                        CL**3
