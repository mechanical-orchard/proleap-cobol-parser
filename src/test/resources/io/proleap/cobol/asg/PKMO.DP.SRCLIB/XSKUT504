       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.      XSKUT504.                                       00030000
       AUTHOR.          RICH KELLEN.                                    00040000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00050000
       DATE-WRITTEN.    07/31/2012.                                     00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *  MODULE TYPE:   COBOL2 BATCH                                    00100000
      *                                                                 00110000
      *  DESCRIPTION:   CREATE DELTA FILE FOR ASSOCIATE EXTRACT.        00120000
      *                 THIS PGM COMPARES PREVIOUS EXTRACT FILE TO THE  00130000
      *                 CURRENT AND CREATES DELTA FILE FOR ASSOCIATE    00140000
      *                 DATA FILE TO REDUCE THE                         00150000
      *                 AMOUNT OF RECORDS SENT TO EDW.                  00160000
      *========================== CHANGE LOG ===========================00170000
      *  AUTH      BY         DATE      DESCRIPTION                     00180000
      *=================================================================00190000
      * WRXXXXX  R.KELLEN   07-31-12  CREATE PROGRAM                    00200000
      ******************************************************************00210000
                                                                        00220000
       ENVIRONMENT DIVISION.                                            00230000
       CONFIGURATION SECTION.                                           00240000
                                                                        00250000
       SOURCE-COMPUTER.        IBM-3090.                                00260000
       OBJECT-COMPUTER.        IBM-3090.                                00270000
                                                                        00280000
       INPUT-OUTPUT SECTION.                                            00290000
       FILE-CONTROL.                                                    00300000
                                                                        00310000
           SELECT STFG-CDE-PREV-INPUT-FILE ASSIGN TO INP01.             00320000
           SELECT STFG-CDE-CURR-INPUT-FILE ASSIGN TO INP02.             00330000
           SELECT STFG-CDE-DELTA-FILE    ASSIGN TO OUT01.               00340000
                                                                        00350000
       DATA DIVISION.                                                   00360000
       FILE SECTION.                                                    00370000
                                                                        00380000
       FD  STFG-CDE-PREV-INPUT-FILE                                     00390000
           LABEL RECORDS STANDARD                                       00400000
           RECORDING MODE IS F                                          00410000
           BLOCK CONTAINS 0 RECORDS.                                    00420000
                                                                        00430000
       01  STFG-CDE-PREV-INPUT-REC       PIC  X(15).                    00440000
                                                                        00450000
       FD  STFG-CDE-CURR-INPUT-FILE                                     00460000
           LABEL RECORDS STANDARD                                       00470000
           RECORDING MODE IS F                                          00480000
           BLOCK CONTAINS 0 RECORDS.                                    00490000
                                                                        00500000
       01  STFG-CDE-CURR-INPUT-REC       PIC  X(15).                    00510000
                                                                        00520000
       FD  STFG-CDE-DELTA-FILE                                          00530000
           LABEL RECORDS STANDARD                                       00540000
           RECORDING MODE IS F                                          00550000
           BLOCK CONTAINS 0 RECORDS.                                    00560000
                                                                        00570000
       01  STFG-CDE-DELTA-FILE-REC       PIC X(15).                     00580000
                                                                        00590000
      ******************************************************************00600000
      *                       WORKING STORAGE                           00610000
      ******************************************************************00620000
       WORKING-STORAGE SECTION.                                         00630000
                                                                        00640000
       01  W-START                       PIC  X(40)                     00650000
           VALUE 'XSKUT504 - WORKING STORAGE BEGINS HERE'.              00660000
                                                                        00670000
      ******************************************************************00680000
      *                  SWITCHES / INDICATORS                          00690000
      ******************************************************************00700000
       01  PS-PROGRAM-SWITCHES.                                         00710000
           05  PS-END-OF-INPUT-FILE01-SW PIC  X(01)  VALUE 'N'.         00720000
               88  PS-END-OF-INPUT-FILE01            VALUE 'Y'.         00730000
               88  PS-NOT-END-OF-INPUT-FILE01        VALUE 'N'.         00740000
           05  PS-END-OF-INPUT-FILE02-SW PIC  X(01)  VALUE 'N'.         00750000
               88  PS-END-OF-INPUT-FILE02            VALUE 'Y'.         00760000
               88  PS-NOT-END-OF-INPUT-FILE02        VALUE 'N'.         00770000
                                                                        00780000
      ******************************************************************00790000
      *                          CONSTANTS                              00800000
      ******************************************************************00810000
       01  PC-PROGRAM-CONSTANTS.                                        00820000
           05  PC-PROGRAM-NAME           PIC  X(08)  VALUE 'XSKUT504'.  00830000
           05  PC-LAB-RECORDS-WRITTEN    PIC  9(08)  VALUE 0.           00840000
                                                                        00850000
      ******************************************************************00860000
      *                          VARIABLES                              00870000
      ******************************************************************00880000
       01  PV-PROGRAM-VARIABLES.                                        00890000
           05  PV-NBR                    PIC +9(04).                    00900000
           05  PV-PARAGRAPH-NAME         PIC  X(30)  VALUE SPACES.      00910000
           05  PV-TALLY                  PIC  9(02)  VALUE ZEROS.       00920000
           05  PV-END-OF-PREV-REC        PIC  9(02)  VALUE ZEROS.       00930000
           05  PV-END-OF-CURR-REC        PIC  9(02)  VALUE ZEROS.       00940000
           05  PV-SUB                    PIC  9(02)  VALUE ZEROS.       00950000
      ******************************************************************00960000
      *                        ERROR HANDLING                           00970000
      ******************************************************************00980000
       01  ABEND-CODE                    PIC S9(4)   VALUE ZEROS        00990000
                                                     COMP SYNC.         01000000
           88  AC-FORCED-ABEND                       VALUE +4444.       01010000
           88  AC-DATE-ROUTINE-ERROR                 VALUE +4019.       01020000
                                                                        01030000
      ******************************************************************01040000
      *                        COPY BOOKS                               01050000
      ******************************************************************01060000
           COPY XSRD152.                                                01070000
                                                                        01080000
           COPY XSRD153.                                                01090000
                                                                        01100000
           COPY XSRD154.                                                01110000
                                                                        01120000
      ******************************************************************01130000
       PROCEDURE DIVISION.                                              01140000
      ******************************************************************01150000
      * MAIN DRIVER                                                     01160000
      ******************************************************************01170000
                                                                        01180000
           PERFORM 1000-INITIALIZE.                                     01190000
           IF PS-END-OF-INPUT-FILE01                                    01200000
            AND PS-END-OF-INPUT-FILE02                                  01210000
             DISPLAY 'NO PREV OR CURR LAB FILES'                        01220000
             DISPLAY 'NO DELTA RECORDS WILL BE PRODUCED'                01230000
           ELSE                                                         01240000
             PERFORM 1400-PROCESS-LAB-REC                               01250000
             UNTIL PS-END-OF-INPUT-FILE01                               01260000
               AND PS-END-OF-INPUT-FILE02                               01270000
           END-IF.                                                      01280000
                                                                        01290000
           PERFORM 9000-CLOSE-FILES.                                    01300000
                                                                        01310000
           GOBACK.                                                      01320000
                                                                        01330000
      ******************************************************************01340000
      * PREFORMS TASK INITIATION                                        01350000
      ******************************************************************01360000
       1000-INITIALIZE.                                                 01370000
           OPEN INPUT  STFG-CDE-PREV-INPUT-FILE                         01380000
                       STFG-CDE-CURR-INPUT-FILE                         01390000
                OUTPUT STFG-CDE-DELTA-FILE.                             01400000
                                                                        01410000
           DISPLAY 'PGM XSKUT504'.                                      01420000
           PERFORM 1200-STFG-CDE-PREV-INPUT-FILE.                       01430000
           IF  PS-END-OF-INPUT-FILE01                                   01440000
               DISPLAY 'NO PREVIOUS LAB EXTR FILE'                      01450000
           END-IF.                                                      01460000
           PERFORM 1250-STFG-CDE-CURR-INPUT-FILE.                       01470000
           IF  PS-END-OF-INPUT-FILE02                                   01480000
               DISPLAY 'NO CURREMT LAB EXTR FILE'                       01490000
           END-IF.                                                      01500000
           MOVE SPACE TO XS154-DELTA-REC.                               01510000
           MOVE '|' TO XS154-DEL-1                                      01520000
                       XS154-DEL-2.                                     01530000
                                                                        01540000
      ******************************************************************01550000
       1200-STFG-CDE-PREV-INPUT-FILE.                                   01560000
      ******************************************************************01570000
           IF PS-END-OF-INPUT-FILE01                                    01580000
             NEXT SENTENCE                                              01590000
           ELSE                                                         01600000
           READ STFG-CDE-PREV-INPUT-FILE INTO XS152-PREV-STFG-CDE-REC   01610000
                AT END SET PS-END-OF-INPUT-FILE01 TO TRUE               01620000
                MOVE ALL '9' TO XS152-PREV-LOC-NBR                      01630000
           END-READ.                                                    01640000
                                                                        01650000
      ******************************************************************01660000
       1250-STFG-CDE-CURR-INPUT-FILE.                                   01670000
      ******************************************************************01680000
           IF PS-END-OF-INPUT-FILE02                                    01690000
             NEXT SENTENCE                                              01700000
           ELSE                                                         01710000
           READ STFG-CDE-CURR-INPUT-FILE INTO XS153-CURR-STFG-CDE-REC   01720000
                AT END SET PS-END-OF-INPUT-FILE02 TO TRUE               01730000
                MOVE ALL '9' TO XS153-CURR-LOC-NBR                      01740000
           END-READ.                                                    01750000
                                                                        01760000
      ******************************************************************01770000
      * PROCESS LABOR RECORDS                                           01780000
      ******************************************************************01790000
       1400-PROCESS-LAB-REC.                                            01800000
           IF XS152-PREV-LOC-NBR < XS153-CURR-LOC-NBR                   01810000
             MOVE XS152-PREV-LOC-NBR (2:4) TO XS154-LOC-NBR             01820000
             MOVE XS152-PREV-STFG-CDE    TO XS154-STFG-CDE              01830000
             MOVE 'D'                    TO XS154-ACT-CDE               01840000
             PERFORM 2000-WRITE-STFG-CDE-DELTA-REC                      01850000
             PERFORM 1200-STFG-CDE-PREV-INPUT-FILE                      01860000
           ELSE                                                         01870000
           IF XS152-PREV-LOC-NBR > XS153-CURR-LOC-NBR                   01880000
             MOVE XS153-CURR-LOC-NBR (2:4) TO XS154-LOC-NBR             01890000
             MOVE XS153-CURR-STFG-CDE    TO XS154-STFG-CDE              01900000
             MOVE 'A'                    TO XS154-ACT-CDE               01910000
             PERFORM 2000-WRITE-STFG-CDE-DELTA-REC                      01920000
             PERFORM 1250-STFG-CDE-CURR-INPUT-FILE                      01930000
           ELSE                                                         01940000
             IF STFG-CDE-PREV-INPUT-REC = STFG-CDE-CURR-INPUT-REC       01950000
               PERFORM 1200-STFG-CDE-PREV-INPUT-FILE                    01960000
               PERFORM 1250-STFG-CDE-CURR-INPUT-FILE                    01970000
             ELSE                                                       01980000
               MOVE SPACE                  TO STFG-CDE-DELTA-FILE-REC   01990000
               MOVE STFG-CDE-CURR-INPUT-REC TO STFG-CDE-DELTA-FILE-REC  02000000
               MOVE 'U' TO XS154-ACT-CDE                                02010000
               PERFORM 2000-WRITE-STFG-CDE-DELTA-REC                    02020000
               PERFORM 1200-STFG-CDE-PREV-INPUT-FILE                    02030000
               PERFORM 1250-STFG-CDE-CURR-INPUT-FILE.                   02040000
                                                                        02050000
      ****************************************************************  02060000
      **WRITE SCHED DELTA RECORD                                        02070000
      ****************************************************************  02080000
       2000-WRITE-STFG-CDE-DELTA-REC.                                   02090000
                                                                        02100000
           WRITE STFG-CDE-DELTA-FILE-REC FROM XS154-DELTA-REC.          02110000
                                                                        02120000
           ADD +1              TO PC-LAB-RECORDS-WRITTEN.               02130000
                                                                        02140000
      ******************************************************************02150000
      * CLOSE FILES.                                                    02160000
      ******************************************************************02170000
       9000-CLOSE-FILES.                                                02180000
                                                                        02190000
           CLOSE STFG-CDE-PREV-INPUT-FILE                               02200000
                 STFG-CDE-CURR-INPUT-FILE                               02210000
                 STFG-CDE-DELTA-FILE.                                   02220000
                                                                        02230000
           DISPLAY 'NBR OF LAB DELTA RECS WRITTEN:  '                   02240000
                   PC-LAB-RECORDS-WRITTEN.                              02250000
