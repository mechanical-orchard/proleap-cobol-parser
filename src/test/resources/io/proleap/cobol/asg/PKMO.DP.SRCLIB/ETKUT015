       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ETKUT015.                                                 
       AUTHOR.        C WYSOCKI.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  01/28/2004.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                         
      *    THIS PROGRAM EXTRACTS ON ORDER DATA FOR THE EDI 852                  
      *    DOCUMENT - PRODUCT ACTIVITY DATA.                                    
      *                                                                         
      *    ON-ORDER MAY BE REPORTED DAILY OR WEEKLY.                            
      *                                                                         
      *    ON-ORDER MAY BE REPORTED AT CORP LEVEL (ALL STORES), STORE           
      *    LEVEL OR STORE LEVEL WITH CORPORATE SUMMARY.                         
      *                                                                         
      *    IF REQUESTED FOR A WEEKLY VENDOR, THE VENDOR MUST                    
      *    BE SET UP TO TRANSMIT ON SUNDAY.  ANY OTHER DAY DOES NOT             
      *    PROVIDE SALES, INVENTORY AND ON ORDER TO BY SYNCHRONIZED.            
      *    THE ON ORDER DATABASE IS UPDATED NIGHTLY AND THERE IS NO             
      *    WEEKLY ROLL UP.                                                      
      *    WEEKLY REPORTING IS FOR A KOHLS DEPARTMENT STORES FISCAL             
      *    WEEK, WHICH BEGINS ON SUNDAY AND ENDS ON SATURDAY.                   
      *    ALL FIGURES ARE FOR END OF THE FISCAL WEEK (PRIOR TO RUN             
      *    DATE).                                                               
      *                                                                         
      *    DAILY REPORTING PROVIDES ON-ORDER AT END OF PRIOR DAY.               
      *                                                                         
      *    ( NOTE:  THE CONTROL CARD INPUT PROCESSING DATE IS THE DATE          
      *             THE JOB IS EXECUTED MINUS 1 DAY  (PREV DAY). )              
      *                                                                         
      *    THIS PROGRAM USES THE 'F4' CALENDAR TO DETERMINE                     
      *    ALL DATES REQUIRED, FROM THE CALENDAR ROUTINE.                       
      *    THUS, THE ONLY REQUIRED INPUTS WILL BE THE SCHEDULED                 
      *    RUN DATE (- 1 DAY), AND THE FISCAL CALENDAR TYPE OF 'F4'.            
      *                                                                         
*******************************************************************             
JF0305*  DATE  02/17/2005                                                       
JF0305*  CHANGE MADE:                                                           
JF0305*   UPDATED PROGRAM FOR STORE EXPANSION TO FIVE DIGITS.                   
JF0305*  MADE BY: JON FIEDLER             WR# : WR26913                         
************************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT ITEM-SELECT-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT EDI-SALES-EXTRACT-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  ITEM-SELECT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS ITEM-SELECT-RECORD.                                   
                                                                                
       01  ITEM-SELECT-RECORD                   PIC X(80).                      
                                                                                
       FD  EDI-SALES-EXTRACT-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS EDI-SALES-EXTRACT-RECORD.                             
                                                                                
       01  EDI-SALES-EXTRACT-RECORD             PIC X(80).                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-RUN-DATE.                                                     
               10  CC-RUN-MM                    PIC X(2)   VALUE SPACES.        
               10  CC-RUN-DD                    PIC X(2)   VALUE SPACES.        
               10  CC-RUN-YY                    PIC X(2)   VALUE SPACES.        
           05  FILLER                           PIC X(1)   VALUE SPACES.        
           05  CC-FISCAL-CALENDAR-TYPE          PIC X(2)   VALUE SPACES.        
           05  FILLER                           PIC X(71)  VALUE SPACES.        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
                                                                                
           05  PC-PROGRAM-NAME                  PIC X(08)  VALUE                
                                                           'ETKUT015'.          
           05  PC-DPKUT500-CALENDAR             PIC X(08)  VALUE                
                                                           'DPKUT500'.          
           05  PC-YES                           PIC X(1)   VALUE 'Y'.           
           05  PC-NO                            PIC X(1)   VALUE 'N'.           
           05  PC-SETUP-ERROR1                  PIC X(80)  VALUE                
           'VENDOR MUST TRANSMIT ON SUNDAY TO RECEIVE ON ORDER'.                
           05  PC-SETUP-ERROR2                  PIC X(40)  VALUE                
           'ON ORDER NOT PROCESSED FOR VENDOR: '.                               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
                                                                                
           05  PS-INPUT-EOF-SW                  PIC X(1)   VALUE 'N'.           
               88  PS-EOF-INPUT                            VALUE 'Y'.           
                                                                                
           05  PS-PROCESS-VENDOR-SW             PIC X(1)   VALUE 'N'.           
               88  PS-PROCESS-THIS-VENDOR                  VALUE 'Y'.           
                                                                                
           05  PS-PROCESS-ONHAND-SW             PIC X(1)   VALUE 'N'.           
               88  PS-PROCESS-ONHAND                       VALUE 'Y'.           
                                                                                
           05  PS-MORE-ITEMS-SW                 PIC X(1)   VALUE 'N'.           
               88  PS-MORE-ITEMS                           VALUE 'Y'.           
               88  PS-NO-MORE-ITEMS                        VALUE 'N'.           
                                                                                
           05  PS-MORE-ORDERS-SW                PIC X(1)   VALUE 'N'.           
               88  PS-MORE-ORDERS                          VALUE 'Y'.           
               88  PS-NO-MORE-ORDERS                       VALUE 'N'.           
                                                                                
           05  PS-REPORT-LEVEL-IND              PIC X(7).                       
               88  PS-STORE-LEVEL               VALUE '       '.                
               88  PS-CORP-LEVEL                VALUE 'CORPSUM'.                
               88  PS-STORE-SUM-LEVEL           VALUE '    SUM'.                
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           05  PV-PARAGRAPH-NAME                PIC X(30)  VALUE SPACES.        
           05  PV-DB2-OP-ATTEMPTED              PIC X(30)  VALUE SPACES.        
           05  PV-BOW-CURR-DTE                  PIC X(10)  VALUE SPACES.        
           05  PV-BOW-LAST-DTE                  PIC X(10)  VALUE SPACES.        
           05  PV-GREG-DTE                      PIC X(6)   VALUE SPACES.        
                                                                                
           05  PV-DATE.                                                         
               10 PV-DATE-CC                    PIC 9(2)   VALUE 20.            
               10 PV-DATE-YY                    PIC 9(2).                       
               10 PV-DATE-MM                    PIC 9(2).                       
               10 PV-DATE-DD                    PIC 9(2).                       
                                                                                
           05  PV-START-DATE                    PIC X(8).                       
           05  PV-END-DATE                      PIC X(8).                       
           05  PV-REPORT-DATE                   PIC X(8).                       
           05  PV-DEPT                          PIC S9(4)  COMP-3.              
           05  PV-DEPT-INHSE-NBR                PIC S9(3)  COMP-3.              
           05  PV-TOT-ORDERED                   PIC S9(7)  COMP-3.              
           05  PV-TOT-ORDERED-CORP              PIC S9(7)  COMP-3.              
           05  PV-NUMERIC-STORE                 PIC 9(5).                       
                                                                                
       01  PV-PROCESSING-DATES.                                                 
           05  PV-YESTERDAY-WKDY                PIC 9(1)   VALUE ZERO.          
           05  PV-RUN-DATE-WKDY                 PIC 9(1)   VALUE ZERO.          
               88  PV-RUN-DATE-SUNDAY           VALUE 1.                        
               88  PV-RUN-DATE-MONDAY           VALUE 2.                        
               88  PV-RUN-DATE-TUESDAY          VALUE 3.                        
               88  PV-RUN-DATE-WEDNESDAY        VALUE 4.                        
               88  PV-RUN-DATE-THURSDAY         VALUE 5.                        
               88  PV-RUN-DATE-FRIDAY           VALUE 6.                        
               88  PV-RUN-DATE-SATURDAY         VALUE 7.                        
           05  PV-PREV-WK-START-DATE.                                           
               10  PV-PREV-WK-START-MM          PIC X(2)   VALUE SPACES.        
               10  PV-PREV-WK-START-DD          PIC X(2)   VALUE SPACES.        
               10  PV-PREV-WK-START-YY          PIC X(2)   VALUE SPACES.        
           05  PV-PREV-WK-END-DATE.                                             
               10  PV-PREV-WK-END-MM            PIC X(2)   VALUE SPACES.        
               10  PV-PREV-WK-END-DD            PIC X(2)   VALUE SPACES.        
               10  PV-PREV-WK-END-YY            PIC X(2)   VALUE SPACES.        
                                                                                
       01  TD-TODAYS-DATE.                                                      
           05  TD-YY                            PIC 9(2)   VALUE ZEROES.        
           05  TD-MM                            PIC 9(2)   VALUE ZEROES.        
           05  TD-DD                            PIC 9(2)   VALUE ZEROES.        
                                                                                
       01  ABEND-CODE                           PIC S9(4) VALUE +0              
                                                           COMP SYNC.           
                                                                                
      *****************************************************************         
      *    CALENDAR ROUTINE WORKING STORAGE  'DPKUT500'                         
      *****************************************************************         
           COPY DPWS500.                                                        
                                                                                
       EJECT                                                                    
      ******************************************************************05/18/93
      *    INPUT RECORD LAYOUT - SELECT ITEM RECORD                     ETRD004 
      ******************************************************************ETRD004 
           COPY ETRD007.                                                        
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *    OUTPUT RECORD LAYOUT - EDI 852 DATA EXTRACT RECORD                   
      *****************************************************************         
           COPY ETRD008.                                                        
       EJECT                                                                    
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *    SQL COMMUNICATIONS AREA                                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *    SKU DB LAYOUT                                                        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSK                                                      
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    SKU CROSS REFERENCE DB LAYOUT                                        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    UPC TO SKU CROSS REFERENCE DB LAYOUT                                 
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPCSKU                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    TRADING PARTNER DB LAYOUT                                            
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TTRDPRT                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    TRADING TRANSACTION DB LAYOUT                                        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TTRDTRN                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    PO-SKU STORE ON ORDER DB LAYOUT                                      
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKSTOO                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *     SELECT ALL ITEMS FOR A DEPT/INHSE COMBINATION                       
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE ITEM_UPC CURSOR FOR                                      
               SELECT ITEM_NMBR,                                                
                      UPC_NBR                                                   
               FROM   TSK, TUPCSKU, TSKXREF                                     
               WHERE  TSKXREF.DEPT      = :PV-DEPT                              
                 AND  VNDR_IN_HSE_NMBR  = :PV-DEPT-INHSE-NBR                    
                 AND  EDI_ORD_IND       = :PC-YES                               
                 AND  TSK.ITEM_NMBR     = TUPCSKU.ITEM_NBR                      
                 AND  TSK.ITEM_NMBR     = TSKXREF.ITM_NBR                       
               ORDER BY TSK.ITEM_NMBR                                           
            END-EXEC.                                                           
                                                                                
      *****************************************************************         
      *     SELECT ALL ON ORDER DATA FOR AN ITEM                                
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE ITEM_ORDERS CURSOR FOR                                   
               SELECT STR_NBR,                                                  
                      SUM(ONORD_UNT_QTY)                                        
               FROM   TSKSTOO                                                   
               WHERE  ITM_NBR = :TSK-ITEM-NMBR                                  
               GROUP BY STR_NBR                                                 
               ORDER BY STR_NBR                                                 
            END-EXEC.                                                           
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
      ******************************************************************        
      *    OPEN ALL FILES                                                       
      *    PERFORM INITIALIZATIONS                                              
      *    PROCESS EACH ITEM SELECT FILE RECORD UNTIL END-OF-FILE               
      *    CLOSE ALL FILES                                                      
      ******************************************************************        
                                                                                
           MOVE 'A100-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           OPEN INPUT  ITEM-SELECT-FILE                                         
                OUTPUT EDI-SALES-EXTRACT-FILE.                                  
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM X100-READ-INPUT-FILE THRU X100-EXIT.                         
                                                                                
           PERFORM B200-PROCESS-INPUT THRU B200-EXIT                            
             UNTIL PS-EOF-INPUT.                                                
                                                                                
           CLOSE ITEM-SELECT-FILE                                               
                 EDI-SALES-EXTRACT-FILE.                                        
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       B100-HOUSEKEEPING.                                                       
                                                                                
      ******************************************************************        
      *    ONE TIME PROCESSING AND INITIALIZATIONS PERFORMED                    
      *    -   EDIT THE CONTROL CARD                                            
      *    -   INITIALIZE EXTRACT FILE WITH COMMON DATA                         
      ******************************************************************        
                                                                                
           MOVE 'B100-HOUSEKEEPING' TO PV-PARAGRAPH-NAME.                       
                                                                                
           INITIALIZE ET008-EDI-852-EXTRACT-REC.                                
                                                                                
           ACCEPT TD-TODAYS-DATE FROM DATE.                                     
           ACCEPT CC-CONTROL-CARD.                                              
           DISPLAY 'CONTROL CARD = ' CC-CONTROL-CARD.                           
                                                                                
           PERFORM Z100-EDIT-CONTROL-CARD.                                      
                                                                                
           MOVE CC-RUN-YY           TO PV-DATE-YY.                              
           MOVE CC-RUN-MM           TO PV-DATE-MM.                              
           MOVE CC-RUN-DD           TO PV-DATE-DD.                              
           MOVE PV-DATE             TO PV-REPORT-DATE.                          
           MOVE PV-PREV-WK-START-YY TO PV-DATE-YY.                              
           MOVE PV-PREV-WK-START-MM TO PV-DATE-MM.                              
           MOVE PV-PREV-WK-START-DD TO PV-DATE-DD.                              
           MOVE PV-DATE             TO PV-START-DATE.                           
           MOVE PV-PREV-WK-END-YY   TO PV-DATE-YY.                              
           MOVE PV-PREV-WK-END-MM   TO PV-DATE-MM.                              
           MOVE PV-PREV-WK-END-DD   TO PV-DATE-DD.                              
           MOVE PV-DATE             TO PV-END-DATE.                             
                                                                                
           COMPUTE PV-RUN-DATE-WKDY = (PV-YESTERDAY-WKDY + 1)                   
           IF PV-RUN-DATE-WKDY = 8                                              
              MOVE 1 TO PV-RUN-DATE-WKDY                                        
           END-IF.                                                              
                                                                                
           DISPLAY 'REPORT DATE = ' PV-REPORT-DATE.                             
           DISPLAY 'START DATE  = ' PV-START-DATE.                              
           DISPLAY 'END DATE    = ' PV-END-DATE.                                
                                                                                
       B100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       B200-PROCESS-INPUT.                                                      
                                                                                
           MOVE 'B200-PROCESS-INPUT' TO PV-PARAGRAPH-NAME.                      
                                                                                
           IF ET007-VENDOR-REC                                                  
              PERFORM C100-PROCESS-VENDOR THRU C100-EXIT                        
           ELSE                                                                 
              IF PS-PROCESS-THIS-VENDOR                                         
                 PERFORM C200-PROCESS-DEPT-REC THRU C200-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM X100-READ-INPUT-FILE THRU X100-EXIT.                         
                                                                                
       B200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C100-PROCESS-VENDOR.                                                     
                                                                                
      ******************************************************************        
      *     DETERMINE IF THIS VENDOR IS:                                        
      *     A) TO BE TRANSMITTED THIS RUN                                       
      *        RUN DATE WKDY IS THE NUMERIC DAY OF WEEK RETURNED FROM           
      *        THE DATE ROUTINE.  1=SUNDAY, 2=MONDAY, ETC.                      
      *     B) TO RECEIVE ON ORDER DATA                                         
      *     C) TO BE TRASNSMITTED ON SUNDAY IF REPORT IS WEEKLY                 
      *     D) AN ACTIVE 852 EDI VENDOR                                         
      *                                                                         
      *     IF ALL OF ABOVE ARE TRUE SET SWITCH TO PROCESS THE VENDOR           
      *                                                                         
      ******************************************************************        
                                                                                
           MOVE 'C100-PROCESS-VENDOR' TO PV-PARAGRAPH-NAME.                     
                                                                                
           MOVE PC-NO TO PS-PROCESS-VENDOR-SW.                                  
                                                                                
           IF PV-RUN-DATE-SUNDAY AND ET007-TRANSMIT-SUN                         
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-MONDAY AND ET007-TRANSMIT-MON                         
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-TUESDAY AND ET007-TRANSMIT-TUE                        
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-WEDNESDAY AND ET007-TRANSMIT-WED                      
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-THURSDAY AND ET007-TRANSMIT-THR                       
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-FRIDAY AND ET007-TRANSMIT-FRI                         
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PV-RUN-DATE-SATURDAY AND ET007-TRANSMIT-SAT                       
              MOVE PC-YES TO PS-PROCESS-VENDOR-SW                               
           END-IF.                                                              
                                                                                
           IF PS-PROCESS-THIS-VENDOR                                            
              IF ET007-REPORT-ORDERED                                           
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE PC-NO TO PS-PROCESS-VENDOR-SW                             
           END-IF.                                                              
                                                                                
           IF PS-PROCESS-THIS-VENDOR                                            
              IF ET007-WEEKLY-REPORT                                            
                 IF ET007-TRANSMIT-SUN                                          
                    CONTINUE                                                    
                 ELSE                                                           
                    DISPLAY PC-SETUP-ERROR1                                     
                    DISPLAY PC-SETUP-ERROR2 ET007-VENDOR-ENT-ID                 
                    MOVE PC-NO TO PS-PROCESS-VENDOR-SW                          
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF PS-PROCESS-THIS-VENDOR                                            
              MOVE ET007-VENDOR-ENT-ID TO TRDPRT-ENT-ID                         
              PERFORM C110-CHECK-EDI-STATUS THRU C110-EXIT                      
           END-IF.                                                              
                                                                                
           IF PS-PROCESS-THIS-VENDOR                                            
              MOVE ET007-VENDOR-ENT-ID TO ET008-ENT-ID                          
              MOVE ET007-ONHAND-IND TO PS-PROCESS-ONHAND-SW                     
              MOVE ET007-REPORT-LEVEL-IND TO PS-REPORT-LEVEL-IND                
              IF ET007-DAILY-REPORT                                             
                 MOVE PV-REPORT-DATE TO ET008-START-DATE                        
                 MOVE SPACES TO ET008-END-DATE                                  
              ELSE                                                              
                 MOVE PV-START-DATE TO ET008-START-DATE                         
                 MOVE PV-END-DATE TO ET008-END-DATE                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       C100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C110-CHECK-EDI-STATUS.                                                   
                                                                                
           EXEC SQL                                                             
                SELECT TTRDPRT.ENT_ID                                           
                  INTO :TRDPRT-ENT-ID                                           
                  FROM TTRDPRT,                                                 
                       TTRDTRN                                                  
                 WHERE TTRDPRT.ENT_ID       = :TRDPRT-ENT-ID                    
                   AND TTRDTRN.TP_DKEY      = TTRDPRT.TP_DKEY                   
                   AND TTRDTRN.TRAN_SET_CDE = '852'                             
                   AND TTRDPRT.INACTV_DTE   = '9999-09-09'                      
                   AND TTRDTRN.INACTV_DTE   = '9999-09-09'                      
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE PC-NO TO PS-PROCESS-VENDOR-SW                             
              ELSE                                                              
                 MOVE 'SELECT TTRDPRT/TTRDTRN' TO PV-DB2-OP-ATTEMPTED           
                 PERFORM Z999-SQL-ABEND THRU Z999-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       C110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C200-PROCESS-DEPT-REC.                                                   
                                                                                
      ******************************************************************        
      *     1)  SELECT ALL ITEMS FOR THE DEPT/INHSE VEND NMBR                   
      *     2)  FOR EACH ITEM GET THE ON ORDER TO BE REPORTED                   
      *     3)  WRITE RECORD(S) FOR EACH STORE TO REPORT                        
      ******************************************************************        
                                                                                
           MOVE 'C200-PROCESS-DEPT-REC' TO PV-PARAGRAPH-NAME.                   
                                                                                
           MOVE ET007-DEPT TO PV-DEPT                                           
                              ET008-DEPT.                                       
           MOVE ET007-DEPT-INHOUSE TO PV-DEPT-INHSE-NBR.                        
                                                                                
           MOVE PC-YES TO PS-MORE-ITEMS-SW.                                     
                                                                                
           PERFORM X300-OPEN-ITEM-CURSOR THRU X300-EXIT.                        
           PERFORM X310-GET-ITEM THRU X310-EXIT.                                
                                                                                
           PERFORM C210-PROCESS-ITEM THRU C210-EXIT                             
             UNTIL PS-NO-MORE-ITEMS.                                            
                                                                                
           PERFORM X320-CLOSE-ITEM-CURSOR THRU X320-EXIT.                       
                                                                                
       C200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C210-PROCESS-ITEM.                                                       
                                                                                
           MOVE 'C210-PROCESS-ITEM' TO PV-PARAGRAPH-NAME.                       
                                                                                
           MOVE TUPCSKU-UPC-NBR TO ET008-UPC-CODE.                              
                                                                                
           MOVE PC-YES TO PS-MORE-ORDERS-SW.                                    
           MOVE ZEROES TO PV-TOT-ORDERED.                                       
           MOVE ZEROES TO PV-TOT-ORDERED-CORP.                                  
                                                                                
           PERFORM X400-OPEN-ORDERS-CURSOR THRU X400-EXIT.                      
           PERFORM X410-GET-ORDERS THRU X410-EXIT.                              
                                                                                
           PERFORM C220-PROCESS-ORDERS THRU C220-EXIT                           
             UNTIL PS-NO-MORE-ORDERS.                                           
                                                                                
           IF PS-CORP-LEVEL OR PS-STORE-SUM-LEVEL                               
              SET ET008-ON-ORDER-REC TO TRUE                                    
JF0305        MOVE '00000' TO ET008-STORE-NUMBER                                
              MOVE PV-TOT-ORDERED-CORP TO ET008-ITEM-QUANTITY                   
              PERFORM X200-WRITE-EXTRACT-FILE THRU X200-EXIT                    
              SET ET008-SALES-REC TO TRUE                                       
              MOVE ZERO TO ET008-ITEM-QUANTITY                                  
              PERFORM X200-WRITE-EXTRACT-FILE THRU X200-EXIT                    
              IF PS-PROCESS-ONHAND                                              
                 SET ET008-ONHAND-REC TO TRUE                                   
                 MOVE ZERO TO ET008-ITEM-QUANTITY                               
                 PERFORM X200-WRITE-EXTRACT-FILE THRU X200-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM X420-CLOSE-ORDERS-CURSOR THRU X420-EXIT.                     
                                                                                
           PERFORM X310-GET-ITEM THRU X310-EXIT.                                
                                                                                
       C210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C220-PROCESS-ORDERS.                                                     
                                                                                
           MOVE 'C220-PROCESS-ORDERS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           IF PS-CORP-LEVEL OR PS-STORE-SUM-LEVEL                               
              ADD PV-TOT-ORDERED TO PV-TOT-ORDERED-CORP                         
           END-IF.                                                              
                                                                                
           IF PS-STORE-LEVEL OR PS-STORE-SUM-LEVEL                              
              SET ET008-ON-ORDER-REC TO TRUE                                    
              MOVE SKSTOO-STR-NBR TO PV-NUMERIC-STORE                           
              MOVE PV-NUMERIC-STORE TO ET008-STORE-NUMBER                       
              MOVE PV-TOT-ORDERED TO ET008-ITEM-QUANTITY                        
              PERFORM X200-WRITE-EXTRACT-FILE THRU X200-EXIT                    
              SET ET008-SALES-REC TO TRUE                                       
              MOVE ZERO TO ET008-ITEM-QUANTITY                                  
              PERFORM X200-WRITE-EXTRACT-FILE THRU X200-EXIT                    
              IF PS-PROCESS-ONHAND                                              
                 SET ET008-ONHAND-REC TO TRUE                                   
                 MOVE ZERO TO ET008-ITEM-QUANTITY                               
                 PERFORM X200-WRITE-EXTRACT-FILE THRU X200-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM X410-GET-ORDERS THRU X410-EXIT.                              
                                                                                
       C220-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       X100-READ-INPUT-FILE.                                                    
                                                                                
           MOVE 'X100-READ-INPUT-FILE' TO PV-PARAGRAPH-NAME.                    
                                                                                
           READ ITEM-SELECT-FILE                                                
                INTO ET007-ITEM-SELECT-RECORD                                   
                AT END MOVE PC-YES TO PS-INPUT-EOF-SW.                          
                                                                                
       X100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       X200-WRITE-EXTRACT-FILE.                                                 
                                                                                
           MOVE 'X200-WRITE-EXTRACT-FILE' TO PV-PARAGRAPH-NAME.                 
                                                                                
           WRITE EDI-SALES-EXTRACT-RECORD                                       
              FROM ET008-EDI-852-EXTRACT-REC.                                   
                                                                                
       X200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       X300-OPEN-ITEM-CURSOR.                                                   
                                                                                
           MOVE 'X300-OPEN-ITEM-CURSOR' TO PV-PARAGRAPH-NAME.                   
                                                                                
           EXEC SQL                                                             
               OPEN ITEM_UPC                                                    
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'OPEN CURSOR - ITEM_UPC' TO PV-DB2-OP-ATTEMPTED              
              PERFORM Z999-SQL-ABEND THRU Z999-EXIT                             
           END-IF.                                                              
                                                                                
       X300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       X310-GET-ITEM.                                                           
                                                                                
           MOVE 'X310-GET-ITEM' TO PV-PARAGRAPH-NAME.                           
                                                                                
           EXEC SQL                                                             
                FETCH ITEM_UPC                                                  
                INTO :TSK-ITEM-NMBR,                                            
                     :TUPCSKU-UPC-NBR                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              SET PS-MORE-ITEMS TO TRUE                                         
           ELSE                                                                 
              IF SQLCODE = 100                                                  
                 SET PS-NO-MORE-ITEMS TO TRUE                                   
              ELSE                                                              
                 MOVE 'FETCH - ITEM_UPC' TO PV-DB2-OP-ATTEMPTED                 
                 PERFORM Z999-SQL-ABEND THRU Z999-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       X310-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       X320-CLOSE-ITEM-CURSOR.                                                  
                                                                                
           MOVE 'X320-CLOSE-ITEM-CURSOR' TO PV-PARAGRAPH-NAME.                  
                                                                                
           EXEC SQL                                                             
               CLOSE ITEM_UPC                                                   
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'CLOSE CURSOR - ITEM_UPC' TO PV-DB2-OP-ATTEMPTED             
              PERFORM Z999-SQL-ABEND THRU Z999-EXIT                             
           END-IF.                                                              
                                                                                
       X320-EXIT.                                                               
           EXIT.                                                                
                                                                                
       X400-OPEN-ORDERS-CURSOR.                                                 
                                                                                
           MOVE 'X400-OPEN-ORDERS-CURSOR' TO PV-PARAGRAPH-NAME.                 
                                                                                
           EXEC SQL                                                             
               OPEN ITEM_ORDERS                                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'OPEN CURSOR - ITEM_ORDERS' TO PV-DB2-OP-ATTEMPTED           
              PERFORM Z999-SQL-ABEND THRU Z999-EXIT                             
           END-IF.                                                              
                                                                                
       X400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       X410-GET-ORDERS.                                                         
                                                                                
           MOVE 'X410-GET-ORDERS' TO PV-PARAGRAPH-NAME.                         
                                                                                
           EXEC SQL                                                             
                FETCH ITEM_ORDERS                                               
                INTO :SKSTOO-STR-NBR,                                           
                     :PV-TOT-ORDERED                                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              SET PS-MORE-ORDERS TO TRUE                                        
           ELSE                                                                 
              IF SQLCODE = 100                                                  
                 SET PS-NO-MORE-ORDERS TO TRUE                                  
              ELSE                                                              
                 MOVE 'FETCH - ITEM_ORDERS' TO PV-DB2-OP-ATTEMPTED              
                 PERFORM Z999-SQL-ABEND THRU Z999-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       X410-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       X420-CLOSE-ORDERS-CURSOR.                                                
                                                                                
           MOVE 'X420-CLOSE-ORDERS-CURSOR' TO PV-PARAGRAPH-NAME.                
                                                                                
           EXEC SQL                                                             
               CLOSE ITEM_ORDERS                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'CLOSE CURSOR - ITEM_ORDERS' TO PV-DB2-OP-ATTEMPTED          
              PERFORM Z999-SQL-ABEND THRU Z999-EXIT                             
           END-IF.                                                              
                                                                                
       X420-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       Z100-EDIT-CONTROL-CARD.                                                  
                                                                                
      ******************************************************************        
      *    VERIFY THAT THE RUN DATE RECEIVED ON THE CONTROL CARD IS             
      *    NUMERIC, THEN UTILIZE THIS DATE TO CALL THE CALENDAR ROUTINE         
      *    AND RETRIVE ALL THE OTHER REQUIRED PROCESSING DATES.                 
      ******************************************************************        
                                                                                
           MOVE 'Z100-EDIT-CONTROL-CARD' TO PV-PARAGRAPH-NAME.                  
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
                                                                                
           IF (CC-RUN-DATE NOT NUMERIC) OR                                      
              (CC-FISCAL-CALENDAR-TYPE = SPACES)                                
               DISPLAY '  '                                                     
               DISPLAY '** ABEND IN ETKUT013 **'                                
               DISPLAY 'CONTROL CARD RUN DATE OR CALENDAR TYPE INVALID'         
               DISPLAY 'CC-RUN-DATE IS ' CC-RUN-DATE                            
               DISPLAY 'CC-FISCAL-CALENDAR-TYPE IS '                            
                                         CC-FISCAL-CALENDAR-TYPE                
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           MOVE CC-RUN-DATE TO DPG52-GREG-DATE.                                 
           PERFORM Z200-CALL-CALENDAR-DPKUT500.                                 
           IF NOT DPG54-SEVERE-ERROR                                            
              MOVE DPG55-NUMERIC-WEEK-DAY TO PV-YESTERDAY-WKDY                  
              MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO PV-BOW-CURR-DTE                 
              DISPLAY 'PV-BOW-CURR-DTE = ' PV-BOW-CURR-DTE                      
              IF DPG55-ALPHA-WEEK-DAY-ABBR3 = 'SAT'                             
                 MOVE DPG55-1ST-WKDY-GREG TO PV-PREV-WK-START-DATE              
                 MOVE DPG55-LAST-WKDY-GREG TO PV-PREV-WK-END-DATE               
                 MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO PV-BOW-LAST-DTE              
                 DISPLAY 'PV-BOW-LAST-DTE = ' PV-BOW-LAST-DTE                   
              ELSE                                                              
      *          *************************************************              
      *          * CALL THE CALENDAR ROUTINE A SECOND TIME, WITH                
      *          * END DATE OF RUN DATE WEEK, MINUS 1                           
      *          * (THIS WILL RETURN THE PREVIOUS WEEKS INFO)                   
      *          *************************************************              
                 MOVE DPG55-1ST-WKDY-GREG TO PV-GREG-DTE                        
                 INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56                 
                 MOVE PV-GREG-DTE TO DPG52-GREG-DATE                            
                 MOVE 1 TO DPG51-INCR-DECR-DAYS-9                               
                 SET DPG51-DECREMENT-DATE TO TRUE                               
                 PERFORM Z200-CALL-CALENDAR-DPKUT500                            
                 IF NOT DPG54-SEVERE-ERROR                                      
                    MOVE DPG55-1ST-WKDY-GREG TO PV-PREV-WK-START-DATE           
                    MOVE DPG55-LAST-WKDY-GREG TO PV-PREV-WK-END-DATE            
                    MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO PV-BOW-LAST-DTE           
                    DISPLAY 'PV-BOW-LAST-DTE = ' PV-BOW-LAST-DTE                
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       Z100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       Z200-CALL-CALENDAR-DPKUT500.                                             
                                                                                
      ******************************************************************        
      *    CALL THE KOHLS INHOUSE CALENDAR ROUTINE TO DETERMINE                 
      *    ALL REQUIRED PROCESSING DATE INFORMATION.                            
      ******************************************************************        
                                                                                
           MOVE 'Z200-CALL-CALENDAR-DPKUT500' TO PV-PARAGRAPH-NAME.             
                                                                                
           MOVE TD-TODAYS-DATE          TO  DPG51-SYSTEM-DATE.                  
           MOVE CC-FISCAL-CALENDAR-TYPE TO  DPG51-CALENDAR-TYPE.                
                                                                                
           SET DPG52-LK-DTE-GREG        TO  TRUE.                               
                                                                                
           CALL  PC-DPKUT500-CALENDAR  USING  DPG51 DPG52                       
                                              DPG53 DPG54                       
                                              DPG55 DPG56.                      
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
                     DISPLAY '** DPKUT500 - SEVERE ERROR :   '                  
                             DPG54-ERROR-MESSAGE                                
                     DISPLAY '**       SYSTEM DATE : '                          
                             DPG51-SYSTEM-DATE                                  
                     DISPLAY '**     CALENDAR TYPE : '                          
                             DPG51-CALENDAR-TYPE                                
                     DISPLAY '**  DATE FORMAT CODE : '                          
                             DPG52-LK-DATE-FORMAT-CODE                          
                     DISPLAY '**        INPUT DATE : '                          
                             DPG52-LK-DATE-INPUT                                
                     MOVE +4019 TO ABEND-CODE                                   
                     CALL 'ILBOABN0' USING ABEND-CODE                           
               WHEN DPG54-WARNING-ERROR                                         
                     DISPLAY '** DPKUT500 - WARNING :   '                       
                             DPG54-ERROR-MESSAGE                                
                     DISPLAY '**       SYSTEM DATE : '                          
                             DPG51-SYSTEM-DATE                                  
                     DISPLAY '**     CALENDAR TYPE : '                          
                             DPG51-CALENDAR-TYPE                                
                     DISPLAY '**  DATE FORMAT CODE : '                          
                             DPG52-LK-DATE-FORMAT-CODE                          
                     DISPLAY '**        INPUT DATE : '                          
                             DPG52-LK-DATE-INPUT                                
               WHEN DPG54-NO-ERROR                                              
                     CONTINUE                                                   
           END-EVALUATE.                                                        
                                                                                
       Z200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       Z999-SQL-ABEND.                                                          
                                                                                
           MOVE 'Z999-SQL-ABEND' TO PV-PARAGRAPH-NAME.                          
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'PROGRAM   = ' PC-PROGRAM-NAME.                              
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OP-ATTEMPTED.                          
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       Z999-EXIT.                                                               
           EXIT.                                                                
