00001 *                                                                 02/05/92
00002 *                                                                 MBKCS102
00003 *                                                                    LV041
00004  IDENTIFICATION DIVISION.                                         MBKCS102
00005                                                                   MBKCS102
00006  PROGRAM-ID.    MBKCS102.                                            CL200
00007  AUTHOR.        BOB LOOS - STRATAGEM.                                CL*13
00008  INSTALLATION.  KOHLS DEPARTMENT STORES.                          MBKCS102
00009  DATE-WRITTEN.  06-26-91.                                            CL*13
00010  DATE-COMPILED.                                                   MBKCS102
00011                                                                   MBKCS102
00012 ***************************************************************** MBKCS102
00013 *    LIST PROCESSOR MERCHANDISE AREA                                 CL*13
00014 *                                                                 MBKCS102
00015 *    THIS PROGRAM WILL RETRIEVE DATA FROM THE MERCHANDISE AREA       CL198
00016 *    TABLE BASED ON REQUEST FROM THE MERCHANDISE AREA SELECTION      CL198
00017 *    PANEL.                                                          CL198
00018 *                                                                 MBKCS102
00019 ***************************************************************** MBKCS102
262978* CHANGED 10/21/2021    BY JIM HUNTER      WR: CHG0262978                 
262978* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
262978* MAKING THE CALL DYNAMIC VS STATIC.                                      
262978******************************************************************        
00020                                                                   MBKCS102
00021 *                                                                 MBKCS102
00022 *                                                                 MBKCS102
00023 *                                                                 MBKCS102
00024  ENVIRONMENT DIVISION.                                            MBKCS102
00025 *                                                                 MBKCS102
00026 *                                                                 MBKCS102
00027 *                                                                 MBKCS102
00028  DATA DIVISION.                                                   MBKCS102
00029 *                                                                 MBKCS102
00030 *                                                                 MBKCS102
00031  WORKING-STORAGE SECTION.                                         MBKCS102
00032  01  AM-ABEND-MESSAGES.                                              CL*57
00033      05  AM-ABEND-MESSAGE-8             PIC X(78)    VALUE           CL*57
00034      'ERROR TRYING TO DELETE TEMP STORAGE QUEUE'.                    CL*57
00035      05  AM-ABEND-MESSAGE-9             PIC X(78)    VALUE           CL*57
00036      'ERROR TRYING TO REWRITE TEMP STORAGE QUEUE'.                   CL*57
00037      05  AM-ABEND-MESSAGE-10            PIC X(78)    VALUE           CL*57
00038      'ERROR TRYING TO WRITE TEMP STORAGE QUEUE'.                     CL*57
00039      05  AM-ABEND-MESSAGE-11            PIC X(78)    VALUE           CL*57
00040      'LOGIC ERROR IN TRYING TO WRITE TO TEMP STORAGE QUEUE - BLOCK   CL*57
00041 -    '      '.                                                       CL*57
00042      05  FILLER REDEFINES AM-ABEND-MESSAGE-11.                       CL*57
00043          10  FILLER                     PIC X(61).                   CL*57
00044          10  AM-BLOCK-NUMBER-11         PIC Z(03)9-.                 CL*57
00045          10  FILLER                     PIC X(12).                   CL*57
00046      05  AM-ABEND-MESSAGE-12            PIC X(78)    VALUE           CL*57
00047      'ERROR TRYING TO READ TEMP STORAGE QUEUE'.                      CL*57
00048      05  AM-ABEND-MESSAGE-13            PIC X(78)    VALUE           CL*58
00049      'ERROR TRYING TO SELECT A RECORD FROM TMDSEAR TABLE - KEY       CL*58
00050 -    '      '.                                                       CL*58
00051      05  FILLER REDEFINES AM-ABEND-MESSAGE-13.                       CL*58
00052          10  FILLER                     PIC X(57).                   CL*58
00053          10  AM-OPERCO-NBR-13           PIC X(02).                   CL*58
00054          10  AM-GMA-NBR-13              PIC X(02).                   CL*58
00055          10  AM-DMA-NBR-13              PIC X(02).                   CL*58
00056          10  AM-DEPT-NBR-13             PIC X(03).                   CL*58
00057          10  AM-MCL-NBR-13              PIC X(02).                   CL*58
00058          10  AM-SCL-NBR-13              PIC X(03).                   CL*58
00059          10  FILLER                     PIC X(07).                   CL*58
00060      05  AM-ABEND-MESSAGE-21            PIC X(78)    VALUE           CL*58
00061      'ERROR TRYING TO OPEN MERCH-CURSOR'.                            CL*58
00062      05  AM-ABEND-MESSAGE-22            PIC X(78)    VALUE           CL*58
00063      'ERROR TRYING TO CLOSE MERCH-CURSOR'.                           CL*58
00064  01  PC-PROGRAM-CONSTANTS.                                        MBKCS102
00065      05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.   CL**5
00066      05  PC-SELECTED                    PIC X(01)    VALUE 'S'.      CL*50
00067      05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072  MBKCS102
00068                                                      COMP SYNC.   MBKCS102
00069      05  PC-CURRENT-MAP-NAME            PIC X(08)    VALUE        MBKCS102
00070      'MB102A  '.                                                     CL201
00071      05  PC-CURRENT-MAPSET-NAME         PIC X(08)    VALUE        MBKCS102
00072      'MBKM102 '.                                                     CL201
00073      05  PC-CURRENT-PROGRAM-NAME        PIC X(08)    VALUE        MBKCS102
00074      'MBKCS102'.                                                     CL200
00075 ***************************************************************** MBKCS102
00076 *    PC-ITEMS-PER-PANEL = MAXIMUM NUMBER OF ITEMS ON THE SCREEN   MBKCS102
00077 ***************************************************************** MBKCS102
00078      05  PC-ITEMS-PER-PANEL             PIC S9(04)   VALUE +14       CL*14
00079                                                      COMP SYNC.   MBKCS102
00080 ***************************************************************** MBKCS102
00081 *    PC-ITEMS-PER-BLOCK = MAXIMUM NUMBER OF ITEMS IN A BLOCK      MBKCS102
00082 ***************************************************************** MBKCS102
00083      05  PC-ITEMS-PER-BLOCK             PIC S9(04)   VALUE +18       CL*14
00084                                                      COMP SYNC.   MBKCS102
00085 ***************************************************************** MBKCS102
00086 *    PC-MAX-ITEMS-TO-BE-READ = THE MAXIMUM NUMBER OF ITEMS TO     MBKCS102
00087 *    BE READ FROM THE DATABASE AT ONE TIME.  USED WHEN THE USER   MBKCS102
00088 *    CHANGES THE BEGINNING LIST NUMBER.                              CL**4
00089 ***************************************************************** MBKCS102
00090      05  PC-MAX-ITEMS-TO-BE-READ        PIC S9(04)   VALUE +100      CL*15
00091                                                      COMP SYNC.   MBKCS102
00092      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)   VALUE +2     MBKCS102
00093                                                      COMP SYNC.   MBKCS102
00094      05  PC-MAX-QUEUE-ITEMS             PIC S9(03)   VALUE +100      CL209
00095                                                      COMP-3.         CL209
00096 ***************************************************************** MBKCS102
00097 *    PC-MAX-ITEMS = MAXIMUM NUMBER OF ITEMS THAT CAN APPEAR ON    MBKCS102
00098 *    THE LIST.                                                    MBKCS102
00099 ***************************************************************** MBKCS102
00100      05  PC-MAX-ITEMS                   PIC S9(09)   VALUE        MBKCS102
00101                                                      +1000000        CL*14
00102                                                      COMP-3.      MBKCS102
00103 ***************************************************************** MBKCS102
00104 *    PC-BLOCK-LENGTH = THE LENGTH OF 1 BLOCK WORTH OF DATA.       MBKCS102
00105 ***************************************************************** MBKCS102
00106      05  PC-BLOCK-LENGTH                PIC S9(04)   VALUE +1243     CL153
00107                                                      COMP SYNC.   MBKCS102
00108 ***************************************************************** MBKCS102
00109 *    PC-FIELD-LENGTH = THE LENGTH OF THE BEGINNING LIST NUMBER.      CL**4
00110 ***************************************************************** MBKCS102
00111      05  PC-FIELD-LENGTH                PIC S9(04)   VALUE +7        CL*49
00112                                                      COMP SYNC.   MBKCS102
00113      05  PC-ERASE-EOF-COMP              PIC S9(04)   VALUE +128   MBKCS102
00114                                                      COMP SYNC.   MBKCS102
00115      05  FILLER REDEFINES PC-ERASE-EOF-COMP.                      MBKCS102
00116          10  FILLER                     PIC X(01).                MBKCS102
00117          10  PC-ERASE-EOF               PIC X(01).                MBKCS102
00118                                                                   MBKCS102
00119      05  PC-NO-END-OF-RANGE             PIC 9(02)    VALUE 51.       CL*51
00120      05  PC-END-BEFORE-START            PIC 9(02)    VALUE 52.       CL*51
00121      05  PC-NO-START-OF-RANGE           PIC 9(02)    VALUE 53.       CL*51
00122      05  PC-MULTIPLE-START-RANGES       PIC 9(02)    VALUE 54.       CL*51
00123      05  PC-MULTIPLE-END-RANGES         PIC 9(02)    VALUE 55.       CL*51
00124      05  PC-MAX-RETRYS                  PIC S9(04)   VALUE +3        CL*50
00125                                                      COMP SYNC.      CL*50
00126      05  PC-OPERCO-NBR                  PIC X(02)    VALUE '03'.     CL*50
00127      05  PC-GMA-LITERAL                 PIC X(03)    VALUE 'GMA'.    CL*37
00128      05  PC-DMA-LITERAL                 PIC X(03)    VALUE 'DMA'.    CL*37
00129      05  PC-DPT-LITERAL                 PIC X(03)    VALUE 'DPT'.    CL*37
00130      05  PC-MCL-LITERAL                 PIC X(03)    VALUE 'MCL'.    CL*37
00131      05  PC-SCL-LITERAL                 PIC X(03)    VALUE 'SCL'.    CL*37
00132      05  PC-NO-DISPLAY-DATE             PIC X(10)    VALUE           CL*29
00133                                                      '9999-09-09'.   CL*29
00134      05  PC-TSYMSG-NUMBERS.                                          CL179
00135          10  PC-TSYMSG-00005            PIC 9(05)    VALUE 00005.    CL179
00136          10  PC-TSYMSG-00008            PIC 9(05)    VALUE 00008.    CL179
00137          10  PC-TSYMSG-00030            PIC 9(05)    VALUE 00030.    CL181
00138          10  PC-TSYMSG-00032            PIC 9(05)    VALUE 00032.    CL181
00139          10  PC-TSYMSG-00045            PIC 9(05)    VALUE 00045.    CL244
00140          10  PC-TSYMSG-00047            PIC 9(05)    VALUE 00047.    CL244
00141          10  PC-TSYMSG-00050            PIC 9(05)    VALUE 00050.    CL240
00142          10  PC-TSYMSG-00051            PIC 9(05)    VALUE 00051.    CL179
00143          10  PC-TSYMSG-00052            PIC 9(05)    VALUE 00052.    CL179
00144          10  PC-TSYMSG-00053            PIC 9(05)    VALUE 00053.    CL179
00145          10  PC-TSYMSG-00054            PIC 9(05)    VALUE 00054.    CL179
00146          10  PC-TSYMSG-00055            PIC 9(05)    VALUE 00055.    CL179
00147          10  PC-TSYMSG-00056            PIC 9(05)    VALUE 00056.    CL179
00148          10  PC-TSYMSG-00057            PIC 9(05)    VALUE 00057.    CL208
00149          10  PC-TSYMSG-00060            PIC 9(05)    VALUE 00060.    CL179
00150          10  PC-TSYMSG-00069            PIC 9(05)    VALUE 00069.    CL179
00151          10  PC-TSYMSG-00070            PIC 9(05)    VALUE 00070.    CL179
00152          10  PC-TSYMSG-00146            PIC 9(05)    VALUE 00146.    CL239
00153          10  PC-TSYMSG-00207            PIC 9(05)    VALUE 00207.    CL*34
00154          10  PC-TSYMSG-99901            PIC 9(05)    VALUE 99901.    CL*26
00155                                                                      CL*36
00156  01  PC-PROGRAM-COUNTERS.                                            CL*50
00157      05  PC-RETRY-CNTR                  PIC S9(04)   VALUE +0        CL*50
00158                                                      COMP SYNC.      CL*50
00159                                                                      CL*50
00160  01  PM-PROGRAM-MESSAGES.                                         MBKCS102
00161      05  INVALID-NEXT-ACT-MSG           PIC X(78)    VALUE        MBKCS102
00162      'UNRECOGNIZED ACTION ENCOUNTERED BY THIS PROGRAM'.           MBKCS102
00163                                                                   MBKCS102
00164  01  PS-PROGRAM-SUBSCRIPTS.                                          CL247
00165      05  PS-SUB                         PIC S9(04)   VALUE +0        CL*50
00166                                                      COMP SYNC.      CL*50
00167      05  PS-SUB1                        PIC S9(04)   VALUE +0        CL209
00168                                                      COMP SYNC.      CL209
00169      05  PS-SUB2                        PIC S9(04)   VALUE +0        CL209
00170                                                      COMP SYNC.      CL209
00171      05  PS-ITEM-SUB                    PIC S9(09)   VALUE +0        CL*52
00172                                                      COMP-3.         CL*50
00173                                                                      CL**9
00174  01  PS-PROGRAM-SWITCHES.                                            CL**9
00175      05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.   MBKCS102
00176          88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.   MBKCS102
00177          88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.   MBKCS102
00178          88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.   MBKCS102
00179          88  PS-SEND-DATA-ONLY                       VALUE '4'.   MBKCS102
00180          88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.   MBKCS102
00181          88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.   MBKCS102
00182      05  PS-INTER-APPL-SW               PIC X(01)    VALUE 'Y'.      CL*23
00183          88  PS-INTER-APPL-INVALID                   VALUE 'N'.      CL*23
00184          88  PS-INTER-APPL-VALID                     VALUE 'Y'.      CL*23
00185      05  PS-SELECTED-HISTORY-SW         PIC X(01)    VALUE 'N'.      CL*10
00186          88  PS-SELECTED-HISTORY                     VALUE 'Y'.      CL*10
00187          88  PS-NO-SELECTED-HISTORY                  VALUE 'N'.      CL*10
00188      05  PS-SELECTED-MCL-SCL-SW         PIC X(01)    VALUE 'N'.      CL*32
00189          88  PS-SELECTED-MCL-SCL                     VALUE 'Y'.      CL*32
00190          88  PS-NO-SELECTED-MCL-SCL                  VALUE 'N'.      CL*32
00191      05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.   MBKCS102
00192          88  PS-ERROR                                VALUE 'Y'.   MBKCS102
00193          88  PS-NO-ERROR                             VALUE 'N'.   MBKCS102
00194      05  PS-TOP-ITEM-IN-ARRAY-SW        PIC X(01)    VALUE 'N'.      CL**8
00195          88  PS-TOP-ITEM-IN-ARRAY                    VALUE 'Y'.      CL**8
00196          88  PS-TOP-ITEM-NOT-IN-ARRAY                VALUE 'N'.      CL**8
00197      05  PS-BOTTOM-ITEM-IN-ARRAY-SW     PIC X(01)    VALUE 'N'.      CL**8
00198          88  PS-BOTTOM-ITEM-IN-ARRAY                 VALUE 'Y'.      CL**8
00199          88  PS-BOTTOM-ITEM-NOT-IN-ARRAY             VALUE 'N'.      CL**8
00200      05  PS-TOP-ITEM-IN-TEMP-SW         PIC X(01)    VALUE 'N'.      CL**8
00201          88  PS-TOP-ITEM-IN-TEMP                     VALUE 'Y'.      CL**8
00202          88  PS-TOP-ITEM-NOT-IN-TEMP                 VALUE 'N'.      CL**8
00203      05  PS-BOTTOM-ITEM-IN-TEMP-SW      PIC X(01)    VALUE 'N'.      CL**8
00204          88  PS-BOTTOM-ITEM-IN-TEMP                  VALUE 'Y'.      CL**8
00205          88  PS-BOTTOM-ITEM-NOT-IN-TEMP              VALUE 'N'.      CL**8
00206      05  PS-LAST-BLOCK-IN-ARRAY-SW      PIC X(01)    VALUE 'N'.      CL**8
00207          88  PS-LAST-BLOCK-IN-ARRAY                  VALUE 'Y'.      CL**8
00208          88  PS-LAST-BLOCK-NOT-IN-ARRAY              VALUE 'N'.      CL**8
00209      05  PS-VALID-SELECTIONS-SW         PIC X(01)    VALUE 'N'.      CL**4
00210          88  PS-VALID-SELECTIONS                     VALUE 'Y'.      CL**4
00211          88  PS-INVALID-SELECTIONS                   VALUE 'N'.      CL**4
00212      05  PS-VALID-BEG-ITEM-SW           PIC X(01)    VALUE 'N'.      CL**4
00213          88  PS-VALID-BEG-ITEM                       VALUE 'Y'.      CL**4
00214          88  PS-INVALID-BEG-ITEM                     VALUE 'N'.      CL**4
00215      05  PS-DB2-RETRY-SW                PIC X(01)    VALUE 'N'.      CL*51
00216          88  PS-DB2-OK                               VALUE 'Y'.      CL*51
00217          88  PS-DB2-RETRY                            VALUE 'N'.      CL*51
00218      05  PS-CURSOR-POSITION-SW          PIC X(01)    VALUE 'N'.      CL*51
00219          88  PS-CURSOR-POSITIONED                    VALUE 'Y'.      CL*51
00220          88  PS-CURSOR-NOT-POSITIONED                VALUE 'N'.      CL*51
00221      05  PS-WARNING-SW                  PIC X(01)    VALUE 'N'.      CL*51
00222          88  PS-WARNING                              VALUE 'Y'.      CL*51
00223          88  PS-NO-WARNING                           VALUE 'N'.      CL*51
00224      05  PS-INFORMATIONAL-SW            PIC X(01)    VALUE 'N'.      CL*53
00225          88  PS-INFORMATIONAL                        VALUE 'Y'.      CL*53
00226          88  PS-NOT-INFORMATIONAL                    VALUE 'N'.      CL*53
00227      05  PS-SAME-ITEMS-DISPLAYED-SW     PIC X(01)    VALUE 'N'.      CL*53
00228          88  PS-SAME-ITEMS-DISPLAYED                 VALUE 'Y'.      CL*51
00229          88  PS-DIFFERENT-ITEMS-DISPLAYED            VALUE 'N'.      CL*56
00230      05  PS-SELECTION-FOUND-SW          PIC X(01)    VALUE 'N'.      CL*56
00231          88  PS-SELECTION-FOUND                      VALUE 'Y'.      CL*58
00232          88  PS-NO-SELECTION-FOUND                   VALUE 'N'.      CL*56
00233      05  PS-MERCH-CURSOR-OPENED-SW      PIC X(01)    VALUE 'N'.      CL*84
00234          88  PS-MERCH-CURSOR-NOT-OPENED              VALUE 'N'.      CL*85
00235          88  PS-MERCH-CURSOR-OPENED                  VALUE 'Y'.      CL*84
00236      05  PS-GMA-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL*98
00237          88  PS-GMA-CURSOR-NOT-OPENED                VALUE 'N'.      CL*98
00238          88  PS-GMA-CURSOR-OPENED                    VALUE 'Y'.      CL*98
00239      05  PS-DMA-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL100
00240          88  PS-DMA-CURSOR-NOT-OPENED                VALUE 'N'.      CL100
00241          88  PS-DMA-CURSOR-OPENED                    VALUE 'Y'.      CL100
00242      05  PS-DPT-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL100
00243          88  PS-DPT-CURSOR-NOT-OPENED                VALUE 'N'.      CL100
00244          88  PS-DPT-CURSOR-OPENED                    VALUE 'Y'.      CL100
00245      05  PS-MCL-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL100
00246          88  PS-MCL-CURSOR-NOT-OPENED                VALUE 'N'.      CL100
00247          88  PS-MCL-CURSOR-OPENED                    VALUE 'Y'.      CL100
00248      05  PS-SCL-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL100
00249          88  PS-SCL-CURSOR-NOT-OPENED                VALUE 'N'.      CL100
00250          88  PS-SCL-CURSOR-OPENED                    VALUE 'Y'.      CL100
00251      05  PS-DMA-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL123
00252          88  PS-DO-NOT-HAVE-GMA-ITEM                 VALUE 'N'.      CL181
00253          88  PS-HAVE-GMA-ITEM                        VALUE 'Y'.      CL181
00254          88  PS-DO-NOT-HAVE-DMA-ITEM                 VALUE 'N'.      CL181
00255          88  PS-HAVE-DMA-ITEM                        VALUE 'Y'.      CL181
00256      05  PS-DPT-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL129
00257          88  PS-DO-NOT-HAVE-DPT-ITEM                 VALUE 'N'.      CL129
00258          88  PS-HAVE-DPT-ITEM                        VALUE 'Y'.      CL129
00259      05  PS-MCL-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL135
00260          88  PS-DO-NOT-HAVE-MCL-ITEM                 VALUE 'N'.      CL135
00261          88  PS-HAVE-MCL-ITEM                        VALUE 'Y'.      CL135
00262      05  PS-SCL-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL148
00263          88  PS-DO-NOT-HAVE-SCL-ITEM                 VALUE 'N'.      CL148
00264          88  PS-HAVE-SCL-ITEM                        VALUE 'Y'.      CL148
00265      05  PS-SELECTION-SW                PIC X(01)    VALUE 'N'.      CL157
00266          88  PS-NO-SELECTIONS-FOUND                  VALUE 'N'.      CL157
00267          88  PS-SELECTIONS-FOUND                     VALUE 'Y'.      CL157
00268      05  PS-CONTINUE-ON-SW              PIC X(01)    VALUE 'N'.      CL235
00269          88  PS-CONTINUE-ON                          VALUE 'N'.      CL235
00270          88  PS-DO-NOT-CONTINUE-ON                   VALUE 'Y'.      CL240
00271      05  PS-PROCESS-ITEM-SW             PIC X(01)    VALUE 'N'.      CL244
00272          88  END-OF-ITEMS                            VALUE 'N'.      CL244
00273          88  NOT-END-OF-ITEMS                        VALUE 'Y'.      CL244
00274      05  PS-SETUP-INTER-APP-SW          PIC X(01)    VALUE 'N'.      CL247
00275          88  DONT-SETUP-INTER-APPL                   VALUE 'N'.      CL247
00276          88  SETUP-INTER-APPL                        VALUE 'Y'.      CL247
00277                                                                   MBKCS102
00278  01  PV-PROGRAM-VARIABLES.                                        MBKCS102
00279      05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0     MBKCS102
00280                                                      COMP SYNC.   MBKCS102
00281      05  PV-HIGHEST-ITEM-TO-BE-READ     PIC S9(09)   VALUE +0     MBKCS102
00282                                                      COMP-3.      MBKCS102
00283      05  PV-ENDING-ITEM-ON-PANEL        PIC S9(09)   VALUE +0     MBKCS102
00284                                                      COMP-3.      MBKCS102
00285      05  PV-ITEM-IN-USE                 PIC S9(09)   VALUE +0     MBKCS102
00286                                                      COMP-3.      MBKCS102
00287      05  PV-TOP-ITEM                    PIC S9(09)   VALUE +0     MBKCS102
00288                                                      COMP-3.      MBKCS102
00289      05  PV-BOTTOM-ITEM                 PIC S9(09)   VALUE +0     MBKCS102
00290                                                      COMP-3.      MBKCS102
00291      05  PV-MAX-ITEMS-TO-BE-READ        PIC S9(09)   VALUE +0     MBKCS102
00292                                                      COMP-3.      MBKCS102
00293      05  PV-FIRST-ITEM-IN-ARRAY         PIC S9(09)   VALUE +0     MBKCS102
00294                                                      COMP-3.      MBKCS102
00295      05  PV-LAST-ITEM-IN-ARRAY          PIC S9(09)   VALUE +0     MBKCS102
00296                                                      COMP-3.      MBKCS102
00297      05  PV-LAST-ITEM-IN-TEMP           PIC S9(09)   VALUE +0     MBKCS102
00298                                                      COMP-3.      MBKCS102
00299      05  PV-BEGINNING-ITEM              PIC S9(09)   VALUE +0     MBKCS102
00300                                                      COMP-3.      MBKCS102
00301      05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)   VALUE +0     MBKCS102
00302                                                      COMP-3.      MBKCS102
00303      05  PV-BLOCK-NUMBER                PIC S9(04)   VALUE +0     MBKCS102
00304                                                      COMP SYNC.   MBKCS102
00305      05  PV-BLOCK                       PIC X(1261)  VALUE SPACES.   CL*25
00306      05  FILLER REDEFINES PV-BLOCK.                                  CL*19
00307          10  FILLER                     PIC X(01).                   CL*19
00308          10  PV-BLOCK-ENTRIES OCCURS 18 TIMES.                       CL*19
00309              15  FILLER                 PIC X(05).                   CL*19
00310              15  PV-SELECTION-ENTRY     PIC X(01).                   CL*19
00311                  88  PV-SELECTION-MADE  VALUE 'S'.                   CL*19
00312              15  PV-SELECTED-GMA        PIC X(02).                   CL*19
00313              15  PV-SELECTED-DMA        PIC X(02).                   CL*19
00314              15  PV-SELECTED-DPT        PIC X(03).                   CL*19
00315              15  PV-SELECTED-MCL        PIC X(02).                   CL*19
00316              15  PV-SELECTED-SCL        PIC X(03).                   CL*19
00317              15  PV-SELECTED-LEVEL      PIC X(03).                   CL*19
00318              15  FILLER                 PIC X(33).                   CL*19
00319              15  PV-SELECTED-END-DATE   PIC X(08).                   CL*19
00320              15  FILLER                 PIC X(08).                   CL*19
00321      05  PV-SELECTED-GMA-NO             PIC  X(02)   VALUE SPACES.   CL236
00322      05  PV-SELECTED-DMA-NO             PIC  X(02)   VALUE SPACES.   CL236
00323      05  PV-SELECTED-DPT-NO             PIC  X(03)   VALUE SPACES.   CL236
00324      05  PV-SELECTED-MCL-NO             PIC  X(02)   VALUE SPACES.   CL236
00325      05  PV-SELECTED-SCL-NO             PIC  X(03)   VALUE SPACES.   CL236
00326      05  PV-SELECTED-LEVEL-CD           PIC  X(03)   VALUE SPACES.   CL236
00327      05  PV-SELECTED-END-DT             PIC  X(08)   VALUE SPACES.   CL236
00328      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)   VALUE +0     MBKCS102
00329                                                      COMP SYNC.   MBKCS102
00330      05  PV-LIST-ITEM-NUMBER            PIC S9(04)   VALUE +0     MBKCS102
00331                                                      COMP SYNC.   MBKCS102
00332      05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)   VALUE +0     MBKCS102
00333                                                      COMP SYNC.   MBKCS102
00334      05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)   VALUE +0     MBKCS102
00335                                                      COMP SYNC.   MBKCS102
00336      05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)   VALUE +0     MBKCS102
00337                                                      COMP SYNC.   MBKCS102
00338      05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)   VALUE +0     MBKCS102
00339                                                      COMP SYNC.   MBKCS102
00340      05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)   VALUE +0     MBKCS102
00341                                                      COMP SYNC.   MBKCS102
00342      05  PV-START-OF-SELECTED-RANGE     PIC S9(04)   VALUE +0     MBKCS102
00343                                                      COMP SYNC.   MBKCS102
00344      05  PV-END-OF-SELECTED-RANGE       PIC S9(04)   VALUE +0     MBKCS102
00345                                                      COMP SYNC.   MBKCS102
00346      05  PV-ITEM                        PIC S9(04)   VALUE +0        CL211
00347                                                      COMP SYNC.      CL211
00348      05  PV-SAVE-ITEM                   PIC S9(04)   VALUE +0        CL244
00349                                                      COMP SYNC.      CL244
00350      05  PV-MA-ITEM                     PIC S9(04)   VALUE +0        CL244
00351                                                      COMP SYNC.      CL244
00352      05  PV-RESP                        PIC S9(04)   VALUE +0        CL211
00353                                                      COMP SYNC.      CL211
00354      05  PV-MESSAGE                     PIC X(78)    VALUE SPACES.   CL*74
00355      05  PV-DISPLAY-7                   PIC ZZZZZZ9.                 CL178
00356      05  PV-DB2-DATE.                                                CL*66
00357          10  PV-DB2-CENTURY             PIC X(02)    VALUE SPACES.   CL*66
00358          10  PV-DB2-YEAR                PIC X(02)    VALUE SPACES.   CL*66
00359          10  PV-DB2-DASH-1              PIC X(01)    VALUE SPACES.   CL*66
00360          10  PV-DB2-MONTH               PIC X(02)    VALUE SPACES.   CL*67
00361          10  PV-DB2-DASH-2              PIC X(01)    VALUE SPACES.   CL*66
00362          10  PV-DB2-DAY                 PIC X(02)    VALUE SPACES.   CL*66
00363                                                                      CL*66
00364      05  PV-PANEL-DATE.                                              CL*67
00365          10  PV-PANEL-MONTH             PIC X(02)    VALUE SPACES.   CL*66
00366          10  PV-PANEL-SLASH-1           PIC X(01)    VALUE '/'.      CL*66
00367          10  PV-PANEL-DAY               PIC X(02)    VALUE SPACES.   CL*66
00368          10  PV-PANEL-SLASH-2           PIC X(01)    VALUE '/'.      CL*66
00369          10  PV-PANEL-YEAR              PIC X(02)    VALUE SPACES.   CL*66
00370      05  PV-TEMP-DATE.                                               CL237
00371          10  PV-TEMPYY                  PIC 9(02)    VALUE ZEROES.   CL237
00372          10  PV-TEMPMM                  PIC 9(02)    VALUE ZEROES.   CL237
00373          10  PV-TEMPDD                  PIC 9(02)    VALUE ZEROES.   CL237
00374                                                                      CL*66
00375      05  PV-MSG-NUMBER-X                PIC X(05)    VALUE SPACES.   CL**7
00376      05  PV-GMA-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00377      05  PV-DMA-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00378      05  PV-DPT-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00379      05  PV-MCL-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00380      05  PV-SCL-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00381      05  PV-GMA-NBR                     PIC X(02)    VALUE SPACES.   CL*81
00382      05  PV-DMA-NBR                     PIC X(02)    VALUE SPACES.   CL*81
00383      05  PV-DPT-NBR                     PIC X(03)    VALUE SPACES.   CL*81
00384      05  PV-MCL-NBR                     PIC X(02)    VALUE SPACES.   CL*81
00385      05  PV-SCL-NBR                     PIC X(03)    VALUE SPACES.   CL*81
00386 *                                                                    CL251
00387      05  PV-HOLD-KEY-DATA.                                           CL251
00388          10  PV-HK-ITEM-GMA             PIC X(02).                   CL251
00389          10  PV-HK-ITEM-DMA             PIC X(02).                   CL251
00390          10  PV-HK-ITEM-DEPT            PIC X(03).                   CL251
00391          10  PV-HK-ITEM-MCLASS          PIC X(02).                   CL251
00392          10  PV-HK-ITEM-SCLASS          PIC X(03).                   CL251
00393          10  PV-HK-ITEM-LEVEL           PIC X(03).                   CL251
00394 *                                                                    CL209
00395      05  PV-QUEUE-NAME.                                              CL213
00396          10  PV-TERM-ID                 PIC X(04).                   CL209
00397          10  PV-TASK-NUMBER             PIC S9(07)    COMP-3.        CL*20
00398 *                                                                    CL209
00399  01  PV-TEMP-WORK-AREA.                                              CL209
00400          10  PV-BLOCK-MODIFIED          PIC X.                       CL210
00401          10  PV-TW-ITEM-ENTRIES          OCCURS  18 TIMES            CL210
00402              INDEXED BY PV-TW-IDX.                                   CL210
00403              15  PV-TW-LIST-ITEM-NUMBER PIC S9(9)     COMP-3.        CL209
00404              15  PV-TW-SELECTED-SW      PIC X(01).                   CL209
00405                  88  PV-TW-SELECT                    VALUE 'S'.      CL209
00406                  88  PV-TW-DESELECT                  VALUE ' '.      CL249
00407            12    PV-TW-KEY-DATA.                                     CL209
00408              15  PV-TW-ITEM-GMA         PIC X(02).                   CL209
00409              15  PV-TW-ITEM-DMA         PIC X(02).                   CL209
00410              15  PV-TW-ITEM-DEPT        PIC X(03).                   CL209
00411              15  PV-TW-ITEM-MCLASS      PIC X(02).                   CL209
00412              15  PV-TW-ITEM-SCLASS      PIC X(03).                   CL209
00413              15  PV-TW-ITEM-LEVEL       PIC X(03).                   CL209
00414                  88  PV-TW-MCL-SCL                   VALUE 'MCL'     CL*32
00415                                                            'SCL'.    CL*32
00416            12    PV-TW-KEY-DATA-END.                                 CL244
00417              15  PV-TW-ITEM-DESC        PIC X(25).                   CL209
00418              15  PV-TW-ITEM-STR-DATE    PIC X(08).                   CL209
00419              15  PV-TW-ITEM-END-DATE    PIC X(08).                   CL209
00420              15  PV-TW-ITEM-ACTION      PIC X(07).                   CL209
00421              15  PV-TW-COLOR            PIC X.                       CL**5
00422  EJECT                                                            MBKCS102
00423 ***************************************************************** MBKCS102
00424 *    LIST TS QUEUE RECORD LAYOUT.                                    CL209
00425 ***************************************************************** MBKCS102
00426  01  SELECTED-LIST-RECORD.                                           CL209
00427      COPY MBWS003.                                                   CL*41
00428                                                                      CL209
00429  EJECT                                                               CL209
00430 *****************************************************************    CL209
00431 *    STANDARD COMMAREA.                                              CL209
00432 *****************************************************************    CL209
00433                                                                      CL209
00434      COPY DPWS020.                                                   CL209
00435      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                MBKCS102
00436                                                                   MBKCS102
00437 ***************************************************************** MBKCS102
00438 *    INTER-APPLICATION COMMAREA.                                  MBKCS102
00439 ***************************************************************** MBKCS102
00440                                                                   MBKCS102
00441          10  INTER-APPL-COMM-AREA       PIC X(200).               MBKCS102
00442      COPY MBWS005.                                                   CL*41
00443                                                                      CL**4
00444 ***************************************************************** MBKCS102
00445 *                                                                *   CL209
00446 *   THIS IS THE APPLICATION SPECIFIC COMMUNICATION AREA FOR      *   CL209
00447 *   PROGRAM MBKCS102.                                            *   CL209
00448 *                                                                *   CL209
00449 *   LENGTH:  2865 BYTES                                          *   CL209
00450 ******************************************************************   CL209
00451          10  MB102-SPECIFIC-COMMAREA.                                CL209
00452              15  FILLER                               PIC X(02).     CL209
00453              15  MB102-CO-NUMBER                      PIC X(02).     CL209
00454              15  MB102-REQUEST-DATA.                                 CL209
00455                  20  MB102-GMA-NUMBER                 PIC X(02).     CL209
00456                      88  MB102-ALL-GMAS               VALUE '* ',    CL209
00457                                                             '**',    CL209
00458                                                             ' *'.    CL209
00459                  20  MB102-DMA-NUMBER                 PIC X(02).     CL209
00460                      88  MB102-ALL-DMAS               VALUE '* ',    CL209
00461                                                             '**',    CL209
00462                                                             ' *'.    CL209
00463                  20  MB102-DEPT-NUMBER                PIC X(03).     CL209
00464                      88  MB102-ALL-DEPARTMENTS        VALUE '*  ',   CL209
00465                                                             '** ',   CL209
00466                                                             '***',   CL209
00467                                                             ' **',   CL209
00468                                                             '  *'.   CL209
00469                  20  MB102-MCLASS-NUMBER              PIC X(02).     CL209
00470                      88  MB102-ALL-MCLASSES           VALUE '* ',    CL209
00471                                                             '**',    CL209
00472                                                             ' *'.    CL209
00473                  20  MB102-SCLASS-NUMBER              PIC X(03).     CL209
00474                      88  MB102-ALL-SCLASSES           VALUE '*  ',   CL209
00475                                                             '** ',   CL209
00476                                                             '***',   CL209
00477                                                             ' **',   CL209
00478                                                             '  *'.   CL209
00479              15  MB102-BLOCK-ARRAY.                                  CL209
00480                  20  MB102-BLOCK-ENTRIES OCCURS 2 TIMES.             CL209
00481                      25  MB102-BLOCK-MODIFIED-SW      PIC X(01).     CL209
00482                          88  MB102-BLOCK-MODIFIED     VALUE 'Y'.     CL209
00483                          88  MB102-BLOCK-NOT-MODIFIED VALUE 'N'.     CL209
00484                      25  MB102-ITEM-ARRAY.                           CL209
00485                          30  MB102-ITEM-ENTRIES OCCURS 18 TIMES.     CL209
00486                              35  MB102-LIST-ITEM-NUMBER              CL209
00487                                                       PIC S9(09)     CL209
00488                                                           COMP-3.    CL209
00489                              35  MB102-SELECTED-SW    PIC X(01).     CL209
00490                                  88  MB102-NO-SELECT  VALUE ' '.     CL209
00491                                  88  MB102-SELECT     VALUE 'S'.     CL209
00492                                  88  MB102-BEGIN      VALUE 'B'.     CL209
00493                                  88  MB102-END        VALUE 'E'.     CL209
00494                           33     MB102-KEY-DATA.                     CL209
00495                              35  MB102-ITEM-GMA       PIC X(02).     CL209
00496                              35  MB102-ITEM-DMA       PIC X(02).     CL209
00497                              35  MB102-ITEM-DEPT      PIC X(03).     CL209
00498                              35  MB102-ITEM-MCLASS    PIC X(02).     CL209
00499                              35  MB102-ITEM-SCLASS    PIC X(03).     CL209
00500                              35  MB102-ITEM-LEVEL     PIC X(03).     CL209
00501                           33     MB102-KEY-DATA-END.                 CL244
00502                              35  MB102-ITEM-DESC      PIC X(25).     CL209
00503                              35  MB102-ITEM-STR-DATE  PIC X(08).     CL209
00504                              35  MB102-ITEM-END-DATE  PIC X(08).     CL209
00505                              35  MB102-ITEM-ACTION    PIC X(07).     CL209
00506                              35  MB102-COLOR          PIC X.         CL255
00507                                                                      CL209
00508              15  MB102-BLK-SUB                        PIC S9(04)     CL210
00509                                                           COMP       CL209
00510                                                           SYNC.      CL209
00511              15  MB102-ITEM-SUB                       PIC S9(04)     CL209
00512                                                           COMP       CL209
00513                                                           SYNC.      CL209
00514              15  MB102-NUMBER-OF-ITEMS-READ           PIC S9(09)     CL209
00515                                                           COMP-3.    CL209
00516              15  MB102-NUMBER-OF-ITEMS-SELECTED       PIC S9(09)     CL209
00517                                                           COMP-3.    CL209
00518              15  MB102-ITEM-AT-TOP-OF-PANEL           PIC S9(09)     CL209
00519                                                           COMP-3.    CL209
00520              15  MB102-HIGHEST-BLOCK-WRITTEN          PIC S9(04)     CL209
00521                                                           COMP       CL209
00522                                                           SYNC.      CL209
00523              15  MB102-START-OF-SELECTED-RANGE        PIC S9(09)     CL209
00524                                                           COMP-3.    CL209
00525              15  MB102-END-OF-SELECTED-RANGE          PIC S9(09)     CL209
00526                                                           COMP-3.    CL209
00527              15  MB102-DB2-CURRENT-DATE               PIC X(10).     CL**4
00528              15  MB102-ITEMS-LEFT-ON-DB-SW            PIC X(01).     CL209
00529                  88  MB102-ITEMS-LEFT-ON-DB           VALUE 'Y'.     CL209
00530                  88  MB102-NO-ITEMS-LEFT-ON-DB        VALUE 'N'.     CL209
00531              15  MB102-TEMP-QUEUE-UPDATE-SW           PIC X(01).     CL244
00532                  88  WE-MUST-UPDATE-TSQ               VALUE 'Y'.     CL244
00533                  88  NO-UPDATE-TO-TSQ                 VALUE 'N'.     CL244
00534              15  MB102-LIST-QUEUE-NAME                PIC X(8).      CL213
00535              15  MB102-TEMP-QUEUE-NAME.                              CL213
00536                  20  MB102-TERM-ID                    PIC X(04).     CL209
00537                  20  MB102-TASK-NUMBER                PIC S9(07)     CL209
00538                                                           COMP-3.    CL209
00539              15  MB102-SAVE-LEVEL-CODE.                              CL209
00540                  20  MB102-SAVE-GMA                   PIC X(03).     CL209
00541                  20  MB102-SAVE-DMA                   PIC X(03).     CL209
00542                  20  MB102-SAVE-DPT                   PIC X(03).     CL209
00543                  20  MB102-SAVE-MCL                   PIC X(03).     CL209
00544                  20  MB102-SAVE-SCL                   PIC X(03).     CL209
00545              15  MB102-SAVE-KEY.                                     CL209
00546                  20  MB102-SAVE-SEQ-NBR               PIC S9(15)V    CL230
00547                                                       COMP-3.        CL230
00548                  20  MB102-SAVE-GMA-NBR               PIC X(02).     CL209
00549                  20  MB102-SAVE-DMA-NBR               PIC X(02).     CL209
00550                  20  MB102-SAVE-DPT-NBR               PIC X(03).     CL209
00551                  20  MB102-SAVE-MCL-NBR               PIC X(02).     CL209
00552                  20  MB102-SAVE-SCL-NBR               PIC X(03).     CL209
00553                                                                      CL209
00554              15  MB102-OPEN-CURSOR-SW                 PIC X(01).     CL209
00555                  88  MB102-NO-CURSOR-OPEN             VALUE '0'.     CL209
00556                  88  MB102-OPEN-MERCH-CURSOR          VALUE '1'.     CL209
00557                  88  MB102-OPEN-GMA-CURSOR            VALUE '2'.     CL209
00558                  88  MB102-OPEN-DMA-CURSOR            VALUE '3'.     CL209
00559                  88  MB102-OPEN-DPT-CURSOR            VALUE '4'.     CL209
00560                  88  MB102-OPEN-MCL-CURSOR            VALUE '5'.     CL209
00561                  88  MB102-OPEN-SCL-CURSOR            VALUE '6'.     CL209
00562              15  MB102-HISTORY-FLAG                   PIC X(01).     CL222
00563                  88  MB102-HISTORY                    VALUE 'Y'.     CL222
00564                  88  MB102-NO-HISTORY                 VALUE 'N'.     CL222
00565              15  MB102-END-DATE                       PIC X(10).     CL222
00566                                                                      CL209
00567              15  FILLER                               PIC X(219).    CL**4
00568 ***************************************************************** MBKCS102
00569 *    ABEND PROCESSING COPYBOOK.                                   MBKCS102
00570 ***************************************************************** MBKCS102
00571                                                                   MBKCS102
00572      COPY DPWS013.                                                MBKCS102
00573                                                                   MBKCS102
00574 ***************************************************************** MBKCS102
00575 *    ATTRIBUTE SETTINGS COPYBOOK.                                 MBKCS102
00576 ***************************************************************** MBKCS102
00577                                                                   MBKCS102
00578      COPY DPWS015.                                                MBKCS102
00579                                                                   MBKCS102
00580 ***************************************************************** MBKCS102
00581 *    CURRENT DATE AND TIME COPYBOOK.                              MBKCS102
00582 ***************************************************************** MBKCS102
00583                                                                   MBKCS102
00584      COPY DPWS017.                                                MBKCS102
00585                                                                   MBKCS102
00586 ***************************************************************** MBKCS102
00587 *    TOGGLE FUNCTION KEY LINES COPYBOOK.                          MBKCS102
00588 ***************************************************************** MBKCS102
00589                                                                   MBKCS102
00590      COPY DPWS009.                                                MBKCS102
                                                                        MBKCS101
      *****************************************************************         
      *    SYSTEM MESSAGE FIELDS                                                
      *****************************************************************         
                                                                                
           COPY DPWS006.                                                        
                                                                                
00592 ***************************************************************** MBKCS102
00593 *    FUNCTION KEYS COPYBOOK.                                      MBKCS102
00594 ***************************************************************** MBKCS102
00595                                                                   MBKCS102
00596      COPY DPWS016.                                                MBKCS102
00597                                                                   MBKCS102
00598 ***************************************************************** MBKCS102
00599 *    DPKUT045 COPYBOOK.                                           MBKCS102
00600 *                                                                 MBKCS102
00601 *    DATA AREA NECESSARY TO INTERACT WITH SUB-ROUTINE DPKUT045    MBKCS102
00602 *                                                                 MBKCS102
00603 *    DPKUT045 IS A UTILITY ROUTINE THAT CONVERTS THE EIBAID       MBKCS102
00604 *    CHARACTER RECEIVED FROM THE TERMINAL INTO A INTEGER VALUE    MBKCS102
00605 *    FROM 1 TO 29. THIS INTEGER VALUE IS STORED IN DP020-SRC-AID  MBKCS102
00606 *    AND IS USED TO INTERROGATE THE DP020-FUNCTION-KEY-ARRAY      MBKCS102
00607 *    TABLE IN THE STANDARD COMMAREA TO DETERMINE WHAT TO DO NEXT. MBKCS102
00608 *    IN ADDITION DPKUT045 WILL PARSE THE SCREEN INPUT COMMAND     MBKCS102
00609 *    FIELD AND CONVERT RECOGNIZED SCREEN MANIPULATION COMMANDS    MBKCS102
00610 *    INTO THE DP020-SRC-AID EQUIVALENT.                           MBKCS102
00611 ***************************************************************** MBKCS102
00612                                                                   MBKCS102
00613      COPY DPWS045.                                                MBKCS102
00614                                                                   MBKCS102
00615 ***************************************************************** MBKCS102
00616 *    NUMERIC EDIT LAYOUT.                                         MBKCS102
00617 ***************************************************************** MBKCS102
00618                                                                   MBKCS102
00619      COPY DPWS010I.                                               MBKCS102
00620                                                                   MBKCS102
00621 *****************************************************************    CL**7
00622 *    SYSTEM MESSAGES RECORD LAYOUT                                   CL**7
00623 *****************************************************************    CL**7
00624                                                                      CL**7
00625      COPY DPRD507I.                                                  CL**7
00626                                                                      CL**7
00627 ***************************************************************** MBKCS102
00628 *    STANDARD ATTENTION IDENTIFIER LIST                           MBKCS102
00629 ***************************************************************** MBKCS102
00630                                                                   MBKCS102
00631      COPY DFHAID.                                                 MBKCS102
00632                                                                   MBKCS102
00633 ***************************************************************** MBKCS102
00634 *    MAP LAYOUT                                                   MBKCS102
00635 ***************************************************************** MBKCS102
00636                                                                   MBKCS102
00637      COPY MBKM102.                                                   CL200
00638                                                                   MBKCS102
00639      COPY MBWS004.                                                   CL*41
00640                                                                   MBKCS102
00641  EJECT                                                            MBKCS102
00642 *****************************************************************    CL*15
00643 *    DB2 COMMUNICATION AREA                                          CL*15
00644 *****************************************************************    CL*15
00645                                                                      CL*15
00646      EXEC SQL                                                        CL*15
00647          INCLUDE SQLCA                                               CL*15
00648      END-EXEC.                                                       CL*15
00649                                                                      CL*15
00650 *****************************************************************    CL*15
00651 *    MERCHANDISE AREA TABLE - TMDSEAR                                CL*15
00652 *****************************************************************    CL*15
00653                                                                      CL*15
00654      EXEC SQL                                                        CL*15
00655          INCLUDE TMDSEAR                                             CL*15
00656      END-EXEC.                                                       CL*15
00657                                                                      CL*15
00658 *****************************************************************    CL165
00659 *    DECLARE FOR THE MERCH-CURSOR                                    CL165
00660 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00661 *       IS FOR ALL GMAS, DMAS, DEPARTMENTS, MAJOR CLASSES,           CL165
00662 *       AND SUB CLASSES                                              CL165
00663 *****************************************************************    CL165
00664                                                                      CL165
00665      EXEC SQL                                                        CL*90
00666          DECLARE MERCH-CURSOR CURSOR FOR                             CL*90
00667              SELECT  OPERCO_NBR                                      CL*90
00668                    , GMA_NBR                                         CL*90
00669                    , DMA_NBR                                         CL*90
00670                    , DEPT_NBR                                        CL*90
00671                    , MAJ_CL_NBR                                      CL*90
00672                    , SUB_CL_NBR                                      CL*90
00673                    , MDSELV_CDE                                      CL*90
00674                    , CHG_DTE                                         CL*90
00675                    , CRE_DTE                                         CL*90
00676                    , STRT_DTE                                        CL*90
00677                    , END_DTE                                         CL*90
00678                    , GEN_MDSE_TYP_CDE                                CL*90
00679                    , MDSEAR_DESC                                     CL*90
00680                    , BYR_NBR                                         CL*90
00681                    , LIST_SEQ_NBR                                    CL107
00682                FROM  TMDSEAR                                         CL*90
00683               WHERE (OPERCO_NBR    = :PC-OPERCO-NBR)                 CL222
00684                 AND (LIST_SEQ_NBR  > :MB102-SAVE-SEQ-NBR)            CL222
00685                 AND (END_DTE       > :MB102-END-DATE)                CL222
00686                 AND (MDSELV_CDE    = :MB102-SAVE-GMA  OR             CL209
00687                      MDSELV_CDE    = :MB102-SAVE-DMA  OR             CL209
00688                      MDSELV_CDE    = :MB102-SAVE-DPT  OR             CL209
00689                      MDSELV_CDE    = :MB102-SAVE-MCL  OR             CL209
00690                      MDSELV_CDE    = :MB102-SAVE-SCL)                CL209
00691            ORDER BY  OPERCO_NBR                                      CL*90
00692                    , GMA_NBR                                         CL*90
00693                    , DMA_NBR                                         CL*90
00694                    , DEPT_NBR                                        CL*90
00695                    , MAJ_CL_NBR                                      CL*90
00696                    , SUB_CL_NBR                                      CL*90
00697      END-EXEC.                                                       CL*90
00698                                                                      CL165
00699 *****************************************************************    CL165
00700 *    DECLARE FOR THE GMA-CURSOR                                      CL165
00701 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00702 *       IS FOR A SPECIFIC GMA, AND ALL LEVELS SELECTED WITHIN        CL165
00703 *       THE REQUESTED GMA                                            CL165
00704 *****************************************************************    CL165
00705                                                                      CL165
00706      EXEC SQL                                                        CL*97
00707          DECLARE GMA-CURSOR CURSOR FOR                               CL*97
00708              SELECT  OPERCO_NBR                                      CL*97
00709                    , GMA_NBR                                         CL*97
00710                    , DMA_NBR                                         CL*97
00711                    , DEPT_NBR                                        CL*97
00712                    , MAJ_CL_NBR                                      CL*97
00713                    , SUB_CL_NBR                                      CL*97
00714                    , MDSELV_CDE                                      CL*97
00715                    , CHG_DTE                                         CL*97
00716                    , CRE_DTE                                         CL*97
00717                    , STRT_DTE                                        CL*97
00718                    , END_DTE                                         CL*97
00719                    , GEN_MDSE_TYP_CDE                                CL*97
00720                    , MDSEAR_DESC                                     CL*97
00721                    , BYR_NBR                                         CL*97
00722                    , LIST_SEQ_NBR                                    CL107
00723                FROM  TMDSEAR                                         CL*97
00724               WHERE (OPERCO_NBR    = :PC-OPERCO-NBR)                 CL222
00725                 AND (GMA_NBR       = :MB102-SAVE-GMA-NBR)            CL222
00726                 AND (END_DTE       > :MB102-END-DATE)                CL222
00727                 AND (LIST_SEQ_NBR  > :MB102-SAVE-SEQ-NBR)            CL222
00728                 AND (MDSELV_CDE    = :MB102-SAVE-GMA  OR             CL209
00729                      MDSELV_CDE    = :MB102-SAVE-DMA  OR             CL209
00730                      MDSELV_CDE    = :MB102-SAVE-DPT  OR             CL209
00731                      MDSELV_CDE    = :MB102-SAVE-MCL  OR             CL209
00732                      MDSELV_CDE    = :MB102-SAVE-SCL)                CL209
00733            ORDER BY  OPERCO_NBR                                      CL*97
00734                    , GMA_NBR                                         CL*97
00735                    , DMA_NBR                                         CL*97
00736                    , DEPT_NBR                                        CL*97
00737                    , MAJ_CL_NBR                                      CL*97
00738                    , SUB_CL_NBR                                      CL*97
00739      END-EXEC.                                                       CL*97
00740                                                                      CL165
00741 *****************************************************************    CL165
00742 *    DECLARE FOR THE DMA-CURSOR                                      CL165
00743 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00744 *       IS FOR A SPECIFIC DMA, AND ALL LEVELS,(DMA AND LOWER),       CL165
00745 *       SELECTED WITHIN THE REQUESTED DMA                            CL165
00746 *****************************************************************    CL165
00747                                                                      CL119
00748      EXEC SQL                                                        CL119
00749          DECLARE DMA-CURSOR CURSOR FOR                               CL119
00750              SELECT  OPERCO_NBR                                      CL119
00751                    , GMA_NBR                                         CL119
00752                    , DMA_NBR                                         CL119
00753                    , DEPT_NBR                                        CL119
00754                    , MAJ_CL_NBR                                      CL119
00755                    , SUB_CL_NBR                                      CL119
00756                    , MDSELV_CDE                                      CL119
00757                    , CHG_DTE                                         CL119
00758                    , CRE_DTE                                         CL119
00759                    , STRT_DTE                                        CL119
00760                    , END_DTE                                         CL119
00761                    , GEN_MDSE_TYP_CDE                                CL119
00762                    , MDSEAR_DESC                                     CL119
00763                    , BYR_NBR                                         CL119
00764                    , LIST_SEQ_NBR                                    CL119
00765                FROM  TMDSEAR                                         CL119
00766               WHERE (OPERCO_NBR    = :PC-OPERCO-NBR)                 CL222
00767                 AND (DMA_NBR       = :MB102-SAVE-DMA-NBR)            CL222
00768                 AND (END_DTE       > :MB102-END-DATE)                CL222
00769                 AND (MDSELV_CDE    = :MB102-SAVE-DMA  OR             CL209
00770                      MDSELV_CDE    = :MB102-SAVE-DPT  OR             CL209
00771                      MDSELV_CDE    = :MB102-SAVE-MCL  OR             CL209
00772                      MDSELV_CDE    = :MB102-SAVE-SCL)                CL209
00773                 AND (LIST_SEQ_NBR  > :MB102-SAVE-SEQ-NBR)            CL222
00774            ORDER BY  OPERCO_NBR                                      CL119
00775                    , GMA_NBR                                         CL119
00776                    , DMA_NBR                                         CL119
00777                    , DEPT_NBR                                        CL119
00778                    , MAJ_CL_NBR                                      CL119
00779                    , SUB_CL_NBR                                      CL119
00780      END-EXEC.                                                       CL119
00781                                                                      CL165
00782 *****************************************************************    CL165
00783 *    DECLARE FOR THE DPT-CURSOR                                      CL165
00784 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00785 *       IS FOR A SPECIFIC DPT, AND ALL LEVELS,(DPT AND LOWER),       CL165
00786 *       SELECTED WITHIN THE REQUESTED DPT                            CL165
00787 *****************************************************************    CL165
00788                                                                      CL129
00789      EXEC SQL                                                        CL129
00790          DECLARE DPT-CURSOR CURSOR FOR                               CL129
00791              SELECT  OPERCO_NBR                                      CL129
00792                    , GMA_NBR                                         CL129
00793                    , DMA_NBR                                         CL129
00794                    , DEPT_NBR                                        CL129
00795                    , MAJ_CL_NBR                                      CL129
00796                    , SUB_CL_NBR                                      CL129
00797                    , MDSELV_CDE                                      CL129
00798                    , CHG_DTE                                         CL129
00799                    , CRE_DTE                                         CL129
00800                    , STRT_DTE                                        CL129
00801                    , END_DTE                                         CL129
00802                    , GEN_MDSE_TYP_CDE                                CL129
00803                    , MDSEAR_DESC                                     CL129
00804                    , BYR_NBR                                         CL129
00805                    , LIST_SEQ_NBR                                    CL129
00806                FROM  TMDSEAR                                         CL129
00807               WHERE (OPERCO_NBR    = :PC-OPERCO-NBR)                 CL222
00808                 AND (DEPT_NBR      = :MB102-SAVE-DPT-NBR)            CL222
00809                 AND (MDSELV_CDE    = :MB102-SAVE-DPT  OR             CL209
00810                      MDSELV_CDE    = :MB102-SAVE-MCL  OR             CL209
00811                      MDSELV_CDE    = :MB102-SAVE-SCL)                CL209
00812                 AND (END_DTE       > :MB102-END-DATE)                CL222
00813                 AND (LIST_SEQ_NBR  > :MB102-SAVE-SEQ-NBR)            CL222
00814            ORDER BY  OPERCO_NBR                                      CL129
00815                    , GMA_NBR                                         CL129
00816                    , DMA_NBR                                         CL129
00817                    , DEPT_NBR                                        CL129
00818                    , MAJ_CL_NBR                                      CL129
00819                    , SUB_CL_NBR                                      CL129
00820      END-EXEC.                                                       CL129
00821                                                                      CL165
00822 *****************************************************************    CL165
00823 *    DECLARE FOR THE MCL-CURSOR                                      CL165
00824 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00825 *       IS FOR A SPECIFIC MCL, WITHIN A DEPARTMENT AND ALL           CL165
00826 *       LEVELS,(MCL AND LOWER), SELECTED WITHIN THE REQUESTED MCL    CL165
00827 *****************************************************************    CL165
00828                                                                      CL*97
00829      EXEC SQL                                                        CL135
00830          DECLARE MCL-CURSOR CURSOR FOR                               CL135
00831              SELECT  OPERCO_NBR                                      CL135
00832                    , GMA_NBR                                         CL135
00833                    , DMA_NBR                                         CL135
00834                    , DEPT_NBR                                        CL135
00835                    , MAJ_CL_NBR                                      CL135
00836                    , SUB_CL_NBR                                      CL135
00837                    , MDSELV_CDE                                      CL135
00838                    , CHG_DTE                                         CL135
00839                    , CRE_DTE                                         CL135
00840                    , STRT_DTE                                        CL135
00841                    , END_DTE                                         CL135
00842                    , GEN_MDSE_TYP_CDE                                CL135
00843                    , MDSEAR_DESC                                     CL135
00844                    , BYR_NBR                                         CL135
00845                    , LIST_SEQ_NBR                                    CL135
00846                FROM  TMDSEAR                                         CL135
00847               WHERE (OPERCO_NBR    = :PC-OPERCO-NBR)                 CL222
00848                AND  (DEPT_NBR      = :MB102-SAVE-DPT-NBR)            CL222
00849                AND ((MAJ_CL_NBR    = :MB102-SAVE-MCL-NBR             CL222
00850                AND  (MDSELV_CDE    = :MB102-SAVE-MCL                 CL222
00851                 OR   MDSELV_CDE    = :MB102-SAVE-SCL))               CL222
00852                 OR  (MDSELV_CDE    = :MB102-SAVE-DPT))               CL222
00853                AND  (END_DTE       > :MB102-END-DATE)                CL222
00854                AND  (LIST_SEQ_NBR  > :MB102-SAVE-SEQ-NBR)            CL222
00855            ORDER BY  OPERCO_NBR                                      CL135
00856                    , GMA_NBR                                         CL135
00857                    , DMA_NBR                                         CL135
00858                    , DEPT_NBR                                        CL135
00859                    , MAJ_CL_NBR                                      CL135
00860                    , SUB_CL_NBR                                      CL135
00861      END-EXEC.                                                       CL135
00862                                                                      CL165
00863 *****************************************************************    CL165
00864 *    DECLARE FOR THE SCL-CURSOR                                      CL165
00865 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00866 *       IS FOR A SPECIFIC SCL, WITHIN A DEPARTMENT AND MAJOR CLASS   CL165
00867 *****************************************************************    CL165
00868                                                                      CL135
00869      EXEC SQL                                                        CL148
00870          DECLARE SCL-CURSOR CURSOR FOR                               CL148
00871              SELECT  OPERCO_NBR                                      CL148
00872                    , GMA_NBR                                         CL148
00873                    , DMA_NBR                                         CL148
00874                    , DEPT_NBR                                        CL148
00875                    , MAJ_CL_NBR                                      CL148
00876                    , SUB_CL_NBR                                      CL148
00877                    , MDSELV_CDE                                      CL148
00878                    , CHG_DTE                                         CL148
00879                    , CRE_DTE                                         CL148
00880                    , STRT_DTE                                        CL148
00881                    , END_DTE                                         CL148
00882                    , GEN_MDSE_TYP_CDE                                CL148
00883                    , MDSEAR_DESC                                     CL148
00884                    , BYR_NBR                                         CL148
00885                    , LIST_SEQ_NBR                                    CL148
00886                FROM  TMDSEAR                                         CL148
00887               WHERE (OPERCO_NBR    = :PC-OPERCO-NBR)                 CL222
00888                 AND (MDSELV_CDE    = :MB102-SAVE-SCL)                CL222
00889                 AND (DEPT_NBR      = :MB102-SAVE-DPT-NBR)            CL222
00890                 AND (MAJ_CL_NBR    = :MB102-SAVE-MCL-NBR)            CL222
00891                 AND (SUB_CL_NBR    = :MB102-SAVE-SCL-NBR)            CL222
00892                 AND (LIST_SEQ_NBR  > :MB102-SAVE-SEQ-NBR)            CL222
00893                 AND (END_DTE       > :MB102-END-DATE)                CL222
00894            ORDER BY  OPERCO_NBR                                      CL148
00895                    , GMA_NBR                                         CL148
00896                    , DMA_NBR                                         CL148
00897                    , DEPT_NBR                                        CL148
00898                    , MAJ_CL_NBR                                      CL148
00899                    , SUB_CL_NBR                                      CL148
00900      END-EXEC.                                                       CL148
00901                                                                      CL148
00902  LINKAGE SECTION.                                                 MBKCS102
00903                                                                   MBKCS102
00904  01  DFHCOMMAREA.                                                 MBKCS102
00905      05  FILLER                         OCCURS  1 TO 4072 TIMES   MBKCS102
00906                                         DEPENDING ON EIBCALEN.    MBKCS102
00907          10  FILLER                     PIC X.                    MBKCS102
00908                                                                   MBKCS102
00909 *                                                                 MBKCS102
00910 *                                                                 MBKCS102
00911 *                                                                 MBKCS102
00912  PROCEDURE DIVISION.                                              MBKCS102
00913                                                                   MBKCS102
00914                                                                   MBKCS102
00915  A100-MAIN-MODULE.                                                MBKCS102
00916                                                                   MBKCS102
00917 ******************************************************************MBKCS102
00918 *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.           MBKCS102
00919 *                                                                 MBKCS102
00920 ******************************************************************MBKCS102
00921                                                                   MBKCS102
00922      PERFORM DP015-0000-CHECK-COMMAREA.                           MBKCS102
00923                                                                   MBKCS102
00924 ******************************************************************MBKCS102
00925 *    DP020-SRC-RETURN-W-TRAN-ID WILL BE TRUE IF THIS PROGRAM WAS  MBKCS102
00926 *    SENT A STANDARD COMMAREA. IF NOT SET TRUE (MEANING OTHER     MBKCS102
00927 *    THAN A STANDARD COMMAREA WAS SENT TO THIS PROGRAM, THIS      MBKCS102
00928 *    PROGRAM WILL RETURN TO CICS.                                 MBKCS102
00929 ******************************************************************MBKCS102
00930                                                                   MBKCS102
00931      IF DP020-SRC-RETURN-W-TRAN-ID                                MBKCS102
00932          PERFORM B100-CONTROL-PROCESSING                          MBKCS102
00933      END-IF.                                                      MBKCS102
00934                                                                   MBKCS102
00935      PERFORM DP003-0000-RETURN-TO-CICS.                           MBKCS102
00936  EJECT                                                            MBKCS102
00937  B100-CONTROL-PROCESSING.                                         MBKCS102
00938                                                                   MBKCS102
00939 ******************************************************************MBKCS102
00940 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT    MBKCS102
00941 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                    MBKCS102
00942 *                                                                 MBKCS102
00943 *    PERFORMED BY A100-MAIN-MODULE                                   CL203
00944 ******************************************************************MBKCS102
00945                                                                   MBKCS102
00946      SET SETUP-INTER-APPL      TO  TRUE.                             CL247
00947      MOVE LENGTH OF MB102-BLOCK-ENTRIES (1)                          CL*18
00948                                TO  PC-BLOCK-LENGTH.                  CL*18
00949      EVALUATE TRUE                                                MBKCS102
00950                                                                   MBKCS102
00951          WHEN DP020-NEXT-ACT-INITIAL                                 CL*22
00952              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 MBKCS102
00953                                                                   MBKCS102
00954              PERFORM C100-INITIALIZE-COMMAREA                        CL**3
00955              PERFORM C200-SAVE-INITIAL-INTERAPPL                     CL**3
00956              IF PS-INTER-APPL-VALID                                  CL*23
00957                  PERFORM D100-SETUP-INIT-PANEL                       CL*23
00958                  IF PS-NO-SELECTIONS-FOUND                           CL*23
00959                      MOVE DP016-PF3             TO DP020-SRC-AID     CL*23
00960                      PERFORM DP015-1000-START-NAVIGATOR              CL*23
00961                  END-IF                                              CL*23
00962              ELSE                                                    CL*23
00963                  PERFORM Z100-RETURN-TO-PREV-TRAN                    CL*23
00964              END-IF                                                  CL*23
00965                                                                      CL157
00966          WHEN DP020-NEXT-ACT-READ-MAP                             MBKCS102
00967              PERFORM C300-PROCESS-PANEL                           MBKCS102
00968                                                                   MBKCS102
00969          WHEN DP020-NEXT-ACT-RETURN                               MBKCS102
00970              SET PS-SEND-MAP-N-DATA-FRSET   TO TRUE                  CL165
00971                                                                   MBKCS102
00972              PERFORM C400-UPDATE-INITIAL-INTERAPPL                   CL**3
00973              PERFORM C500-REFRESH-PANEL-FROM-TEMP                    CL**3
00974                                                                   MBKCS102
00975          WHEN DP020-NEXT-ACT-ERROR                                MBKCS102
00976                                                                   MBKCS102
00977 ******************************************************************MBKCS102
00978 *    THE FOLLOWING CODE HAS BEEN INSERTED IN ORDER TO PRESERVE    MBKCS102
00979 *    THE ERROR MESSAGE BEING RETURNED FROM THE NAVIGATOR. SINCE   MBKCS102
00980 *    THE ERROR BEING RETURNED WILL TYPICALLY BE A SECURITY        MBKCS102
00981 *    VIOLATION OR THE UNAVAILABILITY OF THE REQUESTED TRANSACTION MBKCS102
00982 *    THIS CODE ALSO CHECKS TO SEE HOW  THE NAVIGATION REQUEST WAS MBKCS102
00983 *    INVOKED. IF DP020-NAV-MNEMONIC FIELD IN THE STANDARD         MBKCS102
00984 *    COMMAREA IS SPACES, THE NAVIGATION REQUEST WAS VIA A FUNCTIONMBKCS102
00985 *    KEY. IF NOT, IT WAS VIA A COMMAND ENTERED IN THE COMMAND LINEMBKCS102
00986 *    COMMAND FIELD AND THIS LOGIC SETS THE ATTRIBUTE FOR THIS FIELMBKCS102
00987 *    TO RED, REVERSE VIDEO.                                       MBKCS102
00988 ******************************************************************MBKCS102
00989                                                                   MBKCS102
00990              MOVE DP020-MSG-MESSAGE         TO PV-MESSAGE            CL165
00991                                                                   MBKCS102
00992              IF DP020-NAV-MNEMONIC = SPACES                       MBKCS102
00993                  MOVE SPACES                TO ANAVCMDO              CL165
00994                  MOVE DP015-GREEN           TO ANAVCMDC              CL165
00995                  MOVE DP015-UNDERLINE       TO ANAVCMDH              CL165
00996              ELSE                                                 MBKCS102
00997                  MOVE DP015-RED             TO ANAVCMDC              CL165
00998                  MOVE DP015-REVERSE         TO ANAVCMDH              CL165
00999              END-IF                                               MBKCS102
01000                                                                   MBKCS102
01001              PERFORM E100-EDIT-SELECTIONS                         MBKCS102
01002                                                                   MBKCS102
01003              MOVE -1                        TO ANAVCMDL              CL165
01004              MOVE PV-MESSAGE                TO DP020-MSG-MESSAGE     CL165
01005                                                                   MBKCS102
01006              SET DP020-MSG-FATAL            TO TRUE                  CL165
01007              SET PS-SEND-DATA-ONLY-FRSET    TO TRUE                  CL165
01008              SET PS-SAME-ITEMS-DISPLAYED    TO TRUE                  CL165
01009              SET PS-CURSOR-POSITIONED       TO TRUE                  CL165
01010                                                                   MBKCS102
01011              PERFORM G100-DISPLAY-PANEL                           MBKCS102
01012                                                                   MBKCS102
01013          WHEN OTHER                                               MBKCS102
01014              SET DP013-LOGIC-ABEND          TO TRUE                  CL165
01015              SET DP013-NO-ROLLBACK          TO TRUE                  CL165
01016                                                                   MBKCS102
01017              MOVE 'B100-CONTROL-PROCESSING' TO                    MBKCS102
01018                   DP013-PARAGRAPH                                 MBKCS102
01019              MOVE INVALID-NEXT-ACT-MSG      TO                       CL**3
01020                   DP013-MESSAGE-TEXT(1)                           MBKCS102
01021                                                                   MBKCS102
01022              PERFORM DP013-0000-PROCESS-ABEND                     MBKCS102
01023                                                                   MBKCS102
01024      END-EVALUATE.                                                MBKCS102
01025  EJECT                                                            MBKCS102
01026  C100-INITIALIZE-COMMAREA.                                           CL**3
01027                                                                   MBKCS102
01028 ******************************************************************MBKCS102
01029 *    INITIALIZE THE APPLICATION SPECIFIC AND INTER-APPLICATION    MBKCS102
01030 *    PLACE WHATEVER INITIALIZATION LOGIC IS NECESSARY.            MBKCS102
01031 *                                                                 MBKCS102
01032 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL203
01033 ******************************************************************MBKCS102
01034                                                                      CL*17
01035      INITIALIZE MB102-SPECIFIC-COMMAREA.                             CL209
01036                                                                      CL*17
01037  EJECT                                                            MBKCS102
01038  C200-SAVE-INITIAL-INTERAPPL.                                        CL**3
01039                                                                      CL**3
01040 ******************************************************************   CL**3
01041 *                                                                    CL**3
01042 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL203
01043 ******************************************************************   CL**3
01044                                                                      CL165
01045      IF DP020-MB-MERCH-AREA-SELECT                                   CL*23
01046          MOVE MB005-CO-NUMBER      TO MB102-CO-NUMBER                CL209
01047          MOVE MB005-GMA-NUMBER     TO MB102-GMA-NUMBER               CL209
01048          MOVE MB005-DMA-NUMBER     TO MB102-DMA-NUMBER               CL209
01049          MOVE MB005-DEPT-NUMBER    TO MB102-DEPT-NUMBER              CL209
01050          MOVE MB005-MCLASS-NUMBER  TO MB102-MCLASS-NUMBER            CL209
01051          MOVE MB005-SCLASS-NUMBER  TO MB102-SCLASS-NUMBER            CL209
01052          MOVE MB005-HISTORY-INDICATOR TO MB102-HISTORY-FLAG          CL225
01053          SET PS-INTER-APPL-VALID TO TRUE                             CL*24
01054      ELSE                                                            CL*23
01055         MOVE PC-TSYMSG-99901 TO DP020-MSG-MESSAGE                    CL*23
01056         SET DP020-MSG-FATAL  TO TRUE                                 CL*23
01057         SET PS-INTER-APPL-INVALID TO TRUE                            CL*23
01058         MOVE -1 TO ANAVCMDL                                          CL*23
01059      END-IF.                                                         CL*23
01060                                                                      CL*23
01061                                                                      CL165
01062  EJECT                                                               CL**3
01063  C300-PROCESS-PANEL.                                              MBKCS102
01064                                                                   MBKCS102
01065 ******************************************************************MBKCS102
01066 *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED  MBKCS102
01067 *    ON WHAT FUNCTION KEY WAS SELECTED.                           MBKCS102
01068 *                                                                 MBKCS102
01069 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL203
01070 ******************************************************************MBKCS102
01071                                                                   MBKCS102
01072      PERFORM DP007-0000-GET-PANEL.                                MBKCS102
01073                                                                      CL162
01074      SET PS-SEND-DATA-ONLY-FRSET             TO TRUE.                CL165
01075                                                                   MBKCS102
01076      EVALUATE TRUE                                                MBKCS102
01077                                                                   MBKCS102
01078          WHEN DP020-SRC-AID = DP016-CLEAR                         MBKCS102
01079              SET PS-SEND-MAP-N-DATA-FRSET    TO TRUE                 CL165
01080              SET PS-SAME-ITEMS-DISPLAYED     TO TRUE                 CL165
01081                                                                   MBKCS102
01082              PERFORM E100-EDIT-SELECTIONS                         MBKCS102
01083              PERFORM G100-DISPLAY-PANEL                           MBKCS102
01084                                                                   MBKCS102
01085          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                        CL*21
01086              SET PS-SEND-MAP-N-DATA-FRSET    TO TRUE                 CL165
01087              INITIALIZE ANAVCMDO ANAVKEYO                            CL*21
01088                         DP020-NAV-KEY-DATA                           CL*21
01089              PERFORM Z050-DELETE-TEMP-STORAGE                     MBKCS102
01090              PERFORM D100-SETUP-INIT-PANEL                        MBKCS102
01091                                                                   MBKCS102
01092          WHEN DP020-SRC-AID = DP016-PA2                           MBKCS102
01093              PERFORM DP009-0000-LOAD-FK-LINES                     MBKCS102
01094              PERFORM DP017-0000-GET-CURR-DATE-TIME                MBKCS102
01095                                                                   MBKCS102
01096              SET PS-SEND-DATA-ONLY           TO TRUE                 CL165
01097                                                                   MBKCS102
01098              MOVE -1 TO ANAVCMDL                                  MBKCS102
01099                                                                   MBKCS102
01100              PERFORM DP008-0000-WRITE-PANEL                       MBKCS102
01101                                                                   MBKCS102
01102          WHEN OTHER                                               MBKCS102
01103              SET PS-INVALID-SELECTIONS TO TRUE                       CL*40
01104              PERFORM D200-RECORD-SELECTIONS                       MBKCS102
01105                                                                   MBKCS102
01106              IF PS-VALID-SELECTIONS                               MBKCS102
01107                  PERFORM D300-CHECK-FUNCTION-KEY                  MBKCS102
01108              ELSE                                                 MBKCS102
01109                  SET PS-SAME-ITEMS-DISPLAYED TO TRUE                 CL*71
01110                  PERFORM G100-DISPLAY-PANEL                       MBKCS102
01111              END-IF                                               MBKCS102
01112                                                                   MBKCS102
01113      END-EVALUATE.                                                MBKCS102
01114  EJECT                                                            MBKCS102
01115  C400-UPDATE-INITIAL-INTERAPPL.                                      CL**3
01116                                                                      CL**3
01117 ******************************************************************   CL**3
01118 *    WHENEVER THIS TRANSACTION HAS UPDATED VALUES THAT WERE PASSED   CL**3
01119 *    TO IT THRU THE INTER APPLICATION COMMAREA, THIS PARAGRAPH       CL**3
01120 *    IS INVOKED AND IT UPDATES THE FIELDS IN THE TWO HUNDRED BYTE    CL**3
01121 *    AREA IN THE APPLICATIONS SPECIFIC COMMAREA THAT WAS RESERVED    CL**3
01122 *    FOR THE INITIAL VALUE OF THE ORIGINAL TWO HUNDRED BYTES SENT    CL**3
01123 *    TO THIS TRANSACTION, THIS ALLOWS THE TRANSACTION THAT INVOKED   CL**3
01124 *    THIS TRANSACTION TO RECEIVE UPDATES THRU THE INTER APPL COMM    CL**3
01125 *    AREA WHEN THE USER PRESSES F3 - END ON THIS SCREEN. IN          CL**3
01126 *    ADDITION THIS PARAGRAPH WILL BE INVOKED WHEN THIS TRANSACTION   CL**3
01127 *    IS INVOKED VIA AN F3 - END AID KEY NAVIGATION. CODE MUST BE     CL**3
01128 *    INSERTED IN THIS PARAGRAPH TO HANDLE ANY UPDATES PASSED BACK    CL**3
01129 *    TO THIS TRANSACTION AS WELL. THIS LOGIC MAY BECOME QUITE OMPL   CL**3
01130 *    COMPLEX. FOR AN EXAMPLE OF HOW A SPECIFIC PROGRAM IS DOING      CL**3
01131 *    THIS LOGIC SEE DPKCS019.                                        CL**3
01132 *                                                                    CL**3
01133 *                                                                    CL**3
01134 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL203
01135 ******************************************************************   CL**3
01136                                                                      CL*19
01137 *    MOVE LOW-VALUES TO MB005-INTER-APPL-COMMAREA.                   CL248
01138                                                                      CL*19
01139  EJECT                                                               CL**3
01140  C500-REFRESH-PANEL-FROM-TEMP.                                       CL**3
01141                                                                   MBKCS102
01142 ******************************************************************MBKCS102
01143 *    WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, MBKCS102
01144 *    SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.       MBKCS102
01145 *    EDIT THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL  MBKCS102
01146 *    TO THE USER.                                                 MBKCS102
01147 *                                                                 MBKCS102
01148 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL203
01149 ******************************************************************MBKCS102
01150 *                                                                    CL244
01151      IF  WE-MUST-UPDATE-TSQ                                          CL244
01152          MOVE ZERO             TO  PV-SAVE-ITEM                      CL244
01153                                    PV-MA-ITEM                        CL244
01154          SET NOT-END-OF-ITEMS  TO  TRUE                              CL244
01155          PERFORM C600-UPDATE-TSQ                                     CL244
01156                  UNTIL END-OF-ITEMS                                  CL244
01157          PERFORM X900-DELETE-LIST-QUEUE                              CL*37
01158          MOVE SPACES           TO  MB005-LIST-QUEUE-NAME             CL*37
01159      END-IF.                                                         CL244
01160 *                                                                    CL244
01161      PERFORM D400-REFRESH-BLOCK-ARRAY.                            MBKCS102
01162      PERFORM E100-EDIT-SELECTIONS.                                MBKCS102
01163      SET PS-DIFFERENT-ITEMS-DISPLAYED TO TRUE.                       CL*71
01164      PERFORM G100-DISPLAY-PANEL.                                  MBKCS102
01165 *                                                                    CL244
01166  EJECT                                                               CL244
01167 *                                                                    CL244
01168 ******************************************************************   CL244
01169 *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.          CL244
01170 *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER      CL244
01171 *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN        CL244
01172 *    THE TEMP QUEUE.                                                 CL244
01173 *                                                                    CL244
01174 *    PERFORMED BY C400                                               CL244
01175 ******************************************************************   CL244
01176 *                                                                    CL244
01177  C600-UPDATE-TSQ.                                                    CL244
01178 *                                                                    CL244
01179      ADD 1                     TO  PV-MA-ITEM.                       CL244
01180      PERFORM X600-READ-LIST-QUEUE.                                   CL244
01181      IF  PV-RESP EQUAL DFHRESP(NORMAL)                               CL244
01182          SET MB003-IDX         TO  1                                 CL*27
01183          PERFORM C700-PROCESS-ITEMS                                  CL244
01184              UNTIL MB003-KEY-DATA (MB003-IDX) EQUAL SPACES           CL*27
01185                OR  MB003-IDX GREATER THAN MB003-MAX-ITEMS            CL*27
01186          PERFORM X550-WRITE-TS-QUEUE                                 CL244
01187      ELSE                                                            CL244
01188          SET END-OF-ITEMS      TO  TRUE                              CL*28
01189      END-IF.                                                         CL*28
01190 *                                                                    CL244
01191  EJECT                                                               CL244
01192 *                                                                    CL244
01193 ******************************************************************   CL244
01194 *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.    CL244
01195 *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE    CL244
01196 *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.               CL244
01197 *                                                                    CL244
01198 *    PERFORMED BY C600                                               CL244
01199 ******************************************************************   CL244
01200 *                                                                    CL244
01201  C700-PROCESS-ITEMS.                                                 CL244
01202 *                                                                    CL244
01203      COMPUTE PV-ITEM = 1 + ((MB003-ITEM (MB003-IDX) - 1)             CL*27
01204                      / PC-ITEMS-PER-BLOCK).                          CL244
01205 *                                                                    CL244
01206      IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                              CL244
01207          IF  PV-SAVE-ITEM NOT EQUAL ZERO                             CL244
01208              PERFORM X550-WRITE-TS-QUEUE                             CL244
01209          END-IF                                                      CL244
01210          MOVE PV-ITEM          TO  PV-SAVE-ITEM                      CL244
01211 *                                                                    CL244
01212          PERFORM X250-READ-TS-QUEUE                                  CL244
01213          IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                       CL244
01214              SET END-OF-ITEMS  TO  TRUE                              CL244
01215          END-IF                                                      CL244
01216      END-IF.                                                         CL244
01217 *                                                                    CL244
01218      IF  NOT-END-OF-ITEMS                                            CL244
01219          COMPUTE PS-SUB = (MB003-ITEM (MB003-IDX)                    CL*27
01220                         - PV-TW-LIST-ITEM-NUMBER (1)) + 1            CL244
01221          SET PV-TW-DESELECT (PS-SUB)                                 CL244
01222                                TO  TRUE                              CL244
01223          SUBTRACT 1          FROM  MB005-NUMBER-PROCESSED-ITEMS      CL244
01224                                    MB102-NUMBER-OF-ITEMS-SELECTED    CL245
01225          IF  MB005-NUMBER-PROCESSED-ITEMS EQUAL ZERO                 CL244
01226              SET END-OF-ITEMS  TO  TRUE                              CL244
01227              SET MB003-IDX     TO  MB003-MAX-ITEMS                   CL*27
01228          END-IF                                                      CL244
01229          SET MB003-IDX      UP BY  1                                 CL*27
01230      END-IF.                                                         CL244
01231 *                                                                    CL244
01232  EJECT                                                            MBKCS102
01233  D100-SETUP-INIT-PANEL.                                           MBKCS102
01234                                                                   MBKCS102
01235 ******************************************************************MBKCS102
01236 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                         MBKCS102
01237 *    INITIALIZE ALL COUNTERS AND FLAGS.                           MBKCS102
01238 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.   MBKCS102
01239 *    DISPLAY THE PANEL TO USER.                                   MBKCS102
01240 *                                                                 MBKCS102
01241 *    MB102 - MEMBER NAME USED FOR SPECIFIC COMMAREA.                 CL209
01242 *                                                                 MBKCS102
01243 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL203
01244 *    PERFORMED BY C300-PROCESS-PANEL.                                CL203
01245 ******************************************************************MBKCS102
01246                                                                   MBKCS102
01247      MOVE +1                  TO MB102-BLK-SUB.                      CL210
01248                                                                   MBKCS102
01249      PERFORM I100-INITIALIZE-BLOCK.                               MBKCS102
01250                                                                   MBKCS102
01251      ADD +1                   TO MB102-BLK-SUB.                      CL210
01252                                                                   MBKCS102
01253      PERFORM I100-INITIALIZE-BLOCK.                               MBKCS102
01254                                                                   MBKCS102
01255      MOVE +1                  TO MB102-BLK-SUB.                      CL210
01256      MOVE +0                  TO MB102-ITEM-SUB.                     CL209
01257      MOVE +0                  TO MB102-NUMBER-OF-ITEMS-READ.         CL209
01258      MOVE +0                  TO MB102-NUMBER-OF-ITEMS-SELECTED.     CL209
01259      MOVE SPACES              TO MB102-TEMP-QUEUE-NAME.              CL209
01260      MOVE +0                  TO MB102-HIGHEST-BLOCK-WRITTEN.        CL209
01261      MOVE +0                  TO MB102-START-OF-SELECTED-RANGE.      CL209
01262      MOVE +0                  TO MB102-END-OF-SELECTED-RANGE.        CL209
01263      MOVE +0                  TO MB102-SAVE-SEQ-NBR.                 CL209
01264      MOVE PC-ITEMS-PER-PANEL  TO PV-HIGHEST-ITEM-TO-BE-READ.         CL165
01265                                                                      CL222
01266      PERFORM DP017-0000-GET-CURR-DATE-TIME                           CL255
01267      SET DP017-EDITED-YMD-DATE-DASH-1                                CL255
01268          DP017-EDITED-YMD-DATE-DASH-2 TO TRUE.                       CL255
01269      MOVE DP017-EDITED-DATE-YYYYMMDD                                 CL255
01270                               TO  MB102-DB2-CURRENT-DATE.            CL**4
01271 *                                                                    CL255
01272      IF MB102-HISTORY                                                CL222
01273         MOVE '1900-01-01' TO MB102-END-DATE                          CL226
01274      ELSE                                                            CL222
01275         MOVE DP017-EDITED-DATE-YYYYMMDD TO MB102-END-DATE            CL222
01276      END-IF.                                                         CL222
01277                                                                      CL138
01278      SET MB102-NO-CURSOR-OPEN TO TRUE                                CL209
01279                                                                      CL140
01280      IF MB102-ALL-SCLASSES                                           CL209
01281         MOVE PC-SCL-LITERAL   TO MB102-SAVE-SCL                      CL209
01282      ELSE                                                            CL138
01283          IF (MB102-SCLASS-NUMBER = SPACES) OR                        CL209
01284             (MB102-SCLASS-NUMBER = LOW-VALUES)                       CL209
01285              MOVE SPACES      TO MB102-SAVE-SCL                      CL209
01286              MOVE SPACES      TO MB102-SAVE-SCL-NBR                  CL209
01287          ELSE                                                        CL138
01288              MOVE PC-SCL-LITERAL       TO MB102-SAVE-SCL             CL209
01289              MOVE MB102-SCLASS-NUMBER  TO MB102-SAVE-SCL-NBR         CL209
01290              SET MB102-OPEN-SCL-CURSOR TO TRUE                       CL209
01291          END-IF                                                      CL138
01292      END-IF.                                                         CL138
01293                                                                      CL138
01294      IF MB102-ALL-MCLASSES                                           CL209
01295          MOVE PC-MCL-LITERAL  TO MB102-SAVE-MCL                      CL209
01296      ELSE                                                            CL138
01297          IF (MB102-MCLASS-NUMBER = SPACES) OR                        CL209
01298             (MB102-MCLASS-NUMBER = LOW-VALUES)                       CL209
01299              MOVE SPACES      TO MB102-SAVE-MCL                      CL209
01300              MOVE SPACES      TO MB102-SAVE-MCL-NBR                  CL209
01301          ELSE                                                        CL138
01302              MOVE PC-MCL-LITERAL      TO MB102-SAVE-MCL              CL209
01303              MOVE MB102-MCLASS-NUMBER TO MB102-SAVE-MCL-NBR          CL209
01304              IF MB102-NO-CURSOR-OPEN                                 CL209
01305                  SET MB102-OPEN-MCL-CURSOR TO TRUE                   CL209
01306              END-IF                                                  CL143
01307          END-IF                                                      CL143
01308      END-IF.                                                         CL138
01309                                                                      CL138
01310      IF MB102-ALL-DEPARTMENTS                                        CL209
01311          MOVE PC-DPT-LITERAL    TO MB102-SAVE-DPT                    CL209
01312      ELSE                                                            CL138
01313          IF (MB102-DEPT-NUMBER = SPACES) OR                          CL209
01314             (MB102-DEPT-NUMBER = LOW-VALUES)                         CL209
01315              MOVE SPACES        TO MB102-SAVE-DPT                    CL209
01316              MOVE SPACES        TO MB102-SAVE-DPT-NBR                CL209
01317          ELSE                                                        CL138
01318              MOVE PC-DPT-LITERAL    TO MB102-SAVE-DPT                CL209
01319              MOVE MB102-DEPT-NUMBER TO MB102-SAVE-DPT-NBR            CL209
01320              IF MB102-NO-CURSOR-OPEN                                 CL209
01321                  SET MB102-OPEN-DPT-CURSOR TO TRUE                   CL209
01322              END-IF                                                  CL143
01323          END-IF                                                      CL143
01324      END-IF.                                                         CL138
01325                                                                      CL138
01326      IF MB102-ALL-DMAS                                               CL209
01327          MOVE PC-DMA-LITERAL         TO MB102-SAVE-DMA               CL209
01328      ELSE                                                            CL138
01329          IF (MB102-DMA-NUMBER = SPACES) OR                           CL209
01330             (MB102-DMA-NUMBER = LOW-VALUES)                          CL209
01331              MOVE SPACES               TO MB102-SAVE-DMA             CL209
01332              MOVE SPACES               TO MB102-SAVE-DMA-NBR         CL209
01333          ELSE                                                        CL138
01334              MOVE PC-DMA-LITERAL       TO MB102-SAVE-DMA             CL209
01335              MOVE MB102-DMA-NUMBER     TO MB102-SAVE-DMA-NBR         CL209
01336              IF MB102-NO-CURSOR-OPEN                                 CL209
01337                  SET MB102-OPEN-DMA-CURSOR TO TRUE                   CL209
01338              END-IF                                                  CL142
01339          END-IF                                                      CL142
01340      END-IF.                                                         CL138
01341                                                                      CL138
01342      IF MB102-ALL-GMAS                                               CL209
01343         MOVE PC-GMA-LITERAL         TO MB102-SAVE-GMA                CL209
01344      ELSE                                                            CL138
01345         MOVE PC-GMA-LITERAL         TO MB102-SAVE-GMA                CL209
01346         MOVE MB102-GMA-NUMBER       TO MB102-SAVE-GMA-NBR            CL209
01347         IF MB102-NO-CURSOR-OPEN                                      CL209
01348             SET MB102-OPEN-GMA-CURSOR TO TRUE                        CL209
01349         END-IF                                                       CL142
01350      END-IF.                                                         CL138
01351                                                                      CL138
01352      IF (MB102-ALL-GMAS        AND                                   CL209
01353          MB102-ALL-DMAS        AND                                   CL209
01354          MB102-ALL-DEPARTMENTS AND                                   CL209
01355          MB102-ALL-MCLASSES    AND                                   CL209
01356          MB102-ALL-SCLASSES)                                         CL209
01357          SET MB102-OPEN-MERCH-CURSOR TO TRUE                         CL209
01358      END-IF.                                                         CL113
01359                                                                   MBKCS102
01360      IF (MB102-ALL-GMAS               AND                            CL209
01361          MB102-SAVE-DMA-NBR = SPACES  AND                            CL209
01362          MB102-SAVE-DPT-NBR = SPACES  AND                            CL209
01363          MB102-SAVE-MCL-NBR = SPACES  AND                            CL209
01364          MB102-SAVE-SCL-NBR = SPACES)                                CL209
01365          SET MB102-OPEN-MERCH-CURSOR TO TRUE                         CL209
01366      END-IF.                                                         CL144
01367                                                                      CL177
01368      IF (MB102-ALL-GMAS               AND                            CL209
01369          MB102-ALL-DMAS               AND                            CL209
01370          MB102-SAVE-DPT-NBR = SPACES  AND                            CL209
01371          MB102-SAVE-MCL-NBR = SPACES  AND                            CL209
01372          MB102-SAVE-SCL-NBR = SPACES)                                CL209
01373          SET MB102-OPEN-MERCH-CURSOR TO TRUE                         CL209
01374      END-IF.                                                         CL177
01375                                                                      CL144
01376      IF (MB102-ALL-GMAS        AND                                   CL209
01377          MB102-ALL-DMAS        AND                                   CL209
01378          MB102-ALL-DEPARTMENTS AND                                   CL209
01379          MB102-SAVE-MCL-NBR  = SPACES AND                            CL209
01380          MB102-SAVE-SCL-NBR  = SPACES)                               CL209
01381          SET MB102-OPEN-MERCH-CURSOR TO TRUE                         CL209
01382      END-IF.                                                         CL144
01383                                                                      CL144
01384      IF (MB102-ALL-GMAS        AND                                   CL209
01385          MB102-ALL-DMAS        AND                                   CL209
01386          MB102-ALL-DEPARTMENTS AND                                   CL209
01387          MB102-ALL-MCLASSES    AND                                   CL209
01388          MB102-SAVE-SCL-NBR  = SPACES)                               CL209
01389          SET MB102-OPEN-MERCH-CURSOR TO TRUE                         CL209
01390      END-IF.                                                         CL144
01391                                                                      CL144
01392      PERFORM D500-OPEN-CURSOR.                                       CL137
01393                                                                      CL137
01394      SET MB102-ITEMS-LEFT-ON-DB TO TRUE.                             CL209
01395                                                                   MBKCS102
01396      PERFORM H100-READ-ITEMS-FROM-DB.                             MBKCS102
01397                                                                   MBKCS102
01398      MOVE +1 TO MB102-ITEM-AT-TOP-OF-PANEL.                          CL209
01399                                                                      CL*71
01400      SET PS-DIFFERENT-ITEMS-DISPLAYED TO TRUE.                       CL161
01401                                                                      CL*71
01402      IF PS-NO-SELECTIONS-FOUND                                       CL161
01403          CONTINUE                                                    CL161
01404      ELSE                                                            CL161
01405          PERFORM G100-DISPLAY-PANEL                                  CL157
01406      END-IF.                                                         CL157
01407  EJECT                                                            MBKCS102
01408  D200-RECORD-SELECTIONS.                                          MBKCS102
01409                                                                   MBKCS102
01410 ******************************************************************MBKCS102
01411 *    IF THE USER HIT THE DESELECT KEY, OR A KEY WHICH INVOLVES    MBKCS102
01412 *    LEAVING THIS TRANSACTION AND GOING TO ANOTHER, SET A FLAG    MBKCS102
01413 *    INDICATING THAT ALL SELECTIONS ARE VALID.  IF ANY OTHER KEY  MBKCS102
01414 *    IS HIT, MOVE THE SELECTIONS MADE TO THE COMMAREA, AND EDIT   MBKCS102
01415 *    THOSE SELECTIONS.                                            MBKCS102
01416 *                                                                 MBKCS102
01417 *    IF THE USER ENTERED SOMETHING IN THE COMMAND LINE OR HIT     MBKCS102
01418 *    AN INVALID FUNCTION KEY, DO NOT EDIT THE SELECTIONS.         MBKCS102
01419 *                                                                 MBKCS102
01420 *    PERFORMED BY C300-PROCESS-PANEL.                                CL*92
01421 ******************************************************************MBKCS102
01422                                                                      CL*87
01423      IF (DP020-FK-DESELECT(DP020-SRC-AID))   OR                   MBKCS102
01424         (DP020-FK-SELECT-ALL(DP020-SRC-AID)) OR                      CL*71
01425         (DP020-FK-RETURN(DP020-SRC-AID))     OR                      CL*71
01426         (DP020-FK-GO-TO-TRAN(DP020-SRC-AID))                         CL*71
01427          SET PS-VALID-SELECTIONS TO TRUE                          MBKCS102
01428      ELSE                                                         MBKCS102
01429          PERFORM E200-MOVE-SELECTS-TO-COMMAREA                    MBKCS102
01430                                                                   MBKCS102
01431          IF (DP020-SRC-AID          = DP016-ENTER) AND            MBKCS102
01432             (DP020-NAV-MNEMONIC NOT = SPACES)      AND            MBKCS102
01433             (DP020-NAV-MNEMONIC NOT = LOW-VALUES)                 MBKCS102
01434              SET PS-VALID-SELECTIONS TO TRUE                      MBKCS102
01435          ELSE                                                     MBKCS102
01436                                                                   MBKCS102
01437              IF (DP020-FK-FIRST-PAGE(DP020-SRC-AID)) OR           MBKCS102
01438                 (DP020-FK-PREV-PAGE(DP020-SRC-AID))  OR           MBKCS102
01439                 (DP020-FK-NEXT-PAGE(DP020-SRC-AID))  OR           MBKCS102
01440                 (DP020-SRC-AID = DP016-ENTER)                     MBKCS102
01441                  PERFORM E100-EDIT-SELECTIONS                     MBKCS102
01442              ELSE                                                 MBKCS102
01443                  SET PS-VALID-SELECTIONS TO TRUE                  MBKCS102
01444              END-IF                                               MBKCS102
01445                                                                   MBKCS102
01446          END-IF                                                   MBKCS102
01447                                                                   MBKCS102
01448      END-IF.                                                      MBKCS102
01449  EJECT                                                            MBKCS102
01450  D300-CHECK-FUNCTION-KEY.                                         MBKCS102
01451                                                                   MBKCS102
01452 ******************************************************************MBKCS102
01453 *    PERFORM THE APPROPRIATE PARAGRAPH BASED ON WHICH FUNCTION    MBKCS102
01454 *    KEY HAD BEEN SELECTED BY THE USER.                           MBKCS102
01455 *                                                                 MBKCS102
01456 *    PERFORMED BY C300-PROCESS-PANEL.                                CL*92
01457 ******************************************************************MBKCS102
01458                                                                   MBKCS102
01459      EVALUATE TRUE                                                MBKCS102
01460                                                                   MBKCS102
01461          WHEN DP020-FK-GO-TO-TRAN(DP020-SRC-AID)                     CL**6
01462              SET NO-UPDATE-TO-TSQ TO TRUE                            CL*10
01463              PERFORM E300-NAVIGATE                                MBKCS102
01464 *                                                                    CL**6
01465          WHEN (DP020-FK-RETURN(DP020-SRC-AID))                       CL**6
01466              PERFORM X900-DELETE-LIST-QUEUE                          CL**6
01467              PERFORM E300-NAVIGATE                                   CL**6
01468 *                                                                    CL208
01469 *    FOR ADD PROCESSING, NO QUEUE IS REQUIRED.                       CL208
01470 *                                                                    CL208
01471          WHEN DP020-FK-EDIT-GOTO(DP020-SRC-AID)                      CL208
01472              SET PS-CONTINUE-ON TO TRUE                              CL235
01473              IF  MB102-NUMBER-OF-ITEMS-SELECTED > ZERO               CL*22
01474                  SET WE-MUST-UPDATE-TSQ                              CL*22
01475                            TO  TRUE                                  CL*22
01476                  PERFORM X100-PREPARE-LIST-QUEUE                     CL*22
01477                  IF ((PS-SELECTED-HISTORY) AND                       CL*22
01478                     (DP020-SRC-AID = DP016-PF15 OR                   CL*22
01479                      DP020-SRC-AID = DP016-PF19))                    CL*22
01480                     MOVE PC-TSYMSG-00146  TO DP020-MSG-NUMBER        CL*22
01481                     SET DP020-MSG-FATAL   TO TRUE                    CL*22
01482                     PERFORM G100-DISPLAY-PANEL                       CL*22
01483                  ELSE                                                CL*22
01484                     IF (PS-SELECTED-MCL-SCL AND                      CL*33
01485                        (DP020-SRC-AID = DP016-PF18                   CL*33
01486                     OR  DP020-SRC-AID = DP016-PF19))                 CL*33
01487                         SET DP020-MSG-FATAL TO TRUE                  CL*32
01488                         MOVE PC-TSYMSG-00146                         CL*32
01489                              TO  DP020-MSG-NUMBER                    CL*32
01490                         PERFORM G100-DISPLAY-PANEL                   CL*32
01491                     ELSE                                             CL*32
01492                         PERFORM E300-NAVIGATE                        CL*32
01493                     END-IF                                           CL*32
01494                  END-IF                                              CL*32
01495              ELSE                                                    CL*22
01496                  MOVE PC-TSYMSG-00057  TO DP020-MSG-NUMBER           CL*22
01497                  SET DP020-MSG-WARNING TO TRUE                       CL*38
01498                  PERFORM G100-DISPLAY-PANEL                          CL*22
01499              END-IF                                                  CL*22
01500                                                                      CL*22
01501          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  MBKCS102
01502              PERFORM E400-DISPLAY-TOP-OF-LIST                     MBKCS102
01503                                                                   MBKCS102
01504          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   MBKCS102
01505              PERFORM E500-BROWSE-BACKWARD                         MBKCS102
01506                                                                   MBKCS102
01507          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   MBKCS102
01508              PERFORM E600-BROWSE-FORWARD                          MBKCS102
01509                                                                   MBKCS102
01510          WHEN (DP020-FK-DESELECT(DP020-SRC-AID))   OR                CL*72
01511               (DP020-FK-SELECT-ALL(DP020-SRC-AID))                   CL*72
01512              PERFORM E700-DESELECT-ALL-ITEMS                      MBKCS102
01513                                                                   MBKCS102
01514          WHEN DP020-SRC-AID = DP016-ENTER                         MBKCS102
01515                                                                   MBKCS102
01516              IF (DP020-NAV-MNEMONIC = LOW-VALUES) OR              MBKCS102
01517                 (DP020-NAV-MNEMONIC = SPACES)                     MBKCS102
01518                  PERFORM E800-CHECK-FOR-JUMP-BROWSE               MBKCS102
01519              ELSE                                                 MBKCS102
01520                  SET NO-UPDATE-TO-TSQ                                CL244
01521                                TO  TRUE                              CL244
01522                  PERFORM E300-NAVIGATE                            MBKCS102
01523              END-IF                                               MBKCS102
01524                                                                   MBKCS102
01525          WHEN OTHER                                               MBKCS102
01526              MOVE PC-TSYMSG-00045                                    CL244
01527                                TO  DP020-MSG-NUMBER                  CL244
01528              SET DP020-MSG-FATAL                                     CL244
01529                                TO  TRUE                              CL244
01530              PERFORM G100-DISPLAY-PANEL                              CL244
01531                                                                   MBKCS102
01532      END-EVALUATE.                                                MBKCS102
01533  EJECT                                                            MBKCS102
01534  D400-REFRESH-BLOCK-ARRAY.                                        MBKCS102
01535                                                                   MBKCS102
01536 ******************************************************************MBKCS102
01537 *    WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, MBKCS102
01538 *    READ TEMPORARY STORAGE FILLING IN BOTH BLOCKS IN THE ARRAY   MBKCS102
01539 *    WITH THE ITEMS THAT WERE AT THE TOP OF THE LIST WHEN WE      MBKCS102
01540 *    HAD LEFT PREVIOUSLY.                                         MBKCS102
01541 *                                                                 MBKCS102
01542 ******************************************************************MBKCS102
01543                                                                   MBKCS102
01544      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS102
01545          1 + ((MB102-LIST-ITEM-NUMBER(1 1) - 1) /                    CL209
01546                PC-ITEMS-PER-BLOCK).                               MBKCS102
01547                                                                   MBKCS102
01548      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS102
01549      MOVE +1                   TO MB102-BLK-SUB.                     CL210
01550                                                                   MBKCS102
01551      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS102
01552                                                                   MBKCS102
01553      ADD +1 TO PV-BLOCK-NUMBER.                                   MBKCS102
01554                                                                   MBKCS102
01555      IF (PV-BLOCK-NUMBER < MB102-HIGHEST-BLOCK-WRITTEN) OR           CL209
01556         (PV-BLOCK-NUMBER = MB102-HIGHEST-BLOCK-WRITTEN)              CL209
01557          ADD +1 TO MB102-BLK-SUB                                     CL210
01558                                                                   MBKCS102
01559          PERFORM Z100-READ-TEMP-STORAGE                           MBKCS102
01560      END-IF.                                                      MBKCS102
01561  EJECT                                                            MBKCS102
01562                                                                      CL165
01563  D500-OPEN-CURSOR.                                                   CL137
01564                                                                      CL137
01565 ******************************************************************   CL203
01566 *    DETERMIN WHETHER A CURSOR NEEDS TO BE OPENED                    CL203
01567 *                                                                    CL203
01568 *    PERFORMED BY D100-SETUP-INIT-PANEL.                             CL203
01569 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL203
01570 ******************************************************************   CL203
01571                                                                      CL203
01572      EVALUATE TRUE                                                   CL137
01573                                                                      CL137
01574           WHEN MB102-OPEN-MERCH-CURSOR AND                           CL209
01575                PS-MERCH-CURSOR-NOT-OPENED                            CL152
01576                PERFORM Z200-OPEN-MERCH-CURSOR                        CL137
01577                                                                      CL137
01578           WHEN MB102-OPEN-GMA-CURSOR AND                             CL209
01579                PS-GMA-CURSOR-NOT-OPENED                              CL152
01580                PERFORM Z300-OPEN-GMA-CURSOR                          CL137
01581                                                                      CL137
01582           WHEN MB102-OPEN-DMA-CURSOR AND                             CL209
01583                PS-DMA-CURSOR-NOT-OPENED                              CL152
01584                PERFORM Z400-OPEN-DMA-CURSOR                          CL137
01585                                                                      CL137
01586           WHEN MB102-OPEN-DPT-CURSOR AND                             CL209
01587                PS-DPT-CURSOR-NOT-OPENED                              CL152
01588                PERFORM Z500-OPEN-DPT-CURSOR                          CL137
01589                                                                      CL137
01590           WHEN MB102-OPEN-MCL-CURSOR AND                             CL209
01591                PS-MCL-CURSOR-NOT-OPENED                              CL152
01592                PERFORM Z600-OPEN-MCL-CURSOR                          CL148
01593                                                                      CL148
01594           WHEN MB102-OPEN-SCL-CURSOR AND                             CL209
01595                PS-SCL-CURSOR-NOT-OPENED                              CL152
01596                PERFORM Z700-OPEN-SCL-CURSOR                          CL148
01597                                                                      CL148
01598      END-EVALUATE.                                                   CL138
01599                                                                      CL137
01600  EJECT                                                               CL137
01601  E100-EDIT-SELECTIONS.                                            MBKCS102
01602                                                                   MBKCS102
01603 ******************************************************************MBKCS102
01604 *    EDIT EACH SELECTION ENTRY.  IF EACH ENTRY IS CONSIDERED      MBKCS102
01605 *    VALID, CHECK IF A BEGINNING AND ENDING ENTRY WAS ENTERED,    MBKCS102
01606 *    AND CHECK THE FOLLOWING.                                     MBKCS102
01607 *                                                                 MBKCS102
01608 *    1) IF THERE IS A BEGINNING RANGE, THERE MUST BE AN ENDING.   MBKCS102
01609 *    2) IF THERE IS A ENDING RANGE, THERE MUST BE AN BEGINNING.   MBKCS102
01610 *    3) BEGINNING RANGE MUST PRECEDE ENDING RANGE.                MBKCS102
01611 *                                                                 MBKCS102
01612 *    IF A VALID BEGINNING AND ENDING RANGE HAVE BEEN SELECTED,    MBKCS102
01613 *    MARK ALL ITEMS IN THAT RANGE AS BEING SELECTED.              MBKCS102
01614 *                                                                 MBKCS102
01615 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL203
01616 *    PERFORMED BY C300-PROCESS-PANEL.                                CL203
01617 *    PERFORMED BY D200-RECORD-SELECTIONS                             CL*92
01618 ******************************************************************MBKCS102
01619                                                                   MBKCS102
01620      SET PS-VALID-SELECTIONS TO TRUE.                             MBKCS102
01621                                                                   MBKCS102
01622      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS102
01623          (MB102-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL209
01624                                                                   MBKCS102
01625      MOVE MB102-ITEM-AT-TOP-OF-PANEL TO PV-ITEM-IN-USE.              CL209
01626      MOVE +1                         TO PS-SUB.                   MBKCS102
01627                                                                   MBKCS102
01628      PERFORM F100-EDIT                                            MBKCS102
01629          UNTIL (PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL)    OR   MBKCS102
01630                (PV-ITEM-IN-USE > MB102-NUMBER-OF-ITEMS-READ)         CL209
01631                                                                   MBKCS102
01632      IF PS-VALID-SELECTIONS                                       MBKCS102
01633                                                                   MBKCS102
01634          IF MB102-START-OF-SELECTED-RANGE > +0                       CL209
01635                                                                   MBKCS102
01636              IF MB102-END-OF-SELECTED-RANGE   >                      CL209
01637                 MB102-START-OF-SELECTED-RANGE                        CL209
01638                  PERFORM F200-MARK-RANGE-AS-SELECTED              MBKCS102
01639              ELSE                                                 MBKCS102
01640                                                                   MBKCS102
01641                  IF MB102-END-OF-SELECTED-RANGE = +0                 CL209
01642                      MOVE PC-TSYMSG-00051    TO DP020-MSG-NUMBER     CL179
01643                      SET PS-CURSOR-POSITIONED  TO TRUE               CL*55
01644                      SET PS-WARNING            TO TRUE               CL*55
01645                      IF NOT DP020-NEXT-ACT-ERROR                     CL*19
01646                          SET DP020-MSG-WARNING TO TRUE               CL*55
01647                      END-IF                                          CL*19
01648                  ELSE                                             MBKCS102
01649                      SET PS-INVALID-SELECTIONS TO TRUE            MBKCS102
01650                      SET PS-CURSOR-POSITIONED  TO TRUE               CL*53
01651                      SET DP020-MSG-FATAL       TO TRUE               CL*19
01652                      MOVE PC-TSYMSG-00052    TO DP020-MSG-NUMBER     CL179
01653                      IF NOT DP020-NEXT-ACT-ERROR                     CL*20
01654                          MOVE MB102-ITEM-AT-TOP-OF-PANEL             CL209
01655                                                TO PV-ITEM-IN-USE     CL*20
01656                          MOVE +1               TO PS-SUB             CL*20
01657                          PERFORM                                     CL*20
01658                              UNTIL (PV-ITEM-IN-USE >                 CL*20
01659                                     PV-ENDING-ITEM-ON-PANEL)  OR     CL*20
01660                                    (PV-ITEM-IN-USE >                 CL*20
01661                                     MB102-NUMBER-OF-ITEMS-READ)      CL209
01662                              PERFORM K100-SET-SUBSCRIPTS             CL*20
01663                              IF (MB102-BEGIN(MB102-BLK-SUB           CL210
01664                                              MB102-ITEM-SUB)) OR     CL209
01665                                 (MB102-END(MB102-BLK-SUB             CL210
01666                                            MB102-ITEM-SUB))          CL209
01667                                  MOVE -1                      TO     CL*22
01668                                       MB004I-ASELL(PS-SUB)           CL*22
01669                                  MOVE PV-ENDING-ITEM-ON-PANEL TO     CL*22
01670                                       PV-ITEM-IN-USE                 CL*22
01671                              END-IF                                  CL*22
01672                              ADD +1            TO PS-SUB             CL*22
01673                              ADD +1            TO PV-ITEM-IN-USE     CL*22
01674                          END-PERFORM                                 CL*22
01675                      END-IF                                          CL*22
01676                  END-IF                                              CL*22
01677              END-IF                                               MBKCS102
01678          ELSE                                                     MBKCS102
01679              IF MB102-END-OF-SELECTED-RANGE > +0                     CL209
01680                  SET PS-CURSOR-POSITIONED  TO TRUE                   CL*53
01681                  SET PS-WARNING            TO TRUE                   CL*22
01682                  MOVE PC-TSYMSG-00053      TO DP020-MSG-NUMBER       CL179
01683                  IF NOT DP020-NEXT-ACT-ERROR                         CL*22
01684                      SET DP020-MSG-WARNING TO TRUE                   CL*55
01685                  END-IF                                              CL*22
01686              END-IF                                               MBKCS102
01687          END-IF                                                   MBKCS102
01688      END-IF.                                                      MBKCS102
01689  EJECT                                                            MBKCS102
01690  E200-MOVE-SELECTS-TO-COMMAREA.                                   MBKCS102
01691                                                                   MBKCS102
01692 ******************************************************************MBKCS102
01693 *    MOVE THE SELECTIONS MADE FROM THE CURRENT PAGE INTO          MBKCS102
01694 *    THE COMMAREA.                                                MBKCS102
01695 *                                                                 MBKCS102
01696 *    PERFORMED BY D200-RECORD-SELECTIONS.                            CL*92
01697 ******************************************************************MBKCS102
01698                                                                   MBKCS102
01699      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS102
01700          (MB102-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL209
01701                                                                   MBKCS102
01702      MOVE +1                         TO PS-SUB.                   MBKCS102
01703      MOVE MB102-ITEM-AT-TOP-OF-PANEL TO PV-ITEM-IN-USE.              CL209
01704                                                                   MBKCS102
01705      PERFORM F300-CHECK-SELECTIONS                                MBKCS102
01706          UNTIL PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL.          MBKCS102
01707  EJECT                                                            MBKCS102
01708  E300-NAVIGATE.                                                   MBKCS102
01709                                                                   MBKCS102
01710 ******************************************************************MBKCS102
01711 *    DELETE THE TEMPORARY STORAGE IF THE USER HIT PF3.  IF WE ARE    CL*11
01712 *    GOING TO ANOTHER TRANSACTION, BUT PLANNING TO COME BACK,        CL*11
01713 *    WRITE OUT THE LAST 2 BLOCKS FROM THE ARRAY TO TEMPORARY         CL*11
01714 *    STORAGE. IN EITHER CASE, SET UP THE INTER APPLICATION           CL*11
01715 *    COMMAREA, AND START THE NAVIGATOR.                              CL*11
01716 *                                                                 MBKCS102
01717 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01718 ******************************************************************MBKCS102
01719                                                                   MBKCS102
01720      IF  SETUP-INTER-APPL                                            CL247
01721          PERFORM H400-WRITE-OUT-LAST-BLOCKS                          CL247
01722          PERFORM F400-SETUP-INTER-APPL                               CL247
01723      END-IF.                                                         CL247
01724      SET DP020-MB-MERCH-AREA-LIST TO TRUE.                           CL*30
01725      PERFORM DP015-1000-START-NAVIGATOR.                          MBKCS102
01726  EJECT                                                            MBKCS102
01727                                                                      CL235
01728  E305-TEST-ITEMS-SELECTED.                                           CL240
01729 ******************************************************************   CL253
01730 *                                                                    CL253
01731 *                                                                    CL253
01732 *                                                                    CL253
01733 *                                                                    CL253
01734 ******************************************************************   CL253
01735                                                                      CL235
01736      IF PV-SELECTED-LEVEL-CD = 'SCL'                                 CL236
01737        MOVE PC-TSYMSG-00146 TO DP020-MSG-NUMBER                      CL241
01738        SET PS-DO-NOT-CONTINUE-ON TO TRUE                             CL236
01739        SET DP020-MSG-FATAL TO TRUE                                   CL242
01740      ELSE                                                            CL235
01741        PERFORM DP017-0000-GET-CURR-DATE-TIME                         CL237
01742        MOVE PV-SELECTED-END-DT TO PV-PANEL-DATE                      CL242
01743        MOVE PV-PANEL-YEAR  TO PV-TEMPYY                              CL238
01744        MOVE PV-PANEL-MONTH TO PV-TEMPMM                              CL238
01745        MOVE PV-PANEL-DAY   TO PV-TEMPDD                              CL238
01746        IF DP017-CURRENT-DATE-YMD > PV-TEMP-DATE                      CL242
01747           MOVE PC-TSYMSG-00146 TO DP020-MSG-NUMBER                   CL242
01748           SET PS-DO-NOT-CONTINUE-ON TO TRUE                          CL236
01749           SET DP020-MSG-FATAL  TO TRUE                               CL241
01750        END-IF                                                        CL235
01751      END-IF.                                                         CL241
01752                                                                      CL235
01753  E400-DISPLAY-TOP-OF-LIST.                                        MBKCS102
01754                                                                   MBKCS102
01755 ******************************************************************MBKCS102
01756 *    IF THE USER HITS THE 1ST PAGE KEY, COMPUTE THE TOP ITEM      MBKCS102
01757 *    NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM           MBKCS102
01758 *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS  MBKCS102
01759 *    TO BE READ.                                                  MBKCS102
01760 *                                                                 MBKCS102
01761 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01762 ******************************************************************MBKCS102
01763                                                                   MBKCS102
01764      IF MB102-NUMBER-OF-ITEMS-READ > +0                              CL209
01765          MOVE +1 TO PV-TOP-ITEM                                   MBKCS102
01766                                                                   MBKCS102
01767          PERFORM F500-BROWSE                                      MBKCS102
01768      ELSE                                                         MBKCS102
01769          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL*69
01770          PERFORM G100-DISPLAY-PANEL                               MBKCS102
01771      END-IF.                                                      MBKCS102
01772  EJECT                                                            MBKCS102
01773  E500-BROWSE-BACKWARD.                                            MBKCS102
01774                                                                   MBKCS102
01775 ******************************************************************MBKCS102
01776 *    IF THE USER HITS THE PAGE BACKARD KEY, COMPUTE THE TOP       MBKCS102
01777 *    ITEM NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM      MBKCS102
01778 *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS  MBKCS102
01779 *    TO BE READ.                                                  MBKCS102
01780 *                                                                 MBKCS102
01781 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01782 ******************************************************************MBKCS102
01783                                                                   MBKCS102
01784      IF MB102-NUMBER-OF-ITEMS-READ > +0                              CL209
01785          COMPUTE PV-TOP-ITEM =                                    MBKCS102
01786              MB102-ITEM-AT-TOP-OF-PANEL - PC-ITEMS-PER-PANEL         CL209
01787                                                                   MBKCS102
01788          PERFORM F500-BROWSE                                      MBKCS102
01789      ELSE                                                         MBKCS102
01790          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL*31
01791          PERFORM G100-DISPLAY-PANEL                               MBKCS102
01792      END-IF.                                                      MBKCS102
01793  EJECT                                                            MBKCS102
01794  E600-BROWSE-FORWARD.                                             MBKCS102
01795                                                                   MBKCS102
01796 ******************************************************************MBKCS102
01797 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP       MBKCS102
01798 *    ITEM NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM      MBKCS102
01799 *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS  MBKCS102
01800 *    TO BE READ.                                                  MBKCS102
01801 *                                                                    CL*92
01802 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01803 ******************************************************************MBKCS102
01804                                                                   MBKCS102
01805      IF MB102-NUMBER-OF-ITEMS-READ > +0                              CL209
01806          COMPUTE PV-TOP-ITEM =                                    MBKCS102
01807              MB102-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL         CL209
01808                                                                   MBKCS102
01809          PERFORM F500-BROWSE                                      MBKCS102
01810      ELSE                                                         MBKCS102
01811          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL*31
01812          PERFORM G100-DISPLAY-PANEL                               MBKCS102
01813      END-IF.                                                      MBKCS102
01814  EJECT                                                            MBKCS102
01815  E700-DESELECT-ALL-ITEMS.                                         MBKCS102
01816                                                                   MBKCS102
01817 ******************************************************************MBKCS102
01818 *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION       MBKCS102
01819 *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.          MBKCS102
01820 *                                                                 MBKCS102
01821 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01822 ******************************************************************MBKCS102
01823                                                                   MBKCS102
01824      IF MB102-NUMBER-OF-ITEMS-READ > +0                              CL209
01825          MOVE +1 TO MB102-BLK-SUB                                    CL210
01826                                                                   MBKCS102
01827          PERFORM F600-CLEAR-SELECTED-INDS                         MBKCS102
01828              VARYING MB102-ITEM-SUB FROM 1 BY 1                      CL209
01829              UNTIL MB102-ITEM-SUB > PC-ITEMS-PER-BLOCK               CL209
01830                                                                   MBKCS102
01831          ADD +1 TO MB102-BLK-SUB                                     CL210
01832                                                                   MBKCS102
01833          PERFORM F600-CLEAR-SELECTED-INDS                         MBKCS102
01834              VARYING MB102-ITEM-SUB FROM 1 BY 1                      CL209
01835              UNTIL MB102-ITEM-SUB > PC-ITEMS-PER-BLOCK               CL209
01836                                                                   MBKCS102
01837          MOVE MB102-LIST-ITEM-NUMBER(1 1) TO PV-LIST-ITEM-NUMBER     CL209
01838                                                                      CL**2
01839 ******************************************************************   CL**2
01840 *    CALCULATE THE BLOCK NUMBER OF THE FIRST BLOCK IN WORKING        CL**2
01841 *    STORAGE.  IF THIS BLOCK NUMBER IS NOT = 1, WE KNOW THAT         CL**2
01842 *    THERE ARE BLOCKS WITH SMALLER BLOCK NUMBERS SITTING OUT         CL**2
01843 *    IN TEMPORARY STORAGE THAT NEED TO BE RETRIEVED.                 CL**2
01844 *                                                                    CL**2
01845 *    IF THE BLOCK NUMBER DOES = 1, BUT THE HIGHEST BLOCK NUMBER      CL**2
01846 *    WRITTEN TO TEMPORARY STORAGE IS GREATER THAN THE MAXIMUM        CL**2
01847 *    NUMBER OF BLOCKS IN WORKING STORAGE, WE KNOW THAT               CL**2
01848 *    THERE ARE BLOCKS WITH LARGER BLOCK NUMBERS SITTING OUT          CL**2
01849 *    IN TEMPORARY STORAGE THAT NEED TO BE RETRIEVED.                 CL**2
01850 ******************************************************************   CL**2
01851                                                                   MBKCS102
01852          COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                        MBKCS102
01853              1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-BLOCK) MBKCS102
01854                                                                   MBKCS102
01855          IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                 MBKCS102
01856             (MB102-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)   CL209
01857              MOVE +1 TO MB102-BLK-SUB                                CL210
01858                                                                   MBKCS102
01859              PERFORM Z150-WRITE-TEMP-STORAGE                      MBKCS102
01860                                                                   MBKCS102
01861              MOVE +1 TO PV-BLOCK-NUMBER                           MBKCS102
01862                                                                   MBKCS102
01863              COMPUTE PV-PREV-BLOCK-IN-ARRAY =                     MBKCS102
01864                  PV-FIRST-BLOCK-IN-ARRAY - 1                      MBKCS102
01865                                                                   MBKCS102
01866              PERFORM                                              MBKCS102
01867                  UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY   MBKCS102
01868                                                                   MBKCS102
01869                  PERFORM Z100-READ-TEMP-STORAGE                   MBKCS102
01870                                                                   MBKCS102
01871                  PERFORM F600-CLEAR-SELECTED-INDS                 MBKCS102
01872                      VARYING MB102-ITEM-SUB FROM 1 BY 1              CL209
01873                      UNTIL MB102-ITEM-SUB > PC-ITEMS-PER-BLOCK       CL209
01874                                                                   MBKCS102
01875                  PERFORM Z150-WRITE-TEMP-STORAGE                  MBKCS102
01876                                                                   MBKCS102
01877                  ADD +1 TO PV-BLOCK-NUMBER                        MBKCS102
01878                                                                   MBKCS102
01879              END-PERFORM                                          MBKCS102
01880                                                                   MBKCS102
01881              COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                     MBKCS102
01882                  PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY MBKCS102
01883                                                                   MBKCS102
01884              PERFORM                                              MBKCS102
01885                  UNTIL PV-NEXT-BLOCK-IN-ARRAY >                   MBKCS102
01886                        MB102-HIGHEST-BLOCK-WRITTEN                   CL209
01887                                                                   MBKCS102
01888                  MOVE PV-NEXT-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER   MBKCS102
01889                                                                   MBKCS102
01890                  PERFORM Z100-READ-TEMP-STORAGE                   MBKCS102
01891                                                                   MBKCS102
01892                  PERFORM F600-CLEAR-SELECTED-INDS                 MBKCS102
01893                      VARYING MB102-ITEM-SUB FROM 1 BY 1              CL209
01894                      UNTIL MB102-ITEM-SUB > PC-ITEMS-PER-BLOCK       CL209
01895                                                                   MBKCS102
01896                  PERFORM Z150-WRITE-TEMP-STORAGE                  MBKCS102
01897                                                                   MBKCS102
01898                  ADD +1 TO PV-NEXT-BLOCK-IN-ARRAY                 MBKCS102
01899                                                                   MBKCS102
01900              END-PERFORM                                          MBKCS102
01901                                                                   MBKCS102
01902              MOVE PV-FIRST-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER      MBKCS102
01903                                                                   MBKCS102
01904              PERFORM Z100-READ-TEMP-STORAGE                       MBKCS102
01905          END-IF                                                   MBKCS102
01906                                                                   MBKCS102
01907      END-IF.                                                      MBKCS102
01908                                                                   MBKCS102
01909      IF DP020-FK-DESELECT(DP020-SRC-AID)                             CL*73
01910          MOVE +0 TO MB102-NUMBER-OF-ITEMS-SELECTED                   CL209
01911      END-IF.                                                         CL*73
01912                                                                      CL*73
01913      MOVE +0 TO MB102-START-OF-SELECTED-RANGE.                       CL209
01914      MOVE +0 TO MB102-END-OF-SELECTED-RANGE.                         CL209
01915                                                                      CL*92
01916      SET PS-SAME-ITEMS-DISPLAYED TO TRUE.                            CL*92
01917                                                                      CL*92
01918      PERFORM G100-DISPLAY-PANEL.                                  MBKCS102
01919  EJECT                                                            MBKCS102
01920  E800-CHECK-FOR-JUMP-BROWSE.                                      MBKCS102
01921                                                                   MBKCS102
01922 ******************************************************************MBKCS102
01923 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO     MBKCS102
01924 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.    MBKCS102
01925 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST   MBKCS102
01926 *    RATHER THAN HITTING PF8 TO PAGE FORWARD TO THAT POINT.       MBKCS102
01927 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT      MBKCS102
01928 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.           MBKCS102
01929 *                                                                 MBKCS102
01930 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL203
01931 ******************************************************************MBKCS102
01932                                                                   MBKCS102
01933      SET PS-VALID-BEG-ITEM TO TRUE.                               MBKCS102
01934                                                                   MBKCS102
01935      IF (ABEGITMA                      = PC-ERASE-EOF) OR            CL*57
01936         (MB102-NUMBER-OF-ITEMS-READ    = +0)                         CL209
01937          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL169
01938          PERFORM G100-DISPLAY-PANEL                               MBKCS102
01939      ELSE                                                         MBKCS102
01940                                                                   MBKCS102
01941          MOVE ABEGITMI             TO DP010I-UNEDITED-FIELD          CL*57
01942          MOVE PC-FIELD-LENGTH      TO DP010I-MAXIMUM-DIGITS          CL**2
01943          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS        CL**2
01944          SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                  MBKCS102
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262978              USING DP010I-NUMERIC-EDIT-AREA                              
01946                                                                      CL**2
01947 ******************************************************************   CL**2
01948 *    WE MUST MAKE SURE THAT THE NEW ITEM NUMBER THAT THE USER        CL**2
01949 *    WANTS TO JUMP TO WILL NOT REQUIRE US TO READ THE DATABASE       CL**2
01950 *    TOO MANY TIMES.  WE CALCULATE THE MAXIMUM ITEM NUMBER THAT      CL**2
01951 *    CAN BE AT THE TOP OF THE LIST WITHOUT EXCEEDING THE MAXIMUM     CL**2
01952 *    NUMBER OF READS THAT WE HAVE SET.  IF THE NUMBER THAT THE       CL**2
01953 *    USER WANTS AT THE TOP OF THE LIST IS GREATER THAN THE MAXIMUM   CL**2
01954 *    ITEM NUMBER THAT WE HAVE CALCULATED, A MESSAGE IS DISPLAYED,    CL**2
01955 *    AND WE START THE LIST USING THE MAXIMUM ITEM NUMBER.            CL**2
01956 ******************************************************************   CL**2
01957                                                                   MBKCS102
01958          IF NOT DP010I-ERROR-DETECTED                             MBKCS102
01959              MOVE DP010I-NUMERIC-FIELD TO MB004I-ABEGITMI            CL*58
01960              MOVE MB004I-ABEGITMI      TO PV-BEGINNING-ITEM          CL*33
01961                                                                   MBKCS102
01962              IF (PV-BEGINNING-ITEM >  0)          AND             MBKCS102
01963                 (PV-BEGINNING-ITEM <  PC-MAX-ITEMS)               MBKCS102
01964                  MOVE PV-BEGINNING-ITEM TO PV-TOP-ITEM            MBKCS102
01965                                                                   MBKCS102
01966                  COMPUTE PV-MAX-ITEMS-TO-BE-READ =                MBKCS102
01967                     ((MB102-NUMBER-OF-ITEMS-READ +                   CL209
01968                       PC-MAX-ITEMS-TO-BE-READ)   -                   CL**2
01969                       PC-ITEMS-PER-PANEL)        + 1                 CL**2
01970                                                                   MBKCS102
01971                  IF PV-TOP-ITEM > PV-MAX-ITEMS-TO-BE-READ         MBKCS102
01972                      MOVE PV-MAX-ITEMS-TO-BE-READ TO                 CL**2
01973                           PV-TOP-ITEM                                CL**2
01974                      MOVE -1                      TO ABEGITML        CL*33
01975                      IF MB102-NO-ITEMS-LEFT-ON-DB                    CL209
01976                          MOVE PC-TSYMSG-00070  TO                    CL179
01977                               DP020-MSG-NUMBER                       CL179
01978                      ELSE                                            CL**2
01979                          MOVE PC-TSYMSG-00207                        CL*34
01980                                TO  DP020-MSG-NUMBER                  CL*35
01981                      END-IF                                       MBKCS102
01982                      SET PS-CURSOR-POSITIONED    TO TRUE             CL*53
01983                      SET DP020-MSG-INFORMATIONAL TO TRUE             CL*33
01984                  END-IF                                           MBKCS102
01985                                                                   MBKCS102
01986                  PERFORM F500-BROWSE                              MBKCS102
01987              ELSE                                                 MBKCS102
01988                  MOVE PC-TSYMSG-00005       TO DP020-MSG-NUMBER      CL179
01989                  MOVE -1                    TO ABEGITML              CL*33
01990                  MOVE DP015-REVERSE         TO ABEGITMH              CL*53
01991                  MOVE DP015-RED             TO ABEGITMC              CL*33
01992                  MOVE DP015-UNP-ALP-BRT-MDT TO ABEGITMA              CL167
01993                  SET PS-INVALID-BEG-ITEM    TO TRUE                  CL*33
01994                  SET PS-CURSOR-POSITIONED   TO TRUE                  CL*55
01995                  SET DP020-MSG-FATAL        TO TRUE                  CL*33
01996              END-IF                                               MBKCS102
01997                                                                   MBKCS102
01998          ELSE                                                     MBKCS102
01999              MOVE PC-TSYMSG-00008           TO DP020-MSG-NUMBER      CL179
02000              MOVE -1                        TO ABEGITML              CL*33
02001              MOVE DP015-REVERSE             TO ABEGITMH              CL*53
02002              MOVE DP015-RED                 TO ABEGITMC              CL*33
02003              MOVE DP015-UNP-ALP-BRT-MDT     TO ABEGITMA              CL167
02004              SET PS-INVALID-BEG-ITEM        TO TRUE                  CL*33
02005              SET PS-CURSOR-POSITIONED       TO TRUE                  CL*33
02006              SET DP020-MSG-FATAL            TO TRUE                  CL*33
02007          END-IF                                                   MBKCS102
02008                                                                   MBKCS102
02009      END-IF.                                                      MBKCS102
02010                                                                   MBKCS102
02011      IF PS-INVALID-BEG-ITEM                                       MBKCS102
02012          SET PS-SAME-ITEMS-DISPLAYED        TO TRUE                  CL*73
02013          PERFORM DP017-0000-GET-CURR-DATE-TIME                    MBKCS102
02014          PERFORM I200-FORMAT-LIST-LINES                           MBKCS102
02015          PERFORM I300-FORMAT-NR-SELECTED                          MBKCS102
02016          PERFORM DP005-0000-FORMAT-MSG-LINES                      MBKCS102
02017          PERFORM DP008-0000-WRITE-PANEL                           MBKCS102
02018      END-IF.                                                      MBKCS102
02019  EJECT                                                            MBKCS102
02020  E900-FIND-FIRST-SELECTED.                                           CL233
02021 ******************************************************************   CL233
02022 *    FIND THE FIRST DOCUMENT SELECTED ON THE LIST AND PASS THIS      CL233
02023 *    DOCUMENT TO PEIHDR (INQUIRY HEADER).                            CL233
02024 *                                                                    CL233
02025 *    PERFORMED BY D200-CHECK-FUNCTION-KEY.                           CL233
02026 ******************************************************************   CL233
02027                                                                      CL233
02028      IF PV-SELECTION-MADE(PS-SUB)                                    CL233
02029          SET PS-SELECTION-FOUND TO TRUE                              CL233
02030                                                                      CL233
02031          MOVE PV-SELECTED-GMA(PS-SUB) TO PV-SELECTED-GMA-NO          CL236
02032          MOVE PV-SELECTED-DMA(PS-SUB) TO PV-SELECTED-DMA-NO          CL236
02033          MOVE PV-SELECTED-DPT(PS-SUB) TO PV-SELECTED-DPT-NO          CL236
02034          MOVE PV-SELECTED-MCL(PS-SUB) TO PV-SELECTED-MCL-NO          CL236
02035          MOVE PV-SELECTED-SCL(PS-SUB) TO PV-SELECTED-SCL-NO          CL236
02036          MOVE PV-SELECTED-LEVEL(PS-SUB) TO                           CL236
02037                PV-SELECTED-LEVEL-CD                                  CL236
02038          MOVE PV-SELECTED-END-DATE(PS-SUB) TO                        CL236
02039                PV-SELECTED-END-DT                                    CL236
02040      ELSE                                                            CL233
02041          ADD +1 TO PS-SUB                                            CL233
02042      END-IF.                                                         CL233
02043                                                                      CL233
02044      IF (PS-SUB > PC-ITEMS-PER-BLOCK) AND                            CL233
02045         (PS-NO-SELECTION-FOUND)                                      CL233
02046          MOVE +1 TO PS-SUB                                           CL233
02047                                                                      CL233
02048          ADD +1 TO PV-BLOCK-NUMBER                                   CL233
02049                                                                      CL233
02050          PERFORM Z125-READ-TEMP-STORAGE                              CL233
02051      END-IF.                                                         CL233
02052  EJECT                                                               CL233
02053  F100-EDIT.                                                       MBKCS102
02054                                                                   MBKCS102
02055 ******************************************************************MBKCS102
02056 *    EDIT THE SELECTION ENTRIES.                                  MBKCS102
02057 *                                                                 MBKCS102
02058 *    1) VALID ENTRIES ARE 'S' 'B' OR 'E'.                         MBKCS102
02059 *    2) THERE CAN ONLY BE 1 BEGINNING AND 1 ENDING RANGE.         MBKCS102
02060 *                                                                 MBKCS102
02061 *    PERFORMED BY E100-EDIT-SELECTIONS.                              CL*92
02062 ******************************************************************MBKCS102
02063                                                                   MBKCS102
02064      PERFORM K100-SET-SUBSCRIPTS.                                 MBKCS102
02065                                                                   MBKCS102
02066      IF NOT (MB102-NO-SELECT(MB102-BLK-SUB MB102-ITEM-SUB)) AND      CL210
02067         NOT (MB102-SELECT(MB102-BLK-SUB MB102-ITEM-SUB))             CL210
02068                                                                      CL**2
02069 ******************************************************************   CL**2
02070 *   IF THE CURRENT ENTRY IS A 'B', WE MUST MAKE SURE THAT THIS       CL**2
02071 *   IS THE ONLY 'B' ENTERED ON THE SCREEN.  TO VERIFY THIS, THE      CL**2
02072 *   CURRENT ITEM WE ARE POINTING TO SHOULD EQUAL THE START OF        CL**2
02073 *   SELECTED RANGE FIELD.  IF THEY ARE NOT EQUAL, AND THE START OF   CL**2
02074 *   SELECTED RANGE FIELD DOES NOT EQUAL 0, WE KNOW THAT THIS FIELD   CL**2
02075 *   CONTAINS THE LIST ITEM NUMBER THAT CURRENTLY CONTAINS A 'B'.     CL**2
02076 *   IF THE START OF SELECTED RANGE FIELD EQUALS 0, THAN THIS IS      CL**2
02077 *   THE FIRST 'B' ENTERED ON THE SCREEN, AND WE SET THIS FIELD       CL**2
02078 *   EQUAL TO THE CURRENT ITEM WE ARE POINTING TO.                    CL**2
02079 *                                                                    CL**2
02080 *   THE SAME TYPE OF LOGIC IS DONE FOR THE ENDING RANGE 'E'.         CL**2
02081 ******************************************************************   CL**2
02082                                                                   MBKCS102
02083          IF MB102-BEGIN(MB102-BLK-SUB MB102-ITEM-SUB)                CL210
02084                                                                   MBKCS102
02085              IF MB102-START-OF-SELECTED-RANGE NOT = PV-ITEM-IN-USE   CL209
02086                                                                   MBKCS102
02087                  IF MB102-START-OF-SELECTED-RANGE = +0               CL209
02088                      MOVE PV-ITEM-IN-USE TO                       MBKCS102
02089                          MB102-START-OF-SELECTED-RANGE               CL209
02090                  ELSE                                             MBKCS102
02091                                                                   MBKCS102
02092                      IF PS-VALID-SELECTIONS                       MBKCS102
02093                          MOVE PC-TSYMSG-00054 TO                     CL179
02094                               DP020-MSG-NUMBER                       CL179
02095                          IF NOT DP020-NEXT-ACT-ERROR                 CL*22
02096                              MOVE -1      TO MB004I-ASELL(PS-SUB)    CL*52
02097                          END-IF                                      CL*22
02098                      END-IF                                       MBKCS102
02099                                                                   MBKCS102
02100                      SET DP020-MSG-FATAL       TO TRUE               CL*22
02101                      SET PS-INVALID-SELECTIONS TO TRUE               CL*22
02102                      SET PS-CURSOR-POSITIONED  TO TRUE               CL*53
02103                  END-IF                                           MBKCS102
02104              END-IF                                               MBKCS102
02105          ELSE                                                     MBKCS102
02106              IF MB102-END(MB102-BLK-SUB MB102-ITEM-SUB)              CL210
02107                                                                   MBKCS102
02108                  IF MB102-END-OF-SELECTED-RANGE NOT =                CL209
02109                     PV-ITEM-IN-USE                                MBKCS102
02110                                                                   MBKCS102
02111                      IF MB102-END-OF-SELECTED-RANGE = +0             CL209
02112                          MOVE PV-ITEM-IN-USE TO                   MBKCS102
02113                               MB102-END-OF-SELECTED-RANGE            CL209
02114                      ELSE                                         MBKCS102
02115                                                                   MBKCS102
02116                          IF PS-VALID-SELECTIONS                   MBKCS102
02117                              MOVE PC-TSYMSG-00055 TO                 CL179
02118                                   DP020-MSG-NUMBER                   CL179
02119                              IF NOT DP020-NEXT-ACT-ERROR             CL*22
02120                                 MOVE -1 TO MB004I-ASELL(PS-SUB)      CL*52
02121                              END-IF                                  CL*22
02122                          END-IF                                   MBKCS102
02123                          SET DP020-MSG-FATAL       TO TRUE           CL*22
02124                          SET PS-INVALID-SELECTIONS TO TRUE        MBKCS102
02125                          SET PS-CURSOR-POSITIONED  TO TRUE           CL*53
02126                      END-IF                                       MBKCS102
02127                  END-IF                                           MBKCS102
02128              ELSE                                                 MBKCS102
02129                  IF PS-VALID-SELECTIONS                           MBKCS102
02130                      MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER        CL179
02131                      IF NOT DP020-NEXT-ACT-ERROR                     CL*22
02132                         MOVE -1   TO MB004I-ASELL(PS-SUB)            CL*52
02133                      END-IF                                          CL*22
02134                  END-IF                                              CL*22
02135                  SET DP020-MSG-FATAL       TO TRUE                   CL*22
02136                  SET PS-INVALID-SELECTIONS TO TRUE                   CL*22
02137                  SET PS-CURSOR-POSITIONED  TO TRUE                   CL*53
02138                  MOVE DP015-RED            TO MB004O-ASELC(PS-SUB)   CL*56
02139                  MOVE DP015-REVERSE        TO MB004O-ASELH(PS-SUB)   CL*56
02140              END-IF                                               MBKCS102
02141          END-IF                                                   MBKCS102
02142      END-IF.                                                      MBKCS102
02143                                                                   MBKCS102
02144      ADD +1 TO PV-ITEM-IN-USE.                                    MBKCS102
02145      ADD +1 TO PS-SUB.                                            MBKCS102
02146  EJECT                                                            MBKCS102
02147  F200-MARK-RANGE-AS-SELECTED.                                     MBKCS102
02148                                                                   MBKCS102
02149 ******************************************************************MBKCS102
02150 *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE     MBKCS102
02151 *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS     MBKCS102
02152 *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.              MBKCS102
02153 *                                                                 MBKCS102
02154 *    PERFORMED BY E100-EDIT-SELECTIONS.                              CL*92
02155 ******************************************************************MBKCS102
02156                                                                   MBKCS102
02157      MOVE +1 TO MB102-BLK-SUB.                                       CL210
02158                                                                   MBKCS102
02159      PERFORM G200-MARK-SELECTED-INDS                              MBKCS102
02160          VARYING MB102-ITEM-SUB FROM 1 BY 1                          CL209
02161          UNTIL MB102-ITEM-SUB > PC-ITEMS-PER-BLOCK.                  CL209
02162                                                                   MBKCS102
02163      ADD +1 TO MB102-BLK-SUB.                                        CL210
02164                                                                   MBKCS102
02165      PERFORM G200-MARK-SELECTED-INDS                              MBKCS102
02166          VARYING MB102-ITEM-SUB FROM 1 BY 1                          CL209
02167          UNTIL MB102-ITEM-SUB > PC-ITEMS-PER-BLOCK.                  CL209
02168                                                                      CL**2
02169 ******************************************************************   CL**2
02170 *    AFTER MARKING THE ENTRIES IN THE BLOCKS IN WORKING STORAGE,     CL**2
02171 *    WE MUST SEE IF ANY OF THE ENTRIES IN THE BLOCKS IN TEMPORARY    CL**2
02172 *    STORAGE SHOULD BE MARKED.  TO DETERMINE THIS, WE CALCULATE      CL**2
02173 *    THE BLOCK NUMBERS OF THE BLOCKS IN WORKING STORAGE AND THE      CL**2
02174 *    BLOCK NUMBERS OF THE BLOCKS IN TEMPORARY STORAGE THAT FALL      CL**2
02175 *    WITHIN THE STARTING AND ENDING RANGE SPECIFED BY THE USER.      CL**2
02176 *    IF THE BLOCK NUMBERS OF THE BLOCKS IN TEMPORARY STORAGE         CL**2
02177 *    ARE LESS THAN OR GREATER THAN THE BLOCK NUMBERS OF THE          CL**2
02178 *    BLOCKS IN WORKING STORAGE, WE MUST READ THOSE BLOCKS FROM       CL**2
02179 *    TEMPORARY STORAGE AND MARK THE APPROPRIATE ENTRIES AS BEING     CL**2
02180 *    SELECTED.                                                       CL**2
02181 ******************************************************************   CL**2
02182                                                                   MBKCS102
02183      MOVE MB102-LIST-ITEM-NUMBER(1 1)   TO                           CL209
02184           PV-LIST-ITEM-NUMBER.                                    MBKCS102
02185                                                                   MBKCS102
02186      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            MBKCS102
02187          1 + ((PV-LIST-ITEM-NUMBER - 1) /                         MBKCS102
02188                PC-ITEMS-PER-BLOCK).                               MBKCS102
02189                                                                   MBKCS102
02190      MOVE MB102-START-OF-SELECTED-RANGE TO                           CL209
02191           PV-START-OF-SELECTED-RANGE.                             MBKCS102
02192                                                                   MBKCS102
02193      COMPUTE PV-FIRST-BLOCK-IN-RANGE =                            MBKCS102
02194          1 + ((PV-START-OF-SELECTED-RANGE - 1) /                  MBKCS102
02195                PC-ITEMS-PER-BLOCK).                               MBKCS102
02196                                                                   MBKCS102
02197      MOVE MB102-END-OF-SELECTED-RANGE   TO                           CL209
02198           PV-END-OF-SELECTED-RANGE.                               MBKCS102
02199                                                                   MBKCS102
02200      COMPUTE PV-LAST-BLOCK-IN-RANGE =                             MBKCS102
02201          1 + ((PV-END-OF-SELECTED-RANGE - 1) /                    MBKCS102
02202                PC-ITEMS-PER-BLOCK).                               MBKCS102
02203                                                                   MBKCS102
02204      COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                             MBKCS102
02205          PV-FIRST-BLOCK-IN-ARRAY + 1.                             MBKCS102
02206                                                                   MBKCS102
02207      IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR    MBKCS102
02208         (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)        MBKCS102
02209          MOVE +1 TO MB102-BLK-SUB                                    CL210
02210                                                                   MBKCS102
02211          PERFORM Z150-WRITE-TEMP-STORAGE                          MBKCS102
02212                                                                   MBKCS102
02213          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         MBKCS102
02214              PV-FIRST-BLOCK-IN-ARRAY - 1                          MBKCS102
02215                                                                   MBKCS102
02216          PERFORM                                                  MBKCS102
02217              UNTIL PV-FIRST-BLOCK-IN-RANGE >                      MBKCS102
02218                    PV-PREV-BLOCK-IN-ARRAY                         MBKCS102
02219                                                                   MBKCS102
02220              MOVE PV-FIRST-BLOCK-IN-RANGE TO PV-BLOCK-NUMBER      MBKCS102
02221                                                                   MBKCS102
02222              PERFORM Z100-READ-TEMP-STORAGE                       MBKCS102
02223                                                                   MBKCS102
02224              PERFORM G200-MARK-SELECTED-INDS                      MBKCS102
02225                  VARYING MB102-ITEM-SUB FROM 1 BY 1                  CL209
02226                  UNTIL MB102-ITEM-SUB > PC-ITEMS-PER-BLOCK           CL209
02227                                                                   MBKCS102
02228              PERFORM Z150-WRITE-TEMP-STORAGE                      MBKCS102
02229                                                                   MBKCS102
02230              ADD +1 TO PV-FIRST-BLOCK-IN-RANGE                    MBKCS102
02231                                                                   MBKCS102
02232          END-PERFORM                                              MBKCS102
02233                                                                   MBKCS102
02234          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         MBKCS102
02235              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     MBKCS102
02236                                                                   MBKCS102
02237          PERFORM                                                  MBKCS102
02238              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       MBKCS102
02239                    PV-LAST-BLOCK-IN-RANGE                         MBKCS102
02240                                                                   MBKCS102
02241              MOVE PV-NEXT-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER       MBKCS102
02242                                                                   MBKCS102
02243              PERFORM Z100-READ-TEMP-STORAGE                       MBKCS102
02244                                                                   MBKCS102
02245              PERFORM G200-MARK-SELECTED-INDS                      MBKCS102
02246                  VARYING MB102-ITEM-SUB FROM 1 BY 1                  CL209
02247                  UNTIL MB102-ITEM-SUB > PC-ITEMS-PER-BLOCK           CL209
02248                                                                   MBKCS102
02249              PERFORM Z150-WRITE-TEMP-STORAGE                      MBKCS102
02250                                                                   MBKCS102
02251              ADD +1 TO PV-NEXT-BLOCK-IN-ARRAY                     MBKCS102
02252                                                                   MBKCS102
02253          END-PERFORM                                              MBKCS102
02254                                                                   MBKCS102
02255          MOVE PV-FIRST-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER          MBKCS102
02256                                                                   MBKCS102
02257          PERFORM Z100-READ-TEMP-STORAGE                           MBKCS102
02258      END-IF.                                                      MBKCS102
02259                                                                   MBKCS102
02260      MOVE +0 TO MB102-START-OF-SELECTED-RANGE.                       CL209
02261      MOVE +0 TO MB102-END-OF-SELECTED-RANGE.                         CL209
02262  EJECT                                                            MBKCS102
02263  F300-CHECK-SELECTIONS.                                           MBKCS102
02264                                                                   MBKCS102
02265 ******************************************************************MBKCS102
02266 *    CHECK EACH ENTRY TO DETERMINE IF THE SELECTION CRITERIA IN   MBKCS102
02267 *    THE MAP DIFFERS FROM THAT IN THE ARRAY.  IF THEY DO DIFFER,  MBKCS102
02268 *    AND THE ARRAY ENTRY SHOWS THAT THE ITEM WAS PREVIOUSLY       MBKCS102
02269 *    SELECTED, SUBTRACT 1 FROM THE SELECTED COUNTER.  IF THEY DO  MBKCS102
02270 *    DIFFER, AND THE ARRAY ENTRY SHOWS THAT THIS ITEM WAS MARKED  MBKCS102
02271 *    AS EITHER THE BEGINNING OR ENDING SELECTION POINT, ZERO OUT  MBKCS102
02272 *    THE STARTING RANGE AND/OR ENDING RANGE.  IF THE ENTRY ON     MBKCS102
02273 *    THE MAP SHOWS THAT THIS ITEM WAS SELECTED, ADD 1 TO THE      MBKCS102
02274 *    SELECTED COUNTER.                                            MBKCS102
02275 *                                                                 MBKCS102
02276 *    PERFORMED BY E200-MOVE-SELECTIONS-TO-COMMAREA.                  CL*92
02277 ******************************************************************MBKCS102
02278                                                                   MBKCS102
02279      IF (PV-ITEM-IN-USE < MB102-NUMBER-OF-ITEMS-READ) OR             CL209
02280         (PV-ITEM-IN-USE = MB102-NUMBER-OF-ITEMS-READ)                CL209
02281                                                                   MBKCS102
02282          PERFORM K100-SET-SUBSCRIPTS                              MBKCS102
02283                                                                      CL**2
02284 ******************************************************************   CL**2
02285 *    IF THE ENTRY IN THE BLOCK DOES NOT EQUAL THE ENTRY ON THE       CL**2
02286 *    MAP, SET THE SWITCH SAYING THAT THIS BLOCK HAS BEEN MODIFIED.   CL**2
02287 *                                                                    CL**2
02288 *    IF THE ENTRY IN THE BLOCK EQUALS 'S', SUBTRACT 1 FROM THE       CL**2
02289 *    NUMBER OF SELECTED ITEMS.                                       CL**2
02290 *                                                                    CL**2
02291 *    IF THE ENTRY IN THE BLOCK EQUALS 'B' AND THIS ENTRY WAS EQUAL   CL**2
02292 *    TO THE START OF SELECTED RANGE, MOVE 0 TO THE START OF          CL**2
02293 *    SELECTED RANGE.  IF THERE WAS MORE THAN ONE 'B' ON THE          CL**2
02294 *    SCREEN (ERROR), THE START OF SELECTED RANGE WILL THAN BE        CL**2
02295 *    RESET LATER TO POINT TO ONE OF THE OTHER 'B' ENTRIES.           CL**2
02296 *                                                                    CL**2
02297 *    THE SAME LOGIC APPLIES TO AN 'E' ENTRY.                         CL**2
02298 *                                                                    CL**2
02299 ******************************************************************   CL**2
02300                                                                   MBKCS102
02301          IF MB102-SELECTED-SW(MB102-BLK-SUB                          CL210
02302                               MB102-ITEM-SUB) NOT =                  CL209
02303             MB004I-ASELI(PS-SUB)                                     CL*22
02304                                                                      CL**2
02305              SET MB102-BLOCK-MODIFIED(MB102-BLK-SUB) TO TRUE         CL210
02306                                                                   MBKCS102
02307              IF MB102-SELECT(MB102-BLK-SUB MB102-ITEM-SUB)           CL210
02308                  SUBTRACT +1 FROM MB102-NUMBER-OF-ITEMS-SELECTED     CL209
02309              ELSE                                                 MBKCS102
02310                                                                   MBKCS102
02311                  IF MB102-BEGIN(MB102-BLK-SUB MB102-ITEM-SUB)        CL210
02312                                                                   MBKCS102
02313                      IF MB102-LIST-ITEM-NUMBER(MB102-BLK-SUB         CL210
02314                                                MB102-ITEM-SUB) =     CL209
02315                         MB102-START-OF-SELECTED-RANGE                CL209
02316                                                                      CL**2
02317                          MOVE +0 TO MB102-START-OF-SELECTED-RANGE    CL209
02318                      END-IF                                       MBKCS102
02319                                                                   MBKCS102
02320                  ELSE                                             MBKCS102
02321                                                                   MBKCS102
02322                      IF MB102-END(MB102-BLK-SUB MB102-ITEM-SUB)      CL210
02323                                                                   MBKCS102
02324                          IF MB102-LIST-ITEM-NUMBER(MB102-BLK-SUB     CL210
02325                                                    MB102-ITEM-SUB)   CL209
02326                          =  MB102-END-OF-SELECTED-RANGE              CL209
02327                                                                      CL**2
02328                              MOVE +0 TO                           MBKCS102
02329                                   MB102-END-OF-SELECTED-RANGE        CL209
02330                          END-IF                                   MBKCS102
02331                                                                   MBKCS102
02332                      END-IF                                       MBKCS102
02333                                                                   MBKCS102
02334                  END-IF                                           MBKCS102
02335                                                                   MBKCS102
02336              END-IF                                               MBKCS102
02337                                                                      CL**2
02338 ******************************************************************   CL**2
02339 *    IF THE ENTRY IN THE MAP EQUALS 'S', ADD 1 TO THE NUMBER OF      CL**2
02340 *    SELECTED ITEMS.                                                 CL**2
02341 *                                                                    CL**2
02342 *    IF THE ENTRY IN THE MAP EQUALS SPACES OR ERASE EOF, MOVE        CL**2
02343 *    SPACES TO THE ENTRY IN THE BLOCK.                               CL**2
02344 *                                                                    CL**2
02345 *    IF THE ENTRY IN THE MAP DOES NOT EQUAL SPACES OR ERASE EOF,     CL**2
02346 *    MOVE WHATEVER WAS ENTERED ON THE MAP TO THE ENTRY IN THE        CL**2
02347 *    BLOCK.                                                          CL**2
02348 *                                                                    CL**2
02349 ******************************************************************   CL**2
02350                                                                   MBKCS102
02351              IF MB004I-ASELI(PS-SUB) = PC-SELECTED                   CL*22
02352                  ADD +1 TO MB102-NUMBER-OF-ITEMS-SELECTED            CL209
02353              END-IF                                               MBKCS102
02354                                                                   MBKCS102
02355              IF (MB004I-ASELA(PS-SUB) = PC-ERASE-EOF) OR             CL*73
02356                 (MB004I-ASELI(PS-SUB) = SPACES)       OR             CL174
02357                 (MB004I-ASELI(PS-SUB) = LOW-VALUES)                  CL174
02358                  MOVE SPACES                TO                       CL*73
02359                       MB102-SELECTED-SW(MB102-BLK-SUB                CL210
02360                                         MB102-ITEM-SUB)              CL209
02361              ELSE                                                 MBKCS102
02362                  MOVE MB004I-ASELI(PS-SUB) TO                        CL*22
02363                       MB102-SELECTED-SW(MB102-BLK-SUB                CL210
02364                                         MB102-ITEM-SUB)              CL209
02365              END-IF                                               MBKCS102
02366                                                                   MBKCS102
02367          END-IF                                                   MBKCS102
02368                                                                   MBKCS102
02369      END-IF.                                                      MBKCS102
02370                                                                   MBKCS102
02371      ADD +1 TO PV-ITEM-IN-USE.                                    MBKCS102
02372      ADD +1 TO PS-SUB.                                            MBKCS102
02373  EJECT                                                            MBKCS102
02374  F400-SETUP-INTER-APPL.                                           MBKCS102
02375                                                                   MBKCS102
02376 ******************************************************************MBKCS102
02377 *    PLACING THE 4 DIGIT CODE FOR THIS TRANSACTION IN THE            CL**2
02378 *    STANDARD COMMAREA WILL TELL THE TRANSACTION THAT WE ARE         CL**2
02379 *    GOING TO WHETHER OR NOT THAT THEY CAN USE THE INTER             CL**2
02380 *    APPLICATION COMMAREA.                                           CL**2
02381 *                                                                    CL**2
02382 *    IN THE INTER APPLICATION COMMAREA, WE MUST PUT THE TEMPORARY    CL**2
02383 *    STORAGE QUEUE NAME AND THE NUMBER OF SELECTED ITEMS.  THIS      CL**2
02384 *    WILL ALLOW THE TRANSACTION WE ARE GOING TO, TO READ THE         CL**2
02385 *    QUEUE AND PROCESS THE SELECTED ITEMS.                           CL**2
02386 *                                                                    CL**2
02387 *    PERFORMED BY E300-NAVIGATE.                                     CL*92
02388 ******************************************************************MBKCS102
02389                                                                   MBKCS102
02390 *    MOVE LOW-VALUES               TO INTER-APPL-COMM-AREA.          CL254
02391                                                                   MBKCS102
02392      SET DP020-MB-MERCH-AREA-LIST TO TRUE                            CL*27
02393                                                                   MBKCS102
02394      MOVE MB102-TEMP-QUEUE-NAME          TO                          CL209
02395           MB005-LIST-QUEUE-NAME.                                     CL211
02396      MOVE MB102-NUMBER-OF-ITEMS-SELECTED TO                          CL209
02397           MB005-NUMBER-OF-SELECTED-ITEMS.                            CL214
02398      MOVE MB102-CO-NUMBER                TO                          CL209
02399           MB005-CO-NUMBER.                                           CL207
02400                                                                      CL236
02401      MOVE PV-SELECTED-GMA-NO             TO                          CL236
02402           MB005-GMA-NUMBER.                                          CL207
02403      MOVE PV-SELECTED-DMA-NO             TO                          CL236
02404           MB005-DMA-NUMBER.                                          CL207
02405      MOVE PV-SELECTED-DPT-NO             TO                          CL236
02406           MB005-DEPT-NUMBER.                                         CL207
02407      MOVE PV-SELECTED-MCL-NO             TO                          CL236
02408           MB005-MCLASS-NUMBER.                                       CL207
02409      MOVE PV-SELECTED-SCL-NO             TO                          CL236
02410           MB005-SCLASS-NUMBER.                                       CL207
02411      MOVE PV-SELECTED-LEVEL-CD TO MB005-MDSEAR-LEVEL.                CL236
02412                                                                      CL236
02413  EJECT                                                            MBKCS102
02414  F500-BROWSE.                                                     MBKCS102
02415                                                                   MBKCS102
02416 ******************************************************************MBKCS102
02417 *    CALCULATE THE NEW TOP AND BOTTOM ITEM NUMBERS BASED ON WHERE MBKCS102
02418 *    THE USER SPECIFIED TO GO ON THE LIST AND DETERMINE WHERE     MBKCS102
02419 *    THESE ITEMS CAN BE FOUND (I.E. ARRAY, TEMPORARY STORAGE).    MBKCS102
02420 *                                                                 MBKCS102
02421 *    PERFORMED BY E400-DISPLAY-TOP-OF-LIST.                          CL*92
02422 *    PERFORMED BY E500-BROWSE-BACKWARD.                              CL*92
02423 *    PERFORMED BY E600-BROWSE-FORWARD.                               CL*92
02424 *    PERFORMED BY E800-CHECK-FOR-JUMP-BROWSE.                        CL*92
02425 ******************************************************************MBKCS102
02426                                                                      CL**2
02427 ******************************************************************   CL**2
02428 *    USER HITS PAGE BACKWARD WHEN ALREADY AT THE TOP OF THE LIST.    CL**2
02429 ******************************************************************   CL**2
02430                                                                   MBKCS102
02431      IF PV-TOP-ITEM < +1                                          MBKCS102
02432          MOVE +1                         TO PV-TOP-ITEM              CL*28
02433                                                                   MBKCS102
02434          IF PS-CURSOR-NOT-POSITIONED                                 CL*28
02435              MOVE PC-TSYMSG-00069        TO DP020-MSG-NUMBER         CL179
02436              SET PS-CURSOR-POSITIONED    TO TRUE                     CL*28
02437              SET PS-INFORMATIONAL        TO TRUE                     CL*28
02438              SET DP020-MSG-INFORMATIONAL TO TRUE                     CL*28
02439          END-IF                                                      CL*28
02440      END-IF.                                                      MBKCS102
02441                                                                      CL**2
02442      IF MB102-NO-ITEMS-LEFT-ON-DB                                    CL209
02443                                                                      CL**2
02444 ******************************************************************   CL**2
02445 *    USER HITS PAGE FORWARD WHEN ALREADY AT THE END OF THE LIST.     CL**2
02446 ******************************************************************   CL**2
02447                                                                   MBKCS102
02448          IF PV-TOP-ITEM > MB102-NUMBER-OF-ITEMS-READ                 CL209
02449              MOVE MB102-ITEM-AT-TOP-OF-PANEL TO PV-TOP-ITEM          CL209
02450                                                                   MBKCS102
02451              IF PS-CURSOR-NOT-POSITIONED                             CL*28
02452                  MOVE -1                     TO ANAVCMDL             CL*28
02453                  SET DP020-MSG-INFORMATIONAL TO TRUE                 CL*28
02454                  SET PS-CURSOR-POSITIONED    TO TRUE                 CL*52
02455 *                IF DP020-FK-NEXT-PAGE(DP020-SRC-AID)                CL*34
02456 *                                                                    CL*34
02457                      SET PS-INFORMATIONAL    TO TRUE                 CL*28
02458                      MOVE PC-TSYMSG-00070    TO DP020-MSG-NUMBER     CL179
02459 *                ELSE                                                CL*34
02460 *                    MOVE PC-TSYMSG-00061    TO DP020-MSG-NUMBER     CL*34
02461 *                END-IF                                              CL*34
02462                                                                      CL*28
02463              END-IF                                                  CL*28
02464                                                                   MBKCS102
02465          END-IF                                                   MBKCS102
02466                                                                   MBKCS102
02467      END-IF.                                                      MBKCS102
02468                                                                   MBKCS102
02469      COMPUTE PV-BOTTOM-ITEM =                                     MBKCS102
02470          (PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1.                  MBKCS102
02471                                                                   MBKCS102
02472      IF MB102-NO-ITEMS-LEFT-ON-DB                                    CL209
02473                                                                      CL**2
02474 ******************************************************************   CL**2
02475 *    USER HITS PAGE FORWARD, AND THE NEXT PAGE WILL CAUSE US         CL**2
02476 *    TO BE AT THE END OF THE LIST.  IF TOP ITEM EQUALS 1, THE        CL**2
02477 *    MESSAGE WOULD HAVE BEEN FLAGGED ALREADY ABOVE.                  CL**2
02478 ******************************************************************   CL**2
02479                                                                   MBKCS102
02480          IF PV-BOTTOM-ITEM > MB102-NUMBER-OF-ITEMS-READ              CL209
02481              MOVE MB102-NUMBER-OF-ITEMS-READ TO PV-BOTTOM-ITEM       CL209
02482                                                                   MBKCS102
02483              IF PS-CURSOR-NOT-POSITIONED                             CL*69
02484                                                                      CL*69
02485                  IF PV-TOP-ITEM NOT = +1                             CL*69
02486                      SET PS-INFORMATIONAL        TO TRUE             CL*69
02487                      SET PS-CURSOR-POSITIONED    TO TRUE             CL*69
02488                      SET DP020-MSG-INFORMATIONAL TO TRUE             CL*69
02489                      MOVE PC-TSYMSG-00070        TO                  CL179
02490                           DP020-MSG-NUMBER                           CL*69
02491                  END-IF                                              CL*69
02492                                                                      CL*69
02493              END-IF                                                  CL*69
02494                                                                   MBKCS102
02495          END-IF                                                   MBKCS102
02496                                                                   MBKCS102
02497      END-IF.                                                      MBKCS102
02498                                                                      CL**2
02499 ******************************************************************   CL**2
02500 *    DETEMINE WHETHER THE ITEMS WE NEED ARE IN WORKING STORAGE,      CL**2
02501 *    TEMPORARY STORAGE, OR NEITHER.                                  CL**2
02502 ******************************************************************   CL**2
02503                                                                   MBKCS102
02504      PERFORM G300-DETERMINE-WHAT-IS-WHERE.                        MBKCS102
02505                                                                   MBKCS102
02506      IF PS-TOP-ITEM-IN-ARRAY                                      MBKCS102
02507                                                                   MBKCS102
02508          IF PS-BOTTOM-ITEM-NOT-IN-ARRAY                           MBKCS102
02509                                                                      CL**2
02510 ******************************************************************   CL**2
02511 *    TOP ITEM IS IN WORKING STORAGE AND BOTTOM ITEM IS IN            CL**2
02512 *    TEMPORARY STORAGE.                                              CL**2
02513 ******************************************************************   CL**2
02514                                                                   MBKCS102
02515              IF PS-BOTTOM-ITEM-IN-TEMP                            MBKCS102
02516                  PERFORM G500-SCROLL-FORWARD                      MBKCS102
02517              ELSE                                                 MBKCS102
02518                                                                      CL**2
02519 ******************************************************************   CL**2
02520 *    TOP ITEM IS IN WORKING STORAGE BUT BOTTOM ITEM HAS NOT          CL**2
02521 *    BEEN READ YET.  IF THE LAST LOGICAL BLOCK IS NOT IN WORKING     CL**2
02522 *    STORAGE, READ IT INTO WORKING STORAGE SO WE CAN START FILLING   CL**2
02523 *    IT UP WITH THE NEW ITEMS READ.                                  CL**2
02524 ******************************************************************   CL**2
02525                                                                   MBKCS102
02526                  IF PS-LAST-BLOCK-NOT-IN-ARRAY                    MBKCS102
02527                      PERFORM G500-SCROLL-FORWARD                  MBKCS102
02528                  END-IF                                           MBKCS102
02529                                                                   MBKCS102
02530                  PERFORM G400-READ-ITEMS-INTO-ARRAY               MBKCS102
02531                                                                   MBKCS102
02532              END-IF                                               MBKCS102
02533                                                                   MBKCS102
02534          END-IF                                                   MBKCS102
02535                                                                   MBKCS102
02536      ELSE                                                         MBKCS102
02537                                                                      CL**2
02538 ******************************************************************   CL**2
02539 *    BOTTOM ITEM IS IN WORKING STORAGE AND TOP ITEM IS IN            CL**2
02540 *    TEMPORARY STORAGE.                                              CL**2
02541 ******************************************************************   CL**2
02542                                                                   MBKCS102
02543          IF PS-BOTTOM-ITEM-IN-ARRAY                               MBKCS102
02544              PERFORM G600-SCROLL-BACKWARD                         MBKCS102
02545          ELSE                                                     MBKCS102
02546                                                                      CL**2
02547 ******************************************************************   CL**2
02548 *    TOP ITEM AND BOTTOM ITEM ARE NOT IN WORKING STORAGE.            CL**2
02549 ******************************************************************   CL**2
02550                                                                   MBKCS102
02551              IF PS-TOP-ITEM-IN-TEMP                               MBKCS102
02552                  PERFORM G700-GET-TOP-ITEMS-BLOCK                 MBKCS102
02553                  PERFORM G300-DETERMINE-WHAT-IS-WHERE             MBKCS102
02554                                                                      CL**2
02555 ******************************************************************   CL**2
02556 *    TOP ITEM IS IN TEMPORARY STORAGE AND BOTTOM ITEM HAS NOT        CL**2
02557 *    BEEN READ YET.                                                  CL**2
02558 ******************************************************************   CL**2
02559                                                                   MBKCS102
02560                  IF PS-BOTTOM-ITEM-NOT-IN-ARRAY                   MBKCS102
02561                      PERFORM G400-READ-ITEMS-INTO-ARRAY           MBKCS102
02562                  END-IF                                           MBKCS102
02563                                                                   MBKCS102
02564              ELSE                                                 MBKCS102
02565                                                                      CL**2
02566 ******************************************************************   CL**2
02567 *    TOP AND BOTTOM ITEM HAVE NOT BEEN READ YET.  IF THE LAST        CL**2
02568 *    LOGICAL BLOCK IS NOT IN WORKING STORAGE, READ IT INTO           CL**4
02569 *    WORKING STORAGE SO WE CAN START FILLING IT UP WITH THE          CL**4
02570 *    NEW ITEMS READ.                                                 CL**2
02571 ******************************************************************   CL**2
02572                                                                   MBKCS102
02573                  IF PS-LAST-BLOCK-NOT-IN-ARRAY                    MBKCS102
02574                      PERFORM G800-GET-LAST-BLOCK-IN-TEMP          MBKCS102
02575                  END-IF                                           MBKCS102
02576                                                                   MBKCS102
02577                  PERFORM G400-READ-ITEMS-INTO-ARRAY               MBKCS102
02578              END-IF                                               MBKCS102
02579                                                                   MBKCS102
02580          END-IF                                                   MBKCS102
02581                                                                   MBKCS102
02582      END-IF.                                                      MBKCS102
02583                                                                   MBKCS102
02584      IF PV-TOP-ITEM = MB102-ITEM-AT-TOP-OF-PANEL                     CL209
02585          SET PS-SAME-ITEMS-DISPLAYED      TO TRUE                    CL*69
02586      ELSE                                                            CL*69
02587          SET PS-DIFFERENT-ITEMS-DISPLAYED TO TRUE                    CL*69
02588          MOVE PV-TOP-ITEM TO MB102-ITEM-AT-TOP-OF-PANEL              CL209
02589      END-IF.                                                         CL*69
02590                                                                   MBKCS102
02591      PERFORM G100-DISPLAY-PANEL.                                  MBKCS102
02592  EJECT                                                            MBKCS102
02593  F600-CLEAR-SELECTED-INDS.                                        MBKCS102
02594                                                                   MBKCS102
02595 ******************************************************************MBKCS102
02596 *    MARK EACH ITEM AS BEING NOT SELECTED OR SELECTED BASED ON       CL*93
02597 *    WHICH FUNCTION KEY THAT WAS HIT (DESELECT ALL OR SELECT ALL)    CL*93
02598 *                                                                 MBKCS102
02599 *    PERFORMED BY E700-SELECT-DESELECT-ALL.                          CL*93
02600 ******************************************************************MBKCS102
02601                                                                   MBKCS102
02602      IF MB102-LIST-ITEM-NUMBER(MB102-BLK-SUB                         CL210
02603                                MB102-ITEM-SUB) NOT = +0              CL209
02604                                                                   MBKCS102
02605          IF DP020-FK-DESELECT(DP020-SRC-AID)                         CL*32
02606                                                                      CL*32
02607              IF NOT MB102-NO-SELECT(MB102-BLK-SUB                    CL210
02608                                     MB102-ITEM-SUB)                  CL209
02609                  SET MB102-NO-SELECT(MB102-BLK-SUB                   CL210
02610                                      MB102-ITEM-SUB)   TO TRUE       CL209
02611                                                                   MBKCS102
02612                  SET MB102-BLOCK-MODIFIED(MB102-BLK-SUB)             CL210
02613                                                        TO TRUE       CL*32
02614              END-IF                                                  CL*32
02615          ELSE                                                        CL*32
02616              IF NOT MB102-SELECT(MB102-BLK-SUB MB102-ITEM-SUB)       CL210
02617                  SET MB102-SELECT(MB102-BLK-SUB                      CL210
02618                                   MB102-ITEM-SUB)      TO TRUE       CL209
02619                  ADD +1 TO MB102-NUMBER-OF-ITEMS-SELECTED            CL209
02620                  SET MB102-BLOCK-MODIFIED(MB102-BLK-SUB)             CL210
02621                                                        TO TRUE       CL*32
02622              END-IF                                                  CL*32
02623                                                                      CL*32
02624          END-IF                                                      CL*32
02625                                                                   MBKCS102
02626      END-IF.                                                      MBKCS102
02627  EJECT                                                            MBKCS102
02628  G100-DISPLAY-PANEL.                                              MBKCS102
02629                                                                   MBKCS102
02630 ******************************************************************MBKCS102
02631 *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.       MBKCS102
02632 *                                                                 MBKCS102
02633 *    PERFORMED BY B100-CONTROL-PROCESSING.                           CL203
02634 *    PERFORMED BY C300-PROCESS-PANEL.                                CL203
02635 *    PERFORMED BY D100-SETUP-INIT-PANEL.                             CL203
02636 ******************************************************************MBKCS102
02637                                                                   MBKCS102
02638      PERFORM DP017-0000-GET-CURR-DATE-TIME.                       MBKCS102
02639      MOVE DP017-EDITED-TIME-HMS                                      CL*31
02640                                TO  ATIMEO.                           CL*31
02641                                                                   MBKCS102
02642      IF PS-SEND-MAP-N-DATA-FRSET                                  MBKCS102
02643          PERFORM DP002-0000-FORMAT-TITLE-LINES                    MBKCS102
02644      END-IF.                                                      MBKCS102
02645                                                                   MBKCS102
02646      PERFORM H200-FORMAT-PANEL-BODY.                              MBKCS102
02647                                                                   MBKCS102
02648      PERFORM DP005-0000-FORMAT-MSG-LINES.                         MBKCS102
02649                                                                   MBKCS102
02650      IF PS-SEND-MAP-N-DATA-FRSET                                  MBKCS102
02651          PERFORM DP009-0000-LOAD-FK-LINES                         MBKCS102
02652      END-IF.                                                      MBKCS102
02653                                                                   MBKCS102
02654      PERFORM DP008-0000-WRITE-PANEL.                              MBKCS102
02655  EJECT                                                            MBKCS102
02656  G200-MARK-SELECTED-INDS.                                         MBKCS102
02657                                                                   MBKCS102
02658 ******************************************************************MBKCS102
02659 *    MARK EACH ITEM THAT FALLS WITHIN THE BEGINNING AND ENDING    MBKCS102
02660 *    RANGE AS BEING SELECTED.                                     MBKCS102
02661 *                                                                 MBKCS102
02662 *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED.                       CL*92
02663 ******************************************************************MBKCS102
02664                                                                   MBKCS102
02665      IF (MB102-LIST-ITEM-NUMBER(MB102-BLK-SUB                        CL210
02666                                 MB102-ITEM-SUB) >                    CL209
02667          MB102-START-OF-SELECTED-RANGE) OR                           CL209
02668                                                                      CL**4
02669         (MB102-LIST-ITEM-NUMBER(MB102-BLK-SUB                        CL210
02670                                 MB102-ITEM-SUB) =                    CL209
02671          MB102-START-OF-SELECTED-RANGE)                              CL209
02672                                                                   MBKCS102
02673          IF (MB102-LIST-ITEM-NUMBER(MB102-BLK-SUB                    CL210
02674                                     MB102-ITEM-SUB) <                CL209
02675              MB102-END-OF-SELECTED-RANGE) OR                         CL209
02676                                                                      CL**4
02677             (MB102-LIST-ITEM-NUMBER(MB102-BLK-SUB                    CL210
02678                                     MB102-ITEM-SUB) =                CL209
02679              MB102-END-OF-SELECTED-RANGE)                            CL209
02680                                                                   MBKCS102
02681              IF NOT MB102-SELECT(MB102-BLK-SUB MB102-ITEM-SUB)       CL210
02682                                                                      CL**2
02683                  SET MB102-SELECT(MB102-BLK-SUB                      CL210
02684                                   MB102-ITEM-SUB)          TO TRUE   CL209
02685                                                                      CL**2
02686                  SET MB102-BLOCK-MODIFIED(MB102-BLK-SUB) TO TRUE     CL210
02687                                                                   MBKCS102
02688                  ADD +1 TO MB102-NUMBER-OF-ITEMS-SELECTED            CL209
02689              END-IF                                               MBKCS102
02690                                                                   MBKCS102
02691          END-IF                                                   MBKCS102
02692                                                                   MBKCS102
02693      END-IF.                                                      MBKCS102
02694  EJECT                                                            MBKCS102
02695  G300-DETERMINE-WHAT-IS-WHERE.                                    MBKCS102
02696                                                                   MBKCS102
02697 ******************************************************************MBKCS102
02698 *    DETERMINE WHETHER THE TOP ITEM AND BOTTOM ITEM CAN BE FOUND  MBKCS102
02699 *    IN THE ARRAY, IN TEMPORARY STORAGE, OR NEITHER.              MBKCS102
02700 *                                                                 MBKCS102
02701 *    PERFORMED BY F500-BROWSE.                                       CL*93
02702 ******************************************************************MBKCS102
02703                                                                   MBKCS102
02704 ******************************************************************   CL**2
02705 *    CALCULATE THE FIRST-ITEM-IN-ARRAY (WORKING   STORAGE)           CL**2
02706 *                  LAST-ITEM-IN-ARRAY  (WORKING   STORAGE)           CL**2
02707 *                  LAST-ITEM-IN-TEMP   (TEMPORARY STORAGE)           CL**2
02708 *                                                                    CL**2
02709 *    BY COMPARING THE TOP AND BOTTOM ITEMS TO THESE, WE CAN          CL**2
02710 *    FIGURE OUT WHERE THE ITEMS ARE STORED.                          CL**2
02711 ******************************************************************   CL**2
02712                                                                      CL**2
02713      MOVE MB102-LIST-ITEM-NUMBER(1 1) TO PV-FIRST-ITEM-IN-ARRAY.     CL209
02714                                                                      CL**2
02715                                                                   MBKCS102
02716      COMPUTE PV-LAST-ITEM-IN-ARRAY =                              MBKCS102
02717          (PV-FIRST-ITEM-IN-ARRAY +                                MBKCS102
02718          (PC-MAX-BLOCKS-IN-ARRAY * PC-ITEMS-PER-BLOCK)) - 1.      MBKCS102
02719                                                                   MBKCS102
02720      IF PV-LAST-ITEM-IN-ARRAY > MB102-NUMBER-OF-ITEMS-READ           CL209
02721          MOVE MB102-NUMBER-OF-ITEMS-READ TO PV-LAST-ITEM-IN-ARRAY    CL209
02722      END-IF.                                                      MBKCS102
02723                                                                   MBKCS102
02724      COMPUTE PV-LAST-ITEM-IN-TEMP =                               MBKCS102
02725          MB102-HIGHEST-BLOCK-WRITTEN * PC-ITEMS-PER-BLOCK.           CL209
02726                                                                   MBKCS102
02727      IF PV-LAST-ITEM-IN-TEMP > MB102-NUMBER-OF-ITEMS-READ            CL209
02728          MOVE MB102-NUMBER-OF-ITEMS-READ TO PV-LAST-ITEM-IN-TEMP     CL209
02729      END-IF.                                                      MBKCS102
02730                                                                   MBKCS102
02731      IF (PV-TOP-ITEM > PV-FIRST-ITEM-IN-ARRAY) OR                 MBKCS102
02732         (PV-TOP-ITEM = PV-FIRST-ITEM-IN-ARRAY)                    MBKCS102
02733                                                                   MBKCS102
02734          IF (PV-TOP-ITEM < PV-LAST-ITEM-IN-ARRAY) OR              MBKCS102
02735             (PV-TOP-ITEM = PV-LAST-ITEM-IN-ARRAY)                 MBKCS102
02736              SET PS-TOP-ITEM-IN-ARRAY     TO TRUE                 MBKCS102
02737          ELSE                                                     MBKCS102
02738              SET PS-TOP-ITEM-NOT-IN-ARRAY TO TRUE                 MBKCS102
02739          END-IF                                                   MBKCS102
02740                                                                   MBKCS102
02741      ELSE                                                         MBKCS102
02742          SET PS-TOP-ITEM-NOT-IN-ARRAY TO TRUE                     MBKCS102
02743      END-IF.                                                      MBKCS102
02744                                                                   MBKCS102
02745      IF (PV-BOTTOM-ITEM > PV-FIRST-ITEM-IN-ARRAY) OR              MBKCS102
02746         (PV-BOTTOM-ITEM = PV-FIRST-ITEM-IN-ARRAY)                 MBKCS102
02747                                                                   MBKCS102
02748          IF (PV-BOTTOM-ITEM < PV-LAST-ITEM-IN-ARRAY) OR           MBKCS102
02749             (PV-BOTTOM-ITEM = PV-LAST-ITEM-IN-ARRAY)              MBKCS102
02750              SET PS-BOTTOM-ITEM-IN-ARRAY     TO TRUE              MBKCS102
02751          ELSE                                                     MBKCS102
02752              SET PS-BOTTOM-ITEM-NOT-IN-ARRAY TO TRUE              MBKCS102
02753          END-IF                                                   MBKCS102
02754                                                                   MBKCS102
02755      ELSE                                                         MBKCS102
02756          SET PS-BOTTOM-ITEM-NOT-IN-ARRAY TO TRUE                  MBKCS102
02757      END-IF.                                                      MBKCS102
02758                                                                   MBKCS102
02759      IF (PV-TOP-ITEM < PV-LAST-ITEM-IN-TEMP) OR                   MBKCS102
02760         (PV-TOP-ITEM = PV-LAST-ITEM-IN-TEMP)                      MBKCS102
02761          SET PS-TOP-ITEM-IN-TEMP     TO TRUE                      MBKCS102
02762      ELSE                                                         MBKCS102
02763          SET PS-TOP-ITEM-NOT-IN-TEMP TO TRUE                      MBKCS102
02764      END-IF.                                                      MBKCS102
02765                                                                   MBKCS102
02766      IF (PV-BOTTOM-ITEM < PV-LAST-ITEM-IN-TEMP) OR                MBKCS102
02767         (PV-BOTTOM-ITEM = PV-LAST-ITEM-IN-TEMP)                   MBKCS102
02768          SET PS-BOTTOM-ITEM-IN-TEMP     TO TRUE                   MBKCS102
02769      ELSE                                                         MBKCS102
02770          SET PS-BOTTOM-ITEM-NOT-IN-TEMP TO TRUE                   MBKCS102
02771      END-IF.                                                      MBKCS102
02772                                                                   MBKCS102
02773      IF PV-LAST-ITEM-IN-TEMP > PV-LAST-ITEM-IN-ARRAY              MBKCS102
02774          SET PS-LAST-BLOCK-NOT-IN-ARRAY TO TRUE                   MBKCS102
02775      ELSE                                                         MBKCS102
02776          SET PS-LAST-BLOCK-IN-ARRAY     TO TRUE                   MBKCS102
02777      END-IF.                                                      MBKCS102
02778  EJECT                                                            MBKCS102
02779  G400-READ-ITEMS-INTO-ARRAY.                                      MBKCS102
02780                                                                   MBKCS102
02781 ******************************************************************MBKCS102
02782 *    DETERMINE THE SPOT IN THE ARRAY WHERE THE LAST ITEM FROM     MBKCS102
02783 *    THE DATA BASE WAS ADDED SO WE KNOW WHERE TO ADD THE NEW      MBKCS102
02784 *    ITEMS.                                                       MBKCS102
02785 *                                                                 MBKCS102
02786 *    PERFORMED BY F500-BROWSE.                                       CL*93
02787 ******************************************************************MBKCS102
02788                                                                   MBKCS102
02789      COMPUTE PS-ITEM-SUB =                                        MBKCS102
02790          (MB102-NUMBER-OF-ITEMS-READ -                               CL209
02791           MB102-LIST-ITEM-NUMBER(1 1)) + 1.                          CL209
02792                                                                   MBKCS102
02793      MOVE PS-ITEM-SUB TO MB102-ITEM-SUB.                             CL209
02794                                                                   MBKCS102
02795      IF MB102-ITEM-SUB > PC-ITEMS-PER-BLOCK                          CL209
02796          SUBTRACT PC-ITEMS-PER-BLOCK FROM MB102-ITEM-SUB             CL209
02797                                                                   MBKCS102
02798          MOVE PC-MAX-BLOCKS-IN-ARRAY TO MB102-BLK-SUB                CL210
02799      ELSE                                                         MBKCS102
02800          MOVE +1                     TO MB102-BLK-SUB                CL210
02801      END-IF.                                                      MBKCS102
02802                                                                   MBKCS102
02803      MOVE PV-BOTTOM-ITEM TO PV-HIGHEST-ITEM-TO-BE-READ.           MBKCS102
02804                                                                      CL164
02805      PERFORM D500-OPEN-CURSOR.                                       CL164
02806                                                                      CL164
02807      PERFORM H100-READ-ITEMS-FROM-DB.                             MBKCS102
02808                                                                   MBKCS102
02809      IF MB102-NO-ITEMS-LEFT-ON-DB                                    CL209
02810                                                                   MBKCS102
02811          IF PV-TOP-ITEM > MB102-NUMBER-OF-ITEMS-READ                 CL209
02812              MOVE MB102-NUMBER-OF-ITEMS-READ TO PV-TOP-ITEM          CL209
02813          END-IF                                                   MBKCS102
02814                                                                   MBKCS102
02815          MOVE PC-TSYMSG-00070        TO DP020-MSG-NUMBER             CL179
02816          SET DP020-MSG-INFORMATIONAL TO TRUE                         CL*29
02817          SET PS-INFORMATIONAL        TO TRUE                         CL*29
02818      END-IF.                                                      MBKCS102
02819  EJECT                                                            MBKCS102
02820  G500-SCROLL-FORWARD.                                             MBKCS102
02821                                                                   MBKCS102
02822 ******************************************************************MBKCS102
02823 *    IF THE TOP ITEM WE NEED IS IN THE ARRAY, BUT THE BOTTOM         CL**2
02824 *    ITEM WE NEED IS IN TEMPORARY STORAGE OR HAS NOT BEEN READ       CL**2
02825 *    YET, WRITE OUT THE 1ST BLOCK IN THE ARRAY TO TEMPORARY          CL**2
02826 *    STORAGE.  MOVE THE 2ND BLOCK IN THE ARRAY TO THE 1ST BLOCK      CL**2
02827 *    AND READ THE BLOCK NEEDED INTO THE 2ND BLOCK OF THE ARRAY.      CL**2
02828 *    THE TOP ITEM WILL BE LOCATED IN THE 1ST BLOCK, AND THE          CL**2
02829 *    BOTTOM ITEM WILL EITHER BE LOCATED IN THE 2ND BLOCK OR          CL**2
02830 *    WILL BE PLACED IN THE 2ND BLOCK.                                CL**2
02831 *                                                                 MBKCS102
02832 *    PERFORMED BY F500-BROWSE.                                       CL*93
02833 ******************************************************************MBKCS102
02834                                                                   MBKCS102
02835      MOVE +1 TO MB102-BLK-SUB.                                       CL210
02836                                                                   MBKCS102
02837      PERFORM Z150-WRITE-TEMP-STORAGE.                             MBKCS102
02838                                                                   MBKCS102
02839      MOVE MB102-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY) TO             CL209
02840           MB102-BLOCK-ENTRIES(MB102-BLK-SUB).                        CL210
02841                                                                   MBKCS102
02842      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS102
02843          1 + ((PV-BOTTOM-ITEM - 1) / PC-ITEMS-PER-BLOCK).         MBKCS102
02844                                                                   MBKCS102
02845      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS102
02846                                                                   MBKCS102
02847      ADD +1 TO MB102-BLK-SUB.                                        CL210
02848                                                                   MBKCS102
02849      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS102
02850  EJECT                                                            MBKCS102
02851  G600-SCROLL-BACKWARD.                                            MBKCS102
02852                                                                   MBKCS102
02853 ******************************************************************MBKCS102
02854 *    IF TOP ITEM WE NEED IS NOT IN THE ARRAY, BUT THE BOTTOM      MBKCS102
02855 *    ITEM IS, WRITE BOTH BLOCKS TO TEMPORARY STORAGE.  MOVE       MBKCS102
02856 *    THE 1ST BLOCK IN THE ARRAY TO THE 2ND BLOCK OF THE ARRAY.    MBKCS102
02857 *    READ THE BLOCK THAT CONTAINS THE TOP ITEM INTO THE 1ST BLOCK MBKCS102
02858 *    OF THE ARRAY.                                                MBKCS102
02859 *                                                                 MBKCS102
02860 *    PERFORMED BY F500-BROWSE.                                       CL*93
02861 ******************************************************************MBKCS102
02862                                                                   MBKCS102
02863      PERFORM H400-WRITE-OUT-LAST-BLOCKS.                             CL*93
02864                                                                   MBKCS102
02865      MOVE +1                                   TO                 MBKCS102
02866           MB102-BLK-SUB.                                             CL210
02867                                                                   MBKCS102
02868      MOVE MB102-BLOCK-ENTRIES(MB102-BLK-SUB) TO                      CL210
02869           MB102-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY).               CL209
02870                                                                   MBKCS102
02871      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS102
02872          1 + ((PV-TOP-ITEM - 1) / PC-ITEMS-PER-BLOCK).            MBKCS102
02873                                                                   MBKCS102
02874      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS102
02875                                                                   MBKCS102
02876      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS102
02877  EJECT                                                            MBKCS102
02878  G700-GET-TOP-ITEMS-BLOCK.                                        MBKCS102
02879                                                                   MBKCS102
02880 ******************************************************************MBKCS102
02881 *    IF BOTH THE TOP AND BOTTOM ITEMS WE NEED ARE NOT IN THE      MBKCS102
02882 *    ARRAY, BUT THE TOP ITEM IS IN TEMPORARY STORAGE, WRITE BOTH  MBKCS102
02883 *    BLOCKS IN THE ARRAY TO TEMPORARY STORAGE.  READ THE BLOCK    MBKCS102
02884 *    THAT CONTAINS THE TOP ITEM INTO THE 1ST BLOCK OF THE ARRAY.  MBKCS102
02885 *    IF THERE IS ANOTHER BLOCK IN TEMPORARY STORAGE FOLLOWING     MBKCS102
02886 *    THE ONE JUST READ, READ THAT ONE INTO THE 2ND BLOCK OF THE   MBKCS102
02887 *    ARRAY, OTHERWISE INITIALIZE THE 2ND BLOCK OF THE ARRAY.      MBKCS102
02888 *                                                                 MBKCS102
02889 *    PERFORMED BY F500-BROWSE.                                       CL*93
02890 ******************************************************************MBKCS102
02891                                                                   MBKCS102
02892      PERFORM H400-WRITE-OUT-LAST-BLOCKS.                             CL*93
02893                                                                   MBKCS102
02894      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS102
02895          1 + ((PV-TOP-ITEM - 1) / PC-ITEMS-PER-BLOCK).            MBKCS102
02896                                                                   MBKCS102
02897      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS102
02898      MOVE +1                   TO MB102-BLK-SUB.                     CL210
02899                                                                   MBKCS102
02900      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS102
02901                                                                   MBKCS102
02902      ADD +1 TO PV-BLOCK-NUMBER.                                   MBKCS102
02903      ADD +1 TO MB102-BLK-SUB.                                        CL210
02904                                                                   MBKCS102
02905      IF (PV-BLOCK-NUMBER < MB102-HIGHEST-BLOCK-WRITTEN) OR           CL209
02906         (PV-BLOCK-NUMBER = MB102-HIGHEST-BLOCK-WRITTEN)              CL209
02907          PERFORM Z100-READ-TEMP-STORAGE                           MBKCS102
02908      ELSE                                                         MBKCS102
02909          PERFORM I100-INITIALIZE-BLOCK                            MBKCS102
02910      END-IF.                                                      MBKCS102
02911  EJECT                                                            MBKCS102
02912  G800-GET-LAST-BLOCK-IN-TEMP.                                     MBKCS102
02913                                                                   MBKCS102
02914 ******************************************************************MBKCS102
02915 *    IF BOTH THE TOP AND BOTTOM ITEMS WE NEED ARE NOT IN THE      MBKCS102
02916 *    ARRAY AND BOTH ITEMS ARE NOT IN TEMPORARY STORAGE (THE       MBKCS102
02917 *    ITEMS HAVE NOT BEEN READ FROM THE DATA BASE YET), AND THE    MBKCS102
02918 *    LAST ITEM READ FROM THE DATA BASE IS IN TEMPORARY STORAGE,   MBKCS102
02919 *    WRITE BOTH BLOCKS IN THE ARRAY TO TEMPORARY STORAGE.  READ   MBKCS102
02920 *    THE HIGHEST BLOCK IN TEMPORARY STORAGE INTO THE 1ST BLOCK    MBKCS102
02921 *    OF THE ARRAY, AND INITIALIZE THE 2ND BLOCK IN THE ARRAY.     MBKCS102
02922 *    THE 2ND BLOCK WILL BE USED FOR THE NEW ITEMS TO BE READ.     MBKCS102
02923 *    THE 1ST BLOCK WILL NOW CONTAIN THE ITEMS THAT WERE LAST      MBKCS102
02924 *    READ FROM THE DATA BASE.                                     MBKCS102
02925 *                                                                 MBKCS102
02926 *    PERFORMED BY F500-BROWSE.                                       CL*93
02927 ******************************************************************MBKCS102
02928                                                                   MBKCS102
02929      PERFORM H400-WRITE-OUT-LAST-BLOCKS.                             CL*93
02930                                                                   MBKCS102
02931      MOVE MB102-HIGHEST-BLOCK-WRITTEN TO PV-BLOCK-NUMBER.            CL209
02932      MOVE +1                          TO MB102-BLK-SUB.              CL210
02933                                                                   MBKCS102
02934      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS102
02935                                                                   MBKCS102
02936      ADD +1 TO MB102-BLK-SUB.                                        CL210
02937                                                                   MBKCS102
02938      PERFORM I100-INITIALIZE-BLOCK.                               MBKCS102
02939  EJECT                                                            MBKCS102
02940  H100-READ-ITEMS-FROM-DB.                                         MBKCS102
02941                                                                   MBKCS102
02942 ******************************************************************MBKCS102
02943 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE     MBKCS102
02944 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,      MBKCS102
02945 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND   MBKCS102
02946 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE      MBKCS102
02947 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                    MBKCS102
02948 *                                                                 MBKCS102
02949 *    PERFORMED BY D100-SETUP-INIT-PANEL.                             CL203
02950 ******************************************************************MBKCS102
02951                                                                   MBKCS102
02952      PERFORM                                                      MBKCS102
02953          UNTIL (MB102-NO-ITEMS-LEFT-ON-DB)   OR                      CL209
02954                (MB102-NUMBER-OF-ITEMS-READ =                         CL209
02955                 PV-HIGHEST-ITEM-TO-BE-READ)                          CL102
02956                                                                   MBKCS102
02957          IF MB102-ITEM-SUB < PC-ITEMS-PER-BLOCK                      CL209
02958              ADD +1 TO MB102-ITEM-SUB                                CL209
02959          ELSE                                                     MBKCS102
02960              ADD +1 TO MB102-BLK-SUB                                 CL210
02961                                                                   MBKCS102
02962              MOVE +1 TO MB102-ITEM-SUB                               CL209
02963                                                                   MBKCS102
02964              IF MB102-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY               CL210
02965                  MOVE +1 TO MB102-BLK-SUB                            CL210
02966                                                                   MBKCS102
02967                  PERFORM Z150-WRITE-TEMP-STORAGE                  MBKCS102
02968                                                                   MBKCS102
02969                  MOVE MB102-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY)    CL209
02970                  TO   MB102-BLOCK-ENTRIES(MB102-BLK-SUB)             CL210
02971                                                                   MBKCS102
02972                  ADD +1 TO MB102-BLK-SUB                             CL210
02973                                                                   MBKCS102
02974                  PERFORM I100-INITIALIZE-BLOCK                    MBKCS102
02975              END-IF                                               MBKCS102
02976                                                                   MBKCS102
02977          END-IF                                                   MBKCS102
02978                                                                   MBKCS102
02979          PERFORM H300-READ-ITEMS-FROM-DATABASE                       CL*41
02980                                                                      CL228
02981 ******************************                                       CL227
02982 ******************************                                       CL227
02983 * IN THE EVENT THAT A USER IS READING ALL HISTORY BUT HAS            CL227
02984 * SPECIFIED SOME OF THE UPPER LEVELS (I.E. DMA ) IN THE SELECTION    CL229
02985 * SCREEN, DB2 WILL READ ALL THE ROWS ASSOCIATED WITH THE             CL227
02986 * LEVEL THE USER IS REQUESTING. IN THIS CASE ROWS WOULD BE RETURNE   CL229
02987 * FOR 2 SEPARATE DMA'S.                                              CL228
02988 * THIS WOULD CAUSE AN ERROR CONDITION BECAUSE ONE OF THE DMA'S       CL229
02989 * WOULD NOT MATCH WHAT WAS REQUESTED ON THE SCREEN                   CL229
02990 * SO IF ROWS ARE BEING RETURNED AND PUT INTO THE TABLE WE CAN        CL229
02991 * ASSUME THAT THIS CONDITION HAS OCCURED AND WIPE OUT THE ERROR      CL229
02992 * FLAGS AND MESSAGE ( AT THIS TIME ERROR TSYMSG-00050 IS THE ONLY    CL229
02993 * ERROR BEING RETURNED AT THIS LEVEL)                                CL229
02994 ******************************                                       CL227
02995 ******************************                                       CL227
02996          IF (MB102-NUMBER-OF-ITEMS-READ > 0                          CL*39
02997          AND DP020-MSG-NUMBER NUMERIC)                               CL*40
02998              IF  DP020-MSG-NUMBER = PC-TSYMSG-00050                  CL*40
02999                  SET PS-NO-ERROR TO TRUE                             CL*39
03000                  INITIALIZE DP020-MSG-NUMBER                         CL*39
03001              END-IF                                                  CL*39
03002          END-IF                                                      CL*39
03003                                                                   MBKCS102
03004          IF MB102-ITEMS-LEFT-ON-DB                                   CL209
03005              ADD +1 TO MB102-NUMBER-OF-ITEMS-READ                    CL209
03006                                                                   MBKCS102
03007              MOVE MB102-NUMBER-OF-ITEMS-READ TO                      CL209
03008                   MB102-LIST-ITEM-NUMBER(MB102-BLK-SUB               CL210
03009                                          MB102-ITEM-SUB)             CL209
03010                                                                   MBKCS102
03011              MOVE MDSEAR-GMA-NBR             TO                      CL*47
03012                   MB102-ITEM-GMA(MB102-BLK-SUB                       CL210
03013                                  MB102-ITEM-SUB)                     CL209
03014              MOVE MDSEAR-DMA-NBR             TO                      CL*47
03015                   MB102-ITEM-DMA(MB102-BLK-SUB                       CL210
03016                                  MB102-ITEM-SUB)                     CL209
03017              MOVE MDSEAR-DEPT-NBR            TO                      CL*47
03018                   MB102-ITEM-DEPT(MB102-BLK-SUB                      CL210
03019                                   MB102-ITEM-SUB)                    CL209
03020              MOVE MDSEAR-MAJ-CL-NBR          TO                      CL*47
03021                   MB102-ITEM-MCLASS(MB102-BLK-SUB                    CL210
03022                                     MB102-ITEM-SUB)                  CL209
03023              MOVE MDSEAR-SUB-CL-NBR          TO                      CL*47
03024                   MB102-ITEM-SCLASS(MB102-BLK-SUB                    CL210
03025                                     MB102-ITEM-SUB)                  CL209
03026              MOVE MDSEAR-MDSELV-CDE          TO                      CL*47
03027                   MB102-ITEM-LEVEL(MB102-BLK-SUB                     CL210
03028                                    MB102-ITEM-SUB)                   CL209
03029              MOVE MDSEAR-STRT-DTE            TO                      CL*47
03030                   PV-DB2-DATE                                        CL*66
03031              MOVE PV-DB2-YEAR                TO                      CL*66
03032                   PV-PANEL-YEAR                                      CL*66
03033              MOVE PV-DB2-MONTH               TO                      CL*66
03034                   PV-PANEL-MONTH                                     CL*66
03035              MOVE PV-DB2-DAY                 TO                      CL*66
03036                   PV-PANEL-DAY                                       CL*66
03037              MOVE PV-PANEL-DATE              TO                      CL*66
03038                   MB102-ITEM-STR-DATE(MB102-BLK-SUB                  CL210
03039                                       MB102-ITEM-SUB)                CL209
03040 *                                                                    CL*29
03041              IF  MDSEAR-END-DTE EQUAL PC-NO-DISPLAY-DATE             CL*29
03042                  MOVE SPACES                 TO                      CL*29
03043                       MB102-ITEM-END-DATE(MB102-BLK-SUB              CL*29
03044                                           MB102-ITEM-SUB)            CL*29
03045              ELSE                                                    CL*29
03046                  MOVE MDSEAR-END-DTE         TO                      CL*29
03047                       PV-DB2-DATE                                    CL*29
03048                  MOVE PV-DB2-YEAR            TO                      CL*29
03049                       PV-PANEL-YEAR                                  CL*29
03050                  MOVE PV-DB2-MONTH           TO                      CL*29
03051                       PV-PANEL-MONTH                                 CL*29
03052                  MOVE PV-DB2-DAY             TO                      CL*29
03053                       PV-PANEL-DAY                                   CL*29
03054                  MOVE PV-PANEL-DATE          TO                      CL*29
03055                       MB102-ITEM-END-DATE(MB102-BLK-SUB              CL*29
03056                                           MB102-ITEM-SUB)            CL*29
03057              END-IF                                                  CL*29
03058 *                                                                    CL*29
03059              MOVE MDSEAR-DESC                TO                      CL*48
03060                   MB102-ITEM-DESC(MB102-BLK-SUB                      CL210
03061                                   MB102-ITEM-SUB)                    CL209
03062              IF  MDSEAR-END-DTE LESS THAN MB102-DB2-CURRENT-DATE     CL**4
03063                  MOVE DP015-TURQUOISE        TO                      CL255
03064                  MB102-COLOR(MB102-BLK-SUB MB102-ITEM-SUB)           CL255
03065              ELSE                                                    CL255
03066                  MOVE DP015-BLUE             TO                      CL255
03067                  MB102-COLOR(MB102-BLK-SUB MB102-ITEM-SUB)           CL255
03068              END-IF                                                  CL**2
03069                                                                      CL**2
03070              SET MB102-BLOCK-MODIFIED(MB102-BLK-SUB) TO TRUE         CL210
03071          END-IF                                                   MBKCS102
03072                                                                   MBKCS102
03073      END-PERFORM.                                                 MBKCS102
03074                                                                      CL*10
03075 ******************************************************************   CL*10
03076 *    ONCE WE ARE DONE FILLING UP THE BLOCKS, WE MUST PERFORM         CL*10
03077 *    1 MORE READ AGAINST THE DB TO SEE IF THERE IS ANOTHER           CL*10
03078 *    RECORD OUT THERE.  THIS READ ONLY ALLOWS US TO DETERMINE        CL*10
03079 *    IF WE JUST READ THE LAST ITEM OR IF THERE ARE MORE TO READ.     CL*10
03080 ******************************************************************   CL*10
03081                                                                      CL*10
03082      IF MB102-ITEMS-LEFT-ON-DB                                       CL209
03083          MOVE MDSEAR-GMA-NBR      TO MB102-SAVE-GMA-NBR              CL**9
03084          MOVE MDSEAR-DMA-NBR      TO MB102-SAVE-DMA-NBR              CL**9
03085          MOVE MDSEAR-DEPT-NBR     TO MB102-SAVE-DPT-NBR              CL**9
03086          MOVE MDSEAR-MAJ-CL-NBR   TO MB102-SAVE-MCL-NBR              CL**9
03087          MOVE MDSEAR-SUB-CL-NBR   TO MB102-SAVE-SCL-NBR              CL**9
03088          MOVE MDSEAR-LIST-SEQ-NBR TO MB102-SAVE-SEQ-NBR              CL**9
03089          SET PS-DB2-RETRY TO TRUE                                    CL**7
03090          PERFORM H300-READ-ITEMS-FROM-DATABASE                       CL110
03091              UNTIL PS-DB2-OK                                         CL110
03092                                                                      CL159
03093          IF MB102-NUMBER-OF-ITEMS-READ > +0                          CL209
03094              SET PS-SELECTIONS-FOUND    TO TRUE                      CL159
03095          END-IF                                                      CL159
03096                                                                      CL159
03097      END-IF.                                                         CL102
03098                                                                      CL102
03099      IF MB102-NO-ITEMS-LEFT-ON-DB                                    CL209
03100                                                                      CL112
03101          EVALUATE TRUE                                               CL112
03102                                                                      CL112
03103               WHEN MB102-OPEN-MERCH-CURSOR                           CL209
03104                    PERFORM Z220-CLOSE-MERCH-CURSOR                   CL112
03105                                                                      CL112
03106               WHEN MB102-OPEN-GMA-CURSOR                             CL209
03107                    PERFORM Z320-CLOSE-GMA-CURSOR                     CL112
03108                                                                      CL112
03109               WHEN MB102-OPEN-DMA-CURSOR                             CL209
03110                    PERFORM Z420-CLOSE-DMA-CURSOR                     CL127
03111                                                                      CL129
03112               WHEN MB102-OPEN-DPT-CURSOR                             CL209
03113                    PERFORM Z520-CLOSE-DPT-CURSOR                     CL129
03114                                                                      CL126
03115               WHEN MB102-OPEN-MCL-CURSOR                             CL209
03116                    PERFORM Z620-CLOSE-MCL-CURSOR                     CL148
03117                                                                      CL148
03118               WHEN MB102-OPEN-SCL-CURSOR                             CL209
03119                    PERFORM Z720-CLOSE-SCL-CURSOR                     CL149
03120                                                                      CL149
03121          END-EVALUATE                                                CL112
03122                                                                      CL112
03123          IF MB102-NUMBER-OF-ITEMS-READ = +0                          CL209
03124              SET DP020-MSG-FATAL TO TRUE                             CL217
03125              SET PS-NO-SELECTIONS-FOUND TO TRUE                      CL157
03126          ELSE                                                        CL158
03127              SET PS-SELECTIONS-FOUND    TO TRUE                      CL158
03128          END-IF                                                      CL157
03129      END-IF.                                                         CL157
03130  EJECT                                                            MBKCS102
03131  H200-FORMAT-PANEL-BODY.                                          MBKCS102
03132                                                                   MBKCS102
03133 ******************************************************************MBKCS102
03134 *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.            MBKCS102
03135 *                                                                 MBKCS102
03136 ******************************************************************MBKCS102
03137                                                                   MBKCS102
03138      PERFORM I700-FORMAT-FROM-TO-LINE.                            MBKCS102
03139      PERFORM I200-FORMAT-LIST-LINES.                              MBKCS102
03140      PERFORM I300-FORMAT-NR-SELECTED.                             MBKCS102
03141      PERFORM I800-SET-CURSOR-POSITION.                               CL**4
03142  EJECT                                                               CL*92
03143  H300-READ-ITEMS-FROM-DATABASE.                                      CL*41
03144                                                                      CL*41
03145 ******************************************************************   CL*41
03146 *    READ ITEMS FROM DATABASE.                                       CL*41
03147 *         PERFORMS THE READ BASES ON THE CURSOR THAT IS OPEN         CL165
03148 ******************************************************************   CL*41
03149                                                                      CL*41
03150      SET PS-DB2-RETRY TO TRUE.                                       CL*41
03151                                                                      CL*41
03152      SET PS-DO-NOT-HAVE-GMA-ITEM TO TRUE.                            CL181
03153      SET PS-DO-NOT-HAVE-DMA-ITEM TO TRUE.                            CL181
03154      SET PS-DO-NOT-HAVE-DPT-ITEM TO TRUE.                            CL129
03155      SET PS-DO-NOT-HAVE-MCL-ITEM TO TRUE.                            CL135
03156      SET PS-DO-NOT-HAVE-SCL-ITEM TO TRUE.                            CL152
03157                                                                      CL126
03158      EVALUATE TRUE                                                   CL100
03159                                                                      CL100
03160           WHEN MB102-OPEN-MERCH-CURSOR                               CL209
03161                PERFORM Z210-READ-MERCH-AREA                          CL100
03162                    UNTIL PS-DB2-OK                                   CL100
03163                                                                      CL*41
03164           WHEN MB102-OPEN-GMA-CURSOR                                 CL209
03165                PERFORM H305-PROCESS-GMA-CURSOR                       CL181
03166                    UNTIL PS-HAVE-GMA-ITEM                            CL181
03167                    OR MB102-NO-ITEMS-LEFT-ON-DB                      CL209
03168                                                                      CL100
03169           WHEN MB102-OPEN-DMA-CURSOR                                 CL209
03170                PERFORM H310-PROCESS-DMA-CURSOR                       CL122
03171                    UNTIL PS-HAVE-DMA-ITEM                            CL122
03172                    OR MB102-NO-ITEMS-LEFT-ON-DB                      CL209
03173                                                                      CL129
03174           WHEN MB102-OPEN-DPT-CURSOR                                 CL209
03175                PERFORM H320-PROCESS-DPT-CURSOR                       CL129
03176                    UNTIL PS-HAVE-DPT-ITEM                            CL129
03177                    OR MB102-NO-ITEMS-LEFT-ON-DB                      CL209
03178                                                                      CL119
03179           WHEN MB102-OPEN-MCL-CURSOR                                 CL209
03180                PERFORM H330-PROCESS-MCL-CURSOR                       CL148
03181                    UNTIL PS-HAVE-MCL-ITEM                            CL148
03182                    OR MB102-NO-ITEMS-LEFT-ON-DB                      CL209
03183                                                                      CL135
03184           WHEN MB102-OPEN-SCL-CURSOR                                 CL209
03185                PERFORM H340-PROCESS-SCL-CURSOR                       CL148
03186                    UNTIL PS-HAVE-SCL-ITEM                            CL148
03187                    OR MB102-NO-ITEMS-LEFT-ON-DB                      CL209
03188                                                                      CL148
03189      END-EVALUATE.                                                   CL100
03190  EJECT                                                               CL*41
03191  H305-PROCESS-GMA-CURSOR.                                            CL181
03192                                                                      CL165
03193 ******************************************************************   CL165
03194 *    READ ITEMS FROM DATABASE USING THE GMA CURSOR                   CL181
03195 *       CHECK THE GMA NUMBER AFTER THE FETCH TO SEE IF IT IS         CL165
03196 *       WITHIN THE REQUESTED RANGE                                   CL165
03197 ******************************************************************   CL165
03198                                                                      CL133
03199      SET PS-DB2-RETRY TO TRUE.                                       CL128
03200                                                                      CL133
03201      PERFORM Z310-READ-GMA-AREA                                      CL181
03202          UNTIL PS-DB2-OK                                             CL181
03203                                                                      CL122
03204      IF PS-DB2-OK                                                    CL124
03205          IF MB102-ITEMS-LEFT-ON-DB                                   CL209
03206                                                                      CL133
03207              EVALUATE TRUE                                           CL133
03208                                                                      CL133
03209                  WHEN MB102-ALL-GMAS                                 CL209
03210                       SET PS-HAVE-GMA-ITEM TO TRUE                   CL181
03211                                                                      CL133
03212                  WHEN MB102-GMA-NUMBER = MDSEAR-GMA-NBR              CL209
03213                       SET PS-HAVE-DMA-ITEM TO TRUE                   CL133
03214                                                                      CL133
03215              END-EVALUATE                                            CL133
03216                                                                      CL133
03217          ELSE                                                        CL125
03218              IF MB102-NUMBER-OF-ITEMS-READ = +0                      CL209
03219                  IF PS-NO-ERROR                                      CL180
03220                      MOVE PC-TSYMSG-00050 TO DP020-MSG-NUMBER        CL215
03221                      SET DP020-MSG-FATAL                             CL216
03222                          PS-ERROR         TO TRUE                    CL216
03223                  END-IF                                              CL180
03224              END-IF                                                  CL180
03225          END-IF                                                      CL122
03226      END-IF.                                                         CL122
03227  EJECT                                                               CL129
03228                                                                      CL220
03229  H310-PROCESS-DMA-CURSOR.                                            CL181
03230                                                                      CL181
03231 ******************************************************************   CL181
03232 *    READ ITEMS FROM DATABASE USING THE DMA CURSOR                   CL181
03233 *       CHECK THE GMA NUMBER AFTER THE FETCH TO SEE IF IT IS         CL181
03234 *       WITHIN THE REQUESTED RANGE                                   CL181
03235 ******************************************************************   CL181
03236                                                                      CL181
03237      SET PS-DB2-RETRY TO TRUE.                                       CL181
03238                                                                      CL181
03239      PERFORM Z410-READ-DMA-AREA                                      CL181
03240          UNTIL PS-DB2-OK.                                            CL181
03241                                                                      CL181
03242      IF PS-DB2-OK                                                    CL181
03243          IF MB102-ITEMS-LEFT-ON-DB                                   CL209
03244                                                                      CL181
03245              EVALUATE TRUE                                           CL181
03246                                                                      CL181
03247                  WHEN MB102-ALL-GMAS                                 CL209
03248                       SET PS-HAVE-DMA-ITEM TO TRUE                   CL181
03249                                                                      CL181
03250                  WHEN MB102-GMA-NUMBER = MDSEAR-GMA-NBR              CL209
03251                       SET PS-HAVE-DMA-ITEM TO TRUE                   CL181
03252                                                                      CL181
03253                  WHEN MB102-GMA-NUMBER NOT = MDSEAR-GMA-NBR          CL220
03254                       MOVE PC-TSYMSG-00050 TO DP020-MSG-NUMBER       CL220
03255                       SET DP020-MSG-FATAL                            CL220
03256                           PS-ERROR         TO TRUE                   CL220
03257                                                                      CL181
03258              END-EVALUATE                                            CL181
03259                                                                      CL181
03260          ELSE                                                        CL220
03261              IF MB102-NUMBER-OF-ITEMS-READ = +0                      CL220
03262                  IF PS-NO-ERROR                                      CL220
03263                      MOVE PC-TSYMSG-00050 TO DP020-MSG-NUMBER        CL220
03264                      SET PS-ERROR                                    CL220
03265                          DP020-MSG-FATAL  TO TRUE                    CL220
03266                  END-IF                                              CL220
03267              END-IF                                                  CL220
03268          END-IF                                                      CL181
03269      END-IF.                                                         CL181
03270  EJECT                                                               CL181
03271  H320-PROCESS-DPT-CURSOR.                                            CL131
03272                                                                      CL181
03273 ******************************************************************   CL181
03274 *    READ ITEMS FROM DATABASE USING THE DPT CURSOR                   CL181
03275 *       CHECK THE GMA AND DMA NUMBERS AFTER THE FETCH TO SEE         CL181
03276 *       IF IT IS WITHIN  THE REQUESTED RANGE                         CL182
03277 *                                                                    CL181
03278 ******************************************************************   CL181
03279                                                                      CL132
03280      SET PS-DB2-RETRY TO TRUE.                                       CL129
03281                                                                      CL132
03282      PERFORM Z510-READ-DPT-AREA                                      CL131
03283          UNTIL PS-DB2-OK.                                            CL129
03284                                                                      CL129
03285      IF PS-DB2-OK                                                    CL129
03286          IF MB102-ITEMS-LEFT-ON-DB                                   CL209
03287                                                                      CL132
03288              EVALUATE TRUE                                           CL132
03289                                                                      CL132
03290                  WHEN (MB102-ALL-GMAS AND                            CL209
03291                        MB102-ALL-DMAS)                               CL209
03292                       SET PS-HAVE-DPT-ITEM TO TRUE                   CL132
03293                                                                      CL145
03294                  WHEN (MB102-ALL-GMAS AND                            CL209
03295                        MB102-DMA-NUMBER = MDSEAR-DMA-NBR)            CL209
03296                       SET PS-HAVE-DPT-ITEM TO TRUE                   CL145
03297                                                                      CL132
03298                  WHEN (MB102-GMA-NUMBER = MDSEAR-GMA-NBR AND         CL209
03299                        MB102-ALL-DMAS)                               CL209
03300                       SET PS-HAVE-DPT-ITEM TO TRUE                   CL132
03301                                                                      CL132
03302                  WHEN (MB102-GMA-NUMBER = MDSEAR-GMA-NBR AND         CL209
03303                        MB102-DMA-NUMBER = MDSEAR-DMA-NBR)            CL209
03304                       SET PS-HAVE-DPT-ITEM TO TRUE                   CL132
03305                                                                      CL132
03306                  WHEN MB102-GMA-NUMBER NOT = MDSEAR-GMA-NBR          CL209
03307                       IF NOT MB102-ALL-GMAS                          CL209
03308                           SET  PS-ERROR                              CL221
03309                                DP020-MSG-FATAL TO TRUE               CL221
03310                           MOVE PC-TSYMSG-00050 TO DP020-MSG-NUMBER   CL221
03311                       END-IF                                         CL180
03312                                                                      CL180
03313                  WHEN MB102-DMA-NUMBER NOT = MDSEAR-DMA-NBR          CL209
03314                       IF NOT MB102-ALL-DMAS                          CL209
03315                           IF PS-NO-ERROR                             CL180
03316                               SET PS-ERROR TO TRUE                   CL180
03317                               SET DP020-MSG-FATAL TO TRUE            CL216
03318                               MOVE PC-TSYMSG-00050 TO                CL215
03319                                    DP020-MSG-NUMBER                  CL180
03320                           END-IF                                     CL180
03321                       END-IF                                         CL180
03322                                                                      CL180
03323              END-EVALUATE                                            CL132
03324                                                                      CL133
03325          ELSE                                                        CL133
03326              IF MB102-NUMBER-OF-ITEMS-READ = +0                      CL209
03327                  IF PS-NO-ERROR                                      CL180
03328                      MOVE PC-TSYMSG-00050 TO DP020-MSG-NUMBER        CL215
03329                      SET DP020-MSG-FATAL TO TRUE                     CL216
03330                      SET PS-ERROR         TO TRUE                    CL180
03331                  END-IF                                              CL180
03332              END-IF                                                  CL180
03333          END-IF                                                      CL132
03334      END-IF.                                                         CL132
03335  EJECT                                                               CL122
03336  H330-PROCESS-MCL-CURSOR.                                            CL148
03337                                                                      CL181
03338 ******************************************************************   CL181
03339 *    READ ITEMS FROM DATABASE USING THE MCL CURSOR                   CL181
03340 *       CHECK THE GMA AND DMA NUMBERS AFTER THE FETCH TO SEE         CL181
03341 *       IF IT IS WITHIN  THE REQUESTED RANGE                         CL182
03342 *                                                                    CL181
03343 ******************************************************************   CL181
03344                                                                      CL148
03345      SET PS-DB2-RETRY TO TRUE.                                       CL148
03346                                                                      CL148
03347      PERFORM Z610-READ-MCL-AREA                                      CL148
03348          UNTIL PS-DB2-OK.                                            CL148
03349                                                                      CL148
03350      IF PS-DB2-OK                                                    CL148
03351          IF MB102-ITEMS-LEFT-ON-DB                                   CL209
03352                                                                      CL148
03353              EVALUATE TRUE                                           CL148
03354                                                                      CL148
03355                  WHEN MB102-ALL-GMAS AND                             CL209
03356                       MB102-ALL-DMAS                                 CL209
03357                       SET PS-HAVE-MCL-ITEM TO TRUE                   CL148
03358                                                                      CL148
03359                  WHEN MB102-ALL-GMAS AND                             CL209
03360                       MB102-DMA-NUMBER = MDSEAR-DMA-NBR              CL209
03361                       SET PS-HAVE-MCL-ITEM TO TRUE                   CL148
03362                                                                      CL148
03363                  WHEN MB102-GMA-NUMBER = MDSEAR-GMA-NBR AND          CL209
03364                       MB102-ALL-DMAS                                 CL209
03365                       SET PS-HAVE-MCL-ITEM TO TRUE                   CL148
03366                                                                      CL148
03367                  WHEN MB102-GMA-NUMBER = MDSEAR-GMA-NBR AND          CL209
03368                       MB102-DMA-NUMBER = MDSEAR-DMA-NBR              CL209
03369                       SET PS-HAVE-MCL-ITEM TO TRUE                   CL148
03370                                                                      CL148
03371              END-EVALUATE                                            CL148
03372                                                                      CL148
03373          ELSE                                                        CL148
03374              IF MB102-NUMBER-OF-ITEMS-READ = +0                      CL209
03375                  IF PS-NO-ERROR                                      CL180
03376                      MOVE PC-TSYMSG-00050 TO DP020-MSG-NUMBER        CL215
03377                      SET PS-ERROR         TO TRUE                    CL180
03378                      SET DP020-MSG-FATAL  TO TRUE                    CL216
03379                  END-IF                                              CL180
03380              END-IF                                                  CL180
03381          END-IF                                                      CL148
03382      END-IF.                                                         CL148
03383  EJECT                                                               CL148
03384  H340-PROCESS-SCL-CURSOR.                                            CL149
03385                                                                      CL181
03386 ******************************************************************   CL181
03387 *    READ ITEMS FROM DATABASE USING THE SCL CURSOR                   CL181
03388 *       CHECK THE GMA AND DMA NUMBERS AFTER THE FETCH TO SEE         CL181
03389 *       IF IT IS WITHIN  THE REQUESTED RANGE                         CL182
03390 *                                                                    CL181
03391 ******************************************************************   CL181
03392                                                                      CL149
03393      SET PS-DB2-RETRY TO TRUE.                                       CL149
03394                                                                      CL149
03395      PERFORM Z710-READ-SCL-AREA                                      CL149
03396          UNTIL PS-DB2-OK.                                            CL149
03397                                                                      CL149
03398      IF PS-DB2-OK                                                    CL149
03399          IF MB102-ITEMS-LEFT-ON-DB                                   CL209
03400                                                                      CL149
03401              EVALUATE TRUE                                           CL149
03402                                                                      CL149
03403                  WHEN MB102-ALL-GMAS AND                             CL209
03404                       MB102-ALL-DMAS                                 CL209
03405                       SET PS-HAVE-SCL-ITEM TO TRUE                   CL149
03406                                                                      CL149
03407                  WHEN MB102-ALL-GMAS AND                             CL209
03408                       MB102-DMA-NUMBER = MDSEAR-DMA-NBR              CL209
03409                       SET PS-HAVE-SCL-ITEM TO TRUE                   CL149
03410                                                                      CL149
03411                  WHEN MB102-GMA-NUMBER = MDSEAR-GMA-NBR AND          CL209
03412                       MB102-ALL-DMAS                                 CL209
03413                       SET PS-HAVE-SCL-ITEM TO TRUE                   CL149
03414                                                                      CL149
03415                  WHEN MB102-GMA-NUMBER = MDSEAR-GMA-NBR AND          CL209
03416                       MB102-DMA-NUMBER = MDSEAR-DMA-NBR              CL209
03417                       SET PS-HAVE-SCL-ITEM TO TRUE                   CL149
03418                                                                      CL149
03419              END-EVALUATE                                            CL149
03420                                                                      CL149
03421          ELSE                                                        CL149
03422              IF MB102-NUMBER-OF-ITEMS-READ = +0                      CL209
03423                  IF PS-NO-ERROR                                      CL180
03424                      MOVE PC-TSYMSG-00050 TO DP020-MSG-NUMBER        CL215
03425                      SET PS-ERROR         TO TRUE                    CL180
03426                      SET DP020-MSG-FATAL  TO TRUE                    CL216
03427                  END-IF                                              CL180
03428              END-IF                                                  CL180
03429          END-IF                                                      CL149
03430      END-IF.                                                         CL149
03431  EJECT                                                               CL149
03432  H400-WRITE-OUT-LAST-BLOCKS.                                         CL*92
03433                                                                      CL*92
03434 ******************************************************************   CL*92
03435 *    WRITE OUT THE 2 BLOCKS CURRENTLY IN WORKING STORAGE TO          CL*92
03436 *    TEMPORARY STORAGE.                                              CL*92
03437 *                                                                    CL*92
03438 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
03439 ******************************************************************   CL*92
03440                                                                      CL*92
03441      MOVE +1 TO MB102-BLK-SUB.                                       CL210
03442                                                                      CL*92
03443      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL*92
03444                                                                      CL*92
03445      ADD +1  TO MB102-BLK-SUB.                                       CL210
03446                                                                      CL*92
03447      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL*92
03448                                                                      CL*92
03449  EJECT                                                               CL*92
03450  I100-INITIALIZE-BLOCK.                                           MBKCS102
03451                                                                   MBKCS102
03452 ******************************************************************MBKCS102
03453 *    INITIALIZE A BLOCK IN THE ARRAY.                             MBKCS102
03454 *    PERFORMED BY D100-SETUP-INIT-PANEL.                             CL203
03455 ******************************************************************MBKCS102
03456                                                                   MBKCS102
03457      PERFORM                                                      MBKCS102
03458          VARYING PS-SUB FROM 1 BY 1                               MBKCS102
03459          UNTIL PS-SUB > PC-ITEMS-PER-BLOCK                        MBKCS102
03460                                                                   MBKCS102
03461          MOVE +0 TO MB102-LIST-ITEM-NUMBER(MB102-BLK-SUB PS-SUB)     CL210
03462                                                                   MBKCS102
03463          MOVE SPACES TO MB102-ITEM-GMA(MB102-BLK-SUB PS-SUB)         CL210
03464          MOVE SPACES TO MB102-ITEM-DMA(MB102-BLK-SUB PS-SUB)         CL210
03465          MOVE SPACES TO MB102-ITEM-DEPT(MB102-BLK-SUB PS-SUB)        CL210
03466          MOVE SPACES TO MB102-ITEM-MCLASS(MB102-BLK-SUB PS-SUB)      CL210
03467          MOVE SPACES TO MB102-ITEM-SCLASS(MB102-BLK-SUB PS-SUB)      CL210
03468          MOVE SPACES TO MB102-ITEM-LEVEL(MB102-BLK-SUB PS-SUB)       CL210
03469          MOVE SPACES TO MB102-ITEM-DESC(MB102-BLK-SUB PS-SUB)        CL210
03470          MOVE SPACES TO                                              CL*29
03471               MB102-ITEM-STR-DATE(MB102-BLK-SUB PS-SUB)              CL210
03472          MOVE SPACES TO                                              CL*29
03473               MB102-ITEM-END-DATE(MB102-BLK-SUB PS-SUB)              CL210
03474          MOVE SPACES TO                                              CL*29
03475               MB102-ITEM-ACTION(MB102-BLK-SUB PS-SUB)                CL210
03476                                                                   MBKCS102
03477          SET MB102-NO-SELECT(MB102-BLK-SUB PS-SUB) TO TRUE           CL210
03478      END-PERFORM.                                                 MBKCS102
03479                                                                   MBKCS102
03480      SET MB102-BLOCK-NOT-MODIFIED(MB102-BLK-SUB) TO TRUE.            CL210
03481  EJECT                                                            MBKCS102
03482  I200-FORMAT-LIST-LINES.                                          MBKCS102
03483                                                                   MBKCS102
03484 ******************************************************************MBKCS102
03485 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.     MBKCS102
03486 *                                                                 MBKCS102
03487 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
03488 ******************************************************************MBKCS102
03489                                                                   MBKCS102
03490      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS102
03491          (MB102-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL209
03492                                                                   MBKCS102
03493      MOVE MB102-ITEM-AT-TOP-OF-PANEL TO PV-ITEM-IN-USE.              CL209
03494      MOVE +1                         TO PS-SUB.                   MBKCS102
03495                                                                   MBKCS102
03496      PERFORM J100-PROCESS-ITEMS                                   MBKCS102
03497          UNTIL PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL.          MBKCS102
03498  EJECT                                                            MBKCS102
03499  I300-FORMAT-NR-SELECTED.                                         MBKCS102
03500                                                                   MBKCS102
03501 ******************************************************************MBKCS102
03502 *    FORMAT THE FIELD WHICH WILL SHOW THE NUMBER OF ITEMS THAT    MBKCS102
03503 *    WERE SELECTED BY THE USER.                                   MBKCS102
03504 *                                                                 MBKCS102
03505 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
03506 ******************************************************************MBKCS102
03507                                                                   MBKCS102
03508      MOVE MB102-NUMBER-OF-ITEMS-SELECTED TO PV-DISPLAY-7.            CL209
03509      MOVE PV-DISPLAY-7                   TO ATOTSELO.                CL178
03510  EJECT                                                            MBKCS102
03511  I700-FORMAT-FROM-TO-LINE.                                        MBKCS102
03512                                                                   MBKCS102
03513 ******************************************************************MBKCS102
03514 *    FORMAT THE LINE THAT WILL SHOW THE TOTAL NUMBER OF ITEMS WE  MBKCS102
03515 *    HAVE ON OUR LIST AND SHOW THE NUMBERS ASSOCIATED WITH THE    MBKCS102
03516 *    FIRST AND LAST ITEMS ON CURRENT PAGE.  THIS WILL HELP THE    MBKCS102
03517 *    USER DETERMINE HOW FAR THEY ARE INTO THE LIST.               MBKCS102
03518 *                                                                 MBKCS102
03519 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
03520 ******************************************************************MBKCS102
03521                                                                   MBKCS102
03522 ******************************************************************   CL**3
03523 *    ON MAP      ITEMS XXXXXXX TO XXXXXXX OF XXXXXXX                 CL**3
03524 *                        BEG   TO   END   OF   TOT                   CL**3
03525 ******************************************************************   CL**3
03526                                                                      CL**3
03527      MOVE MB102-CO-NUMBER                TO MB004O-AOPERCOO.         CL209
03528                                                                      CL*68
03529      IF MB102-NUMBER-OF-ITEMS-READ > +0                              CL209
03530          MOVE MB102-ITEM-AT-TOP-OF-PANEL TO MB004O-ABEGITMO          CL209
03531      ELSE                                                         MBKCS102
03532          MOVE 0                          TO MB004O-ABEGITMO          CL*22
03533      END-IF.                                                      MBKCS102
03534                                                                   MBKCS102
03535      MOVE DP015-GREEN                    TO ABEGITMC.                CL*22
03536      MOVE DP015-UNDERLINE                TO ABEGITMH.                CL*56
03537      MOVE DP015-UNP-NUM-BRT-MDT          TO ABEGITMA.                CL*22
03538                                                                      CL*22
03539      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS102
03540          (MB102-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL209
03541                                                                   MBKCS102
03542      IF PV-ENDING-ITEM-ON-PANEL < MB102-NUMBER-OF-ITEMS-READ         CL209
03543          MOVE PV-ENDING-ITEM-ON-PANEL    TO MB004O-AENDITMO          CL*22
03544      ELSE                                                         MBKCS102
03545          MOVE MB102-NUMBER-OF-ITEMS-READ TO MB004O-AENDITMO          CL209
03546      END-IF.                                                      MBKCS102
03547                                                                   MBKCS102
03548      MOVE SPACES TO MB004O-AOFO.                                     CL*22
03549      MOVE ZEROES TO MB004O-ATOTITMO.                                 CL*22
03550                                                                   MBKCS102
03551      IF (MB102-NUMBER-OF-ITEMS-READ > +0) AND                        CL209
03552         (MB102-NO-ITEMS-LEFT-ON-DB)                                  CL209
03553          MOVE 'OF'                       TO MB004O-AOFO              CL*22
03554                                                                      CL**3
03555          MOVE MB102-NUMBER-OF-ITEMS-READ TO MB004O-ATOTITMO          CL209
03556      END-IF.                                                      MBKCS102
03557  EJECT                                                            MBKCS102
03558  I800-SET-CURSOR-POSITION.                                        MBKCS102
03559                                                                   MBKCS102
03560 ******************************************************************MBKCS102
03561 *    IF NO ITEMS WERE READ FROM THE DATA BASE, POINT THE CURSOR   MBKCS102
03562 *    TO THE COMMAND LINE, OTHERWISE POINT THE CURSOR TO ONE OF    MBKCS102
03563 *    THE SELECTION ENTRIES.                                       MBKCS102
03564 *                                                                 MBKCS102
03565 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
03566 ******************************************************************MBKCS102
03567                                                                   MBKCS102
03568      IF (PS-CURSOR-NOT-POSITIONED) OR                                CL157
03569         (PS-INFORMATIONAL)         OR                                CL157
03570         (PS-WARNING)                                                 CL157
03571          PERFORM J200-FIND-LAST-SELECTION                            CL157
03572      END-IF.                                                      MBKCS102
03573  EJECT                                                            MBKCS102
03574  J100-PROCESS-ITEMS.                                              MBKCS102
03575                                                                   MBKCS102
03576 ******************************************************************MBKCS102
03577 *    PROCESS EACH ITEM IN THE ARRAY AND MOVE IT TO THE MAP.       MBKCS102
03578 *    FILL THE MAP UNTIL WE HAVE REACHED THE MAXIMUM NUMBER OF     MBKCS102
03579 *    MAP ENTRIES.                                                 MBKCS102
03580 *                                                                 MBKCS102
03581 *    PERFORMED BY I200-FORMAT-LIST-LINES.                            CL*93
03582 ******************************************************************MBKCS102
03583                                                                   MBKCS102
03584      IF (PV-ITEM-IN-USE < MB102-NUMBER-OF-ITEMS-READ) OR             CL209
03585         (PV-ITEM-IN-USE = MB102-NUMBER-OF-ITEMS-READ)                CL209
03586          PERFORM K100-SET-SUBSCRIPTS                              MBKCS102
03587                                                                   MBKCS102
03588          MOVE DP015-UNP-ALP-BRT-MDT TO MB004I-ASELA(PS-SUB)          CL*22
03589                                                                   MBKCS102
03590          IF (MB102-SELECT(MB102-BLK-SUB MB102-ITEM-SUB))      OR     CL210
03591             (MB102-NO-SELECT(MB102-BLK-SUB MB102-ITEM-SUB))          CL210
03592              MOVE DP015-UNDERLINE TO MB004O-ASELH(PS-SUB)            CL*44
03593              MOVE DP015-GREEN     TO MB004O-ASELC(PS-SUB)            CL*44
03594          ELSE                                                        CL*44
03595              IF (MB102-BEGIN(MB102-BLK-SUB MB102-ITEM-SUB)) OR       CL210
03596                 (MB102-END(MB102-BLK-SUB MB102-ITEM-SUB))            CL210
03597                                                                      CL*44
03598                  IF (DP020-MSG-NUMBER = PC-NO-START-OF-RANGE) OR     CL*44
03599                     (DP020-MSG-NUMBER = PC-NO-END-OF-RANGE)          CL*44
03600                      MOVE DP015-REVERSE TO MB004O-ASELH(PS-SUB)      CL*44
03601                      MOVE DP015-YELLOW  TO MB004O-ASELC(PS-SUB)      CL*44
03602                  END-IF                                              CL*44
03603                                                                      CL*45
03604                  IF (DP020-MSG-NUMBER = PC-MULTIPLE-START-RANGES)    CL*70
03605                 AND (MB102-BEGIN(MB102-BLK-SUB MB102-ITEM-SUB))      CL210
03606                      MOVE DP015-REVERSE TO MB004O-ASELH(PS-SUB)      CL*45
03607                      MOVE DP015-RED     TO MB004O-ASELC(PS-SUB)      CL*70
03608                  END-IF                                              CL*45
03609                  IF (DP020-MSG-NUMBER = PC-MULTIPLE-END-RANGES)      CL*70
03610                 AND (MB102-END(MB102-BLK-SUB MB102-ITEM-SUB))        CL210
03611                      MOVE DP015-REVERSE TO MB004O-ASELH(PS-SUB)      CL*70
03612                      MOVE DP015-RED     TO MB004O-ASELC(PS-SUB)      CL*70
03613                  END-IF                                              CL*70
03614                  IF DP020-MSG-NUMBER = PC-END-BEFORE-START           CL*56
03615                      MOVE DP015-REVERSE TO MB004O-ASELH(PS-SUB)      CL*45
03616                      MOVE DP015-RED     TO MB004O-ASELC(PS-SUB)      CL*70
03617                  END-IF                                              CL*45
03618              END-IF                                                  CL*45
03619          END-IF                                                      CL*45
03620                                                                   MBKCS102
03621          MOVE MB102-SELECTED-SW(MB102-BLK-SUB MB102-ITEM-SUB) TO     CL210
03622               MB004O-ASELO(PS-SUB)                                   CL*45
03623                                                                      CL*45
03624          IF (PS-SEND-MAP-N-DATA-FRSET)     OR                        CL*45
03625             (PS-DIFFERENT-ITEMS-DISPLAYED)                           CL*45
03626             MOVE MB102-ITEM-GMA (MB102-BLK-SUB                       CL210
03627                                  MB102-ITEM-SUB)           TO        CL209
03628                  MB004O-AGMAO(PS-SUB)                                CL*45
03629             MOVE MB102-ITEM-DMA (MB102-BLK-SUB                       CL210
03630                                  MB102-ITEM-SUB)           TO        CL209
03631                  MB004O-ADMAO(PS-SUB)                                CL*45
03632             MOVE MB102-ITEM-DEPT (MB102-BLK-SUB                      CL210
03633                                  MB102-ITEM-SUB)           TO        CL209
03634                  MB004O-ADEPTO(PS-SUB)                               CL*45
03635             MOVE MB102-ITEM-MCLASS(MB102-BLK-SUB                     CL210
03636                                    MB102-ITEM-SUB)         TO        CL209
03637                  MB004O-AMCLSO(PS-SUB)                               CL*65
03638             MOVE MB102-ITEM-SCLASS(MB102-BLK-SUB                     CL210
03639                                    MB102-ITEM-SUB)         TO        CL209
03640                  MB004O-ASCLSO(PS-SUB)                               CL*65
03641             MOVE MB102-ITEM-LEVEL(MB102-BLK-SUB                      CL210
03642                                   MB102-ITEM-SUB)          TO        CL209
03643                  MB004O-ALVLCO(PS-SUB)                               CL*65
03644             MOVE MB102-ITEM-DESC (MB102-BLK-SUB                      CL210
03645                                   MB102-ITEM-SUB)          TO        CL209
03646                  MB004O-ADESCO(PS-SUB)                               CL*45
03647             MOVE MB102-ITEM-STR-DATE(MB102-BLK-SUB                   CL210
03648                                      MB102-ITEM-SUB)       TO        CL209
03649                  MB004O-ASDATO(PS-SUB)                               CL*57
03650             MOVE MB102-ITEM-END-DATE(MB102-BLK-SUB                   CL210
03651                                      MB102-ITEM-SUB)       TO        CL209
03652                  MB004O-AEDATO(PS-SUB)                               CL*45
03653 *                                                                    CL**1
03654             MOVE MB102-COLOR (MB102-BLK-SUB MB102-ITEM-SUB)          CL**1
03655                                TO  MB004O-AGMAC(PS-SUB)              CL**1
03656                                    MB004O-ADMAC(PS-SUB)              CL**1
03657                                    MB004O-ADEPTC(PS-SUB)             CL**1
03658                                    MB004O-AMCLSC(PS-SUB)             CL**1
03659                                    MB004O-ASCLSC(PS-SUB)             CL**1
03660                                    MB004O-ALVLCC(PS-SUB)             CL**1
03661                                    MB004O-ADESCC(PS-SUB)             CL**1
03662                                    MB004O-ASDATC(PS-SUB)             CL**1
03663                                    MB004O-AEDATC(PS-SUB)             CL**1
03664                                    MB004O-AACTNC(PS-SUB)             CL**1
03665 *                                                                    CL**1
03666             MOVE SPACES                                    TO        CL*45
03667                  MB004O-AACTNO(PS-SUB)                               CL*45
03668             MOVE DP015-HL-OFF                              TO        CL*45
03669                  MB004O-AGMAH(PS-SUB)                                CL*45
03670             MOVE DP015-HL-OFF                              TO        CL*45
03671                  MB004O-ADMAH(PS-SUB)                                CL*45
03672             MOVE DP015-HL-OFF                              TO        CL*45
03673                  MB004O-ADEPTH(PS-SUB)                               CL*45
03674             MOVE DP015-HL-OFF                              TO        CL*93
03675                  MB004O-AMCLSH(PS-SUB)                               CL*93
03676             MOVE DP015-HL-OFF                              TO        CL*93
03677                  MB004O-ASCLSH(PS-SUB)                               CL*93
03678             MOVE DP015-HL-OFF                              TO        CL*93
03679                  MB004O-ALVLCH(PS-SUB)                               CL*93
03680             MOVE DP015-HL-OFF                              TO        CL*45
03681                  MB004O-ADESCH(PS-SUB)                               CL*45
03682             MOVE DP015-HL-OFF                              TO        CL*45
03683                  MB004O-ASDATH(PS-SUB)                               CL*45
03684             MOVE DP015-HL-OFF                              TO        CL*45
03685                  MB004O-AEDATH(PS-SUB)                               CL*45
03686             MOVE DP015-HL-OFF                              TO        CL*45
03687                  MB004O-AACTNH(PS-SUB)                               CL*45
03688 *                                                                    CL204
03689 *                                                                    CL204
03690 *                                                                    CL204
03691             IF MB004O-AGMAO(PS-SUB) = '00'                           CL206
03692                MOVE SPACES TO MB004O-AGMAO(PS-SUB)                   CL204
03693             END-IF                                                   CL204
03694             IF MB004O-ADMAO(PS-SUB) = '00'                           CL206
03695                MOVE SPACES TO MB004O-ADMAO(PS-SUB)                   CL205
03696             END-IF                                                   CL205
03697             IF MB004O-ADEPTO(PS-SUB) = '000'                         CL204
03698                MOVE SPACES TO MB004O-ADEPTO(PS-SUB)                  CL204
03699             END-IF                                                   CL204
03700             IF MB004O-AMCLSO(PS-SUB) = '00'                          CL206
03701                MOVE SPACES TO MB004O-AMCLSO(PS-SUB)                  CL205
03702             END-IF                                                   CL205
03703             IF MB004O-ASCLSO(PS-SUB) = '000'                         CL205
03704                MOVE SPACES TO MB004O-ASCLSO(PS-SUB)                  CL205
03705             END-IF                                                   CL205
03706          END-IF                                                      CL*45
03707      ELSE                                                            CL*45
03708          MOVE DP015-ASK-DRK-OFF                            TO        CL170
03709               MB004I-ASELA(PS-SUB)                                   CL176
03710          MOVE SPACES                                       TO        CL*56
03711               MB004O-ASELO(PS-SUB)                                   CL*45
03712          MOVE SPACES                                       TO        CL*45
03713               MB004O-AGMAO(PS-SUB)                                   CL*45
03714          MOVE SPACES                                       TO        CL*45
03715               MB004O-ADMAO(PS-SUB)                                   CL*45
03716          MOVE SPACES                                       TO        CL*45
03717               MB004O-ADEPTO(PS-SUB)                                  CL*64
03718          MOVE SPACES                                       TO        CL*93
03719               MB004O-AMCLSO(PS-SUB)                                  CL*93
03720          MOVE SPACES                                       TO        CL*93
03721               MB004O-ASCLSO(PS-SUB)                                  CL*93
03722          MOVE SPACES                                       TO        CL*93
03723               MB004O-ALVLCO(PS-SUB)                                  CL*93
03724          MOVE SPACES                                       TO        CL*45
03725               MB004O-ADESCO(PS-SUB)                                  CL*45
03726          MOVE SPACES                                       TO        CL*45
03727               MB004O-ASDATO(PS-SUB)                                  CL*45
03728          MOVE SPACES                                       TO        CL*45
03729               MB004O-AEDATO(PS-SUB)                                  CL*45
03730         MOVE SPACES                                        TO        CL*93
03731              MB004O-AACTNO(PS-SUB)                                   CL*93
03732      END-IF.                                                         CL*45
03733                                                                      CL*45
03734      ADD +1 TO PV-ITEM-IN-USE.                                    MBKCS102
03735      ADD +1 TO PS-SUB.                                            MBKCS102
03736  EJECT                                                            MBKCS102
03737  J200-FIND-LAST-SELECTION.                                        MBKCS102
03738                                                                   MBKCS102
03739 ******************************************************************MBKCS102
03740 *    FIND THE LAST SELECTION ENTRY IN THE MAP THAT IS NOT         MBKCS102
03741 *    SPACES OR LOW VALUES AND POINT THE CURSOR TO THIS ENTRY.     MBKCS102
03742 *    IF ALL ENTRIES ARE SPACES OR LOW VALUES, POINT THE CURSOR    MBKCS102
03743 *    TO THE 1ST SELECTION ENTRY IN THE MAP.                       MBKCS102
03744 *                                                                 MBKCS102
03745 *    PERFORMED BY I800-SET-CURSOR-POSITION.                          CL*93
03746 ******************************************************************MBKCS102
03747                                                                   MBKCS102
03748      MOVE PC-ITEMS-PER-PANEL TO PS-SUB.                           MBKCS102
03749                                                                   MBKCS102
03750      SET PS-NO-SELECTION-FOUND TO TRUE.                           MBKCS102
03751                                                                   MBKCS102
03752      PERFORM                                                      MBKCS102
03753          UNTIL (PS-SUB = +0)        OR                            MBKCS102
03754                (PS-SELECTION-FOUND)                               MBKCS102
03755                                                                   MBKCS102
03756          IF (MB004O-ASELO(PS-SUB) NOT = SPACES)     AND              CL*22
03757             (MB004O-ASELO(PS-SUB) NOT = LOW-VALUES)                  CL*22
03758              SET PS-SELECTION-FOUND TO TRUE                       MBKCS102
03759          ELSE                                                     MBKCS102
03760              SUBTRACT +1 FROM PS-SUB                              MBKCS102
03761          END-IF                                                   MBKCS102
03762                                                                   MBKCS102
03763      END-PERFORM.                                                 MBKCS102
03764                                                                   MBKCS102
03765      IF PS-SELECTION-FOUND                                        MBKCS102
03766          MOVE -1 TO MB004I-ASELL(PS-SUB)                             CL*56
03767      ELSE                                                         MBKCS102
03768          MOVE -1 TO MB004I-ASELL(1)                                  CL*56
03769      END-IF.                                                      MBKCS102
03770  EJECT                                                            MBKCS102
03771  K100-SET-SUBSCRIPTS.                                             MBKCS102
03772                                                                   MBKCS102
03773 ******************************************************************MBKCS102
03774 *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM  MBKCS102
03775 *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL MBKCS102
03776 *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                   MBKCS102
03777 *                                                                 MBKCS102
03778 *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS    MBKCS102
03779 *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT     MBKCS102
03780 *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION   MBKCS102
03781 *    US IN THE 2ND BLOCK OF THE ARRAY.                            MBKCS102
03782 *                                                                    CL*92
03783 *    PERFORMED BY E100-EDIT-SELECTIONS.                              CL203
03784 *    PERFORMED BY F300-CHECK-SELECTIONS                              CL*92
03785 ******************************************************************MBKCS102
03786                                                                   MBKCS102
03787      COMPUTE PS-ITEM-SUB =                                        MBKCS102
03788          (PV-ITEM-IN-USE - MB102-LIST-ITEM-NUMBER(1 1)) + 1.         CL209
03789                                                                   MBKCS102
03790      MOVE PS-ITEM-SUB TO MB102-ITEM-SUB.                             CL209
03791                                                                   MBKCS102
03792      IF MB102-ITEM-SUB > PC-ITEMS-PER-BLOCK                          CL209
03793          SUBTRACT PC-ITEMS-PER-BLOCK FROM MB102-ITEM-SUB             CL209
03794                                                                   MBKCS102
03795          MOVE PC-MAX-BLOCKS-IN-ARRAY TO MB102-BLK-SUB                CL210
03796      ELSE                                                         MBKCS102
03797          MOVE +1                     TO MB102-BLK-SUB                CL210
03798      END-IF.                                                      MBKCS102
03799 *                                                                    CL208
03800  EJECT                                                               CL208
03801 ******************************************************************   CL208
03802 *    PREPARE THE TEMPORARY STORAGE QUEUE.  MOVE ALL SELECTED         CL208
03803 *    GROUPS FROM EITHER TEMP STORAGE OR THE ARRAY IN THE COMM        CL208
03804 *    AREA TO THE LIST TS QUEUE RECORD(S).                            CL208
03805 *                                                                    CL208
03806 *    PERFORMED BY D300                                               CL208
03807 ******************************************************************   CL208
03808 *                                                                    CL208
03809  X100-PREPARE-LIST-QUEUE.                                            CL208
03810 *                                                                    CL208
03811      SET DONT-SETUP-INTER-APPL TO  TRUE.                             CL247
03812      MOVE +1                   TO  MB102-BLK-SUB.                    CL208
03813      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL208
03814      ADD +1                    TO  MB102-BLK-SUB.                    CL208
03815      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL208
03816 *                                                                    CL209
03817      IF  MB102-HIGHEST-BLOCK-WRITTEN = ZERO                          CL209
03818          MOVE EIBTRMID         TO  PV-TERM-ID                        CL209
03819          MOVE EIBTASKN         TO  PV-TASK-NUMBER                    CL209
03820      ELSE                                                            CL209
03821          MOVE MB102-TERM-ID    TO  PV-TERM-ID                        CL209
03822          MOVE MB102-TASK-NUMBER                                      CL209
03823                                TO  PV-TASK-NUMBER                    CL209
03824      END-IF.                                                         CL209
03825      SUBTRACT 1              FROM  PV-TASK-NUMBER.                   CL209
03826 *                                                                    CL208
03827      MOVE PV-QUEUE-NAME        TO  MB005-LIST-QUEUE-NAME             CL209
03828                                    MB102-LIST-QUEUE-NAME.            CL208
03829      MOVE LENGTH OF SELECTED-LIST-RECORD                             CL209
03830                                TO  MB005-LIST-QUEUE-LENGTH.          CL208
03831      PERFORM X900-DELETE-LIST-QUEUE.                                 CL208
03832 *                                                                    CL208
03833      SET MB003-IDX             TO  1.                                CL*27
03834      INITIALIZE SELECTED-LIST-RECORD.                                CL209
03835      PERFORM X200-PROCESS-TS-QUEUE                                   CL208
03836              VARYING PV-ITEM                                         CL208
03837              FROM 1 BY 1                                             CL208
03838              UNTIL PV-ITEM GREATER MB102-HIGHEST-BLOCK-WRITTEN.      CL208
03839 *                                                                    CL208
03840      PERFORM X500-WRITE-LIST-QUEUE.                                  CL208
03841 *                                                                    CL208
03842  EJECT                                                               CL208
03843 ******************************************************************   CL208
03844 *    OBTAIN A TS QUEUE RECORD, MOVE SELECTED RECORDS TO THE          CL244
03845 *    LIST QUEUE RECORD.                                              CL244
03846 *                                                                    CL208
03847 *    PERFORMED BY X100                                               CL208
03848 ******************************************************************   CL208
03849 *                                                                    CL208
03850  X200-PROCESS-TS-QUEUE.                                              CL208
03851 *                                                                    CL208
03852      PERFORM X250-READ-TS-QUEUE.                                     CL244
03853      IF  PV-RESP EQUAL DFHRESP(NORMAL)                               CL208
03854          PERFORM                                                     CL208
03855          VARYING PV-TW-IDX                                           CL208
03856          FROM 1 BY 1                                                 CL208
03857          UNTIL PV-TW-IDX GREATER THAN PC-ITEMS-PER-BLOCK             CL208
03858            OR  PV-TW-LIST-ITEM-NUMBER (PV-TW-IDX) EQUAL ZERO         CL208
03859 *                                                                    CL208
03860              IF  PV-TW-SELECT (PV-TW-IDX)                            CL208
03861                  MOVE PV-TW-KEY-DATA (PV-TW-IDX)                     CL209
03862                                TO  MB003-KEY-DATA (MB003-IDX)        CL*27
03863                                    PV-HOLD-KEY-DATA                  CL251
03864                  MOVE PV-TW-LIST-ITEM-NUMBER (PV-TW-IDX)             CL244
03865                                TO  MB003-ITEM       (MB003-IDX)      CL*27
03866                  SET MB003-IDX UP BY 1                               CL*27
03867                  IF PV-TW-COLOR(PV-TW-IDX) = DP015-TURQUOISE         CL*14
03868                     SET PS-SELECTED-HISTORY TO TRUE                  CL*11
03869                  END-IF                                              CL*11
03870                  IF PV-TW-MCL-SCL(PV-TW-IDX)                         CL*32
03871                     SET PS-SELECTED-MCL-SCL TO TRUE                  CL*32
03872                  END-IF                                              CL*32
03873                  IF  MB003-IDX GREATER PC-MAX-QUEUE-ITEMS            CL*27
03874                      PERFORM X500-WRITE-LIST-QUEUE                   CL208
03875                  END-IF                                              CL208
03876              END-IF                                                  CL208
03877 *                                                                    CL208
03878          END-PERFORM                                                 CL208
03879      END-IF.                                                         CL208
03880 *                                                                    CL244
03881  EJECT                                                               CL244
03882 ******************************************************************   CL244
03883 *    READ A TS QUEUE RECORD.                                         CL244
03884 *                                                                    CL244
03885 *    PERFORMED BY X200                                               CL244
03886 ******************************************************************   CL244
03887 *                                                                    CL244
03888  X250-READ-TS-QUEUE.                                                 CL245
03889 *                                                                    CL244
03890      EXEC CICS                                                       CL244
03891          READQ TS                                                    CL244
03892          QUEUE(MB102-TEMP-QUEUE-NAME)                                CL244
03893          INTO(PV-TEMP-WORK-AREA)                                     CL244
03894          LENGTH(PC-BLOCK-LENGTH)                                     CL244
03895          ITEM(PV-ITEM)                                               CL244
03896          RESP(PV-RESP)                                               CL244
03897      END-EXEC.                                                       CL244
03898 *                                                                    CL208
03899  EJECT                                                               CL208
03900 ******************************************************************   CL208
03901 *    WRITE THE LIST QUEUE RECORD.                                    CL209
03902 *                                                                    CL208
03903 *    PERFORMED BY X100, X300                                         CL209
03904 ******************************************************************   CL208
03905 *                                                                    CL208
03906  X500-WRITE-LIST-QUEUE.                                              CL208
03907 *                                                                    CL208
03908      IF  MB003-IDX GREATER THAN 1                                    CL*27
03909          MOVE PC-MAX-QUEUE-ITEMS                                     CL209
03910                                TO  MB003-MAX-ITEMS                   CL*27
03911 *        MOVE MB102-NUMBER-OF-ITEMS-SELECTED                         CL214
03912 *                              TO  MB003-ITEMS-SELECTED              CL*27
03913 *                                                                    CL208
03914          IF  DP020-SRC-AID EQUAL DP016-PF14                          CL208
03915          OR  DP020-SRC-AID EQUAL DP016-PF18                          CL209
03916              SET MB003-INQUIRY TO    TRUE                            CL*27
03917          END-IF                                                      CL208
03918          IF  DP020-SRC-AID EQUAL DP016-PF15                          CL208
03919          OR  DP020-SRC-AID EQUAL DP016-PF19                          CL209
03920              SET MB003-UPDATE    TO  TRUE                            CL*27
03921          END-IF                                                      CL208
03922 *                                                                    CL208
03923          EXEC CICS                                                   CL208
03924               WRITEQ TS                                              CL208
03925               QUEUE(MB005-LIST-QUEUE-NAME)                           CL208
03926               FROM(SELECTED-LIST-RECORD)                             CL209
03927               LENGTH(MB005-LIST-QUEUE-LENGTH)                        CL208
03928               ITEM(PV-MA-ITEM)                                       CL252
03929               RESP(PV-RESP)                                          CL208
03930          END-EXEC                                                    CL208
03931 *                                                                    CL208
03932          IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                       CL208
03933              SET DP013-LOGIC-ABEND TO TRUE                           CL211
03934              SET DP013-NO-ROLLBACK TO TRUE                           CL211
03935              MOVE 'X500-WRITE-LIST-QUEUE'   TO                       CL212
03936                   DP013-PARAGRAPH                                    CL211
03937              MOVE PV-BLOCK-NUMBER           TO                       CL211
03938                   AM-BLOCK-NUMBER-11                                 CL211
03939              MOVE AM-ABEND-MESSAGE-11       TO                       CL211
03940                   DP013-MESSAGE-TEXT(1)                              CL211
03941              PERFORM DP013-0000-PROCESS-ABEND                        CL211
03942          END-IF                                                      CL208
03943 *                                                                    CL208
03944          INITIALIZE SELECTED-LIST-RECORD                             CL209
03945          SET MB003-IDX         TO  1                                 CL*27
03946      END-IF.                                                         CL208
03947 *                                                                    CL244
03948  EJECT                                                               CL244
03949 ******************************************************************   CL244
03950 *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C700.                 CL244
03951 *                                                                    CL244
03952 *    PERFORMED BY C600, C700                                         CL244
03953 ******************************************************************   CL244
03954 *                                                                    CL244
03955  X550-WRITE-TS-QUEUE.                                                CL244
03956 *                                                                    CL244
03957      EXEC CICS                                                       CL244
03958          WRITEQ TS                                                   CL244
03959          REWRITE                                                     CL244
03960          QUEUE(MB102-TEMP-QUEUE-NAME)                                CL244
03961          FROM(PV-TEMP-WORK-AREA)                                     CL244
03962          LENGTH(PC-BLOCK-LENGTH)                                     CL244
03963          ITEM(PV-SAVE-ITEM)                                          CL244
03964          RESP(PV-RESP)                                               CL244
03965      END-EXEC.                                                       CL244
03966 *                                                                    CL244
03967  EJECT                                                               CL244
03968 ******************************************************************   CL244
03969 *    READ THE LIST QUEUE RECORD.                                     CL244
03970 *                                                                    CL244
03971 *    PERFORMED BY C600                                               CL244
03972 ******************************************************************   CL244
03973 *                                                                    CL244
03974  X600-READ-LIST-QUEUE.                                               CL244
03975 *                                                                    CL244
03976      EXEC CICS                                                       CL244
03977           READQ TS                                                   CL244
03978           QUEUE(MB005-LIST-QUEUE-NAME)                               CL244
03979           INTO(SELECTED-LIST-RECORD)                                 CL244
03980           LENGTH(MB005-LIST-QUEUE-LENGTH)                            CL244
03981           ITEM(PV-MA-ITEM)                                           CL244
03982           RESP(PV-RESP)                                              CL244
03983      END-EXEC.                                                       CL244
03984 *                                                                    CL208
03985  EJECT                                                               CL208
03986 ******************************************************************   CL208
03987 *    DELETE THE SKU LIST TEMP STORAGE QUEUE.                         CL208
03988 *                                                                    CL208
03989 *    PERFORMED BY X100                                               CL208
03990 ******************************************************************   CL208
03991 *                                                                    CL208
03992  X900-DELETE-LIST-QUEUE.                                             CL208
03993 *                                                                    CL208
03994      EXEC CICS                                                       CL208
03995          DELETEQ TS                                                  CL208
03996          QUEUE(MB005-LIST-QUEUE-NAME)                                CL208
03997          RESP(PV-RESP)                                               CL208
03998      END-EXEC.                                                       CL208
03999 *                                                                    CL208
04000  EJECT                                                            MBKCS102
04001  Z050-DELETE-TEMP-STORAGE.                                        MBKCS102
04002                                                                   MBKCS102
04003 ******************************************************************MBKCS102
04004 *    DELETE THE TEMPORARY STORAGE QUEUE.                          MBKCS102
04005 *                                                                 MBKCS102
04006 *    PERFORMED BY C300-PROCESS-PANEL.                                CL203
04007 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL203
04008 ******************************************************************MBKCS102
04009                                                                   MBKCS102
04010      EXEC CICS                                                    MBKCS102
04011          DELETEQ TS                                               MBKCS102
04012          QUEUE(MB102-TEMP-QUEUE-NAME)                                CL209
04013          RESP(PV-CICS-RESPONSE)                                      CL**4
04014      END-EXEC.                                                    MBKCS102
04015                                                                   MBKCS102
04016      IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)) AND                 CL**4
04017         (PV-CICS-RESPONSE NOT = DFHRESP(QIDERR))                     CL**4
04018          SET DP013-LOGIC-ABEND           TO TRUE                     CL*22
04019          SET DP013-NO-ROLLBACK           TO TRUE                     CL*22
04020          MOVE 'Z050-DELETE-TEMP-STORAGE' TO DP013-PARAGRAPH          CL*22
04021          MOVE AM-ABEND-MESSAGE-8         TO DP013-MESSAGE-TEXT(1)    CL*22
04022      END-IF.                                                      MBKCS102
04023                                                                   MBKCS102
04024      MOVE +0 TO MB102-HIGHEST-BLOCK-WRITTEN.                         CL209
04025  EJECT                                                            MBKCS102
04026                                                                      CL*23
04027  Z100-RETURN-TO-PREV-TRAN.                                           CL*23
04028 ******************************************************************   CL*23
04029 *    FORCE USER BACK TO PREVIOUS TRANSACTION.                        CL*23
04030 *                                                                    CL*23
04031 ******************************************************************   CL*23
04032       MOVE DP016-PF3 TO DP020-SRC-AID.                               CL*23
04033       PERFORM DP015-1000-START-NAVIGATOR.                            CL*23
04034                                                                      CL*23
04035                                                                      CL*23
04036  Z100-READ-TEMP-STORAGE.                                          MBKCS102
04037                                                                   MBKCS102
04038 ******************************************************************MBKCS102
04039 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.          MBKCS102
04040 *                                                                 MBKCS102
04041 *    PERFORMED BY D400-REFRESH-BLOCJ-ARRAY.                          CL203
04042 *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED.                       CL203
04043 ******************************************************************MBKCS102
04044                                                                   MBKCS102
04045      EXEC CICS                                                    MBKCS102
04046          READQ TS                                                 MBKCS102
04047          QUEUE(MB102-TEMP-QUEUE-NAME)                                CL209
04048          INTO(MB102-BLOCK-ENTRIES(MB102-BLK-SUB))                    CL210
04049          LENGTH(PC-BLOCK-LENGTH)                                  MBKCS102
04050          ITEM(PV-BLOCK-NUMBER)                                    MBKCS102
04051          RESP(PV-CICS-RESPONSE)                                      CL**4
04052      END-EXEC.                                                    MBKCS102
04053                                                                   MBKCS102
04054      IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)) AND                 CL*69
04055         (PV-CICS-RESPONSE NOT = DFHRESP(ITEMERR))                    CL*69
04056                                                                   MBKCS102
04057          EXEC CICS                                                MBKCS102
04058               DELETEQ TS                                          MBKCS102
04059               QUEUE(MB102-TEMP-QUEUE-NAME)                           CL209
04060               RESP(PV-CICS-RESPONSE)                                 CL**4
04061          END-EXEC                                                 MBKCS102
04062                                                                   MBKCS102
04063          SET DP013-LOGIC-ABEND         TO TRUE                       CL*28
04064          SET DP013-NO-ROLLBACK         TO TRUE                       CL*28
04065          MOVE 'Z100-READ-TEMP-STORAGE' TO DP013-PARAGRAPH            CL*28
04066          MOVE AM-ABEND-MESSAGE-12      TO DP013-MESSAGE-TEXT(1)      CL*57
04067          PERFORM DP013-0000-PROCESS-ABEND                            CL*28
04068                                                                      CL*28
04069      END-IF.                                                      MBKCS102
04070                                                                   MBKCS102
04071      SET MB102-BLOCK-NOT-MODIFIED(MB102-BLK-SUB) TO TRUE.            CL210
04072  EJECT                                                            MBKCS102
04073  Z125-READ-TEMP-STORAGE.                                             CL233
04074                                                                      CL233
04075 ******************************************************************   CL233
04076 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.             CL233
04077 *                                                                    CL233
04078 *    PERFORMED BY E900-FIND-FIRST-SELECTED.                          CL233
04079 ******************************************************************   CL233
04080                                                                      CL233
04081      EXEC CICS                                                       CL233
04082          READQ TS                                                    CL233
04083          QUEUE(MB102-TEMP-QUEUE-NAME)                                CL233
04084          INTO(PV-BLOCK)                                              CL233
04085          LENGTH(PC-BLOCK-LENGTH)                                     CL233
04086          ITEM(PV-BLOCK-NUMBER)                                       CL233
04087          RESP(PV-CICS-RESPONSE)                                      CL233
04088      END-EXEC.                                                       CL233
04089                                                                      CL233
04090      IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)) AND                 CL233
04091         (PV-CICS-RESPONSE NOT = DFHRESP(ITEMERR))                    CL233
04092                                                                      CL233
04093          EXEC CICS                                                   CL233
04094               DELETEQ TS                                             CL233
04095               QUEUE(MB102-TEMP-QUEUE-NAME)                           CL233
04096               RESP(PV-CICS-RESPONSE)                                 CL233
04097          END-EXEC                                                    CL233
04098                                                                      CL233
04099          SET DP013-LOGIC-ABEND         TO TRUE                       CL233
04100          SET DP013-NO-ROLLBACK         TO TRUE                       CL233
04101          MOVE 'Z100-READ-TEMP-STORAGE' TO DP013-PARAGRAPH            CL233
04102          MOVE AM-ABEND-MESSAGE-12      TO DP013-MESSAGE-TEXT(1)      CL233
04103          PERFORM DP013-0000-PROCESS-ABEND                            CL233
04104                                                                      CL233
04105      END-IF.                                                         CL233
04106                                                                      CL233
04107  EJECT                                                               CL233
04108  Z150-WRITE-TEMP-STORAGE.                                         MBKCS102
04109                                                                   MBKCS102
04110 ******************************************************************MBKCS102
04111 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.       MBKCS102
04112 *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.     MBKCS102
04113 *                                                                 MBKCS102
04114 *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED                        CL*92
04115 ******************************************************************MBKCS102
04116                                                                   MBKCS102
04117      IF MB102-HIGHEST-BLOCK-WRITTEN = +0                             CL209
04118          MOVE EIBTASKN TO DP020-SRC-TASK-NUMBER                   MBKCS102
04119          MOVE EIBTASKN TO MB102-TASK-NUMBER                          CL209
04120          MOVE EIBTRMID TO MB102-TERM-ID                              CL209
04121      END-IF.                                                      MBKCS102
04122                                                                   MBKCS102
04123      MOVE MB102-LIST-ITEM-NUMBER(MB102-BLK-SUB 1) TO                 CL210
04124           PV-LIST-ITEM-NUMBER.                                    MBKCS102
04125                                                                   MBKCS102
04126      COMPUTE PV-BLOCK-NUMBER =                                    MBKCS102
04127          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-BLOCK).    MBKCS102
04128                                                                   MBKCS102
04129      IF (PV-BLOCK-NUMBER < MB102-HIGHEST-BLOCK-WRITTEN) OR           CL209
04130         (PV-BLOCK-NUMBER = MB102-HIGHEST-BLOCK-WRITTEN)              CL209
04131                                                                   MBKCS102
04132          IF MB102-BLOCK-MODIFIED(MB102-BLK-SUB)                      CL210
04133                                                                   MBKCS102
04134              EXEC CICS                                            MBKCS102
04135                  WRITEQ TS                                        MBKCS102
04136                  QUEUE(MB102-TEMP-QUEUE-NAME)                        CL209
04137                  FROM(MB102-BLOCK-ENTRIES(MB102-BLK-SUB))            CL210
04138                  LENGTH(PC-BLOCK-LENGTH)                          MBKCS102
04139                  ITEM(PV-BLOCK-NUMBER)                            MBKCS102
04140                  REWRITE                                          MBKCS102
04141                  RESP(PV-CICS-RESPONSE)                              CL**4
04142              END-EXEC                                             MBKCS102
04143                                                                   MBKCS102
04144              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)               CL**4
04145                  EXEC CICS                                        MBKCS102
04146                      DELETEQ TS                                   MBKCS102
04147                      QUEUE(MB102-TEMP-QUEUE-NAME)                    CL209
04148                      RESP(PV-CICS-RESPONSE)                          CL**4
04149                  END-EXEC                                         MBKCS102
04150                                                                   MBKCS102
04151                  SET DP013-LOGIC-ABEND TO TRUE                       CL*26
04152                  SET DP013-NO-ROLLBACK TO TRUE                       CL*26
04153                  MOVE 'Z150-WRITE-TEMP-STORAGE' TO                   CL*26
04154                       DP013-PARAGRAPH                                CL*26
04155                  MOVE AM-ABEND-MESSAGE-9        TO                   CL*26
04156                       DP013-MESSAGE-TEXT(1)                          CL*26
04157                  PERFORM DP013-0000-PROCESS-ABEND                    CL*26
04158              END-IF                                               MBKCS102
04159                                                                   MBKCS102
04160          END-IF                                                   MBKCS102
04161                                                                   MBKCS102
04162      ELSE                                                         MBKCS102
04163                                                                   MBKCS102
04164          COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       MBKCS102
04165              MB102-HIGHEST-BLOCK-WRITTEN + 1                         CL209
04166                                                                   MBKCS102
04167          IF PV-BLOCK-NUMBER = PV-HIGHEST-BLOCK-WRITTEN            MBKCS102
04168                                                                   MBKCS102
04169              EXEC CICS                                            MBKCS102
04170                  WRITEQ TS                                        MBKCS102
04171                  QUEUE(MB102-TEMP-QUEUE-NAME)                        CL209
04172                  FROM(MB102-BLOCK-ENTRIES(MB102-BLK-SUB))            CL210
04173                  LENGTH(PC-BLOCK-LENGTH)                          MBKCS102
04174                  ITEM(PV-BLOCK-NUMBER)                            MBKCS102
04175                  RESP(PV-CICS-RESPONSE)                              CL**4
04176              END-EXEC                                             MBKCS102
04177                                                                   MBKCS102
04178              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)               CL**4
04179                                                                      CL**3
04180                  EXEC CICS                                           CL**3
04181                      DELETEQ TS                                   MBKCS102
04182                      QUEUE(MB102-TEMP-QUEUE-NAME)                    CL209
04183                      RESP(PV-CICS-RESPONSE)                          CL**4
04184                  END-EXEC                                         MBKCS102
04185                                                                   MBKCS102
04186                  SET DP013-LOGIC-ABEND TO TRUE                       CL*26
04187                  SET DP013-NO-ROLLBACK TO TRUE                       CL*26
04188                  MOVE 'Z150-WRITE-TEMP-STORAGE' TO                   CL*26
04189                       DP013-PARAGRAPH                                CL*26
04190                  MOVE AM-ABEND-MESSAGE-10       TO                   CL*26
04191                       DP013-MESSAGE-TEXT(1)                          CL*26
04192                  PERFORM DP013-0000-PROCESS-ABEND                    CL*26
04193              END-IF                                               MBKCS102
04194                                                                   MBKCS102
04195              ADD +1 TO MB102-HIGHEST-BLOCK-WRITTEN                   CL209
04196          ELSE                                                     MBKCS102
04197              SET DP013-LOGIC-ABEND TO TRUE                           CL*26
04198              SET DP013-NO-ROLLBACK TO TRUE                           CL*26
04199              MOVE 'Z150-WRITE-TEMP-STORAGE' TO                       CL*26
04200                   DP013-PARAGRAPH                                    CL*26
04201              MOVE PV-BLOCK-NUMBER           TO                       CL*26
04202                   AM-BLOCK-NUMBER-11                                 CL*26
04203              MOVE AM-ABEND-MESSAGE-11       TO                       CL*26
04204                   DP013-MESSAGE-TEXT(1)                              CL*26
04205              PERFORM DP013-0000-PROCESS-ABEND                        CL*26
04206          END-IF                                                   MBKCS102
04207                                                                   MBKCS102
04208      END-IF.                                                      MBKCS102
04209                                                                   MBKCS102
04210      SET MB102-BLOCK-NOT-MODIFIED(MB102-BLK-SUB) TO TRUE.            CL210
04211  EJECT                                                            MBKCS102
04212  Z200-OPEN-MERCH-CURSOR.                                             CL*92
04213                                                                      CL*92
04214 ******************************************************************   CL*39
04215 *    OPEN MERCHANDISE AREA CURSOR                                    CL*39
04216 *                                                                    CL203
04217 *    PERFORMEN BY D500-OPEN-CURSOR.                                  CL203
04218 ******************************************************************   CL*39
04219                                                                      CL*39
04220      EXEC SQL                                                        CL*39
04221          OPEN MERCH-CURSOR                                           CL*39
04222      END-EXEC.                                                       CL*39
04223                                                                      CL*58
04224      IF (SQLCODE = +0)   OR                                          CL*58
04225         (SQLCODE = +100)                                             CL*58
04226          SET PS-MERCH-CURSOR-OPENED     TO TRUE                      CL*84
04227      ELSE                                                            CL*58
04228          SET DP013-DB2-ABEND            TO TRUE                      CL*58
04229          SET DP013-NO-ROLLBACK          TO TRUE                      CL*58
04230          MOVE 'Z200-OPEN-MERCH-CURSOR'  TO                           CL*58
04231               DP013-PARAGRAPH                                        CL*58
04232          MOVE AM-ABEND-MESSAGE-21       TO                           CL*58
04233               DP013-MESSAGE-TEXT(1)                                  CL*58
04234          MOVE SQLCA                     TO                           CL*58
04235               DP013-SQLCA                                            CL*58
04236          PERFORM DP013-0000-PROCESS-ABEND                            CL*58
04237      END-IF.                                                         CL*58
04238  EJECT                                                               CL*39
04239  Z210-READ-MERCH-AREA.                                               CL*39
04240                                                                      CL137
04241 ******************************************************************   CL137
04242 *    READ MERCHANDISE AREA CURSOR                                    CL137
04243 ******************************************************************   CL137
04244                                                                      CL*39
04245      EXEC SQL                                                        CL*39
04246          FETCH MERCH-CURSOR                                          CL*39
04247           INTO :MDSEAR-OPERCO-NBR,                                   CL*62
04248                :MDSEAR-GMA-NBR,                                      CL*61
04249                :MDSEAR-DMA-NBR,                                      CL*47
04250                :MDSEAR-DEPT-NBR,                                     CL*47
04251                :MDSEAR-MAJ-CL-NBR,                                   CL*47
04252                :MDSEAR-SUB-CL-NBR,                                   CL*47
04253                :MDSEAR-MDSELV-CDE,                                   CL*47
04254                :MDSEAR-CHG-DTE,                                      CL*47
04255                :MDSEAR-CRE-DTE,                                      CL*47
04256                :MDSEAR-STRT-DTE,                                     CL*47
04257                :MDSEAR-END-DTE,                                      CL*47
04258                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL*47
04259                :MDSEAR-DESC,                                         CL*48
04260                :MDSEAR-BYR-NBR,                                      CL108
04261                :MDSEAR-LIST-SEQ-NBR                                  CL108
04262      END-EXEC.                                                       CL*39
04263                                                                      CL*42
04264      SET PS-DB2-OK TO TRUE                                           CL*58
04265                                                                      CL*42
04266      IF (SQLCODE = -904) OR                                          CL*42
04267         (SQLCODE = -913)                                             CL*42
04268          SET PS-DB2-RETRY TO TRUE                                    CL*42
04269          ADD 1 TO PC-RETRY-CNTR                                      CL*42
04270          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL*42
04271              SET PS-DB2-OK TO TRUE                                   CL*42
04272          END-IF                                                      CL*42
04273      END-IF.                                                         CL*42
04274                                                                      CL*42
04275      IF PS-DB2-OK                                                    CL*42
04276          IF SQLCODE = +0                                             CL*42
04277              CONTINUE                                                CL*42
04278          ELSE                                                        CL*42
04279              IF SQLCODE = +100                                       CL*42
04280                  SET MB102-NO-ITEMS-LEFT-ON-DB TO TRUE               CL209
04281              ELSE                                                    CL*42
04282                  SET DP013-DB2-ABEND   TO TRUE                       CL*42
04283                  SET DP013-NO-ROLLBACK TO TRUE                       CL*42
04284                  MOVE 'Z210-READ-MERCH-CURSOR' TO                    CL*42
04285                       DP013-PARAGRAPH                                CL*42
04286                  MOVE PC-OPERCO-NBR            TO                    CL*42
04287                       AM-OPERCO-NBR-13                               CL*42
04288                  MOVE MB102-SAVE-GMA           TO                    CL209
04289                       AM-GMA-NBR-13                                  CL*58
04290                  MOVE MB102-SAVE-DMA           TO                    CL209
04291                       AM-DMA-NBR-13                                  CL*58
04292                  MOVE MB102-SAVE-DPT           TO                    CL209
04293                       AM-DEPT-NBR-13                                 CL*58
04294                  MOVE MB102-SAVE-MCL           TO                    CL209
04295                       AM-MCL-NBR-13                                  CL*58
04296                  MOVE MB102-SAVE-SCL           TO                    CL209
04297                       AM-SCL-NBR-13                                  CL*58
04298                  MOVE AM-ABEND-MESSAGE-13      TO                    CL*42
04299                       DP013-MESSAGE-TEXT(1)                          CL*59
04300                  MOVE SQLCA                    TO                    CL*42
04301                       DP013-SQLCA                                    CL*42
04302                  PERFORM DP013-0000-PROCESS-ABEND                    CL*42
04303              END-IF                                                  CL*42
04304          END-IF                                                      CL*42
04305      END-IF.                                                         CL*42
04306  EJECT                                                               CL*39
04307  Z220-CLOSE-MERCH-CURSOR.                                            CL*42
04308                                                                      CL*42
04309 ******************************************************************MBKCS102
04310 *    CLOSE THE MERCH-CURSOR                                          CL*42
04311 ******************************************************************MBKCS102
04312                                                                   MBKCS102
04313      EXEC SQL                                                        CL*42
04314          CLOSE MERCH-CURSOR                                          CL*42
04315      END-EXEC.                                                       CL*42
04316                                                                      CL*42
04317      IF (SQLCODE = +0)   OR                                          CL*42
04318         (SQLCODE = +100)                                             CL*42
04319          SET PS-MERCH-CURSOR-NOT-OPENED TO TRUE                      CL*84
04320      ELSE                                                            CL*42
04321          SET DP013-DB2-ABEND            TO TRUE                      CL*42
04322          SET DP013-NO-ROLLBACK          TO TRUE                      CL*42
04323          MOVE 'Z210-CLOSE-MERCH-CURSOR' TO                           CL*42
04324               DP013-PARAGRAPH                                        CL*42
04325          MOVE AM-ABEND-MESSAGE-22       TO                           CL*42
04326               DP013-MESSAGE-TEXT(1)                                  CL*42
04327          MOVE SQLCA                     TO                           CL*42
04328               DP013-SQLCA                                            CL*42
04329          PERFORM DP013-0000-PROCESS-ABEND                            CL*42
04330      END-IF.                                                         CL*42
04331  EJECT                                                            MBKCS102
04332  Z300-OPEN-GMA-CURSOR.                                               CL*97
04333                                                                      CL*97
04334 ******************************************************************   CL*97
04335 *    OPEN MERCHANDISE AREA CURSOR -- GMA ONLY                        CL*97
04336 *                                                                    CL203
04337 *    PERFORMEN BY D500-OPEN-CURSOR.                                  CL203
04338 ******************************************************************   CL*97
04339                                                                      CL*97
04340      EXEC SQL                                                        CL*97
04341          OPEN GMA-CURSOR                                             CL*97
04342      END-EXEC.                                                       CL*97
04343                                                                      CL*97
04344      IF (SQLCODE = +0)   OR                                          CL*97
04345         (SQLCODE = +100)                                             CL*97
04346          SET PS-GMA-CURSOR-OPENED       TO TRUE                      CL112
04347      ELSE                                                            CL*97
04348          SET DP013-DB2-ABEND            TO TRUE                      CL*97
04349          SET DP013-NO-ROLLBACK          TO TRUE                      CL*97
04350          MOVE 'Z300-OPEN-GMA-CURSOR'    TO                           CL*98
04351               DP013-PARAGRAPH                                        CL*97
04352          MOVE AM-ABEND-MESSAGE-21       TO                           CL*97
04353               DP013-MESSAGE-TEXT(1)                                  CL*97
04354          MOVE SQLCA                     TO                           CL*97
04355               DP013-SQLCA                                            CL*97
04356          PERFORM DP013-0000-PROCESS-ABEND                            CL*97
04357      END-IF.                                                         CL*97
04358  EJECT                                                               CL*97
04359  Z310-READ-GMA-AREA.                                                 CL*98
04360                                                                      CL137
04361 ******************************************************************   CL137
04362 *    READ MERCHANDISE AREA CURSOR -- GMA ONLY                        CL137
04363 ******************************************************************   CL137
04364                                                                      CL*97
04365      EXEC SQL                                                        CL*97
04366          FETCH GMA-CURSOR                                            CL111
04367           INTO :MDSEAR-OPERCO-NBR,                                   CL*97
04368                :MDSEAR-GMA-NBR,                                      CL*97
04369                :MDSEAR-DMA-NBR,                                      CL*97
04370                :MDSEAR-DEPT-NBR,                                     CL*97
04371                :MDSEAR-MAJ-CL-NBR,                                   CL*97
04372                :MDSEAR-SUB-CL-NBR,                                   CL*97
04373                :MDSEAR-MDSELV-CDE,                                   CL*97
04374                :MDSEAR-CHG-DTE,                                      CL*97
04375                :MDSEAR-CRE-DTE,                                      CL*97
04376                :MDSEAR-STRT-DTE,                                     CL*97
04377                :MDSEAR-END-DTE,                                      CL*97
04378                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL*97
04379                :MDSEAR-DESC,                                         CL*97
04380                :MDSEAR-BYR-NBR,                                      CL108
04381                :MDSEAR-LIST-SEQ-NBR                                  CL108
04382      END-EXEC.                                                       CL*97
04383                                                                      CL*97
04384      SET PS-DB2-OK TO TRUE                                           CL*97
04385                                                                      CL*97
04386      IF (SQLCODE = -904) OR                                          CL*97
04387         (SQLCODE = -913)                                             CL*97
04388          SET PS-DB2-RETRY TO TRUE                                    CL*97
04389          ADD 1 TO PC-RETRY-CNTR                                      CL*97
04390          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL*97
04391              SET PS-DB2-OK TO TRUE                                   CL*97
04392          END-IF                                                      CL*97
04393      END-IF.                                                         CL*97
04394                                                                      CL*97
04395      IF PS-DB2-OK                                                    CL*97
04396          IF SQLCODE = +0                                             CL*97
04397              CONTINUE                                                CL*97
04398          ELSE                                                        CL*97
04399              IF SQLCODE = +100                                       CL*97
04400                  SET MB102-NO-ITEMS-LEFT-ON-DB TO TRUE               CL209
04401              ELSE                                                    CL*97
04402                  SET DP013-DB2-ABEND   TO TRUE                       CL*97
04403                  SET DP013-NO-ROLLBACK TO TRUE                       CL*97
04404                  MOVE 'Z310-READ-GMA-CURSOR'   TO                    CL119
04405                       DP013-PARAGRAPH                                CL*97
04406                  MOVE PC-OPERCO-NBR            TO                    CL*97
04407                       AM-OPERCO-NBR-13                               CL*97
04408                  MOVE MB102-SAVE-GMA           TO                    CL209
04409                       AM-GMA-NBR-13                                  CL*97
04410                  MOVE MB102-SAVE-DMA           TO                    CL209
04411                       AM-DMA-NBR-13                                  CL*97
04412                  MOVE MB102-SAVE-DPT           TO                    CL209
04413                       AM-DEPT-NBR-13                                 CL*97
04414                  MOVE MB102-SAVE-MCL           TO                    CL209
04415                       AM-MCL-NBR-13                                  CL*97
04416                  MOVE MB102-SAVE-SCL           TO                    CL209
04417                       AM-SCL-NBR-13                                  CL*97
04418                  MOVE AM-ABEND-MESSAGE-13      TO                    CL*97
04419                       DP013-MESSAGE-TEXT(1)                          CL*97
04420                  MOVE SQLCA                    TO                    CL*97
04421                       DP013-SQLCA                                    CL*97
04422                  PERFORM DP013-0000-PROCESS-ABEND                    CL*97
04423              END-IF                                                  CL*97
04424          END-IF                                                      CL*97
04425      END-IF.                                                         CL*97
04426  EJECT                                                               CL*97
04427  Z320-CLOSE-GMA-CURSOR.                                              CL112
04428                                                                      CL*97
04429 ******************************************************************   CL*97
04430 *    CLOSE THE GMA-CURSOR                                            CL*98
04431 ******************************************************************   CL*97
04432                                                                      CL*97
04433      EXEC SQL                                                        CL*97
04434          CLOSE GMA-CURSOR                                            CL*98
04435      END-EXEC.                                                       CL*97
04436                                                                      CL*97
04437      IF (SQLCODE = +0)   OR                                          CL*97
04438         (SQLCODE = +100)                                             CL*97
04439          SET PS-GMA-CURSOR-NOT-OPENED TO TRUE                        CL*98
04440      ELSE                                                            CL*97
04441          SET DP013-DB2-ABEND            TO TRUE                      CL*97
04442          SET DP013-NO-ROLLBACK          TO TRUE                      CL*97
04443          MOVE 'Z310-CLOSE-GMA-CURSOR'   TO                           CL*98
04444               DP013-PARAGRAPH                                        CL*97
04445          MOVE AM-ABEND-MESSAGE-22       TO                           CL*97
04446               DP013-MESSAGE-TEXT(1)                                  CL*97
04447          MOVE SQLCA                     TO                           CL*97
04448               DP013-SQLCA                                            CL*97
04449          PERFORM DP013-0000-PROCESS-ABEND                            CL*97
04450      END-IF.                                                         CL*97
04451  EJECT                                                               CL*97
04452  Z400-OPEN-DMA-CURSOR.                                               CL119
04453                                                                      CL119
04454 ******************************************************************   CL119
04455 *    OPEN MERCHANDISE AREA CURSOR -- DMA ONLY                        CL119
04456 *                                                                    CL203
04457 *    PERFORMEN BY D500-OPEN-CURSOR.                                  CL203
04458 ******************************************************************   CL119
04459                                                                      CL119
04460      EXEC SQL                                                        CL119
04461          OPEN DMA-CURSOR                                             CL119
04462      END-EXEC.                                                       CL119
04463                                                                      CL119
04464      IF (SQLCODE = +0)   OR                                          CL119
04465         (SQLCODE = +100)                                             CL119
04466          SET PS-DMA-CURSOR-OPENED       TO TRUE                      CL119
04467      ELSE                                                            CL119
04468          SET DP013-DB2-ABEND            TO TRUE                      CL119
04469          SET DP013-NO-ROLLBACK          TO TRUE                      CL119
04470          MOVE 'Z400-OPEN-DMA-CURSOR'    TO                           CL119
04471               DP013-PARAGRAPH                                        CL119
04472          MOVE AM-ABEND-MESSAGE-21       TO                           CL119
04473               DP013-MESSAGE-TEXT(1)                                  CL119
04474          MOVE SQLCA                     TO                           CL119
04475               DP013-SQLCA                                            CL119
04476          PERFORM DP013-0000-PROCESS-ABEND                            CL119
04477      END-IF.                                                         CL119
04478  EJECT                                                               CL119
04479  Z410-READ-DMA-AREA.                                                 CL119
04480                                                                      CL137
04481 ******************************************************************   CL137
04482 *    READ MERCHANDISE AREA CURSOR -- DMA ONLY                        CL137
04483 ******************************************************************   CL137
04484                                                                      CL119
04485      EXEC SQL                                                        CL119
04486          FETCH DMA-CURSOR                                            CL119
04487           INTO :MDSEAR-OPERCO-NBR,                                   CL119
04488                :MDSEAR-GMA-NBR,                                      CL119
04489                :MDSEAR-DMA-NBR,                                      CL119
04490                :MDSEAR-DEPT-NBR,                                     CL119
04491                :MDSEAR-MAJ-CL-NBR,                                   CL119
04492                :MDSEAR-SUB-CL-NBR,                                   CL119
04493                :MDSEAR-MDSELV-CDE,                                   CL119
04494                :MDSEAR-CHG-DTE,                                      CL119
04495                :MDSEAR-CRE-DTE,                                      CL119
04496                :MDSEAR-STRT-DTE,                                     CL119
04497                :MDSEAR-END-DTE,                                      CL119
04498                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL119
04499                :MDSEAR-DESC,                                         CL119
04500                :MDSEAR-BYR-NBR,                                      CL119
04501                :MDSEAR-LIST-SEQ-NBR                                  CL119
04502      END-EXEC.                                                       CL119
04503                                                                      CL119
04504      SET PS-DB2-OK TO TRUE                                           CL119
04505                                                                      CL119
04506      IF (SQLCODE = -904) OR                                          CL119
04507         (SQLCODE = -913)                                             CL119
04508          SET PS-DB2-RETRY TO TRUE                                    CL119
04509          ADD 1 TO PC-RETRY-CNTR                                      CL119
04510          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL119
04511              SET PS-DB2-OK TO TRUE                                   CL119
04512          END-IF                                                      CL119
04513      END-IF.                                                         CL119
04514                                                                      CL119
04515      IF PS-DB2-OK                                                    CL119
04516          IF SQLCODE = +0                                             CL119
04517              CONTINUE                                                CL119
04518          ELSE                                                        CL119
04519              IF SQLCODE = +100                                       CL119
04520                  SET MB102-NO-ITEMS-LEFT-ON-DB TO TRUE               CL209
04521              ELSE                                                    CL119
04522                  SET DP013-DB2-ABEND   TO TRUE                       CL119
04523                  SET DP013-NO-ROLLBACK TO TRUE                       CL119
04524                  MOVE 'Z410-READ-DMA-CURSOR'   TO                    CL119
04525                       DP013-PARAGRAPH                                CL119
04526                  MOVE PC-OPERCO-NBR            TO                    CL119
04527                       AM-OPERCO-NBR-13                               CL119
04528                  MOVE MB102-SAVE-GMA           TO                    CL209
04529                       AM-GMA-NBR-13                                  CL119
04530                  MOVE MB102-SAVE-DMA           TO                    CL209
04531                       AM-DMA-NBR-13                                  CL119
04532                  MOVE MB102-SAVE-DPT           TO                    CL209
04533                       AM-DEPT-NBR-13                                 CL119
04534                  MOVE MB102-SAVE-MCL           TO                    CL209
04535                       AM-MCL-NBR-13                                  CL119
04536                  MOVE MB102-SAVE-SCL           TO                    CL209
04537                       AM-SCL-NBR-13                                  CL119
04538                  MOVE AM-ABEND-MESSAGE-13      TO                    CL119
04539                       DP013-MESSAGE-TEXT(1)                          CL119
04540                  MOVE SQLCA                    TO                    CL119
04541                       DP013-SQLCA                                    CL119
04542                  PERFORM DP013-0000-PROCESS-ABEND                    CL119
04543              END-IF                                                  CL119
04544          END-IF                                                      CL119
04545      END-IF.                                                         CL119
04546  EJECT                                                               CL119
04547  Z420-CLOSE-DMA-CURSOR.                                              CL119
04548                                                                      CL119
04549 ******************************************************************   CL119
04550 *    CLOSE THE DMA-CURSOR                                            CL119
04551 ******************************************************************   CL119
04552                                                                      CL119
04553      EXEC SQL                                                        CL119
04554          CLOSE DMA-CURSOR                                            CL119
04555      END-EXEC.                                                       CL119
04556                                                                      CL119
04557      IF (SQLCODE = +0)   OR                                          CL119
04558         (SQLCODE = +100)                                             CL119
04559          SET PS-DMA-CURSOR-NOT-OPENED TO TRUE                        CL119
04560      ELSE                                                            CL119
04561          SET DP013-DB2-ABEND            TO TRUE                      CL119
04562          SET DP013-NO-ROLLBACK          TO TRUE                      CL119
04563          MOVE 'Z410-CLOSE-DMA-CURSOR'   TO                           CL119
04564               DP013-PARAGRAPH                                        CL119
04565          MOVE AM-ABEND-MESSAGE-22       TO                           CL119
04566               DP013-MESSAGE-TEXT(1)                                  CL119
04567          MOVE SQLCA                     TO                           CL119
04568               DP013-SQLCA                                            CL119
04569          PERFORM DP013-0000-PROCESS-ABEND                            CL119
04570      END-IF.                                                         CL119
04571  EJECT                                                               CL119
04572  Z500-OPEN-DPT-CURSOR.                                               CL131
04573                                                                      CL129
04574 ******************************************************************   CL129
04575 *    OPEN MERCHANDISE AREA CURSOR -- DPT ONLY                        CL129
04576 *                                                                    CL203
04577 *    PERFORMEN BY D500-OPEN-CURSOR.                                  CL203
04578 ******************************************************************   CL129
04579                                                                      CL129
04580      EXEC SQL                                                        CL129
04581          OPEN DPT-CURSOR                                             CL129
04582      END-EXEC.                                                       CL129
04583                                                                      CL129
04584      IF (SQLCODE = +0)   OR                                          CL129
04585         (SQLCODE = +100)                                             CL129
04586          SET PS-DPT-CURSOR-OPENED       TO TRUE                      CL129
04587      ELSE                                                            CL129
04588          SET DP013-DB2-ABEND            TO TRUE                      CL129
04589          SET DP013-NO-ROLLBACK          TO TRUE                      CL129
04590          MOVE 'Z500-OPEN-DPT-CURSOR'    TO                           CL129
04591               DP013-PARAGRAPH                                        CL129
04592          MOVE AM-ABEND-MESSAGE-21       TO                           CL129
04593               DP013-MESSAGE-TEXT(1)                                  CL129
04594          MOVE SQLCA                     TO                           CL129
04595               DP013-SQLCA                                            CL129
04596          PERFORM DP013-0000-PROCESS-ABEND                            CL129
04597      END-IF.                                                         CL129
04598  EJECT                                                               CL129
04599  Z510-READ-DPT-AREA.                                                 CL129
04600                                                                      CL137
04601 ******************************************************************   CL137
04602 *    READ MERCHANDISE AREA CURSOR -- DPT ONLY                        CL137
04603 ******************************************************************   CL137
04604                                                                      CL129
04605      EXEC SQL                                                        CL129
04606          FETCH DPT-CURSOR                                            CL129
04607           INTO :MDSEAR-OPERCO-NBR,                                   CL129
04608                :MDSEAR-GMA-NBR,                                      CL129
04609                :MDSEAR-DMA-NBR,                                      CL129
04610                :MDSEAR-DEPT-NBR,                                     CL129
04611                :MDSEAR-MAJ-CL-NBR,                                   CL129
04612                :MDSEAR-SUB-CL-NBR,                                   CL129
04613                :MDSEAR-MDSELV-CDE,                                   CL129
04614                :MDSEAR-CHG-DTE,                                      CL129
04615                :MDSEAR-CRE-DTE,                                      CL129
04616                :MDSEAR-STRT-DTE,                                     CL129
04617                :MDSEAR-END-DTE,                                      CL129
04618                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL129
04619                :MDSEAR-DESC,                                         CL129
04620                :MDSEAR-BYR-NBR,                                      CL129
04621                :MDSEAR-LIST-SEQ-NBR                                  CL129
04622      END-EXEC.                                                       CL129
04623                                                                      CL129
04624      SET PS-DB2-OK TO TRUE                                           CL129
04625                                                                      CL129
04626      IF (SQLCODE = -904) OR                                          CL129
04627         (SQLCODE = -913)                                             CL129
04628          SET PS-DB2-RETRY TO TRUE                                    CL129
04629          ADD 1 TO PC-RETRY-CNTR                                      CL129
04630          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL129
04631              SET PS-DB2-OK TO TRUE                                   CL129
04632          END-IF                                                      CL129
04633      END-IF.                                                         CL129
04634                                                                      CL129
04635      IF PS-DB2-OK                                                    CL129
04636          IF SQLCODE = +0                                             CL129
04637              CONTINUE                                                CL129
04638          ELSE                                                        CL129
04639              IF SQLCODE = +100                                       CL129
04640                  SET MB102-NO-ITEMS-LEFT-ON-DB TO TRUE               CL209
04641              ELSE                                                    CL129
04642                  SET DP013-DB2-ABEND   TO TRUE                       CL129
04643                  SET DP013-NO-ROLLBACK TO TRUE                       CL129
04644                  MOVE 'Z510-READ-DPT-CURSOR'   TO                    CL129
04645                       DP013-PARAGRAPH                                CL129
04646                  MOVE PC-OPERCO-NBR            TO                    CL129
04647                       AM-OPERCO-NBR-13                               CL129
04648                  MOVE MB102-SAVE-GMA           TO                    CL209
04649                       AM-GMA-NBR-13                                  CL129
04650                  MOVE MB102-SAVE-DMA           TO                    CL209
04651                       AM-DMA-NBR-13                                  CL129
04652                  MOVE MB102-SAVE-DPT           TO                    CL209
04653                       AM-DEPT-NBR-13                                 CL129
04654                  MOVE MB102-SAVE-MCL           TO                    CL209
04655                       AM-MCL-NBR-13                                  CL129
04656                  MOVE MB102-SAVE-SCL           TO                    CL209
04657                       AM-SCL-NBR-13                                  CL129
04658                  MOVE AM-ABEND-MESSAGE-13      TO                    CL129
04659                       DP013-MESSAGE-TEXT(1)                          CL129
04660                  MOVE SQLCA                    TO                    CL129
04661                       DP013-SQLCA                                    CL129
04662                  PERFORM DP013-0000-PROCESS-ABEND                    CL129
04663              END-IF                                                  CL129
04664          END-IF                                                      CL129
04665      END-IF.                                                         CL129
04666  EJECT                                                               CL129
04667  Z520-CLOSE-DPT-CURSOR.                                              CL129
04668                                                                      CL129
04669 ******************************************************************   CL129
04670 *    CLOSE THE DPT-CURSOR                                            CL148
04671 ******************************************************************   CL129
04672                                                                      CL129
04673      EXEC SQL                                                        CL129
04674          CLOSE DPT-CURSOR                                            CL129
04675      END-EXEC.                                                       CL129
04676                                                                      CL129
04677      IF (SQLCODE = +0)   OR                                          CL129
04678         (SQLCODE = +100)                                             CL129
04679          SET PS-DPT-CURSOR-NOT-OPENED TO TRUE                        CL129
04680      ELSE                                                            CL129
04681          SET DP013-DB2-ABEND            TO TRUE                      CL129
04682          SET DP013-NO-ROLLBACK          TO TRUE                      CL129
04683          MOVE 'Z520-CLOSE-DPT-CURSOR'   TO                           CL148
04684               DP013-PARAGRAPH                                        CL129
04685          MOVE AM-ABEND-MESSAGE-22       TO                           CL129
04686               DP013-MESSAGE-TEXT(1)                                  CL129
04687          MOVE SQLCA                     TO                           CL129
04688               DP013-SQLCA                                            CL129
04689          PERFORM DP013-0000-PROCESS-ABEND                            CL129
04690      END-IF.                                                         CL129
04691  EJECT                                                               CL129
04692  Z600-OPEN-MCL-CURSOR.                                               CL148
04693                                                                      CL148
04694 ******************************************************************   CL148
04695 *    OPEN MERCHANDISE AREA CURSOR -- MCL ONLY                        CL148
04696 *                                                                    CL203
04697 *    PERFORMEN BY D500-OPEN-CURSOR.                                  CL203
04698 ******************************************************************   CL148
04699                                                                      CL148
04700      EXEC SQL                                                        CL148
04701          OPEN MCL-CURSOR                                             CL148
04702      END-EXEC.                                                       CL148
04703                                                                      CL148
04704      IF (SQLCODE = +0)   OR                                          CL148
04705         (SQLCODE = +100)                                             CL148
04706          SET PS-MCL-CURSOR-OPENED       TO TRUE                      CL148
04707      ELSE                                                            CL148
04708          SET DP013-DB2-ABEND            TO TRUE                      CL148
04709          SET DP013-NO-ROLLBACK          TO TRUE                      CL148
04710          MOVE 'Z600-OPEN-MCL-CURSOR'    TO                           CL148
04711               DP013-PARAGRAPH                                        CL148
04712          MOVE AM-ABEND-MESSAGE-21       TO                           CL148
04713               DP013-MESSAGE-TEXT(1)                                  CL148
04714          MOVE SQLCA                     TO                           CL148
04715               DP013-SQLCA                                            CL148
04716          PERFORM DP013-0000-PROCESS-ABEND                            CL148
04717      END-IF.                                                         CL148
04718  EJECT                                                               CL148
04719  Z610-READ-MCL-AREA.                                                 CL148
04720                                                                      CL148
04721 ******************************************************************   CL148
04722 *    READ MERCHANDISE AREA CURSOR -- MCL ONLY                        CL148
04723 ******************************************************************   CL148
04724                                                                      CL148
04725      EXEC SQL                                                        CL148
04726          FETCH MCL-CURSOR                                            CL148
04727           INTO :MDSEAR-OPERCO-NBR,                                   CL148
04728                :MDSEAR-GMA-NBR,                                      CL148
04729                :MDSEAR-DMA-NBR,                                      CL148
04730                :MDSEAR-DEPT-NBR,                                     CL148
04731                :MDSEAR-MAJ-CL-NBR,                                   CL148
04732                :MDSEAR-SUB-CL-NBR,                                   CL148
04733                :MDSEAR-MDSELV-CDE,                                   CL148
04734                :MDSEAR-CHG-DTE,                                      CL148
04735                :MDSEAR-CRE-DTE,                                      CL148
04736                :MDSEAR-STRT-DTE,                                     CL148
04737                :MDSEAR-END-DTE,                                      CL148
04738                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL148
04739                :MDSEAR-DESC,                                         CL148
04740                :MDSEAR-BYR-NBR,                                      CL148
04741                :MDSEAR-LIST-SEQ-NBR                                  CL148
04742      END-EXEC.                                                       CL148
04743                                                                      CL148
04744      SET PS-DB2-OK TO TRUE                                           CL148
04745                                                                      CL148
04746      IF (SQLCODE = -904) OR                                          CL148
04747         (SQLCODE = -913)                                             CL148
04748          SET PS-DB2-RETRY TO TRUE                                    CL148
04749          ADD 1 TO PC-RETRY-CNTR                                      CL148
04750          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL148
04751              SET PS-DB2-OK TO TRUE                                   CL148
04752          END-IF                                                      CL148
04753      END-IF.                                                         CL148
04754                                                                      CL148
04755      IF PS-DB2-OK                                                    CL148
04756          IF SQLCODE = +0                                             CL148
04757              CONTINUE                                                CL148
04758          ELSE                                                        CL148
04759              IF SQLCODE = +100                                       CL148
04760                  SET MB102-NO-ITEMS-LEFT-ON-DB TO TRUE               CL209
04761              ELSE                                                    CL148
04762                  SET DP013-DB2-ABEND   TO TRUE                       CL148
04763                  SET DP013-NO-ROLLBACK TO TRUE                       CL148
04764                  MOVE 'Z610-READ-MCL-CURSOR'   TO                    CL148
04765                       DP013-PARAGRAPH                                CL148
04766                  MOVE PC-OPERCO-NBR            TO                    CL148
04767                       AM-OPERCO-NBR-13                               CL148
04768                  MOVE MB102-SAVE-GMA           TO                    CL209
04769                       AM-GMA-NBR-13                                  CL148
04770                  MOVE MB102-SAVE-DMA           TO                    CL209
04771                       AM-DMA-NBR-13                                  CL148
04772                  MOVE MB102-SAVE-DPT           TO                    CL209
04773                       AM-DEPT-NBR-13                                 CL148
04774                  MOVE MB102-SAVE-MCL           TO                    CL209
04775                       AM-MCL-NBR-13                                  CL148
04776                  MOVE MB102-SAVE-SCL           TO                    CL209
04777                       AM-SCL-NBR-13                                  CL148
04778                  MOVE AM-ABEND-MESSAGE-13      TO                    CL148
04779                       DP013-MESSAGE-TEXT(1)                          CL148
04780                  MOVE SQLCA                    TO                    CL148
04781                       DP013-SQLCA                                    CL148
04782                  PERFORM DP013-0000-PROCESS-ABEND                    CL148
04783              END-IF                                                  CL148
04784          END-IF                                                      CL148
04785      END-IF.                                                         CL148
04786  EJECT                                                               CL148
04787  Z620-CLOSE-MCL-CURSOR.                                              CL148
04788                                                                      CL148
04789 ******************************************************************   CL148
04790 *    CLOSE THE MCL-CURSOR                                            CL148
04791 ******************************************************************   CL148
04792                                                                      CL148
04793      EXEC SQL                                                        CL148
04794          CLOSE MCL-CURSOR                                            CL148
04795      END-EXEC.                                                       CL148
04796                                                                      CL148
04797      IF (SQLCODE = +0)   OR                                          CL148
04798         (SQLCODE = +100)                                             CL148
04799          SET PS-MCL-CURSOR-NOT-OPENED TO TRUE                        CL148
04800      ELSE                                                            CL148
04801          SET DP013-DB2-ABEND            TO TRUE                      CL148
04802          SET DP013-NO-ROLLBACK          TO TRUE                      CL148
04803          MOVE 'Z620-CLOSE-MCL-CURSOR'   TO                           CL148
04804               DP013-PARAGRAPH                                        CL148
04805          MOVE AM-ABEND-MESSAGE-22       TO                           CL148
04806               DP013-MESSAGE-TEXT(1)                                  CL148
04807          MOVE SQLCA                     TO                           CL148
04808               DP013-SQLCA                                            CL148
04809          PERFORM DP013-0000-PROCESS-ABEND                            CL148
04810      END-IF.                                                         CL148
04811  EJECT                                                               CL148
04812  Z700-OPEN-SCL-CURSOR.                                               CL150
04813                                                                      CL150
04814 ******************************************************************   CL150
04815 *    OPEN MERCHANDISE AREA CURSOR -- SCL ONLY                        CL150
04816 *                                                                    CL203
04817 *    PERFORMEN BY D500-OPEN-CURSOR.                                  CL203
04818 ******************************************************************   CL150
04819                                                                      CL150
04820      EXEC SQL                                                        CL150
04821          OPEN SCL-CURSOR                                             CL150
04822      END-EXEC.                                                       CL150
04823                                                                      CL150
04824      IF (SQLCODE = +0)   OR                                          CL150
04825         (SQLCODE = +100)                                             CL150
04826          SET PS-SCL-CURSOR-OPENED       TO TRUE                      CL150
04827      ELSE                                                            CL150
04828          SET DP013-DB2-ABEND            TO TRUE                      CL150
04829          SET DP013-NO-ROLLBACK          TO TRUE                      CL150
04830          MOVE 'Z700-OPEN-SCL-CURSOR'    TO                           CL150
04831               DP013-PARAGRAPH                                        CL150
04832          MOVE AM-ABEND-MESSAGE-21       TO                           CL150
04833               DP013-MESSAGE-TEXT(1)                                  CL150
04834          MOVE SQLCA                     TO                           CL150
04835               DP013-SQLCA                                            CL150
04836          PERFORM DP013-0000-PROCESS-ABEND                            CL150
04837      END-IF.                                                         CL150
04838  EJECT                                                               CL150
04839  Z710-READ-SCL-AREA.                                                 CL150
04840                                                                      CL150
04841 ******************************************************************   CL150
04842 *    READ MERCHANDISE AREA CURSOR -- SCL ONLY                        CL150
04843 ******************************************************************   CL150
04844                                                                      CL150
04845      EXEC SQL                                                        CL150
04846          FETCH SCL-CURSOR                                            CL151
04847           INTO :MDSEAR-OPERCO-NBR,                                   CL150
04848                :MDSEAR-GMA-NBR,                                      CL150
04849                :MDSEAR-DMA-NBR,                                      CL150
04850                :MDSEAR-DEPT-NBR,                                     CL150
04851                :MDSEAR-MAJ-CL-NBR,                                   CL150
04852                :MDSEAR-SUB-CL-NBR,                                   CL150
04853                :MDSEAR-MDSELV-CDE,                                   CL150
04854                :MDSEAR-CHG-DTE,                                      CL150
04855                :MDSEAR-CRE-DTE,                                      CL150
04856                :MDSEAR-STRT-DTE,                                     CL150
04857                :MDSEAR-END-DTE,                                      CL150
04858                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL150
04859                :MDSEAR-DESC,                                         CL150
04860                :MDSEAR-BYR-NBR,                                      CL150
04861                :MDSEAR-LIST-SEQ-NBR                                  CL150
04862      END-EXEC.                                                       CL150
04863                                                                      CL150
04864      SET PS-DB2-OK TO TRUE                                           CL150
04865                                                                      CL150
04866      IF (SQLCODE = -904) OR                                          CL150
04867         (SQLCODE = -913)                                             CL150
04868          SET PS-DB2-RETRY TO TRUE                                    CL150
04869          ADD 1 TO PC-RETRY-CNTR                                      CL150
04870          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL150
04871              SET PS-DB2-OK TO TRUE                                   CL150
04872          END-IF                                                      CL150
04873      END-IF.                                                         CL150
04874                                                                      CL150
04875      IF PS-DB2-OK                                                    CL150
04876          IF SQLCODE = +0                                             CL150
04877              CONTINUE                                                CL150
04878          ELSE                                                        CL150
04879              IF SQLCODE = +100                                       CL150
04880                  SET MB102-NO-ITEMS-LEFT-ON-DB TO TRUE               CL209
04881              ELSE                                                    CL150
04882                  SET DP013-DB2-ABEND   TO TRUE                       CL150
04883                  SET DP013-NO-ROLLBACK TO TRUE                       CL150
04884                  MOVE 'Z710-READ-SCL-CURSOR'   TO                    CL150
04885                       DP013-PARAGRAPH                                CL150
04886                  MOVE PC-OPERCO-NBR            TO                    CL150
04887                       AM-OPERCO-NBR-13                               CL150
04888                  MOVE MB102-SAVE-GMA           TO                    CL209
04889                       AM-GMA-NBR-13                                  CL150
04890                  MOVE MB102-SAVE-DMA           TO                    CL209
04891                       AM-DMA-NBR-13                                  CL150
04892                  MOVE MB102-SAVE-DPT           TO                    CL209
04893                       AM-DEPT-NBR-13                                 CL150
04894                  MOVE MB102-SAVE-MCL           TO                    CL209
04895                       AM-MCL-NBR-13                                  CL150
04896                  MOVE MB102-SAVE-SCL           TO                    CL209
04897                       AM-SCL-NBR-13                                  CL150
04898                  MOVE AM-ABEND-MESSAGE-13      TO                    CL150
04899                       DP013-MESSAGE-TEXT(1)                          CL150
04900                  MOVE SQLCA                    TO                    CL150
04901                       DP013-SQLCA                                    CL150
04902                  PERFORM DP013-0000-PROCESS-ABEND                    CL150
04903              END-IF                                                  CL150
04904          END-IF                                                      CL150
04905      END-IF.                                                         CL150
04906  EJECT                                                               CL150
04907  Z720-CLOSE-SCL-CURSOR.                                              CL150
04908                                                                      CL150
04909 ******************************************************************   CL150
04910 *    CLOSE THE SCL-CURSOR                                            CL150
04911 ******************************************************************   CL150
04912                                                                      CL150
04913      EXEC SQL                                                        CL150
04914          CLOSE SCL-CURSOR                                            CL150
04915      END-EXEC.                                                       CL150
04916                                                                      CL150
04917      IF (SQLCODE = +0)   OR                                          CL150
04918         (SQLCODE = +100)                                             CL150
04919          SET PS-SCL-CURSOR-NOT-OPENED TO TRUE                        CL150
04920      ELSE                                                            CL150
04921          SET DP013-DB2-ABEND            TO TRUE                      CL150
04922          SET DP013-NO-ROLLBACK          TO TRUE                      CL150
04923          MOVE 'Z720-CLOSE-SCL-CURSOR'   TO                           CL150
04924               DP013-PARAGRAPH                                        CL150
04925          MOVE AM-ABEND-MESSAGE-22       TO                           CL150
04926               DP013-MESSAGE-TEXT(1)                                  CL150
04927          MOVE SQLCA                     TO                           CL150
04928               DP013-SQLCA                                            CL150
04929          PERFORM DP013-0000-PROCESS-ABEND                            CL150
04930      END-IF.                                                         CL150
04931  EJECT                                                               CL150
04932 ******************************************************************   CL*42
04933 *    FILL IN SCREEN HEADER MODULE                                    CL*42
04934 ******************************************************************   CL*42
04935                                                                      CL*42
04936      COPY DPPD002.                                                   CL*42
04937  EJECT                                                               CL*42
04938 ******************************************************************MBKCS102
04939 *    RETURN TO CICS MODULE                                        MBKCS102
04940 ******************************************************************MBKCS102
04941                                                                   MBKCS102
04942      COPY DPPD003.                                                   CL**3
04943  EJECT                                                            MBKCS102
04944 ******************************************************************MBKCS102
04945 *    FORMAT MESSAGE LINES MODULE                                  MBKCS102
04946 ******************************************************************MBKCS102
04947                                                                   MBKCS102
04948      COPY DPPD005.                                                   CL184
04949  EJECT                                                            MBKCS102
04950 ******************************************************************MBKCS102
04951 *    GET PANEL MODULE                                             MBKCS102
04952 ******************************************************************MBKCS102
04953                                                                   MBKCS102
04954      COPY DPPD007.                                                   CL**3
04955  EJECT                                                            MBKCS102
04956 ******************************************************************MBKCS102
04957 *    WRITE PANEL MODULE                                           MBKCS102
04958 ******************************************************************MBKCS102
04959                                                                   MBKCS102
04960      COPY DPPD008.                                                   CL**3
04961  EJECT                                                            MBKCS102
04962 ******************************************************************MBKCS102
04963 *    TOGGLE THE FUNCTION KEY LINE MODULE                          MBKCS102
04964 ******************************************************************MBKCS102
04965                                                                   MBKCS102
04966      COPY DPPD009.                                                   CL**3
04967  EJECT                                                            MBKCS102
04968 ******************************************************************MBKCS102
04969 *    ABEND PROCESSOR MODULE                                       MBKCS102
04970 ******************************************************************MBKCS102
04971                                                                   MBKCS102
04972      COPY DPPD013.                                                   CL**3
04973  EJECT                                                            MBKCS102
04974 ******************************************************************MBKCS102
04975 *    CHECK COMMUNICATION AREA MODULE                              MBKCS102
04976 ******************************************************************MBKCS102
04977                                                                   MBKCS102
04978      COPY DPPD015.                                                   CL**3
04979  EJECT                                                            MBKCS102
04980 ******************************************************************MBKCS102
04981 *    UPDATE TIME AND DATE MODULE                                  MBKCS102
04982 ******************************************************************MBKCS102
04983                                                                   MBKCS102
04984      COPY DPPD017.                                                   CL**3
04985  EJECT                                                            MBKCS102
