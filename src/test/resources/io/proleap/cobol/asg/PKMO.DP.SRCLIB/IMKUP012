       IDENTIFICATION DIVISION.                                         00010023
                                                                        00020024
       PROGRAM-ID.    IMKUP012                                          00030024
       AUTHOR.        PAUL KEBER.                                       00040024
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050024
       DATE-WRITTEN.  11-03-99.                                         00060024
       DATE-COMPILED.                                                   00070024
                                                                        00080024
      ******************************************************************00090024
      *     IMKUP012 - VSM TABLES NON-PURGED PURGE                     *00100024
      ******************************************************************00110024
      *     THIS PROGRAM WILL PURGE ROWS FROM THE TVNDSTL, TMDARVS,    *00120024
      * AND TUPC DB2 TABLES WHICH HAD BEEN SELECTED TO BE PURGED, BUT  *00120024
      * HAD NOT BEEN PURGED BECAUSE THERE WERE NO TUPCPLS ROWS FOR     *00120024
      * THE VENDOR STYLE ID.  COMMITS ARE TAKEN EVERY TEN SECONDS, AND *00120024
      * CHECKPOINT RESTART LOGIC IS INCORPORATED.                      *00180024
      *                                                                *00200031
      *    WR/PITS#     DATE        DESCRIPTION                        *00200031
      *    --------   --------     ---------------------------------   *00210031
      *    ECOMM      11/15/00     LRECL CHANGES TO TUPC AND TMDARVS   *        
      *                                                                *00220031
      *    ECOMM      12/20/00     P. KEBER   PHASE 2 SKU LEVEL CHGS   *00220031
      *    DECREASE SIZE OF TMDARVS-PURGE-RECORD TO REMOVE             *00220031
      *    SHIP_RSTRC_SEQ_NBR, E_PRD_GP_NBR AND E_PRD_GP_IND.          *        
      *                                                                *00220031
      *    ECOMM       3/5/01      D. THEEL                            *00220031
      *    INCREASE SIZE OF TUPC FILE TO REFLECT NEW COLUMN TX_PRD_    *00220031
      *    CDE_SEQ_NBR.                                                *        
      *                                                                *00220031
      *    ECOMM       4/6/01      D. THEEL                            *00220031
      *    INCREASE SIZE OF TUPC FILE TO REFLECT NEW COLUMN CUSTFACG_  *00220031
      *    CLOR_DESC.                                                  *        
      ******************************************************************        
      *    WR22746   12/12/01      D. THEEL                            *00220031
      *    INCREASE SIZE OF TUPC FILE TO REFLECT NEW COLUMN INTR_UPC_  *00220031
      *    DESC.                                                       *        
      ******************************************************************        
      *    WR23443    3/5/02      D. THEEL                             *00220031
      *    INCREASE SIZE OF TMDARVS FILE TO REFLECT NEW COLUMN STYL_   *00220031
      *    RECLS_IND.                                                  *        
      ******************************************************************        
      *    WR23220    5/8/02      D. THEEL                             *00220031
      *    INCREASE SIZE OF TMDARVS FILE TO REFLECT NEW COLUMN RTL_    *00220031
      *    CLSN_SEQ_NBR.                                               *        
      ******************************************************************        
      *    WR23749    5/28/02     D. THEEL                             *00220031
      *    INCREASE SIZE OF TMDARVS FILE TO REFLECT NEW COLS DEPT_NBR, *00220031
      *    MAJ_CL_NBR, AND SUB_CL_NBR.                                 *        
      ******************************************************************        
      *    WR23861    8/07/02     D. THEEL                             *00220031
      *    INCREASE SIZE OF TMDARVS FILE TO REFLECT NEW COLUMN         *00220031
      *    ESL SEQ NBR.                                                *        
      ******************************************************************        
      *    WR25951    1/21/04     D. THEEL                             *00220031
      *    INCREASE SIZE OF TMDARVS FILE TO REFLECT NEW COLUMNS.       *00220031
      ******************************************************************        
      *    WR26741    4/23/04     P. KEBER                             *00220031
      *    INCREASE SIZE OF TMDARVS FILE FOR NEW MAP_UNT_RTL_AMT.      *00220031
      ******************************************************************        
204416*    CHG0204416  07/31/18    Change the definitions of           *        
204416*                            PC-1000000000 and PC-2000000000     *00270024
204416*                            to eliminate the hex values which   *        
204416*                            fail in the mainframe refactoring   *        
204416*                            process.                            *        
204416******************************************************************00270024
                                                                        00280024
                                                                        00290024
       ENVIRONMENT DIVISION.                                            00300024
                                                                        00310024
       CONFIGURATION SECTION.                                           00320024
       SOURCE-COMPUTER.        IBM-3090.                                00330024
       OBJECT-COMPUTER.        IBM-3090.                                00340024
       INPUT-OUTPUT SECTION.                                            00350024
       FILE-CONTROL.                                                    00360024
           SELECT TVNDSTL-PURGE-FILE                                    00370024
               ASSIGN TO INP01.                                         00380024
                                                                        00390024
           SELECT TMDARVS-PURGE-FILE                                    00370024
               ASSIGN TO INP02.                                         00380024
                                                                        00390024
           SELECT TUPC-PURGE-FILE                                       00370024
               ASSIGN TO INP03.                                         00380024
                                                                        00390024
                                                                        00400024
       DATA DIVISION.                                                   00410024
       FILE SECTION.                                                    00420024
                                                                        00430024
       FD  TVNDSTL-PURGE-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  TVNDSTL-PURGE-RECORD        PIC  X(95).                              
                                                                                
       FD  TMDARVS-PURGE-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  TMDARVS-PURGE-RECORD        PIC  X(93).                              
                                                                                
       FD  TUPC-PURGE-FILE                                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  TUPC-PURGE-RECORD           PIC  X(214).                             
                                                                                
                                                                        00510024
       WORKING-STORAGE SECTION.                                         00520024
                                                                        00530024
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00540024
                                                                        00550024
       01  PC-PROGRAM-CONSTANTS.                                        00560024
           05  PC-JOB-NAME             PIC  X(06)  VALUE 'IMK505'.      00570024
           05  PC-PROGRAM-NAME         PIC  X(08)  VALUE 'IMKUP012'.    00580024
           05  PC-MAX-RETRIES          PIC S9(03)  VALUE +3 COMP-3.     00590024
           05  PC-MAX-DEADLOCKS        PIC S9(03)  VALUE +3 COMP-3.     00600024
                                                                        00610024
       01  PC-PROGRAM-COUNTER.                                          00620024
204416     05  PC-1000000000    PIC S9(18)  COMP SYNC VALUE 1000000000. 00540024
204416     05  FILLER REDEFINES PC-1000000000.                          00540024
204416         10  FILLER              PIC X(4).                        00540024
204416         10  PC-1000000000-NUM   PIC S9(9) COMP.                  00540024
204416     05  PC-2000000000    PIC S9(18)  COMP SYNC VALUE 2000000000. 00540024
204416     05  FILLER REDEFINES PC-2000000000.                          00540024
204416         10  FILLER              PIC X(4).                        00540024
204416         10  PC-2000000000-NUM   PIC S9(9) COMP.                  00540024
           05  PC-DISPLAY-CNT          PIC  9(09)  VALUE ZERO.          00640028
           05  PC-DELETE-CNT           PIC  9(09)  VALUE ZERO.          00640028
                                                                        00650024
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00660024
           05  PE-MSG-01                       PIC X(50) VALUE          00881037
                'ATTEMPT TO DELETE ROWS ON TABLE TUPC FAILED     '.     00700024
           05  PE-MSG-02                       PIC X(50) VALUE          00690024
                'ATTEMPT TO DELETE ROW ON TABLE TVNDSTL FAILED    '.    00700024
           05  PE-MSG-03                       PIC X(50) VALUE          00710024
                'ATTEMPT TO DELETE ROWS ON TABLE TMDARVS FAILED   '.    00700024
           05  PE-MSG-04                       PIC X(50) VALUE          00730024
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00740024
           05  PE-MSG-05                       PIC X(50) VALUE          00750024
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00760024
           05  PE-MSG-06                       PIC X(50) VALUE          00770024
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00780024
           05  PE-MSG-07                       PIC X(50) VALUE          00790024
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00800024
           05  PE-MSG-10                       PIC X(50) VALUE          00850024
                'UPDATE OF STATUS CODE ON TCKRSTCNTL UNSUCCESSFUL'.     00860024
           05  PE-MSG-11                       PIC X(50) VALUE          00870024
                'ATTEMPT TO SELECT TIMESTAMP + CKPT_FRQNCY_QTY   '.     00880024
      *                                                                 00890024
       01  PS-PROGRAM-SWITCHES.                                         00900024
           05  PS-INPUT-FILE-EOF-SW         PIC  X        VALUE 'N'.    00910024
               88  PS-MORE-INPUT-RECORDS                  VALUE 'N'.    00920024
               88  PS-NO-MORE-INPUT-RECORDS               VALUE 'Y'.    00930024
           05  PS-ALL-FILES-PROCESSED-SW    PIC  X        VALUE 'N'.    00910024
               88  PS-ALL-FILES-PROCESSED                 VALUE 'Y'.    00920024
           05  PS-FIRST-COMMIT-SW           PIC X         VALUE 'N'.    00960024
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00970024
           05  PS-DEADLOCK-RESTART-SW       PIC X         VALUE 'N'.    00980024
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00990024
               88  PS-PGM-NOT-DEADLOCKED                  VALUE 'N'.    00990024
           05  PS-INPUT-FILE-TYPE-SW        PIC X         VALUE 'V'.    00960024
               88  PS-TVNDSTL-INPUT-FILE                  VALUE 'V'.    00970024
               88  PS-TMDARVS-INPUT-FILE                  VALUE 'M'.    00970024
               88  PS-TUPC-INPUT-FILE                     VALUE 'U'.    00970024
      *                                                                 01030024
      *                                                                 01040024
       01  PROGRAM-VARIABLES.                                           01050024
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     01060024
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01070024
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01080024
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01090024
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01100024
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01110024
           05  PV-SQLCODE-DISPLAY              PIC  ++++9 VALUE ZERO.   01120024
           05  PV-UPC-ID-1.                                             01110024
               10  PV-UPC-ID-1HIGH             PIC  9(1)  VALUE 1.      01170024
               10  PV-UPC-ID-1LOW              PIC  9(9)  VALUE ZEROES. 01170024
           05  PV-UPC-ID-2.                                             01110024
               10  PV-UPC-ID-2HIGH             PIC  9(1)  VALUE 2.      01170024
               10  PV-UPC-ID-2LOW              PIC  9(9)  VALUE ZEROES. 01170024
      *                                                                 01130024
       01  PA-PROGRAM-ACCUMULATORS.                                     01150024
           05  PA-RECORD-COUNTS.                                        01160024
               10  PA-INPUT-COUNT              PIC  9(9)  VALUE ZEROES. 01170024
               10  PA-DEL-TMDARVS-COUNT        PIC  9(9)  VALUE ZEROES. 01190037
               10  PA-DEL-TUPC-COUNT           PIC  9(9)  VALUE ZEROES. 01190137
               10  PA-DEL-TVNDSTL-COUNT        PIC  9(9)  VALUE ZEROES. 01200037
               10  PA-COMMIT-COUNT             PIC  9(9)  VALUE ZEROES. 01210024
      *                                                                 01220024
       01  PD-PROGRAM-DISPLAYS.                                         01230024
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01240024
      *                                                                 01250024
      *                                                                 01450024
      ******************************************************************01510024
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01520024
      ******************************************************************01530024
           COPY DPWS004.                                                01540024
      *                                                                 01550024
      ******************************************************************01560024
      *  USED FOR DB2 ERRORS                                            01570024
      ******************************************************************01580024
           EXEC SQL                                                     01590024
               INCLUDE SQLCA                                            01600024
           END-EXEC.                                                    01610024
      *                                                                 01620024
      *****************************************************************         
      * DCLGEN FOR THE MERCHANDISE AREA VENDOR STYLE TABLE                      
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE UPC TABLE                                                
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE VENDOR STYLE TABLE                                       
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TVNDSTL                                                  
           END-EXEC.                                                            
                                                                                
      ***************************************************************** 01700024
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01710024
      ***************************************************************** 01720024
           EXEC SQL                                                     01730024
               INCLUDE TCKRSTCN                                         01740024
           END-EXEC.                                                    01750024
      *                                                                 01760024
           EXEC SQL                                                     01770024
               INCLUDE TCKRSTIN                                         01780024
           END-EXEC.                                                    01790024
      *                                                                 01800024
      ******************************************************************01810024
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01820024
      ******************************************************************01830024
       01  CR-CHECKPOINT-RESTART-AREA.                                  01840024
           05  CR-AREA-LEN                  PIC S9(04) VALUE +63 COMP.  01850037
           05  CR-CHECKPOINT-RESTART-VAR.                               01860024
               10  CR-INPUT-FILE-TYPE-SW    PIC  X(01) VALUE SPACE.     01870024
                   88  CR-TVNDSTL-INPUT-FILE           VALUE 'V'.       00970024
                   88  CR-TMDARVS-INPUT-FILE           VALUE 'M'.       00970024
                   88  CR-TUPC-INPUT-FILE              VALUE 'U'.       00970024
               10  CR-PREV-DTL-KEY          PIC  X(15) VALUE SPACES.    01870024
               10  CR-TVNDSTL-KEY REDEFINES CR-PREV-DTL-KEY.            01880024
                   15  CR-TVNDSTL-VS-ID     PIC  9(10).                 01890024
                   15  FILLER               PIC  X(05).                 01890024
               10  CR-TMDARVS-KEY REDEFINES CR-PREV-DTL-KEY.            01880024
                   15  CR-TMDARVS-SCMDSE-ID PIC  9(06).                 01890024
                   15  CR-TMDARVS-VS-ID     PIC  9(09).                 01890024
               10  CR-TUPC-KEY REDEFINES CR-PREV-DTL-KEY.               01880024
                   15  CR-TUPC-UPC-ID       PIC S9(09) COMP.            01890024
                   15  FILLER               PIC  X(11).                 01890024
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01940024
               10  CR-COMMIT-COUNT          PIC  9(09) VALUE ZEROES.    01950024
               10  CR-DEL-TMDARVS-COUNT     PIC  9(09) VALUE ZEROES.    01960037
               10  CR-DEL-TUPC-COUNT        PIC  9(09) VALUE ZEROES.    01961037
               10  CR-DEL-TVNDSTL-COUNT     PIC  9(09) VALUE ZEROES.    01970037
      *                                                                 01990024
      *                                                                 02110024
      *-------------------*                                             02120024
       PROCEDURE DIVISION.                                              02130024
      *-------------------*                                             02140024
                                                                        02150024
      ******************************************************************        
      * EACH OF THE FOUR INPUT FILES ARE PROCESSED ONE AT A TIME.               
      * IN THE EVENT OF A -911 (DEADLOCK) SQL RETURN CODE THE PROGRAM           
      * AUTOMATICALLY BE RESTARTED FROM THE LAST CHECKPOINT TAKEN. THIS         
      * IS BECAUSE A -911 DOES AN AUTOMATIC ROLLBACK AND TRANSACTIONS           
      * CAN BE LOST.                                                            
      ******************************************************************        
       0000-MAIN-MODULE.                                                02160024
                                                                        02170024
           PERFORM 1000-INITIALIZATION.                                 02180024
      *                                                                 02190024
           PERFORM 2000-PROCESS-INPUT                                   02200024
               UNTIL PS-ALL-FILES-PROCESSED.                            02210024
      *                                                                 02220024
           PERFORM 8000-EOJ-ROUTINE.                                    02230024
                                                                        02240024
           STOP RUN.                                                    02250024
      *                                                                 02260024
      *                                                                 02270024
      *                                                                 02280024
      ******************************************************************02290024
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02300024
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02310024
      ******************************************************************02320024
      *                                                                 02330024
       1000-INITIALIZATION.                                             02340024
      *                                                                 02350024
           OPEN INPUT TVNDSTL-PURGE-FILE                                02360024
                      TMDARVS-PURGE-FILE                                02360024
                      TUPC-PURGE-FILE.                                  02360024
      *                                                                 02370024
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02380024
      *                                                                 02390024
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02400024
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02410024
               PERFORM 7500-SELECT-RESTART-INFO                         02420024
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02430024
                                             CR-CHECKPOINT-RESTART-VAR  02440024
               MOVE CR-INPUT-FILE-TYPE-SW   TO PS-INPUT-FILE-TYPE-SW    02430024
      * MOVE RESTART COUNTS TO PA-FIELDS FOR ACCURATE DISPLAY AT EOJ    02460024
               MOVE CR-DEL-TMDARVS-COUNT    TO PA-DEL-TMDARVS-COUNT     02480037
               MOVE CR-DEL-TUPC-COUNT       TO PA-DEL-TUPC-COUNT        02490034
               MOVE CR-DEL-TVNDSTL-COUNT    TO PA-DEL-TVNDSTL-COUNT     02491034
               MOVE CR-COMMIT-COUNT         TO PA-COMMIT-COUNT          02500024
           ELSE                                                         02510024
               SET PS-TVNDSTL-INPUT-FILE TO TRUE                                
               SET PS-FIRST-COMMIT TO TRUE                              02520024
           END-IF.                                                      02530024
      *                                                                 02540024
           DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                      02550024
                   TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'.             02560024
      *                                                                 02570024
           EVALUATE TRUE                                                00970024
               WHEN PS-TVNDSTL-INPUT-FILE                               00970024
                   PERFORM 4000-READ-TVNDSTL-PURGE-FILE                 08040024
                       UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT       08050024
                         OR PS-NO-MORE-INPUT-RECORDS                    08060024
                   IF PS-NO-MORE-INPUT-RECORDS                          08060024
                       SET PS-TMDARVS-INPUT-FILE TO TRUE                        
                       SET PS-MORE-INPUT-RECORDS TO TRUE                        
                       MOVE ZERO TO PA-INPUT-COUNT                              
                       PERFORM 4100-READ-TMDARVS-PURGE-FILE             08040024
                   END-IF                                                       
               WHEN PS-TMDARVS-INPUT-FILE                               00970024
                   PERFORM 4100-READ-TMDARVS-PURGE-FILE                 08040024
                       UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT       08050024
                         OR PS-NO-MORE-INPUT-RECORDS                    08060024
                   IF PS-NO-MORE-INPUT-RECORDS                          08060024
                       SET PS-TUPC-INPUT-FILE TO TRUE                           
                       SET PS-MORE-INPUT-RECORDS TO TRUE                        
                       MOVE ZERO TO PA-INPUT-COUNT                              
                       PERFORM 4200-READ-TUPC-PURGE-FILE                08040024
                   END-IF                                                       
               WHEN PS-TUPC-INPUT-FILE                                  00970024
                   PERFORM 4200-READ-TUPC-PURGE-FILE                    08040024
                       UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT       08050024
                         OR PS-NO-MORE-INPUT-RECORDS                    08060024
                   IF PS-NO-MORE-INPUT-RECORDS                          08060024
                       SET PS-ALL-FILES-PROCESSED TO TRUE                       
                   END-IF                                                       
           END-EVALUATE.                                                00970024
      *                                                                 02610024
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02620024
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              02680024
                        PA-INPUT-COUNT                                  02690024
               EVALUATE TRUE                                            00970024
                   WHEN PS-TVNDSTL-INPUT-FILE                           00970024
                       DISPLAY 'OF INPUT FILE: TVNDSTL PURGE'           07910024
                       DISPLAY 'VENDOR STYLE ID   ' VNDSTL-VS-ID        02630024
                   WHEN PS-TMDARVS-INPUT-FILE                           00970024
                       DISPLAY 'OF INPUT FILE: TMDARVS PURGE'           07910024
                       DISPLAY 'SUB CL MERCH ID   '                     02630024
                             MDARVS-SUB-CL-MDSEAR-ID                            
                       DISPLAY 'VENDOR STYLE ID   ' MDARVS-VS-ID        02630024
                   WHEN PS-TUPC-INPUT-FILE                              00970024
                       DISPLAY 'OF INPUT FILE: TUPC PURGE'              07910024
                       IF UPC-UPC-ID < PC-1000000000-NUM                02630024
                           DISPLAY 'UPC ID            ' UPC-UPC-ID              
                       ELSE                                             02770024
                           IF UPC-UPC-ID < PC-2000000000-NUM            02630024
                               MOVE UPC-UPC-ID TO PV-UPC-ID-1LOW        02630024
                               DISPLAY 'UPC ID            '             02630024
                                     PV-UPC-ID-1                        02630024
                           ELSE                                         02770024
                               MOVE UPC-UPC-ID TO PV-UPC-ID-2LOW        02630024
                               DISPLAY 'UPC ID            '             02630024
                                     PV-UPC-ID-2                        02630024
                           END-IF                                       02770024
                       END-IF                                           02770024
               END-EVALUATE                                             00970024
           END-IF.                                                      02700024
                                                                        02710024
           IF PS-NO-MORE-INPUT-RECORDS                                  02720024
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02730024
                   CONTINUE                                             02740024
               ELSE                                                     02750024
                   DISPLAY '** NO RECORDS ON INPUT TVNDSTL FILE **'     02760024
           ELSE                                                         02770024
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02780024
           END-IF.                                                      02790024
      *                                                                 02800024
      *                                                                 02810024
      ******************************************************************02820024
      * PROCESS EACH INPUT PURGE FILE                                   02830024
      ******************************************************************02840024
       2000-PROCESS-INPUT.                                              02850024
      *                                                                 02880024
           EVALUATE TRUE                                                00970024
               WHEN PS-TVNDSTL-INPUT-FILE                               00970024
                   PERFORM 2100-PROCESS-TVNDSTL-INPUT                   02960037
                       UNTIL PS-NO-MORE-INPUT-RECORDS                   02210024
                   DISPLAY ' '                                          08350024
                   DISPLAY 'TVNDSTL INPUT RECORDS   ', PA-INPUT-COUNT   08350024
                   IF PA-DEL-TVNDSTL-COUNT > ZERO                       03570024
                       PERFORM 7750-COMMIT-PROCESSING                   02960037
                   END-IF                                                       
                   SET PS-TMDARVS-INPUT-FILE TO TRUE                            
                   SET PS-MORE-INPUT-RECORDS TO TRUE                            
                   MOVE ZERO TO PA-INPUT-COUNT                                  
                   PERFORM 4100-READ-TMDARVS-PURGE-FILE                 08040024
               WHEN PS-TMDARVS-INPUT-FILE                               00970024
                   PERFORM 2200-PROCESS-TMDARVS-INPUT                   02960037
                       UNTIL PS-NO-MORE-INPUT-RECORDS                   08060024
                   DISPLAY 'TMDARVS INPUT RECORDS   ', PA-INPUT-COUNT   08350024
                   IF PA-DEL-TMDARVS-COUNT > ZERO                       03570024
                       PERFORM 7750-COMMIT-PROCESSING                   02960037
                   END-IF                                                       
                   SET PS-TUPC-INPUT-FILE TO TRUE                               
                   SET PS-MORE-INPUT-RECORDS TO TRUE                            
                   MOVE ZERO TO PA-INPUT-COUNT                                  
                   PERFORM 4200-READ-TUPC-PURGE-FILE                    08040024
               WHEN PS-TUPC-INPUT-FILE                                  00970024
                   PERFORM 2300-PROCESS-TUPC-INPUT                      02960037
                       UNTIL PS-NO-MORE-INPUT-RECORDS                   08060024
                   DISPLAY 'TUPC INPUT RECORDS      ', PA-INPUT-COUNT   08350024
                   IF PA-DEL-TUPC-COUNT > ZERO                          03570024
                       PERFORM 7750-COMMIT-PROCESSING                   02960037
                   END-IF                                                       
                   SET PS-ALL-FILES-PROCESSED TO TRUE                           
           END-EVALUATE.                                                00970024
      *                                                                 02880024
      *                                                                 02810024
      ******************************************************************02820024
      * PROCESS EACH INPUT TVNDSTL PURGE FILE RECORD                    02830024
      ******************************************************************02840024
       2100-PROCESS-TVNDSTL-INPUT.                                      02850024
      *                                                                 03480024
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02870024
      *                                                                 03490024
           PERFORM 6000-DELETE-TVNDSTL-ROW                                      
           IF PS-PGM-NOT-DEADLOCKED                                             
               PERFORM 7700-COMMIT-CHECK                                        
           END-IF.                                                              
      *                                                                 03490024
           IF PS-PGM-NOT-DEADLOCKED                                             
               PERFORM 4000-READ-TVNDSTL-PURGE-FILE                             
           END-IF.                                                              
      *                                                                 02880024
      *                                                                 02810024
      ******************************************************************02820024
      * PROCESS EACH INPUT TMDARVS PURGE FILE RECORD                    02830024
      ******************************************************************02840024
       2200-PROCESS-TMDARVS-INPUT.                                      02850024
      *                                                                 03480024
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02870024
      *                                                                 03490024
           PERFORM 6100-DELETE-TMDARVS-ROW                                      
           IF PS-PGM-NOT-DEADLOCKED                                             
               PERFORM 7700-COMMIT-CHECK                                        
           END-IF.                                                              
      *                                                                 03490024
           IF PS-PGM-NOT-DEADLOCKED                                             
               PERFORM 4100-READ-TMDARVS-PURGE-FILE                             
           END-IF.                                                              
      *                                                                 02880024
      *                                                                 02810024
      ******************************************************************02820024
      * PROCESS EACH INPUT TUPC PURGE FILE RECORD                       02830024
      ******************************************************************02840024
       2300-PROCESS-TUPC-INPUT.                                         02850024
      *                                                                 03480024
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02870024
      *                                                                 03490024
           PERFORM 6200-DELETE-TUPC-ROW                                         
           IF PS-PGM-NOT-DEADLOCKED                                             
               PERFORM 7700-COMMIT-CHECK                                        
           END-IF.                                                              
      *                                                                 03490024
           IF PS-PGM-NOT-DEADLOCKED                                             
               PERFORM 4200-READ-TUPC-PURGE-FILE                                
           END-IF.                                                              
      *                                                                 03490024
      *                                                                 03490024
      ******************************************************************03500024
      * READS THE TVNDSTL PURGE FILE INTO THE TVNDSTL LAYOUT            03510024
      ******************************************************************03520024
       4000-READ-TVNDSTL-PURGE-FILE.                                    03530024
           READ TVNDSTL-PURGE-FILE INTO DCLTVNDSTL                      03540024
               AT END MOVE 'Y' TO PS-INPUT-FILE-EOF-SW                  03550024
               NOT AT END                                                       
                   ADD 1                     TO PA-INPUT-COUNT                  
                                                PC-DELETE-CNT                   
                   IF PC-DELETE-CNT > 100000                                    
                       ADD 1                 TO PC-DISPLAY-CNT                  
                       DISPLAY 'TVNDSTL DELETE COUNT: ' PC-DISPLAY-CNT  03860027
                       MOVE +0               TO PC-DELETE-CNT                   
                   END-IF.                                                      
      *                                                                 03560024
      *                                                                 03490024
      ******************************************************************03500024
      * READS THE TMDARVS PURGE FILE INTO THE TMDARVS LAYOUT            03510024
      ******************************************************************03520024
       4100-READ-TMDARVS-PURGE-FILE.                                    03530024
           READ TMDARVS-PURGE-FILE INTO DCLTMDARVS                      03540024
               AT END MOVE 'Y' TO PS-INPUT-FILE-EOF-SW                  03550024
               NOT AT END                                                       
                   ADD 1                     TO PA-INPUT-COUNT                  
                                                PC-DELETE-CNT                   
                   IF PC-DELETE-CNT > 100000                                    
                       ADD 1                 TO PC-DISPLAY-CNT                  
                       DISPLAY 'TMDARVS DELETE COUNT: ' PC-DISPLAY-CNT  03860027
                       MOVE +0               TO PC-DELETE-CNT                   
                   END-IF.                                                      
      *                                                                 03560024
      *                                                                 03490024
      ******************************************************************03500024
      * READS THE TUPC PURGE FILE INTO THE TUPC LAYOUT                  03510024
      ******************************************************************03520024
       4200-READ-TUPC-PURGE-FILE.                                       03530024
           READ TUPC-PURGE-FILE INTO DCLTUPC                            03540024
               AT END MOVE 'Y' TO PS-INPUT-FILE-EOF-SW                  03550024
               NOT AT END                                                       
                   ADD 1                     TO PA-INPUT-COUNT                  
                                                PC-DELETE-CNT                   
                   IF PC-DELETE-CNT > 100000                                    
                       ADD 1                 TO PC-DISPLAY-CNT                  
                       DISPLAY 'TUPC DELETE COUNT: ' PC-DISPLAY-CNT     03860027
                       MOVE +0               TO PC-DELETE-CNT                   
                   END-IF.                                                      
      *                                                                 03900024
      *                                                                 03901037
      ******************************************************************03910024
      * DELETE A ROW FROM THE TVNDSTL TABLE                             03920024
      ******************************************************************03940024
       6000-DELETE-TVNDSTL-ROW.                                         03950037
      *                                                                 03901037
      *                                                                 03960024
           EXEC SQL                                                     05110024
               DELETE                                                           
                  FROM TVNDSTL                                                  
               WHERE VS_ID = :VNDSTL-VS-ID                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                             04780024
               WHEN 0                                                   04790024
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
                   ADD 1              TO PA-DEL-TVNDSTL-COUNT           04792037
               WHEN +100                                                04790024
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
               WHEN -911                                                04810024
                   ADD +1             TO PV-DEADLOCK-COUNT              04820024
                   DISPLAY 'DEADLOCK -911 IN 6000-DELETE-TVNDSTL-ROW'   04830037
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04840024
                       MOVE '6000-DELETE-TVNDSTL-ROW'                   04850037
                                      TO PV-PARAGRAPH-NAME              04860037
                       PERFORM 9000-DEADLOCK-ERROR                      04870024
                   ELSE                                                 04880024
                       PERFORM 7999-RESTART-PROCESS                     04890024
                   END-IF                                               04900024
               WHEN OTHER                                               04910024
                   MOVE '6000-DELETE-TVNDSTL-ROW'                       04920037
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE PE-MSG-02     TO PV-OPERATION-ATTEMPTED         04930037
                   PERFORM 9999-SQL-ABEND                               04940024
           END-EVALUATE.                                                04950024
      *                                                                 03900024
      *                                                                 03901037
      ******************************************************************03910024
      * DELETE A ROW FROM THE TMDARVS TABLE                             03920024
      ******************************************************************03940024
       6100-DELETE-TMDARVS-ROW.                                         03950037
      *                                                                 03960024
           EXEC SQL                                                     05110024
               DELETE                                                           
                  FROM TMDARVS                                                  
               WHERE VS_ID = :MDARVS-VS-ID                                      
                 AND SUB_CL_MDSEAR_ID = :MDARVS-SUB-CL-MDSEAR-ID                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                             04780024
               WHEN 0                                                   04790024
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
                   ADD 1              TO PA-DEL-TMDARVS-COUNT           04792037
               WHEN +100                                                04790024
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
               WHEN -911                                                04810024
                   ADD +1             TO PV-DEADLOCK-COUNT              04820024
                   DISPLAY 'DEADLOCK -911 IN 6100-DELETE-TMDARVS-ROW'   04830037
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04840024
                       MOVE '6100-DELETE-TMDARVS-ROW'                   04850037
                                      TO PV-PARAGRAPH-NAME              04860037
                       PERFORM 9000-DEADLOCK-ERROR                      04870024
                   ELSE                                                 04880024
                       PERFORM 7999-RESTART-PROCESS                     04890024
                   END-IF                                               04900024
               WHEN OTHER                                               04910024
                   MOVE '6100-DELETE-TMDARVS-ROW'                       04920037
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE PE-MSG-03     TO PV-OPERATION-ATTEMPTED         04930037
                   PERFORM 9999-SQL-ABEND                               04940024
           END-EVALUATE.                                                04950024
      *                                                                 04470024
      *                                                                 04480024
      ******************************************************************03910024
      * DELETE A ROW FROM THE TUPC TABLE THAT MATCHES THE INPUT UPC ID  03920024
      ******************************************************************03940024
       6200-DELETE-TUPC-ROW.                                            03950037
      *                                                                 03960024
           EXEC SQL                                                     05110024
               DELETE                                                           
                  FROM TUPC                                                     
               WHERE UPC_ID= :UPC-UPC-ID                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                             04780024
               WHEN 0                                                   04790024
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
                   ADD 1              TO PA-DEL-TUPC-COUNT              04792037
               WHEN +100                                                04790024
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04791037
               WHEN -911                                                04810024
                   ADD +1             TO PV-DEADLOCK-COUNT              04820024
                   DISPLAY 'DEADLOCK -911 IN 6200-DELETE-TUPC-ROW'      04830037
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04840024
                       MOVE '6200-DELETE-TUPC-ROW'                      04850037
                                      TO PV-PARAGRAPH-NAME              04860037
                       PERFORM 9000-DEADLOCK-ERROR                      04870024
                   ELSE                                                 04880024
                       PERFORM 7999-RESTART-PROCESS                     04890024
                   END-IF                                               04900024
               WHEN OTHER                                               04910024
                   MOVE '6200-DELETE-TUPC-ROW'                          04920037
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE PE-MSG-01     TO PV-OPERATION-ATTEMPTED         04930037
                   PERFORM 9999-SQL-ABEND                               04940024
           END-EVALUATE.                                                04950024
      *                                                                 03900024
      *                                                                 03901037
      ******************************************************************05720024
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05730024
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05740024
      *            CONTROL TABLE                                       *05750024
      ******************************************************************05760024
       7300-GET-COMMIT-TIMESTAMP.                                       05770024
                                                                        05780024
           EXEC SQL                                                     05790024
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05800024
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05810024
               INTO :PV-COMMIT-TIMESTAMP                                05820024
               FROM TCKRSTCNTL                                          05830024
              WHERE JOB_NAME  = :PC-JOB-NAME                            05840024
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05850024
           END-EXEC.                                                    05860024
                                                                        05870024
           IF SQLCODE = 0                                               05880030
               CONTINUE                                                 05890030
           ELSE                                                         05900030
               MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME    05910030
               MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED     05920030
               PERFORM 9999-SQL-ABEND                                   05930030
           END-IF.                                                      05940030
      *                                                                 05950024
      *                                                                 05960024
      ******************************************************************05970024
      * 7400-INIT-CHECKPOINT-SELECT                                    *05980024
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05990024
      *            IF WE ARE IN A RESTART CONDITION.                   *06000024
      ******************************************************************06010024
       7400-INIT-CHECKPOINT-SELECT.                                     06020024
                                                                        06030024
           EXEC SQL                                                     06040024
             SELECT  CKPT_STAT_CODE                                     06050024
                    ,CKPT_FRQNCY_QTY                                    06060024
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         06070024
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        06080024
               FROM  TCKRSTCNTL                                         06090024
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06100024
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06110024
           END-EXEC.                                                    06120024
                                                                        06130024
           IF SQLCODE = 0                                               06140024
               CONTINUE                                                 06150024
           ELSE                                                         06160024
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  06170024
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     06180024
               PERFORM 9999-SQL-ABEND                                   06190024
           END-IF.                                                      06200024
      *                                                                 06210024
      *                                                                 06220024
      ******************************************************************06230024
      * 7500-SELECT-RESTART-INFO                                       *06240024
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *06250024
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *06260024
      ******************************************************************06270024
       7500-SELECT-RESTART-INFO.                                        06280024
                                                                        06290024
           EXEC SQL                                                     06300024
             SELECT  WORK_STRG_DESC                                     06310024
               INTO  :TCKRSTINF-WORK-STRG-DESC                          06320024
               FROM  TCKRSTINF                                          06330024
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06340024
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06350024
           END-EXEC.                                                    06360024
                                                                        06370024
           IF SQLCODE = 0                                               06380024
               CONTINUE                                                 06390024
           ELSE                                                         06400024
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06410024
               PERFORM 9999-SQL-ABEND                                   06420024
           END-IF.                                                      06430024
      *                                                                 06440024
      *                                                                 06450024
      ******************************************************************06460024
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06470024
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06480024
      ******************************************************************06490024
       7600-SET-CURRENT-TIMESTAMP.                                      06500024
                                                                        06510024
           EXEC SQL                                                     06520024
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06530024
           END-EXEC.                                                    06540024
      *                                                                 06550024
           IF SQLCODE = 0                                               06560024
               CONTINUE                                                 06570024
           ELSE                                                         06580024
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06590024
               PERFORM 9999-SQL-ABEND                                   06600024
           END-IF.                                                      06610024
      *                                                                 06620024
      *                                                                 06630024
      ******************************************************************06640024
      * 7700-COMMIT-CHECK.                                             *06650024
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06660024
      ******************************************************************06680024
       7700-COMMIT-CHECK.                                               06690024
           MOVE '*7700-COMMIT-CHECK*' TO PV-PARAGRAPH-NAME.             06700024
                                                                        06710024
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06720024
      *                                                                 06730024
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06740024
               PERFORM 7750-COMMIT-PROCESSING                           06690024
           END-IF.                                                              
      *                                                                 06620024
      *                                                                 06630024
      ******************************************************************06640024
      * 7750-COMMIT-PROCESSING.                                        *06650024
      * PROCESSES: PREPARE CHECKPOINT RECORD FOR UPDATE OF             *06660024
      *            WORK_STRG_DESC AND TAKES A COMMIT.                  *06670024
      ******************************************************************06680024
       7750-COMMIT-PROCESSING.                                          06690024
           MOVE '*7500-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06700024
                                                                        06710024
           ADD 1                              TO PA-COMMIT-COUNT.       06760024
           MOVE PS-INPUT-FILE-TYPE-SW         TO CR-INPUT-FILE-TYPE-SW. 02430024
           MOVE SPACES                        TO CR-PREV-DTL-KEY.               
           EVALUATE TRUE                                                00970024
               WHEN PS-TVNDSTL-INPUT-FILE                               00970024
                   MOVE VNDSTL-VS-ID          TO CR-TVNDSTL-VS-ID       03580024
               WHEN PS-TMDARVS-INPUT-FILE                               00970024
                   MOVE MDARVS-SUB-CL-MDSEAR-ID                         03580024
                                              TO CR-TMDARVS-SCMDSE-ID   03580024
                   MOVE MDARVS-VS-ID          TO CR-TMDARVS-VS-ID       03580024
               WHEN PS-TUPC-INPUT-FILE                                  00970024
                   MOVE UPC-UPC-ID            TO CR-TUPC-UPC-ID         03580024
           END-EVALUATE.                                                00970024
           MOVE PA-COMMIT-COUNT               TO CR-COMMIT-COUNT.               
           MOVE PA-INPUT-COUNT                TO CR-INPUT-READ-COUNT.   06830024
           MOVE PA-DEL-TMDARVS-COUNT          TO CR-DEL-TMDARVS-COUNT.  06840037
           MOVE PA-DEL-TUPC-COUNT             TO CR-DEL-TUPC-COUNT.     06841037
           MOVE PA-DEL-TVNDSTL-COUNT          TO CR-DEL-TVNDSTL-COUNT.  06850037
      *    MOVE COMMIT INFO TO WORKING STORAGE ROW                      06870024
           MOVE CR-CHECKPOINT-RESTART-AREA    TO                        06880024
                                             TCKRSTINF-WORK-STRG-DESC.  06890024
           IF PS-FIRST-COMMIT                                           06900024
               MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                    06910024
               PERFORM 7900-UPDATE-CHECKPOINT-STATUS                    06920024
               MOVE 'N' TO PS-FIRST-COMMIT-SW                           06930024
           END-IF.                                                      06940024
           PERFORM 7800-COMMIT-CHANGES.                                 06950024
           PERFORM 7300-GET-COMMIT-TIMESTAMP.                           06960024
      *                                                                 06980024
      *                                                                 06990024
      ******************************************************************07000024
      * 7800-COMMIT-CHANGES.                                            07010024
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      07020024
      *            TAKE A COMMIT.                                       07030024
      *  ==> PERFORMED FROM: 7750-COMMIT-PROCESSING.                    07040024
      ******************************************************************07050024
       7800-COMMIT-CHANGES.                                             07060024
                                                                        07070024
           EXEC SQL                                                     07080024
             UPDATE TCKRSTINF                                           07090024
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07100024
              WHERE JOB_NAME       = :PC-JOB-NAME                       07110024
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07120024
           END-EXEC.                                                    07130024
                                                                        07140024
           IF SQLCODE = 0                                               07150024
               CONTINUE                                                 07160024
           ELSE                                                         07170024
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED07180024
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07190024
               PERFORM 9999-SQL-ABEND                                   07200024
           END-IF.                                                      07210024
                                                                        07220024
           EXEC SQL                                                     07230024
               COMMIT                                                   07240024
           END-EXEC.                                                    07250024
                                                                        07260024
           IF SQLCODE = 0                                               07270024
               CONTINUE                                                 07280024
           ELSE                                                         07290024
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED07300024
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07310024
               PERFORM 9999-SQL-ABEND                                   07320024
           END-IF.                                                      07330024
      *                                                                 07340024
      *                                                                 07350024
      ******************************************************************07360024
      * 7900-UPDATE-CHECKPOINT-STATUS                                   07370024
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   07380024
      *                                                                 07390024
      ******************************************************************07400024
       7900-UPDATE-CHECKPOINT-STATUS.                                   07410024
                                                                        07420024
           EXEC SQL                                                     07430024
             UPDATE  TCKRSTCNTL                                         07440024
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        07450024
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07460024
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07470024
           END-EXEC.                                                    07480024
                                                                        07490024
           IF SQLCODE = 0                                               07500024
               CONTINUE                                                 07510024
           ELSE                                                         07520024
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07530024
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED07540024
               PERFORM 9999-SQL-ABEND                                   07550024
           END-IF.                                                      07560024
                                                                        07570024
           EJECT                                                        07580024
      *                                                                 07590024
      *                                                                 07600024
      ******************************************************************07610024
      *  PROGRAM RESTART DUE TO A DEADLOCK.                             07620024
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07630024
      ******************************************************************07640024
       7999-RESTART-PROCESS.                                            07650024
      *                                                                 07660024
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07670024
      *                                                                 07680024
           CLOSE TVNDSTL-PURGE-FILE                                     02360024
                 TMDARVS-PURGE-FILE                                     02360024
                 TUPC-PURGE-FILE.                                       02360024
      *                                                                 07700024
           PERFORM 7500-SELECT-RESTART-INFO.                            07710024
      *                                                                 07720024
           DISPLAY '**** RESTART **** '.                                07730024
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07740024
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07750024
      *                                                                 07760024
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07770024
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07780024
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       07790037
           IF PS-FIRST-COMMIT                                           07800024
               INITIALIZE TCKRSTINF-WORK-STRG-DESC                      07810024
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07820024
                      ' BEGINNING'                                      07830024
           ELSE                                                         07840024
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07850024
                        TCKRSTINF-WORK-STRG-DESC (1:63)                 07860040
      ***    INFO ON LAST CHECKPOINT TAKEN IS IN                        07870024
      ***    CR-CHECKPOINT-RESTART-AREA.                                07880024
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07890024
                     CR-CHECKPOINT-RESTART-AREA                         07900024
               MOVE CR-INPUT-FILE-TYPE-SW TO PS-INPUT-FILE-TYPE-SW      02430024
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07910024
               EVALUATE TRUE                                            00970024
                   WHEN PS-TVNDSTL-INPUT-FILE                           00970024
                       DISPLAY 'INPUT FILE: TVNDSTL PURGE'              07910024
                       DISPLAY 'VENDOR STYLE ID   ' CR-TVNDSTL-VS-ID    02630024
                   WHEN PS-TMDARVS-INPUT-FILE                           00970024
                       DISPLAY 'INPUT FILE: TMDARVS PURGE'              07910024
                       DISPLAY 'SUB CL MERCH ID   ' CR-TMDARVS-SCMDSE-ID02630024
                       DISPLAY 'VENDOR STYLE ID   ' CR-TMDARVS-VS-ID    02630024
                   WHEN PS-TUPC-INPUT-FILE                              00970024
                       DISPLAY 'INPUT FILE: TUPC PURGE'                 07910024
                       IF CR-TUPC-UPC-ID < PC-1000000000-NUM            02630024
                           DISPLAY 'UPC ID            ' CR-TUPC-UPC-ID          
                       ELSE                                             02770024
                           IF CR-TUPC-UPC-ID < PC-2000000000-NUM        02630024
                               MOVE CR-TUPC-UPC-ID TO PV-UPC-ID-1LOW    02630024
                               DISPLAY 'UPC ID            '             02630024
                                     PV-UPC-ID-1                        02630024
                           ELSE                                         02770024
                               MOVE CR-TUPC-UPC-ID TO PV-UPC-ID-2LOW    02630024
                               DISPLAY 'UPC ID            '             02630024
                                     PV-UPC-ID-2                        02630024
                           END-IF                                       02770024
                       END-IF                                           02770024
               END-EVALUATE                                             00970024
               DISPLAY 'INPUT RECS READ   ' CR-INPUT-READ-COUNT         07970024
           END-IF.                                                      07980024
      *                                                                 07990024
           OPEN INPUT TVNDSTL-PURGE-FILE                                02360024
                      TMDARVS-PURGE-FILE                                02360024
                      TUPC-PURGE-FILE.                                  02360024
           MOVE ZEROES                          TO PA-INPUT-COUNT.      08010024
      *                                                                 08020024
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          08030024
           EVALUATE TRUE                                                00970024
               WHEN PS-TVNDSTL-INPUT-FILE                               00970024
                   PERFORM 4000-READ-TVNDSTL-PURGE-FILE                 08040024
                       UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT       08050024
                         OR PS-NO-MORE-INPUT-RECORDS                    08060024
               WHEN PS-TMDARVS-INPUT-FILE                               00970024
                   PERFORM 4100-READ-TMDARVS-PURGE-FILE                 08040024
                       UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT       08050024
                         OR PS-NO-MORE-INPUT-RECORDS                    08060024
               WHEN PS-TUPC-INPUT-FILE                                  00970024
                   PERFORM 4200-READ-TUPC-PURGE-FILE                    08040024
                       UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT       08050024
                         OR PS-NO-MORE-INPUT-RECORDS                    08060024
           END-EVALUATE.                                                00970024
           IF PS-NO-MORE-INPUT-RECORDS                                  08070024
               DISPLAY 'END OF INPUT FILE ON RESTART'                   08080024
           ELSE                                                         08090024
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              08100024
                        PA-INPUT-COUNT                                  08110024
               EVALUATE TRUE                                            00970024
                   WHEN PS-TVNDSTL-INPUT-FILE                           00970024
                       DISPLAY 'OF INPUT FILE: TVNDSTL PURGE'           07910024
                       DISPLAY 'VENDOR STYLE ID   ' VNDSTL-VS-ID        02630024
                   WHEN PS-TMDARVS-INPUT-FILE                           00970024
                       DISPLAY 'OF INPUT FILE: TMDARVS PURGE'           07910024
                       DISPLAY 'SUB CL MERCH ID   '                     02630024
                             MDARVS-SUB-CL-MDSEAR-ID                            
                       DISPLAY 'VENDOR STYLE ID   ' MDARVS-VS-ID        02630024
                   WHEN PS-TUPC-INPUT-FILE                              00970024
                       DISPLAY 'OF INPUT FILE: TUPC PURGE'              07910024
                       IF UPC-UPC-ID < PC-1000000000-NUM                02630024
                           DISPLAY 'UPC ID            ' UPC-UPC-ID              
                       ELSE                                             02770024
                           IF UPC-UPC-ID < PC-2000000000-NUM            02630024
                               MOVE UPC-UPC-ID TO PV-UPC-ID-1LOW        02630024
                               DISPLAY 'UPC ID            '             02630024
                                     PV-UPC-ID-1                        02630024
                           ELSE                                         02770024
                               MOVE UPC-UPC-ID TO PV-UPC-ID-2LOW        02630024
                               DISPLAY 'UPC ID            '             02630024
                                     PV-UPC-ID-2                        02630024
                           END-IF                                       02770024
                       END-IF                                           02770024
               END-EVALUATE                                             00970024
           END-IF.                                                      08170024
      *                                                                 08180024
      *RE-ESTABLISH COUNTS FOR PROCESSED INPUT (FROM CHECKPOINT RECORD) 08190024
           MOVE CR-DEL-TMDARVS-COUNT    TO PA-DEL-TMDARVS-COUNT.        08200037
           MOVE CR-DEL-TUPC-COUNT       TO PA-DEL-TUPC-COUNT.           08201037
           MOVE CR-DEL-TVNDSTL-COUNT    TO PA-DEL-TVNDSTL-COUNT.        08210037
           MOVE CR-COMMIT-COUNT         TO PA-COMMIT-COUNT.             08230024
      *                                                                 08240024
      *                                                                 08250024
      ******************************************************************08260024
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILES   *08270024
      ******************************************************************08280024
       8000-EOJ-ROUTINE.                                                08290024
      *                                                                 08300024
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       08310024
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       08320024
      *                                                                 08330024
           DISPLAY '                                             '.     08340024
           DISPLAY 'TVNDSTL RECS DELETED  ', PA-DEL-TVNDSTL-COUNT.      08360337
           DISPLAY 'TMDARVS RECS DELETED  ', PA-DEL-TMDARVS-COUNT.      08360034
           DISPLAY 'TUPC RECS DELETED     ', PA-DEL-TUPC-COUNT.         08360138
           DISPLAY '                                             '.     08390024
           DISPLAY 'COMMITS TAKEN   ', PA-COMMIT-COUNT.                 08400024
      *                                                                 08410024
           CLOSE TVNDSTL-PURGE-FILE                                     02360024
                 TMDARVS-PURGE-FILE                                     02360024
                 TUPC-PURGE-FILE.                                       02360024
      *                                                                 08430024
      *                                                                 08440024
      ******************************************************************08450024
      *  PERFORMED BY 6100-UPDATE AND 7200-INSERT                       08460037
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   08470024
      ******************************************************************08480024
       9000-DEADLOCK-ERROR.                                             08490024
      *                                                                 08500024
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     08510024
               PERFORM 9999-SQL-ABEND.                                  08520024
      *                                                                 08530024
      ******************************************************************08540024
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08550024
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08560024
      ******************************************************************08570024
       9999-SQL-ABEND.                                                  08580024
                                                                        08590024
           MOVE +4013                 TO ABEND-CODE.                    08600024
           MOVE SQLCODE               TO PV-SQLCODE-DISPLAY.            08610024
           DISPLAY '**  ABEND     **'.                                  08620024
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08630024
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08640024
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08650024
           DISPLAY '**  SQLCODE   **  = ' PV-SQLCODE-DISPLAY.           08660024
                                                                        08670024
           EXEC SQL                                                     08680024
                ROLLBACK                                                08690024
           END-EXEC.                                                    08700024
                                                                        08710024
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08720024
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08730024
                                                                        08740024
           COPY DPPD004.                                                08750024
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08760024
