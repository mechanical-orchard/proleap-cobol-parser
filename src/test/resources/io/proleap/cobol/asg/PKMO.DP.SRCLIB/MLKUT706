       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT706.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08-21-2002.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      * PURPOSE:  TO CONVERT PRICE CHANGES FROM THE COMPOSITE FILE BACK         
      *           TO A MLRD017 PRICE CHANGE FORMAT.                             
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT COMPOSITE-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT PCHG-MARKDOWN-FILE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT PCHG-MARKUP-FILE                                              
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  COMPOSITE-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CONTROL-DATE-RECORD      PIC X(175).                                 
                                                                                
       FD  PCHG-MARKDOWN-FILE                                                   
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PCHG-MARKDOWN-RECORD     PIC X(100).                                 
                                                                                
       FD  PCHG-MARKUP-FILE                                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PCHG-MARKUP-RECORD       PIC X(100).                                 
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01 ABEND-AREAS.                                                          
           05 ABEND-CODE            PIC S9(04) VALUE ZERO                       
                                               COMP SYNC.                       
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-PROCESS-DONE-SW   PIC X(01)  VALUE 'N'.                       
               88  PROCESSING-COMPLETE         VALUE 'Y'.                       
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-READ-COUNT        PIC S9(13) COMP-3 VALUE ZEROS.              
           05  PA-MARKUP-COUNT      PIC S9(13) COMP-3 VALUE ZEROS.              
           05  PA-MARKDOWN-COUNT    PIC S9(13) COMP-3 VALUE ZEROS.              
           05  PA-PCHG-COUNT        PIC S9(13) COMP-3 VALUE ZEROS.              
           05  PA-OTHER-COUNT       PIC S9(13) COMP-3 VALUE ZEROS.              
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-JOB-NAME          PIC X(06)  VALUE 'MLK9ZZ'.                  
           05  PC-PROGRAM-NAME      PIC X(08)  VALUE 'MLTUT006'.                
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PRICE-CHANGE-AMT  PIC S9(4)V99  VALUE ZERO.                   
           05  PV-WORK-AMOUNT       PIC 9(4)V99   VALUE ZERO.                   
                                                                                
       01  WS-EDIT-MAJOR-CLASS.                                                 
           05  WS-EDIT-FIRST-PART   PIC X(02)  VALUE SPACES.                    
           05  WS-EDIT-MAJ-CLASS    PIC X(01)  VALUE SPACE.             25160000
           05  WS-EDIT-LAST-BYTE    PIC X(01)  VALUE SPACE.                     
                                                                                
      /*****************************************************************        
      * COPYBOOKS USED IN MLKUT706!!!!!!                                        
      ******************************************************************        
      * COPY BOOK - PRICE CHANGE RECORD LAYOUT                                  
           COPY MLRD017.                                                        
                                                                                
      / COPY BOOK - MERCH LEDGER COMPOSITE FILE RECORD LAYOUT                   
           COPY MLRD020.                                                        
                                                                                
      /*****************************************************************        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE-MLKUT706.                                                  
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS                                            
               UNTIL PROCESSING-COMPLETE.                                       
                                                                                
           PERFORM 9000-END-OF-JOB-TASKS.                                       
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      ******************************************************************        
                                                                                
           OPEN INPUT  COMPOSITE-FILE                                           
                OUTPUT PCHG-MARKDOWN-FILE                                       
                OUTPUT PCHG-MARKUP-FILE.                                        
                                                                                
           MOVE ZEROS               TO PA-READ-COUNT.                           
           MOVE ZEROS               TO PA-MARKUP-COUNT.                         
           MOVE ZEROS               TO PA-MARKDOWN-COUNT.                       
           MOVE ZEROS               TO PA-PCHG-COUNT.                           
           MOVE 'N'                 TO PS-PROCESS-DONE-SW.                      
                                                                                
           PERFORM 8000-READ-COMPOSITE-FILE.                                    
                                                                                
      /*****************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
                                                                                
           ADD 1                    TO PA-READ-COUNT.                           
                                                                                
           IF ML020-MKDOWN                                                      
               PERFORM 3000-DETER-MKDN                                          
           ELSE                                                                 
           IF ML020-MKUP                                                        
               PERFORM 3100-DETER-MKUP                                          
           ELSE                                                                 
               ADD 1                TO PA-OTHER-COUNT                           
           END-IF.                                                              
                                                                                
           PERFORM 8000-READ-COMPOSITE-FILE.                                    
                                                                                
      ******************************************************************        
       3000-DETER-MKDN.                                                         
      ******************************************************************        
                                                                                
           PERFORM 4000-FORMAT-RECORD.                                          
                                                                                
           MOVE '4'                 TO ML017-PC-CDE.                    MLRD017 
                                                                                
           IF ML020-PC-OLD-PR-AMT GREATER ML020-PC-NEW-PR-AMT           25220000
               MOVE '1'             TO ML017-PC-TYP-CDE                 MLRD017 
           ELSE                                                                 
           IF ML020-PC-OLD-PR-AMT LESS ML020-PC-NEW-PR-AMT              25220000
               MOVE '2'             TO ML017-PC-TYP-CDE                 MLRD017 
           ELSE                                                                 
               MOVE '1'             TO ML017-PC-TYP-CDE                 MLRD017 
           END-IF.                                                              
                                                                                
           PERFORM 7000-WRITE-MARKDOWN.                                         
                                                                                
      ******************************************************************        
       3100-DETER-MKUP.                                                         
      ******************************************************************        
                                                                                
           PERFORM 4000-FORMAT-RECORD.                                          
                                                                                
           MOVE '5'                 TO ML017-PC-CDE.                    MLRD017 
                                                                                
           IF ML020-PC-OLD-PR-AMT GREATER ML020-PC-NEW-PR-AMT           25220000
               MOVE '4'             TO ML017-PC-TYP-CDE                 MLRD017 
           ELSE                                                                 
           IF ML020-PC-OLD-PR-AMT LESS ML020-PC-NEW-PR-AMT              25220000
               MOVE '3'             TO ML017-PC-TYP-CDE                 MLRD017 
           ELSE                                                                 
               MOVE '3'             TO ML017-PC-TYP-CDE                 MLRD017 
           END-IF.                                                              
                                                                                
           PERFORM 7100-WRITE-MARKUP.                                           
                                                                                
      ******************************************************************        
       4000-FORMAT-RECORD.                                                      
      ******************************************************************        
                                                                                
           INITIALIZE ML017-PRICE-CHANGE-RECORD.                        25130000
                                                                        25140000
           MOVE ML020-STORE-NBR     TO ML017-PC-STORE-NBR.              25150000
           MOVE ML020-SKU           TO ML017-PC-SKU.                    25160000
           MOVE ML020-DEPT-NBR      TO ML017-PC-DEPT-NBR.               25160000
           MOVE ML020-CLASS-NBR     TO ML017-PC-CLASS-NBR.              25160000
                                                                                
           MOVE SPACES              TO WS-EDIT-MAJOR-CLASS.                     
           MOVE '00'                TO WS-EDIT-FIRST-PART.                      
           MOVE ML020-CLASS-NBR (1:1)  TO WS-EDIT-MAJ-CLASS.            25160000
           MOVE '0'                 TO WS-EDIT-LAST-BYTE.                       
           MOVE WS-EDIT-MAJOR-CLASS TO ML017-PC-MAJ-CLASS.              25160000
                                                                                
           MOVE ML020-PC-DTE        TO ML017-PC-DTE.                    25210000
           MOVE ML020-PC-OLD-PR-AMT TO ML017-PC-OLD-PR-AMT.             25220000
           MOVE ML020-PC-NEW-PR-AMT TO ML017-PC-NEW-PR-AMT.             25230000
           MOVE ML020-PC-QTY        TO ML017-PC-QTY.                    25240000
           MOVE ML020-PC-OLD-RSN-CDE  TO ML017-PC-RSN-CDE.              25250000
           MOVE ML020-PC-DOC-5-NBR    TO ML017-PC-DOC-5-NBR.            25280000
           MOVE ML020-PC-BATCH-NBR-3  TO ML017-PC-BATCH-NBR.            25320000
           MOVE ML020-PC-MKDN-MKUP-RSN-CDE                              25370000
                                    TO ML017-PC-MKDN-MKUP-RSN-CDE.      25380000
           MOVE ML020-PC-CATEG-CDE  TO ML017-PC-CATEG-CDE.              25410000
           MOVE ML020-PC-DOC-7-NBR  TO ML017-PC-DOC-7-NBR.              25420000
           MOVE ML020-PC-KOHLS-MM   TO ML017-PC-KOHLS-MM.               25430000
           MOVE ML020-PC-VENDOR-INHSE-NMBR                              25441099
                                    TO ML017-PC-VENDOR-INHSE-NMBR.      25442099
                                                                                
           COMPUTE PV-PRICE-CHANGE-AMT = ML017-PC-OLD-PR-AMT                    
                                       - ML017-PC-NEW-PR-AMT.                   
           MOVE PV-PRICE-CHANGE-AMT    TO ML017-PC-DIFFER-AMT.                  
                                                                                
      ******************************************************************        
       7000-WRITE-MARKDOWN.                                                     
      ******************************************************************        
                                                                                
           WRITE PCHG-MARKDOWN-RECORD FROM ML017-PRICE-CHANGE-RECORD.           
           ADD 1                    TO PA-MARKDOWN-COUNT.                       
           ADD 1                    TO PA-PCHG-COUNT.                           
                                                                                
      ******************************************************************        
       7100-WRITE-MARKUP.                                                       
      ******************************************************************        
                                                                                
           WRITE PCHG-MARKUP-RECORD FROM ML017-PRICE-CHANGE-RECORD.             
           ADD 1                    TO PA-MARKUP-COUNT.                         
           ADD 1                    TO PA-PCHG-COUNT.                           
                                                                                
      ******************************************************************        
       8000-READ-COMPOSITE-FILE.                                                
      ******************************************************************        
                                                                                
           READ COMPOSITE-FILE                                                  
             INTO ML020-COMPOSITE-RECORD                                        
               AT END  MOVE 'Y'     TO PS-PROCESS-DONE-SW.                      
                                                                                
      ******************************************************************        
       9000-END-OF-JOB-TASKS.                                                   
      ******************************************************************        
                                                                                
           DISPLAY '*********************************'                          
           DISPLAY '**  CONTROL SUMMARY - MLKUT706 **'                          
           DISPLAY '*'                                                          
           DISPLAY '* RECORDS READ   = ', PA-READ-COUNT                         
           DISPLAY '*   WRITE - MKDN = ', PA-MARKDOWN-COUNT                     
           DISPLAY '*         - MKUP = ', PA-MARKUP-COUNT                       
           DISPLAY '*         - PCHG = ', PA-PCHG-COUNT                         
           DISPLAY '* OTHER RECORDS  = ', PA-OTHER-COUNT                        
           DISPLAY '*'                                                          
           DISPLAY '*********************************'                          
                                                                                
           CLOSE COMPOSITE-FILE                                                 
                 PCHG-MARKDOWN-FILE                                             
                 PCHG-MARKUP-FILE.                                              
                                                                                
