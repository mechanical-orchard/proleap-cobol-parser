000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    APKRP035.                                                 
000300 AUTHOR.        ROBIN BERNOSKI.                                           
000400 DATE-WRITTEN.  NOVEMBER 2000.                                            
000500 DATE-COMPILED.                                                           
000600*****************************************************************         
000700*     SUM OF FREIGHT COST BY VENDOR FOR TRANCODES 41 AND 42     *         
000800*****************************************************************         
000900*    COBOL II                                                   *         
001000*****************************************************************         
001100*    THIS PROGRAM PRODUCES A REPORT WHICH SHOWS THE FREIGHT     *         
001200*    COST BY VENDOR FOR TRANCODES 41 AND 42.                    *         
001300*                                                               *         
001400* INPUT:                                                        *         
001500*  1. SORTED MONTH END ADJUSTMENT JOURNAL EXTRACT (APRD046)     *         
001600*                                                               *         
001700* OUTPUT:                                                       *         
001800*  1. REPORT - FREIGHT COST BY VENDOR REPORT                    *         
001900*                                                               *         
002000*****************************************************************         
002100*****************************************************************         
002200* MAINTENANCE LOG:                                              *         
002300*                                                               *         
002400* 05/16/2002                                                    *         
002500*   CHANGE MADE:  NI-SKU CHANGES.  RECOMPILE FROM PRODUCTION    *         
002600*   TO INCORPORATE CHANGES TO COPYBOOK APRD046.                 *         
002700* MADE BY:  KEVIN CATLIN                   WR/IR #: 21278       *         
002800*                                                               *         
002900*****************************************************************         
003000 ENVIRONMENT DIVISION.                                                    
003100 CONFIGURATION SECTION.                                                   
003200 SOURCE-COMPUTER.    IBM-3090.                                            
003300 OBJECT-COMPUTER.    IBM-3090.                                            
003400                                                                          
003500 INPUT-OUTPUT SECTION.                                                    
003600 FILE-CONTROL.                                                            
003700     SELECT FREIGHT-ADJSTMNT-FILE         ASSIGN TO INP01.                
003800     SELECT REPORT-FILE                   ASSIGN TO RPT01.                
003900                                                                          
004000 DATA DIVISION.                                                           
004100 FILE SECTION.                                                            
004200                                                                          
004300 FD  FREIGHT-ADJSTMNT-FILE                                                
004400     LABEL RECORDS ARE OMITTED                                            
004500     RECORDING MODE IS F                                                  
004600     RECORD CONTAINS 250 CHARACTERS                                       
004700     BLOCK CONTAINS 0 RECORDS.                                            
004800 01  FREIGHT-ADJSTMNT-REC             PIC  X(250).                        
004900                                                                          
005000 FD  REPORT-FILE                                                          
005100     LABEL RECORDS ARE STANDARD                                           
005200     RECORDING MODE IS F                                                  
005300     BLOCK CONTAINS 0 RECORDS.                                            
005400 01  REPORT-RECORD                    PIC  X(132).                        
005500                                                                          
005600 EJECT                                                                    
005700 WORKING-STORAGE SECTION.                                                 
005800                                                                          
005900 01  PV-PROGRAM-VARIABLES.                                                
006000     05  FILLER                  PIC  X(30)  VALUE                        
006100         '** BEGINING OF APKRP035 W/S **'.                                
006200     05  PV-PROGRAM-NAME           PIC  X(08)  VALUE 'APKRP035'.          
006300     05  PV-PAGE-CNT               PIC  9(03)  VALUE ZERO.                
006400     05  PV-MAX-LINES              PIC S9(03)  VALUE +60 COMP-3.          
006500     05  PV-LINE-CNT               PIC S9(03)  VALUE +99 COMP-3.          
006600     05  PV-READ-CNT               PIC  9(07)  VALUE ZERO.                
006700     05  PV-WRITE-CNT              PIC  9(07)  VALUE ZERO.                
006800     05  PV-CURRENT-DATE.                                                 
006900         10  PV-CURRENT-YEAR       PIC  9(02)  VALUE ZERO.                
007000         10  PV-CURRENT-MONTH      PIC  9(02)  VALUE ZERO.                
007100         10  PV-CURRENT-DAY        PIC  9(02)  VALUE ZERO.                
007200     05  PV-CURRENT-TIME.                                                 
007300         10  PV-CURRENT-HOUR       PIC  9(02)  VALUE ZERO.                
007400         10  PV-CURRENT-MINUTE     PIC  9(02)  VALUE ZERO.                
007500         10  FILLER                PIC  9(02)  VALUE ZERO.                
007600     05  PV-DATE.                                                         
007700         10  PV-MM                 PIC  X(02).                            
007800         10  FILLER                PIC  X(01)  VALUE '/'.                 
007900         10  PV-DD                 PIC  X(02).                            
008000         10  FILLER                PIC  X(01)  VALUE '/'.                 
008100         10  PV-YY                 PIC  X(02).                            
008200     05  PV-HOLD-FIELDS.                                                  
008300         10  PV-HOLD-PSTG-FMN-NBR  PIC  X(02)  VALUE SPACE.               
008400         10  PV-HOLD-PSTG-FYR-NBR  PIC  X(04)  VALUE SPACE.               
008500         10  PV-HOLD-VENDOR-NBR    PIC  X(09)  VALUE SPACE.               
008600         10  PV-HOLD-FREIGHT-AMT   PIC S9(11)V99 VALUE ZERO.              
008700         10  PV-HOLD-MONTHLY-TOT   PIC S9(11)V99 VALUE ZERO.              
008800         10  PV-HOLD-GRAND-TOTAL   PIC S9(11)V99 VALUE ZERO.              
008900         10  PV-HOLD-VENDOR-NME    PIC  X(30)  VALUE SPACE.               
009000                                                                          
009100 01  PS-PROGRAM-SWITCHES.                                                 
009200     05  PS-END-OF-FILE-SW         PIC X(01) VALUE 'N'.                   
009300         88  END-OF-FILE                     VALUE 'Y'.                   
009400         88  NOT-END-OF-FILE                 VALUE 'N'.                   
009500                                                                          
009600 EJECT                                                                    
009700*----------------------------------------------------------------*        
009800*  ADJUSTMENT JOURNAL MERCHANDISE EXTRACT LAYOUT                 *        
009900*----------------------------------------------------------------*        
010000     COPY APRD046.                                                        
010100 EJECT                                                                    
010200*----------------------------------------------------------------*        
010300*  STANDARD HEADER LAYOUT                                        *        
010400*----------------------------------------------------------------*        
010500     COPY DPWS132O.                                                       
010600                                                                          
010700 01  H1-HEADER1.                                                          
010800     05  FILLER                  PIC  X(37) VALUE SPACE.                  
010900     05  H1-TITLE                PIC  X(58) VALUE                         
011000    'SUM OF FREIGHT COST BY VENDOR FOR TRANCODES 41 AND 42 ONLY'.         
011100     05  FILLER                  PIC  X(37) VALUE SPACE.                  
011200                                                                          
011300 EJECT                                                                    
011400 01  DH-DETAIL-HEADER.                                                    
011500     05  FILLER                  PIC  X(26)  VALUE SPACE.                 
011600     05  FILLER                  PIC  X(11)  VALUE 'VENDOR NAME'.         
011700     05  FILLER                  PIC  X(33)  VALUE SPACE.                 
011800     05  FILLER                  PIC  X(12)  VALUE 'FREIGHT COST'.        
011900     05  FILLER                  PIC  X(58)  VALUE SPACE.                 
012000                                                                          
012100 01  TH-MONTHLY-TOTAL-LINE.                                               
012200     05  FILLER                  PIC  X(25)  VALUE SPACE.                 
012300     05  FILLER                  PIC  X(10)  VALUE 'TOTALS FOR'.          
012400     05  FILLER                  PIC  X(01)  VALUE SPACE.                 
012500     05  TH-MONTH                PIC  X(02)  VALUE SPACE.                 
012600     05  FILLER                  PIC  X(01)  VALUE '/'.                   
012700     05  TH-YEAR                 PIC  X(04)  VALUE SPACE.                 
012800     05  FILLER                  PIC  X(23)  VALUE SPACE.                 
012900     05  TH-MONTHLY-TOTAL-AMT    PIC  ---,---,---.99 VALUE SPACE.         
013000     05  FILLER                  PIC  X(71)  VALUE SPACE.                 
013100                                                                          
013200 01  GT-GRAND-TOTAL-LINE.                                                 
013300     05  FILLER                  PIC  X(25)  VALUE SPACE.                 
013400     05  FILLER                  PIC  X(11)  VALUE 'GRAND TOTAL'.         
013500     05  FILLER                  PIC  X(30)  VALUE SPACE.                 
013600     05  GT-GRAND-TOTAL-AMT      PIC  ---,---,---.99 VALUE SPACE.         
013700     05  FILLER                  PIC  X(81)  VALUE SPACE.                 
013800                                                                          
013900 01  ND-NO-DATA-LINE.                                                     
014000     05  FILLER                  PIC  X(42)  VALUE SPACE.                 
014100     05  FILLER                  PIC  X(80)  VALUE                        
014200         'NO ELIGIBLE TRAN CODES TO REPORT ON'.                           
014300                                                                          
014400 01  DL-DETAIL-LINE.                                                      
014500     05  FILLER                  PIC  X(26)  VALUE SPACE.                 
014600     05  DL-VENDOR-NME           PIC  X(30)  VALUE SPACE.                 
014700     05  FILLER                  PIC  X(10)  VALUE SPACE.                 
014800     05  DL-FREIGHT-AMT          PIC  ---,---,---.99                      
014900                                             VALUE SPACE.                 
015000     05  FILLER                  PIC  X(48)  VALUE SPACE.                 
015100                                                                          
015200 01  RE-REPORT-END-LINE.                                                  
015300     05  FILLER                  PIC  X(49)  VALUE SPACE.                 
015400     05  FILLER                  PIC  X(28)  VALUE                        
015500         '***  END OF REPORT APKRP035-'.                                  
015600     05  RE-REPORT-NUMBER        PIC  X(01)  VALUE '1'.                   
015700     05  FILLER                  PIC  X(05)  VALUE '  ***'.               
015800     05  FILLER                  PIC  X(49)  VALUE SPACE.                 
015900 EJECT                                                                    
016000*----------------------------------------------------------------*        
016100*  ABEND DATA AREAS                                              *        
016200*----------------------------------------------------------------*        
016300 01  ABEND-CODE                  PIC S9(04)  VALUE ZEROS                  
016400                                             COMP SYNC.                   
016500     88  AC-BAD-DATA                         VALUE +4003.                 
016600     88  AC-DB2-ERROR                        VALUE +4013.                 
016700                                                                          
016800 01  ABEND-AREAS.                                                         
016900     05  AA-ABEND-LIT            PIC  X(40)  VALUE                        
017000             '*****       ABEND'.                                         
017100     05  AA-PROGRAM-LIT          PIC  X(40)  VALUE                        
017200             '*****   PROGRAM: APKRP035'.                                 
017300     05  AA-PARAGRAPH-LIT.                                                
017400         10  FILLER              PIC  X(17)  VALUE                        
017500             '***** PARAGRAPH: '.                                         
017600         10  AA-PARAGRAPH-NAME   PIC  X(35)  VALUE SPACES.                
017700     05  AA-MESSAGE-LINE.                                                 
017800         10  FILLER              PIC  X(06)  VALUE '*****'.               
017900         10  AA-MESSAGE          PIC  X(127) VALUE SPACES.                
018000     05  AA-DB2-ERROR-LIT        PIC  X(40)  VALUE                        
018100             '*****    DB2 ERROR'.                                        
018200     05  AA-DB2-OPERATION-LIT.                                            
018300         10  FILLER              PIC  X(17)  VALUE                        
018400             '***** OPERATION: '.                                         
018500         10  AA-DB2-OPERATION.                                            
018600             15  AA-DB2-OP-1     PIC  X(25)  VALUE SPACES.                
018700             15  AA-DB2-OP-2     PIC  X(25)  VALUE SPACES.                
018800     05  AA-DB2-TABLE-1          PIC  X(08)  VALUE SPACES.                
018900     05  AA-DB2-TABLE-2          PIC  X(08)  VALUE SPACES.                
019000     05  AA-DB2-TABLE-3          PIC  X(08)  VALUE SPACES.                
019100                                                                          
019200                                                                          
019300     COPY DPWS004.                                                        
019400 01  FILLER                      PIC  X(25)  VALUE                        
019500     '** END OF APKRP035 W/S **'.                                         
019600 EJECT                                                                    
019700 PROCEDURE DIVISION.                                                      
019800 A100-MAIN.                                                               
019900                                                                          
020000     PERFORM B100-INITIALIZE.                                             
020100                                                                          
020200     PERFORM B200-PROCESS-TRAN-CODES                                      
020300         UNTIL END-OF-FILE.                                               
020400                                                                          
020500     PERFORM B300-END-PROGRAM.                                            
020600                                                                          
020700     GOBACK.                                                              
020800 EJECT                                                                    
020900*----------------------------------------------------------------*        
021000*    INITIALIZATION PROCESSING                                   *        
021100*----------------------------------------------------------------*        
021200 B100-INITIALIZE.                                                         
021300                                                                          
021400     OPEN INPUT  FREIGHT-ADJSTMNT-FILE                                    
021500          OUTPUT REPORT-FILE.                                             
021600                                                                          
021700     ACCEPT PV-CURRENT-DATE FROM DATE.                                    
021800     MOVE PV-CURRENT-MONTH            TO DP132O-RUN-MONTH.                
021900     MOVE PV-CURRENT-DAY              TO DP132O-RUN-DAY.                  
022000     MOVE PV-CURRENT-YEAR             TO DP132O-RUN-YEAR.                 
022100                                                                          
022200     ACCEPT PV-CURRENT-TIME FROM TIME.                                    
022300     MOVE PV-CURRENT-HOUR             TO DP132O-RUN-HOUR.                 
022400     MOVE PV-CURRENT-MINUTE           TO DP132O-RUN-MINUTE.               
022500     MOVE PV-PROGRAM-NAME             TO DP132O-PROGRAM-NAME.             
022600     MOVE '1'                         TO DP132O-REPORT-NUMBER.            
022700                                                                          
022800     PERFORM R100-READ-FREIGHT-ADJSTMT-FILE.                              
022900     MOVE AP046-PSTG-FMN-NBR TO TH-MONTH.                                 
023000     MOVE AP046-PSTG-FYR-NBR TO TH-YEAR.                                  
023100     MOVE AP046-PSTG-FMN-NBR TO PV-HOLD-PSTG-FMN-NBR.                     
023200     MOVE AP046-VND-NBR      TO PV-HOLD-VENDOR-NBR.                       
023300     MOVE AP046-ENT-NME-DESC TO PV-HOLD-VENDOR-NME.                       
023400                                                                          
023500     IF END-OF-FILE                                                       
023600         PERFORM W100-WRITE-HEADERS                                       
023700         WRITE REPORT-RECORD FROM ND-NO-DATA-LINE AFTER 1                 
023800     END-IF.                                                              
023900 EJECT                                                                    
024000*----------------------------------------------------------------*        
024100*    LOOPS THROUGH INPUT FILE CHECKING FOR A CHANGE IN VENDOR,   *        
024200*    WHEN NO CHANGE IS FOUND, THE FREIGHT IS CONTINUALLY SUMMED, *        
024300*    WHEN A CHANGE IS FOUND, THE VENDOR NAME AND FREIGHT AMOUNT  *        
024400*    IS LISTED ON THE REPORT.                                    *        
024500*----------------------------------------------------------------*        
024600 B200-PROCESS-TRAN-CODES.                                                 
024700                                                                          
024800     IF AP046-VND-NBR = PV-HOLD-VENDOR-NBR                                
024900        ADD AP046-FRGT-AMT TO PV-HOLD-FREIGHT-AMT                         
025000     ELSE                                                                 
025100        IF AP046-PSTG-FMN-NBR = PV-HOLD-PSTG-FMN-NBR                      
025200           MOVE AP046-PSTG-FMN-NBR TO TH-MONTH                            
025300           MOVE AP046-PSTG-FYR-NBR TO TH-YEAR                             
025400           PERFORM C100-FILL-REPORT-FIELDS                                
025500           PERFORM W200-WRITE-DETAIL                                      
025600           ADD AP046-FRGT-AMT TO PV-HOLD-FREIGHT-AMT                      
025700        ELSE                                                              
025800           MOVE PV-HOLD-PSTG-FMN-NBR    TO TH-MONTH                       
025900           MOVE PV-HOLD-PSTG-FYR-NBR    TO TH-YEAR                        
026000           MOVE PV-HOLD-MONTHLY-TOT     TO TH-MONTHLY-TOTAL-AMT           
026100           MOVE ZEROS                   TO PV-HOLD-MONTHLY-TOT            
026200           PERFORM W300-WRITE-MONTHLY-TOTALS                              
026300           PERFORM C100-FILL-REPORT-FIELDS                                
026400           PERFORM W200-WRITE-DETAIL                                      
026500           ADD AP046-FRGT-AMT TO PV-HOLD-FREIGHT-AMT                      
026600        END-IF                                                            
026700     END-IF.                                                              
026800                                                                          
026900     MOVE AP046-PSTG-FMN-NBR TO PV-HOLD-PSTG-FMN-NBR.                     
027000     MOVE AP046-PSTG-FYR-NBR TO PV-HOLD-PSTG-FYR-NBR.                     
027100     MOVE AP046-VND-NBR      TO PV-HOLD-VENDOR-NBR.                       
027200     MOVE AP046-ENT-NME-DESC TO PV-HOLD-VENDOR-NME.                       
027300                                                                          
027400     PERFORM R100-READ-FREIGHT-ADJSTMT-FILE.                              
027500                                                                          
027600                                                                          
027700 EJECT                                                                    
027800*----------------------------------------------------------------*        
027900*    FINISHES REPORT, CLOSES FILES, DISPLAYS COUNTS.             *        
028000*----------------------------------------------------------------*        
028100 B300-END-PROGRAM.                                                        
028200                                                                          
028300     IF AP046-PSTG-FMN-NBR = PV-HOLD-PSTG-FMN-NBR                         
028400        MOVE AP046-PSTG-FMN-NBR TO TH-MONTH                               
028500        MOVE AP046-PSTG-FYR-NBR TO TH-YEAR                                
028600        PERFORM C100-FILL-REPORT-FIELDS                                   
028700        PERFORM W200-WRITE-DETAIL                                         
028800        ADD AP046-FRGT-AMT TO PV-HOLD-FREIGHT-AMT                         
028900     ELSE                                                                 
029000        MOVE PV-HOLD-PSTG-FMN-NBR    TO TH-MONTH                          
029100        MOVE PV-HOLD-PSTG-FYR-NBR    TO TH-YEAR                           
029200        MOVE PV-HOLD-MONTHLY-TOT     TO TH-MONTHLY-TOTAL-AMT              
029300        MOVE ZEROS                   TO PV-HOLD-MONTHLY-TOT               
029400        PERFORM W300-WRITE-MONTHLY-TOTALS                                 
029500        PERFORM C100-FILL-REPORT-FIELDS                                   
029600        PERFORM W200-WRITE-DETAIL                                         
029700        ADD AP046-FRGT-AMT TO PV-HOLD-FREIGHT-AMT                         
029800     END-IF.                                                              
029900                                                                          
030000     MOVE PV-HOLD-MONTHLY-TOT TO TH-MONTHLY-TOTAL-AMT.                    
030100     MOVE PV-HOLD-GRAND-TOTAL TO GT-GRAND-TOTAL-AMT.                      
030200     PERFORM W300-WRITE-MONTHLY-TOTALS.                                   
030300     PERFORM W400-WRITE-GRAND-TOTALS.                                     
030400                                                                          
030500     WRITE REPORT-RECORD FROM RE-REPORT-END-LINE AFTER 2.                 
030600                                                                          
030700     CLOSE FREIGHT-ADJSTMNT-FILE                                          
030800           REPORT-FILE.                                                   
030900                                                                          
031000     DISPLAY AA-PROGRAM-LIT.                                              
031100     DISPLAY 'RECORDS READ    = ' PV-READ-CNT.                            
031200     DISPLAY 'RECORDS WRITTEN = ' PV-WRITE-CNT.                           
031300                                                                          
031400 EJECT                                                                    
031500*----------------------------------------------------------------*        
031600*    PREPARES THE FIELDS FOR THE SUM OF FREIGHT COST BY          *        
031700*    TRANCODE REPORT                                             *        
031800*----------------------------------------------------------------*        
031900 C100-FILL-REPORT-FIELDS.                                                 
032000                                                                          
032100     MOVE SPACES               TO DL-DETAIL-LINE.                         
032200                                                                          
032300     MOVE PV-HOLD-VENDOR-NME   TO DL-VENDOR-NME.                          
032400     MOVE PV-HOLD-FREIGHT-AMT  TO DL-FREIGHT-AMT.                         
032500                                                                          
032600     ADD  PV-HOLD-FREIGHT-AMT  TO PV-HOLD-MONTHLY-TOT.                    
032700     ADD  PV-HOLD-FREIGHT-AMT  TO PV-HOLD-GRAND-TOTAL.                    
032800                                                                          
032900     MOVE ZEROES               TO PV-HOLD-FREIGHT-AMT.                    
033000                                                                          
033100 EJECT                                                                    
033200*----------------------------------------------------------------*        
033300*  READS A RECORD FROM THE SORTED MONTH END ADJUSTMENT JOURNAL   *        
033400*  EXTRACT FILE.                                                 *        
033500*----------------------------------------------------------------*        
033600 R100-READ-FREIGHT-ADJSTMT-FILE.                                          
033700                                                                          
033800     READ FREIGHT-ADJSTMNT-FILE INTO AP046-EXTRACT-FILE                   
033900         AT END SET END-OF-FILE TO TRUE.                                  
034000                                                                          
034100     IF NOT-END-OF-FILE                                                   
034200         ADD +1 TO PV-READ-CNT                                            
034300     END-IF.                                                              
034400                                                                          
034500 EJECT                                                                    
034600*----------------------------------------------------------------*        
034700*    WRITES THE HEADER LINES.                                    *        
034800*----------------------------------------------------------------*        
034900 W100-WRITE-HEADERS.                                                      
035000                                                                          
035100     ADD 1                       TO PV-PAGE-CNT.                          
035200                                                                          
035300     MOVE PV-PAGE-CNT            TO DP132O-PAGE-NUMBER.                   
035400                                                                          
035500     WRITE REPORT-RECORD                                                  
035600           FROM DP132O-STANDRD-HEADER-132-COLS AFTER PAGE.                
035700     WRITE REPORT-RECORD         FROM H1-HEADER1       AFTER 1.           
035800     WRITE REPORT-RECORD         FROM DH-DETAIL-HEADER AFTER 2.           
035900                                                                          
036000     MOVE SPACES TO REPORT-RECORD.                                        
036100     WRITE REPORT-RECORD                               AFTER 1.           
036200                                                                          
036300     MOVE 4                      TO PV-LINE-CNT.                          
036400                                                                          
036500 EJECT                                                                    
036600*----------------------------------------------------------------*        
036700*    WRITES THE DETAIL LINE.                                     *        
036800*----------------------------------------------------------------*        
036900 W200-WRITE-DETAIL.                                                       
037000                                                                          
037100     IF PV-LINE-CNT > PV-MAX-LINES                                        
037200        PERFORM W100-WRITE-HEADERS                                        
037300     END-IF.                                                              
037400                                                                          
037500     WRITE REPORT-RECORD FROM DL-DETAIL-LINE AFTER 1.                     
037600     ADD 1  TO PV-LINE-CNT.                                               
037700     ADD 1  TO PV-WRITE-CNT.                                              
037800                                                                          
037900 EJECT                                                                    
038000*----------------------------------------------------------------*        
038100*    WRITES THE MONTHLY TOTAL LINE.                              *        
038200*----------------------------------------------------------------*        
038300 W300-WRITE-MONTHLY-TOTALS.                                               
038400                                                                          
038500     IF PV-LINE-CNT > PV-MAX-LINES                                        
038600        PERFORM W100-WRITE-HEADERS                                        
038700     END-IF.                                                              
038800                                                                          
038900     WRITE REPORT-RECORD FROM TH-MONTHLY-TOTAL-LINE AFTER 2.              
039000     ADD 1  TO PV-LINE-CNT.                                               
039100     ADD 1  TO PV-WRITE-CNT.                                              
039200                                                                          
039300 EJECT                                                                    
039400*----------------------------------------------------------------*        
039500*    WRITES THE GRAND TOTAL LINE.                                *        
039600*----------------------------------------------------------------*        
039700 W400-WRITE-GRAND-TOTALS.                                                 
039800                                                                          
039900     IF PV-LINE-CNT > PV-MAX-LINES                                        
040000        PERFORM W100-WRITE-HEADERS                                        
040100     END-IF.                                                              
040200                                                                          
040300     WRITE REPORT-RECORD FROM GT-GRAND-TOTAL-LINE AFTER 1.                
040400     ADD 1  TO PV-LINE-CNT.                                               
040500     ADD 1  TO PV-WRITE-CNT.                                              
040600                                                                          
040700*----------------------------------------------------------------*        
040800*    ABEND ROUTINE                                               *        
040900*----------------------------------------------------------------*        
041000 Z999-ABEND.                                                              
041100                                                                          
041200     CLOSE FREIGHT-ADJSTMNT-FILE                                          
041300           REPORT-FILE.                                                   
041400                                                                          
041500     DISPLAY AA-ABEND-LIT.                                                
041600     DISPLAY AA-PROGRAM-LIT.                                              
041700     DISPLAY AA-PARAGRAPH-LIT.                                            
041800     DISPLAY AA-MESSAGE-LINE.                                             
041900     CALL 'ILBOABN0' USING ABEND-CODE.                                    
042000 EJECT                                                                    
