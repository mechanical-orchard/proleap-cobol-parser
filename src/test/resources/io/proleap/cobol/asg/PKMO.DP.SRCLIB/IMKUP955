       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    IMKUP955.                                                 
       AUTHOR.        D. THEEL.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03/25/2008.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *   IMKUP955 - MONTHLY IMT_RECLS PURGE                           *        
      ******************************************************************        
      * THIS PROGRAM WILL FIND ALL THE RECLASSES THAT ARE MORE THAN    *        
      * 2 YEARS OLD.  IT WILL WRITE AN AUDIT REC FOR EACH AND THEN     *        
      * DELETE EACH ROW COMMITTING EVERY 1000 DELETES.                 *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT OUTPUT-FILE               ASSIGN TO OUT01.                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
                                                                                
       01  OUTPUT-RECORD                    PIC X(49).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004       *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************   CL*15
      * PROGRAM ACCUMULATORS                                           *   CL*15
      ******************************************************************   CL*15
       01  PA-PROGRAM-ACCUMULATORS.                                        CL*15
           05  PA-IMTRECLS-FETCH-CNT        PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
           05  PA-IMTRECLS-DELETE-CNT       PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
           05  PA-WRITE-CNT                 PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
           05  PA-NBR-DELS-SINCE-COMMIT     PIC 9(09)  VALUE ZERO.         CL*15
           05  PA-NBR-COMMITS-TAKEN         PIC 9(09)  VALUE ZERO.         CL*15
                                                                           CL*15
      ******************************************************************   CL*15
      * PROGRAM CONSTANTS                                              *   CL*15
      ******************************************************************   CL*15
       01  PC-PROGRAM-CONSTANTS.                                           CL*15
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE               CL*15
               'IMKUP955'.                                                 CL*15
010700     05  PC-MAXIMUM-RETRY-COUNT       PIC S9(04)  VALUE +2                
010800                                                     COMP SYNC.           
                                                                           CL**1
      ******************************************************************   CL*15
      *  GENERAL WORKING STORAGE VARIABLES                             *        
      ******************************************************************        
       01  WS-SWITCHES.                                                         
           05  IMTRECLS-DATA-SW                 PIC X(03)  VALUE 'NO '.         
               88  MORE-IMTRECLS-DATA                  VALUE 'YES'.             
               88  NO-MORE-IMTRECLS-DATA               VALUE 'NO '.             
                                                                                
      ******************************************************************   CL**1
      * PROGRAM VARIABLES                                              *   CL**1
      ******************************************************************   CL**1
       01  PV-PROGRAM-VARIABLES.                                           CL**1
           05  PV-RETRY-COUNT               PIC S9(04)  VALUE +0                
                                                        COMP SYNC.              
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30)   VALUE SPACE.       CL**1
           05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACE.       CL**1
           05  PV-ABEND-CODE                PIC S9(04)  VALUE +0  COMP.         
           05  PV-OPERATION-ATTEMPTED       PIC X(30)   VALUE SPACES.           
      *****************************************************************         
      *  DCLGEN FOR THE IMT_RECLS TABLE                               *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE IMT00015                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************    CL**4
      *  IMTRECLS CURSOR DECLARE                                      *    CL**4
      *****************************************************************    CL**4
                                                                           CL**4
           EXEC SQL                                                        CL**4
             DECLARE IMTRECLS_CSR CURSOR WITH HOLD FOR                          
              SELECT RECLS_TMST                                                 
                    ,OLD_DEPT_NBR                                               
                    ,OLD_MAJ_CL_NBR                                             
                    ,OLD_SUB_CL_NBR                                             
                    ,SKU_NBR                                                    
                    ,NW_DEPT_NBR                                                
                    ,NW_MAJ_CL_NBR                                              
                    ,NW_SUB_CL_NBR                                              
                FROM IMT_RECLS                                                  
               WHERE DATE(RECLS_TMST) < CURRENT DATE - 2 YEARS                  
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SQL COMMUNICATIONS WORK AREA                                  *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      *  MAIN MODULE PROCESSING                                        *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           SET MORE-IMTRECLS-DATA TO TRUE.                                      
                                                                                
           PERFORM 4000-OPEN-IMTRECLS-CURSOR.                                   
                                                                                
           PERFORM 4010-FETCH-IMTRECLS-CURSOR                                   
               UNTIL NO-MORE-IMTRECLS-DATA.                                     
                                                                                
           PERFORM 4020-CLOSE-IMTRECLS-CURSOR.                                  
                                                                                
           PERFORM 8000-COMMIT.                                                 
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                    *   CL**6
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           INITIALIZE DCLIMT00015.                                              
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
      ******************************************************************        
      *  OPEN THE IMT_RECLS CURSOR                                     *        
      ******************************************************************        
       4000-OPEN-IMTRECLS-CURSOR.                                               
                                                                                
           MOVE '4000-OPEN-IMTRECLS-CURSOR' TO PV-PARAGRAPH-NAME.       03350020
                                                                        03420020
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                        03960024
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
                                                                        03420020
               EXEC SQL                                                         
                   OPEN IMTRECLS_CSR                                            
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                   WHEN 0                                                       
                       CONTINUE                                                 
                   WHEN -911                                                0481
                   WHEN -913                                                0481
                       ADD 1               TO PV-RETRY-COUNT                0484
                   WHEN OTHER                                               0491
                       MOVE '4000-OPEN-IMTRECLS-CSR'                        0492
                                          TO PV-PARAGRAPH-NAME              0492
045800                 MOVE 'OPEN CSR IMTRECLS'                            CL*11
045900                                 TO PV-OPERATION-ATTEMPTED           CL**1
                       PERFORM 9999-SQL-ABEND                               0494
               END-EVALUATE                                                 0495
           END-PERFORM.                                                         
046400     IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE '4000-OPEN-IMTRECLS-CSR'                                0492
046600                                 TO PV-PARAGRAPH-NAME                CL**1
045800         MOVE 'OPEN CSR IMTRECLS'                                    CL*11
046800                                 TO PV-OPERATION-ATTEMPTED           CL**1
               PERFORM 9999-SQL-ABEND                                       0494
047000     END-IF.                                                              
                                                                                
      *****************************************************************         
      *  FETCH A IMTRECLS CURSOR ROW                                  *         
      *****************************************************************         
       4010-FETCH-IMTRECLS-CURSOR.                                              
           MOVE '4010-FETCH-IMTRECLS' TO PV-PARAGRAPH-NAME.             03350020
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
                                                                                
               EXEC SQL                                                         
                   FETCH  IMTRECLS_CSR                                          
                   INTO  :IMT00015-RECLS-TMST                                   
                        ,:IMT00015-OLD-DEPT-NBR                                 
                        ,:IMT00015-OLD-MAJ-CL-NBR                               
                        ,:IMT00015-OLD-SUB-CL-NBR                               
                        ,:IMT00015-SKU-NBR                                      
                        ,:IMT00015-NW-DEPT-NBR                                  
                        ,:IMT00015-NW-MAJ-CL-NBR                                
                        ,:IMT00015-NW-SUB-CL-NBR                                
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                   WHEN 0                                                       
                       ADD +1 TO PA-IMTRECLS-FETCH-CNT                          
                       PERFORM 4100-DELETE-IMTRECLS                             
                       PERFORM 5000-WRITE-OUTPUT                                
                   WHEN +100                                                    
                       SET NO-MORE-IMTRECLS-DATA TO TRUE                        
                   WHEN -911                                                0481
                   WHEN -913                                                0481
                       ADD 1               TO PV-RETRY-COUNT                0484
                   WHEN OTHER                                               0491
                       MOVE '4010-FETCH-IMTRECLS-CSR'                       0492
                                          TO PV-PARAGRAPH-NAME              0492
045800                 MOVE 'FETCH IMTRECLS CSR'                           CL*11
045900                                 TO PV-OPERATION-ATTEMPTED           CL**1
                       PERFORM 9999-SQL-ABEND                               0494
               END-EVALUATE                                                 0495
           END-PERFORM.                                                         
046400     IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE '4010-FETCH-IMTRECLS-CSR'                               0492
046600                                 TO PV-PARAGRAPH-NAME                CL**1
045800         MOVE 'FETCH CSR IMTRECLS'                                   CL*11
046800                                 TO PV-OPERATION-ATTEMPTED           CL**1
               PERFORM 9999-SQL-ABEND                                       0494
047000     END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CLOSE THE TUPC CURSOR                                         *        
      ******************************************************************        
       4020-CLOSE-IMTRECLS-CURSOR.                                              
           MOVE '4020-CLOSE-IMTRECLS-CURSOR' TO PV-PARAGRAPH-NAME.      03350020
                                                                                
           EXEC SQL                                                             
               CLOSE IMTRECLS_CSR                                               
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '4020-CLOSE-IMTRECLS-CURSOR'                            
                                  TO PV-PARAGRAPH-NAME                          
                   MOVE 'CLOSE IMTRECLS CURSOR'                            C    
                                  TO PV-OPERATION-ATTEMPTED                C    
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  DELETE THE ROW FROM IMT_RECLS                                 *        
      ******************************************************************        
       4100-DELETE-IMTRECLS.                                                    
           MOVE '4100-DELETE-IMTRECLS' TO PV-PARAGRAPH-NAME.            03350020
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   DELETE                                                       
                   FROM IMT_RECLS                                               
                   WHERE RECLS_TMST     = :IMT00015-RECLS-TMST                  
                     AND OLD_DEPT_NBR   = :IMT00015-OLD-DEPT-NBR                
                     AND OLD_MAJ_CL_NBR = :IMT00015-OLD-MAJ-CL-NBR              
                     AND OLD_SUB_CL_NBR = :IMT00015-OLD-SUB-CL-NBR              
                     AND SKU_NBR        = :IMT00015-SKU-NBR                     
               END-EXEC                                                         
               EVALUATE SQLCODE                                             0478
                   WHEN 0                                                   0479
                       ADD +1 TO PA-IMTRECLS-DELETE-CNT                    CL*15
                       ADD +1 TO PA-NBR-DELS-SINCE-COMMIT                  CL*15
                   WHEN -911                                                0481
                   WHEN -913                                                0481
                       ADD 1               TO PV-RETRY-COUNT                0484
                   WHEN OTHER                                               0491
                       MOVE '4100-DELETE-IMTRECLS'                          0492
                                          TO PV-PARAGRAPH-NAME              0492
045800                 MOVE 'DELETE IMTRECLS'                              CL*11
045900                                 TO PV-OPERATION-ATTEMPTED           CL**1
                       PERFORM 9999-SQL-ABEND                               0494
               END-EVALUATE                                                 0495
           END-PERFORM.                                                         
046400     IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE '4100-DELETE-IMTRECLS'                                  0492
046600                                 TO PV-PARAGRAPH-NAME                CL**1
045800         MOVE 'DELETE IMTRECLS'                                      CL*11
046800                                 TO PV-OPERATION-ATTEMPTED           CL**1
               PERFORM 9999-SQL-ABEND                                       0494
047000     END-IF.                                                              
                                                                                
           IF PA-NBR-DELS-SINCE-COMMIT >= +1000                                 
               PERFORM 8000-COMMIT                                              
               INITIALIZE PA-NBR-DELS-SINCE-COMMIT                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE AUDIT REC IN IMT_RECLS FORMAT                           *        
      ******************************************************************        
       5000-WRITE-OUTPUT.                                                       
           WRITE OUTPUT-RECORD FROM DCLIMT00015.                                
                                                                                
           ADD 1                         TO PA-WRITE-CNT.                       
      ******************************************************************        
      *  COMMIT DB2 UPDATES                                            *        
      ******************************************************************        
       8000-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT WORK                                                      
           END-EXEC.                                                            
           ADD +1 TO PA-NBR-COMMITS-TAKEN.                                      
                                                                           CL**1
      ******************************************************************   CL**1
      * PERFORM WRAP UP PROCESSING                                     *   CL*12
      ******************************************************************   CL**1
       9000-EOJ-ROUTINE.                                                   CL**1
           CLOSE OUTPUT-FILE.                                              CL**1
           PERFORM 9005-DISPLAY-TOTALS.                                    CL*16
                                                                           CL*16
      ******************************************************************   CL*16
      *  DISPLAYS PROGRAM TOTALS                                       *   CL*16
      ******************************************************************   CL*16
       9005-DISPLAY-TOTALS.                                                CL*16
                                                                           CL*16
           DISPLAY '** IMKUP955 TOTALS ********************'.              CL*16
           DISPLAY 'IMT RECLS FETCHS  ' PA-IMTRECLS-FETCH-CNT.          01200037
           DISPLAY 'IMT RECLS DELETES ' PA-IMTRECLS-DELETE-CNT.         01200037
           DISPLAY 'RECS WRITTEN      ' PA-WRITE-CNT.                   01200037
           DISPLAY 'COMMIT COUNT      ' PA-NBR-COMMITS-TAKEN.           01200037
           DISPLAY '** END OF IMKUP955 TOTALS *************'.              CL*16
                                                                           CL*16
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
           DISPLAY 'IMT RECLS FETCHS  ' PA-IMTRECLS-FETCH-CNT.          01200037
           DISPLAY 'IMT RECLS DELETES ' PA-IMTRECLS-DELETE-CNT.         01200037
           DISPLAY 'RECS WRITTEN      ' PA-WRITE-CNT.                   01200037
           DISPLAY 'COMMIT COUNT      ' PA-NBR-COMMITS-TAKEN.           01200037
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO PV-ABEND-CODE.                                         
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
                                                                                
