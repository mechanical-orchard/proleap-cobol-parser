      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PEKUT102.                                        
       AUTHOR.                 KELLY LARUM                                      
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           MAY 2004.                                        
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PEKUT102 - PERM DOMAIN EXTRACT FOR XXT_PRCHG_TYP            *        
      *                                                                *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
070941                                                                          
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
070941                                                                          
           SELECT PRCHG-TYP-FILE                                                
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PRCHG-TYP-FILE                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PRCHG-TYP-LINE                   PIC X(200).                         
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD      PIC X(80).                               
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-PRCHG-TYP-CSR-SW   PIC  X(01)  VALUE 'N'.              
               88  PS-END-OF-PRCHG-TYP-CSR              VALUE 'Y'.              
               88  PS-NOT-END-OF-PRCHG-TYP-CSR          VALUE 'N'.              
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(1)    VALUE 'N'.              
070941         88  PS-END-OF-LOGON-FILE                 VALUE 'Y'.              
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC X(1)    VALUE 'N'.              
070941         88  PS-ORA-SRC-EMPTY                     VALUE 'Y'.              
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                         VALUE '/'.              
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
                                                           'PEKUT102'.          
           05  PC-DASH                      PIC  X(01)  VALUE '-'.              
           05  PC-SPACE                     PIC  X(01)  VALUE ' '.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4) VALUE ZEROS               
070941                                              COMP SYNC.                  
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4) VALUE ZEROS               
070941                                              COMP SYNC.                  
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-SYS-DATE                  PIC  X(26)  VALUE SPACES.           
           05  PV-SYS-DATE-XS REDEFINES PV-SYS-DATE.                            
               10  PV-SYS-DATE-DATE         PIC  X(10).                         
               10  PV-PERIOD                PIC  X(01).                         
               10  PV-SYS-DATE-TIME         PIC  X(15).                         
           05  PV-SELECT-COUNTER       PIC  S9(04) COMP VALUE +0.               
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRY-CNT        PIC  S9(04) COMP VALUE +10.              
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-EVNT-CTG-CDE          PIC  X(03) VALUE ZEROES.                
           05  PV-PRCHG-TYP-CDE         PIC  X(03) VALUE ZEROES.                
           05  PV-EVNT-CTG-DESC         PIC  X(30) VALUE ZEROES.                
           05  PV-PRCHG-TYP-DESC        PIC  X(30) VALUE ZEROES.                
           05  PV-ACTV-IND              PIC  X(01) VALUE ZEROES.                
           05  PV-SOR-CRE-TMST          PIC  X(26) VALUE ZEROES.                
           05  PV-SOR-LAST-UPD-TMST     PIC  X(26) VALUE ZEROES.                
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
           05  PA-PRCHG-TYP-CNT             PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE XXT_PRCHG_TYP DOMAIN TABLE.                         
      ******************************************************************        
           COPY PERD101.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - RESET SWITCHES                                             *         
      *  - OPEN  OUTPUT FILE                                          *         
      *  - SET UP ORACLE                                              *         
      *  - FIND THE CURRENT SYS DATE                                  *         
      *  - DUMP DATA FROM PET_PRCHG_TYP (GET DESC FROM PET_EVNT_CTG)  *         
      *  - WRITE THE TOTAL LINES                                      *         
      *  - CLOSE THE OUTPUT FILE                                      *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           OPEN OUTPUT PRCHG-TYP-FILE.                                          
                                                                                
070941*    PERFORM PE700-A000-ORACLE-SETUP.                                     
070941     PERFORM B120-READ-ORACLE-LOGON.                                      
                                                                                
           PERFORM C100-FIND-SYSDATE.                                           
                                                                                
           PERFORM C150-LOCK-TABLES.                                            
                                                                                
           PERFORM C200-DECLARE-PRCHG-TYP-CSR.                                  
           PERFORM C300-OPEN-PRCHG-TYP-CSR.                                     
           PERFORM C400-FETCH-PRCHG-TYP-CSR.                                    
           PERFORM C500-PROCESS-PRCHG-TYP-CSR                                   
               UNTIL PS-END-OF-PRCHG-TYP-CSR                                    
           PERFORM C600-CLOSE-PRCHG-TYP-CSR.                                    
                                                                                
           CLOSE  PRCHG-TYP-FILE.                                               
                                                                                
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
           DISPLAY 'PRCHGTYP CSR FETCH FROM ORACLE = ' PA-PRCHG-TYP-CNT.        
                                                                                
           STOP RUN.                                                            
                                                                                
070941 B120-READ-ORACLE-LOGON.                                                  
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
070941                                                                          
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                 ' - ORACLE LOGON CONTROL CARD NOT PROVIDED'              
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-CURRENT-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'        
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-CURRENT-PROGRAM-NAME                                     
070941              ' - LOGON TO ORACLE AS USER '                               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY 'B120-READ-ORACLE-LOGON'                                 
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                 ' - ERROR LOGGING ON TO ORACLE'                          
070941         PERFORM Z100-ABEND                                               
070941     END-IF.                                                              
070941                                                                          
070941                                                                          
070941     EXEC SQL                                                             
070941        ALTER SESSION                                                     
070941           SET NLS_DATE_FORMAT = 'YYYY-MM-DD-HH24.MI.SS'                  
070941     END-EXEC.                                                            
      *****************************************************************         
      * C100-FIND-SYSDATE:                                            *         
      * ==> PROCESSES:                                                *         
      *  - SINCE NO TABLE IS ACTUALLY USED WE USE THE DUAL TABLE      *         
      *  - FIND THE CURRENT DATE AND TIME                                       
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C100-FIND-SYSDATE.                                                       
           EXEC SQL                                                             
               SELECT                                                           
                   SYSDATE                                                      
               INTO :PV-SYS-DATE                                                
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 MOVE PC-DASH TO PV-PERIOD                                      
                 STRING PV-SYS-DATE-XS DELIMITED BY SPACE                       
                        '.000000'      DELIMITED BY SIZE                        
                        INTO PV-SYS-DATE-XS                                     
                 DISPLAY 'SYS DATE = ' PV-SYS-DATE-XS                           
                 CONTINUE                                                       
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-FIND-SYSDATE'   TO PV-PARAGRAPH-NAME                  
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C150-LOCK-TABLES.                                             *         
      * ==> PROCESSES:                                                *         
      *  - LOCKS TABLES WHILE PROGRAM IS RUNNING SO THAT THE EXTRACT  *         
      *    WILL MATCH DOMAINS.                                        *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C150-LOCK-TABLES.                                                        
                                                                                
           PERFORM WITH TEST AFTER                                              
             VARYING PV-SELECT-COUNTER                                          
             FROM 1 BY 1                                                        
           UNTIL (PV-SELECT-COUNTER > PC-MAX-RETRY-CNT OR                       
                  SQLCODE NOT = -0054)                                          
               EXEC SQL                                                         
                   LOCK TABLE PET_PRCHG_TYP                                     
                             ,PET_EVNT_CTG                                      
                             IN EXCLUSIVE MODE NOWAIT                           
               END-EXEC                                                         
               EVALUATE TRUE                                                    
                 WHEN SQLCODE = ZEROES                                          
                     DISPLAY 'PET_PRCHG_TYP TABLE IS LOCKED...'                 
                     DISPLAY 'PET_EVNT_CTG  TABLE IS LOCKED...'                 
                 WHEN SQLCODE = -0054                                           
                     CONTINUE                                                   
                 WHEN OTHER                                                     
                     MOVE 'C150-LOCK-TABLES                '                    
                                       TO PV-PARAGRAPH-NAME                     
                     MOVE 'ERROR LOCKING TABLES           '                     
                                       TO PV-OPERATION-MSG-1                    
                     PERFORM Z100-ABEND                                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SELECT-COUNTER > PC-MAX-RETRY-CNT                              
               DISPLAY 'MAX RETRY ATTEMPTING TO LOCK TABLES:  '                 
               DISPLAY 'PET_PRCHG_TYP,'                                         
               DISPLAY 'PET_EVNT_CTG.'                                          
               DISPLAY 'RETRIED ' PC-MAX-RETRY-CNT ' TIMES - '                  
               DISPLAY 'RESTART JOB IN A FEW MINUTES.'                          
               MOVE 'C150-LOCK-TABLES                '                          
                                 TO PV-PARAGRAPH-NAME                           
               MOVE 'ERROR LOCKING TABLES           '                           
                                 TO PV-OPERATION-MSG-1                          
               PERFORM Z100-ABEND                                               
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * C200-DECLARE-PRCHG-TYP-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - USED A VIEW IN THE FROM CLAUSE INSTEAD OF IND TABLES       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C200-DECLARE-PRCHG-TYP-CSR.                                              
                                                                                
           EXEC SQL                                                             
                DECLARE PCSR                                                    
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.EVNT_CTG_CDE                                         
                        ,A.PRCHG_TYP_CDE                                        
                        ,A.PRCHG_TYP_DESC                                       
                        ,A.ACTV_IND                                             
                        ,TO_CHAR(A.CRE_TMST,                                    
                                 'YYYY-MM-DD-HH24.MI.SS')                       
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                 'YYYY-MM-DD-HH24.MI.SS')                       
                        ,B.EVNT_CTG_DESC                                        
                    FROM PET_PRCHG_TYP A                                        
                        ,PET_EVNT_CTG B                                         
                    WHERE                                                       
                          A.EVNT_CTG_CDE = B.EVNT_CTG_CDE                       
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      * C300-OPEN-PRCHG-TYP-CSR.                                    *           
      * ==> PROCESSES:                                              *           
      *  - OPEN PRCHG-TYP-CSR                                       *           
      * ==> SQLCODE:                                                *           
      *  - 0 = SUCCESSFUL OPEN                                      *           
      * ==> PERFORMED FROM: A100-MAINLINE                           *           
      ***************************************************************           
       C300-OPEN-PRCHG-TYP-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                 OPEN PCSR                                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-PRCHG-TYP-CSR      '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PRCHG-TYP-CSR '                          
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C400-FETCH-PRCHG-TYP-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C500-PROCESS-PRCHG-TYP-CSR *         
      *****************************************************************         
       C400-FETCH-PRCHG-TYP-CSR.                                                
                                                                                
           EXEC SQL                                                             
               FETCH PCSR                                                       
                INTO  :PV-EVNT-CTG-CDE                                          
                    , :PV-PRCHG-TYP-CDE                                         
                    , :PV-PRCHG-TYP-DESC                                        
                    , :PV-ACTV-IND                                              
                    , :PV-SOR-CRE-TMST                                          
                    , :PV-SOR-LAST-UPD-TMST                                     
                    , :PV-EVNT-CTG-DESC                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   PERFORM D100-MOVE-TO-PRCHG-TYP-CPYBK                         
                   ADD 1 TO PA-PRCHG-TYP-CNT                                    
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'PRCHG-TYP COUNT FROM FETCH = '                      
                                                       PA-PRCHG-TYP-CNT         
                   DISPLAY 'END OF PRCHG-TYP CSR'                               
                   SET PS-END-OF-PRCHG-TYP-CSR TO TRUE                          
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C400-FETCH-PRCHG-TYP-CSR        '                      
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PRCHG-TYP-CSR   '                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C500-PROCESS-PRCHG-TYP-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C500-PROCESS-PRCHG-TYP-CSR.                                              
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C400-FETCH-PRCHG-TYP-CSR.                                    
                                                                                
      *****************************************************************         
      * C600-CLOSE-PRCHG-TYP-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PRCHG-TYP-CSR                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFUL                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C600-CLOSE-PRCHG-TYP-CSR.                                                
                                                                                
           EXEC SQL                                                             
                CLOSE PCSR                                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C600-CLOSE-PRCHG-TYP-CSR      '                         
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PRCHG-TYP-CSR     '                      
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D100-MOVE-TO-PRCHG-TYP-CPYBK                                  *         
      * ==> PROCESSES:                                                *         
      *  - MOVE THE VARIABLES TO THE COPYBOOK                         *         
      * ==> PERFORMED FROM: C400-FETCH-PRCHG-TYP-CSR                  *         
      *****************************************************************         
       D100-MOVE-TO-PRCHG-TYP-CPYBK.                                            
                                                                                
           INITIALIZE PE101-RECORD.                                             
           MOVE PV-EVNT-CTG-CDE       TO PE101-EVNT-CTG-CDE.                    
           MOVE PV-PRCHG-TYP-CDE      TO PE101-PRCHG-TYP-CDE.                   
           MOVE PV-EVNT-CTG-DESC      TO PE101-EVNT-CTG-DESC.                   
           MOVE PV-PRCHG-TYP-DESC     TO PE101-PRCHG-TYP-DESC.                  
           MOVE PV-ACTV-IND           TO PE101-ACTV-IND.                        
           STRING PV-SOR-CRE-TMST DELIMITED BY SPACE                            
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PE101-SOR-CRE-TMST.                                      
           STRING PV-SOR-LAST-UPD-TMST DELIMITED BY SPACE                       
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PE101-SOR-LAST-UPD-TMST.                                 
                                                                                
      *****************************************************************         
      * W100-WRITE-DETAIL.                                            *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DETAIL LINE TO THE OUTPUT FILE                   *         
      * ==> PERFORMED FROM: C500-PROCESS-PRCHG-TYP-CSR                *         
      *****************************************************************         
       W100-WRITE-DETAIL.                                                       
                                                                                
           WRITE PRCHG-TYP-LINE FROM PE101-RECORD.                              
                                                                                
           ADD +1                    TO PA-RECS-WRITTEN.                        
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PEPD700.                                                        
                                                                                
      *****************************************************************         
      * Z100-ABEND                                                    *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE            *         
      *****************************************************************         
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
