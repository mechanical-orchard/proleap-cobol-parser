000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    SUKUP034.                                                 
000400 AUTHOR.        JIM ARENDT.                                               
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN   MARCH 2017.                                               
000700 DATE-COMPILED.                                                           
000800 ENVIRONMENT DIVISION.                                                    
000900 CONFIGURATION SECTION.                                                   
001000 SOURCE-COMPUTER.        IBM-3090.                                        
001100 OBJECT-COMPUTER.        IBM-3090.                                        
001200                                                                          
002000******************************************************************        
001300 INPUT-OUTPUT SECTION.                                                    
001400 FILE-CONTROL.                                                            
001500                                                                          
001600     SELECT INPUT-FILE                                                    
001700         ASSIGN TO INP01.                                                 
001800                                                                          
002000******************************************************************        
002100 DATA DIVISION.                                                           
002200 FILE SECTION.                                                            
002300                                                                          
002400 FD  INPUT-FILE                                                           
002500     RECORDING MODE IS F                                                  
002600     LABEL RECORDS ARE STANDARD                                           
002700     BLOCK CONTAINS 0 RECORDS.                                            
002800                                                                          
003000 01  INPUT-REC                       PIC X(100).                          
003100                                                                          
002000******************************************************************        
003200 WORKING-STORAGE SECTION.                                                 
005400 01  PC-PROGRAM-CONSTANTS.                                                
005500     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08) VALUE 'SUKUP034'.         
005500     05  PC-YES                      PIC  X(01) VALUE 'Y'.                
008600     05  PC-COMMIT-HOLD              PIC  9(03) VALUE 100.                
004500                                                                          
006000 01  PV-PROGRAM-VARIABLES.                                                
006200     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
006300     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
006400     05  PV-READ-COUNTER             PIC  9(09)    VALUE ZERO.            
006400     05  PV-UPDATE-COUNTER           PIC  9(09)    VALUE ZERO.            
010500     05  PV-COMMIT-COUNTER           PIC  9(03)    VALUE ZERO.            
010300     05  PV-COMMIT-COUNT             PIC S9(09)    VALUE +0               
010400                                                   COMP SYNC.             
006400     05  PV-DISP-UNT-ID              PIC  9(18)    VALUE ZERO.            
006800     05  PV-DISPLAY-SQLCODE          PIC  999-     VALUE ZEROES.          
006800     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
004800                                                                          
004900 01  PS-PROGRAM-SWITCHES.                                                 
005000     05  PS-EOF-FILE-SW              PIC  X(01)    VALUE 'N'.             
005100         88  PS-EOF-YES                            VALUE 'Y'.             
005200         88  PS-EOF-NO                             VALUE 'N'.             
007000                                                                          
004600 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
004700                                                   COMP SYNC.             
005600                                                                          
002000******************************************************************        
003400     COPY SURD153.                                                        
003500                                                                          
003600     COPY DPWS004.                                                        
003300                                                                          
002000******************************************************************        
003700* DCLGEN TABLE(SUT_OTBND_STR_SCN)                                         
002000******************************************************************        
003800     EXEC SQL                                                             
003900         INCLUDE SUT00121                                                 
004000     END-EXEC.                                                            
004100                                                                          
004200     EXEC SQL                                                             
004300          INCLUDE SQLCA                                                   
004400     END-EXEC.                                                            
005300                                                                          
002000******************************************************************        
007100 PROCEDURE DIVISION.                                                      
007200                                                                          
007300     PERFORM 1000-INITIALIZE.                                             
007400                                                                          
007500     PERFORM 2000-PROCESS                                                 
007600        UNTIL PS-EOF-YES.                                                 
007700                                                                          
007800     PERFORM 9000-EOJ-ROUTINE.                                            
007900                                                                          
008000     GOBACK.                                                              
007900                                                                          
002000******************************************************************        
008200 1000-INITIALIZE.                                                         
008300                                                                          
008400     DISPLAY ' '.                                                         
008400     DISPLAY '* * * * * * * * * * * '.                                    
008500     DISPLAY ' PROGRAM ' PC-CURRENT-PROGRAM-NAME.                         
008400     DISPLAY ' '.                                                         
008600                                                                          
008700     OPEN INPUT INPUT-FILE.                                               
008600                                                                          
008800     PERFORM 8000-READ-INPUT-REC.                                         
008900                                                                          
015300     IF PS-EOF-YES                                                        
015300        DISPLAY ' '                                                       
015300        DISPLAY '** EMPTY INPUT FILE **'                                  
015300        DISPLAY ' '                                                       
015300     END-IF.                                                              
015300                                                                          
002000******************************************************************        
009000 2000-PROCESS.                                                            
009100                                                                          
009200     PERFORM 4000-UPDATE-RTN.                                             
013700     ADD +1                       TO PV-COMMIT-COUNTER.                   
013800                                                                          
013900     IF PV-COMMIT-COUNTER > PC-COMMIT-HOLD                                
014000         EXEC SQL                                                         
014100            COMMIT                                                        
014200         END-EXEC                                                         
014300                                                                          
014400         IF SQLCODE NOT = +0                                              
014500             DISPLAY '### COMMIT FAILED ! ###'                            
017800             MOVE '2000-PROCESS'  TO PV-PARAGRAPH-NAME                    
017900             MOVE 'COMMIT'        TO PV-DB2-OPERATION-ATTEMPTED           
014600             PERFORM 9100-SQL-ABEND                                       
014700         END-IF                                                           
014800                                                                          
014900         ADD  +1                  TO PV-COMMIT-COUNT                      
015000         MOVE +0                  TO PV-COMMIT-COUNTER                    
015100     END-IF.                                                              
009100                                                                          
009800     PERFORM 8000-READ-INPUT-REC.                                         
009900                                                                          
002000******************************************************************        
009200 4000-UPDATE-RTN.                                                         
052088                                                                          
120800     EXEC SQL                                                             
120900       UPDATE SUT_OTBND_STR_SCN                                           
121000          SET PRCD_IND        = :PC-YES                                   
121200        WHERE SHIPT_UNT_ID    = :SU153-SHIPT-UNT-ID                       
121300          AND CRE_TMST        = :SU153-CRTN-SCAN-TMST                     
121300          AND SCN_TYP_CDE     = :SU153-SCN-TYPE-CDE                       
121400     END-EXEC.                                                            
121500                                                                          
121600     EVALUATE TRUE                                                        
121700         WHEN SQLCODE = +0                                                
006400             ADD 1                    TO PV-UPDATE-COUNTER                
121500                                                                          
121900         WHEN OTHER                                                       
017800             MOVE '4000-UPDATE-RTN'   TO PV-PARAGRAPH-NAME                
017900             MOVE 'UPDATE PROCESSED INDICATOR'  TO                        
017900                                      PV-DB2-OPERATION-ATTEMPTED          
122500             DISPLAY '-- ERROR FIELDS --'                                 
122400             MOVE SU153-SHIPT-UNT-ID  TO PV-DISP-UNT-ID                   
122500             DISPLAY 'SHIPT-UNT-ID   = ' PV-DISP-UNT-ID                   
122700             DISPLAY 'CARTN-SCAN-TMST= ' SU153-CRTN-SCAN-TMST             
122700             DISPLAY 'SCN-TYPE-CDE   = ' SU153-SCN-TYPE-CDE               
122800             MOVE  SQLCODE            TO PV-DISPLAY-SQLCODE               
122800             DISPLAY 'SQLCODE = '        PV-DISPLAY-SQLCODE               
122800             DISPLAY ' '                                                  
017400             PERFORM 9100-SQL-ABEND                                       
124800     END-EVALUATE.                                                        
124900                                                                          
002000******************************************************************        
014900 8000-READ-INPUT-REC.                                                     
014900                                                                          
015000     READ INPUT-FILE INTO                                                 
015100         SU153-EXTRACT-RECORD                                             
015200     AT END                                                               
015300         SET PS-EOF-YES               TO TRUE                             
015400     NOT AT END                                                           
015500         ADD +1                       TO PV-READ-COUNTER                  
015600     END-READ.                                                            
015700                                                                          
002000******************************************************************        
015800 9000-EOJ-ROUTINE.                                                        
015900                                                                          
016000     CLOSE INPUT-FILE.                                                    
016100                                                                          
016200     DISPLAY '-- PROGRAM TOTALS --'.                                      
016200     MOVE PV-READ-COUNTER             TO PV-DISPLAY-COUNT.                
016300     DISPLAY 'TOTAL RECORDS READ    = '  PV-DISPLAY-COUNT.                
016400                                                                          
016200     MOVE PV-UPDATE-COUNTER           TO PV-DISPLAY-COUNT.                
016300     DISPLAY 'TOTAL RECORDS UPDATED = '  PV-DISPLAY-COUNT.                
016400                                                                          
020500     MOVE PV-COMMIT-COUNT             TO PV-DISPLAY-COUNT.                
020600     DISPLAY 'TOTAL COMMITS TAKEN   = '  PV-DISPLAY-COUNT.                
016400                                                                          
017200     DISPLAY ' '.                                                         
017200     DISPLAY '* * * * * * * * * * * '.                                    
017200     DISPLAY ' '.                                                         
017300                                                                          
002000******************************************************************        
017400 9100-SQL-ABEND.                                                          
017300                                                                          
017600     DISPLAY '** ABEND     **'.                                           
017700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
017800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
017900     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
018000                                                                          
018100     COPY DPPD004.                                                        
018200                                                                          
018300     MOVE +4013                  TO ABEND-CODE.                           
018400     CALL 'ILBOABN0' USING ABEND-CODE.                                    
018500                                                                          
