      *-----------------------*                                         03/19/93
       IDENTIFICATION DIVISION.                                         TFKCS435
      *-----------------------*                                            LV067
       PROGRAM-ID.    TFKCS435.                                            CL**1
       AUTHOR.        JACK KRAMER.                                         CL**1
       INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**1
       DATE-WRITTEN.  SEPTEMBER 1996.                                      CL**1
       DATE-COMPILED.                                                      CL**1
                                                                           CL**1
      *---------------------------------------------------------------*    CL**1
      *    P435 - TRANSFER ROUTING INQUIRY.                           *    CL**1
      *                                                               *    CL**1
      *    THIS PROGRAM DISPLAYS STORE ROUTING INFORMATION FOR A      *    CL**1
      *    SPECIFIED TRANSFER.                                        *    CL**1
      *                                                               *    CL**1
      *---------------------------------------------------------------*    CL**1
      *  1/20/2003 - RAY GRAF                                                   
      *   PROCESS NONINTELLIGENT SKU.                                           
      ******************************************************************        
080204**DATE:08/02/2004 CHANGED BY: RAY GRAF                                    
080204**     RECOMPILE DUE TO PRINTER EXPANSION IN TFWS001                      
      *****************************************************************         
100404**DATE:10/04/2004 CHANGED BY: RAY GRAF                                    
100404**     EXPAND STORE NUMBER. MAKE DOMAIN COMPLIANT.                        
      *****************************************************************         
      * DATE:        09/24/2021                                                 
      * WR/IR#:      CHG0261678                                                 
      * PROGRAMMER:  GERMAN BANDA                                               
      * DESCRIPTION: CHANGE CALL OF DPKUT100 TO                                 
      *              CALL DP010I-NUMERIC-EDIT-ROUTINE                           
      *              MAKING THE CALL DYNAMIC VS STATIC                          
      *----------------------------------------------------------------*        
CICS  * CHANGED 11/17/2022    BY JIM HUNTER      WR: CHG0278345                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
                                                                           CL**1
       ENVIRONMENT DIVISION.                                               CL**1
                                                                           CL**1
       DATA DIVISION.                                                      CL**1
                                                                           CL**1
       WORKING-STORAGE SECTION.                                            CL**1
                                                                           CL**1
       01  PC-PROGRAM-CONSTANTS.                                           CL**1
           05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE            CL**1
                                                          'TF435A  '.      CL**2
           05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE            CL**1
                                                          'TFKM435 '.      CL**2
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE            CL**1
                                                          'TFKCS435'.      CL**2
           05  PC-DTL-QUEUE-SEQ               PIC  9(01)  VALUE 3.         CL**2
           05  PC-POSITIVE-RECEIPT            PIC X(02)    VALUE 'PR'.     CL**1
           05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +14        CL**2
                                                          COMP SYNC.       CL**1
           05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2         CL**1
                                                          COMP SYNC.       CL**1
           05  PC-MAX-ITEMS                   PIC S9(09)  VALUE            CL**1
                                                          +1000000         CL**1
                                                          COMP-3.          CL**1
           05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5         CL**1
                                                          COMP SYNC.       CL**1
           05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3         CL**1
                                                           COMP-3.         CL**1
                                                                           CL**8
       01  PC-TSYMSG-NUMBERS.                                              CL**1
           05  PC-TSYMSG-00008                PIC 9(5)    VALUE 00008.     CL**1
           05  PC-TSYMSG-00050                PIC 9(5)    VALUE 00050.     CL**1
           05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.     CL**1
           05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.     CL**1
           05  PC-TSYMSG-00101                PIC 9(5)    VALUE 00101.     CL**1
           05  PC-TSYMSG-00148                PIC 9(5)    VALUE 00148.     CL*41
           05  PC-TSYMSG-00279                PIC 9(5)    VALUE 00279.     CL*66
           05  PC-TSYMSG-00302                PIC 9(5)    VALUE 00302.     CL*41
                                                                           CL**8
       01  PS-PROGRAM-SUBSCRIPTS.                                          CL**1
           05  PS-SUB                         PIC S9(04)   VALUE +0        CL**1
                                                           COMP SYNC.      CL**1
                                                                           CL**1
       01  PS-PROGRAM-SWITCHES.                                            CL**1
           05  PS-TRANSFER-FOUND-SW     PIC X.                          00609107
               88  PS-TRANSFER-FOUND             VALUE 'Y'.             00609207
               88  PS-TRANSFER-NOT-FOUND         VALUE 'N'.             00609307
           05  PS-ERROR-SW                    PIC X(01)    VALUE 'Y'.      CL**1
               88  PS-NO-ERROR                             VALUE 'Y'.      CL**1
               88  PS-ERROR                                VALUE 'N'.      CL**1
           05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.      CL**1
               88  PS-COMMAREA-VALID                       VALUE 'Y'.      CL**1
               88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.      CL**1
           EJECT                                                           CL**1
       01  PV-PROGRAM-VARIABLES.                                           CL**1
           05  PV-XFER-NBR.                                             00700000
100404*        10  PV-XFER-FROM-STORE-NBR     PIC 9(03).                        
100404         10  PV-XFER-FROM-STORE-NBR     PIC 9(04).                        
               10                             PIC X    VALUE '-'.               
               10  PV-XFER-SEQ-NBR            PIC 9(06).                        
           05  PV-DATE.                                                 00700000
               10  PV-DATE-MM                 PIC X(02).                        
               10                             PIC X    VALUE '/'.               
               10  PV-DATE-DD                 PIC X(02).                        
               10                             PIC X    VALUE '/'.               
               10  PV-DATE-YYYY               PIC X(04).                        
           05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0            00700000
                                                    COMP SYNC.          00700122
           05  PV-REMAINDER                   PIC S9(04)   VALUE +0        CL**1
                                                           COMP SYNC.      CL**1
           05  PV-READ-COUNT                  PIC S9(09)   VALUE +0        CL**1
                                                           COMP-3.         CL**1
           05  PV-TOP-ITEM                    PIC S9(09)   VALUE +0        CL**1
                                                           COMP-3.         CL**1
           05  PV-BOTTOM-ITEM                 PIC S9(09)   VALUE +0        CL*58
                                                           COMP-3.         CL*58
           05  PV-BLOCK-NUMBER                PIC S9(04)   VALUE +0        CL**1
                                                           COMP SYNC.      CL**1
           05  PV-CICS-RESP                   PIC S9(04)   VALUE +0        CL**1
                                                           COMP SYNC.      CL**1
           05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)   VALUE +0        CL**1
                                                           COMP SYNC.      CL**1
           05  PV-LIST-ITEM-NUMBER            PIC S9(04)   VALUE +0        CL**1
                                                           COMP SYNC.      CL**1
           05  PV-SQL-SUB                     PIC S9(4)    COMP.           CL**1
                                                                           CL*42
       01  DK-DB2-KEY-AREA.                                                CL**1
           15  DK-XFER-DKEY-ID             PIC S9(09)  VALUE ZERO          CL**1
                                                       COMP.                    
           15  DK-STORE-NBR                PIC S9(04)  VALUE ZERO       00291007
                                                       COMP.                    
                                                                           CL**1
       EJECT                                                               CL**1
      *---------------------------------------------------------------*    CL**1
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *    CL**1
      *---------------------------------------------------------------*    CL**1
                                                                           CL**1
           COPY DPWS030.                                                   CL*67
CICS       COPY DPWS930.                                                   CL*67
       EJECT                                                               CL**1
      *---------------------------------------------------------------*    CL**1
      *    STANDARD COMMAREA.                                         *    CL**1
      *---------------------------------------------------------------*    CL**1
                                                                           CL**1
           COPY DPWS020.                                                   CL**1
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                   CL**1
                                                                           CL**1
      *---------------------------------------------------------------*    CL**1
      *    INTER-APPLICATION COMMAREA.                                *    CL**1
      *---------------------------------------------------------------*    CL**1
                                                                           CL**1
               10  INTER-APPL-COMM-AREA       PIC X(200).                  CL**1
           COPY TFWS001.                                                   CL*67
      *---------------------------------------------------------------*    CL**1
      *    SPECIFIC COMMMAREA FOR TFKCS435.                           *    CL**2
      *---------------------------------------------------------------*    CL**1
               10  ASC-SPECIFIC-COMMAREA.                                  CL**1
                   15  ASC-LIST-KEYS.                                      CL**1
                       20  ASC-XFER-DKEY-ID                PIC S9(09)      CL**1
                                                           COMP.           CL**1
                   15  ASC-XFER-SEQ-NBR                    PIC 9(06).      CL**1
100404*            15  ASC-FROM-STORE-NBR                  PIC 9(03).      CL*18
100404             15  ASC-FROM-STORE-NBR                  PIC 9(04).      CL*18
                   15  ASC-FROM-STORE-NAME                 PIC X(10).      CL*18
100404*            15  ASC-TO-STORE-NBR                    PIC 9(03).      CL*18
100404             15  ASC-TO-STORE-NBR                    PIC 9(04).      CL*18
                   15  ASC-TO-STORE-NAME                   PIC X(10).      CL*18
100404*            15  ASC-RECV-STORE-NBR                  PIC 9(03).      CL*18
100404             15  ASC-RECV-STORE-NBR                  PIC 9(04).      CL*18
                   15  ASC-RECV-STORE-NAME                 PIC X(10).      CL*18
                   15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                   CL**1
                       20  ASC-ITEM-ARRAY.                                 CL**1
                                                                           CL*11
                           25  ASC-ITEM-ENTRIES OCCURS 14 TIMES.           CL**2
                               30  ASC-LIST-ITEM-NUMBER    PIC S9(09)      CL*11
                                                           COMP-3.         CL*11
100404*                        30  ASC-STORE-NBR           PIC 9(03).      CL*18
100404                         30  ASC-STORE-NBR           PIC 9(04).      CL*18
                               30  ASC-STORE-NAME          PIC X(10).      CL*18
                               30  ASC-DATE.                               CL*18
                                   35  ASC-DATE-YYYY       PIC X(04).      CL*18
                                   35                      PIC X.               
                                   35  ASC-DATE-MM         PIC X(02).      CL*18
                                   35                      PIC X.               
                                   35  ASC-DATE-DD         PIC X(02).      CL*18
                               30  ASC-STATUS-DESC         PIC X(20).      CL*18
                                                                           CL**1
                   15  ASC-BLK-SUB                         PIC S9(04)      CL**1
                                                               COMP        CL**1
                                                               SYNC.       CL**1
                   15  ASC-ITEM-SUB                        PIC S9(04)      CL**1
                                                               COMP        CL**1
                                                               SYNC.       CL**1
                   15  ASC-SEL-SUB                         PIC S9(04)      CL*41
                                                               COMP.       CL*41
                   15  ASC-SEL-ITEM                        PIC S9(04)      CL*41
                                                               COMP.       CL*41
                   15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)      CL**1
                                                               COMP-3.     CL**1
                   15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)      CL*41
                                                               COMP-3.     CL*41
                   15  ASC-ITEMS-DISPLAYED                 PIC S9(03)      CL**1
                                                               COMP-3.     CL**1
                   15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)      CL**1
                                                               COMP-3.     CL**1
                   15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)      CL**1
                                                               COMP-3.     CL**1
                   15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)      CL**1
                                                               COMP        CL**1
                                                               SYNC.       CL**1
                   15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)      CL**1
                                                               COMP-3.     CL**1
                   15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)      CL**1
                                                               COMP-3.     CL**1
                   15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).     CL**1
                       88  ASC-ITEMS-LEFT-ON-DB              VALUE 'Y'.    CL**1
                       88  ASC-NO-ITEMS-LEFT-ON-DB           VALUE 'N'.    CL**1
                   15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).     CL*41
                       88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.    CL*41
                   15  ASC-DESC-LINE-IND                   PIC  X(01).     CL*45
                       88  ASC-DESC-LINE-DONE                VALUE 'Y'.    CL*45
                   15  ASC-CORR-LINE-IND                   PIC  X(01).     CL*45
                       88  ASC-CORR-LINE-DONE                VALUE 'Y'.    CL*45
                   15  ASC-LIST-QUEUE-NAME.                                CL**1
                       20  ASC-LIST-TERM-ID                PIC  X(04).     CL**1
                       20  ASC-LIST-TASK-NUMBER            PIC S9(05)      CL**1
                                                               COMP-3.     CL**1
                       20  ASC-LIST-SEQ                    PIC  9(01).     CL**1
                   15  ASC-DTL-QUEUE-NAME.                                 CL**2
                       20  ASC-DTL-TERM-ID                 PIC  X(04).     CL**2
                       20  ASC-DTL-TASK-NUMBER             PIC S9(05)      CL**2
                                                               COMP-3.     CL**2
                       20  ASC-DTL-SEQ                     PIC  9(01).     CL**2
                                                                           CL**1
                   15  ASC-START-ITEM-REQUEST.                             CL**1
                       20  ASC-START-ITEM-REQUEST-N        PIC  9(05).     CL**1
                                                                           CL**1
                                                                           CL**1
           EJECT                                                           CL**1
      *---------------------------------------------------------------*    CL**1
      *    ABEND PROCESSING COPYBOOK.                                 *    CL**1
      *---------------------------------------------------------------*    CL**1
                                                                           CL**1
           COPY DPWS013.                                                   CL**1
                                                                           CL**1
           EJECT                                                           CL**1
      *---------------------------------------------------------------*    CL**1
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *    CL**1
      *---------------------------------------------------------------*    CL**1
                                                                           CL**1
           COPY DPWS015.                                                   CL**1
                                                                           CL**1
      *---------------------------------------------------------------*    CL**1
      *    FUNCTION KEYS COPYBOOK.                                    *    CL**1
      *---------------------------------------------------------------*    CL**1
                                                                           CL**1
           COPY DPWS016.                                                   CL**1
                                                                           CL**1
      *---------------------------------------------------------------*    CL**1
      *    NUMERIC EDIT LAYOUT.                                       *    CL**1
      *---------------------------------------------------------------*    CL**1
                                                                           CL**1
           COPY DPWS010I.                                                  CL**1
                                                                           CL**1
      *---------------------------------------------------------------*    CL**1
      *    MAP LAYOUT                                                 *    CL**1
      *---------------------------------------------------------------*    CL**1
                                                                           CL**1
           COPY TFKM435.                                                   CL**1
                                                                           CL**8
       01  MR-MAP-REDEFINITION       REDEFINES  TF435AO.                   CL**1
           05  FILLER                PIC X(12).                            CL**1
           05  MR-XFER-NBRL          PIC S9(04) COMP.                      CL**1
           05  MR-XFER-NBRA          PIC  X(01).                           CL**1
           05  MR-XFER-NBRC          PIC  X(01).                           CL**1
           05  MR-XFER-NBRH          PIC  X(01).                           CL**1
100404*    05  MR-XFER-NBR           PIC  X(10).                           CL**1
100404     05  MR-XFER-NBR           PIC  X(11).                           CL**1
      *                                                                         
           05  MR-FROM-STORE-NBRL    PIC S9(04) COMP.                      CL**1
           05  MR-FROM-STORE-NBRA    PIC  X(01).                           CL**1
           05  MR-FROM-STORE-NBRC    PIC  X(01).                           CL**1
           05  MR-FROM-STORE-NBRH    PIC  X(01).                           CL**1
100404*    05  MR-FROM-STORE-NBR     PIC  9(03).                           CL**1
100404     05  MR-FROM-STORE-NBR     PIC  9(04).                           CL**1
      *                                                                         
           05  MR-FROM-STORE-NAMEL   PIC S9(04) COMP.                      CL**1
           05  MR-FROM-STORE-NAMEA   PIC  X(01).                           CL**1
           05  MR-FROM-STORE-NAMEC   PIC  X(01).                           CL**1
           05  MR-FROM-STORE-NAMEH   PIC  X(01).                           CL**1
           05  MR-FROM-STORE-NAME    PIC  X(10).                           CL**1
      *                                                                         
           05  MR-TO-STORE-NBRL      PIC S9(04) COMP.                      CL**1
           05  MR-TO-STORE-NBRA      PIC  X(01).                           CL**1
           05  MR-TO-STORE-NBRC      PIC  X(01).                           CL**1
           05  MR-TO-STORE-NBRH      PIC  X(01).                           CL**1
100404*    05  MR-TO-STORE-NBR       PIC  9(03).                           CL**1
100404     05  MR-TO-STORE-NBR       PIC  9(04).                           CL**1
      *                                                                         
           05  MR-TO-STORE-NAMEL     PIC S9(04) COMP.                      CL**1
           05  MR-TO-STORE-NAMEA     PIC  X(01).                           CL**1
           05  MR-TO-STORE-NAMEC     PIC  X(01).                           CL**1
           05  MR-TO-STORE-NAMEH     PIC  X(01).                           CL**1
           05  MR-TO-STORE-NAME      PIC  X(10).                           CL**1
      *                                                                         
           05  MR-RECV-STORE-NBRL    PIC S9(04) COMP.                      CL**1
           05  MR-RECV-STORE-NBRA    PIC  X(01).                           CL**1
           05  MR-RECV-STORE-NBRC    PIC  X(01).                           CL**1
           05  MR-RECV-STORE-NBRH    PIC  X(01).                           CL**1
100404*    05  MR-RECV-STORE-NBR     PIC  9(03).                           CL**1
100404     05  MR-RECV-STORE-NBR     PIC  9(04).                           CL**1
      *                                                                         
           05  MR-RECV-STORE-NAMEL   PIC S9(04) COMP.                      CL**1
           05  MR-RECV-STORE-NAMEA   PIC  X(01).                           CL**1
           05  MR-RECV-STORE-NAMEC   PIC  X(01).                           CL**1
           05  MR-RECV-STORE-NAMEH   PIC  X(01).                           CL**1
           05  MR-RECV-STORE-NAME    PIC  X(10).                           CL**1
      *                                                                         
           05  MR-STARTL             PIC S9(04) COMP.                           
           05  MR-STARTA             PIC  X(01).                                
           05  MR-STARTC             PIC  X(01).                                
           05  MR-STARTH             PIC  X(01).                                
           05  MR-START              PIC 9(05).                                 
      *                                                                         
           05  MR-ENDL               PIC S9(04) COMP.                           
           05  MR-ENDA               PIC  X(01).                                
           05  MR-ENDC               PIC  X(01).                                
           05  MR-ENDH               PIC  X(01).                                
           05  MR-END                PIC 9(05).                                 
      *                                                                         
           05  MR-TOTALL             PIC S9(04) COMP.                           
           05  MR-TOTALA             PIC  X(01).                                
           05  MR-TOTALC             PIC  X(01).                                
           05  MR-TOTALH             PIC  X(01).                                
           05  MR-TOTAL              PIC 9(05).                                 
      *                                                                         
           05  MR-SCROLL-AREA OCCURS 13 TIMES.                             CL**1
               10  MR-STORE-NBRL     PIC S9(04) COMP.                      CL**1
               10  MR-STORE-NBRA     PIC  X(01).                           CL**1
               10  MR-STORE-NBRC     PIC  X(01).                           CL**1
               10  MR-STORE-NBRH     PIC  X(01).                           CL**1
               10  MR-STORE-NBR-X.                                         CL**1
100404*            15  MR-STORE-NBR  PIC  9(03).                           CL**1
100404             15  MR-STORE-NBR  PIC  9(04).                           CL**1
      *                                                                         
               10  MR-STORE-NAMEL    PIC S9(04) COMP.                      CL**1
               10  MR-STORE-NAMEA    PIC  X(01).                           CL**1
               10  MR-STORE-NAMEC    PIC  X(01).                           CL**1
               10  MR-STORE-NAMEH    PIC  X(01).                           CL**1
               10  MR-STORE-NAME     PIC  X(10).                           CL**1
      *                                                                         
               10  MR-DATEL          PIC S9(04) COMP.                      CL**1
               10  MR-DATEA          PIC  X(01).                           CL**1
               10  MR-DATEC          PIC  X(01).                           CL**1
               10  MR-DATEH          PIC  X(01).                           CL**1
               10  MR-DATE           PIC  X(10).                           CL**1
      *                                                                         
               10  MR-STATUS-DESCL   PIC S9(04) COMP.                      CL**1
               10  MR-STATUS-DESCA   PIC  X(01).                           CL**1
               10  MR-STATUS-DESCC   PIC  X(01).                           CL**1
               10  MR-STATUS-DESCH   PIC  X(01).                           CL**1
               10  MR-STATUS-DESC    PIC  X(20).                           CL**1
      *                                                                         
                                                                           CL**1
       EJECT                                                               CL**1
      *---------------------------------------------------------------*    CL**1
      *    DB2 AREA FOR TRANSFER TABLE                                *    CL**1
      *---------------------------------------------------------------*    CL**1
           EXEC SQL                                                        CL**1
                INCLUDE TTFMSTR                                            CL**1
           END-EXEC.                                                       CL**1
      *---------------------------------------------------------------*    CL**1
      *    DB2 AREA FOR TRANSFER RECEIPT TABLE                        *    CL**1
      *---------------------------------------------------------------*    CL**1
           EXEC SQL                                                        CL**1
                INCLUDE TTFRCPT                                            CL**1
           END-EXEC.                                                       CL**1
      *---------------------------------------------------------------*    CL**1
      *    DB2 AREA FOR TRANSFER STATUS TABLE                         *    CL**1
      *---------------------------------------------------------------*    CL**1
           EXEC SQL                                                        CL**1
                INCLUDE TTFSTAT                                            CL**1
           END-EXEC.                                                       CL**1
      *---------------------------------------------------------------*    CL**1
      *    DB2 AREA FOR TRANSFER STATUS CODE TABLE                    *    CL**1
      *---------------------------------------------------------------*    CL**1
           EXEC SQL                                                        CL**1
                INCLUDE TTFSTCD                                            CL**1
           END-EXEC.                                                       CL**1
      *---------------------------------------------------------------*    CL**1
100404*    DB2 AREA FOR XST_LOC TABLE                                 *    CL**1
      *---------------------------------------------------------------*    CL**1
           EXEC SQL                                                        CL**1
100404          INCLUDE XST00001                                           CL**1
           END-EXEC.                                                       CL**1
      *---------------------------------------------------------------*    CL**1
      *    DB2 AREA FOR COMMUNICATIONS                                *    CL**1
      *---------------------------------------------------------------*    CL**1
           EXEC SQL                                                        CL**1
                INCLUDE SQLCA                                              CL**1
           END-EXEC.                                                       CL**1
                                                                           CL**1
      *---------------------------------------------------------------*    CL**1
      *    CURSOR TO OBTAIN TRANSFER ROUTING INFORMATION              *    CL**1
      *---------------------------------------------------------------*    CL**1
                                                                           CL**1
           EXEC SQL                                                        CL**1
                DECLARE ROUTING-CSR CURSOR                                 CL**1
                FOR SELECT                                                 CL**1
                      R.STR_NBR                                            CL**1
                    , R.RCV_XFER_BEG_TMST                                  CL**1
100404              , S.LOC_10_ABBR_NM                                     CL**1
                    , SC.XFER_STAT_DESC                                    CL**1
                FROM                                                       CL**1
                      TTFRCPT R                                            CL**1
100404              , XST_LOC S                                            CL**1
                    , TTFSTAT ST                                           CL**1
                    , TTFSTCD SC                                           CL**1
                WHERE                                                      CL**1
                      R.XFER_DKEY_ID       = :DK-XFER-DKEY-ID    AND       CL**1
                      ST.XFER_DKEY_ID      = :DK-XFER-DKEY-ID    AND       CL**1
                      ST.XFER_STAT_TMST    = R.RCV_XFER_BEG_TMST AND       CL**1
                      ST.XFER_STAT_CDE    IN ('CM', 'PR', 'IR')  AND       CL**1
                      ST.XFER_STAT_CDE     = SC.XFER_STAT_CDE    AND       CL**1
100404                R.STR_NBR            = S.LOC_NBR                     CL**1
                ORDER BY                                                   CL**1
                      R.RCV_XFER_BEG_TMST                                  CL**1
100404          WITH UR                                                         
           END-EXEC.                                                       CL**1
                                                                           CL**1
       EJECT                                                               CL**1
       LINKAGE SECTION.                                                    CL**1
                                                                           CL**1
       01  DFHCOMMAREA.                                                    CL**1
           05  FILLER                         OCCURS  1 TO 4072 TIMES      CL**1
                                              DEPENDING ON EIBCALEN.       CL**1
               10  FILLER                     PIC X.                       CL**1
                                                                           CL**1
       EJECT                                                               CL**1
       PROCEDURE DIVISION.                                                 CL**1
                                                                           CL**1
       0000-MAIN-MODULE.                                                   CL**1
                                                                           CL**8
           INITIALIZE DP030-CICS-API-FIELDS.                               CL**1
           MOVE +1                      TO DP030-NUMBER-OF-MAPS.           CL**1
           MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.              CL**1
           MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).             CL**1
           SET DP030-RECEIVE-APPL-MAP   TO TRUE.                           CL**1
           MOVE LENGTH OF TF435AI       TO DP030-MAP-LENGTH (1).           CL**1
           MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).         CL**1
           PERFORM 0001-CALL-CICS-ARCH-API.                                CL**1
                                                                           CL**8
           PERFORM 1000-CONTROL-PROCESSING.                                CL**1
                                                                           CL**8
           MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).         CL**1
           PERFORM 0001-CALL-CICS-ARCH-API.                                CL**1
                                                                           CL**1
      *----------------------------------------------------------------*   CL*63
      *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *   CL*63
      *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *   CL*63
      *----------------------------------------------------------------*   CL*63
                                                                           CL*63
       0001-CALL-CICS-ARCH-API.                                            CL**1
                                                                           CL**8
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          TF435AI.                              
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                    CL**1
               CONTINUE                                                    CL**1
           ELSE                                                            CL**1
               SET DP013-NO-ROLLBACK TO TRUE                               CL**1
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                      CL**1
               SET DP013-CICS-ABEND TO TRUE                                CL**1
               MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH           CL**1
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                      CL**1
                                     TO DP013-MESSAGE-TEXT (3)             CL**1
               PERFORM DP013-0000-PROCESS-ABEND                            CL**1
           END-IF.                                                         CL**1
                                                                           CL**8
      *----------------------------------------------------------------*   CL**1
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *   CL**1
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *   CL**1
      *                                                                *   CL**1
      * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *   CL**1
      *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *   CL**1
      *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *   CL**1
      *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       1000-CONTROL-PROCESSING.                                            CL**1
                                                                           CL**1
           EVALUATE TRUE                                                   CL**1
               WHEN DP020-NEXT-ACT-INITIAL                                 CL**1
                   MOVE EIBTRMID          TO ASC-DTL-TERM-ID               CL**2
                   MOVE DP020-SRC-TASK-NUMBER                              CL**1
                                          TO ASC-DTL-TASK-NUMBER           CL**2
                   MOVE PC-DTL-QUEUE-SEQ  TO ASC-DTL-SEQ                   CL**2
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                    CL**1
      ***-----$TEMPLATE NOTE$-------------------------------------------   CL**1
      *** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,   CL**1
      *** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO     CL**1
      *** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.   CL**1
      ***---------------------------------------------------------------   CL**1
                   IF  PS-COMMAREA-NOT-VALID                               CL**1
      ***              MOVE PC-TSYMSG-99901                                CL**1
      ***                            TO  DP020-MSG-NUMBER                  CL**1
      ***              SET DP020-MSG-FATAL                                 CL**1
      ***                            TO  TRUE                              CL**1
                       SET DP020-NEXT-ACT-APPL-ERROR                       CL**1
                                     TO  TRUE                              CL**1
                   ELSE                                                    CL**1
                       SET PS-TRANSFER-NOT-FOUND  TO TRUE               06700007
                       PERFORM 4100-GET-TRANSFER                        06670003
                       IF PS-TRANSFER-FOUND                             06700007
                           PERFORM 2050-DELETE-DETAIL-QUEUE                CL*56
                           PERFORM 4000-BUILD-INITIAL-PANEL                CL**1
                       ELSE                                                CL**4
                           MOVE PC-TSYMSG-00148                            CL**4
                                     TO DP020-MSG-NUMBER                   CL**4
                           SET DP020-MSG-FATAL                             CL**4
                           DP020-NEXT-ACT-APPL-ERROR  TO TRUE              CL**4
                       END-IF                                           06720007
                   END-IF                                                  CL**1
                                                                           CL**1
               WHEN DP020-NEXT-ACT-READ-MAP                                CL**1
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                           CL**1
                                     TO PV-TOP-ITEM                        CL**1
                   PERFORM 2000-PROCESS-PANEL                              CL**1
                                                                           CL**1
               WHEN DP020-NEXT-ACT-RETURN                                  CL**1
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                           CL**1
                                     TO PV-TOP-ITEM                        CL**1
                   MOVE ZERO         TO ASC-ITEM-AT-TOP-OF-PANEL           CL**1
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**1
                                                                           CL**1
               WHEN OTHER                                                  CL**1
                   SET DP013-LOGIC-ABEND TO TRUE                           CL**1
                   SET DP013-NO-ROLLBACK TO TRUE                           CL**1
                   MOVE '1000-CONTROL-PROCESSING'                          CL**1
                                     TO  DP013-PARAGRAPH                   CL**1
                   MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'       CL**1
                                     TO  DP013-MESSAGE-TEXT(1)             CL**1
                   PERFORM DP013-0000-PROCESS-ABEND                        CL**1
           END-EVALUATE.                                                   CL**1
                                                                           CL**1
       EJECT                                                               CL**1
      *----------------------------------------------------------------*   CL**1
      * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA.      *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       1100-PROCESS-INTER-APPL-COMM.                                       CL**1
                                                                           CL**1
           SET PS-COMMAREA-VALID     TO TRUE.                              CL**1
           INITIALIZE ASC-SPECIFIC-COMMAREA.                               CL**1
           MOVE SPACE                TO ASC-START-ITEM-REQUEST.            CL**1
           IF TF001-DKEY-ID > ZERO                                         CL**4
               MOVE TF001-DKEY-ID    TO ASC-XFER-DKEY-ID                   CL**1
                                        DK-XFER-DKEY-ID                    CL**1
           ELSE                                                            CL**4
               MOVE PC-TSYMSG-00148                                        CL**4
                                     TO DP020-MSG-NUMBER                   CL**4
               SET DP020-MSG-FATAL                                         CL**4
                   DP020-NEXT-ACT-APPL-ERROR                               CL**4
                   PS-COMMAREA-NOT-VALID  TO TRUE                          CL**1
           END-IF.                                                         CL**4
                                                                           CL**1
      *----------------------------------------------------------------*   CL**1
      *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *   CL**1
      *    ON WHAT FUNCTION KEY WAS SELECTED.                          *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       2000-PROCESS-PANEL.                                                 CL**1
                                                                           CL**8
           EVALUATE TRUE                                                   CL**1
               WHEN DP020-SRC-AID = DP016-CLEAR                            CL**1
                   MOVE ZERO TO ASC-ITEM-AT-TOP-OF-PANEL                   CL**1
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**1
                   PERFORM 3200-RESET-ATTRIBUTES                           CL*44
                                                                           CL**1
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                       CL**1
                   MOVE ASC-XFER-DKEY-ID                                   CL**1
                                     TO  DK-XFER-DKEY-ID                   CL**1
                   PERFORM 2050-DELETE-DETAIL-QUEUE                        CL*44
                   PERFORM 4000-BUILD-INITIAL-PANEL                        CL**1
                   PERFORM 3200-RESET-ATTRIBUTES                           CL*44
                                                                           CL**1
               WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                     CL*11
                   MOVE +1 TO PV-TOP-ITEM                                  CL*11
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL*11
                   PERFORM 3200-RESET-ATTRIBUTES                           CL*44
                                                                           CL*11
               WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                      CL*11
                   PERFORM 2110-BROWSE-BACKWARD                            CL*11
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL*11
                   PERFORM 3200-RESET-ATTRIBUTES                           CL*44
                                                                           CL*11
               WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                      CL*11
                   PERFORM 2120-BROWSE-FORWARD                             CL*11
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL*11
                   PERFORM 3200-RESET-ATTRIBUTES                           CL*44
                                                                           CL*11
               WHEN DP020-SRC-AID = DP016-ENTER                            CL*11
                   PERFORM 2130-CHECK-FOR-JUMP-BROWSE                      CL*11
                   IF  PS-ERROR                                            CL*11
                       CONTINUE                                            CL*11
                   ELSE                                                    CL*11
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                CL*11
                       PERFORM 3200-RESET-ATTRIBUTES                       CL*44
                   END-IF                                                  CL*11
                                                                           CL*11
               WHEN OTHER                                                  CL*11
                   SET DP013-NO-ROLLBACK TO TRUE                           CL*11
                   SET DP013-XCTL-DISPLAY-RESTART TO TRUE                  CL*11
                   SET DP013-CICS-ABEND TO TRUE                            CL*11
                   MOVE '2100-CHECK-FUNCTION-KEY' TO DP013-PARAGRAPH       CL*11
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'         CL*11
                                         TO DP013-MESSAGE-TEXT (1)         CL*11
                   PERFORM DP013-0000-PROCESS-ABEND                        CL*11
                                                                           CL*11
           END-EVALUATE.                                                   CL*11
       EJECT                                                               CL**1
      *----------------------------------------------------------------*   CL**1
      *    DELETE THE TEMPORARY STORAGE QUEUE.                         *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       2050-DELETE-DETAIL-QUEUE.                                           CL**6
                                                                           CL**1
           EXEC CICS                                                       CL**1
               DELETEQ TS                                                  CL**1
                   QUEUE (ASC-DTL-QUEUE-NAME)                              CL**6
                   RESP  (PV-CICS-RESP)                                    CL**1
           END-EXEC.                                                       CL**1
                                                                           CL**1
           IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                          CL**1
              (PV-CICS-RESP = DFHRESP(QIDERR))                             CL**1
               CONTINUE                                                    CL**1
           ELSE                                                            CL**1
               SET DP013-CICS-ABEND TO TRUE                                CL**1
               SET DP013-NO-ROLLBACK TO TRUE                               CL**1
                                                                           CL**1
               MOVE '2050-DELETE-DETAIL-QUEUE' TO DP013-PARAGRAPH          CL**6
               MOVE 'ERROR TRYING TO DELETE DETAIL TEMP STORAGE QUEUE'     CL**6
                              TO DP013-MESSAGE-TEXT (1)                    CL**1
               PERFORM DP013-0000-PROCESS-ABEND                            CL**1
           END-IF.                                                         CL**1
                                                                           CL**1
           MOVE +0 TO ASC-HIGHEST-BLOCK-WRITTEN.                           CL**1
       EJECT                                                               CL**1
      *----------------------------------------------------------------*   CL**1
      *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *   CL**1
      *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *   CL**1
      *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *   CL**1
      *    MESSAGE.                                                    *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       2110-BROWSE-BACKWARD.                                               CL**1
                                                                           CL**8
           SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL       CL**1
               GIVING PV-TOP-ITEM.                                         CL**1
                                                                           CL**8
           IF  PV-TOP-ITEM < +1                                            CL**1
               MOVE +1              TO PV-TOP-ITEM                         CL**1
      *        -- TOP OF LIST --                                           CL**1
               MOVE PC-TSYMSG-00069 TO DP020-MSG-NUMBER                    CL**1
               SET DP020-MSG-INFORMATIONAL TO TRUE                         CL**1
           END-IF.                                                         CL**1
                                                                           CL**1
       EJECT                                                               CL**1
                                                                           CL**8
      *----------------------------------------------------------------*   CL*58
      *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *   CL*58
      *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *   CL*58
      *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *   CL*58
      *----------------------------------------------------------------*   CL*58
                                                                           CL*58
       2120-BROWSE-FORWARD.                                                CL*58
                                                                           CL*58
           ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                 CL*58
               GIVING PV-TOP-ITEM.                                         CL*58
           IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                      CL*58
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                               CL*58
                                         TO PV-TOP-ITEM                    CL*58
      *        -- BOTTOM OF LIST --                                        CL*58
               MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER               CL*58
               SET DP020-MSG-INFORMATIONAL                                 CL*58
                                         TO TRUE                           CL*58
           ELSE                                                            CL*58
               COMPUTE PV-BOTTOM-ITEM =                                    CL*58
                   PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                    CL*58
               IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ               CL*58
      *            -- BOTTOM OF LIST --                                    CL*58
                   MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER               CL*58
                   SET DP020-MSG-INFORMATIONAL                             CL*58
                                         TO TRUE                           CL*58
               END-IF                                                      CL*58
           END-IF.                                                         CL*58
       EJECT                                                               CL*58
      *----------------------------------------------------------------*   CL**1
      *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *   CL**1
      *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *   CL**1
      *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *   CL**1
      *    RATHER THAN HITTING PF8 TO PAGE FORWARD TO THAT POINT.      *   CL**1
      *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *   CL**1
      *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       2130-CHECK-FOR-JUMP-BROWSE.                                         CL*58
                                                                           CL*58
           IF MR-STARTL > ZERO                                             CL*58
               MOVE MR-START             TO ASC-START-ITEM-REQUEST         CL*58
           ELSE                                                            CL*58
               IF MR-STARTA = DP015-ERASE-EOF                              CL*58
                   INITIALIZE ASC-START-ITEM-REQUEST                       CL*58
               END-IF                                                      CL*58
           END-IF.                                                         CL*58
                                                                           CL*58
           SET PS-NO-ERROR               TO TRUE.                          CL*58
           IF (ASC-START-ITEM-REQUEST = SPACE)                             CL*58
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                               CL*58
                                         TO PV-TOP-ITEM                    CL*58
           ELSE                                                            CL*58
               MOVE ASC-START-ITEM-REQUEST                                 CL*58
                                         TO DP010I-UNEDITED-FIELD          CL*58
               MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS          CL*58
               MOVE +0                   TO DP010I-MAXIMUM-DECIMALS        CL*58
               SET DP010I-NEGATIVE-NOT-ALLOWED                             CL*58
                                         TO TRUE                           CL*58
               CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
                         USING DP010I-NUMERIC-EDIT-AREA                         
                                                                           CL*58
               IF  NOT DP010I-ERROR-DETECTED                               CL*58
                   MOVE DP010I-NUMERIC-FIELD                               CL*58
                                         TO  ASC-START-ITEM-REQUEST-N      CL*58
                   IF  ASC-START-ITEM-REQUEST-N > ZERO                     CL*58
                       IF  ASC-START-ITEM-REQUEST-N >                      CL*58
                                                ASC-NUMBER-OF-ITEMS-READ   CL*58
                           MOVE SPACE TO ASC-START-ITEM-REQUEST            CL*58
                           COMPUTE PV-TOP-ITEM =                           CL*58
                                   ASC-NUMBER-OF-ITEMS-READ                CL*58
                                   - PC-ITEMS-PER-PANEL + 1                CL*58
                           IF  PV-TOP-ITEM < +1                            CL*58
                               MOVE +1   TO PV-TOP-ITEM                    CL*58
                               MOVE PC-TSYMSG-00070                        CL*58
                                         TO DP020-MSG-NUMBER               CL*58
                               SET DP020-MSG-INFORMATIONAL                 CL*58
                                         TO TRUE                           CL*58
                           END-IF                                          CL*58
                       ELSE                                                CL*58
                           MOVE ASC-START-ITEM-REQUEST-N                   CL*58
                                         TO PV-TOP-ITEM                    CL*58
                           MOVE SPACE    TO ASC-START-ITEM-REQUEST         CL*58
                           COMPUTE PV-BOTTOM-ITEM =                        CL*58
                               PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1        CL*58
                           IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ   CL*58
      *                        -- BOTTOM OF LIST --                        CL*58
                               MOVE PC-TSYMSG-00070                        CL*58
                                         TO DP020-MSG-NUMBER               CL*58
                               SET DP020-MSG-INFORMATIONAL                 CL*58
                                         TO TRUE                           CL*58
                           END-IF                                          CL*58
                       END-IF                                              CL*58
                   ELSE                                                    CL*58
      *                --- VALUE OUT OF RANGE ----                         CL*58
                       MOVE PC-TSYMSG-00101                                CL*58
                                         TO DP020-MSG-NUMBER               CL*58
                       MOVE -1           TO MR-STARTL                      CL*58
                       SET DP030-SET-CURSOR-APPL-1                         CL*58
                                         TO TRUE                           CL*58
                       MOVE DP015-REVERSE                                  CL*58
                                         TO MR-STARTH                      CL*58
                       MOVE DP015-RED    TO MR-STARTC                      CL*58
                       MOVE DP015-UNP-NUM-BRT-MDT                          CL*58
                                         TO MR-STARTA                      CL*58
                       SET DP020-MSG-FATAL                                 CL*58
                                         TO TRUE                           CL*58
                       SET PS-ERROR      TO TRUE                           CL*58
                   END-IF                                                  CL*58
                                                                           CL*58
               ELSE                                                        CL*58
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER               CL*58
                   MOVE -1               TO MR-STARTL                      CL*58
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                     CL*58
                   MOVE DP015-REVERSE    TO MR-STARTH                      CL*58
                   MOVE DP015-RED        TO MR-STARTC                      CL*58
                   MOVE DP015-UNP-NUM-BRT-MDT                              CL*58
                                         TO MR-STARTA                      CL*58
                   SET DP020-MSG-FATAL   TO TRUE                           CL*58
                   SET PS-ERROR          TO TRUE                           CL*58
               END-IF                                                      CL*58
           END-IF.                                                         CL*58
       EJECT                                                               CL**1
      *----------------------------------------------------------------*   CL**1
      *    RESET ATTRIBUTES.                                           *   CL*61
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       3200-RESET-ATTRIBUTES.                                              CL**1
                                                                           CL**8
           MOVE DP015-UNP-ALP-NOR-OFF    TO MR-STARTA.                     CL**1
           MOVE DP015-GREEN              TO MR-STARTC.                     CL**1
           MOVE DP015-UNDERLINE          TO MR-STARTH.                     CL**1
           MOVE -1                       TO MR-STARTL.                     CL*62
           SET DP030-SET-CURSOR-APPL-1   TO TRUE.                          CL*62
                                                                           CL**8
       EJECT                                                               CL**1
                                                                           CL**8
      *----------------------------------------------------------------*   CL**1
      *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *   CL**1
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *   CL**1
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *   CL**1
      *    DISPLAY THE PANEL TO USER.                                  *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       4000-BUILD-INITIAL-PANEL.                                           CL**1
                                                                           CL**1
           INITIALIZE                  ASC-BLOCK-ENTRIES (1)               CL**1
                                       ASC-BLOCK-ENTRIES (2).              CL**1
           MOVE +1                 TO  ASC-BLK-SUB.                        CL**1
           SET ASC-ITEMS-LEFT-ON-DB                                        CL**1
                                   TO  TRUE.                               CL**1
                                                                           CL**1
           MOVE +0                 TO  ASC-ITEM-SUB                        CL**1
                                       ASC-NUMBER-OF-ITEMS-READ            CL**1
                                       ASC-HIGHEST-BLOCK-WRITTEN           CL**1
                                       ASC-START-OF-SELECTED-RANGE         CL**1
                                       ASC-END-OF-SELECTED-RANGE           CL**1
                                       ASC-ITEM-AT-TOP-OF-PANEL.           CL**1
                                                                           CL*52
           PERFORM 4010-OPEN-CURSOR.                                       CL**1
                                                                           CL**1
           PERFORM 4300-READ-ITEMS-FROM-DB.                                CL**1
                                                                           CL**1
           PERFORM 4020-CLOSE-CURSOR.                                      CL**1
                                                                           CL**1
           MOVE +1                 TO  PV-TOP-ITEM.                        CL**1
           IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                             CL**1
               SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                       CL**1
               SET DP020-MSG-FATAL   TO  TRUE                              CL**1
               MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                  CL**1
           ELSE                                                            CL**1
               MOVE -1             TO MR-STARTL                            CL**1
               SET DP030-SET-CURSOR-APPL-1 TO TRUE                         CL**1
               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                        CL**1
           END-IF.                                                         CL**1
       EJECT                                                               CL**1
      *----------------------------------------------------------------*   CL**1
      * OPEN THE CURSOR.                                               *   CL*12
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       4010-OPEN-CURSOR.                                                   CL**1
                                                                           CL**1
           EXEC SQL                                                        CL*64
                OPEN ROUTING-CSR                                           CL*64
           END-EXEC.                                                       CL*64
                                                                           CL*64
           EVALUATE TRUE                                                   CL*64
               WHEN SQLCODE = ZERO                                         CL*64
                    CONTINUE                                               CL*64
               WHEN OTHER                                                  CL*64
                    MOVE '4010-OPEN-CURSOR'                                CL*64
                                 TO  DP013-PARAGRAPH                       CL*64
                    MOVE 'OPEN ROUTING CURSOR'                             CL*64
                                 TO  DP013-MESSAGE-TEXT (1)                CL*64
                    MOVE SQLCA   TO  DP013-SQLCA                           CL*64
                    SET DP013-DB2-ABEND                                    CL*64
                                 TO  TRUE                                  CL*64
                    PERFORM DP013-0000-PROCESS-ABEND                       CL*64
           END-EVALUATE.                                                   CL*64
                                                                           CL**1
       EJECT                                                               CL**1
      *----------------------------------------------------------------*   CL**1
      * CLOSE THE CURSOR.                                              *   CL*12
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       4020-CLOSE-CURSOR.                                                  CL**1
                                                                           CL**1
           EXEC SQL                                                        CL*64
               CLOSE ROUTING-CSR                                           CL*64
           END-EXEC.                                                       CL*64
                                                                           CL*64
           EVALUATE TRUE                                                   CL*64
               WHEN SQLCODE = ZERO                                         CL*64
                    CONTINUE                                               CL*64
               WHEN OTHER                                                  CL*64
                    MOVE '4020-CLOSE-CURSOR'                               CL*64
                                 TO  DP013-PARAGRAPH                       CL*64
                    MOVE 'CLOSE ROUTING CURSOR'                            CL*64
                                 TO  DP013-MESSAGE-TEXT (1)                CL*64
                    MOVE SQLCA   TO  DP013-SQLCA                           CL*64
                    SET DP013-DB2-ABEND                                    CL*64
                                 TO  TRUE                                  CL*64
                    PERFORM DP013-0000-PROCESS-ABEND                       CL*64
           END-EVALUATE.                                                   CL*64
                                                                           CL**1
       EJECT                                                               CL**1
      *----------------------------------------------------------------*06740000
      * SELECT DATA FROM THE TRANSFER TABLES                            06750002
      *----------------------------------------------------------------*06760000
       4100-GET-TRANSFER.                                               06770002
           PERFORM                                                      06780000
               WITH TEST AFTER                                          06790000
               VARYING PV-RETRY-COUNTER                                 06800000
               FROM 1 BY 1                                              06810000
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               06820000
                         OR (SQLCODE = ZERO)                            06830000
                         OR (SQLCODE = +100))                           06840000
      *                                                                 06850000
               EXEC SQL                                                 06860000
                   SELECT FR_STR_NBR                                    06870013
                         ,XFER_SEQ_NBR                                  06870013
                         ,TO_STR_NBR                                    06891002
                   INTO   :TFMSTR-FR-STR-NBR                            06903019
                         ,:TFMSTR-XFER-SEQ-NBR                          06904019
                         ,:TFMSTR-TO-STR-NBR                            06904019
                   FROM   TTFMSTR                                       06930018
                   WHERE  XFER_DKEY_ID     = :DK-XFER-DKEY-ID           06940013
               END-EXEC                                                 06950000
                                                                        06960000
               EVALUATE TRUE                                            06970000
                   WHEN SQLCODE = ZERO                                  06980000
                      SET PS-TRANSFER-FOUND     TO TRUE                 06980107
                      MOVE TFMSTR-FR-STR-NBR    TO ASC-FROM-STORE-NBR   06981519
                      MOVE TFMSTR-XFER-SEQ-NBR  TO ASC-XFER-SEQ-NBR     06981519
                      MOVE TFMSTR-TO-STR-NBR    TO ASC-TO-STORE-NBR     06981619
                      PERFORM 5000-GET-STORE-NAMES                      06986003
                      PERFORM 6000-READ-TRANSFER-RECEIPT                06986003
                   WHEN SQLCODE = +100                                  06990000
                   WHEN SQLCODE = -904                                  07000000
                   WHEN SQLCODE = -913                                  07010000
                       CONTINUE                                         07020000
                   WHEN OTHER                                           07030000
                       MOVE '4100-GET-TRANSFER'                         07060002
                                         TO DP013-PARAGRAPH             07070000
                       MOVE 'SELECT TRANSFER DATA'                      07080002
                                         TO DP013-MESSAGE-TEXT (1)      07090000
                       MOVE SQLCA        TO DP013-SQLCA                 07100000
                       MOVE 'TTFMSTR'    TO DP013-DB2-TABLE-NAME (1)    07110018
                       SET DP013-DB2-ABEND                              07120000
                           DP013-XCTL-DISPLAY-RESTART                   07130000
                                         TO TRUE                        07140000
                       PERFORM DP013-0000-PROCESS-ABEND                 07150000
               END-EVALUATE                                             07160000
           END-PERFORM.                                                 07170000
      *                                                                 07180000
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        07190000
               MOVE '4100-GET-TRANSFER'                                 07200002
                                         TO DP013-PARAGRAPH             07210000
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'     07220000
                                         TO DP013-MESSAGE-TEXT (1)      07230000
               MOVE 'TRANSFER DATA'      TO DP013-MESSAGE-TEXT (2)      07240002
               MOVE SQLCA                TO DP013-SQLCA                 07250000
               MOVE 'TTFMSTR'            TO DP013-DB2-TABLE-NAME (1)    07261018
               SET DP013-DB2-ABEND                                      07270000
                   DP013-XCTL-DISPLAY-RESTART                           07280000
                                         TO TRUE                        07290000
               PERFORM DP013-0000-PROCESS-ABEND                         07300000
           END-IF.                                                      07310000
       EJECT                                                            07310102
      *----------------------------------------------------------------*   CL**1
      *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *   CL**1
      *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *   CL**1
      *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *   CL**1
      *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *   CL**1
      *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *   CL**1
      *                                                                *   CL**1
      *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *   CL**1
      *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *   CL**1
      *    UPON INITIALIZATION.                                        *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       4300-READ-ITEMS-FROM-DB.                                            CL**1
                                                                           CL**1
           PERFORM                                                         CL**1
               VARYING PV-READ-COUNT                                       CL**1
               FROM 1 BY 1                                                 CL**1
               UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                       CL**1
                  OR (ASC-NO-ITEMS-LEFT-ON-DB))                            CL**1
                                                                           CL**1
               PERFORM 4310-FETCH                                          CL**1
                                                                           CL**1
               IF  ASC-ITEMS-LEFT-ON-DB                                    CL**1
                   PERFORM 4320-GET-SUB                                    CL*30
                   MOVE TFRCPT-STR-NBR                                     CL**1
                                     TO  ASC-STORE-NBR                     CL*18
                                             (ASC-BLK-SUB                  CL**1
                                              ASC-ITEM-SUB)                CL**1
                   MOVE TFRCPT-RCV-XFER-BEG-TMST                           CL**1
                                     TO  ASC-DATE                          CL*18
                                             (ASC-BLK-SUB                  CL**1
                                              ASC-ITEM-SUB)                CL**1
100404             MOVE XST00001-LOC-10-ABBR-NM                            CL**1
                                     TO  ASC-STORE-NAME                    CL*18
                                             (ASC-BLK-SUB                  CL**1
                                              ASC-ITEM-SUB)                CL**1
                   MOVE TFSTCD-XFER-STAT-DESC                              CL**1
                                     TO  ASC-STATUS-DESC                   CL*18
                                             (ASC-BLK-SUB                  CL**1
                                              ASC-ITEM-SUB)                CL**1
               END-IF                                                      CL**1
           END-PERFORM.                                                    CL**1
                                                                           CL**1
           MOVE +1                       TO  ASC-BLK-SUB.                  CL*59
           PERFORM 5100-WRITE-TEMP-STORAGE.                                CL*59
           ADD +1                        TO  ASC-BLK-SUB.                  CL*59
           PERFORM 5100-WRITE-TEMP-STORAGE.                                CL*59
                                                                           CL*59
           MOVE 1                        TO  ASC-BLK-SUB                   CL*59
                                             PV-BLOCK-NUMBER.              CL*59
           PERFORM 4410-READ-TEMP-STORAGE.                                 CL*59
           IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                    CL*59
               MOVE 2                    TO  ASC-BLK-SUB                   CL*59
                                             PV-BLOCK-NUMBER               CL*59
               PERFORM 4410-READ-TEMP-STORAGE                              CL*59
           END-IF.                                                         CL*59
       EJECT                                                               CL**1
      *----------------------------------------------------------------*   CL**1
      *    FETCH THE ROUTING CURSOR.                                   *   CL*61
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       4310-FETCH.                                                         CL**1
                                                                           CL**1
           PERFORM                                                         CL**1
               WITH TEST AFTER                                             CL**1
               VARYING PV-SQL-SUB                                          CL**1
               FROM 1 BY 1                                                 CL**1
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                CL**1
                  OR SQLCODE = 0                                           CL**1
                  OR SQLCODE = +100                                        CL**1
                                                                           CL**1
               EXEC SQL                                                    CL*15
                    FETCH ROUTING-CSR                                      CL*15
                    INTO                                                   CL*15
                          :TFRCPT-STR-NBR                                  CL**1
                        , :TFRCPT-RCV-XFER-BEG-TMST                        CL**1
100404                  , :XST00001-LOC-10-ABBR-NM                         CL**1
                        , :TFSTCD-XFER-STAT-DESC                           CL**1
               END-EXEC                                                    CL*15
                                                                           CL**1
               EVALUATE TRUE                                               CL**1
                   WHEN SQLCODE = ZERO                                     CL**1
                   WHEN SQLCODE = +100                                     CL**1
                   WHEN SQLCODE = -904                                     CL**1
                   WHEN SQLCODE = -913                                     CL**1
                        CONTINUE                                           CL**1
                   WHEN OTHER                                              CL**1
                        MOVE '4310-FETCH-CURSOR'                           CL**1
                                     TO  DP013-PARAGRAPH                   CL**1
                        MOVE 'FETCH ROUTING CURSOR'                        CL**1
                                     TO  DP013-MESSAGE-TEXT (1)            CL**1
                        MOVE SQLCA   TO  DP013-SQLCA                       CL**1
                        SET DP013-DB2-ABEND                                CL**1
                                     TO  TRUE                              CL**1
                        PERFORM DP013-0000-PROCESS-ABEND                   CL**1
               END-EVALUATE                                                CL**1
           END-PERFORM.                                                    CL**1
                                                                           CL**1
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                      CL**1
               MOVE '4310-FETCH-CURSOR'                                    CL**1
                                     TO  DP013-PARAGRAPH                   CL**1
               MOVE 'MAX RETRIES EXCEEDED FOR FETCH CURSOR'                CL**1
                                     TO  DP013-MESSAGE-TEXT (1)            CL**1
               MOVE SQLCA            TO  DP013-SQLCA                       CL**1
               SET DP013-DB2-ABEND   TO  TRUE                              CL**1
               PERFORM DP013-0000-PROCESS-ABEND                            CL**1
           END-IF.                                                         CL**1
                                                                           CL**1
           IF  SQLCODE EQUAL +100                                          CL**1
               SET ASC-NO-ITEMS-LEFT-ON-DB                                 CL**1
                                     TO  TRUE                              CL**1
           END-IF.                                                         CL**1
                                                                           CL**1
       EJECT                                                               CL**1
      *----------------------------------------------------------------*   CL*30
      *    GET THE CORRECT SUBSCRIPTS FOR PLACING THE TEXT LINE.       *   CL*32
      *----------------------------------------------------------------*   CL*30
                                                                           CL*30
       4320-GET-SUB.                                                       CL*30
                                                                           CL*30
           IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                           CL*30
               ADD +1            TO  ASC-ITEM-SUB                          CL*30
           ELSE                                                            CL*30
               ADD +1            TO  ASC-BLK-SUB                           CL*30
               MOVE +1           TO  ASC-ITEM-SUB                          CL*30
                                                                           CL*30
               IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                    CL*30
                   MOVE +1       TO  ASC-BLK-SUB                           CL*30
                   PERFORM 5100-WRITE-TEMP-STORAGE                         CL*30
                   MOVE ASC-BLOCK-ENTRIES                                  CL*30
                             (PC-MAX-BLOCKS-IN-ARRAY)                      CL*30
                   TO   ASC-BLOCK-ENTRIES(ASC-BLK-SUB)                     CL*30
                   ADD +1        TO  ASC-BLK-SUB                           CL*30
                   INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)              CL*30
               END-IF                                                      CL*30
           END-IF.                                                         CL*30
                                                                           CL*30
           ADD +1            TO ASC-NUMBER-OF-ITEMS-READ.                  CL*30
           MOVE ASC-NUMBER-OF-ITEMS-READ TO ASC-LIST-ITEM-NUMBER           CL*33
                                            (ASC-BLK-SUB                   CL*33
                                             ASC-ITEM-SUB).                CL*50
                                                                           CL*33
      *----------------------------------------------------------------*   CL**1
      *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       4400-MOVE-COMMAREA-TO-SCREEN.                                          *1
                                                                           CL**8
           MOVE ASC-FROM-STORE-NBR     TO MR-FROM-STORE-NBR                CL*53
                                          PV-XFER-FROM-STORE-NBR.               
           MOVE ASC-XFER-SEQ-NBR       TO PV-XFER-SEQ-NBR.                      
           MOVE PV-XFER-NBR            TO MR-XFER-NBR.                          
           MOVE ASC-TO-STORE-NBR       TO MR-TO-STORE-NBR.                 CL*53
           MOVE ASC-RECV-STORE-NBR     TO MR-RECV-STORE-NBR.               CL*53
           MOVE ASC-FROM-STORE-NAME    TO MR-FROM-STORE-NAME.              CL*53
           MOVE ASC-TO-STORE-NAME      TO MR-TO-STORE-NAME.                CL*53
           MOVE ASC-RECV-STORE-NAME    TO MR-RECV-STORE-NAME.              CL*53
                                                                           CL*53
           IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                CL**1
               AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))             CL**1
               OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))             CL**1
                   DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                CL**1
                       GIVING PV-BLOCK-NUMBER                              CL**1
                       REMAINDER PV-REMAINDER                              CL**1
                   IF  PV-REMAINDER > ZERO                                 CL**1
                       ADD +1 TO PV-BLOCK-NUMBER                           CL**1
                   END-IF                                                  CL**1
                   MOVE +1    TO ASC-BLK-SUB                               CL**1
                   PERFORM 4410-READ-TEMP-STORAGE                          CL**1
                   ADD +1     TO ASC-BLK-SUB                               CL**1
                                 PV-BLOCK-NUMBER                           CL**1
                   PERFORM 4410-READ-TEMP-STORAGE                          CL**1
           END-IF.                                                         CL**1
           MOVE +1                       TO ASC-BLK-SUB.                   CL**1
           COMPUTE ASC-ITEM-SUB =                                          CL**1
               ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                 CL**1
               + PC-ITEMS-PER-PANEL).                                      CL**1
                                                                           CL**8
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                           CL**1
               ADD +1   TO ASC-BLK-SUB                                     CL**1
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB               CL**1
           END-IF.                                                         CL**1
                                                                           CL**8
           COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                              CL**1
               ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                   CL**1
           PERFORM 4420-FORMAT-LIST-LINES                                  CL**1
               VARYING PS-SUB                                              CL**1
               FROM PC-ITEMS-PER-PANEL BY -1                               CL**1
               UNTIL PS-SUB < +1.                                          CL**1
                                                                           CL**8
           COMPUTE ASC-ITEMS-DISPLAYED =                                   CL**1
               ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).             CL**1
           MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL       CL**1
                                            MR-START.                      CL**1
           MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                       CL**1
           MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                       CL**1
                                                                           CL*36
           IF  DP020-MSG-NUMBER-X = SPACE                                  CL*65
               IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ     CL*65
      *            --- MORE ITEMS TO DISPLAY ----                          CL*65
                   MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER               CL*65
                   SET DP020-MSG-INFORMATIONAL TO TRUE                     CL*65
               END-IF                                                      CL*65
           END-IF.                                                         CL*65
                                                                           CL*65
      *----------------------------------------------------------------*   CL**1
      *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       4410-READ-TEMP-STORAGE.                                             CL**1
                                                                           CL**1
           EXEC CICS                                                       CL**1
               READQ TS                                                    CL**1
                   QUEUE (ASC-DTL-QUEUE-NAME)                              CL*17
                   INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                  CL**1
                   ITEM  (PV-BLOCK-NUMBER)                                 CL**1
                   RESP  (PV-CICS-RESP)                                    CL**1
           END-EXEC.                                                       CL**1
                                                                           CL**1
           IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                          CL**1
             OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                        CL**1
               CONTINUE                                                    CL**1
           ELSE                                                            CL**1
               SET DP013-CICS-ABEND TO  TRUE                               CL**1
               SET DP013-NO-ROLLBACK TO  TRUE                              CL**1
               MOVE '4410-READ-TEMP-STORAGE'                               CL**1
                                     TO  DP013-PARAGRAPH                   CL**1
               MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'         CL**1
                                     TO  DP013-MESSAGE-TEXT(1)             CL**1
               PERFORM DP013-0000-PROCESS-ABEND                            CL**1
           END-IF.                                                         CL**1
                                                                           CL**1
      *    SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                CL*17
                                                                           CL**8
           IF  PV-CICS-RESP = DFHRESP (ITEMERR)                            CL**1
               INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                  CL**1
           END-IF.                                                         CL**1
       EJECT                                                               CL**1
      *----------------------------------------------------------------*   CL**1
      *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       4420-FORMAT-LIST-LINES.                                             CL**1
                                                                           CL**8
           IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ         CL**1
               SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                   CL**1
               MOVE SPACE              TO MR-STORE-NBR-X (PS-SUB)          CL*17
                                          MR-STORE-NAME  (PS-SUB)          CL*17
                                          MR-DATE        (PS-SUB)          CL*17
                                          MR-STATUS-DESC (PS-SUB)          CL*17
           ELSE                                                            CL**1
               IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL              CL**1
                   MOVE ASC-STORE-NBR (ASC-BLK-SUB ASC-ITEM-SUB)           CL*18
                                       TO MR-STORE-NBR (PS-SUB)            CL*18
                   MOVE ASC-STORE-NAME (ASC-BLK-SUB ASC-ITEM-SUB)          CL*18
                                       TO MR-STORE-NAME (PS-SUB)           CL*18
                   MOVE ASC-DATE-YYYY (ASC-BLK-SUB ASC-ITEM-SUB)           CL*18
                                       TO PV-DATE-YYYY                     CL*18
                   MOVE ASC-DATE-MM   (ASC-BLK-SUB ASC-ITEM-SUB)           CL*18
                                       TO PV-DATE-MM                       CL*18
                   MOVE ASC-DATE-DD   (ASC-BLK-SUB ASC-ITEM-SUB)           CL*18
                                       TO PV-DATE-DD                       CL*18
                   MOVE PV-DATE        TO MR-DATE (PS-SUB)                 CL*18
                   MOVE ASC-STATUS-DESC (ASC-BLK-SUB ASC-ITEM-SUB)         CL*18
                                       TO MR-STATUS-DESC (PS-SUB)          CL*18
               END-IF                                                      CL**1
           END-IF.                                                         CL**1
           SUBTRACT +1 FROM ASC-ITEM-SUB.                                  CL*25
           IF  ASC-ITEM-SUB < + 1                                          CL**1
               MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                     CL**1
               MOVE +1                 TO ASC-BLK-SUB                      CL**1
           END-IF.                                                         CL**1
                                                                           CL**8
      *----------------------------------------------------------------*   CL**1
      *  GET THE FROM AND TO STORE NAMES.                              *   CL**1
      *----------------------------------------------------------------*   CL**1
       5000-GET-STORE-NAMES.                                            07417503
           MOVE ASC-FROM-STORE-NBR       TO DK-STORE-NBR.               07417615
100404     PERFORM 5050-READ-XST-LOC.                                   07417703
100404     MOVE XST00001-LOC-10-ABBR-NM  TO ASC-FROM-STORE-NAME.        07417803
           MOVE ASC-TO-STORE-NBR         TO DK-STORE-NBR.               07417907
100404     PERFORM 5050-READ-XST-LOC.                                   07418003
100404     MOVE XST00001-LOC-10-ABBR-NM  TO ASC-TO-STORE-NAME.          07418103
       EJECT                                                            07418203
      *----------------------------------------------------------------*   CL**1
      *  GET THE 10 CHARACTER ABBREVIATED STORE NAME FROM THE STORE TBL*   CL**1
      *----------------------------------------------------------------*   CL**1
100404 5050-READ-XST-LOC.                                               07418303
           PERFORM WITH TEST AFTER                                      07418403
               VARYING PV-RETRY-COUNTER                                 07418522
               FROM 1 BY 1                                              07418622
               UNTIL (PV-RETRY-COUNTER > PC-MAX-RETRIES)                07418722
                    OR SQLCODE = ZERO                                   07419003
                    OR SQLCODE = +100                                   07419103
               EXEC SQL                                                 07419203
100404             SELECT LOC_10_ABBR_NM                                07419303
100404                 INTO :XST00001-LOC-10-ABBR-NM                    07419403
100404                 FROM XST_LOC                                     07419503
100404                 WHERE (LOC_NBR  = :DK-STORE-NBR)                 07419607
               END-EXEC                                                 07419703
               EVALUATE TRUE                                            07419803
                   WHEN SQLCODE = ZERO                                  07420303
                       CONTINUE                                         07420403
                   WHEN SQLCODE = -904                                  07419903
                   WHEN SQLCODE = -913                                  07420003
                   WHEN SQLCODE = +100                                  07420103
100404                 MOVE 'INVALID'     TO XST00001-LOC-10-ABBR-NM    07420203
                   WHEN OTHER                                           07420503
                       MOVE '5050-READ-XST-LOC'                         07420803
                                         TO DP013-PARAGRAPH             07420903
                       MOVE 'SELECT A ROW FROM XST_LOC'                 07421003
                                         TO DP013-MESSAGE-TEXT (1)      07421103
                       MOVE SQLCA        TO DP013-SQLCA                 07421203
                       MOVE 'XST_LOC'    TO DP013-DB2-TABLE-NAME (1)    07421303
                       SET DP013-DB2-ABEND                              07421403
                           DP013-XCTL-DISPLAY-RESTART                   07421503
                                         TO TRUE                        07421603
                       PERFORM DP013-0000-PROCESS-ABEND                 07421703
               END-EVALUATE                                             07421803
           END-PERFORM.                                                 07421903
                                                                        07422003
      *                                                                 07422103
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        07422203
               MOVE '5050-READ-XST-LOC'                                 07422303
                                         TO DP013-PARAGRAPH             07422403
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'     07422503
                                         TO DP013-MESSAGE-TEXT (1)      07422603
               MOVE 'A ROW FROM XST_LOC' TO DP013-MESSAGE-TEXT (2)      07422703
               MOVE SQLCA                TO DP013-SQLCA                 07422803
               MOVE 'XST_LOC'            TO DP013-DB2-TABLE-NAME (1)    07422903
               SET DP013-DB2-ABEND                                      07423003
                   DP013-XCTL-DISPLAY-RESTART                           07423103
                                         TO TRUE                        07423203
               PERFORM DP013-0000-PROCESS-ABEND                         07423303
           END-IF.                                                      07423403
       EJECT                                                            07423504
      *----------------------------------------------------------------*   CL**1
      *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
       5100-WRITE-TEMP-STORAGE.                                            CL**1
                                                                           CL**8
           MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                        CL**1
                                     TO  PV-LIST-ITEM-NUMBER.              CL**1
           COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                              CL*23
               ASC-HIGHEST-BLOCK-WRITTEN + 1.                              CL*51
                                                                           CL*51
           EXEC CICS                                                       CL*51
               WRITEQ TS                                                   CL*51
                   QUEUE (ASC-DTL-QUEUE-NAME)                              CL*51
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                  CL*51
                   ITEM  (PV-HIGHEST-BLOCK-WRITTEN)                        CL*51
                   RESP  (PV-CICS-RESP)                                    CL*51
           END-EXEC.                                                       CL*51
                                                                           CL*51
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                         CL*51
               SET DP013-CICS-ABEND                                        CL*51
                             TO  TRUE                                      CL*51
               SET DP013-NO-ROLLBACK                                       CL*51
                             TO  TRUE                                      CL*51
               MOVE '5100-WRITE-TEMP-STORAGE'                              CL*51
                             TO  DP013-PARAGRAPH                           CL*51
               MOVE 'ATTEMPTING TO WRITE TEMP STORAGE LIST BLOCK'          CL*51
                             TO  DP013-MESSAGE-TEXT(1)                     CL*51
               PERFORM DP013-0000-PROCESS-ABEND                            CL*51
           END-IF.                                                         CL*51
           ADD +1            TO  ASC-HIGHEST-BLOCK-WRITTEN.                CL*51
                                                                           CL**1
       EJECT                                                            07423504
      *----------------------------------------------------------------*   CL**1
      *   READ THE TRANSFER RECEIPT TABLE FOR THE RECEIVE STORE NUMBER.*   CL**1
      *----------------------------------------------------------------*   CL**1
       6000-READ-TRANSFER-RECEIPT.                                      07423604
           PERFORM WITH TEST AFTER                                      07424004
               VARYING PV-RETRY-COUNTER FROM 1 BY 1                     07424122
                 UNTIL PV-RETRY-COUNTER > PC-MAX-RETRIES                07424222
                    OR SQLCODE = ZERO                                   07425004
                    OR SQLCODE = +100                                   07426004
               EXEC SQL                                                 07427004
                   SELECT R.STR_NBR                                     07428021
100404                   ,S.LOC_10_ABBR_NM                              07428021
                      INTO :TFRCPT-STR-NBR                              07429219
100404                   , :XST00001-LOC-10-ABBR-NM                     07429219
                      FROM TTFRCPT R                                    07429618
100404                   , XST_LOC S                                    07429618
                         , TTFSTAT ST                                   07429618
                      WHERE ST.XFER_STAT_CDE = :PC-POSITIVE-RECEIPT AND 07429819
                            ST.XFER_STAT_TMST = R.RCV_XFER_BEG_TMST AND 07429819
                            R.XFER_DKEY_ID   = :DK-XFER-DKEY-ID     AND 07429715
                            ST.XFER_DKEY_ID  = :DK-XFER-DKEY-ID     AND 07429715
100404                      S.LOC_NBR        = R.STR_NBR                07429819
               END-EXEC                                                 07429904
               EVALUATE TRUE                                            07430004
                   WHEN SQLCODE = -904                                  07430104
                   WHEN SQLCODE = -913                                  07430204
                   WHEN SQLCODE = +100                                  07430304
                       CONTINUE                                         07430407
                   WHEN SQLCODE = ZERO                                  07430504
                       MOVE TFRCPT-STR-NBR                              07430622
                                          TO ASC-RECV-STORE-NBR         07430713
100404                 MOVE XST00001-LOC-10-ABBR-NM                     07418103
                                          TO ASC-RECV-STORE-NAME        07418103
                   WHEN OTHER                                           07431404
                       MOVE '6000-READ-TRANSFER-RECEIPT'                07431704
                                         TO DP013-PARAGRAPH             07431804
                       MOVE 'SELECT DATA FROM TTFRCPT'                  07431926
                                         TO DP013-MESSAGE-TEXT (1)      07432004
                       MOVE SQLCA        TO DP013-SQLCA                 07432104
                       MOVE 'TTFRCPT'    TO DP013-DB2-TABLE-NAME (1)    07432218
                       MOVE 'XST_LOC'    TO DP013-DB2-TABLE-NAME (2)    07432218
                       MOVE 'TTFSTAT'    TO DP013-DB2-TABLE-NAME (3)    07432218
                       SET DP013-DB2-ABEND                              07432304
                           DP013-XCTL-DISPLAY-RESTART                   07432404
                                         TO TRUE                        07432504
                       PERFORM DP013-0000-PROCESS-ABEND                 07432604
               END-EVALUATE                                             07432704
           END-PERFORM.                                                 07432804
      *                                                                 07432904
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        07433004
               MOVE '6000-READ-TRANSFER-RECEIPT'                        07433104
                                         TO DP013-PARAGRAPH             07433204
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'     07433304
                                         TO DP013-MESSAGE-TEXT (1)      07433404
               MOVE 'DATA FROM TTFRCPT'  TO DP013-MESSAGE-TEXT (2)      07433522
               MOVE SQLCA                TO DP013-SQLCA                 07433604
               MOVE 'TTFRCPT'            TO DP013-DB2-TABLE-NAME (1)    07433718
               MOVE 'XST_LOC'            TO DP013-DB2-TABLE-NAME (2)    07432218
               MOVE 'TTFSTAT'            TO DP013-DB2-TABLE-NAME (3)    07432218
               SET DP013-DB2-ABEND                                      07433804
                   DP013-XCTL-DISPLAY-RESTART                           07433904
                                         TO TRUE                        07434004
               PERFORM DP013-0000-PROCESS-ABEND                         07434104
           END-IF.                                                      07434204
       EJECT                                                               CL*47
      *----------------------------------------------------------------*   CL**1
      *    ABEND PROCESSOR MODULE                                      *   CL**1
      *----------------------------------------------------------------*   CL**1
                                                                           CL**1
           COPY DPPD013.                                                   CL**1
