       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MAKUT017.                                                 
       AUTHOR.        DAVE METZGER                                              
       DATE-WRITTEN.  DECEMBER 15, 2004                                         
      ****************************************************************          
      *          MA - WEEKLY SALES EXTRACT                           *          
      ****************************************************************          
      *    THE PURPOSE OF THIS PROGRAM IS TO EXTRACT AND SUM DAILY              
      *    SALES, BY SKU, FOR ONE WEEK.  THE SKU/STORE WEEKLY SALES             
      *    TABLE IS READ AND SUMMED FOR NON-CLEARANCE SKUS ONLY.                
      *    THE PROGRAM ALSO BUILDS A DUMMY FILE WHICH WILL SERVE TWO            
      *    PURPOSES.  IT WILL BE USED TO INITIATE THE LOAD SEQUENCE             
      *    ON THE DEC, AND IT WILL CONTAIN THE WEEK BEGINNING DATE FOR          
      *    SALES LOAD PROGRAM.                                                  
MAK140*    CHG0106356 - PRB0052432 - MODIFIED THE PROGRAM TO INCLUDE            
MAK140*                              SKU,DEPT,CLASS AND SUBCLASS IN             
MAK140*                              THE WEEKLY SALES EXTRACT FILE              
MAK140*                              TO BE USED IN MAK140 TO FIX                
MAK140*                              THE ORACLE ERROR.                          
      ****************************************************************          
      ****************************************************************          
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT DATE-CONTROL-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT SALES-EXTRACT-FILE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT DUMMY-OUTPUT-FILE                                             
               ASSIGN TO OUT02.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  DATE-CONTROL-FILE                                                    
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATE-CONTROL-RECORD             PIC  X(80).                          
                                                                                
                                                                                
       FD  SALES-EXTRACT-FILE                                                   
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SALES-EXTRACT-RECORD            PIC  X(42).                          
                                                                                
                                                                                
       FD  DUMMY-OUTPUT-FILE                                                    
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DUMMY-OUTPUT-RECORD             PIC  X(20).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                            PIC S9(04) VALUE ZERO          
                                                            COMP SYNC.          
           88  ABEND-4001                                   VALUE +4001.        
           88  ABEND-4002                                   VALUE +4002.        
           88  ABEND-4019                                   VALUE +4019.        
                                                                                
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           05  PV-SKU-SLS-OUTPUT-RECS            PIC  9(10) VALUE ZERO.         
           05  PV-CSR1-RECS                      PIC  9(10) VALUE ZERO.         
           05  PV-SQL-SUB                        PIC S9(04) COMP.               
           05  PV-PARAGRAPH-NAME                 PIC  X(30) VALUE SPACE.        
           05  PV-DB2-OPER-ATTEMPTED             PIC  X(30) VALUE SPACE.        
           05  PV-FSCL-BOW-DTE                   PIC  X(10) VALUE SPACE.        
           05  PV-FSCL-EOW-DTE                   PIC  X(10) VALUE SPACE.        
           05  PV-STR-NBR-9                      PIC  9(04).                    
           05  PV-STR-NBR-COMP                   PIC S9(4)  COMP.               
           05  PV-HOST-DATE.                                                    
               10  PV-HOST-DATE-YR               PIC  9(02) VALUE ZERO.         
               10  PV-HOST-DATE-MO               PIC  9(02) VALUE ZERO.         
               10  PV-HOST-DATE-DY               PIC  9(02) VALUE ZERO.         
                                                                                
           05  PV-GREGORIAN-DATE.                                               
               10  PV-GD-MONTH                   PIC  9(02) VALUE ZERO.         
               10  PV-GD-DAY                     PIC  9(02) VALUE ZERO.         
               10  PV-GD-YEAR                    PIC  9(02) VALUE ZERO.         
                                                                                
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
                                                          'MAKUT017'.           
           05  PC-ONE                      PIC  9(02)    VALUE  1 COMP.         
           05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3 COMP.         
           05  PC-MAX-DAYS                 PIC S9(04)    VALUE +7 COMP.         
           05  PC-WEEK-DAY-SUN             PIC  9(01)    VALUE  1 COMP.         
           05  PC-WEEK-DAY-SAT             PIC  9(01)    VALUE  7 COMP.         
           05  PC-MID-DAY-TIME             PIC  X(16)                           
                                               VALUE '-12.00.00.000000'.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-SKU-SLS-CSR1-SW      PIC X  VALUE 'N'.                 
               88  PS-END-OF-SKU-SLS-CSR1            VALUE 'Y'.                 
                                                                                
       01  DC-DATE-CONTROL.                                                     
           05  DC-DATE-INDICATOR    PIC X(01)      VALUE SPACE.                 
               88  PS-NO-DATE-CONTROL-CARD               VALUE 'N'.             
           05  DC-CONTROL-CENTURY   PIC 9(02)      VALUE ZEROES.                
           05  DC-CONTROL-YEAR      PIC 9(02)      VALUE ZEROES.                
           05  DC-CONTROL-MONTH     PIC 9(02)      VALUE ZEROES.                
           05  DC-CONTROL-DAY       PIC 9(02)      VALUE ZEROES.                
                                                                                
                                                                                
       01  DUMMY-FILE-DATE.                                                     
           05  DF-CENTURY           PIC 9(02)      VALUE ZEROES.                
           05  DF-YEAR              PIC 9(02)      VALUE ZEROES.                
           05  FILLER               PIC X(01)      VALUE '-'.                   
           05  DF-MONTH             PIC 9(02)      VALUE ZEROES.                
           05  FILLER               PIC X(01)      VALUE '-'.                   
           05  DF-DAY               PIC 9(02)      VALUE ZEROES.                
           05  FILLER               PIC X(01)      VALUE '-'.                   
           05  DF-HOUR              PIC 9(02)      VALUE ZEROES.                
           05  FILLER               PIC X(01)      VALUE '.'.                   
           05  DF-MINUTES           PIC 9(02)      VALUE ZEROES.                
           05  FILLER               PIC X(01)      VALUE '.'.                   
           05  DF-SECONDS           PIC 9(02)      VALUE ZEROES.                
           05  FILLER               PIC X(01)      VALUE SPACE.                 
                                                                                
      ******************************************************************        
      *  RECORD LAYOUT FOR THE SKU/STORE SALES EXTRACT OUTPUT FILE.             
      ******************************************************************        
           COPY MARD017.                                                        
                                                                                
      ******************************************************************        
      *    WORK AREA FOR THE ABEND ROUTINE.                                     
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    CALENDAR ROUTINE VARIABLES.                                          
      ******************************************************************        
           COPY DPWS500.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    NEW SKU STORE DAILY SALES TABLE - MHT_DLY_SLS               *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE MHT00000                                                 
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    SKU CROSS REFERENCE TABLE - TSKXREF                        *         
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    SQL COMMUNICATIONS AREA                                     *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      * THIS SKU-SALES CURSOR RETRIEVES THE SALES BY STORE FOR SKUS    *        
      * THAT ARE IN A STATUS OTHER THAN CLEARANCE OR MIXED AND THAT    *        
      * HAVE NOT UNDERGONE A STATUS CHANGE DURING THE WEEK BEING       *        
      * EXTRACTED.                                                     *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               DECLARE SKU-SLS-CSR1 CURSOR FOR                                  
                                                                                
            SELECT B.ITM_NBR                                                    
                  ,A.STR_NBR                                                    
                  ,SUM(A.SLS_UNT_QTY)                                           
MAK140            ,B.DEPT                                                       
MAK140            ,B.CLASS                                                      
MAK140            ,B.SUBCLASS                                                   
MAK140            ,B.SKU                                                        
            FROM MHT_DLY_SLS A                                                  
                ,TSKXREF     B                                                  
            WHERE A.PRCG_DTE        BETWEEN  :PV-FSCL-BOW-DTE                   
                                        AND  :PV-FSCL-EOW-DTE                   
              AND A.SKU_NBR         = B.SKU                                     
              AND A.SKU_STAT_CDE NOT IN ('25','30')                             
            GROUP BY B.ITM_NBR                                                  
                    ,A.STR_NBR                                                  
MAK140              ,B.DEPT                                                     
MAK140              ,B.CLASS                                                    
MAK140              ,B.SUBCLASS                                                 
MAK140              ,B.SKU                                                      
        ORDER BY 1,2                                                            
                                                                                
        END-EXEC.                                                               
                                                                                
      *       AND A.SKU_STAT_CDE IN ('20','40','90')                            
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
      *    PROCESS SKU-SLS-CSR1 TO EXTRACT THE SALES BY STORE FOR SKUS          
      *    THAT ARE IN A STATUS OTHER THAN CLEARANCE OR MIXED AND THAT          
      *    HAVE NOT UNDERGONE A STATUS CHANGE DURING THE WEEK.                  
                                                                                
           DISPLAY 'BEGINNING TO PROCESS SKU-SLS-CSR1.'                         
           PERFORM 8400-OPEN-SKU-SLS-CSR1.                                      
                                                                                
           PERFORM 8500-FETCH-SKU-SLS-CSR1.                                     
                                                                                
           PERFORM 2000-PROCESS-SKU-SLS-CSR1                                    
               UNTIL PS-END-OF-SKU-SLS-CSR1.                                    
                                                                                
           PERFORM 8600-CLOSE-SKU-SLS-CSR1.                                     
                                                                                
           CLOSE DATE-CONTROL-FILE                                              
                 SALES-EXTRACT-FILE                                             
                 DUMMY-OUTPUT-FILE.                                             
                                                                                
           DISPLAY 'SKU/STORE SALES EXTRACT RECORDS WRITTEN: '                  
                                                 PV-SKU-SLS-OUTPUT-RECS.        
                                                                                
           GOBACK.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *  INITIALIZE                                                    *        
      *     -- OPEN FILES.                                             *        
      *     -- READ CURRENT DATE.                                      *        
      *     -- MOVE CURRENT DATE TO DUMMY FILE.                        *        
      *     -- WRITE DUMMY FILE.                                       *        
      *     -- OPEN SKU SALES CURSOR.                                  *        
      *                                                                *        
      *----------------------------------------------------------------*        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  DATE-CONTROL-FILE                                        
                OUTPUT SALES-EXTRACT-FILE                                       
                       DUMMY-OUTPUT-FILE.                                       
                                                                                
           PERFORM 1100-READ-DATE-CONTROL.                                      
                                                                                
           PERFORM 1200-SET-CONTROL-DATES.                                      
                                                                                
           WRITE DUMMY-OUTPUT-RECORD                                            
                 FROM DUMMY-FILE-DATE.                                          
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
      *    READ IN THE DATE CONTROL CARD.                              *        
      *                                                                *        
      *----------------------------------------------------------------*        
       1100-READ-DATE-CONTROL.                                                  
                                                                                
           READ DATE-CONTROL-FILE                                               
                   INTO DC-DATE-CONTROL                                         
           END-READ.                                                            
           DISPLAY 'DC-DATE-CONTROL: ' DC-DATE-CONTROL.                         
                                                                                
      *----------------------------------------------------------------*        
      *----------------------------------------------------------------*        
       1200-SET-CONTROL-DATES.                                                  
                                                                                
                                                                                
      *    IF THERE IS NO CONTROL CARD DATE THE PROGRAM DEFAULTS                
      *    THE PROCESSING DATE TO THE SUNDAY OF THE PREVIOUS WEEK.              
                                                                                
           IF PS-NO-DATE-CONTROL-CARD                                           
               DISPLAY 'USING SYSTEM DATE TO DETERMINE WEEK TO PROCESS.'        
               INITIALIZE DPG51 DPG52                                           
                          DPG53 DPG54                                           
                          DPG55 DPG56                                           
                                                                                
               ACCEPT PV-HOST-DATE FROM DATE                                    
                                                                                
               MOVE PV-HOST-DATE-YR          TO  PV-GD-YEAR                     
               MOVE PV-HOST-DATE-MO          TO  PV-GD-MONTH                    
               MOVE PV-HOST-DATE-DY          TO  PV-GD-DAY                      
                                                                                
               SET DPG51-FISCAL-JAN-DEC-F2   TO  TRUE                           
               SET DPG52-LK-DTE-GREG         TO  TRUE                           
               MOVE PV-GREGORIAN-DATE        TO  DPG52-GREG-DATE-N              
                                                                                
      *        CALL THE CALENDAR ROUTINE TO GET THE FIRST DAY                   
      *        OF THE CURRENT WEEK.                                             
                                                                                
               CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51 DPG52             
                                                        DPG53 DPG54             
                                                        DPG55 DPG56             
               IF ( (DPG54-SEVERE-ERROR)                                        
                 OR (DPG54-WARNING-ERROR AND DPG54-DATE-INVALID) )              
                   DISPLAY '** ABEND **'                                        
                   DISPLAY PC-CURRENT-PROGRAM-NAME                              
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR ON FIRST DAY OF WEEK LOOKUP'              
                   DISPLAY PC-CURRENT-PROGRAM-NAME                              
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR INDICATOR = '  DPG54-ERROR-IND            
                   DISPLAY PC-CURRENT-PROGRAM-NAME                              
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR MESSAGE = '  DPG54-ERROR-MESSAGE          
                   SET  ABEND-4019  TO  TRUE                                    
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               END-IF                                                           
                                                                                
      *        NEXT, SUBTRACT 7 CALENDAR DAYS FROM THE RETURNED DATE            
      *        TO GET THE FIRST DAY OF THE WEEK THAT NEEDS TO BE                
      *        PROCESSED.                                                       
                                                                                
               MOVE  DPG55-1ST-WKDY-GREG-YR  TO  PV-GD-YEAR                     
               MOVE  DPG55-1ST-WKDY-GREG-MO  TO  PV-GD-MONTH                    
               MOVE  DPG55-1ST-WKDY-GREG-DY  TO  PV-GD-DAY                      
               INITIALIZE DPG51 DPG52                                           
                          DPG53 DPG54                                           
                          DPG55 DPG56                                           
               SET DPG51-FISCAL-JAN-DEC-F2   TO  TRUE                           
               MOVE  PV-GREGORIAN-DATE       TO  DPG52-GREG-DATE-N              
               SET   DPG51-DECREMENT-DATE    TO  TRUE                           
               MOVE  7                       TO  DPG51-INCR-DECR-DAYS-9         
               SET DPG52-LK-DTE-GREG         TO TRUE                            
                                                                                
               CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51 DPG52             
                                                        DPG53 DPG54             
                                                        DPG55 DPG56             
               IF ( (DPG54-SEVERE-ERROR)                                        
                 OR (DPG54-WARNING-ERROR AND DPG54-DATE-INVALID) )              
                   DISPLAY '** ABEND **'                                        
                   DISPLAY PC-CURRENT-PROGRAM-NAME                              
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR ON DECREMENT DAYS FROM DATE.'             
                   DISPLAY PC-CURRENT-PROGRAM-NAME                              
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR INDICATOR = '  DPG54-ERROR-IND            
                   DISPLAY PC-CURRENT-PROGRAM-NAME                              
                   ' - CALENDAR ROUTINE '                                       
                               'ERROR MESSAGE = '  DPG54-ERROR-MESSAGE          
                   SET  ABEND-4019  TO  TRUE                                    
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               END-IF                                                           
                                                                                
               MOVE  DPG55-DB2-ISO-DATE-FMT-YR-CC TO DF-CENTURY                 
               MOVE  DPG55-DB2-ISO-DATE-FMT-YR-YY TO DF-YEAR                    
               MOVE  DPG55-DB2-ISO-DATE-FMT-MO    TO DF-MONTH                   
               MOVE  DPG55-DB2-ISO-DATE-FMT-DY    TO DF-DAY                     
           ELSE                                                                 
               DISPLAY                                                          
                   'USING CONTROL CARD TO DETERMINE WEEK TO PROCESS.'           
               MOVE  DC-CONTROL-CENTURY  TO  DF-CENTURY                         
               MOVE  DC-CONTROL-YEAR     TO  DF-YEAR                            
               MOVE  DC-CONTROL-MONTH    TO  DF-MONTH                           
               MOVE  DC-CONTROL-DAY      TO  DF-DAY                             
           END-IF.                                                              
                                                                                
           MOVE  DF-YEAR               TO  PV-GD-YEAR.                          
           MOVE  DF-MONTH              TO  PV-GD-MONTH.                         
           MOVE  DF-DAY                TO  PV-GD-DAY.                           
           INITIALIZE DPG51 DPG52                                               
                      DPG53 DPG54                                               
                      DPG55 DPG56                                               
           SET   DPG51-DO-NOT-INCR-DECR-DATE                                    
                                         TO  TRUE.                              
           MOVE  ZERO                    TO  DPG51-INCR-DECR-DAYS-9.            
                                                                                
      *    NEXT, RETRIEVE THE BOW DATE FOR THE PROCESSING WEEK.                 
                                                                                
           SET DPG51-FISCAL-JAN-DEC-F2 TO  TRUE.                                
           SET DPG52-LK-DTE-GREG       TO  TRUE.                                
           MOVE  PV-GREGORIAN-DATE     TO  DPG52-GREG-DATE-N.                   
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51 DPG52                 
                                                    DPG53 DPG54                 
                                                    DPG55 DPG56                 
           IF ( (DPG54-SEVERE-ERROR)                                            
             OR (DPG54-WARNING-ERROR AND DPG54-DATE-INVALID) )                  
               DISPLAY '** ABEND **'                                            
               DISPLAY PC-CURRENT-PROGRAM-NAME                                  
               ' - CALENDAR ROUTINE '                                           
                           'ERROR ON DECREMENT DAYS FROM DATE.'                 
               DISPLAY PC-CURRENT-PROGRAM-NAME                                  
               ' - CALENDAR ROUTINE '                                           
                           'ERROR INDICATOR = '  DPG54-ERROR-IND                
               DISPLAY PC-CURRENT-PROGRAM-NAME                                  
               ' - CALENDAR ROUTINE '                                           
                           'ERROR MESSAGE = '  DPG54-ERROR-MESSAGE              
               SET  ABEND-4019  TO  TRUE                                        
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           MOVE  DPG55-1ST-WKDY-DB2ISO-FMT TO PV-FSCL-BOW-DTE                   
           MOVE DPG55-LAST-WKDY-DB2ISO-FMT TO PV-FSCL-EOW-DTE.                  
                                                                                
           DISPLAY 'PV-FSCL-BOW-DTE:          ' PV-FSCL-BOW-DTE.                
           DISPLAY 'PV-FSCL-EOW-DTE:          ' PV-FSCL-EOW-DTE.                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * PROCESS THE CURSOR FOR SKUS THAT ARE IN A STATUS OTHER THAN    *        
      * CLEARANCE OR MIXED AND THAT HAVE NOT UNDERGONE A STATUS CHANGE *        
      * DURING THE WEEK.  ACCUMULATE ALL SALES FOR THE ENTIRE WEEK.    *        
      * WRITE A RECORD.                                                *        
      *----------------------------------------------------------------*        
       2000-PROCESS-SKU-SLS-CSR1.                                               
                                                                                
      *    MOVE CURSOR VALUES TO RECORD AND WRITE RECORD                        
           PERFORM 2100-BUILD-EXTRACT-RECORD.                                   
                                                                                
      *    FETCH THE NEXT ROW.                                                  
           PERFORM 8500-FETCH-SKU-SLS-CSR1.                                     
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * MOVE DATA FROM CUSRSOR AND WRITE THE EXTRACT RECORD            *        
      *----------------------------------------------------------------*        
       2100-BUILD-EXTRACT-RECORD.                                               
                                                                                
           MOVE SKXREF-ITM-NBR        TO   MA017-ITM-NBR.                       
                                                                                
           MOVE MHT00000-STR-NBR       TO PV-STR-NBR-9.                         
           MOVE PV-STR-NBR-9           TO MA017-STORE                           
                                                                                
      *    MOVE MHT00000-STR-NBR      TO   MA017-STORE.                         
                                                                                
           MOVE MHT00000-SLS-UNT-QTY  TO   MA017-SALES-QTY.                     
MAK140     MOVE SKXREF-DEPT           TO   MA017-SKU-DEPT.                      
MAK140     MOVE SKXREF-CLASS          TO   MA017-SKU-CLASS.                     
MAK140     MOVE SKXREF-SUBCLASS       TO   MA017-SKU-SUBCLS.                    
MAK140     MOVE SKXREF-SKU            TO   MA017-SKU.                           
                                                                                
           WRITE SALES-EXTRACT-RECORD FROM MA017-SKU-STORE-SALES.               
           ADD PC-ONE                 TO   PV-SKU-SLS-OUTPUT-RECS.              
                                                                                
      *----------------------------------------------------------------*        
      * OPEN SKU SALES CURSOR                                          *        
      *----------------------------------------------------------------*        
       8400-OPEN-SKU-SLS-CSR1.                                                  
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL                                                         
                  OPEN  SKU-SLS-CSR1                                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '8400-OPEN-SKU-SLS-CSR1'                           
                                      TO  PV-PARAGRAPH-NAME                     
                        MOVE 'OPEN SKU-SLS-CSR1'                                
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                        PERFORM 9900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '8400-OPEN-SKU-SLS-CSR1'                                    
                                      TO  PV-PARAGRAPH-NAME                     
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED'                            
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
RCF001* NIS CHANGE, REMOVED DEPT, CLASS, AND SEQ. REPLACE WITH                  
RCF001* ITEM NUMBER                                                             
       8500-FETCH-SKU-SLS-CSR1.                                                 
                                                                                
           EXEC SQL                                                             
               FETCH SKU-SLS-CSR1                                               
               INTO  :SKXREF-ITM-NBR                                            
                   , :MHT00000-STR-NBR                                          
                   , :MHT00000-SLS-UNT-QTY                                      
MAK140             , :SKXREF-DEPT                                               
MAK140             , :SKXREF-CLASS                                              
MAK140             , :SKXREF-SUBCLASS                                           
MAK140             , :SKXREF-SKU                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   ADD 1 TO PV-CSR1-RECS                                        
               WHEN SQLCODE = +100                                              
                    IF PV-CSR1-RECS = ZERO                                      
                        SET ABEND-4002 TO TRUE                                  
                        DISPLAY '**  ABEND     **'                              
                        DISPLAY '**  PROGRAM   **  =  MAKUT017'                 
                        DISPLAY '**  NO ROWS RETURNED '                         
                                'FROM SKU-SLS-CSR1  **'                         
                        CALL 'ILBOABN0'  USING ABEND-CODE                       
                    END-IF                                                      
                    SET PS-END-OF-SKU-SLS-CSR1 TO    TRUE                       
               WHEN SQLWARN NOT = SPACES                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '8500-FETCH-SKU-SLS-CSR1'                              
                                      TO  PV-PARAGRAPH-NAME                     
                    MOVE 'FETCH SKU-SLS-CSR1'                                   
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                    PERFORM 9900-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8600-CLOSE-SKU-SLS-CSR1.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  CLOSE SKU-SLS-CSR1                                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '8600-CLOSE-SKU-SLS-CSR1'                          
                                      TO  PV-PARAGRAPH-NAME                     
                        MOVE 'CLOSE SKU-SLS-CSR1'                               
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                        PERFORM 9900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '8600-CLOSE-SKU-SLS-CSR1'                                   
                                      TO  PV-PARAGRAPH-NAME                     
               MOVE 'MAX NUMBER OF RETRIES EXCEEDED'                            
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       9900-SQL-ABEND.                                                          
                                                                                
           SET ABEND-4001             TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MAKUT017'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
