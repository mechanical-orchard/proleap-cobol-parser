       IDENTIFICATION DIVISION.                                                 
      *                                                                         
       PROGRAM-ID.              FSKUT047.                                       
       AUTHOR.                  COGNIZANT.                                      
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            09-16-2013.                                     
       DATE-COMPILED.                                                           
      *----------------------------------------------------------------*        
      *          FSKUT047 - ITEM NUMBER SPLITUP PROGRAM                         
      *----------------------------------------------------------------*        
      * THIS PROGRAM CREATES THE ITEM NUMBER RANGE FILES WHICH WILL BE          
      * USED BY MULTI THREADED FSK028A-E FOR PROCESSING.                        
      * THE PROGRAM ALSO USES A CONTROL CARD THAT HAVE THE PARTITION            
      * DETAILS ON TSKST. IT WILL CONTAIN THE MAX ITEM# A PARTITION CAN         
      * HOLD. PER DBA, THIS INFO WILL NOT CHANGE VERY OFTEN.                    
      * THE PROGRAM READS THE ITEM# PRESENT IN 1/5TH, 2/5TH, 3/5TH,             
      * 4/5TH POSITIONS OF THE SORTED SALES AUDIT UNLOAD FILE. THEN,            
      * FINDS THE ITEM# FROM THE PARTITION CONTROL CARD FSSI472 THAT IS         
      * THAT IS CLOSE TO THE ONES IN THESE 1/5,2/5,3/5,4/5 POSITIONS.           
      * THEN WRITES THESE RANGES IN THE 5 OUTPUT FILES.                         
      *----------------------------------------------------------------*        
      *                        MODFICATION LOG                         *        
      *----------------------------------------------------------------*        
      *  09/16/2013 - CHG0063998                                                
      *             - INITIAL INSTALL.                                          
      *             - KARTHIK K(COGNIZANT)                                      
      *----------------------------------------------------------------*        
      *                                                                         
      *----------------------------------------------------------------*        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
      *                                                                         
      *----------------------------------------------------------------*        
      * INPUT ITEM FILE                                                         
      *----------------------------------------------------------------*        
           SELECT IN-ITEM-FILE                                                  
               ASSIGN TO IN01.                                                  
      *                                                                         
      *----------------------------------------------------------------*        
      * INPUT PARTITION RANGE FILE                                              
      *----------------------------------------------------------------*        
           SELECT IN-PARTITION-FILE                                             
               ASSIGN TO IN02.                                                  
      *                                                                         
      *----------------------------------------------------------------*        
      * OUTPUT RANGE FILE 1                                                     
      *----------------------------------------------------------------*        
           SELECT OUT-RANGE-FILE-1                                              
               ASSIGN TO OUT01.                                                 
      *                                                                         
      *----------------------------------------------------------------*        
      * OUTPUT RANGE FILE 2                                                     
      *----------------------------------------------------------------*        
           SELECT OUT-RANGE-FILE-2                                              
               ASSIGN TO OUT02.                                                 
      *                                                                         
      *----------------------------------------------------------------*        
      * OUTPUT RANGE FILE 3                                                     
      *----------------------------------------------------------------*        
           SELECT OUT-RANGE-FILE-3                                              
               ASSIGN TO OUT03.                                                 
      *                                                                         
      *----------------------------------------------------------------*        
      * OUTPUT RANGE FILE 4                                                     
      *----------------------------------------------------------------*        
           SELECT OUT-RANGE-FILE-4                                              
               ASSIGN TO OUT04.                                                 
      *                                                                         
      *----------------------------------------------------------------*        
      * OUTPUT RANGE FILE 5                                                     
      *----------------------------------------------------------------*        
           SELECT OUT-RANGE-FILE-5                                              
               ASSIGN TO OUT05.                                                 
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD  IN-ITEM-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 8 CHARACTERS.                                        
       01  IN-ITEM-REC                 PIC  X(8).                               
      *                                                                         
       FD  IN-PARTITION-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS.                                       
       01  IN-PARTITION-REC            PIC  X(80).                              
      *                                                                         
       FD  OUT-RANGE-FILE-1                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 16 CHARACTERS.                                       
       01  OUT-REC-1                   PIC  X(16).                              
      *                                                                         
       FD  OUT-RANGE-FILE-2                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 16 CHARACTERS.                                       
       01  OUT-REC-2                   PIC  X(16).                              
      *                                                                         
       FD  OUT-RANGE-FILE-3                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 16 CHARACTERS.                                       
       01  OUT-REC-3                   PIC  X(16).                              
      *                                                                         
       FD  OUT-RANGE-FILE-4                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 16 CHARACTERS.                                       
       01  OUT-REC-4                   PIC  X(16).                              
      *                                                                         
       FD  OUT-RANGE-FILE-5                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 16 CHARACTERS.                                       
       01  OUT-REC-5                   PIC  X(16).                              
      *                                                                         
      *                                                                         
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME         PIC X(8)        VALUE 'FSKUT047'.        
      *                                                                         
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-IN-CNT               PIC 9(15)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-IN-CNT-1FIFTH        PIC 9(15)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-IN-CNT-2FIFTH        PIC 9(15)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-IN-CNT-3FIFTH        PIC 9(15)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-IN-CNT-4FIFTH        PIC 9(15)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-OUT-CNT              PIC 9(15)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-IN-PART-CNT          PIC 9(15)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-PART-SUB-1           PIC 9(03)       VALUE 1.                 
           05  PV-PART-SUB-2           PIC 9(03)       VALUE 1.                 
           05  PV-MAX-PARTITIONS       PIC 9(03)       VALUE 500.               
      *                                                                         
       01  PS-SWITCHES.                                                         
           05  PS-IN-EOF-SW            PIC X(01)       VALUE 'N'.               
               88  PS-IN-EOF-NO                        VALUE 'N'.               
               88  PS-IN-EOF-YES                       VALUE 'Y'.               
           05  PS-IN-PART-EOF-SW       PIC X(01)       VALUE 'N'.               
               88  PS-IN-PART-EOF-NO                   VALUE 'N'.               
               88  PS-IN-PART-EOF-YES                  VALUE 'Y'.               
      *                                                                         
       01  ABEND-CODE                  PIC S9(04)      VALUE ZERO               
                                                       COMP                     
                                                       SYNC.                    
      *                                                                         
      *----------------------------------------------------------------*        
      *    INPUT RECORD LAYOUT                                                  
      *----------------------------------------------------------------*        
       01  INPUT-RECORD.                                                        
           10 INPUT-REC-ITM-NBR        PIC  S9(15)V COMP-3.                     
      *                                                                         
      *----------------------------------------------------------------*        
      *    INPUT PARTITION REC                                                  
      *----------------------------------------------------------------*        
       01  INPUT-PARTITON-REC.                                                  
           10 INPUT-PART-ITM-MAX       PIC   9(15).                             
      *                                                                         
      *----------------------------------------------------------------*        
      *    OUTPUT RECORD LAYOUT                                                 
      *----------------------------------------------------------------*        
       01  OUTPUT-RECORD.                                                       
           10 OUTPUT-REC-ITM-LOWER     PIC  S9(15)V COMP-3.                     
           10 OUTPUT-REC-ITM-HIGHER    PIC  S9(15)V COMP-3.                     
      *                                                                         
      *----------------------------------------------------------------*        
      *    PARTITION ARRAY                                                      
      *----------------------------------------------------------------*        
       01  INTERNAL-TABLES.                                                     
           05 PARTITION-ARRAY  OCCURS 500 TIMES INDEXED BY IDX.                 
              10 PARTITION-ITM-MAX     PIC   9(15).                             
      *                                                                         
      *                                                                         
      *                                                                         
           EJECT                                                                
       LINKAGE SECTION.                                                         
      *                                                                         
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
      *                                                                         
       0000-MAIN-MODULE.                                                        
      *----------------------------------------------------------------*        
      *  MAIN PROCESSING.                                              *        
      *----------------------------------------------------------------*        
      *                                                                         
           PERFORM 1000-HOUSEKEEPING.                                           
      *                                                                         
           PERFORM 2000-PROCESS.                                                
      *                                                                         
           PERFORM 3000-FINALIZATION.                                           
      *                                                                         
           STOP RUN.                                                            
      *----------------------------------------------------------------*        
      *  OPEN FILES & INITIALIZE.                                      *        
      *----------------------------------------------------------------*        
       1000-HOUSEKEEPING.                                                       
      *                                                                         
           DISPLAY '*-------------------*'.                                     
           DISPLAY '* START OF FSKUT047 *'.                                     
           DISPLAY '*-------------------*'.                                     
           DISPLAY ' '                                                          
      *                                                                         
           OPEN  INPUT IN-ITEM-FILE                                             
                       IN-PARTITION-FILE.                                       
           OPEN OUTPUT OUT-RANGE-FILE-1                                         
                       OUT-RANGE-FILE-2                                         
                       OUT-RANGE-FILE-3                                         
                       OUT-RANGE-FILE-4                                         
                       OUT-RANGE-FILE-5.                                        
      *                                                                         
           PERFORM 8000-READ-ITEM-FILE                                          
             UNTIL PS-IN-EOF-YES.                                               
      *                                                                         
           CLOSE IN-ITEM-FILE.                                                  
           OPEN  INPUT IN-ITEM-FILE.                                            
      *                                                                         
           PERFORM 8100-LOAD-PARTITION-TABLE                                    
             UNTIL PS-IN-PART-EOF-YES.                                          
      *                                                                         
      *----------------------------------------------------------------*        
      *  PROCESS THE INPUT FILE.                                                
      *----------------------------------------------------------------*        
       2000-PROCESS.                                                            
      *                                                                         
           DIVIDE  PV-IN-CNT BY 5 GIVING PV-IN-CNT-1FIFTH ROUNDED.              
           COMPUTE PV-IN-CNT-2FIFTH = PV-IN-CNT-1FIFTH * 2.                     
           COMPUTE PV-IN-CNT-3FIFTH = PV-IN-CNT-1FIFTH * 3.                     
           COMPUTE PV-IN-CNT-4FIFTH = PV-IN-CNT-1FIFTH * 4.                     
      *                                                                         
           MOVE 0 TO PV-IN-CNT.                                                 
           MOVE 1 TO PV-PART-SUB-1.                                             
           MOVE 1 TO PV-PART-SUB-2.                                             
      *                                                                         
           PERFORM 8000-READ-ITEM-FILE                                          
             UNTIL PV-IN-CNT = PV-IN-CNT-1FIFTH.                                
      *                                                                         
           MOVE ZEROES TO OUTPUT-REC-ITM-LOWER.                                 
      ***  MOVE INPUT-REC-ITM-NBR TO OUTPUT-REC-ITM-HIGHER.                     
           PERFORM 2100-GET-CLOSEST-PARTITION-ITM.                              
           WRITE OUT-REC-1 FROM  OUTPUT-RECORD.                                 
           DISPLAY ' RANGE OF FIRST FILE  :', OUTPUT-REC-ITM-LOWER              
                    ' TO ' OUTPUT-REC-ITM-HIGHER.                               
      *                                                                         
           PERFORM 8000-READ-ITEM-FILE                                          
             UNTIL PV-IN-CNT = PV-IN-CNT-2FIFTH.                                
      *                                                                         
           COMPUTE OUTPUT-REC-ITM-LOWER = OUTPUT-REC-ITM-HIGHER + 1.            
      ***  MOVE INPUT-REC-ITM-NBR TO OUTPUT-REC-ITM-HIGHER.                     
           PERFORM 2100-GET-CLOSEST-PARTITION-ITM.                              
           WRITE OUT-REC-2 FROM  OUTPUT-RECORD.                                 
           DISPLAY ' RANGE OF SECOND FILE :', OUTPUT-REC-ITM-LOWER              
                    ' TO ' OUTPUT-REC-ITM-HIGHER.                               
      *                                                                         
           PERFORM 8000-READ-ITEM-FILE                                          
             UNTIL PV-IN-CNT = PV-IN-CNT-3FIFTH.                                
      *                                                                         
           COMPUTE OUTPUT-REC-ITM-LOWER = OUTPUT-REC-ITM-HIGHER + 1.            
      ***  MOVE INPUT-REC-ITM-NBR TO OUTPUT-REC-ITM-HIGHER.                     
           PERFORM 2100-GET-CLOSEST-PARTITION-ITM.                              
           WRITE OUT-REC-3 FROM  OUTPUT-RECORD.                                 
           DISPLAY ' RANGE OF THIRD FILE  :', OUTPUT-REC-ITM-LOWER              
                    ' TO ' OUTPUT-REC-ITM-HIGHER.                               
      *                                                                         
           PERFORM 8000-READ-ITEM-FILE                                          
             UNTIL PV-IN-CNT = PV-IN-CNT-4FIFTH.                                
      *                                                                         
           COMPUTE OUTPUT-REC-ITM-LOWER = OUTPUT-REC-ITM-HIGHER + 1.            
      ***  MOVE INPUT-REC-ITM-NBR TO OUTPUT-REC-ITM-HIGHER.                     
           PERFORM 2100-GET-CLOSEST-PARTITION-ITM.                              
           WRITE OUT-REC-4 FROM  OUTPUT-RECORD.                                 
           DISPLAY ' RANGE OF FOURTH FILE :', OUTPUT-REC-ITM-LOWER              
                    ' TO ' OUTPUT-REC-ITM-HIGHER.                               
      *                                                                         
           COMPUTE OUTPUT-REC-ITM-LOWER = OUTPUT-REC-ITM-HIGHER + 1.            
           MOVE +9999999999999999 TO OUTPUT-REC-ITM-HIGHER.                     
           WRITE OUT-REC-5 FROM  OUTPUT-RECORD.                                 
           DISPLAY ' RANGE OF FIFTH FILE  :', OUTPUT-REC-ITM-LOWER              
                    ' TO ' OUTPUT-REC-ITM-HIGHER.                               
      *                                                                         
      *----------------------------------------------------------------*        
      *  GET THE CLOSEST ITM# FROM THE PARTION TABLE                            
      *----------------------------------------------------------------*        
       2100-GET-CLOSEST-PARTITION-ITM.                                          
           PERFORM                                                              
            VARYING PV-PART-SUB-1 FROM PV-PART-SUB-1 BY 1                       
            UNTIL PARTITION-ITM-MAX(PV-PART-SUB-1) >= INPUT-REC-ITM-NBR         
           END-PERFORM.                                                         
           PERFORM                                                              
             VARYING PV-PART-SUB-2 FROM PV-PART-SUB-2 BY 1                      
             UNTIL PV-PART-SUB-2  = PV-PART-SUB-1 - 1                           
           END-PERFORM.                                                         
           IF (INPUT-REC-ITM-NBR - PARTITION-ITM-MAX(PV-PART-SUB-2))            
              <= (PARTITION-ITM-MAX(PV-PART-SUB-1) - INPUT-REC-ITM-NBR)         
             MOVE PARTITION-ITM-MAX(PV-PART-SUB-2) TO                           
                  OUTPUT-REC-ITM-HIGHER                                         
           ELSE                                                                 
             MOVE PARTITION-ITM-MAX(PV-PART-SUB-1)                              
                           TO OUTPUT-REC-ITM-HIGHER                             
           END-IF.                                                              
      *                                                                         
      *----------------------------------------------------------------*        
      *  CLOSE THE FILES.                                              *        
      *----------------------------------------------------------------*        
       3000-FINALIZATION.                                                       
      *                                                                         
           CLOSE IN-ITEM-FILE                                                   
                 OUT-RANGE-FILE-1                                               
                 OUT-RANGE-FILE-2                                               
                 OUT-RANGE-FILE-3                                               
                 OUT-RANGE-FILE-4                                               
                 OUT-RANGE-FILE-5.                                              
      *                                                                         
           DISPLAY '*-----------------*'.                                       
           DISPLAY '* END OF FSKUT047 *'.                                       
           DISPLAY '*-----------------*'.                                       
      *----------------------------------------------------------------*        
      *  READ THE ITEMS FROM FS AUDIT TABLE FILE.                               
      *----------------------------------------------------------------*        
       8000-READ-ITEM-FILE.                                                     
      *                                                                         
           READ IN-ITEM-FILE                                                    
               INTO INPUT-RECORD                                                
               AT END                                                           
                   SET PS-IN-EOF-YES TO TRUE                                    
               NOT AT END                                                       
                   ADD 1  TO  PV-IN-CNT                                         
           END-READ.                                                            
      *----------------------------------------------------------------*        
      *  READ THE MAX ITEM# FOR EACH PARTITION FROM PARTITION FILE.             
      *----------------------------------------------------------------*        
       8100-LOAD-PARTITION-TABLE.                                               
      *                                                                         
           READ IN-PARTITION-FILE                                               
               INTO INPUT-PARTITON-REC                                          
               AT END                                                           
                   SET PS-IN-PART-EOF-YES TO TRUE                               
               NOT AT END                                                       
                   ADD 1  TO  PV-IN-PART-CNT                                    
      *                                                                         
                   IF PV-PART-SUB-1 > PV-MAX-PARTITIONS                         
                       DISPLAY '*********** ABEND *******************'  **'     
                       DISPLAY ' TABLE OVERFLOW ON PARTITION         '    '     
                       DISPLAY ' INCREASE PV-MAX-PARTITIONS LIMIT    '    '     
                       DISPLAY ' CURRENT LIMIT = ' PV-MAX-PARTITIONS            
                       DISPLAY '*************************************'  **'     
                       MOVE +4003 TO ABEND-CODE                                 
                       CALL 'ILBOABN0' USING ABEND-CODE                         
                       DISPLAY '  '                                             
                   END-IF                                                       
      *                                                                         
                   MOVE INPUT-PART-ITM-MAX TO                                   
                                   PARTITION-ITM-MAX(PV-PART-SUB-1)             
                   ADD 1  TO  PV-PART-SUB-1                                     
           END-READ.                                                            
