       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         IMKRP019.                                            
       AUTHOR.             D. THEEL.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       MARCH 2008.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  PRICE COMPARATIVE CHANGE REPORT                                        
      *  THIS REPORT WILL RUN DAILY WITH AN INPUT FILE CONTAINING THE           
      *  ITEM DOMAIN STYLE RECORDS THAT HAVE HAD A UPDATE TO THE                
      *  PRICE COMPARTATIVE VALUE (TMDARVS.RTL_CLSN_SEQ_NBR). THE INPUT         
      *  FILE WILL CONTAIN 2 RECORDS FOR EACH STYLE - OLD AND NEW.              
      *  CREATE A CSV REC OF THE CHANGE.  WE ACCEPT IN A CONTROL CARD           
      *  OF THE ESP RUN DATE SO IF IT IS SUNDAY WE CAN CREATE A TITLE           
      *  AND COLUMN HEADER RECORD.  THE CONTROL CARD DATE FORMAT IS             
      *  MM/DD/CCYY.                                                            
      ******************************************************************        
      ******************************************************************        
      *  CHANGES:                                                               
      *                                                                         
      *  03/03/2008 D. THEEL                                                    
      *     INITIAL PROGRAM                                                     
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT CONTROL-CARD-FILE         ASSIGN TO INP01.                    
           SELECT STYLE-FILE                ASSIGN TO INP02.                    
                                                                                
           SELECT REPORT-FILE               ASSIGN TO RPT01.                    
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-FILE                                            00590094
           RECORDING MODE IS F                                          00600094
           BLOCK CONTAINS 0 RECORDS.                                    00610094
       01  SD-CONTROL-RECORD       PIC X(80).                           00620094
                                                                                
       FD  STYLE-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 510 CHARACTERS                                       
           DATA RECORD IS STYLE-RECORD.                                         
       01  STYLE-RECORD                PIC  X(510).                             
                                                                                
       FD  REPORT-FILE                                                          
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
                                                                                
       01  REPORT-RECORD                    PIC X(157).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE +0 COMP SYNC.          
           88  PV-ABEND-4001                   VALUE +4001.                     
      *        EMPTY CONTROL CARD                                               
                                                                                
                                                                                
      ******************************************************************        
      *    PC-PROGRAM CONSTANTS                                        *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'IMKRP019'.        
           05  PC-BUYER-NOT-FOUND           PIC X(18)  VALUE                    
                   'BUYER NOT FOUND   '.                                        
010700     05  PC-MAXIMUM-RETRY-COUNT       PIC S9(04)  VALUE +2                
010800                                                  COMP SYNC.              
                                                                                
      ****************************************************************  00830094
      * RECORD LAYOUT FOR CONTROL CARD FILE.                            00840094
      * DATE WILL BE ESP CONTROLLED - SYSTEM DATE                       00840094
      ****************************************************************  00850094
       01  CC-CONTROL-CARD.                                             00860094
           05  CC-CONTROL-DATE              PIC X(10).                  00870094
           05  FILLER                       PIC X(70).                  00880094
                                                                                
      ******************************************************************        
      *       PROGRAM VARIABLES                                        *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.            
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(30)  VALUE SPACES.            
           05  PV-CONTROL-DATE              PIC X(10)  VALUE SPACES.            
           05  PV-DB2-DAY                   PIC S9(4)  COMP VALUE +0.           
           05  PV-ONHAND-QTY                PIC S9(7)V COMP-3 VALUE +0.         
           05  PV-HOLD-RMS-STYL-ID          PIC S9(8)V COMP-3 VALUE +0.         
           05  PV-MIN-FIRST-RCVD            PIC X(10)  VALUE SPACES.            
007000     05  PV-RETRY-COUNT               PIC S9(04) VALUE ZERO               
007100                                                 COMP SYNC.               
           05  PV-DEPT-NBR                  PIC 9(03)  VALUE ZERO.              
           05  PV-SCL-NBR                   PIC 9(03)  VALUE ZERO.              
       01  SA-REC-OUT.                                                          
           05  SA-BUYER-NBR                 PIC X(03)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-BUYER-LAST-NAME           PIC X(18)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-DEPT-NBR                  PIC X(03)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-SCL-NBR                   PIC X(03)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-VS-NBR                    PIC X(20)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-VS-DESC                   PIC X(30)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-ONHAND                    PIC -Z,ZZZ,ZZ9                      
                                                       VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-FIRST-RCVD-DATE           PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-OLD-PRICE-COMP-DESC       PIC X(20)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-NEW-PRICE-COMP-DESC       PIC X(20)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-CHANGE-DATE               PIC X(10)  VALUE SPACES.            
                                                                                
      ******************************************************************        
      *    PS-PROGRAM SWITCHES                                         *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW            PIC  X(01)   VALUE 'N'.             
               88  PS-END-OF-FILE                        VALUE 'Y'.             
           05  SW-END-OF-CONTROL-CARDS-SW  PIC X(01)  VALUE 'N'.        01590094
               88  END-OF-CONTROL-CARDS               VALUE 'Y'.        01600094
           05  PS-STYLE-CURR-FND-SW            PIC  X(01)   VALUE 'N'.          
               88  PS-STYLE-CURR-FND                       VALUE 'Y'.           
               88  PS-STYLE-CURR-NOT-FND                   VALUE 'N'.           
                                                                                
      ******************************************************************        
      *    PA-PROGRAM ACCUMULATORS                                     *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-REPORT-RECORDS-WRITTEN    PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-SUM-OH-SELECTS            PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-RECS-READ-CNT             PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-MIN-FIRST-RCVD-CNT        PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-STYLE-SELECT-CNT          PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
                                                                                
      ******************************************************************        
      *    STYLE INPUT RECORD LAYOUT                                   *        
      ******************************************************************        
019000     COPY IMRD058.                                                        
33885  01  REPORT-TITLE-LINE-CSV.                                               
33885      05  FILLER                       PIC X(58)  VALUE ',,,,'.            
33885      05  REPORT-TITLE-LINE1-CSV.                                          
33885          10  REPORT-TITLE-LINEA-CSV   PIC X(41)  VALUE                    
33885         ', WEEKLY PRICE COMPARATIVE CHANGE REPORT '.                      
33885      05  FILLER                       PIC X(10)  VALUE ',,,,'.            
           05  FILLER                       PIC X(07)  VALUE                    
33885                                     'DATE:  '.                            
           05  TITLE-DATE                   PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(31)  VALUE ',,,,'.            
                                                                                
33885  01  REPORT-HEADING-LINE-CSV.                                             
33885      05  FILLER                       PIC X(07)  VALUE                    
33885          'BUYER #'.                                                       
33885      05  FILLER                       PIC X(02)  VALUE ', '.              
33885      05  FILLER                       PIC X(10)  VALUE                    
33885          'BUYER NAME'.                                                    
33885      05  FILLER                       PIC X(02)  VALUE ', '.              
33885      05  FILLER                       PIC X(04)  VALUE                    
33885          'DEPT'.                                                          
33885      05  FILLER                       PIC X(02)  VALUE ', '.              
33885      05  FILLER                       PIC X(09)  VALUE                    
33885          'SUB CLASS'.                                                     
33885      05  FILLER                       PIC X(02)  VALUE ', '.              
33885      05  FILLER                       PIC X(07)  VALUE                    
33885          'STYLE #'.                                                       
33885      05  FILLER                       PIC X(02)  VALUE ', '.              
33885      05  FILLER                       PIC X(10)  VALUE                    
33885          'STYLE DESC'.                                                    
33885      05  FILLER                       PIC X(02)  VALUE ', '.              
33885      05  FILLER                       PIC X(07)  VALUE                    
33885          'ON HAND'.                                                       
33885      05  FILLER                       PIC X(02)  VALUE ', '.              
33885      05  FILLER                       PIC X(18)  VALUE                    
33885          'FIRST RECEIPT DATE'.                                            
33885      05  FILLER                       PIC X(02)  VALUE ', '.              
33885      05  FILLER                       PIC X(14)  VALUE                    
33885          'OLD PRICE COMP'.                                                
33885      05  FILLER                       PIC X(02)  VALUE ', '.              
33885      05  FILLER                       PIC X(14)  VALUE                    
33885          'NEW PRICE COMP'.                                                
33885      05  FILLER                       PIC X(02)  VALUE ', '.              
33885      05  FILLER                       PIC X(11)  VALUE                    
33885          'CHANGE DATE'.                                                   
33885      05  FILLER                       PIC X(26)  VALUE ',,'.              
                                                                                
      ******************************************************************        
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    MB-MERCHANDISE AREA TABLE                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *    MB-MERCHANDISE AREA TABLE                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TRSPBAR                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *    DEPT SCL STYLE TABLE                                        *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *    SKU STORE                                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKST                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TSKXREF  GET SKU FROM ITEM NMB                              *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TUPC                                                        *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TVSCHVL                                                     *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    STANDARD LAYOUT FOR THE SQL COMMUNCATIONS AREA              *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAINLINE.                                                           
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B200-PROCESS-REPORT                                          
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM B300-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *     B100-INITIALIZE                                            *        
      * ==> PROCESSES:                                                 *        
      *     - READS THE CONTROL FILE RECORD                            *        
      *     - READS THE FIRST RECORD FROM STYLE FILE                   *        
      * ==> PERFORMED FROM:  A100-MAINLINE                             *        
      ******************************************************************        
       B100-INITIALIZE.                                                         
           OPEN INPUT CONTROL-CARD-FILE                                         
33885                 STYLE-FILE                                                
33885           OUTPUT REPORT-FILE.                                             
                                                                                
           PERFORM C100-READ-CONTROL-CARD.                              03240094
           MOVE CC-CONTROL-DATE          TO PV-CONTROL-DATE.                    
           PERFORM D100-GET-DAY-OF-WEEK.                                        
      * IF SUNDAY, WRITE TITLE AND HEADER AND POPULATE TITLE DATE   *           
           IF PV-DB2-DAY = 1                                                    
               MOVE PV-CONTROL-DATE TO TITLE-DATE                               
               WRITE REPORT-RECORD FROM REPORT-TITLE-LINE-CSV AFTER 1           
               WRITE REPORT-RECORD FROM REPORT-HEADING-LINE-CSV                 
                   AFTER 1                                                      
           END-IF.                                                              
           PERFORM C200-READ-STYLE-REC.                                         
           IF PS-END-OF-FILE                                                    
               DISPLAY ' '                                                      
               DISPLAY '***********************************'                    
               DISPLAY ' NO INPUT RECS TO PROCESS.'                             
               DISPLAY '***********************************'                    
               DISPLAY ' '                                                      
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     B200-PROCESS-REPORT                                        *        
      * ==> PROCESSES:                                                 *        
      * ==> PERFORMED FROM:  A100-MAINLINE                             *        
      ******************************************************************        
       B200-PROCESS-REPORT.                                                     
      * THERE ARE 2 RECS FOR EACH RMS STYL ID                                   
           IF IM058-RMS-STYL-ID = PV-HOLD-RMS-STYL-ID                           
               IF PS-STYLE-CURR-FND                                             
      * THEN THIS SECOND REC IS THE OLD                                         
                   MOVE IM058-RTL-CLSN-DESC TO                                  
                                           SA-OLD-PRICE-COMP-DESC               
                   SET PS-STYLE-CURR-NOT-FND TO TRUE                            
               ELSE                                                             
      * THEN THIS SECOND REC IS THE NEW PRICE COMPARATIVE DESC CHANGE           
                   PERFORM D400-OLD-OR-NEW                                      
               END-IF                                                           
               PERFORM F100-PRINT-DETAIL                                        
           ELSE                                                                 
               PERFORM D400-OLD-OR-NEW                                          
           END-IF.                                                              
           MOVE IM058-RMS-STYL-ID TO PV-HOLD-RMS-STYL-ID.                       
           PERFORM C200-READ-STYLE-REC.                                         
                                                                                
      ******************************************************************        
      *     B300-EOJ-ROUTINE                                           *        
      * ==> PROCESSES:                                                 *        
      *     - CLOSES THE FILES                                         *        
      *     - DISPLAYS THE NUMBER OF RECORDS READ AND WRITTEN          *        
      * ==> PERFORMED FROM:  A100-MAINLINE                             *        
      ******************************************************************        
       B300-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE CONTROL-CARD-FILE                                              
33885            STYLE-FILE                                                     
33885            REPORT-FILE.                                                   
                                                                                
           DISPLAY '*******'.                                                   
           DISPLAY 'MESSAGES FOR PROGRAM   '  PC-PROGRAM-NAME.                  
           DISPLAY 'RECS READ      :       '  PA-RECS-READ-CNT.                 
           DISPLAY 'STYLE SELECTS  :       '  PA-STYLE-SELECT-CNT.              
           DISPLAY 'ONHAND SELECTS :       '  PA-SUM-OH-SELECTS.                
           DISPLAY '1ST RCVD SELECTS:      '  PA-MIN-FIRST-RCVD-CNT.            
           DISPLAY 'RECS WRITTEN    :      '  PA-REPORT-RECORDS-WRITTEN.        
           DISPLAY '*******'.                                                   
                                                                                
      ******************************************************************        
      *     C100-READ-CONTROL-CARD                                     *        
      ******************************************************************        
       C100-READ-CONTROL-CARD.                                                  
           MOVE 'C100-READ-CONTROL-CARD' TO PV-PARAGRAPH-NAME.                  
                                                                        07210094
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                  07190094
               AT END SET END-OF-CONTROL-CARDS TO TRUE.                 07200094
                                                                        07210094
           IF END-OF-CONTROL-CARDS                                      07220094
               DISPLAY '*** NO DATE CONTROL CARD ***'                   07230094
               SET PV-ABEND-4001 TO TRUE                                        
               PERFORM 9999-ABEND                                       07240094
           ELSE                                                         07250094
               DISPLAY ' CONTROL-DATE ' CC-CONTROL-DATE                 07260094
           END-IF.                                                      07280094
                                                                                
      ******************************************************************        
      *     C200-READ-STYLE-REC                                        *        
      ******************************************************************        
       C200-READ-STYLE-REC.                                                     
           MOVE 'C200-READ-STYLE-REC' TO PV-PARAGRAPH-NAME.                     
           INITIALIZE IM058-ITEM-STYLE-DOMAIN-REC.                              
           READ STYLE-FILE                                                      
               INTO IM058-ITEM-STYLE-DOMAIN-REC                                 
                   AT END                                                       
                       MOVE 'Y'     TO PS-END-OF-FILE-SW                        
                   NOT AT END                                                   
                       ADD +1                 TO PA-RECS-READ-CNT               
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *     D100-GET-DAY-OF-WEEK  (1 = SUNDAY)                         *        
      ******************************************************************        
       D100-GET-DAY-OF-WEEK.                                                    
           MOVE 'D100-GET-DAY-OF-WEEK' TO PV-PARAGRAPH-NAME.                    
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
                                                                                
               EXEC SQL                                                         
                   SELECT DAYOFWEEK(:PV-CONTROL-DATE)                           
                     INTO :PV-DB2-DAY                                           
                     FROM SYSIBM.SYSDUMMY1                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY '************************'                       
                       DISPLAY ' D100-GET-DAY-OF-WEEK'                          
                       DISPLAY ' CC DATE = ' PV-CONTROL-DATE                    
                       DISPLAY '************************'                       
                       MOVE 'D100-GET-DAY-OF-WEEK'                              
                                         TO PV-PARAGRAPH-NAME                   
                       MOVE 'SELECT FROM SYSIBM.SYSDUMMY1'                      
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
046200     END-PERFORM.                                                         
046300*                                                                         
046400     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE 'D100-GET-DAY-OF-WEEK'                                      
046600                                 TO PV-PARAGRAPH-NAME                CL**1
               MOVE 'SELECT FROM SYSIBM.SYSDUMMY1'                              
046800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM Z900-SQL-ABEND                                           
047000     END-IF.                                                              
                                                                                
      ******************************************************************        
      *     D300-SELECT-BUYER-NAME                                     *        
      ******************************************************************        
       D300-SELECT-BUYER-NAME.                                                  
           MOVE IM058-DEPT-NBR TO PV-DEPT-NBR.                                  
           MOVE PV-DEPT-NBR TO MDSEAR-DEPT-NBR.                                 
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
024800                OR SQLCODE = +100                                         
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT C.WORKER_LAST_NAME                                    
                        , A.BYR_NBR                                             
                     INTO :RSPBAR-WORKER-LAST-NAME                              
                        , :MDSEAR-BYR-NBR                                       
                     FROM TMDSEAR A                                             
                        , TRSPBAR C                                             
                    WHERE A.OPERCO_NBR  = '03'                                  
                      AND A.DEPT_NBR    = :MDSEAR-DEPT-NBR                      
                      AND A.MDSELV_CDE  = 'DPT'                                 
                      AND A.MDSEAR_DKEY = C.MDSEAR_DKEY                         
                      AND C.RSPLVL_CDE  = 'BYR'                                 
                      AND (CURRENT DATE BETWEEN A.STRT_DTE                      
                                            AND A.END_DTE)                      
                      AND (CURRENT DATE BETWEEN C.WORKER_STRT_DTE               
                                            AND C.WORKER_END_DTE)               
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       MOVE MDSEAR-BYR-NBR TO SA-BUYER-NBR                      
                       MOVE RSPBAR-WORKER-LAST-NAME TO                          
                                   SA-BUYER-LAST-NAME                           
                   WHEN SQLCODE = +100                                          
33885                  MOVE SPACES           TO SA-BUYER-NBR                    
                       MOVE PC-BUYER-NOT-FOUND                                  
                                             TO SA-BUYER-LAST-NAME              
                   WHEN OTHER                                                   
                       MOVE 'D300-SELECT-BUYER-NAME'                            
                                         TO PV-PARAGRAPH-NAME                   
                       MOVE 'SELECT BUYER NAME'                                 
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
046200     END-PERFORM.                                                         
046300*                                                                         
046400     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE 'D300-SELECT-BUYER-NAME'                                    
046600                                 TO PV-PARAGRAPH-NAME                CL**1
               MOVE 'SELECT BUYER NAME'                                         
046800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM Z900-SQL-ABEND                                           
047000     END-IF.                                                              
                                                                                
      ******************************************************************        
      *     D400-OLD-OR-NEW - TRY TO MATCH CURRENT TMDARVS ROW TO THE  *        
      *     INPUT RTL-CLSN-DESC TO DETERMINE IF THIS IS THE OLD OR     *        
      *     NEW CHANGE RECORD FOR THIS STYLE                           *        
      ******************************************************************        
       D400-OLD-OR-NEW.                                                         
                                                                                
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
024800                OR SQLCODE = +100                                         
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT 'Y'                                                   
                    INTO :PS-STYLE-CURR-FND-SW                                  
                     FROM TMDARVS A                                             
                         ,TVSCHVL B                                             
                    WHERE A.RMS_STYL_ID = :IM058-RMS-STYL-ID                    
                      AND A.ACTV_IND = 'Y'                                      
                      AND B.CHARTC_CDE = SMALLINT(6)                            
                      AND B.CHARTC_SEQ_NBR = A.RTL_CLSN_SEQ_NBR                 
                      AND B.CHARTC_VAL_DESC = :IM058-RTL-CLSN-DESC              
                   FETCH FIRST 1 ROWS ONLY                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD +1 TO PA-STYLE-SELECT-CNT                            
                       PERFORM E100-PREP-WRITE                                  
                   WHEN SQLCODE = +100                                          
                       MOVE IM058-RTL-CLSN-DESC TO                              
                                         SA-OLD-PRICE-COMP-DESC                 
                       SET PS-STYLE-CURR-NOT-FND TO TRUE                        
                   WHEN OTHER                                                   
                       MOVE 'D400-OLD-OR-NEW'                                   
                                         TO PV-PARAGRAPH-NAME                   
                       MOVE 'STYLE EXISTENCE CK'                                
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
046200     END-PERFORM.                                                         
046300*                                                                         
046400     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE 'D400-OLD-OR-NEW'                                           
046600                                 TO PV-PARAGRAPH-NAME                CL**1
               MOVE 'STYLE EXISTENCE CK'                                        
046800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM Z900-SQL-ABEND                                           
047000     END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *     D600-GET-SUM-ONHANDS.                                      *        
      * ==> PROCESSES:                                                 *        
      *     -SUM OF ONHANDS FOR ALL SKUS IN THE STYLE                  *        
      ******************************************************************        
       D600-GET-SUM-ONHANDS.                                                    
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
00558          EXEC SQL                                                         
                   SELECT SUM(D.ONHAND_QTY)                                     
                   INTO :PV-ONHAND-QTY                                          
                   FROM TMDARVS A                                               
                       ,TUPC B                                                  
                       ,TSKXREF C                                               
                       ,TSKST D                                                 
                   WHERE A.RMS_STYL_ID = :IM058-RMS-STYL-ID                     
                     AND A.VS_ID = B.VS_ID                                      
                     AND A.SUB_CL_MDSEAR_ID = B.SUB_CL_MDSEAR_ID                
                     AND B.UPC_TYP_CDE = 'I'                                    
                     AND B.EFF_END_DTE IS NULL                                  
                     AND B.UPC_ID    = C.INTR_UPC_ID                            
                     AND C.ITM_NBR   = D.ITEM_NMBR                              
                     AND D.LOC_NBR   = SMALLINT(0)                              
00564          END-EXEC                                                         
044800                                                                          
044900         EVALUATE TRUE                                                    
045000             WHEN SQLCODE = +0                                            
                        ADD +1         TO PA-SUM-OH-SELECTS                     
045200             WHEN SQLCODE = -911                                          
045300             WHEN SQLCODE = -913                                          
045400                  ADD 1          TO PV-RETRY-COUNT                        
045500             WHEN OTHER                                                   
                        MOVE 'D600-GET-SUM-ONHANDS'                             
045700                                 TO PV-PARAGRAPH-NAME                CL**1
045800                  MOVE 'GET SUM ONHANDS'                             CL*11
045900                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
                        PERFORM Z900-SQL-ABEND                                  
046100         END-EVALUATE                                                     
046200     END-PERFORM.                                                         
046300*                                                                         
046400     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE 'D600-GET-SUM-ONHANDS'                                      
046600                                 TO PV-PARAGRAPH-NAME                CL**1
045800         MOVE 'GET SUM ONHANDS'                                      CL*11
046800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM Z900-SQL-ABEND                                           
047000     END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *     D700-GET-MIN-FIRST-RCVD.                                   *        
      * ==> PROCESSES:                                                 *        
      *     -FIND THE MIN FIRST RECEIVED DATE FOR ALL SKUS IN STYLE    *        
      ******************************************************************        
       D700-GET-MIN-FIRST-RCVD.                                                 
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
00558          EXEC SQL                                                         
                   SELECT MIN(VT1.FIRST_RCVD_DATE)                              
                   INTO :PV-MIN-FIRST-RCVD                                      
                   FROM                                                         
                   (SELECT                                                      
                      CASE                                                      
                      WHEN (INTEGER(SUBSTR(CHAR(D.FIRST_RCVD_DATE),3,2))        
                                                 < 30                           
                       AND INTEGER(SUBSTR(CHAR(D.FIRST_RCVD_DATE),7,2))         
                                                 > 0) THEN                      
                          DATE('20'||SUBSTR(CHAR(D.FIRST_RCVD_DATE),3,2)        
                              ||'-'||                                           
                          SUBSTR(CHAR(D.FIRST_RCVD_DATE),5,2)                   
                              ||'-'||                                           
                          SUBSTR(CHAR(D.FIRST_RCVD_DATE),7,2))                  
                      WHEN D.FIRST_RCVD_DATE = 0 THEN                           
                           '9999-09-09'                                         
                      ELSE                                                      
                          DATE('19'||SUBSTR(CHAR(D.FIRST_RCVD_DATE),3,2)        
                              ||'-'||                                           
                          SUBSTR(CHAR(D.FIRST_RCVD_DATE),5,2)                   
                              ||'-'||                                           
                          SUBSTR(CHAR(D.FIRST_RCVD_DATE),7,2))                  
                      END AS FIRST_RCVD_DATE                                    
                      FROM TMDARVS A                                            
                          ,TUPC B                                               
                          ,TSKXREF C                                            
                          ,TSKST D                                              
                      WHERE A.RMS_STYL_ID = :IM058-RMS-STYL-ID                  
                        AND A.VS_ID = B.VS_ID                                   
                        AND A.SUB_CL_MDSEAR_ID = B.SUB_CL_MDSEAR_ID             
                        AND B.UPC_TYP_CDE = 'I'                                 
                        AND B.EFF_END_DTE IS NULL                               
                        AND B.UPC_ID    = C.INTR_UPC_ID                         
                        AND C.ITM_NBR   = D.ITEM_NMBR                           
                        AND D.LOC_NBR   = SMALLINT(0)) AS VT1                   
00564          END-EXEC                                                         
044800                                                                          
044900         EVALUATE TRUE                                                    
045000             WHEN SQLCODE = +0                                            
                        ADD +1         TO PA-MIN-FIRST-RCVD-CNT                 
045200             WHEN SQLCODE = -911                                          
045300             WHEN SQLCODE = -913                                          
045400                  ADD 1          TO PV-RETRY-COUNT                        
045500             WHEN OTHER                                                   
045600                  MOVE 'D700-GET-MIN-FIRST-RCVD'                         C
045700                                 TO PV-PARAGRAPH-NAME                CL**1
045800                  MOVE 'GET MIN FIRST RCVD'                          CL*11
045900                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
                        PERFORM Z900-SQL-ABEND                                  
046100         END-EVALUATE                                                     
046200     END-PERFORM.                                                         
046300*                                                                         
046400     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
045600         MOVE 'D700-GET-MIN-FIRST-RCVD'                                  C
046600                                 TO PV-PARAGRAPH-NAME                CL**1
045800         MOVE 'GET MIN FIRST RCVD'                                   CL*11
046800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM Z900-SQL-ABEND                                           
047000     END-IF.                                                              
                                                                                
      ******************************************************************        
      *     E100-PREP-WRITE                                            *        
      ******************************************************************        
       E100-PREP-WRITE.                                                         
                                                                                
           MOVE IM058-RTL-CLSN-DESC TO                                          
                                SA-NEW-PRICE-COMP-DESC.                         
           PERFORM D300-SELECT-BUYER-NAME.                                      
           MOVE PV-DEPT-NBR TO SA-DEPT-NBR.                                     
           MOVE IM058-SUB-CL-NBR TO PV-SCL-NBR.                                 
           MOVE PV-SCL-NBR TO SA-SCL-NBR.                                       
           MOVE IM058-VS-NBR TO SA-VS-NBR.                                      
           MOVE IM058-VS-DESC TO SA-VS-DESC.                                    
           PERFORM D600-GET-SUM-ONHANDS.                                        
           MOVE PV-ONHAND-QTY TO SA-ONHAND.                                     
           PERFORM D700-GET-MIN-FIRST-RCVD.                                     
           IF PV-MIN-FIRST-RCVD = '9999-09-09'                                  
               MOVE 'NONE' TO PV-MIN-FIRST-RCVD                                 
           END-IF.                                                              
           MOVE PV-MIN-FIRST-RCVD TO SA-FIRST-RCVD-DATE.                        
           MOVE PV-CONTROL-DATE TO SA-CHANGE-DATE.                              
                                                                                
      ******************************************************************        
      *     F100-PRINT-DETAIL                                          *        
      * ==> PROCESSES:                                                 *        
      *     - WRITES THE DETAIL LINES FOR THE REPORT                   *        
      * ==> PERFORMED FROM:  B200-WRITE-REPORT                         *        
      ******************************************************************        
       F100-PRINT-DETAIL.                                                       
                                                                                
                                                                                
           WRITE REPORT-RECORD FROM SA-REC-OUT AFTER 1.                         
                                                                                
           ADD 1                         TO PA-REPORT-RECORDS-WRITTEN.          
                                                                                
      ******************************************************************        
      *     Z900-SQL-ABEND                                             *        
      * ==> PROCESSES:                                                 *        
      *     -PERFORMS ABEND PROCESSING FOR SQL ERRORS.                 *        
      * ==> PERFORMED FROM:                                            *        
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           DISPLAY 'Z900-SQL-ABEND'.                                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** = ' PC-PROGRAM-NAME.                       
           DISPLAY '**  PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                     
           DISPLAY '**  OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.            
           DISPLAY 'RECS READ      :       '  PA-RECS-READ-CNT.                 
           DISPLAY 'STYLE SELECTS  :       '  PA-STYLE-SELECT-CNT.              
           DISPLAY 'ONHAND SELECTS :       '  PA-SUM-OH-SELECTS.                
           DISPLAY '1ST RCVD SELECTS:      '  PA-MIN-FIRST-RCVD-CNT.            
           DISPLAY 'RECS WRITTEN    :      '  PA-REPORT-RECORDS-WRITTEN.        
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                    TO ABEND-CODE.                         
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       9999-ABEND.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME.         07780094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
