       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    RVKUT024.                                                 
       AUTHOR.        JAN BLAZICH.                                              
       DATE-WRITTEN.  MAY 2007.                                                 
       DATE-COMPILED.                                                           
      *---------------------------------------------------------------*         
      *    RVKUT024 GENCO PHYSICAL INVENTORY                          *         
      *                                                               *         
      *    THIS PROGRAM READS THE DOWNLOADED GENCO PHYSICAL INVENTORY *         
      *    FILE, MAKES SURE THE DATA IS VALID, AND CREATES A FILE OF  *         
      *    INVENTORY RECORDS THAT PASSED THE VALIDATION, AN ERROR     *         
      *    FILE AND AN INVENTORY SUMMARY REPORT OF THE VALID RECORDS. *         
      *                                                               *         
      *---------------------------------------------------------------*         
      *  REVISIONS:                                                   *         
      *  DATE: 05/31/07   IR #:WR31391     MADE BY: JAN BLAZICH       *         
      *  DESC: INITIAL INSTALL                                        *         
W40134*                                                               *         
W40134*  DATE         CHANGED BY      DESC                                      
W40134*  -----------  --------------  ----------------------------------        
W40134*  10/30/2011   C. STRODTHOFF - GENCO / RTV UPGRADE. ADD LOC LOGIC        
W40134*                               FOR MULTIPLE RTV LOCS. REMOVE             
W40134*                               HARDCODED '811'.                          
W40134*                               PRJ ID 3219 - WR40134.                    
      *---------------------------------------------------------------*         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT GENCO-INVENTORY-INPUT    ASSIGN TO INP01.                     
                                                                                
           SELECT INVENTORY-OUTPUT         ASSIGN TO OUT01.                     
                                                                                
           SELECT SUMMARY-REPORT           ASSIGN TO RPT02.                     
                                                                                
           SELECT ERROR-OUTPUT             ASSIGN TO ERR03.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  GENCO-INVENTORY-INPUT                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS  189 CHARACTERS                                      
           DATA RECORD IS RV022-INVENTORY-RCD.                                  
                                                                                
       01  RV022-INVENTORY-RCD             PIC X(189).                          
                                                                                
       FD  INVENTORY-OUTPUT                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS  189 CHARACTERS                                      
           DATA RECORD IS INVENTORY-RECORD-OUT.                                 
                                                                                
       01  INVENTORY-RECORD-OUT            PIC X(189).                          
                                                                                
       FD  SUMMARY-REPORT                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           DATA RECORD IS SUMMARY-REPORT-REC.                                   
                                                                                
       01  SUMMARY-REPORT-REC              PIC X(132).                          
                                                                                
       FD  ERROR-OUTPUT                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           DATA RECORD IS RV025-GENCO-INV-ERROR-RCD.                            
                                                                                
           COPY RVRD025.                                                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  ABEND-CODE               PIC S9(04)  VALUE ZERO                      
                                                COMP SYNC.                      
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-PROGRAM-NAME              PIC X(8) VALUE 'RVKUT024'.          
W40134     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
W40134                                               COMP SYNC.                 
           05  MAX-SUB                      PIC 9(3) VALUE 50.                  
           05  SUB                          PIC 9(3) VALUE 0.                   
           05  SUB2                         PIC 9(3) VALUE 0.                   
           05  PC-EMPTY-FILE                        PIC X(32)                   
               VALUE '*EMPTY PHYSICAL INVENTORY FILE* '.                        
           05  PC-DEPT-NOT-FOUND                    PIC X(32)                   
               VALUE 'DEPT NOT FOUND                  '.                        
           05  PC-BL-NBR-NOT-NUMERIC                PIC X(32)                   
               VALUE 'BILL OF LADING NOT NUMERIC      '.                        
           05  PC-SKU-NOT-NUMERIC                   PIC X(32)                   
               VALUE 'SKU NOT NUMERIC                 '.                        
           05  PC-DEPT-NBR-NOT-NUMERIC              PIC X(32)                   
               VALUE 'DEPT NBR NOT NUMERIC            '.                        
           05  PC-SUB-CL-NBR-NOT-NUMERIC            PIC X(32)                   
               VALUE 'SUB CL NBR NOT NUMERIC          '.                        
           05  PC-UNT-COST-AMT-NOT-NUMERIC          PIC X(32)                   
               VALUE 'UNT COST AMT NOT NUMERIC        '.                        
           05  PC-UNT-QTY-NOT-NUMERIC               PIC X(32)                   
               VALUE 'UNT QTY NOT NUMERIC             '.                        
           05  PC-TRNS-NBR-STR-NOT-NUMERIC          PIC X(32)                   
               VALUE 'TRNS NBR STR NOT NUMERIC        '.                        
           05  PC-TRNS-NBR-NOT-NUMERIC              PIC X(32)                   
               VALUE 'TRNS NBR NOT NUMERIC            '.                        
           05  PC-INVALID-BL-STATUS                 PIC X(32)                   
               VALUE 'INVALID BILL OF LADING STATUS   '.                        
           05  PC-INVALID-COST-SIGN                 PIC X(32)                   
               VALUE 'INVALID COST SIGN               '.                        
           05  PC-INVALID-RTL-AMT-SIGN              PIC X(32)                   
               VALUE 'INVALID RTL AMT SIGN            '.                        
           05  PC-INVALID-UNT-AVAIL-SIGN            PIC X(32)                   
               VALUE 'INVALID UNT AVAIL SIGN          '.                        
           05  PC-INVALID-SCN-DATE                  PIC X(32)                   
               VALUE 'INVALID SCAN DATE               '.                        
           05  PC-INVALID-LOC-ID                    PIC X(32)                   
               VALUE 'INVALID LOC ID                  '.                        
           05  PC-INVALID-DISP                      PIC X(32)                   
               VALUE 'INVALID DISPOSITION             '.                        
           05  PC-INVALID-PUT-AWAY-DATE             PIC X(32)                   
               VALUE 'INVALID PUT AWAY DATE           '.                        
           05  PC-INVALID-RTV-LOC                   PIC X(32)                   
               VALUE 'INVALID RTV LOC                 '.                        
W40134     05  PC-MULTIPLE-RTV-LOC                  PIC X(32)                   
W40134         VALUE 'MULTIPLE RTV LOC                '.                        
           05  PC-INVALID-RECORD-TYPE               PIC X(32)                   
               VALUE 'INVALID RECORD TYPE             '.                        
           05  PC-INVALID-RETURN-TYPE               PIC X(32)                   
               VALUE 'INVALID RETURN TYPE             '.                        
           05  PC-INVALID-UNIT-COST-AMT             PIC X(32)                   
               VALUE 'INVALID UNIT COST AMT        '.                           
      * CONTROL RECORD ERRORS                                                   
           05  PC-CTL-C-COST-AMT-NOT-NUMERIC        PIC X(32)                   
               VALUE 'INVALID CALLBK CTL UNIT COST AMT'.                        
           05  PC-CTL-C-RTL-AMT-NOT-NUMERIC         PIC X(32)                   
               VALUE 'INVALID CALLBK CTL UNIT RTL AMT '.                        
           05  PC-CTL-C-QTY-AMT-NOT-NUMERIC         PIC X(32)                   
               VALUE 'INVALID CALLBK CTL UNIT QTY AMT '.                        
           05  PC-CTL-D-COST-AMT-NOT-NUMERIC        PIC X(32)                   
               VALUE 'INVALID DND CTL UNIT COST AMT'.                           
           05  PC-CTL-D-RTL-AMT-NOT-NUMERIC         PIC X(32)                   
               VALUE 'INVALID DND CTL UNIT RTL AMT '.                           
           05  PC-CTL-D-QTY-AMT-NOT-NUMERIC         PIC X(32)                   
               VALUE 'INVALID DND CTL UNIT QTY AMT '.                           
           05  PC-CTL-M-COST-AMT-NOT-NUMERIC        PIC X(32)                   
               VALUE 'INVALID MOS CTL UNIT COST AMT'.                           
           05  PC-CTL-M-RTL-AMT-NOT-NUMERIC         PIC X(32)                   
               VALUE 'INVALID MOS CTL UNIT RTL AMT '.                           
           05  PC-CTL-M-QTY-AMT-NOT-NUMERIC         PIC X(32)                   
               VALUE 'INVALID MOS CTL UNIT QTY AMT '.                           
           05  PC-CTL-R-COST-AMT-NOT-NUMERIC        PIC X(32)                   
               VALUE 'INVALID RTV CTL UNIT COST AMT'.                           
           05  PC-CTL-R-RTL-AMT-NOT-NUMERIC         PIC X(32)                   
               VALUE 'INVALID RTV CTL UNIT RTL AMT '.                           
           05  PC-CTL-R-QTY-AMT-NOT-NUMERIC         PIC X(32)                   
               VALUE 'INVALID RTV CTL UNIT QTY AMT '.                           
      * TOO MANY CONTROL RECORDS                                                
           05  PC-CTL-COUNT-ERROR                   PIC X(32)                   
               VALUE '*MULT CNTL RECS READ;USED FIRST*'.                        
      * NO CONTROL RECORD                                                       
           05  PC-CTL-NO-CONTROL-REC                PIC X(32)                   
               VALUE '** NO CONTROL RECORD READ **'.                            
      * NO DETAIL RECORD                                                        
           05  PC-DTL-NO-DETAIL-RECS                PIC X(32)                   
               VALUE '** NO DETAIL RECORDS READ **'.                            
      * OUT OF BALANCE                                                          
           05  PC-CTL-OUT-OF-BALANCE                PIC X(32)                   
               VALUE '**  OUT OF BALANCE ERROR   **'.                           
      * ABEND MESSAGES                                                          
           05  PC-ABEND-MSG1                        PIC X(32)                   
               VALUE 'ERROR TABLE MAXIMUM EXCEEDED'.                            
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-ABEND-CODE                PIC S9(4) VALUE +0                  
                                                      COMP SYNC.                
W40134     05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.            
W40134     05  PV-ABEND-TABLE-1             PIC  X(20) VALUE SPACES.            
W40134     05  PV-ABEND-TABLE-2             PIC  X(20) VALUE SPACES.            
W40134     05  PV-ABEND-TABLE-3             PIC  X(20) VALUE SPACES.            
W40134     05  PV-ABEND-TABLE-4             PIC  X(20) VALUE SPACES.            
W40134     05  PV-ABEND-TABLE-5             PIC  X(20) VALUE SPACES.            
W40134     05  PV-ERROR-MSG-1               PIC  X(60) VALUE SPACES.            
W40134     05  PV-ERROR-MSG-2               PIC  X(60) VALUE SPACES.            
W40134     05  PV-ERROR-MSG-3               PIC  X(60) VALUE SPACES.            
W40134     05  PV-RETRY-COUNTER             PIC S9(04) VALUE +0                 
W40134                                                 COMP SYNC.               
W40134     05  PV-KEY-LOC-NBR               PIC S9(04) VALUE +0                 
W40134                                                 COMP SYNC.               
W40134     05  PV-LOC-NBR-CHAR              PIC  X(04).                         
W40134     05  FILLER REDEFINES  PV-LOC-NBR-CHAR.                               
W40134         10  PV-LOC-NBR-NUMERIC       PIC  9(04).                         
W40134                                                                          
           05  PV-DEPT-NM                   PIC X(20).                          
           05  PV-DEPT-NBR                  PIC S9(04)  COMP-3.                 
                                                                                
           05  PV-FIELD-SIGNS               PIC  X(01) VALUE ' '.               
               88  PV-VALID-SIGNS                VALUE ' ', '-'.                
               88  PV-POSITIVE                   VALUE ' '.                     
               88  PV-NEGATIVE                   VALUE '-'.                     
                                                                                
           05  PV-ERROR-TBL        OCCURS 50  TIMES.                            
               10  PV-ERROR-MSG             PIC X(32).                          
               10  PV-ERROR-VALUE           PIC X(15).                          
               10  PV-VALUE-13V2 REDEFINES PV-ERROR-VALUE  PIC 9(13)V99.        
               10  PV-VALUE-9V2  REDEFINES PV-ERROR-VALUE  PIC 9(09)V99.        
               10  PV-VALUE-3    REDEFINES PV-ERROR-VALUE  PIC 9(03).           
                                                                                
           05  PV-DATE-YYYYMMDD                     PIC X(8).                   
           05  FILLER            REDEFINES PV-DATE-YYYYMMDD.                    
               10  PV-DATE-YYYY                     PIC 9(4).                   
               10  PV-DATE-MM                       PIC 9(2).                   
               10  PV-DATE-DD                       PIC 9(2).                   
                                                                                
           05  PV-GREG-DATE.                                                    
               10  PV-GREG-MM                       PIC 9(2).                   
               10  FILLER                           PIC X  VALUE '/'.           
               10  PV-GREG-DD                       PIC 9(2).                   
               10  FILLER                           PIC X  VALUE '/'.           
               10  PV-GREG-YYYY                     PIC 9(4).                   
                                                                                
           05  PV-MMDDYYYY-CHECK         VALUE SPACES.                          
               10  PV-MON                           PIC 9(2).                   
               10  PV-DD                            PIC 9(2).                   
               10  PV-CCYY                          PIC 9(4).                   
               10  FILLER                           PIC X(2).                   
                                                                                
           05  PV-TIME.                                                         
               10  PV-HOUR                          PIC 9(2).                   
               10  PV-MIN                           PIC 9(2).                   
               10  FILLER                           PIC X(4).                   
                                                                                
           05  PV-HR-MN.                                                        
               10  PV-HR                            PIC 9(2).                   
               10  FILLER                           PIC X(1)  VALUE ':'.        
               10  PV-MN                            PIC 9(2).                   
                                                                                
           05  PV-PAGE-COUNT                   PIC  9(5)   VALUE ZERO.          
           05  PV-LINE-COUNT                   PIC  9(02)  VALUE ZERO.          
                                                                                
W40134 01  PROGRAM-HOLDS.                                                       
W40134     05  PH-LOC-NBR                   PIC 9(04).                          
W40134     05  PH-FIRST-LOC-NBR             PIC X(04).                          
W40134     05  PH-LOC-10-ABBR-NM            PIC X(10).                          
W40134                                                                          
       01  PROGRAM-SWITCHES.                                                    
           05  PS-END-OF-FILE-SW                    PIC X  VALUE 'N'.           
               88  PS-END-OF-FILE                          VALUE 'Y'.           
           05  PS-VALID-AMT-SW                      PIC X  VALUE 'N'.           
               88  PS-VALID-AMT                            VALUE 'Y'.           
               88  PS-VALID-AMT-NO                         VALUE 'N'.           
           05  PS-DEPT-FOUND-SW                     PIC X  VALUE 'N'.           
               88  PS-DEPT-FOUND                           VALUE 'Y'.           
               88  PS-DEPT-FOUND-NO                        VALUE 'N'.           
           05  PS-ERROR-SW                          PIC X  VALUE 'N'.           
               88  PS-ERROR                                VALUE 'Y'.           
               88  PS-NO-ERROR                             VALUE 'N'.           
W40134     05  PS-XLT00010-SWITCH                   PIC X  VALUE 'N'.           
W40134         88  PS-XLT00010-FOUND                       VALUE 'Y'.           
W40134         88  PS-XLT00010-NOTFND                      VALUE 'N'.           
                                                                                
       01  PROGRAM-ACCUMULATORS.                                                
           05  PA-DTL-RECS-WRITTEN         PIC S9(13) COMP-3 VALUE 0.           
           05  PA-CTL-RECS-WRITTEN         PIC S9(13) COMP-3 VALUE 0.           
           05  PA-DTL-RECS-READ            PIC S9(13) COMP-3 VALUE 0.           
           05  PA-CTL-RECS-READ            PIC S9(13) COMP-3 VALUE 0.           
           05  PA-DTL-RECS-IN-ERROR        PIC S9(13) COMP-3 VALUE 0.           
                                                                                
      ******************************************************************        
      * HL  HEADER LINES FOR DOCUMENT                                  *        
      ******************************************************************        
       01  HL-BLANK-LINE.                                                       
           05  FILLER                    PIC  X(80)    VALUE SPACES.            
                                                                                
       01  HL01-HEADER-LINE-01.                                                 
           05  FILLER                    PIC  X(47)     VALUE SPACES.           
           05  FILLER                    PIC  X(38)     VALUE                   
               'RTV INVENTORY INTERFACE SUMMARY REPORT'.                        
           05  FILLER                    PIC  X(47)     VALUE SPACES.           
                                                                                
W40134 01  HL02-HEADER-LINE-02.                                                 
W40134     05  FILLER                          PIC X(17)                        
W40134         VALUE 'RTV LOCATION  :  '.                                       
W40134     05  HL02-RTV-LOC-NBR                PIC 9999  VALUE ZEROES.          
W40134     05  FILLER                          PIC X(02) VALUE SPACES.          
W40134     05  HL02-RTV-LOC-NM                 PIC X(10) VALUE SPACES.          
W40134                                                                          
       01  DH01-DETAIL-HEADING-01.                                              
           05  FILLER                    PIC X(27)     VALUE SPACES.            
           05  FILLER                    PIC X(51)     VALUE 'GENCO'.           
           05  FILLER                    PIC X(54)     VALUE 'KOHL''S'.         
                                                                                
       01  DH03-DETAIL-HEADING-03.                                              
           05  FILLER                    PIC X(11)     VALUE SPACES.            
           05  FILLER                    PIC X(121)    VALUE 'RETURN'.          
                                                                                
       01  DH04-DETAIL-HEADING-04.                                              
           05  FILLER                    PIC X(12)     VALUE '  DATE'.          
           05  FILLER                    PIC X(46)     VALUE                    
               'TYPE  TOTAL COUNT       TOTAL COST'.                            
           05  FILLER                    PIC X(57)     VALUE                    
               'TOTAL RETAIL     TOTAL COUNT       TOTAL COST'.                 
           05  FILLER                    PIC X(12)     VALUE                    
               'TOTAL RETAIL'.                                                  
                                                                                
       01  DH05-DETAIL-HEADING-05.                                              
           05  FILLER                    PIC X(132)    VALUE ALL '-'.           
                                                                                
       01  DH06-DETAIL-HEADING-06.                                              
           05  FILLER                    PIC X(34)     VALUE SPACES.            
           05  FILLER                    PIC X(12) VALUE 'DIFFERENCES'.         
                                                                                
       01  DH07-DETAIL-HEADING-07.                                              
           05  FILLER                    PIC X(34)     VALUE SPACES.            
           05  FILLER                    PIC X(12) VALUE '___________'.         
                                                                                
       01  DH09-DETAIL-HEADING-09.                                              
           05  FILLER                    PIC X(11)     VALUE SPACES.            
           05  FILLER                    PIC X(121)    VALUE 'RETURN'.          
                                                                                
       01  DH10-DETAIL-HEADING-10.                                              
           05  FILLER                    PIC X(12)     VALUE '  DATE'.          
           05  FILLER                    PIC X(46)     VALUE                    
               'TYPE  TOTAL COUNT       TOTAL COST'.                            
           05  FILLER                    PIC X(57)     VALUE                    
               'TOTAL RETAIL'.                                                  
                                                                                
       01  DH11-DETAIL-HEADING-11.                                              
           05  FILLER                    PIC X(73)     VALUE ALL '-'.           
                                                                                
       01  DL01-DETAIL-LINE-01.                                                 
           05  DL01-DATE                 PIC X(10).                             
           05  FILLER                    PIC X(02)    VALUE SPACES.             
           05  DL01-RETURN-TYPE          PIC X(03).                             
           05  DL01-GENCO-COUNT          PIC Z,ZZZ,ZZZ,ZZ9-.                    
           05  DL01-GENCO-COST           PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
           05  DL01-GENCO-RETAIL         PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
           05  DL01-KOHLS-COUNT          PIC Z,ZZZ,ZZZ,ZZ9-.                    
           05  DL01-KOHLS-COST           PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
           05  DL01-KOHLS-RETAIL         PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
                                                                                
       01  DL02-DETAIL-LINE-02.                                                 
           05  DL02-DATE                 PIC X(10).                             
           05  FILLER                    PIC X(02)    VALUE SPACES.             
           05  DL02-RETURN-TYPE          PIC X(03).                             
           05  DL02-COUNT-DIF            PIC Z,ZZZ,ZZZ,ZZ9-.                    
           05  DL02-COST-DIF             PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
           05  DL02-RETAIL-DIF           PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
                                                                                
       01  TL01-TOTAL-LINE-01.                                                  
           05  FILLER                    PIC X(15)    VALUE SPACES.             
           05  FILLER                    PIC X(117)   VALUE ALL '_'.            
                                                                                
       01  TL02-TOTAL-LINE-02.                                                  
           05  FILLER                    PIC X(15)    VALUE 'TOTAL'.            
           05  TL02-GENCO-COUNT          PIC Z,ZZZ,ZZZ,ZZ9-.                    
           05  TL02-GENCO-COST           PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
           05  TL02-GENCO-RETAIL         PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
           05  TL02-KOHLS-COUNT          PIC Z,ZZZ,ZZZ,ZZ9-.                    
           05  TL02-KOHLS-COST           PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
           05  TL02-KOHLS-RETAIL         PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
                                                                                
       01  TL03-TOTAL-LINE-03.                                                  
           05  FILLER                    PIC X(15)    VALUE SPACES.             
           05  FILLER                    PIC X(58)    VALUE ALL '_'.            
                                                                                
       01  TL04-TOTAL-LINE-04.                                                  
           05  FILLER                    PIC X(15)    VALUE 'TOTAL'.            
           05  TL04-COUNT-DIF            PIC Z,ZZZ,ZZZ,ZZ9-.                    
           05  TL04-COST-DIF             PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
           05  TL04-RETAIL-DIF           PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.            
                                                                                
W40134 01  END-OF-REPORT-LINE.                                                  
W40134     05  FILLER                          PIC  X(54)  VALUE SPACE.         
W40134     05  FILLER                          PIC  X(25)  VALUE                
W40134         '***** END OF REPORT *****'.                                     
W40134     05  FILLER                          PIC  X(53)  VALUE SPACE.         
W40134                                                                          
      *****************************************************************         
      *   ACCUMULATORS                                                          
      *****************************************************************         
           COPY RVRD026.                                                        
                                                                                
      *****************************************************************         
      *    INPUT RECORD LAYOUT                                                  
      *****************************************************************         
           COPY RVRD022.                                                        
                                                                                
      *****************************************************************         
      *    WORK AREA FOR CALENDAR ROUTINE                                       
      *****************************************************************         
           COPY DPWS500.                                                        
                                                                                
      *****************************************************************         
      *    SQL ABEND ROUTINE WORKING STORAGE                                    
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      ****************************************************************          
      *  LAYOUT FOR THE STANDARD 132 WIDE REPORT HEADER                         
      ****************************************************************          
           COPY DPWS132.                                                        
                                                                                
      *****************************************************************         
      *    DB2 XLT_DEPT = XLT00002                                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE XLT00002                                                 
           END-EXEC.                                                            
                                                                                
W40134*---------------------------------------------------------------*         
W40134*    DCLGEN FOR THE LOGISTICS LOCATION TABLE                    *         
W40134*---------------------------------------------------------------*         
W40134     EXEC SQL                                                             
W40134         INCLUDE XLT00010                                                 
W40134     END-EXEC.                                                            
W40134                                                                          
      *****************************************************************         
      *    DB2 COMMUNICATIONS AREA                                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
       0000-MAINLINE.                                                           
           OPEN INPUT  GENCO-INVENTORY-INPUT                                    
                OUTPUT INVENTORY-OUTPUT                                         
                       SUMMARY-REPORT                                           
                       ERROR-OUTPUT.                                            
                                                                                
           PERFORM 9000-HOUSEKEEPING.                                           
           PERFORM 4000-READ-INVENTORY-FILE                                     
W40134     MOVE RV022-RTV-LOC                 TO PH-FIRST-LOC-NBR.              
                                                                                
           IF NOT PS-END-OF-FILE                                                
               PERFORM 1000-PROCESSING                                          
                  UNTIL PS-END-OF-FILE                                          
           END-IF.                                                              
                                                                                
           PERFORM 5000-FINISH-VALIDATION.                                      
                                                                                
W40134     PERFORM 9700-EOJ-ROUTINE.                                            
W40134                                                                          
       0000-MAINLINE-EXIT.                                                      
           GOBACK.                                                              
                                                                                
      *-----------------------------------------------------------------        
      *   PERFORM THE NECESSARY VALIDATION FOR THE TWO TYPES OF RECORDS         
      *   CONTROLS DETAIL AND CONTROL PROCESSING                                
      *                                                                         
      *-----------------------------------------------------------------        
       1000-PROCESSING.                                                         
                                                                                
           EVALUATE TRUE                                                        
               WHEN RV022-RECORD-TYPE-DETAIL                                    
                   ADD 1                    TO PA-DTL-RECS-READ                 
                   INITIALIZE RV025-GENCO-INV-ERROR-RCD                         
                   PERFORM 1200-DETAIL-FIELD-EDITS                              
                   IF PS-VALID-AMT                                              
                       PERFORM 2000-ACCUM-DETAIL-TOTS                           
                   END-IF                                                       
                   IF SUB > ZERO                                                
                       PERFORM 6200-DETAIL-ERRORS                               
                          VARYING SUB2 FROM 1 BY 1                              
                            UNTIL SUB2 > SUB                                    
                       MOVE ZERO         TO SUB                                 
                   ELSE                                                         
                       ADD 1                TO PA-DTL-RECS-WRITTEN              
                       WRITE INVENTORY-RECORD-OUT                               
                           FROM RV022-INV-DETAIL-RECORD                         
                   END-IF                                                       
               WHEN RV022-RECORD-TYPE-CONTROL                                   
                   IF PA-CTL-RECS-READ = ZERO                                   
                       ADD 1                 TO PA-CTL-RECS-READ                
                       PERFORM 3000-CONTROL-REC-EDITS                           
                       IF SUB = ZERO                                            
                           PERFORM 3200-CONTROL-TOTS                            
                           PERFORM 3300-CALC-DIFFERENCES                        
                           PERFORM 3400-CALC-GRAND-TOTALS                       
                           PERFORM 8000-REPORT-PROCESSING                       
                       END-IF                                                   
                   ELSE                                                         
                       ADD 1                 TO PA-CTL-RECS-READ                
                   END-IF                                                       
               WHEN OTHER                                                       
                   PERFORM 6000-CHECK-ERROR-TABLE                               
                   MOVE PC-INVALID-RECORD-TYPE  TO PV-ERROR-MSG (SUB)           
                   MOVE RV022-RECORD-TYPE       TO PV-ERROR-VALUE (SUB)         
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4000-READ-INVENTORY-FILE.                                    
                                                                                
                                                                                
      ******************************************************************        
      *    EDIT DETAIL RECORDS                                                  
      ******************************************************************        
       1200-DETAIL-FIELD-EDITS.                                                 
           IF RV022-BL-NBR NOT NUMERIC                                          
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-BL-NBR-NOT-NUMERIC     TO PV-ERROR-MSG (SUB)             
               MOVE RV022-BL-NBR              TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
           IF RV022-BL-STATUS = 'D' OR 'X'                                      
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-INVALID-BL-STATUS      TO PV-ERROR-MSG (SUB)             
               MOVE RV022-BL-STATUS           TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
           IF RV022-SKU NOT NUMERIC                                             
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-SKU-NOT-NUMERIC        TO PV-ERROR-MSG (SUB)             
               MOVE RV022-SKU                 TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
           IF RV022-DEPT-NBR NUMERIC                                            
               PERFORM 4500-VALIDATE-DEPT                                       
           ELSE                                                                 
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-DEPT-NBR-NOT-NUMERIC   TO PV-ERROR-MSG (SUB)             
               MOVE RV022-DEPT-NBR            TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
           IF RV022-SUB-CL-NBR NOT NUMERIC                                      
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-SUB-CL-NBR-NOT-NUMERIC TO PV-ERROR-MSG (SUB)             
               MOVE RV022-SUB-CL-NBR          TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
           IF RV022-RETURN-TYPE = 'R' OR 'M' OR 'D' OR 'C'                      
               NEXT SENTENCE                                                    
           ELSE                                                                 
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-INVALID-RETURN-TYPE TO PV-ERROR-MSG (SUB)                
               MOVE RV022-RETURN-TYPE      TO PV-ERROR-VALUE(SUB)               
           END-IF.                                                              
                                                                                
           IF RV022-COST-SIGN = '-' OR SPACE                                    
               NEXT SENTENCE                                                    
           ELSE                                                                 
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-INVALID-COST-SIGN      TO PV-ERROR-MSG (SUB)             
               MOVE RV022-COST-SIGN           TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
           IF RV022-UNT-COST-AMT NOT NUMERIC                                    
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-UNT-COST-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)           
               INITIALIZE PV-ERROR-VALUE (SUB)                                  
               MOVE RV022-UNT-COST-AMT      TO PV-VALUE-9V2 (SUB)               
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-RTL-AMT-SIGN = '-' OR SPACE                                 
               NEXT SENTENCE                                                    
           ELSE                                                                 
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-INVALID-RTL-AMT-SIGN   TO PV-ERROR-MSG (SUB)             
               MOVE RV022-RTL-AMT-SIGN        TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
           IF RV022-UNT-AVAIL-SIGN = '-' OR SPACE                               
               NEXT SENTENCE                                                    
           ELSE                                                                 
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-INVALID-UNT-AVAIL-SIGN TO PV-ERROR-MSG (SUB)             
               MOVE RV022-UNT-AVAIL-SIGN      TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
           IF RV022-UNT-QTY NOT NUMERIC                                         
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-UNT-QTY-NOT-NUMERIC    TO PV-ERROR-MSG (SUB)             
               MOVE RV022-UNT-QTY             TO PV-ERROR-VALUE(SUB)            
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           MOVE RV022-SCN-DATE            TO PV-MMDDYYYY-CHECK                  
           PERFORM 1900-KOHLS-CALENDAR-ROUTINE                                  
           EVALUATE TRUE                                                        
               WHEN RV022-SCN-DATE = SPACES                                     
               WHEN RV022-SCN-DATE (1:2) > 12                                   
               WHEN RV022-SCN-DATE (3:2) > 31                                   
               WHEN RV022-SCN-DATE (5:4) < 2007                                 
               WHEN DPG54-SEVERE-ERROR                                          
               WHEN DPG54-WARNING-ERROR                                         
                   PERFORM 6000-CHECK-ERROR-TABLE                               
                   MOVE PC-INVALID-SCN-DATE   TO PV-ERROR-MSG (SUB)             
                   MOVE RV022-SCN-DATE        TO PV-ERROR-VALUE(SUB)            
           END-EVALUATE.                                                        
                                                                                
           IF RV022-LOC-ID EQUAL SPACES                                         
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-INVALID-LOC-ID         TO PV-ERROR-MSG (SUB)             
               MOVE RV022-LOC-ID              TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
           IF RV022-DISP NUMERIC                                                
           OR RV022-DISP ALPHABETIC                                             
               IF RV022-DISP = SPACES                                           
               OR RV022-DISP = ZERO                                             
                   PERFORM 6000-CHECK-ERROR-TABLE                               
                   MOVE PC-INVALID-DISP           TO PV-ERROR-MSG (SUB)         
                   MOVE RV022-DISP                TO PV-ERROR-VALUE(SUB)        
               END-IF                                                           
           ELSE                                                                 
               IF RV022-DISP = LOW-VALUES                                       
                   PERFORM 6000-CHECK-ERROR-TABLE                               
                   MOVE PC-INVALID-DISP           TO PV-ERROR-MSG (SUB)         
                   MOVE RV022-DISP                TO PV-ERROR-VALUE(SUB)        
           END-IF.                                                              
                                                                                
           MOVE RV022-PUT-AWAY-DATE       TO PV-MMDDYYYY-CHECK                  
           PERFORM 1900-KOHLS-CALENDAR-ROUTINE                                  
           EVALUATE TRUE                                                        
               WHEN RV022-PUT-AWAY-DATE = SPACES                                
               WHEN RV022-PUT-AWAY-DATE (1:2) > 12                              
               WHEN RV022-PUT-AWAY-DATE (3:2) > 31                              
               WHEN RV022-PUT-AWAY-DATE (5:4) < 2007                            
               WHEN DPG54-SEVERE-ERROR                                          
               WHEN DPG54-WARNING-ERROR                                         
                   PERFORM 6000-CHECK-ERROR-TABLE                               
                   MOVE PC-INVALID-PUT-AWAY-DATE TO PV-ERROR-MSG (SUB)          
                   MOVE RV022-PUT-AWAY-DATE   TO PV-ERROR-VALUE(SUB)            
           END-EVALUATE.                                                        
                                                                                
           IF RV022-TRNS-NBR-STR NOT NUMERIC                                    
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-TRNS-NBR-STR-NOT-NUMERIC TO PV-ERROR-MSG (SUB)           
               MOVE RV022-TRNS-NBR-STR        TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
           IF RV022-TRNS-NBR NOT NUMERIC                                        
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-TRNS-NBR-NOT-NUMERIC   TO PV-ERROR-MSG (SUB)             
               MOVE RV022-TRNS-NBR            TO PV-ERROR-VALUE(SUB)            
           END-IF.                                                              
                                                                                
W40134***  FIRST CHECK WILL MATCH LOC TO DB2 LOC TABLE.    *****                
W40134     SET PS-XLT00010-NOTFND             TO TRUE.                          
W40134     MOVE RV022-RTV-LOC                 TO PV-LOC-NBR-CHAR.               
W40134     INSPECT PV-LOC-NBR-CHAR REPLACING LEADING ' ' BY '0'.                
W40134     IF PV-LOC-NBR-NUMERIC IS NUMERIC                                     
W40134         MOVE PV-LOC-NBR-NUMERIC        TO PV-KEY-LOC-NBR                 
W40134         PERFORM 9600-READ-XLT00010                                       
W40134     END-IF.                                                              
W40134***  IF RTV LOC IS NOT FOUND ON THE DB2 LOC TABLE,   *****                
W40134***  ERROR MESSAGE 'INVALID RTV LOC' IS WRITTEN.     *****                
W40134     IF PS-XLT00010-NOTFND                                                
W40134         MOVE SPACES                    TO PH-LOC-10-ABBR-NM              
W40134         PERFORM 6000-CHECK-ERROR-TABLE                                   
W40134         MOVE PC-INVALID-RTV-LOC        TO PV-ERROR-MSG (SUB)             
W40134         MOVE RV022-RTV-LOC             TO PV-ERROR-VALUE(SUB)            
W40134     END-IF.                                                              
                                                                                
W40134***  SECOND CHECK WILL COMPARE ALL DETAIL RECORDS TO FIRST LOC.           
W40134***  ALL LOC NUMBERS SHOULD MATCH OR AN              *****                
W40134***  ERROR MESSAGE 'MULTIPLE RTV LOC' IS WRITTEN.    *****                
W40134     IF RV022-RTV-LOC IS NOT EQUAL TO PH-FIRST-LOC-NBR                    
W40134         PERFORM 6000-CHECK-ERROR-TABLE                                   
W40134         MOVE PC-MULTIPLE-RTV-LOC       TO PV-ERROR-MSG (SUB)             
W40134         MOVE RV022-RTV-LOC             TO PV-ERROR-VALUE(SUB)            
W40134     END-IF.                                                              
W40134                                                                          
      ******************************************************************        
      *    DATE ROUTINE CHECKS FOR VALIDITY                                     
      ******************************************************************        
       1900-KOHLS-CALENDAR-ROUTINE.                                             
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
                                                                                
           SET DPG51-ACTUAL-CALENDAR-ONLY                                       
               DPG52-LK-DTE-ISO-FMT                                             
               DPG54-NO-ERROR             TO TRUE.                              
                                                                                
           SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE                               
           SET DPG52-LK-DTE-GREG-CC       TO TRUE                               
           MOVE PV-MMDDYYYY-CHECK         TO DPG52-GREG-CC-DATE                 
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
                USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
                                                                                
                                                                                
      *****************************************************************         
      *  WHEN THE DETAIL FIELDS HAVE PASSED THE VALIDATION,                     
      *  CALCULATE THE TOTALS BY RETURN TYPE.  IF THE RETURN TYPE               
      *  IS NOT ONE OF THE FOUR LISTED, IT WILL NEVER GET HERE.                 
      *****************************************************************         
       2000-ACCUM-DETAIL-TOTS.                                                  
           EVALUATE RV022-RETURN-TYPE                                           
               WHEN 'R'                                                         
                   PERFORM 2500-ACCUM-RTV-DETAIL                                
               WHEN 'M'                                                         
                   PERFORM 2600-ACCUM-MOS-DETAIL                                
               WHEN 'D'                                                         
                   PERFORM 2700-ACCUM-DND-DETAIL                                
               WHEN 'C'                                                         
                   PERFORM 2800-ACCUM-CBK-DETAIL                                
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
       2500-ACCUM-RTV-DETAIL.                                                   
           MOVE RV022-COST-SIGN TO PV-FIELD-SIGNS                               
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-RTV-TOT-COST                             
                                    = RV026-KOHLS-RTV-TOT-COST                  
                                    + (RV022-UNT-COST-AMT * +1)                 
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-RTV-TOT-COST                             
                                    = RV026-KOHLS-RTV-TOT-COST                  
                                    + (RV022-UNT-COST-AMT * -1)                 
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-RTL-AMT-SIGN TO PV-FIELD-SIGNS                            
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-RTV-TOT-RTL                              
                                    = RV026-KOHLS-RTV-TOT-RTL                   
                                    + (RV022-RTL-AMT * +1)                      
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-RTV-TOT-RTL                              
                                    = RV026-KOHLS-RTV-TOT-RTL                   
                                    + (RV022-RTL-AMT * -1)                      
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-UNT-AVAIL-SIGN TO PV-FIELD-SIGNS                          
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-RTV-TOT-CNT                              
                                    = RV026-KOHLS-RTV-TOT-CNT                   
                                    + (RV022-UNT-QTY * +1)                      
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-RTV-TOT-CNT                              
                                    = RV026-KOHLS-RTV-TOT-CNT                   
                                    + (RV022-UNT-QTY * -1)                      
           END-EVALUATE.                                                        
                                                                                
       2600-ACCUM-MOS-DETAIL.                                                   
           MOVE RV022-COST-SIGN TO PV-FIELD-SIGNS                               
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-MOS-TOT-COST                             
                                    = RV026-KOHLS-MOS-TOT-COST                  
                                    + (RV022-UNT-COST-AMT * +1)                 
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-MOS-TOT-COST                             
                                    = RV026-KOHLS-MOS-TOT-COST                  
                                    + (RV022-UNT-COST-AMT * -1)                 
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-RTL-AMT-SIGN TO PV-FIELD-SIGNS                            
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-MOS-TOT-RTL                              
                                    = RV026-KOHLS-MOS-TOT-RTL                   
                                    + (RV022-RTL-AMT * +1)                      
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-MOS-TOT-RTL                              
                                    = RV026-KOHLS-MOS-TOT-RTL                   
                                    + (RV022-RTL-AMT * -1)                      
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-UNT-AVAIL-SIGN TO PV-FIELD-SIGNS                          
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-MOS-TOT-CNT                              
                                    = RV026-KOHLS-MOS-TOT-CNT                   
                                    + (RV022-UNT-QTY * +1)                      
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-MOS-TOT-CNT                              
                                    = RV026-KOHLS-MOS-TOT-CNT                   
                                    + (RV022-UNT-QTY * -1)                      
           END-EVALUATE.                                                        
                                                                                
       2700-ACCUM-DND-DETAIL.                                                   
           MOVE RV022-COST-SIGN TO PV-FIELD-SIGNS                               
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-DND-TOT-COST                             
                                    = RV026-KOHLS-DND-TOT-COST                  
                                    + (RV022-UNT-COST-AMT * +1)                 
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-DND-TOT-COST                             
                                    = RV026-KOHLS-DND-TOT-COST                  
                                    + (RV022-UNT-COST-AMT * -1)                 
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-RTL-AMT-SIGN TO PV-FIELD-SIGNS                            
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-DND-TOT-RTL                              
                                    = RV026-KOHLS-DND-TOT-RTL                   
                                    + (RV022-RTL-AMT * +1)                      
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-DND-TOT-RTL                              
                                    = RV026-KOHLS-DND-TOT-RTL                   
                                    + (RV022-RTL-AMT * -1)                      
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-UNT-AVAIL-SIGN TO PV-FIELD-SIGNS                          
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-DND-TOT-CNT                              
                                    = RV026-KOHLS-DND-TOT-CNT                   
                                    + (RV022-UNT-QTY * +1)                      
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-DND-TOT-CNT                              
                                    = RV026-KOHLS-DND-TOT-CNT                   
                                    + (RV022-UNT-QTY * -1)                      
           END-EVALUATE.                                                        
                                                                                
       2800-ACCUM-CBK-DETAIL.                                                   
           MOVE RV022-COST-SIGN TO PV-FIELD-SIGNS                               
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-CBK-TOT-COST                             
                                    = RV026-KOHLS-CBK-TOT-COST                  
                                    + (RV022-UNT-COST-AMT * +1)                 
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-CBK-TOT-COST                             
                                    = RV026-KOHLS-CBK-TOT-COST                  
                                    + (RV022-UNT-COST-AMT * -1)                 
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-RTL-AMT-SIGN TO PV-FIELD-SIGNS                            
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-CBK-TOT-RTL                              
                                    = RV026-KOHLS-CBK-TOT-RTL                   
                                    + (RV022-RTL-AMT * +1)                      
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-CBK-TOT-RTL                              
                                    = RV026-KOHLS-CBK-TOT-RTL                   
                                    + (RV022-RTL-AMT * -1)                      
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-UNT-AVAIL-SIGN TO PV-FIELD-SIGNS                          
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-KOHLS-CBK-TOT-CNT                              
                                    = RV026-KOHLS-CBK-TOT-CNT                   
                                    + (RV022-UNT-QTY * +1)                      
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-KOHLS-CBK-TOT-CNT                              
                                    = RV026-KOHLS-CBK-TOT-CNT                   
                                    + (RV022-UNT-QTY * -1)                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  VALIDATE THE CONTROL RECORD.  THERE SHOULD ONLY BE ONE.                
      ******************************************************************        
       3000-CONTROL-REC-EDITS.                                                  
                                                                                
           IF RV022-CBK-UNIT-COST-AMT NOT NUMERIC                               
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-C-COST-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)         
               MOVE RV022-CBK-UNIT-COST-AMT TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-CBK-UNIT-RTL-AMT NOT NUMERIC                                
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-C-RTL-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)          
               MOVE RV022-CBK-UNIT-RTL-AMT  TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-CBK-UNIT-QTY-AMT NOT NUMERIC                                
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-C-QTY-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)          
               MOVE RV022-CBK-UNIT-QTY-AMT  TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-DND-UNIT-COST-AMT NOT NUMERIC                               
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-D-COST-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)         
               MOVE RV022-DND-UNIT-COST-AMT TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-DND-UNIT-RTL-AMT NOT NUMERIC                                
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-D-RTL-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)          
               MOVE RV022-DND-UNIT-RTL-AMT  TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-DND-UNIT-QTY-AMT NOT NUMERIC                                
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-D-QTY-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)          
               MOVE RV022-DND-UNIT-QTY-AMT  TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-MOS-UNIT-COST-AMT NOT NUMERIC                               
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-M-COST-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)         
               MOVE RV022-MOS-UNIT-COST-AMT TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-MOS-UNIT-RTL-AMT NOT NUMERIC                                
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-M-RTL-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)          
               MOVE RV022-MOS-UNIT-RTL-AMT  TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-MOS-UNIT-QTY-AMT NOT NUMERIC                                
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-M-QTY-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)          
               MOVE RV022-MOS-UNIT-QTY-AMT  TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-RTV-UNIT-COST-AMT NOT NUMERIC                               
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-R-COST-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)         
               MOVE RV022-RTV-UNIT-COST-AMT TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-RTV-UNIT-RTL-AMT NOT NUMERIC                                
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-R-RTL-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)          
               MOVE RV022-RTV-UNIT-RTL-AMT  TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
           IF RV022-RTV-UNIT-QTY-AMT NOT NUMERIC                                
               PERFORM 6000-CHECK-ERROR-TABLE                                   
               MOVE PC-CTL-R-QTY-AMT-NOT-NUMERIC TO PV-ERROR-MSG (SUB)          
               MOVE RV022-RTV-UNIT-QTY-AMT  TO PV-VALUE-13V2 (SUB)              
               SET PS-VALID-AMT-NO          TO TRUE                             
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------         
      *  STORE FIELDS AS SIGNED NUMERICS                                        
      *  COMPARE THE GENCO TOTALS WITH THE CALCULATED TOTALS AND                
      *  PRODUCE AN ERROR IF THEY ARE NOT EQUAL.                                
      *----------------------------------------------------------------         
       3200-CONTROL-TOTS.                                                       
                                                                                
      ** RTV TOTALS                                                             
                                                                                
           MOVE RV022-RTV-UNIT-COST-SIGN TO PV-FIELD-SIGNS                      
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-RTV-TOT-COST                             
                                    = (RV022-RTV-UNIT-COST-AMT * +1)            
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-RTV-TOT-COST                             
                                    = (RV022-RTV-UNIT-COST-AMT * -1)            
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-RTV-UNIT-RTL-SIGN TO PV-FIELD-SIGNS                       
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-RTV-TOT-RTL                              
                                    = (RV022-RTV-UNIT-RTL-AMT * +1)             
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-RTV-TOT-RTL                              
                                    = (RV022-RTV-UNIT-RTL-AMT * -1)             
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-RTV-UNIT-QTY-SIGN TO PV-FIELD-SIGNS                       
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-RTV-TOT-CNT                              
                                    = (RV022-RTV-UNIT-QTY-AMT * +1)             
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-RTV-TOT-CNT                              
                                    = (RV022-RTV-UNIT-QTY-AMT * -1)             
           END-EVALUATE.                                                        
                                                                                
      ** MOS TOTALS                                                             
                                                                                
           MOVE RV022-MOS-UNIT-COST-SIGN TO PV-FIELD-SIGNS                      
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-MOS-TOT-COST                             
                                    = (RV022-MOS-UNIT-COST-AMT * +1)            
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-MOS-TOT-COST                             
                                    = (RV022-MOS-UNIT-COST-AMT * -1)            
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-MOS-UNIT-RTL-SIGN TO PV-FIELD-SIGNS                       
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-MOS-TOT-RTL                              
                                    = (RV022-MOS-UNIT-RTL-AMT * +1)             
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-MOS-TOT-RTL                              
                                    = (RV022-MOS-UNIT-RTL-AMT * -1)             
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-MOS-UNIT-QTY-SIGN TO PV-FIELD-SIGNS                       
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-MOS-TOT-CNT                              
                                    = (RV022-MOS-UNIT-QTY-AMT * +1)             
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-MOS-TOT-CNT                              
                                    = (RV022-MOS-UNIT-QTY-AMT * -1)             
           END-EVALUATE.                                                        
                                                                                
      ** DND TOTALS                                                             
                                                                                
           MOVE RV022-DND-UNIT-COST-SIGN TO PV-FIELD-SIGNS                      
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-DND-TOT-COST                             
                                    = (RV022-DND-UNIT-COST-AMT * +1)            
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-DND-TOT-COST                             
                                    = (RV022-DND-UNIT-COST-AMT * -1)            
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-DND-UNIT-RTL-SIGN TO PV-FIELD-SIGNS                       
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-DND-TOT-RTL                              
                                    = (RV022-DND-UNIT-RTL-AMT * +1)             
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-DND-TOT-RTL                              
                                    = (RV022-DND-UNIT-RTL-AMT * -1)             
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-DND-UNIT-QTY-SIGN TO PV-FIELD-SIGNS                       
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-DND-TOT-CNT                              
                                    = (RV022-DND-UNIT-QTY-AMT * +1)             
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-DND-TOT-CNT                              
                                    = (RV022-DND-UNIT-QTY-AMT * -1)             
           END-EVALUATE.                                                        
                                                                                
      ** CALL BACK (CB) TOTALS                                                  
                                                                                
           MOVE RV022-CBK-UNIT-COST-SIGN TO PV-FIELD-SIGNS                      
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-CBK-TOT-COST                             
                                    = (RV022-CBK-UNIT-COST-AMT * +1)            
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-CBK-TOT-COST                             
                                    = (RV022-CBK-UNIT-COST-AMT * -1)            
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-CBK-UNIT-RTL-SIGN TO PV-FIELD-SIGNS                       
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-CBK-TOT-RTL                              
                                    = (RV022-CBK-UNIT-RTL-AMT * +1)             
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-CBK-TOT-RTL                              
                                    = (RV022-CBK-UNIT-RTL-AMT * -1)             
           END-EVALUATE.                                                        
                                                                                
           MOVE RV022-CBK-UNIT-QTY-SIGN TO PV-FIELD-SIGNS                       
           EVALUATE TRUE                                                        
               WHEN PV-POSITIVE                                                 
                   COMPUTE RV026-GENCO-CBK-TOT-CNT                              
                                    = (RV022-CBK-UNIT-QTY-AMT * +1)             
               WHEN PV-NEGATIVE                                                 
                   COMPUTE RV026-GENCO-CBK-TOT-CNT                              
                                    = (RV022-CBK-UNIT-QTY-AMT * -1)             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------         
      *  CALCULATE DIFFERENCES BETWEEN THE GENCO AND KOHLS                      
      *----------------------------------------------------------------         
       3300-CALC-DIFFERENCES.                                                   
           COMPUTE RV026-RTV-TOT-COST-DIF =                                     
               RV026-GENCO-RTV-TOT-COST                                         
               - RV026-KOHLS-RTV-TOT-COST.                                      
                                                                                
           COMPUTE RV026-RTV-TOT-RTL-DIF =                                      
               RV026-GENCO-RTV-TOT-RTL                                          
               - RV026-KOHLS-RTV-TOT-RTL .                                      
                                                                                
           COMPUTE RV026-RTV-TOT-CNT-DIF =                                      
               RV026-GENCO-RTV-TOT-CNT                                          
               - RV026-KOHLS-RTV-TOT-CNT .                                      
                                                                                
           COMPUTE RV026-MOS-TOT-COST-DIF =                                     
               RV026-GENCO-MOS-TOT-COST                                         
               - RV026-KOHLS-MOS-TOT-COST.                                      
                                                                                
           COMPUTE RV026-MOS-TOT-RTL-DIF =                                      
               RV026-GENCO-MOS-TOT-RTL                                          
               - RV026-KOHLS-MOS-TOT-RTL .                                      
                                                                                
           COMPUTE RV026-MOS-TOT-CNT-DIF =                                      
               RV026-GENCO-MOS-TOT-CNT                                          
               - RV026-KOHLS-MOS-TOT-CNT .                                      
                                                                                
           COMPUTE RV026-DND-TOT-COST-DIF =                                     
               RV026-GENCO-DND-TOT-COST                                         
               - RV026-KOHLS-DND-TOT-COST.                                      
                                                                                
           COMPUTE RV026-DND-TOT-RTL-DIF =                                      
               RV026-GENCO-DND-TOT-RTL                                          
               - RV026-KOHLS-DND-TOT-RTL .                                      
                                                                                
           COMPUTE RV026-DND-TOT-CNT-DIF =                                      
               RV026-GENCO-DND-TOT-CNT                                          
               - RV026-KOHLS-DND-TOT-CNT .                                      
                                                                                
           COMPUTE RV026-CBK-TOT-COST-DIF =                                     
               RV026-GENCO-CBK-TOT-COST                                         
               - RV026-KOHLS-CBK-TOT-COST.                                      
                                                                                
           COMPUTE RV026-CBK-TOT-RTL-DIF =                                      
               RV026-GENCO-CBK-TOT-RTL                                          
               - RV026-KOHLS-CBK-TOT-RTL .                                      
                                                                                
           COMPUTE RV026-CBK-TOT-CNT-DIF =                                      
               RV026-GENCO-CBK-TOT-CNT                                          
               - RV026-KOHLS-CBK-TOT-CNT .                                      
                                                                                
      *----------------------------------------------------------------         
      *  CALCULATE GRAND TOTALS                                                 
      *----------------------------------------------------------------         
       3400-CALC-GRAND-TOTALS.                                                  
                                                                                
           COMPUTE RV026-GENCO-GRAND-TOT-CNT =                                  
                   RV026-GENCO-MOS-TOT-CNT                                      
                 + RV026-GENCO-RTV-TOT-CNT                                      
                 + RV026-GENCO-DND-TOT-CNT                                      
                 + RV026-GENCO-CBK-TOT-CNT                                      
                                                                                
           COMPUTE RV026-GENCO-GRAND-TOT-RTL =                                  
                   RV026-GENCO-MOS-TOT-RTL                                      
                 + RV026-GENCO-RTV-TOT-RTL                                      
                 + RV026-GENCO-DND-TOT-RTL                                      
                 + RV026-GENCO-CBK-TOT-RTL                                      
                                                                                
           COMPUTE RV026-GENCO-GRAND-TOT-COST =                                 
                   RV026-GENCO-MOS-TOT-COST                                     
                 + RV026-GENCO-RTV-TOT-COST                                     
                 + RV026-GENCO-DND-TOT-COST                                     
                 + RV026-GENCO-CBK-TOT-COST                                     
                                                                                
           COMPUTE RV026-KOHLS-GRAND-TOT-CNT =                                  
                   RV026-KOHLS-MOS-TOT-CNT                                      
                 + RV026-KOHLS-RTV-TOT-CNT                                      
                 + RV026-KOHLS-DND-TOT-CNT                                      
                 + RV026-KOHLS-CBK-TOT-CNT                                      
                                                                                
           COMPUTE RV026-KOHLS-GRAND-TOT-RTL =                                  
                   RV026-KOHLS-MOS-TOT-RTL                                      
                 + RV026-KOHLS-RTV-TOT-RTL                                      
                 + RV026-KOHLS-DND-TOT-RTL                                      
                 + RV026-KOHLS-CBK-TOT-RTL                                      
                                                                                
           COMPUTE RV026-KOHLS-GRAND-TOT-COST =                                 
                   RV026-KOHLS-MOS-TOT-COST                                     
                 + RV026-KOHLS-RTV-TOT-COST                                     
                 + RV026-KOHLS-DND-TOT-COST                                     
                 + RV026-KOHLS-CBK-TOT-COST                                     
                                                                                
      *-----------------------------------------------------------------        
      *  CALCULATE GRAND TOTAL DIFFERENCES                                      
      *-----------------------------------------------------------------        
           COMPUTE RV026-GRAND-TOT-CNT-DIF =                                    
                   RV026-MOS-TOT-CNT-DIF                                        
                 + RV026-RTV-TOT-CNT-DIF                                        
                 + RV026-DND-TOT-CNT-DIF                                        
                 + RV026-CBK-TOT-CNT-DIF                                        
                                                                                
           COMPUTE RV026-GRAND-TOT-RTL-DIF =                                    
                   RV026-MOS-TOT-RTL-DIF                                        
                 + RV026-RTV-TOT-RTL-DIF                                        
                 + RV026-DND-TOT-RTL-DIF                                        
                 + RV026-CBK-TOT-RTL-DIF                                        
                                                                                
           COMPUTE RV026-GRAND-TOT-COST-DIF =                                   
                   RV026-MOS-TOT-COST-DIF                                       
                 + RV026-RTV-TOT-COST-DIF                                       
                 + RV026-DND-TOT-COST-DIF                                       
                 + RV026-CBK-TOT-COST-DIF.                                      
                                                                                
      ******************************************************************        
      *  READ INPUT FILE AND RESET INDEX TO ERROR TABLE                         
      ******************************************************************        
       4000-READ-INVENTORY-FILE.                                                
           IF NOT PS-END-OF-FILE                                                
               READ GENCO-INVENTORY-INPUT                                       
                   INTO RV022-INVENTORY-RECORD                                  
                   AT END                                                       
                       SET PS-END-OF-FILE TO TRUE                               
           END-IF.                                                              
                                                                                
           MOVE ZERO                   TO SUB.                                  
           SET PS-VALID-AMT            TO TRUE.                                 
      ******************************************************************        
      *  LOOK UP THE DEPT NUMBER ON THE DB2 TABLE                               
      ******************************************************************        
       4500-VALIDATE-DEPT.                                                      
           MOVE RV022-DEPT-NBR             TO PV-DEPT-NBR.                      
                                                                                
           EXEC SQL                                                             
              SELECT DEPT_NM                                                    
                 INTO :PV-DEPT-NM                                               
                 FROM XLT_DEPT                                                  
                WHERE DEPT_NBR = :PV-DEPT-NBR                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = -811                                              
                   CONTINUE                                                     
                                                                                
               WHEN SQLCODE =  100                                              
                   DISPLAY '4500-VALIDATE-DEPT' PV-DEPT-NBR                     
                   PERFORM 6000-CHECK-ERROR-TABLE                               
                   MOVE PC-DEPT-NOT-FOUND  TO PV-ERROR-MSG  (SUB)               
                   INITIALIZE PV-ERROR-VALUE (SUB)                              
                   MOVE RV022-DEPT-NBR   TO PV-VALUE-3 (SUB)                    
      *            PERFORM 6200-DETAIL-ERRORS                                   
      *                VARYING SUB2 FROM 1 BY 1                                 
      *                  UNTIL SUB2 GREATER THAN SUB                            
                                                                                
               WHEN OTHER                                                       
                   PERFORM 9999-DB2-ABEND                                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------         
      *  SHOULD HAVE ONE CONTROL RECORD AND MANY DETAIL RECORDS                 
      *  OTHERWISE THE FILE IS INVALID                                          
      *----------------------------------------------------------------         
       5000-FINISH-VALIDATION.                                                  
           EVALUATE TRUE                                                        
               WHEN PA-DTL-RECS-READ > ZERO                                     
                    AND PA-CTL-RECS-READ = ZERO                                 
                   PERFORM 5200-NO-CTL-REC                                      
                                                                                
               WHEN PA-DTL-RECS-READ = ZERO                                     
                    AND PA-CTL-RECS-READ = ZERO                                 
                   PERFORM 5400-NO-DTL-RECS                                     
                                                                                
               WHEN PA-CTL-RECS-READ = 1                                        
                    AND PA-DTL-RECS-READ = ZERO                                 
                   PERFORM 5600-HAVE-CTL-REC                                    
                                                                                
               WHEN PA-CTL-RECS-READ = 1                                        
                    AND PA-DTL-RECS-READ > ZERO                                 
                   PERFORM 5800-COMPARE-GRAND-TOTS                              
                                                                                
               WHEN PA-CTL-RECS-READ > 1                                        
                   PERFORM 6000-CHECK-ERROR-TABLE                               
                   MOVE PC-CTL-COUNT-ERROR    TO PV-ERROR-MSG (SUB)             
                   INITIALIZE PV-ERROR-VALUE (SUB)                              
                   MOVE PA-CTL-RECS-READ      TO PV-VALUE-3 (SUB)               
                                                                                
               WHEN OTHER                                                       
                  CONTINUE                                                      
           END-EVALUATE.                                                        
                                                                                
           PERFORM 6500-CONTROL-ERRORS                                          
              VARYING SUB2 FROM 1 BY 1                                          
                UNTIL SUB2 > SUB.                                               
      *-----------------------------------------------------------------        
      *   IF DETAIL RECORDS WERE READ BUT NO CONTROL RECORD,                    
      *   THE FILE IS INVALID                                                   
      *-----------------------------------------------------------------        
       5200-NO-CTL-REC.                                                         
           PERFORM 6000-CHECK-ERROR-TABLE                                       
           MOVE SPACES                TO PV-ERROR-VALUE(SUB)                    
           MOVE PC-CTL-NO-CONTROL-REC TO PV-ERROR-MSG (SUB)                     
           PERFORM 3300-CALC-DIFFERENCES                                        
           PERFORM 3400-CALC-GRAND-TOTALS                                       
           PERFORM 8000-REPORT-PROCESSING.                                      
                                                                                
      *-----------------------------------------------------------------        
      *   IF NO DETAIL RECORDS READ.                                            
      *   THE FILE IS INVALID                                                   
      *-----------------------------------------------------------------        
       5400-NO-DTL-RECS.                                                        
                                                                                
           INITIALIZE RV022-INVENTORY-RECORD                                    
           PERFORM 6000-CHECK-ERROR-TABLE                                       
           MOVE PC-EMPTY-FILE     TO PV-ERROR-MSG (SUB)                         
           MOVE SPACES            TO PV-ERROR-VALUE(SUB)                        
           PERFORM 8000-REPORT-PROCESSING.                                      
                                                                                
      *-----------------------------------------------------------------        
      *   IF A CONTROL RECORD WAS READ BUT NO DETAIL RECORDS,                   
      *   THE FILE IS INVALID                                                   
      *-----------------------------------------------------------------        
       5600-HAVE-CTL-REC.                                                       
           INITIALIZE RV022-INVENTORY-RECORD                                    
           PERFORM 6000-CHECK-ERROR-TABLE                                       
           MOVE SPACES                TO PV-ERROR-VALUE(SUB)                    
           MOVE PC-DTL-NO-DETAIL-RECS TO PV-ERROR-MSG (SUB).                    
                                                                                
      ******************************************************************        
      *     IF THE GRAND TOTAL DIFFERENCES ARE ZERO, SUCCESS           *        
      *     IF NOT SET THE RETURN CODE TO 4095.  THE NEXT STEP IN      *        
      *     THE JOB WILL NOTIFY THE APPROPIATE PERSON BY EMAIL.        *        
      ******************************************************************        
       5800-COMPARE-GRAND-TOTS.                                                 
                                                                                
           IF  RV026-GRAND-TOT-CNT-DIF  = ZERO                                  
           AND RV026-GRAND-TOT-COST-DIF = ZERO                                  
           AND RV026-GRAND-TOT-RTL-DIF  = ZERO                                  
               DISPLAY '** SUUCCESSFUL COMPLETION OF RVKUT024 **'               
               DISPLAY '** DETAIL RECORDS READ      '                           
                   PA-DTL-RECS-READ ' **'                                       
               DISPLAY '** CONTROL RECORDS READ     '                           
                   PA-CTL-RECS-READ ' **'                                       
               DISPLAY '** TOT DETAIL RECS WRITTEN  '                           
                   PA-DTL-RECS-WRITTEN ' **'                                    
               DISPLAY '** TOT CONTROL RECS WRITTEN '                           
                   PA-CTL-RECS-WRITTEN ' **'                                    
           ELSE                                                                 
               INITIALIZE RV025-GENCO-INV-ERROR-RCD                             
      *        PERFORM 6000-CHECK-ERROR-TABLE                                   
      *        MOVE PC-CTL-OUT-OF-BALANCE  TO PV-ERROR-MSG (SUB)                
      *        INITIALIZE PV-ERROR-VALUE (SUB)                                  
      *        PERFORM 6500-CONTROL-ERRORS                                      
      *           VARYING SUB2 FROM 1 BY 1                                      
      *             UNTIL SUB2 > SUB                                            
               MOVE 4095         TO RETURN-CODE                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  MAKE SURE THE TABLE MAXIMUM IS NOT EXCEEDED                            
      ******************************************************************        
       6000-CHECK-ERROR-TABLE.                                                  
           IF SUB <  MAX-SUB                                                    
               ADD 1               TO SUB                                       
           ELSE                                                                 
               DISPLAY  PC-ABEND-MSG1                                           
               PERFORM 9800-NON-DB2-ABEND                                       
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE ALL THE ERRORS FOR EACH DETAIL RECORD                            
      ******************************************************************        
       6200-DETAIL-ERRORS.                                                      
           INITIALIZE RV025-GENCO-INV-ERROR-RCD.                                
           MOVE RV022-BL-NBR           TO RV025-BL-NBR                          
           MOVE RV022-RETURN-TYPE      TO RV025-RETURN-TYPE                     
           MOVE RV022-DEPT-NBR         TO RV025-DEPT                            
           MOVE RV022-SUB-CL-NBR       TO RV025-SUB-CLASS                       
           MOVE RV022-SKU              TO RV025-SKU-NBR                         
W40134     MOVE PH-LOC-NBR             TO RV025-LOC-NBR.                        
W40134     MOVE PH-LOC-10-ABBR-NM      TO RV025-LOC-10-ABBR-NM.                 
                                                                                
           IF SUB2 = 1                                                          
               MOVE 1                      TO RV025-UNT-QTY                     
               MOVE RV022-RTL-AMT          TO RV025-RTL-AMT                     
               MOVE RV022-UNT-COST-AMT     TO RV025-COST-AMT                    
           ELSE                                                                 
               MOVE ZERO                   TO RV025-UNT-QTY                     
               MOVE ZEROS                  TO RV025-RTL-AMT                     
               MOVE ZEROS                  TO RV025-COST-AMT                    
           END-IF.                                                              
                                                                                
           MOVE PV-ERROR-MSG (SUB2)        TO RV025-ERROR-MSG                   
           MOVE PV-ERROR-VALUE (SUB2)      TO RV025-ERROR-VALUE.                
                                                                                
W40134     SET PS-ERROR                    TO TRUE.                             
           WRITE RV025-GENCO-INV-ERROR-RCD.                                     
                                                                                
      ******************************************************************        
      *  WRITE ALL THE ERRORS FOR EACH CONTROL RECORD                           
      ******************************************************************        
       6500-CONTROL-ERRORS.                                                     
           INITIALIZE RV025-GENCO-INV-ERROR-RCD.                                
                                                                                
W40134     MOVE PH-LOC-NBR                 TO RV025-LOC-NBR.                    
W40134     MOVE PH-LOC-10-ABBR-NM          TO RV025-LOC-10-ABBR-NM.             
W40134                                                                          
           MOVE PV-ERROR-MSG (SUB2)        TO RV025-ERROR-MSG                   
           MOVE PV-ERROR-VALUE (SUB2)      TO RV025-ERROR-VALUE.                
                                                                                
W40134     SET PS-ERROR                    TO TRUE.                             
           WRITE RV025-GENCO-INV-ERROR-RCD.                                     
                                                                                
W40134******************************************************************        
W40134*  WRITE AN ERROR RECORD FOR AN EMPTY ERROR FILE.                         
W40134*  RV025-RECORD-TYPE-IND = '0', INDICATES AN EMPTY FILE.                  
W40134******************************************************************        
       6600-EMPTY-FILE-RECORD.                                                  
W40134     INITIALIZE RV025-GENCO-INV-ERROR-RCD.                                
W40134     MOVE PH-LOC-NBR             TO RV025-LOC-NBR.                        
W40134     MOVE PH-LOC-10-ABBR-NM      TO RV025-LOC-10-ABBR-NM.                 
W40134     MOVE '0'                    TO RV025-RECORD-TYPE-IND.                
                                                                                
W40134     WRITE RV025-GENCO-INV-ERROR-RCD.                                     
                                                                                
      ******************************************************************        
      *  PERFORM PARAGRAPHS THAT WRITE THE SUMMARY REPORT                       
      ******************************************************************        
       8000-REPORT-PROCESSING.                                                  
           PERFORM 8200-PRINT-PAGE-HEADER                                       
           PERFORM 8400-PRINT-DETAIL                                            
           PERFORM 8600-DIFFERENCES-HDRS                                        
           PERFORM 8800-DETAIL-DIFFS.                                           
                                                                                
      ******************************************************************        
      *     WRITES HEADER LINES FOR THE DETAIL                         *        
      ******************************************************************        
       8200-PRINT-PAGE-HEADER.                                                  
           ADD 1                        TO PV-PAGE-COUNT.                       
           MOVE PV-PAGE-COUNT           TO DP132-PAGE-NUMBER.                   
           MOVE PV-GREG-DATE            TO DP132-RUN-DATE.                      
           MOVE PV-HR-MN                TO DP132-RUN-TIME.                      
           MOVE PC-PROGRAM-NAME         TO DP132-PROGRAM-NAME.                  
           MOVE 1                       TO DP132-REPORT-NUMBER.                 
                                                                                
           WRITE SUMMARY-REPORT-REC FROM DP132-STANDRD-HEADER-132-COLS          
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE SUMMARY-REPORT-REC FROM HL01-HEADER-LINE-01                    
               AFTER ADVANCING 1 LINES.                                         
                                                                                
W40134     MOVE PH-LOC-NBR              TO HL02-RTV-LOC-NBR.                    
W40134     MOVE PH-LOC-10-ABBR-NM       TO HL02-RTV-LOC-NM.                     
W40134     WRITE SUMMARY-REPORT-REC FROM HL02-HEADER-LINE-02                    
W40134         AFTER ADVANCING 1 LINES.                                         
W40134                                                                          
           WRITE SUMMARY-REPORT-REC FROM DH01-DETAIL-HEADING-01                 
               AFTER ADVANCING 3 LINES.                                         
                                                                                
           WRITE SUMMARY-REPORT-REC FROM DH03-DETAIL-HEADING-03                 
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           WRITE SUMMARY-REPORT-REC FROM DH04-DETAIL-HEADING-04                 
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           WRITE SUMMARY-REPORT-REC FROM DH05-DETAIL-HEADING-05                 
               AFTER ADVANCING 1 LINES.                                         
                                                                                
      ******************************************************************        
      *     WRITES THE DETAIL LINE                                     *        
      ******************************************************************        
       8400-PRINT-DETAIL.                                                       
           ADD 1          TO  PV-LINE-COUNT.                                    
                                                                                
           IF PV-LINE-COUNT > 60                                                
               PERFORM 8200-PRINT-PAGE-HEADER                                   
               MOVE 1     TO PV-LINE-COUNT                                      
           END-IF.                                                              
                                                                                
           MOVE 'MOS'                    TO DL01-RETURN-TYPE.                   
           MOVE PV-GREG-DATE             TO DL01-DATE.                          
           MOVE RV026-GENCO-MOS-TOT-CNT  TO DL01-GENCO-COUNT                    
           MOVE RV026-GENCO-MOS-TOT-COST TO DL01-GENCO-COST                     
           MOVE RV026-GENCO-MOS-TOT-RTL  TO DL01-GENCO-RETAIL.                  
           MOVE RV026-KOHLS-MOS-TOT-CNT  TO DL01-KOHLS-COUNT                    
           MOVE RV026-KOHLS-MOS-TOT-COST TO DL01-KOHLS-COST                     
           MOVE RV026-KOHLS-MOS-TOT-RTL  TO DL01-KOHLS-RETAIL.                  
           WRITE SUMMARY-REPORT-REC FROM DL01-DETAIL-LINE-01                    
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 'RTV'                    TO DL01-RETURN-TYPE.                   
           MOVE SPACES                   TO DL01-DATE.                          
           MOVE RV026-GENCO-RTV-TOT-CNT  TO DL01-GENCO-COUNT                    
           MOVE RV026-GENCO-RTV-TOT-COST TO DL01-GENCO-COST                     
           MOVE RV026-GENCO-RTV-TOT-RTL  TO DL01-GENCO-RETAIL.                  
           MOVE RV026-KOHLS-RTV-TOT-CNT  TO DL01-KOHLS-COUNT                    
           MOVE RV026-KOHLS-RTV-TOT-COST TO DL01-KOHLS-COST                     
           MOVE RV026-KOHLS-RTV-TOT-RTL  TO DL01-KOHLS-RETAIL.                  
           WRITE SUMMARY-REPORT-REC FROM DL01-DETAIL-LINE-01                    
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 'DND'                    TO DL01-RETURN-TYPE.                   
           MOVE RV026-GENCO-DND-TOT-CNT  TO DL01-GENCO-COUNT                    
           MOVE RV026-GENCO-DND-TOT-COST TO DL01-GENCO-COST                     
           MOVE RV026-GENCO-DND-TOT-RTL  TO DL01-GENCO-RETAIL.                  
           MOVE RV026-KOHLS-DND-TOT-CNT  TO DL01-KOHLS-COUNT                    
           MOVE RV026-KOHLS-DND-TOT-COST TO DL01-KOHLS-COST                     
           MOVE RV026-KOHLS-DND-TOT-RTL  TO DL01-KOHLS-RETAIL.                  
           WRITE SUMMARY-REPORT-REC FROM DL01-DETAIL-LINE-01                    
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 'CB'                     TO DL01-RETURN-TYPE.                   
           MOVE RV026-GENCO-CBK-TOT-CNT  TO DL01-GENCO-COUNT                    
           MOVE RV026-GENCO-CBK-TOT-COST TO DL01-GENCO-COST                     
           MOVE RV026-GENCO-CBK-TOT-RTL  TO DL01-GENCO-RETAIL.                  
           MOVE RV026-KOHLS-CBK-TOT-CNT  TO DL01-KOHLS-COUNT                    
           MOVE RV026-KOHLS-CBK-TOT-COST TO DL01-KOHLS-COST                     
           MOVE RV026-KOHLS-CBK-TOT-RTL  TO DL01-KOHLS-RETAIL.                  
           WRITE SUMMARY-REPORT-REC FROM DL01-DETAIL-LINE-01                    
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE SUMMARY-REPORT-REC FROM TL01-TOTAL-LINE-01                     
               AFTER ADVANCING 1 LINE.                                          
           MOVE RV026-GENCO-GRAND-TOT-CNT  TO TL02-GENCO-COUNT                  
           MOVE RV026-GENCO-GRAND-TOT-COST TO TL02-GENCO-COST                   
           MOVE RV026-GENCO-GRAND-TOT-RTL  TO TL02-GENCO-RETAIL.                
           MOVE RV026-KOHLS-GRAND-TOT-CNT  TO TL02-KOHLS-COUNT                  
           MOVE RV026-KOHLS-GRAND-TOT-COST TO TL02-KOHLS-COST                   
           MOVE RV026-KOHLS-GRAND-TOT-RTL  TO TL02-KOHLS-RETAIL.                
           WRITE SUMMARY-REPORT-REC FROM TL02-TOTAL-LINE-02                     
               AFTER ADVANCING 1 LINE.                                          
                                                                                
      ******************************************************************        
      *     WRITES HEADER LINES FOR THE DIFFERENCES                    *        
      ******************************************************************        
       8600-DIFFERENCES-HDRS.                                                   
                                                                                
           WRITE SUMMARY-REPORT-REC FROM HL-BLANK-LINE                          
               AFTER ADVANCING 6 LINES.                                         
                                                                                
           WRITE SUMMARY-REPORT-REC FROM DH06-DETAIL-HEADING-06                 
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           WRITE SUMMARY-REPORT-REC FROM DH07-DETAIL-HEADING-07                 
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           WRITE SUMMARY-REPORT-REC FROM DH09-DETAIL-HEADING-09                 
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           WRITE SUMMARY-REPORT-REC FROM DH10-DETAIL-HEADING-10                 
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           WRITE SUMMARY-REPORT-REC FROM DH11-DETAIL-HEADING-11                 
               AFTER ADVANCING 1 LINES.                                         
                                                                                
      ******************************************************************        
      *     WRITES DIFFERENCES BY RETURN TYPE                          *        
      ******************************************************************        
       8800-DETAIL-DIFFS.                                                       
                                                                                
           MOVE 'MOS'                    TO DL02-RETURN-TYPE.                   
           MOVE PV-GREG-DATE             TO DL02-DATE.                          
           MOVE RV026-MOS-TOT-CNT-DIF    TO DL02-COUNT-DIF                      
           MOVE RV026-MOS-TOT-COST-DIF   TO DL02-COST-DIF                       
           MOVE RV026-MOS-TOT-RTL-DIF    TO DL02-RETAIL-DIF.                    
           WRITE SUMMARY-REPORT-REC FROM DL02-DETAIL-LINE-02                    
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE SPACES                   TO DL02-DATE.                          
           MOVE 'RTV'                    TO DL02-RETURN-TYPE.                   
           MOVE RV026-RTV-TOT-CNT-DIF    TO DL02-COUNT-DIF                      
           MOVE RV026-RTV-TOT-COST-DIF   TO DL02-COST-DIF                       
           MOVE RV026-RTV-TOT-RTL-DIF    TO DL02-RETAIL-DIF.                    
           WRITE SUMMARY-REPORT-REC FROM DL02-DETAIL-LINE-02                    
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 'DND'                    TO DL02-RETURN-TYPE.                   
           MOVE RV026-DND-TOT-CNT-DIF    TO DL02-COUNT-DIF                      
           MOVE RV026-DND-TOT-COST-DIF   TO DL02-COST-DIF                       
           MOVE RV026-DND-TOT-RTL-DIF    TO DL02-RETAIL-DIF.                    
           WRITE SUMMARY-REPORT-REC FROM DL02-DETAIL-LINE-02                    
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 'CB'                     TO DL02-RETURN-TYPE.                   
           MOVE RV026-CBK-TOT-CNT-DIF    TO DL02-COUNT-DIF                      
           MOVE RV026-CBK-TOT-COST-DIF   TO DL02-COST-DIF                       
           MOVE RV026-CBK-TOT-RTL-DIF    TO DL02-RETAIL-DIF.                    
           WRITE SUMMARY-REPORT-REC FROM DL02-DETAIL-LINE-02                    
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE SUMMARY-REPORT-REC FROM TL03-TOTAL-LINE-03                     
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE RV026-GRAND-TOT-CNT-DIF  TO TL04-COUNT-DIF                      
           MOVE RV026-GRAND-TOT-COST-DIF TO TL04-COST-DIF                       
           MOVE RV026-GRAND-TOT-RTL-DIF  TO TL04-RETAIL-DIF.                    
           WRITE SUMMARY-REPORT-REC FROM TL04-TOTAL-LINE-04                     
               AFTER ADVANCING 1 LINE.                                          
                                                                                
      ******************************************************************        
      *    HOUSEKEEPING                                                         
      *        SET GRAND TOTAL DIFFERENCE FIELDS TO THEIR MAX VALUE             
      *        SO THAT LATER IF THE ACTUAL DIFFERENCE IS NOT CALCULATED,        
      *        THE PROGRAM KNOWS THERE IS A PROBLEM.                            
      ******************************************************************        
       9000-HOUSEKEEPING.                                                       
           ACCEPT PV-DATE-YYYYMMDD FROM DATE YYYYMMDD                           
           MOVE PV-DATE-YYYY       TO PV-GREG-YYYY                              
           MOVE PV-DATE-MM         TO PV-GREG-MM                                
           MOVE PV-DATE-DD         TO PV-GREG-DD                                
           MOVE PV-GREG-DATE       TO RV026-RUN-DATE.                           
           ACCEPT PV-TIME          FROM TIME                                    
           MOVE PV-HOUR            TO PV-HR                                     
           MOVE PV-MIN             TO PV-MN                                     
                                                                                
           INITIALIZE RV026-GENCO-INV-SUMMARY-RCD                               
                                                                                
           MOVE 9999999999999.99      TO RV026-GRAND-TOT-CNT-DIF                
                                         RV026-GRAND-TOT-COST-DIF               
                                         RV026-GRAND-TOT-RTL-DIF.               
W40134     MOVE ZEROES                TO PH-LOC-NBR.                            
W40134     MOVE SPACES                TO PH-LOC-10-ABBR-NM.                     
W40134     SET PS-NO-ERROR            TO TRUE.                                  
                                                                                
W40134******************************************************************        
W40134*  READ XLT00010 - XLT_LOC TABLE.                                         
W40134******************************************************************        
W40134 9600-READ-XLT00010.                                                      
W40134     INITIALIZE DCLXLT00010.                                              
W40134                                                                          
W40134     PERFORM                                                              
W40134         WITH TEST AFTER                                                  
W40134         VARYING PV-RETRY-COUNTER                                         
W40134         FROM 1 BY 1                                                      
W40134         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
W40134                   OR (SQLCODE = ZERO)                                    
W40134                   OR (SQLCODE = +100))                                   
W40134         EXEC SQL                                                         
W40134              SELECT  LOC_10_ABBR_NM                                      
W40134              INTO  :XLT00010-LOC-10-ABBR-NM                              
W40134              FROM XLT_LOC                                                
W40134              WHERE LOC_NBR = :PV-KEY-LOC-NBR                             
W40134         END-EXEC                                                         
W40134                                                                          
W40134         EVALUATE TRUE                                                    
W40134             WHEN SQLCODE = ZERO                                          
W40134                 SET PS-XLT00010-FOUND TO TRUE                            
W40134                 MOVE PV-KEY-LOC-NBR   TO PH-LOC-NBR                      
W40134                 MOVE XLT00010-LOC-10-ABBR-NM                             
W40134                                       TO PH-LOC-10-ABBR-NM               
W40134             WHEN SQLCODE = -904                                          
W40134             WHEN SQLCODE = -911                                          
W40134             WHEN SQLCODE = -913                                          
W40134                 SET PS-XLT00010-FOUND TO TRUE                            
W40134             WHEN SQLCODE = +100                                          
W40134                 SET PS-XLT00010-NOTFND TO TRUE                           
W40134             WHEN SQLWARN0 NOT EQUAL SPACE                                
W40134             WHEN SQLCODE NOT EQUAL ZERO                                  
W40134                 DISPLAY '** ABEND **'                                    
W40134                 DISPLAY '** PROGRAM = RVKUT024 **'                       
W40134                 DISPLAY '** PARAGRAPH = '                                
W40134                         '9600-READ-XLT00010 **'                          
W40134                 MOVE 'SELECT XLT_LOC'  TO PV-ERROR-MSG-1                 
W40134                 MOVE PV-KEY-LOC-NBR    TO PV-LOC-NBR-CHAR                
W40134                 MOVE 'XLT_LOC'         TO PV-ABEND-TABLE-1               
W40134                 PERFORM 9850-SQL-ERROR                                   
W40134         END-EVALUATE                                                     
W40134     END-PERFORM.                                                         
W40134                                                                          
W40134     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
W40134         DISPLAY '** ABEND **'                                            
W40134         DISPLAY '** PROGRAM = RVKUT024 **'                               
W40134         DISPLAY '** PARAGRAPH = 9600-READ-XLT00010 **'                   
W40134         MOVE 'SELECT MAX RETRIES'                                        
W40134                                        TO PV-ERROR-MSG-1                 
W40134         MOVE PV-KEY-LOC-NBR            TO PV-LOC-NBR-CHAR                
W40134         MOVE 'XLT_LOC'                 TO PV-ABEND-TABLE-1               
W40134         PERFORM 9850-SQL-ERROR                                           
W40134     END-IF.                                                              
                                                                                
W40134******************************************************************        
W40134* 9700-EOJ-ROUTINE - ENDS REPORT AND CLOSES FILES.                        
W40134******************************************************************        
W40134 9700-EOJ-ROUTINE.                                                        
W40134     WRITE SUMMARY-REPORT-REC FROM END-OF-REPORT-LINE                     
W40134         AFTER ADVANCING 3 LINES.                                         
W40134                                                                          
W40134     IF PS-NO-ERROR                                                       
W40134         PERFORM 6600-EMPTY-FILE-RECORD                                   
W40134     END-IF.                                                              
W40134                                                                          
W40134     DISPLAY '**********  END OF RVKUT024 ***********'                    
W40134                                                                          
W40134     CLOSE  GENCO-INVENTORY-INPUT                                         
W40134            INVENTORY-OUTPUT                                              
W40134            SUMMARY-REPORT                                                
W40134            ERROR-OUTPUT.                                                 
W40134                                                                          
      ******************************************************************        
      *    ABENDS THAT ARE NOT ASSOCIATED WITH DB2                              
      ******************************************************************        
       9800-NON-DB2-ABEND.                                                      
           DISPLAY '**************************************************'.        
           DISPLAY '**  ABEND     **  = ' ABEND-CODE.                           
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**************************************************'.        
           DISPLAY '******** CURRENT STATS AT TIME OF ABEND  *********'         
           DISPLAY SPACES                                                       
           DISPLAY '** DETAIL RECORDS READ      ' PA-DTL-RECS-READ ' **'        
           DISPLAY '** CONTROL RECORDS READ     ' PA-CTL-RECS-READ ' **'        
           DISPLAY '** TOT DETAIL RECS WRITTEN  ' PA-DTL-RECS-WRITTEN           
                   ' **'                                                        
           DISPLAY '** TOT CONTROL RECS WRITTEN ' PA-CTL-RECS-WRITTEN           
                   ' **'.                                                       
           DISPLAY '** BILL OF LADING NUMBER  **' RV022-BL-NBR                  
           DISPLAY '** SKU                    **' RV022-SKU                     
                                                                                
           MOVE +4013          TO ABEND-CODE.                                   
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
                                                                                
      ******************************************************************        
      *    DB2 ABENDS                                                           
      ******************************************************************        
W40134*****************************************************************         
W40134*  PARAGRAPH EXECUTED WHEN SERIOUS DB2 ERROR OCCURRED                     
W40134*****************************************************************         
W40134 9850-SQL-ERROR.                                                          
W40134     CLOSE  GENCO-INVENTORY-INPUT                                         
W40134            INVENTORY-OUTPUT                                              
W40134            SUMMARY-REPORT                                                
W40134            ERROR-OUTPUT.                                                 
W40134                                                                          
W40134     MOVE SQLCODE                     TO SQLCODE2.                        
W40134     MOVE SQLERRD(3)                  TO SQLERRD3.                        
W40134                                                                          
W40134     DISPLAY '** ABEND **         ** = SQLERROR'.                         
W40134     DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
W40134     DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                        
W40134     DISPLAY '** LOC NBR   ** = ' PV-LOC-NBR-CHAR.                        
W40134     DISPLAY '**           ** = ' PV-ABEND-TABLE-1.                       
W40134     DISPLAY '**           ** = ' PV-ABEND-TABLE-2.                       
W40134     DISPLAY '**           ** = ' PV-ABEND-TABLE-3.                       
W40134     DISPLAY '**           ** = ' PV-ABEND-TABLE-4.                       
W40134     DISPLAY '**           ** = ' PV-ABEND-TABLE-5.                       
W40134     DISPLAY '**           ** = ' PV-ERROR-MSG-1                          
W40134     DISPLAY '**           ** = ' PV-ERROR-MSG-2                          
W40134     DISPLAY '**           ** = ' PV-ERROR-MSG-3                          
W40134                                                                          
W40134     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
W40134     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
W40134     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
W40134     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
W40134                                                                          
W40134     PERFORM 9999-DB2-ABEND.                                              
W40134                                                                          
W40134     MOVE +4013                       TO PV-ABEND-CODE.                   
W40134     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
W40134                                                                          
       9999-DB2-ABEND.                                                          
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
