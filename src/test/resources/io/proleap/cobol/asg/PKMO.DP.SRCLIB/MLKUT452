       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT452.                                                 
       AUTHOR.        RONNA BUTZ.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  05-15-00.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                         
      *    MLKUT452 -  CREATE DATA INTEGRITY TWKMCLS EXTRACT FILE               
      *                                                                         
      * THIS PROGRAM WILL CREATE THE DATA INTEGRITY EXTRACT FILE FOR            
      * DB2 TABLE TWKMCLS.                                                      
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION             
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATA-INTEGRITY-CTL-FILE                                       
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT DATA-INTEGRITY-EXTR-FILE                                      
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DATA-INTEGRITY-CTL-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATA-INTEGRITY-CTL-REC      PIC X(80).                               
                                                                                
       FD  DATA-INTEGRITY-EXTR-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
W26603 01  DATA-INTEGRITY-EXTR-REC     PIC X(120).                              
                                                                                
      *                                                                         
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-TWKMCLS-DONE         PIC X       VALUE 'N'.                   
               88  PS-TWKMCLS-IS-DONE              VALUE 'Y'.                   
               88  PS-TWKMCLS-IS-NOT-DONE          VALUE 'N'.                   
                                                                                
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
           05  PA-TWKMCLS-ROWS-FETCHED PIC S9(13)  VALUE ZERO.                  
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.                  
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PREV-FSCL-MN-END-DTE PIC X(10) VALUE SPACE.                   
           05  PV-PREV-DEPT-NBR        PIC X(03) VALUE SPACE.                   
           05  PV-PREV-MAJ-CL-NBR      PIC X(02) VALUE SPACE.                   
           05  PV-PREV-STR-NBR         PIC S9(4) VALUE ZERO                     
                                                 COMP.                          
           05  PV-STRT-DTE             PIC X(10).                               
           05  PV-END-DTE              PIC X(10).                               
                                                                                
       01  CD-COUNTER-DISPLAY.                                                  
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.                   
                                                                                
       01 ABEND-AREAS.                                                          
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO                   
                                                   COMP SYNC.                   
               88  AC-BAD-DATA                     VALUE +4003.                 
               88  AC-DB2-ERROR                    VALUE +4013.                 
           05  AA-ABEND-LIT            PIC X(40)   VALUE                        
                 '*****       ABEND'.                                           
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
                   '*****   PROGRAM: MLKUT452'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER              PIC X(17)   VALUE                        
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER              PIC X(06)   VALUE '*****'.               
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER              PIC X(17)   VALUE                        
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
                                                                                
       EJECT                                                                    
                                                                                
      *** DATA INTEGRITY CONTROL CARD                                           
           COPY MLRD110.                                                        
                                                                                
                                                                                
      *** DATA INTEGRITY TWKMCLS EXTRACT FILE                                   
W26603     COPY MLRD111.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  DB2 ERRORS                                                             
      ******************************************************************        
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      *  DB2 COMMUNICATION AREA                                                 
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 TWKMCLS TABLE LAYOUT                                               
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE TWKMCLS                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DATE ATTRIBUTE TABLE LAYOUT                                            
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE TDTATTR                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  MERCHANDISE LEDGER CONTROL TABLE LAYOUT                                
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE TMLCNTL                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 TWKMCLS CURSOR                                                     
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE TWKMCLS_CURSOR CURSOR FOR                                
                SELECT B.FSCL_MN_NBR,                                           
                       B.FSCL_MN_END_DTE,                                       
                       A.FSCL_WK_NBR,                                           
                       A.FSCL_WK_END_DTE,                                       
                       DEPT_NBR,                                                
                       MAJ_CL_NBR,                                              
                       STR_NBR,                                                 
                       SLS_RTL_AMT,                                             
                       MKDN_RTL_AMT,                                            
                       MKUP_RTL_AMT,                                            
                       XFER_IN_RTL_AMT,                                         
                       XFER_OUT_RTL_AMT,                                        
                       RCPT_RTL_AMT,                                            
                       RCPT_NET_COST_AMT,                                       
                       PADJ_RTL_AMT,                                            
                       PADJ_NET_COST_AMT,                                       
                       INV_ADJ_RTL_AMT,                                         
                       SHNK_RTL_AMT,                                            
                       END_INV_RTL_AMT                                          
                  FROM TWKMCLS A                                                
                      ,TDTATTR B                                                
                 WHERE (A.FSCL_WK_END_DTE                                       
                            BETWEEN :PV-STRT-DTE AND :PV-END-DTE)               
                   AND A.FSCL_WK_END_DTE = B.KY_DTE                             
              ORDER BY  STR_NBR                                                 
                       ,MAJ_CL_NBR                                              
                       ,DEPT_NBR                                                
                       ,FSCL_WK_END_DTE DESC                                    
W26603        WITH UR                                                           
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAINLINE-MLKUT452.                                                  
      ******************************************************************        
      *                                                                         
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS.                                           
                                                                                
           PERFORM 8000-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      ******************************************************************        
                                                                                
           OPEN INPUT  DATA-INTEGRITY-CTL-FILE.                                 
           OPEN OUTPUT DATA-INTEGRITY-EXTR-FILE.                                
                                                                                
           PERFORM 4000-READ-CONTROL-CARD.                                      
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*** MLKUT452 INPUT CONTROL CARD VALUES ***'                 
           DISPLAY ML110-DATA-INTEGRITY-CC-REC.                                 
                                                                                
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
                                                                                
      *    DETERMINE RANGE OF DATES TO BE PROCESSED.                            
      *                                                                         
      *    THE START DATE WILL BE THE FIRST DAY OF THE FISCAL MONTH             
      *    INTO WHICH ML110-STRT-DTE FALLS.                                     
      *                                                                         
      *    THE END DATE WILL BE ML110-END-DTE OR THE MAX POSTING FSCL WK        
      *    DATE FROM TMLCNTL, WHICHEVER IS GREATER.  THE REASON THE END         
      *    DATE CANNOT BE GREATER THAN THE MAX POSTING FSCL WK DATE IS          
      *    THAT END INV RETAIL IS TAKEN FROM THE MOST RECENT WEEK               
      *    RETURNED BY THE TWKMCLS CURSOR. IF THE CURSOR RETURNS THE            
      *    FUTURE WEEK ROWS THAT EXIST FOR ON-ORDER, END INV RETAIL WILL        
      *    BE PICKED UP FROM THOSE ROWS.  END INV RTL WILL BE ZERO ON           
      *    THOSE ROWS BECAUSE WE ONLY RECALCULATE UP THROUGH THE MAX            
      *    POSTING FSCL WK DATE.                                                
                                                                                
           MOVE ML110-STRT-DTE         TO DTATTR-KY-DTE.                        
           PERFORM 7000-GET-FSCL-MN-BEG-DTE.                                    
           MOVE DTATTR-FSCL-MN-BEG-DTE TO PV-STRT-DTE.                          
                                                                                
           PERFORM 7050-GET-MXPSTG-FSCL-WK-DTE.                                 
           IF ML110-END-DTE > MLCNTL-MXPSTG-FSCL-WK-DTE                         
               MOVE MLCNTL-MXPSTG-FSCL-WK-DTE TO PV-END-DTE                     
           ELSE                                                                 
               MOVE ML110-END-DTE             TO PV-END-DTE                     
           END-IF.                                                              
                                                                                
           DISPLAY 'START DATE: ' PV-STRT-DTE.                                  
           DISPLAY 'END DATE:   ' PV-END-DTE.                                   
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
      *                                                                         
           PERFORM 7100-OPEN-CURSOR-TWKMCLS.                                    
                                                                                
           PERFORM 7200-FETCH-TWKMCLS.                                          
                                                                                
           PERFORM                                                              
             UNTIL PS-TWKMCLS-IS-DONE                                           
               PERFORM 5000-WRITE-EXTRACT                                       
               PERFORM 7200-FETCH-TWKMCLS                                       
           END-PERFORM.                                                         
                                                                                
           PERFORM 7300-CLOSE-CURSOR-TWKMCLS.                                   
                                                                                
                                                                                
      ******************************************************************        
       4000-READ-CONTROL-CARD.                                                  
      ******************************************************************        
      *                                                                         
           READ DATA-INTEGRITY-CTL-FILE                                         
             INTO ML110-DATA-INTEGRITY-CC-REC                                   
               AT END                                                           
                   MOVE '4000-READ-CONTROL-CARD'                                
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'CONTROL CARD IS MISSING'                               
                                       TO AA-MESSAGE                            
                   MOVE +4001          TO ABEND-CODE                            
                   PERFORM 9900-COMMON-ABEND-ROUTINE                            
           END-READ.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5000-WRITE-EXTRACT.                                                      
      ******************************************************************        
      *                                                                         
           MOVE DTATTR-FSCL-MN-END-DTE TO ML111-FSCL-MN-END-DTE.                
           MOVE DTATTR-FSCL-MN-NBR     TO ML111-FSCL-MN-NBR.                    
           MOVE WKMCLS-DEPT-NBR        TO ML111-DEPT-NBR.                       
           MOVE WKMCLS-MAJ-CL-NBR      TO ML111-MAJ-CL-NBR.                     
           MOVE '00'                   TO ML111-SUB-CL-NBR.                     
           MOVE WKMCLS-STR-NBR         TO ML111-STR-NBR.                        
           MOVE WKMCLS-SLS-RTL-AMT     TO ML111-SLS-RTL-AMT.                    
           MOVE WKMCLS-MKDN-RTL-AMT    TO ML111-MKDN-RTL-AMT.                   
           MOVE WKMCLS-MKUP-RTL-AMT    TO ML111-MKUP-RTL-AMT.                   
           MOVE WKMCLS-XFER-IN-RTL-AMT TO ML111-XFER-IN-RTL-AMT.                
           MOVE WKMCLS-XFER-OUT-RTL-AMT                                         
                                       TO ML111-XFER-OUT-RTL-AMT.               
           MOVE WKMCLS-RCPT-RTL-AMT    TO ML111-RCPT-RTL-AMT.                   
           MOVE WKMCLS-RCPT-NET-COST-AMT                                        
                                       TO ML111-RCPT-NET-COST-AMT.              
           MOVE WKMCLS-PADJ-RTL-AMT    TO ML111-PADJ-RTL-AMT.                   
           MOVE WKMCLS-PADJ-NET-COST-AMT                                        
                                       TO ML111-PADJ-NET-COST-AMT.              
           MOVE WKMCLS-INV-ADJ-RTL-AMT TO ML111-INV-ADJ-RTL-AMT.                
           MOVE WKMCLS-SHNK-RTL-AMT    TO ML111-SHNK-RTL-AMT.                   
                                                                                
      *    ENDING INVENTORY IS ONLY FILLED IN ON THE RECORD FOR THE             
      *    FIRST (MOST RECENT) FISCAL WEEK IN THE FISCAL MONTH.                 
      *    (THE FISCAL WEEKS ARE PROCESSED IN DESCENDING SEQUENCE.)             
           IF  DTATTR-FSCL-MN-END-DTE  = PV-PREV-FSCL-MN-END-DTE                
           AND WKMCLS-DEPT-NBR         = PV-PREV-DEPT-NBR                       
           AND WKMCLS-MAJ-CL-NBR       = PV-PREV-MAJ-CL-NBR                     
           AND WKMCLS-STR-NBR          = PV-PREV-STR-NBR                        
               MOVE ZERO                   TO ML111-END-INV-RTL-AMT             
           ELSE                                                                 
               MOVE WKMCLS-END-INV-RTL-AMT TO ML111-END-INV-RTL-AMT             
               MOVE DTATTR-FSCL-MN-END-DTE TO PV-PREV-FSCL-MN-END-DTE           
               MOVE WKMCLS-DEPT-NBR        TO PV-PREV-DEPT-NBR                  
               MOVE WKMCLS-MAJ-CL-NBR      TO PV-PREV-MAJ-CL-NBR                
               MOVE WKMCLS-STR-NBR         TO PV-PREV-STR-NBR                   
           END-IF.                                                              
                                                                                
           WRITE DATA-INTEGRITY-EXTR-REC                                        
               FROM ML111-DATA-INTEGRITY-EXTR-REC.                              
           ADD 1                       TO PA-OUTPUT-RECS-WRITTEN.               
                                                                                
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7000-GET-FSCL-MN-BEG-DTE.                                                
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               SELECT  FSCL_MN_BEG_DTE                                          
                 INTO :DTATTR-FSCL-MN-BEG-DTE                                   
                 FROM TDTATTR                                                   
                WHERE KY_DTE = :DTATTR-KY-DTE                                   
W26603          WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7000-GET-FSCL-MN-BEG-DTE'                              
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'NO TDTATTR MATCH FOUND FOR KY_DTE'                     
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT FROM'  TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
       7050-GET-MXPSTG-FSCL-WK-DTE.                                             
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               SELECT  MXPSTG_FSCL_WK_DTE                                       
                 INTO :MLCNTL-MXPSTG-FSCL-WK-DTE                                
                 FROM TMLCNTL                                                   
W26603         WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7050-GET-MXPSTG-FSCL-WK-DTE'                           
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TMLCNTL ROW NOT FOUND'                                 
                                       TO AA-MESSAGE                            
                   MOVE 'SELECT'       TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
       7100-OPEN-CURSOR-TWKMCLS.                                                
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               OPEN TWKMCLS_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7100-OPEN-CURSOR-TWKMCLS'                              
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TWKMCLS TABLE ERROR'                                   
                                       TO AA-MESSAGE                            
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7200-FETCH-TWKMCLS.                                                      
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               FETCH TWKMCLS_CURSOR                                             
                INTO :DTATTR-FSCL-MN-NBR,                                       
                     :DTATTR-FSCL-MN-END-DTE,                                   
                     :WKMCLS-FSCL-WK-NBR,                                       
                     :WKMCLS-FSCL-WK-END-DTE,                                   
                     :WKMCLS-DEPT-NBR,                                          
                     :WKMCLS-MAJ-CL-NBR,                                        
                     :WKMCLS-STR-NBR,                                           
                     :WKMCLS-SLS-RTL-AMT,                                       
                     :WKMCLS-MKDN-RTL-AMT,                                      
                     :WKMCLS-MKUP-RTL-AMT,                                      
                     :WKMCLS-XFER-IN-RTL-AMT,                                   
                     :WKMCLS-XFER-OUT-RTL-AMT,                                  
                     :WKMCLS-RCPT-RTL-AMT,                                      
                     :WKMCLS-RCPT-NET-COST-AMT,                                 
                     :WKMCLS-PADJ-RTL-AMT,                                      
                     :WKMCLS-PADJ-NET-COST-AMT,                                 
                     :WKMCLS-INV-ADJ-RTL-AMT,                                   
                     :WKMCLS-SHNK-RTL-AMT,                                      
                     :WKMCLS-END-INV-RTL-AMT                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   ADD +1              TO PA-TWKMCLS-ROWS-FETCHED               
               WHEN SQLCODE = +100                                              
                   MOVE 'Y'            TO PS-TWKMCLS-DONE                       
               WHEN OTHER                                                       
                   MOVE '7200-FETCH-TWKMCLS'                                    
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TWKMCLS TABLE ERROR'                                   
                                       TO AA-MESSAGE                            
                   MOVE 'FETCH'        TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7300-CLOSE-CURSOR-TWKMCLS.                                               
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               CLOSE TWKMCLS_CURSOR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7300-CLOSE-CURSOR-TWKMCLS'                             
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'TWKMCLS TABLE ERROR'                                   
                                       TO AA-MESSAGE                            
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION                      
                   PERFORM 9901-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
       8000-EOJ.                                                                
      ******************************************************************        
      *                                                                         
           DISPLAY ' '.                                                         
           DISPLAY '  PROGRAM MLKUT452 PROCESSING SUMMARY'.                     
           DISPLAY '  -----------------------------------'.                     
           DISPLAY ' '.                                                         
      *                                                                         
           MOVE PA-TWKMCLS-ROWS-FETCHED TO CD-COUNT-HOLD1.                      
           DISPLAY 'TWKMCLS ROWS FETCHED =      ' CD-COUNT-HOLD1.               
      *                                                                         
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.                       
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.               
      *                                                                         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE DATA-INTEGRITY-CTL-FILE.                                       
           CLOSE DATA-INTEGRITY-EXTR-FILE.                                      
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9900-COMMON-ABEND-ROUTINE.                                               
      ******************************************************************        
      *                                                                         
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY ABEND-CODE.                                                  
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9901-SQL-ERROR.                                                          
      ******************************************************************        
      *                                                                         
           CLOSE DATA-INTEGRITY-CTL-FILE                                        
                 DATA-INTEGRITY-EXTR-FILE.                                      
                                                                                
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-DB2-ERROR-LIT.                                            
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY AA-DB2-OPERATION-LIT.                                        
           SET AC-DB2-ERROR TO TRUE.                                            
           MOVE +4013                  TO ABEND-CODE.                           
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
