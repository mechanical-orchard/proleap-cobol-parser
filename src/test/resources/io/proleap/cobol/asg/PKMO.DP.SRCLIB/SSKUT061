      ******************************************************************00010082
       IDENTIFICATION DIVISION.                                         00020082
      ******************************************************************00030082
                                                                        00040082
       PROGRAM-ID.             SSKUT061.                                00050099
       AUTHOR.                 BRYAN BUKOWSKI.                          00060099
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070082
       DATE-WRITTEN            01-28-2009.                              00080099
       DATE-COMPILED.                                                   00090082
                                                                        00100082
      ******************************************************************00120082
      ** THIS IS AN WEEKLY PROGRAM THAT WILL PROVIDE                  **00130099
      ** POS TRANSACTION TIMES FOR ALL STORES OR THE DATE THAT IT IS  **00140099
      ** RUN ON.                                                      **00150099
      ** THE USER WANTS NUMBERS GENERATED BY DAY AND THE JOB RUN      **00170099
      ** WEEKLY.                                                      **00180099
      ** TIMES ARE EXTRACTED FROM THE TEPTRN TABLE AND UNITS SOLD     **00210082
      ** FROM THE TEPITM TABLE.                                       **00220082
      ** TRANSACTIONS ARE COUNTED FOR ONLY THE FRONT REGISTERS        **00230082
      ** (REGISTER NBR < 25) AND WHEN THE TRANSACTIN IS FOLLOWED      **00240082
      ** BY ANOTHER WITHIN 1 SECOND OR WHEN THE TRANSACTION IS        **00250099
      ** BETWEEN 1 AND 5 SECONDS.  THIS INSURES THAT THE ENTIRE       **00251099
      ** PROCESS WAS COMPLETED AS OPPOSED TO A TRANSACTION THAT WAS   **00260082
      ** COMPLETED, BUT THEN BAGGING WAS DONE AFTERWARDS.             **00270082
      ** ALSO, SALES CORRECTIONS AND TRANSACTIONS SPANNING MIDNIGHT   **00280082
      ** ARE EXCLUDED.                                                **00290082
      ** DB2 CALCULATES THE ELAPSED TIMES IN HHMMSS.  THE PROGRAM     **00300082
      ** CONVERTS THE ELAPSED TIMES INTO SECONDS.                     **00310082
      ******************************************************************00370082
FSH13 * HISTORY LOG:                                                    00372099
FSH13 *                                                                 00373099
FSH13 * DATE: 09/06/13                                                  00374099
FSH13 * WR/IR#: CHG0062728 - FSH 2013                                   00375099
FSH13 * PROGRAMMER: COGNIZANT                                           00376099
FSH13 * DESCRIPTION: REMOVED LOGIC THAT REFERENCING THE FOLLOWING       00377099
FSH13 *              HISTORY TABLES.                                    00378099
FSH13 *              TEPITMH                                            00379099
FSH13 *              TEPTRNH                                            00379199
      ******************************************************************00380082
       ENVIRONMENT DIVISION.                                            00390082
      ******************************************************************00400082
                                                                        00410082
       CONFIGURATION SECTION.                                           00420082
                                                                        00430082
       SOURCE-COMPUTER.        IBM-3090.                                00440082
       OBJECT-COMPUTER.        IBM-3090.                                00450082
                                                                        00460082
       INPUT-OUTPUT SECTION.                                            00470082
                                                                        00480082
       FILE-CONTROL.                                                    00490082
           SELECT DATE-CARD-FILE                                        00491099
               ASSIGN TO INP01.                                         00492099
                                                                        00493099
           SELECT TRAN-TIME-FILE                                        00493199
               ASSIGN TO TLOG.                                          00493299
                                                                        00494082
      ******************************************************************00510082
       DATA DIVISION.                                                   00520082
      ******************************************************************00530082
                                                                        00540082
       FILE SECTION.                                                    00550082
                                                                        00551099
       FD  DATE-CARD-FILE                                               00552099
           RECORDING MODE IS F                                          00553099
           BLOCK CONTAINS 0 RECORDS                                     00554099
           LABEL RECORDS ARE STANDARD.                                  00555099
                                                                        00556099
       01  DATE-CARD-RECORD            PIC X(80).                       00557099
                                                                        00558099
       FD  TRAN-TIME-FILE                                               00559099
           RECORDING MODE IS F                                          00559199
           LABEL RECORDS ARE STANDARD                                   00559299
           BLOCK CONTAINS 0  RECORDS.                                   00559399
                                                                        00559899
       01  TRAN-TIME-RECORD             PIC X(200).                     00559999
                                                                        00570082
       WORKING-STORAGE SECTION.                                         00580082
      ******************************************************************00581099
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                    00582099
      ******************************************************************00583099
       01  CC-CONTROL-CARD.                                             00584099
           05  CC-CONTROL-DATE         PIC  X(10)  VALUE SPACE.         00585099
           05  FILLER                  PIC  X(70)  VALUE SPACE.         00586099
                                                                        00587099
       01  PV-MONTH-START-DATE.                                         00588099
           05  PV-MSD-CC                 PIC 9(02)  VALUE ZERO.         00589099
           05  PV-MSD-YY                 PIC 9(02)  VALUE ZERO.         00589199
           05  FILLER                    PIC X(01)  VALUE '-'.          00589299
           05  PV-MSD-MM                 PIC 9(02)  VALUE ZERO.         00589399
           05  FILLER                    PIC X(01)  VALUE '-'.          00589499
           05  PV-MSD-DD                 PIC 9(02)  VALUE ZERO.         00589599
       01 PV-MONTH-START-DATE-CHAR       PIC X(10)  VALUE SPACE.        00589699
                                                                        00589799
                                                                        00589899
      **********************************************************        00589999
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)             00590099
      **********************************************************        00590199
           COPY DPWS500.                                                00590299
                                                                        00590399
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      00590499
                                                        COMP SYNC.      00590599
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    00590699
           88  AC-DB2-ERROR                             VALUE +4013.    00590799
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    00590899
                                                                        00590999
                                                                        00592089
       01  ABEND-CODE                  PIC S9(4)    VALUE ZEROS         00600082
                                       COMP SYNC.                       00610082
           88  ABEND-1111-TABLE-OVERFLOW                 VALUE +1111.   00620082
           88  ABEND-2222-DB2-ERROR                      VALUE +2222.   00630082
                                                                        00640082
                                                                        00650082
       01  PS-PROGRAM-SWITCHES.                                         00660082
           05  PS-END-OF-CURSOR-SW           PIC X(01)    VALUE 'N'.    00670099
               88  PS-END-OF-CURSOR                       VALUE 'Y'.    00680099
               88  PS-NOT-END-OF-CURSOR                   VALUE 'N'.    00690099
           05  PS-END-OF-FILE                PIC X(01)    VALUE 'N'.    00691099
               88  PS-END-OF-FILE-YES                     VALUE 'Y'.    00692099
                                                                        00700082
       01  PC-PROGRAM-CONSTANTS.                                        00710082
           05  PC-CURRENT-PROGRAM-NAME PIC X(08)    VALUE 'SSKUT061'.   00720099
           05  PC-NUMBER-PROCESS-DAYS  PIC 9(1)     VALUE 7.            00721099
           05  PC-MAX-NUMBER-STORES    PIC 9(4)     VALUE 2000.         00722099
                                                                        00730082
       01 PT-STORE-TABLE.                                               00740082
           05 PT-STORE                 OCCURS 2000 TIMES.               00751099
              10 PT-STORE-NBR             PIC 9(4)  VALUE ZEROS COMP-3. 00760099
              10 PT-STORE-TRAN-1-SEC-CNT  PIC 9(7)  VALUE ZEROS COMP-3. 00770199
              10 PT-STORE-TRAN-5-SEC-CNT  PIC 9(7)  VALUE ZEROS COMP-3. 00771099
              10 PT-STORE-ELAPSED-TIME    PIC 9(11) VALUE ZEROS COMP-3. 00780099
              10 PT-STORE-ELAPSED-5-SEC-TIME PIC 9(11)                  00781099
                                                    VALUE ZEROS COMP-3. 00782099
              10 PT-STORE-UNITS-SOLD      PIC 9(9)  VALUE ZEROS COMP-3. 00790099
              10 PT-STORE-UNITS-5-SEC-SOLD PIC 9(9) VALUE ZEROS COMP-3. 00791099
                                                                        00800082
       01 PT-PROGRAM-TOTALS.                                            00810082
           05  PT-TOTAL-TRANS-READ           PIC 9(7)   VALUE ZEROES.   00820082
           05  PT-GT-1-SECOND-TRANS-SKIPPED  PIC 9(7)   VALUE ZEROES.   00830082
           05  PT-LT-5-SECOND-TRANS-SKIPPED  PIC 9(7)   VALUE ZEROES.   00831099
           05  PT-REGISTER-TRANS-SKIPPED     PIC 9(7)   VALUE ZEROES.   00840082
           05  PT-MIDNIGHT-SKIPPED           PIC 9(7)   VALUE ZEROES.   00850082
           05  PT-AVERAGE-ELAPSED-TIME       PIC 9(7)   VALUE ZEROES.   00860082
           05  PT-AVERAGE-UNITS-SOLD         PIC 9(3)V9 VALUE ZEROES.   00870082
           05  PT-TOTAL-TRAN-CNT             PIC 9(7)   VALUE ZEROS.    00880082
           05  PT-TOTAL-ELAPSED-TIME         PIC 9(11)  VALUE ZEROS.    00890082
           05  PT-TOTAL-UNITS-SOLD           PIC 9(9)   VALUE ZEROS.    00900082
                                                                        00910082
       01  PV-PROGRAM-VARIABLES-NONRESET.                               00910199
           05  PV-NUMBER-DAYS                PIC 9(9)   VALUE ZEROS.    00911099
                                                                        00912099
       01  PV-PROGRAM-VARIABLES.                                        00920082
           05  PV-DB2-FUNCTION               PIC X(30)    VALUE SPACES. 00930099
           05  PV-PARAGRAPH-NAME             PIC X(30)    VALUE SPACES. 00940099
           05  PV-DISPLAY-DECIMAL            PIC Z(8)9.9  VALUE ZEROES. 00950099
           05  PV-DISPLAY-NUMBER             PIC Z(10)9   VALUE ZEROES. 00960099
           05  PV-END-TIME-PLUS-1-SECOND     PIC X(8)     VALUE SPACES. 00970099
           05  PV-END-TIME-PLUS-5-SECOND     PIC X(8)     VALUE SPACES. 00971099
           05  PV-ELAPSED-TIME             PIC S9(9) COMP VALUE ZEROES. 00980099
           05  PV-ELAPSED-TIME-NUM           PIC 9(6)     VALUE ZEROES. 00990099
           05  PV-ELAPSED-TIME-REDEF REDEFINES PV-ELAPSED-TIME-NUM.     01000082
               10  PV-ELAPSED-HOURS        PIC 99.                      01010099
               10  PV-ELAPSED-MINUTES      PIC 99.                      01020099
               10  PV-ELAPSED-SECONDS      PIC 99.                      01030099
           05  PV-LAST-STORE-PROCESSED     PIC 9(4)     VALUE ZEROES.   01040099
           05  PV-TRAN-ELAPSED-TIME        PIC 9(7)     VALUE ZEROES.   01050099
           05  PREV-STORE                  PIC 9(4)     VALUE ZEROES.   01060099
           05  PREV-REGISTER               PIC 9(4)     VALUE ZEROES.   01070099
           05  PREV-TRAN                   PIC 9(4)     VALUE ZEROES.   01080099
           05  PREV-ELAPSED-TIME           PIC S9(9)    VALUE ZEROES.   01090099
           05  PREV-SALES-DATE             PIC X(10)    VALUE SPACES.   01100099
           05  PREV-END-TIME-PLUS-1-SECOND PIC X(8)     VALUE SPACES.   01110099
           05  PREV-END-TIME-PLUS-5-SECOND PIC X(8)     VALUE SPACES.   01111099
           05  PREV-UNITS-SOLD             PIC 9(5)     VALUE ZEROES.   01120099
           05  PREV-UNITS-5-SEC-SOLD       PIC 9(5)     VALUE ZEROES.   01121099
           05  STORE-SUB                   PIC 9(5)     VALUE ZEROES.   01130099
           05  SUB                         PIC 9(5)     VALUE ZEROES.   01140099
           05  PV-OPERATION-MSG-1          PIC  X(40)   VALUE SPACE.    01140199
           05  PV-OPERATION-MSG-2          PIC  X(40)   VALUE SPACE.    01140299
                                                                        01140399
      * THE NEXT 3 SECTIONS ARE ALL REPORT CREATION VARIABLES           01140499
      * THE FIRST COLUMN OF THE REPORT IS EITHER AN A, B, OR C          01140599
      * FOR SORTING PURPOSES.                                           01140699
      *                                                                 01140799
      * GRAND TOTAL REPORT SECTION                                      01140899
       01  PC-REPORT-TOTAL-HEADER-ROW-1.                                01140999
           05 PC-SORT-COL-1            PIC X(1)    VALUE 'A'.           01141099
           05 FILLER                   PIC X(1)    VALUE ','.           01141199
           05 PC-TOTAL-TRAN-READ       PIC X(22)   VALUE                01141299
                                      'TOTAL TRANSACTION READ'.         01141399
           05 FILLER                   PIC X(1)    VALUE ','.           01141499
           05 PV-TOTAL-TRAN-READ       PIC X(8)    VALUE SPACE.         01141599
       01  PC-REPORT-TOTAL-HEADER-ROW-2.                                01141699
           05 PC-SORT-COL-2            PIC X(1)    VALUE 'A'.           01141799
           05 FILLER                   PIC X(1)    VALUE ','.           01141899
           05 PC-TRANS-SKIP-GT-1-SEC   PIC X(41)   VALUE                01141999
                   'TRANS SKIPPED NEXT TRAN GT 1 SECOND LATER'.         01142099
           05 FILLER                   PIC X(1)    VALUE ','.           01142199
           05 PV-TRAN-SKIP-GT-1-SEC    PIC X(8)    VALUE SPACE.         01142299
       01  PC-REPORT-TOTAL-HEADER-ROW-3.                                01142399
           05 PC-SORT-COL-3            PIC X(1)    VALUE 'A'.           01142499
           05 FILLER                   PIC X(1)    VALUE ','.           01142599
           05 PC-TRANS-SKIP-GT-1-SEC   PIC X(43)   VALUE                01142699
                   'TRANS SKIPPED NEXT TRAN GT 1 LT 5 SEC LATER'.       01142799
           05 FILLER                   PIC X(1)    VALUE ','.           01142899
           05 PV-TRAN-SKIP-LT-5-SEC    PIC X(8)    VALUE SPACE.         01142999
       01  PC-REPORT-TOTAL-HEADER-ROW-4.                                01143099
           05 PC-SORT-COL-4            PIC X(1)    VALUE 'A'.           01143199
           05 FILLER                   PIC X(1)    VALUE ','.           01143299
           05 PC-TRANS-SKIP-REG-24     PIC X(33)   VALUE                01143399
                      'TRANS SKIPPED REGISTER NBR GT 24'.               01143499
           05 FILLER                   PIC X(1)    VALUE ','.           01143599
           05 PV-TRAN-SKIP-REG-24      PIC X(8)    VALUE SPACE.         01143699
       01  PC-REPORT-TOTAL-HEADER-ROW-5.                                01143799
           05 PC-SORT-COL-5            PIC X(1)    VALUE 'A'.           01143899
           05 FILLER                   PIC X(1)    VALUE ','.           01143999
           05 PC-TRANS-SKIP-SPAN-MID   PIC X(31)   VALUE                01144099
                      'TRANS SKIPPED SPANNED MIDNIGHT'.                 01144199
           05 FILLER                   PIC X(1)    VALUE ','.           01144299
           05 PV-TRANS-SKIP-SPAN-MID   PIC X(8)    VALUE SPACE.         01144399
                                                                        01144499
      *  VALUES FOR EACH STORE                                          01144599
       01  PV-STORE-VALUES-ROW.                                         01144699
           05 PC-SORT-COL-STORE         PIC X(1)     VALUE 'C'.         01144799
           05 FILLER                    PIC X(1)     VALUE ','.         01144899
           05 PV-STORE-NUMBER           PIC X(4)     VALUE SPACES.      01144999
           05 FILLER                    PIC X(1)     VALUE ','.         01145099
           05 PV-STORE-DATE             PIC X(10)    VALUE SPACES.      01145199
           05 FILLER                    PIC X(1)     VALUE ','.         01145299
           05 PV-STORE-1-SEC-TRAN-COUNT PIC 9(8)     VALUE ZEROS.       01145599
           05 FILLER                    PIC X(1)     VALUE ','.         01145699
           05 PV-STORE-1-SEC-ELAP-TIME  PIC 9(8)     VALUE ZEROS.       01145799
           05 FILLER                    PIC X(1)     VALUE ','.         01145899
           05 PV-STORE-1-SEC-UNIT-SOLD  PIC 9(8)     VALUE ZEROS.       01145999
           05 FILLER                    PIC X(1)     VALUE ','.         01146099
           05 PV-STORE-5-SEC-TRAN-COUNT PIC 9(8)     VALUE ZEROS.       01146199
           05 FILLER                    PIC X(1)     VALUE ','.         01146299
           05 PV-STORE-5-SEC-ELAP-TIME  PIC 9(8)     VALUE ZEROS.       01146399
           05 FILLER                    PIC X(1)     VALUE ','.         01146499
           05 PV-STORE-5-SEC-UNIT-SOLD  PIC 9(8)     VALUE ZEROS.       01146599
                                                                        01146699
      * STORE HEADER ROW                                                01146799
       01  PC-STORE-HEADER-ROW.                                         01146899
           05 PC-SORT-COL-HEADER       PIC X(1)    VALUE 'B'.           01146999
           05 FILLER                   PIC X(1)    VALUE ','.           01147099
           05 PC-STORE-NUMBER          PIC X(12)    VALUE               01147199
                                       'STORE NUMBER'.                  01147299
           05 FILLER                   PIC X(1)     VALUE ','.          01147399
           05 PC-STORE-DATE            PIC X(4)     VALUE               01147499
                                       'DATE'.                          01147599
           05 FILLER                   PIC X(1)     VALUE ','.          01147699
           05 PC-STORE-1-SEC-TRAN      PIC X(24)    VALUE               01147999
                                       '1 SEC TRANSACTION COUNT'.       01148099
           05 FILLER                   PIC X(1)     VALUE ','.          01148199
           05 PC-STORE-ELAPSED-TIME    PIC X(18)    VALUE               01148299
                                       '1 SEC ELAPSED TIME'.            01148399
           05 FILLER                   PIC X(1)     VALUE ','.          01148499
           05 PC-STORE-UNITS-SOLD      PIC X(16)    VALUE               01148599
                                       '1 SEC UNITS SOLD'.              01148699
           05 FILLER                   PIC X(1)     VALUE ','.          01148799
           05 PC-STORE-5-SEC-TRAN      PIC X(23)    VALUE               01148899
                                       '5 SEC TRANSACTION COUNT'.       01148999
           05 FILLER                   PIC X(1)     VALUE ','.          01149099
           05 PC-STORE-ELAPSED-TIME    PIC X(18)    VALUE               01149199
                                       '5 SEC ELAPSED TIME'.            01149299
           05 FILLER                   PIC X(1)     VALUE ','.          01149399
           05 PC-STORE-UNITS-SOLD      PIC X(16)    VALUE               01149499
                                       '5 SEC UNITS SOLD'.              01149599
           05 FILLER                   PIC X(1)     VALUE ','.          01149699
                                                                        01149799
                                                                        01154091
      ******************************************************************01160082
      *    DB2 ABEND AREA                                             * 01170082
      ******************************************************************01180082
           COPY DPWS004.                                                01190082
                                                                        01200082
      ******************************************************************01210082
      *    DB2 AREA FOR COMMUNICATIONS                                * 01220082
      ******************************************************************01230082
           EXEC SQL                                                     01240082
                INCLUDE SQLCA                                           01250082
           END-EXEC.                                                    01260082
                                                                        01270082
      ***************************************************************** 01280082
      *  SQL AND COBOL DECLARATIONS FOR PARTITION TABLE (TEPPART)       01290082
      ***************************************************************** 01300082
                                                                        01310082
           EXEC SQL                                                     01320082
               INCLUDE TEPPART                                          01330082
           END-EXEC.                                                    01340082
                                                                        01350082
      ***************************************************************** 01360082
      *  SQL AND COBOL DECLARATIONS FOR THE TRAN TABLE (TEPTRN)         01370082
      ***************************************************************** 01380082
                                                                        01390082
           EXEC SQL                                                     01400082
               INCLUDE TEPTRN                                           01410082
           END-EXEC.                                                    01420082
                                                                        01430082
      ***************************************************************** 01520082
      *  SQL AND COBOL DECLARATIONS FOR THE TRAN TABLE (TEPITM)         01530082
      ***************************************************************** 01540082
                                                                        01550082
           EXEC SQL                                                     01560082
               INCLUDE TEPITM                                           01570082
           END-EXEC.                                                    01580082
                                                                        01590082
      ******************************************************************01680082
      *      DECLARE POS-TIME-CURSOR CURSOR WITH HOLD FOR               01681089
      *    POS-TIME CURSOR                                             *01690082
      ******************************************************************01700082
           EXEC SQL                                                     01710082
             DECLARE POS-TIME-CURSOR CURSOR WITH HOLD FOR               01720089
                 SELECT  B.SLS_DTE                                      01730082
                       , B.STR_NBR                                      01740082
                       , B.RGST_ID                                      01750082
                       , B.TRN_NBR                                      01760082
                       , B.STRT_TM                                      01770082
                       , B.END_TM                                       01780082
                       , B.END_TM + 1 SECOND                            01790082
                       , B.END_TM - B.STRT_TM                           01800082
                       , SUM(C.SLD_QTY)                                 01810082
                   FROM  TEPPART A                                      01820082
                       , TEPTRN  B                                      01830082
                       , TEPITM  C                                      01840082
                  WHERE  A.PARTN_NBR     = B.PARTN_NBR                  01850082
                    AND  A.PARTN_NBR     = C.PARTN_NBR                  01860082
                    AND  B.STR_NBR       = C.STR_NBR                    01870082
                    AND  B.RGST_ID       = C.RGST_ID                    01880082
                    AND  B.TRN_NBR       = C.TRN_NBR                    01890082
                    AND  B.STRT_TM       = C.STRT_TM                    01900082
                    AND  A.SLS_DTE       = B.SLS_DTE                    01910082
                    AND  B.SLS_DTE       = :CC-CONTROL-DATE             01930099
                    AND  B.TRN_STAT_CDE IN ('V','Z','L')                01960082
                    AND  B.TRN_TYP_CDE  IN ('01','02','21','24','25')   01970082
                    AND  B.SLS_CORR_SEQ_NBR = 0                         01980082
                    AND  B.VOID_ABRT_CDE = 'N'                          01990082
               GROUP BY  B.STR_NBR                                      02000082
                       , B.SLS_DTE                                      02010082
                       , B.RGST_ID                                      02020082
                       , B.TRN_NBR                                      02030082
                       , B.STRT_TM                                      02040082
                       , B.END_TM                                       02050082
               ORDER BY  B.STR_NBR                                      02060082
                       , B.SLS_DTE                                      02070082
                       , B.RGST_ID                                      02080082
                       , B.TRN_NBR                                      02090082
                       , B.STRT_TM                                      02100082
                       , B.END_TM                                       02110082
              WITH UR                                                   02120082
           END-EXEC.                                                    02130082
                                                                        02140082
      ******************************************************************02640099
      *      DECLARE POS-TIME-CURSOR CURSOR WITH HOLD FOR               02650099
      *    POS-TIME CURSOR                                             *02660099
      ******************************************************************02670099
           EXEC SQL                                                     02680099
             DECLARE POS-TIME-5-CURSOR CURSOR WITH HOLD FOR             02690099
                 SELECT  B.SLS_DTE                                      02700099
                       , B.STR_NBR                                      02710099
                       , B.RGST_ID                                      02720099
                       , B.TRN_NBR                                      02730099
                       , B.STRT_TM                                      02740099
                       , B.END_TM + 1 SECOND                            02750099
                       , B.END_TM + 5 SECOND                            02760099
                       , B.END_TM - B.STRT_TM                           02770099
                       , SUM(C.SLD_QTY)                                 02780099
                   FROM  TEPPART A                                      02790099
                       , TEPTRN  B                                      02800099
                       , TEPITM  C                                      02810099
                  WHERE  A.PARTN_NBR     = B.PARTN_NBR                  02820099
                    AND  A.PARTN_NBR     = C.PARTN_NBR                  02830099
                    AND  B.STR_NBR       = C.STR_NBR                    02840099
                    AND  B.RGST_ID       = C.RGST_ID                    02850099
                    AND  B.TRN_NBR       = C.TRN_NBR                    02860099
                    AND  B.STRT_TM       = C.STRT_TM                    02870099
                    AND  A.SLS_DTE       = B.SLS_DTE                    02880099
                    AND  B.SLS_DTE       = :CC-CONTROL-DATE             02900099
                    AND  B.TRN_STAT_CDE IN ('V','Z','L')                02930099
                    AND  B.TRN_TYP_CDE  IN ('01','02','21','24','25')   02940099
                    AND  B.SLS_CORR_SEQ_NBR = 0                         02950099
                    AND  B.VOID_ABRT_CDE = 'N'                          02960099
               GROUP BY  B.STR_NBR                                      02970099
                       , B.SLS_DTE                                      02980099
                       , B.RGST_ID                                      02990099
                       , B.TRN_NBR                                      03000099
                       , B.STRT_TM                                      03010099
                       , B.END_TM                                       03020099
               ORDER BY  B.STR_NBR                                      03030099
                       , B.SLS_DTE                                      03031099
                       , B.RGST_ID                                      03032099
                       , B.TRN_NBR                                      03033099
                       , B.STRT_TM                                      03034099
                       , B.END_TM                                       03035099
              WITH UR                                                   03036099
           END-EXEC.                                                    03037099
                                                                        03038099
                                                                        03039099
      ******************************************************************03045082
       PROCEDURE DIVISION.                                              03050082
      ******************************************************************03060082
      *   DATES REQUESTED WILL FALL IN OPERATIONAL TABLES ONLY.         03070099
      *   REMOVED COSA HISTORICAL TABLES REFERENCES.                    03080099
      ******************************************************************03100082
       0000-MAINLINE.                                                   03110082
                                                                        03120082
      *    LOAD THE CONTROL CARD FROM JCL                               03120199
           PERFORM 0500-PROCESS-CONTROL-CARD.                           03121099
                                                                        03122099
      *    LOOP THROUGH SQL 7 TIMES, ONE FOR EACH DAY, AND WRITE A      03123099
      *    RECORD FOR EACH STORES VALUE                                 03123199
           PERFORM 0900-PROCESS-WEEK-DAYS                               03124099
             UNTIL PV-NUMBER-DAYS >= PC-NUMBER-PROCESS-DAYS.            03125099
                                                                        03132099
      *    DISPLAY THE TOTALS FROM THE REPORT.                          03132199
           PERFORM 9000-EOJ-ROUTINE.                                    03133099
                                                                        03300082
           STOP RUN.                                                    03310082
                                                                        03320082
      ******************************************************************03320199
      * RESET THE VALUES FOR PROGRAM VARIABLES                          03320299
      ******************************************************************03320399
       0100-INITIALIZE-ALL.                                             03320499
           INITIALIZE  PREV-STORE                                       03320599
                       PREV-REGISTER                                    03320699
                       PREV-TRAN                                        03320799
                       PREV-ELAPSED-TIME                                03320899
                       PREV-SALES-DATE                                  03320999
                       PREV-END-TIME-PLUS-1-SECOND                      03321099
                       PREV-END-TIME-PLUS-5-SECOND                      03321199
                       PREV-UNITS-SOLD                                  03321299
                       PREV-UNITS-5-SEC-SOLD                            03321399
                       PV-PROGRAM-VARIABLES                             03321499
                       PT-STORE-TABLE.                                  03321599
                                                                        03322899
      ******************************************************************03322999
      *     OPEN FILES. READ CONTROL CARD TO GET POS DATE IN MMDDYY     03323199
      *     FORMAT. CALL DATE ROUTINE TO CONVERT DATE TO A CCYY-MM-DD   03324099
      *     PROCESS DATE.                                               03325099
      ******************************************************************03326099
       0500-PROCESS-CONTROL-CARD.                                       03326199
                                                                        03327099
           OPEN INPUT  DATE-CARD-FILE                                   03328099
                OUTPUT TRAN-TIME-FILE.                                  03329099
                                                                        03329199
           READ DATE-CARD-FILE INTO CC-CONTROL-CARD                     03329299
              AT END                                                    03329399
                 SET PS-END-OF-FILE-YES TO TRUE.                        03329499
                                                                        03329599
           CLOSE DATE-CARD-FILE.                                        03329699
                                                                        03329799
           IF PS-END-OF-FILE-YES                                        03329899
               MOVE '0500-PROCESS-CONTROL-CARD'  TO PV-PARAGRAPH-NAME   03329999
               MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1  03330099
               SET AC-CONTROL-CARD-ERROR TO TRUE                        03330199
               PERFORM 9999-ABEND                                       03330299
           END-IF.                                                      03330399
                                                                        03330499
      ******************************************************************03335599
      * GET THE PRIOR DAY FOR CURRENT PROCESSING                        03335699
      ******************************************************************03335799
       0560-GET-PRIOR-DATE.                                             03335899
                                                                        03335999
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03336099
                                                                        03336199
           SET DPG52-LK-DTE-ISO-FMT                                     03336399
               DPG51-DECREMENT-DATE                                     03336499
               DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                      03336599
                                                                        03336699
           MOVE CC-CONTROL-DATE TO DPG52-LK-DATE-INPUT.                 03336799
           MOVE -1       TO DPG51-INCR-DECR-DAYS-9.                     03336999
                                                                        03337099
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                03337199
                                                   DPG52                03337299
                                                   DPG53                03337399
                                                   DPG54                03337499
                                                   DPG55                03337599
                                                   DPG56.               03337699
                                                                        03337799
           EVALUATE TRUE                                                03337899
              WHEN DPG54-NO-ERROR                                       03337999
                  MOVE DPG55-DB2-ISO-DATE-FMT TO CC-CONTROL-DATE        03338099
                                                                        03338199
              WHEN DPG54-WARNING-ERROR                                  03338299
              WHEN DPG54-SEVERE-ERROR                                   03338399
                  DISPLAY '           '                                 03338499
                  DISPLAY 'SYS-DATE  = ' CC-CONTROL-DATE                03338599
                                                                        03338699
                   MOVE '0560-CONVERT-INPUT-DATE'                       03338799
                                                 TO PV-PARAGRAPH-NAME   03338899
                   MOVE 'ERROR CONVERTING INPUT CARD DATE'              03338999
                                                 TO PV-OPERATION-MSG-1  03339099
                   SET AC-CALENDAR-ROUTINE-ERROR TO TRUE                03339199
                   MOVE DPG54-ERROR-MESSAGE      TO PV-OPERATION-MSG-2  03339299
                   PERFORM 9999-ABEND                                   03339399
                                                                        03340199
           END-EVALUATE.                                                03340299
                                                                        03340399
      ******************************************************************03340599
      * THIS PROCESS WILL CREATE A REPORT ROW FOR EACH STORE PER DAY    03340699
      ******************************************************************03340799
       0900-PROCESS-WEEK-DAYS.                                          03340899
                                                                        03341099
      *    GENERATE RESULTS FOR 1 SECOND OR LESS TRANSACTION            03342099
           PERFORM 1000-PROCESS-OPER-TRANS.                             03342999
                                                                        03343199
      *    GENERATE RESULTS FOR 1-5 SECOND TRANSACTIONS                 03343299
           PERFORM 1050-PROCESS-OPER-5-SEC-TRANS.                       03343399
                                                                        03345699
      *    WRITE RECORDS FOR EACH STORE TO A FILE                       03345799
           PERFORM 9050-CYCLE-STORE-TOTALS.                             03345899
                                                                        03345999
      *    GET THE PREVIOUS DAY FROM THE LAST REPORT CYCLE              03346099
      *    PROGRAM WILL CONTINUE THIS UNTIL 7 DAYS HAVE RUN.            03346199
           PERFORM 0560-GET-PRIOR-DATE.                                 03346299
           ADD +1 TO PV-NUMBER-DAYS.                                    03346399
                                                                        03346499
           PERFORM 0100-INITIALIZE-ALL.                                 03346599
                                                                        03346699
      *----------------------------------------------------------------*03346799
      *  RETRIEVE DATA FROM THE POS-TIME CURSOR                        *03347082
      *----------------------------------------------------------------*03350082
       1000-PROCESS-OPER-TRANS.                                         03360082
                                                                        03370082
           SET PS-NOT-END-OF-CURSOR TO TRUE.                            03380082
           PERFORM 1100-OPEN-POS-TIME-CURSOR.                           03390082
           PERFORM 1200-FETCH-POS-TIME-CURSOR.                          03400082
           PERFORM UNTIL PS-END-OF-CURSOR                               03410082
               IF  EPTRN-RGST-ID < 25                                   03420082
                   IF  EPTRN-STR-NBR  = PREV-STORE                      03430082
                   AND EPTRN-RGST-ID  = PREV-REGISTER                   03440082
                   AND EPTRN-SLS-DTE  = PREV-SALES-DATE                 03450082
                   AND EPTRN-STRT-TM <= PREV-END-TIME-PLUS-1-SECOND     03460082
      **           IF ANY OF THE ABOVE CHANGE, THEN THE PREV TRAN       03470082
      **           WASN'T FOLLOWED BY ANOTHER WITHIN 1 SECOND           03480082
      **           ELAPSED TIME < 0 INDICATES TRANSACTIONS              03500099
      **           CROSSING MIDNIGHT - SKIP THEM.                       03510099
                       IF PREV-ELAPSED-TIME > 0                         03511099
                           PERFORM 3000-PROCESS-POS-TIME                03520082
                       ELSE                                             03530099
                           ADD +1   TO PT-MIDNIGHT-SKIPPED              03540099
                       END-IF                                           03550099
                   ELSE                                                 03560082
                       IF PREV-STORE  > ZEROES                          03570082
                           ADD +1   TO PT-GT-1-SECOND-TRANS-SKIPPED     03580082
                       END-IF                                           03590082
                   END-IF                                               03600082
               ELSE                                                     03610082
                   ADD +1            TO PT-REGISTER-TRANS-SKIPPED       03620082
               END-IF                                                   03630082
               MOVE EPTRN-SLS-DTE           TO PREV-SALES-DATE          03640082
               MOVE EPTRN-STR-NBR           TO PREV-STORE               03650082
               MOVE EPTRN-RGST-ID           TO PREV-REGISTER            03660082
               MOVE EPTRN-TRN-NBR           TO PREV-TRAN                03670082
               MOVE PV-ELAPSED-TIME         TO PREV-ELAPSED-TIME        03680082
               MOVE PV-END-TIME-PLUS-1-SECOND                           03690082
                                     TO PREV-END-TIME-PLUS-1-SECOND     03700082
               MOVE EPITM-SLD-QTY           TO PREV-UNITS-SOLD          03710082
               PERFORM 1200-FETCH-POS-TIME-CURSOR                       03720082
           END-PERFORM.                                                 03730082
                                                                        03731099
           PERFORM 1400-CLOSE-POS-TIME-CURSOR.                          03740082
                                                                        03741199
      *----------------------------------------------------------------*03742099
      *  RETRIEVE DATA FROM THE POS-TIME CURSOR                        *03743099
      *----------------------------------------------------------------*03744099
       1050-PROCESS-OPER-5-SEC-TRANS.                                   03745099
                                                                        03746099
           SET PS-NOT-END-OF-CURSOR TO TRUE.                            03747099
           PERFORM 1150-OPEN-POS-TIME-5-CURSOR.                         03749099
           PERFORM 1250-FETCH-POS-TIME-5-CURSOR.                        03749299
           PERFORM UNTIL PS-END-OF-CURSOR                               03749399
               IF  EPTRN-RGST-ID < 25                                   03749499
                   IF  EPTRN-STR-NBR  = PREV-STORE                      03749599
                   AND EPTRN-RGST-ID  = PREV-REGISTER                   03749699
                   AND EPTRN-SLS-DTE  = PREV-SALES-DATE                 03749799
                   AND EPTRN-STRT-TM >= PREV-END-TIME-PLUS-1-SECOND     03749899
                   AND EPTRN-STRT-TM <= PREV-END-TIME-PLUS-5-SECOND     03749999
      **           IF ANY OF THE ABOVE CHANGE, THEN THE PREV TRAN       03750099
      **           WASN'T FOLLOWED BY ANOTHER BETWEEN 1 AND 5 SECONDS   03750199
                       IF PREV-ELAPSED-TIME > 0                         03750299
      **               ELAPSED TIME < 0 INDICATES TRANSACTIONS          03750399
      **               CROSSING MIDNIGHT - SKIP THEM.                   03750499
                           PERFORM 3050-PROCESS-POS-TIME                03750599
                       ELSE                                             03750699
                           ADD +1   TO PT-MIDNIGHT-SKIPPED              03750799
                       END-IF                                           03750899
                   ELSE                                                 03750999
                       IF PREV-STORE  > ZEROES                          03751099
                           ADD +1   TO PT-LT-5-SECOND-TRANS-SKIPPED     03751199
                       END-IF                                           03751299
                   END-IF                                               03751399
               END-IF                                                   03751699
               MOVE EPTRN-SLS-DTE           TO PREV-SALES-DATE          03751799
               MOVE EPTRN-STR-NBR           TO PREV-STORE               03751899
               MOVE EPTRN-RGST-ID           TO PREV-REGISTER            03751999
               MOVE EPTRN-TRN-NBR           TO PREV-TRAN                03752099
               MOVE PV-ELAPSED-TIME         TO PREV-ELAPSED-TIME        03752199
               MOVE PV-END-TIME-PLUS-1-SECOND                           03752299
                                     TO PREV-END-TIME-PLUS-1-SECOND     03752399
               MOVE PV-END-TIME-PLUS-5-SECOND                           03752499
                                     TO PREV-END-TIME-PLUS-5-SECOND     03752599
               MOVE EPITM-SLD-QTY           TO PREV-UNITS-5-SEC-SOLD    03752699
               PERFORM 1250-FETCH-POS-TIME-5-CURSOR                     03752799
           END-PERFORM.                                                 03752899
                                                                        03752999
           PERFORM 1450-CLOSE-POS-TIME-5-CURSOR.                        03753099
                                                                        03754082
      *----------------------------------------------------------------*03760082
      *    OPEN THE POS-TIME CURSOR.                                   *03770082
      *----------------------------------------------------------------*03780082
       1100-OPEN-POS-TIME-CURSOR.                                       03790082
                                                                        03800082
           EXEC SQL                                                     03810082
                OPEN POS-TIME-CURSOR                                    03820082
           END-EXEC.                                                    03830082
           EVALUATE TRUE                                                03840082
               WHEN SQLCODE = ZERO                                      03850082
                   CONTINUE                                             03860082
               WHEN OTHER                                               03870082
                   MOVE '1100-OPEN-POS-TIME-CURSOR'                     03880082
                                    TO PV-PARAGRAPH-NAME                03890082
                   MOVE 'OPEN CURSOR FOR TABLES TEPTRN,TEPITM'          03900082
                                    TO PV-DB2-FUNCTION                  03910082
                   PERFORM 9990-DB2-ABEND                               03920082
           END-EVALUATE.                                                03930082
                                                                        03940082
      *----------------------------------------------------------------*03941099
      *    OPEN THE POS-TIME CURSOR.                                   *03942099
      *----------------------------------------------------------------*03943099
       1150-OPEN-POS-TIME-5-CURSOR.                                     03944099
                                                                        03945099
           EXEC SQL                                                     03946099
                OPEN POS-TIME-5-CURSOR                                  03947099
           END-EXEC.                                                    03948099
           EVALUATE TRUE                                                03949099
               WHEN SQLCODE = ZERO                                      03949199
                   CONTINUE                                             03949299
               WHEN OTHER                                               03949399
                   MOVE '1150-OPEN-POS-TIME-CURSOR'                     03949499
                                    TO PV-PARAGRAPH-NAME                03949599
                   MOVE 'OPEN CURSOR FOR TABLES TEPTRN,TEPITM'          03949699
                                    TO PV-DB2-FUNCTION                  03949799
                   PERFORM 9990-DB2-ABEND                               03949899
           END-EVALUATE.                                                03949999
                                                                        03950099
      *----------------------------------------------------------------*03951082
      *   FETCH THE POS-TIME CURSOR.                                   *03960082
      *----------------------------------------------------------------*03970082
       1200-FETCH-POS-TIME-CURSOR.                                      03980082
                                                                        03990082
           EXEC SQL                                                     04000082
               FETCH POS-TIME-CURSOR                                    04010082
               INTO  :EPTRN-SLS-DTE                                     04020082
                   , :EPTRN-STR-NBR                                     04030082
                   , :EPTRN-RGST-ID                                     04040082
                   , :EPTRN-TRN-NBR                                     04050082
                   , :EPTRN-STRT-TM                                     04060082
                   , :EPTRN-END-TM                                      04070082
                   , :PV-END-TIME-PLUS-1-SECOND                         04080082
                   , :PV-ELAPSED-TIME                                   04090082
                   , :EPITM-SLD-QTY                                     04100082
           END-EXEC.                                                    04110082
           EVALUATE TRUE                                                04120082
               WHEN SQLCODE = ZERO                                      04130082
                   ADD +1 TO PT-TOTAL-TRANS-READ                        04140082
               WHEN SQLCODE = +100                                      04150082
                   SET PS-END-OF-CURSOR TO TRUE                         04160082
                   IF PREV-STORE > ZEROES                               04170082
      **               BECAUSE THE LAST RECORD CAN'T BE FOLLOWED BY     04180082
      **               A TRAN WITHIN 1 SECOND, IT WILL ALWAYS BE        04190082
      **               SKIPPED.                                         04200082
                       ADD +1   TO PT-GT-1-SECOND-TRANS-SKIPPED         04210082
                   END-IF                                               04220082
               WHEN OTHER                                               04230082
                   MOVE '1200-FETCH-POS-TIME-CURSOR'                    04240082
                                    TO PV-PARAGRAPH-NAME                04250082
                   MOVE 'FETCH CURSOR FOR TABLES TEPTRN,TEPITM'         04260082
                                    TO PV-DB2-FUNCTION                  04270082
                   PERFORM 9990-DB2-ABEND                               04280082
           END-EVALUATE.                                                04290082
                                                                        04300082
                                                                        04310099
      *----------------------------------------------------------------*04310199
      *   FETCH THE POS-TIME CURSOR.                                   *04310299
      *----------------------------------------------------------------*04310399
       1250-FETCH-POS-TIME-5-CURSOR.                                    04310499
                                                                        04310599
           EXEC SQL                                                     04310699
               FETCH POS-TIME-5-CURSOR                                  04310799
               INTO  :EPTRN-SLS-DTE                                     04310899
                   , :EPTRN-STR-NBR                                     04310999
                   , :EPTRN-RGST-ID                                     04311099
                   , :EPTRN-TRN-NBR                                     04311199
                   , :EPTRN-STRT-TM                                     04311299
                   , :PV-END-TIME-PLUS-1-SECOND                         04311399
                   , :PV-END-TIME-PLUS-5-SECOND                         04311499
                   , :PV-ELAPSED-TIME                                   04311599
                   , :EPITM-SLD-QTY                                     04311699
           END-EXEC.                                                    04311799
           EVALUATE TRUE                                                04311899
               WHEN SQLCODE = ZERO                                      04311999
                   ADD +1 TO PT-TOTAL-TRANS-READ                        04312099
               WHEN SQLCODE = +100                                      04312199
                   SET PS-END-OF-CURSOR TO TRUE                         04312299
                   IF PREV-STORE > ZEROES                               04312399
      **               BECAUSE THE LAST RECORD CAN'T BE FOLLOWED BY     04312499
      **               A TRAN WITHIN 1 SECOND, IT WILL ALWAYS BE        04312599
      **               SKIPPED.                                         04312699
                       ADD +1   TO PT-LT-5-SECOND-TRANS-SKIPPED         04312799
                   END-IF                                               04312899
               WHEN OTHER                                               04312999
                   MOVE '1250-FETCH-POS-TIME-5-CURSOR'                  04313099
                                    TO PV-PARAGRAPH-NAME                04313199
                   MOVE 'FETCH CURSOR FOR TABLES TEPTRN,TEPITM'         04313299
                                    TO PV-DB2-FUNCTION                  04313399
                   PERFORM 9990-DB2-ABEND                               04313499
           END-EVALUATE.                                                04313599
                                                                        04313699
                                                                        04318082
      *----------------------------------------------------------------*04320082
      *   CLOSE THE POS-TIME CURSOR.                                   *04330082
      *----------------------------------------------------------------*04340082
       1400-CLOSE-POS-TIME-CURSOR.                                      04350082
           EXEC SQL                                                     04360082
                CLOSE POS-TIME-CURSOR                                   04370082
           END-EXEC.                                                    04380082
           EVALUATE TRUE                                                04390082
               WHEN SQLCODE = ZERO                                      04400082
                   CONTINUE                                             04410082
               WHEN OTHER                                               04420082
                   MOVE '1400-CLOSE-POS-TIME-CURSOR'                    04430082
                                    TO PV-PARAGRAPH-NAME                04440082
                   MOVE 'CLOSE CURSOR FOR TABLES TEPTRN,TEPITM'         04450082
                                    TO PV-DB2-FUNCTION                  04460082
                   PERFORM 9990-DB2-ABEND                               04470082
           END-EVALUATE.                                                04480082
                                                                        04490082
      *----------------------------------------------------------------*04491099
      *   CLOSE THE POS-TIME CURSOR.                                   *04492099
      *----------------------------------------------------------------*04493099
       1450-CLOSE-POS-TIME-5-CURSOR.                                    04494099
           EXEC SQL                                                     04495099
                CLOSE POS-TIME-5-CURSOR                                 04496099
           END-EXEC.                                                    04497099
           EVALUATE TRUE                                                04498099
               WHEN SQLCODE = ZERO                                      04499099
                   CONTINUE                                             04499199
               WHEN OTHER                                               04499299
                   MOVE '1450-CLOSE-POS-TIME-5-CURSOR'                  04499399
                                    TO PV-PARAGRAPH-NAME                04499499
                   MOVE 'CLOSE CURSOR FOR TABLES TEPTRN,TEPITM'         04499599
                                    TO PV-DB2-FUNCTION                  04499699
                   PERFORM 9990-DB2-ABEND                               04499799
           END-EVALUATE.                                                04499899
                                                                        04499999
      *----------------------------------------------------------------*05680082
      *  ADD TO STORE TOTALS (TABLE SET TO HANDLE 2000 STORES)         *05690099
      *  BECAUSE WE DON'T PROCESS A RECORD UNTIL WE KNOW THAT THE NEXT *05700082
      *  RECORD STARTED WITHIN 1 SECOND AFTER IT, THIS PARAGRAPH IS    *05710082
      *  ALWAYS PROCESSING THE PREVIOS RECORD FETCHED, NOT THE CURRENT *05720082
      *  RECORD.                                                       *05730082
      *  - FOR EACH NEW STORE, ADD STORE TO NEXT OPEN TABLE ENTRY.     *05740082
      *  - ABEND IF TABLE OVERFLOWS.                                   *05750082
      *  - SINCE STORES ARE FETCHED IN SEQUENCE AND THEY ARE LOADED    *05760082
      *    TO TABLE ON THEIR FIRST OCCURENCE, THE MATCH CONDITION      *05770082
      *    SHOULD BE HIT ONLY ONCE PER STORE IF IT IS FOUND IN         *05780099
      *    THE OPERATIONAL TABLES.                                     *05790099
      *----------------------------------------------------------------*05800082
       3000-PROCESS-POS-TIME.                                           05810082
                                                                        05820082
           PERFORM 4000-FIND-STORE.                                     05821099
                                                                        05822099
           ADD 1           TO  PT-STORE-TRAN-1-SEC-CNT (STORE-SUB)      06050099
                               PT-TOTAL-TRAN-CNT.                       06060099
           MOVE PREV-ELAPSED-TIME TO  PV-ELAPSED-TIME-NUM.              06070099
           COMPUTE PT-STORE-ELAPSED-TIME (STORE-SUB) =                  06080099
                   PT-STORE-ELAPSED-TIME (STORE-SUB) +                  06090099
                  (PV-ELAPSED-HOURS * 3600)          +                  06100099
                  (PV-ELAPSED-MINUTES * 60)          +                  06110099
                   PV-ELAPSED-SECONDS.                                  06120099
           COMPUTE PT-TOTAL-ELAPSED-TIME     =                          06130099
                   PT-TOTAL-ELAPSED-TIME     +                          06140099
                  (PV-ELAPSED-HOURS * 3600)  +                          06150099
                  (PV-ELAPSED-MINUTES * 60)  +                          06160099
                   PV-ELAPSED-SECONDS.                                  06170099
           ADD PREV-UNITS-SOLD TO PT-STORE-UNITS-SOLD (STORE-SUB)       06180099
                                 PT-TOTAL-UNITS-SOLD.                   06190099
                                                                        06200082
      *----------------------------------------------------------------*06210099
      *  ADD TO STORE TOTALS (TABLE SET TO HANDLE 2000 STORES)         *06220099
      *  BECAUSE WE DON'T PROCESS A RECORD UNTIL WE KNOW THAT THE NEXT *06230099
      *  RECORD STARTED BETWEEN 1 AND 5 SEC AFTER.THIS PARAGRAPH IS    *06240099
      *  ALWAYS PROCESSING THE PREVIOS RECORD FETCHED, NOT THE CURRENT *06250099
      *  RECORD.                                                       *06260099
      *  - FOR EACH NEW STORE, ADD STORE TO NEXT OPEN TABLE ENTRY.     *06270099
      *  - ABEND IF TABLE OVERFLOWS.                                   *06280099
      *  - SINCE STORES ARE FETCHED IN SEQUENCE AND THEY ARE LOADED    *06290099
      *    TO TABLE ON THEIR FIRST OCCURENCE, THE MATCH CONDITION      *06291099
      *    SHOULD BE HIT ONLY ONCE PER STORE IF IT IS FOUND IN         *06292099
      *    THE OPERATIONAL TABLES.                                     *06293099
      *----------------------------------------------------------------*06294099
       3050-PROCESS-POS-TIME.                                           06295099
                                                                        06296099
           PERFORM 4000-FIND-STORE.                                     06296199
                                                                        06301199
           ADD 1            TO  PT-STORE-TRAN-5-SEC-CNT (STORE-SUB)     06301699
                                PT-TOTAL-TRAN-CNT.                      06301799
           MOVE PREV-ELAPSED-TIME TO  PV-ELAPSED-TIME-NUM.              06301899
           COMPUTE PT-STORE-ELAPSED-5-SEC-TIME  (STORE-SUB) =           06301999
                   PT-STORE-ELAPSED-5-SEC-TIME  (STORE-SUB) +           06302099
                  (PV-ELAPSED-HOURS * 3600)          +                  06302199
                  (PV-ELAPSED-MINUTES * 60)          +                  06302299
                   PV-ELAPSED-SECONDS.                                  06302399
           COMPUTE PT-TOTAL-ELAPSED-TIME     =                          06302499
                   PT-TOTAL-ELAPSED-TIME     +                          06302599
                  (PV-ELAPSED-HOURS * 3600)  +                          06302699
                  (PV-ELAPSED-MINUTES * 60)  +                          06302799
                   PV-ELAPSED-SECONDS.                                  06302899
           ADD PREV-UNITS-5-SEC-SOLD TO                                 06302999
                             PT-STORE-UNITS-5-SEC-SOLD (STORE-SUB)      06303099
                             PT-TOTAL-UNITS-SOLD.                       06303199
                                                                        06303399
      *----------------------------------------------------------------*06303499
      *  DISPLAY THE GRAND TOTALS FOR THE REPORT                        06303599
      *----------------------------------------------------------------*06303699
       4000-FIND-STORE.                                                 06303799
           IF PREV-STORE NOT = PV-LAST-STORE-PROCESSED                  06303899
               PERFORM VARYING SUB FROM 1 BY 1                          06303999
                 UNTIL PT-STORE-NBR (SUB) = PREV-STORE                  06304099
                    OR PT-STORE-NBR (SUB) = ZEROES                      06304199
                    OR SUB > PC-MAX-NUMBER-STORES                       06304299
                       MOVE SUB   TO STORE-SUB                          06304399
               END-PERFORM                                              06304599
               MOVE SUB           TO STORE-SUB                          06304699
               IF PT-STORE-NBR (SUB) = ZEROES                           06304799
      **           STORE NOT FOUND ON TABLE                             06304899
                   MOVE PREV-STORE     TO  PT-STORE-NBR (STORE-SUB)     06304999
                                           PV-LAST-STORE-PROCESSED      06305099
               END-IF                                                   06305199
               IF SUB > PC-MAX-NUMBER-STORES                            06305299
                   SET ABEND-1111-TABLE-OVERFLOW  TO TRUE               06305399
                   DISPLAY '3050-PROCESS-POS-TIME'                      06305499
                   DISPLAY 'STORE TABLE OVERFLOW - NBR OF ENTRIES = '   06305599
                            SUB                                         06305699
                   CALL 'ILBOABN0' USING ABEND-CODE                     06305799
               END-IF                                                   06305899
           END-IF.                                                      06305999
                                                                        06306099
                                                                        06306199
      *----------------------------------------------------------------*06307082
      *  DISPLAY THE GRAND TOTALS FOR THE REPORT                        06310099
      *----------------------------------------------------------------*06320082
       9000-EOJ-ROUTINE.                                                06330082
                                                                        06340082
           MOVE  PT-TOTAL-TRANS-READ          TO PV-TOTAL-TRAN-READ.    06371099
           MOVE  PT-GT-1-SECOND-TRANS-SKIPPED TO PV-TRAN-SKIP-GT-1-SEC. 06411099
           MOVE  PT-LT-5-SECOND-TRANS-SKIPPED TO PV-TRAN-SKIP-LT-5-SEC. 06412099
           MOVE  PT-REGISTER-TRANS-SKIPPED    TO PV-TRAN-SKIP-REG-24.   06451099
           MOVE  PT-MIDNIGHT-SKIPPED          TO PV-TRANS-SKIP-SPAN-MID.06491099
                                                                        06520082
           WRITE TRAN-TIME-RECORD FROM PC-REPORT-TOTAL-HEADER-ROW-1.    06521099
           WRITE TRAN-TIME-RECORD FROM PC-REPORT-TOTAL-HEADER-ROW-2.    06521199
           WRITE TRAN-TIME-RECORD FROM PC-REPORT-TOTAL-HEADER-ROW-3.    06521299
           WRITE TRAN-TIME-RECORD FROM PC-REPORT-TOTAL-HEADER-ROW-4.    06521399
           WRITE TRAN-TIME-RECORD FROM PC-REPORT-TOTAL-HEADER-ROW-5.    06521499
           WRITE TRAN-TIME-RECORD FROM PC-STORE-HEADER-ROW.             06521699
                                                                        06620082
      *----------------------------------------------------------------*06620199
      *      CYCLE THROUGH THE STORES AND DISPLAY THE VALUES           *06620299
      *----------------------------------------------------------------*06620399
       9050-CYCLE-STORE-TOTALS.                                         06621099
                                                                        06622199
           PERFORM VARYING SUB FROM 1 BY 1                              06622299
             UNTIL PT-STORE-NBR (SUB) = ZEROES                          06622399
                OR SUB > PC-MAX-NUMBER-STORES                           06622499
                   MOVE SUB   TO STORE-SUB                              06622699
                   PERFORM 9100-DISPLAY-STORE-TOTALS                    06622799
           END-PERFORM.                                                 06622899
                                                                        06623099
      *----------------------------------------------------------------*06630082
      *      CALCULATE AND DISPLAY STORE TOTALS FOR THE MONTH          *06640082
      *----------------------------------------------------------------*06650082
       9100-DISPLAY-STORE-TOTALS.                                       06660082
                                                                        06740082
           MOVE PT-STORE-NBR (STORE-SUB)             TO PV-STORE-NUMBER.06771099
           MOVE CC-CONTROL-DATE                      TO PV-STORE-DATE.  06772099
           MOVE PT-STORE-TRAN-1-SEC-CNT (STORE-SUB)  TO                 06801199
                                             PV-STORE-1-SEC-TRAN-COUNT. 06802099
           MOVE PT-STORE-TRAN-5-SEC-CNT  (STORE-SUB) TO                 06803099
                                             PV-STORE-5-SEC-TRAN-COUNT. 06804099
           MOVE PT-STORE-ELAPSED-TIME (STORE-SUB)    TO                 06831599
                                             PV-STORE-1-SEC-ELAP-TIME.  06832099
           MOVE PT-STORE-ELAPSED-5-SEC-TIME (STORE-SUB) TO              06832199
                                             PV-STORE-5-SEC-ELAP-TIME.  06832299
           MOVE PT-STORE-UNITS-SOLD (STORE-SUB)      TO                 06832399
                                             PV-STORE-1-SEC-UNIT-SOLD.  06833099
           MOVE PT-STORE-UNITS-5-SEC-SOLD (STORE-SUB) TO                06833199
                                             PV-STORE-5-SEC-UNIT-SOLD.  06833299
                                                                        06837099
           WRITE TRAN-TIME-RECORD FROM PV-STORE-VALUES-ROW.             06941399
           INITIALIZE TRAN-TIME-RECORD.                                 06941499
                                                                        06941599
                                                                        06960082
      *----------------------------------------------------------------*07300082
      *      PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *07310082
      *----------------------------------------------------------------*07320082
       9990-DB2-ABEND.                                                  07330082
                                                                        07340082
           DISPLAY '** ABEND **'.                                       07350082
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        07360082
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07370082
           DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                07380082
                                                                        07390082
      * ROUTINE TO DISPLAY ABEND IN READABLE FORMAT                     07400082
           COPY DPPD004.                                                07410082
                                                                        07420082
           SET ABEND-2222-DB2-ERROR TO TRUE.                            07430082
           CALL 'ILBOABN0' USING ABEND-CODE.                            07440082
                                                                        07441099
      ******************************************************************07450099
       9999-ABEND.                                                      07460099
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  07470099
      ******************************************************************07480099
           DISPLAY '** ABEND           **'.                             07490099
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  07500099
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07510099
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1        07520099
           DISPLAY '** PARTITION       ** = ' EPTRN-PARTN-NBR           07530099
           DISPLAY '** STORE           ** = ' EPTRN-STR-NBR             07540099
           DISPLAY '** REGISTER        ** = ' EPTRN-RGST-ID             07550099
           DISPLAY '** TRANSACTION     ** = ' EPTRN-TRN-NBR             07560099
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07570099
                                                                        07580099
