       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.    MLKUT475.                                         00020007
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030001
       DATE-WRITTEN.  10-23-2000.                                       00040007
       DATE-COMPILED.                                                   00050001
                                                                        00060007
      ******************************************************************00070001
      * MLKUT475 - DESCRIPTION.............                             00080007
      * THIS PROGRAM WILL CREATE THE DATA INTEGRITY EXTRACT FILE FOR    00090001
      * TMNMCLC.                                                        00100007
W26603******************************************************************00110012
W26603* CHG ID   DATE        DESCRIPTION                               *00120012
W26603* ------   ----------  ----------------------------------------- *00130012
W26603* 26603    03-17-2004  STORE EXPANSION.                          *00140012
W26603*                      INCREASED LRECL FOR OUT01 BECAUSE OF      *00150012
W26603*                      CHANGES TO MLRD153.                       *00160012
W26603*                      EXPANDED WORKING STORAGE VARIABLE.        *00170012
W26603*                      ADDED 'WITH UR' TO A SQL SELECT.          *00180012
W26603*                      - RONNA MCDONALD                          *00190012
      ******************************************************************00200001
      *                                                                 00210001
       ENVIRONMENT DIVISION.                                            00220001
      *                                                                 00230001
       CONFIGURATION SECTION.                                           00240001
      *                                                                 00250001
       SOURCE-COMPUTER.        IBM-3090.                                00260001
       OBJECT-COMPUTER.        IBM-3090.                                00270001
      *                                                                 00280001
       INPUT-OUTPUT SECTION.                                            00290001
       FILE-CONTROL.                                                    00300001
                                                                        00310001
           SELECT DATA-INTEGRITY-CTL-FILE                               00320001
               ASSIGN TO INP01.                                         00330001
                                                                        00340001
           SELECT DATA-INTEGRITY-EXTR-FILE                              00350001
               ASSIGN TO OUT01.                                         00360001
                                                                        00370001
      ******************************************************************00380001
       DATA DIVISION.                                                   00390001
      ******************************************************************00400001
                                                                        00410001
       FILE SECTION.                                                    00420001
                                                                        00430001
       FD  DATA-INTEGRITY-CTL-FILE                                      00440001
           LABEL RECORDS ARE STANDARD                                   00450001
           RECORDING MODE IS F                                          00460001
           BLOCK CONTAINS 0 RECORDS.                                    00470001
       01  DATA-INTEGRITY-CTL-REC      PIC X(80).                       00480001
                                                                        00490001
       FD  DATA-INTEGRITY-EXTR-FILE                                     00500001
           RECORDING MODE IS F                                          00510001
           LABEL RECORDS ARE STANDARD                                   00520001
           BLOCK CONTAINS 0 RECORDS.                                    00530001
                                                                        00540001
W26603 01  DATA-INTEGRITY-EXTR-REC     PIC X(120).                      00550012
                                                                        00560001
      *                                                                 00570001
      ******************************************************************00580001
       WORKING-STORAGE SECTION.                                         00590001
      ******************************************************************00600001
                                                                        00610001
       01  PS-PROGRAM-SWITCHES.                                         00620001
           05  PS-TMNMCLC-DONE         PIC X         VALUE 'N'.         00630012
               88  PS-TMNMCLC-IS-DONE                VALUE 'Y'.         00640012
               88  PS-TMNMCLC-IS-NOT-DONE            VALUE 'N'.         00650012
                                                                        00660001
       01  PC-PROGRAM-CONSTANTS.                                        00670001
           05  PC-SUB-CL-NBR-ZEROS     PIC  X(02)    VALUE '00'.        00680012
                                                                        00690001
       01  PA-PROGRAM-ACCUMULATORS                   COMP-3.            00700012
           05  PA-TMNMCLC-ROWS-FETCHED PIC S9(13)    VALUE ZERO.        00710012
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)    VALUE ZERO.        00720012
W26603*    05  PA-WORK-AMOUNT          PIC S9(9)V99  VALUE ZEROS.       00730012
W26603     05  PA-WORK-AMOUNT          PIC S9(11)V99 VALUE ZEROS.       00740012
                                                                        00750001
       01  CD-COUNTER-DISPLAY.                                          00760001
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00770001
                                                                        00780001
       01 ABEND-AREAS.                                                  00790001
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00800001
                                                   COMP SYNC.           00810001
               88  AC-BAD-DATA                     VALUE +4003.         00820001
               88  AC-DB2-ERROR                    VALUE +4013.         00830001
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00840001
                 '*****       ABEND'.                                   00850001
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00860001
                   '*****   PROGRAM: MLKUT475'.                         00870007
           05  AA-PARAGRAPH-LIT.                                        00880001
               10  FILLER              PIC X(17)   VALUE                00890001
                   '***** PARAGRAPH: '.                                 00900001
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00910001
           05  AA-MESSAGE-LINE.                                         00920001
               10  FILLER              PIC X(06)   VALUE '*****'.       00930001
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00940001
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00950001
                   '*****    DB2 ERROR'.                                00960001
           05  AA-DB2-OPERATION-LIT.                                    00970001
               10  FILLER              PIC X(17)   VALUE                00980001
                   '***** OPERATION: '.                                 00990001
               10  AA-DB2-OPERATION.                                    01000001
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        01010001
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        01020001
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        01030001
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        01040001
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        01050001
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        01060001
                                                                        01070001
       EJECT                                                            01080001
      ******************************************************************01090001
      *** DATA INTEGRITY CONTROL CARD                                   01100001
      ******************************************************************01110001
           COPY MLRD110.                                                01120001
                                                                        01130001
      ******************************************************************01140001
      *** DATA INTEGRITY TMNMCLC EXTRACT FILE                           01150007
      ******************************************************************01160001
           COPY MLRD153.                                                01170010
                                                                        01180001
       EJECT                                                            01190001
      ******************************************************************01200001
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01210001
      ******************************************************************01220001
           COPY DPWS004.                                                01230001
                                                                        01240001
      ******************************************************************01250001
      *  DB2 ERRORS                                                     01260001
      ******************************************************************01270001
           COPY SQLCA2.                                                 01280001
                                                                        01290001
      ******************************************************************01300001
      *  DB2 COMMUNICATION AREA                                         01310001
      ******************************************************************01320001
           EXEC SQL                                                     01330001
                INCLUDE SQLCA                                           01340001
           END-EXEC.                                                    01350001
                                                                        01360001
      ******************************************************************01370001
      *  DB2 TMNMCLC TABLE LAYOUT                                       01380007
      ******************************************************************01390001
           EXEC SQL                                                     01400001
                INCLUDE TMNMCLC                                         01410007
           END-EXEC.                                                    01420001
                                                                        01430001
      ******************************************************************01440001
      *  DB2 TMNMCLC CURSOR                                             01450007
      ******************************************************************01460001
           EXEC SQL                                                     01470001
               DECLARE TMNMCLC_CURSOR CURSOR FOR                        01480007
                SELECT FSCL_MN_NBR                                      01490007
                      ,FSCL_MN_END_DTE                                  01500007
                      ,DEPT_NBR                                         01510007
                      ,MAJ_CL_NBR                                       01520007
                      ,CHNL_CDE                                         01530007
                      ,SLS_RTL_AMT                                      01540007
                      ,MKDN_PLU_RTL_AMT                                 01550007
                      ,MKDN_RGST_RTL_AMT                                01560007
                      ,MKDN_UMTCH_RTL_AMT                               01570007
                      ,MKDN_BYR_RTL_AMT                                 01580007
                      ,MKDN_STR_RTL_AMT                                 01590007
                      ,MKUP_RTL_AMT                                     01600007
                      ,XFER_IN_RTL_AMT                                  01610007
                      ,XFER_OUT_RTL_AMT                                 01620007
                      ,RCPT_RTL_AMT                                     01630007
                      ,RCPT_NET_COST_AMT                                01640007
                      ,PADJ_RTL_AMT                                     01650007
                      ,PADJ_NET_COST_AMT                                01660007
                      ,PADJ_FRGT_COST_AMT                               01670007
                      ,INV_ADJ_RTL_AMT                                  01680007
                      ,SHNK_RTL_AMT                                     01690007
                      ,END_INV_RTL_AMT                                  01700007
                      ,CUM_MKUP_PCT                                     01710007
                      ,END_INV_COST_AMT                                 01720007
                      ,SLS_COST_AMT                                     01730007
                      ,GRO_MRGN_AMT                                     01740007
                  FROM TMNMCLC                                          01750007
                 WHERE (FSCL_MN_END_DTE >= :ML110-STRT-DTE)             01760001
                   AND (FSCL_MN_END_DTE <= :ML110-END-DTE)              01770001
*****************  AND CHNL_CDE = 'A'                                   01780010
              ORDER BY FSCL_MN_END_DTE,                                 01790001
                       DEPT_NBR,                                        01800001
                       MAJ_CL_NBR,                                      01810011
                       CHNL_CDE                                         01820011
W26603        WITH UR                                                   01830012
           END-EXEC.                                                    01840001
                                                                        01850001
       EJECT                                                            01860001
      ******************************************************************01870001
       PROCEDURE DIVISION.                                              01880001
      ******************************************************************01890001
                                                                        01900001
      ******************************************************************01910001
       0000-MAINLINE-MLKUT475.                                          01920007
      ******************************************************************01930001
      *                                                                 01940001
           PERFORM 1000-INITIALIZATION.                                 01950001
                                                                        01960001
           PERFORM 2000-MAIN-PROCESS.                                   01970001
                                                                        01980001
           PERFORM 8000-EOJ.                                            01990001
                                                                        02000001
           STOP RUN.                                                    02010001
                                                                        02020001
       EJECT                                                            02030001
      ******************************************************************02040001
       1000-INITIALIZATION.                                             02050001
      ******************************************************************02060001
      *                                                                 02070001
           OPEN INPUT  DATA-INTEGRITY-CTL-FILE                          02080001
                OUTPUT DATA-INTEGRITY-EXTR-FILE.                        02090001
                                                                        02100001
           PERFORM 4000-READ-CONTROL-CARD.                              02110001
                                                                        02120001
           DISPLAY ' '.                                                 02130001
           DISPLAY '*** MLKUT475 INPUT CONTROL CARD VALUES ***'         02140007
           DISPLAY ML110-DATA-INTEGRITY-CC-REC.                         02150001
                                                                        02160001
       EJECT                                                            02170001
      ******************************************************************02180001
       2000-MAIN-PROCESS.                                               02190001
      ******************************************************************02200001
      *                                                                 02210001
           PERFORM 7100-OPEN-CURSOR-TMNMCLC.                            02220007
                                                                        02230001
           PERFORM 7200-FETCH-TMNMCLC.                                  02240007
                                                                        02250001
           PERFORM                                                      02260001
             UNTIL PS-TMNMCLC-IS-DONE                                   02270007
               PERFORM 5000-WRITE-EXTRACT                               02280001
               PERFORM 7200-FETCH-TMNMCLC                               02290007
           END-PERFORM.                                                 02300001
                                                                        02310001
           PERFORM 7300-CLOSE-CURSOR-TMNMCLC.                           02320007
                                                                        02330001
       EJECT                                                            02340001
      ******************************************************************02350001
       4000-READ-CONTROL-CARD.                                          02360001
      ******************************************************************02370001
      *                                                                 02380001
           READ DATA-INTEGRITY-CTL-FILE                                 02390001
             INTO ML110-DATA-INTEGRITY-CC-REC                           02400001
               AT END                                                   02410001
                   MOVE '4000-READ-CONTROL-CARD'                        02420001
                                       TO AA-PARAGRAPH-NAME             02430001
                   MOVE 'CONTROL CARD IS MISSING'                       02440001
                                       TO AA-MESSAGE                    02450001
                   MOVE +4001          TO ABEND-CODE                    02460001
                   PERFORM 9900-COMMON-ABEND-ROUTINE                    02470001
           END-READ.                                                    02480001
                                                                        02490001
       EJECT                                                            02500001
      ******************************************************************02510001
       5000-WRITE-EXTRACT.                                              02520001
      ******************************************************************02530001
      *                                                                 02540001
           INITIALIZE ML153-DATA-INTEGRITY-EXTR-REC.                    02550010
           MOVE MNMCLC-FSCL-MN-END-DTE  TO ML153-FSCL-MN-END-DTE.       02560010
           MOVE MNMCLC-FSCL-MN-NBR      TO ML153-FSCL-MN-NBR.           02570010
           MOVE MNMCLC-DEPT-NBR         TO ML153-DEPT-NBR.              02580010
           MOVE MNMCLC-MAJ-CL-NBR       TO ML153-MAJ-CL-NBR.            02590010
           MOVE MNMCLC-MAJ-CL-NBR       TO ML153-SUB-CL-NBR.            02600010
           MOVE '00'                    TO ML153-SUB-CL-NBR.            02610012
           MOVE MNMCLC-CHNL-CDE         TO ML153-CHNL-CDE.              02620010
           MOVE MNMCLC-SLS-RTL-AMT      TO ML153-SLS-RTL-AMT.           02630010
                                                                        02640007
           COMPUTE PA-WORK-AMOUNT      = MNMCLC-MKDN-PLU-RTL-AMT        02650007
                                       + MNMCLC-MKDN-RGST-RTL-AMT       02660007
                                       + MNMCLC-MKDN-UMTCH-RTL-AMT      02670007
                                       + MNMCLC-MKDN-BYR-RTL-AMT        02680007
                                       + MNMCLC-MKDN-STR-RTL-AMT.       02690007
           MOVE PA-WORK-AMOUNT         TO ML153-MKDN-RTL-AMT.           02700010
                                                                        02710007
           MOVE MNMCLC-MKUP-RTL-AMT     TO ML153-MKUP-RTL-AMT.          02720010
           MOVE MNMCLC-XFER-IN-RTL-AMT  TO ML153-XFER-IN-RTL-AMT.       02730010
           MOVE MNMCLC-XFER-OUT-RTL-AMT TO ML153-XFER-OUT-RTL-AMT.      02740010
           MOVE MNMCLC-RCPT-RTL-AMT     TO ML153-RCPT-RTL-AMT.          02750010
           MOVE MNMCLC-RCPT-NET-COST-AMT                                02760008
                                        TO ML153-RCPT-NET-COST-AMT.     02770010
           MOVE MNMCLC-PADJ-RTL-AMT     TO ML153-PADJ-RTL-AMT.          02780010
           MOVE MNMCLC-PADJ-NET-COST-AMT                                02790008
                                        TO ML153-PADJ-NET-COST-AMT.     02800010
           MOVE MNMCLC-INV-ADJ-RTL-AMT  TO ML153-INV-ADJ-RTL-AMT.       02810010
           MOVE MNMCLC-SHNK-RTL-AMT     TO ML153-SHNK-RTL-AMT.          02820010
           MOVE MNMCLC-END-INV-RTL-AMT  TO ML153-END-INV-RTL-AMT.       02830010
                                                                        02840001
           WRITE DATA-INTEGRITY-EXTR-REC                                02850001
               FROM ML153-DATA-INTEGRITY-EXTR-REC.                      02860010
           ADD 1                        TO PA-OUTPUT-RECS-WRITTEN.      02870001
                                                                        02880001
       EJECT                                                            02890001
      ******************************************************************02900001
       7100-OPEN-CURSOR-TMNMCLC.                                        02910007
      ******************************************************************02920001
      *                                                                 02930001
           EXEC SQL                                                     02940001
               OPEN TMNMCLC_CURSOR                                      02950007
           END-EXEC.                                                    02960001
                                                                        02970001
           EVALUATE TRUE                                                02980001
               WHEN SQLCODE = 0                                         02990001
                   CONTINUE                                             03000001
               WHEN OTHER                                               03010001
                   MOVE '7100-OPEN-CURSOR-TMNMCLC'                      03020007
                                       TO AA-PARAGRAPH-NAME             03030001
                   MOVE 'TMNMCLC TABLE ERROR'                           03040007
                                       TO AA-MESSAGE                    03050001
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              03060001
                   PERFORM 9901-SQL-ERROR                               03070001
           END-EVALUATE.                                                03080001
                                                                        03090001
       EJECT                                                            03100001
      ******************************************************************03110001
       7200-FETCH-TMNMCLC.                                              03120007
      ******************************************************************03130001
      *                                                                 03140001
           EXEC SQL                                                     03150001
               FETCH TMNMCLC_CURSOR                                     03160007
               INTO :MNMCLC-FSCL-MN-NBR,                                03170007
                    :MNMCLC-FSCL-MN-END-DTE,                            03180007
                    :MNMCLC-DEPT-NBR,                                   03190007
                    :MNMCLC-MAJ-CL-NBR,                                 03200007
                    :MNMCLC-CHNL-CDE,                                   03210007
                    :MNMCLC-SLS-RTL-AMT,                                03220007
                    :MNMCLC-MKDN-PLU-RTL-AMT,                           03230007
                    :MNMCLC-MKDN-RGST-RTL-AMT,                          03240007
                    :MNMCLC-MKDN-UMTCH-RTL-AMT,                         03250007
                    :MNMCLC-MKDN-BYR-RTL-AMT,                           03260007
                    :MNMCLC-MKDN-STR-RTL-AMT,                           03270007
                    :MNMCLC-MKUP-RTL-AMT,                               03280007
                    :MNMCLC-XFER-IN-RTL-AMT,                            03290007
                    :MNMCLC-XFER-OUT-RTL-AMT,                           03300007
                    :MNMCLC-RCPT-RTL-AMT,                               03310007
                    :MNMCLC-RCPT-NET-COST-AMT,                          03320007
                    :MNMCLC-PADJ-RTL-AMT,                               03330007
                    :MNMCLC-PADJ-NET-COST-AMT,                          03340007
                    :MNMCLC-PADJ-FRGT-COST-AMT,                         03350007
                    :MNMCLC-INV-ADJ-RTL-AMT,                            03360007
                    :MNMCLC-SHNK-RTL-AMT,                               03370007
                    :MNMCLC-END-INV-RTL-AMT,                            03380007
                    :MNMCLC-CUM-MKUP-PCT,                               03390007
                    :MNMCLC-END-INV-COST-AMT,                           03400007
                    :MNMCLC-SLS-COST-AMT,                               03410007
                    :MNMCLC-GRO-MRGN-AMT                                03420007
           END-EXEC.                                                    03430001
                                                                        03440001
           EVALUATE TRUE                                                03450001
               WHEN SQLCODE = 0                                         03460001
                   ADD +1              TO PA-TMNMCLC-ROWS-FETCHED       03470007
               WHEN SQLCODE = +100                                      03480001
                   MOVE 'Y'            TO PS-TMNMCLC-DONE               03490007
               WHEN OTHER                                               03500001
                   MOVE '7200-FETCH-TMNMCLC'                            03510007
                                       TO AA-PARAGRAPH-NAME             03520001
                   MOVE 'TMNMCLC TABLE ERROR'                           03530007
                                       TO AA-MESSAGE                    03540001
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              03550001
                   PERFORM 9901-SQL-ERROR                               03560001
           END-EVALUATE.                                                03570001
                                                                        03580001
       EJECT                                                            03590001
      ******************************************************************03600001
       7300-CLOSE-CURSOR-TMNMCLC.                                       03610007
      ******************************************************************03620001
      *                                                                 03630001
           EXEC SQL                                                     03640001
               CLOSE TMNMCLC_CURSOR                                     03650007
           END-EXEC.                                                    03660001
                                                                        03670001
           EVALUATE TRUE                                                03680001
               WHEN SQLCODE = 0                                         03690001
                   CONTINUE                                             03700001
               WHEN OTHER                                               03710001
                   MOVE '7300-CLOSE-CURSOR-TMNMCLC'                     03720007
                                       TO AA-PARAGRAPH-NAME             03730001
                   MOVE 'TMNMCLC TABLE ERROR'                           03740007
                                       TO AA-MESSAGE                    03750001
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03760001
                   PERFORM 9901-SQL-ERROR                               03770001
           END-EVALUATE.                                                03780001
                                                                        03790001
       EJECT                                                            03800001
      ******************************************************************03810001
       8000-EOJ.                                                        03820001
      ******************************************************************03830001
      *                                                                 03840001
           DISPLAY ' '.                                                 03850001
           DISPLAY '  PROGRAM MLKUT475 PROCESSING SUMMARY'.             03860007
           DISPLAY '  -----------------------------------'.             03870001
           DISPLAY ' '.                                                 03880001
      *                                                                 03890001
           MOVE PA-TMNMCLC-ROWS-FETCHED TO CD-COUNT-HOLD1.              03900007
           DISPLAY 'TMNMCLC ROWS FETCHED =      ' CD-COUNT-HOLD1.       03910007
      *                                                                 03920001
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               03930001
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.       03940001
      *                                                                 03950001
           DISPLAY ' '.                                                 03960001
                                                                        03970001
           CLOSE DATA-INTEGRITY-CTL-FILE.                               03980001
           CLOSE DATA-INTEGRITY-EXTR-FILE.                              03990001
                                                                        04000001
       EJECT                                                            04010001
      ******************************************************************04020001
       9900-COMMON-ABEND-ROUTINE.                                       04030001
      ******************************************************************04040001
      *                                                                 04050001
           DISPLAY AA-ABEND-LIT.                                        04060001
           DISPLAY AA-PROGRAM-LIT.                                      04070001
           DISPLAY AA-PARAGRAPH-LIT.                                    04080001
           DISPLAY AA-MESSAGE-LINE.                                     04090001
           DISPLAY ABEND-CODE.                                          04100001
                                                                        04110001
           CALL 'ILBOABN0' USING ABEND-CODE.                            04120001
                                                                        04130001
       EJECT                                                            04140001
      ******************************************************************04150001
       9901-SQL-ERROR.                                                  04160001
      ******************************************************************04170001
      *                                                                 04180001
           CLOSE DATA-INTEGRITY-CTL-FILE                                04190001
                 DATA-INTEGRITY-EXTR-FILE.                              04200001
                                                                        04210001
           DISPLAY AA-ABEND-LIT.                                        04220001
           DISPLAY AA-DB2-ERROR-LIT.                                    04230001
           DISPLAY AA-PROGRAM-LIT.                                      04240001
           DISPLAY AA-PARAGRAPH-LIT.                                    04250001
           DISPLAY AA-MESSAGE-LINE.                                     04260001
           DISPLAY AA-DB2-OPERATION-LIT.                                04270001
           SET AC-DB2-ERROR TO TRUE.                                    04280001
           MOVE +4013                  TO ABEND-CODE.                   04290001
                                                                        04300001
           COPY DPPD004.                                                04310001
           CALL 'ILBOABN0' USING ABEND-CODE.                            04320001
                                                                        04330001
