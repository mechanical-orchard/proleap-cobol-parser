       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT448.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11-26-2004.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * MLKUT448 -                                                              
      *                                                                         
      * THIS PROGRAM WILL MOVE THE PARTITION NUMBER AND DEPARTMENT              
      * GROUP CODE TO THE WEEKLY COMMON RECALC EXTRACT.                         
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      * WR26006 2005-03-09 V. LEE YANG DISPLAY PROCESSING DEPT AND SHOW         
      *                                NUMBER OF PARTITION CNTL ADDED           
      *         2006-10-09 L. MSIKINYA CHANGE RECORD LENGTH FROM 324 TO         
      *                                400 FOR BRAND VENDOR SMOOTHING.          
      * WR34517 2008-10-14 INFOSYS     DETERMINE TABLE LEVEL CODE PASSED        
      *                                TO MLPD500 SUBROUTINE BASED ON           
      *                                THE TABLE TO BE PROCESSED.NEW            
      *                                CONTROL FILE DETERMINES WHETHER          
      *                                WKLY_VND OR MNLY_VND TABLE IS TO         
      *                                BE PROCESSED.                            
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT COMMON-RECALC-IN-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
R34517     SELECT CONTROL-TABLE-FILE                                            
R34517         ASSIGN TO INP02.                                                 
R34517                                                                          
           SELECT COMMON-RECALC-OUT-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  COMMON-RECALC-IN-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
LUNGIS 01  RECALC-INPUT-REC            PIC X(400).                              
LUNGIS*01  RECALC-INPUT-REC            PIC X(324).                              
                                                                                
R34517 FD  CONTROL-TABLE-FILE                                                   
R34517     RECORDING MODE IS F                                                  
R34517     LABEL RECORDS ARE STANDARD                                           
R34517     BLOCK CONTAINS 0 RECORDS.                                            
R34517 01  CONTROL-TABLE-REC           PIC X(080).                              
R34517                                                                          
       FD  COMMON-RECALC-OUT-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
LUNGIS 01  RECALC-OUTPUT-REC           PIC X(400).                              
LUNGIS*01  RECALC-OUTPUT-REC           PIC X(324).                              
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
R34517******************************************************************        
R34517*    FILE LAYOUT OF CONTROL TABLE FILE                                    
R34517******************************************************************        
R34517 01  CC-TABLE-REC.                                                        
R34517     05  FILLER                  PIC  X(01) VALUE SPACES.                 
R34517     05  CC-TABLE-NAME           PIC  X(29) VALUE SPACES.                 
R34517         88 CC-TABLE-WKLY-VND               VALUE 'MLT_WKLY_VND'.         
R34517         88 CC-TABLE-MNLY-VND               VALUE 'MLT_MNLY_VND'.         
R34517     05  FILLER                  PIC  X(50) VALUE SPACES.                 
R34517                                                                          
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EXTRACT-EOF-SW              PIC X(01) VALUE 'N'.              
               88  PS-NO-MORE-RECORDS                   VALUE 'Y'.              
R34517     05  PS-EMPTY-CONTROL-EOF-SW        PIC X(01) VALUE 'N'.              
R34517         88  PS-EMPTY-CONTROL-FILE                VALUE 'Y'.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DEPT-NBR                PIC S9(4)V COMP-3                     
                                                      VALUE ZEROS.              
           05  PV-SAVE-PARTN-NBR          PIC S9(4)  COMP                       
                                                     VALUE ZEROS.               
           05  PV-SAVE-DEPT-GP-CDE        PIC X(01)  VALUE SPACE.               
           05  PV-SAVE-DEPT-GP-CDE-9 REDEFINES PV-SAVE-DEPT-GP-CDE              
                                          PIC 9(01).                            
           05  PV-FSCL-END-DTE            PIC X(10)  VALUE SPACES.              
           05  PV-PARTN-NBR               PIC S9(04)  VALUE ZEROS.              
           05  PV-PARTN-GROUP             PIC S9(04)  VALUE ZEROS.              
                                                                                
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
           05  PA-INPUT-COUNT-IN       PIC S9(13)  VALUE ZEROS.                 
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZEROS.                 
           05  PA-PARTN-REUSE          PIC 9(11)   VALUE ZEROS.                 
           05  PA-PARTN-LOOKUP         PIC 9(11)   VALUE ZEROS.                 
           05  PA-PARTN-CHANGE         PIC 9(11)   VALUE ZEROS.                 
                                                                                
       01  CD-COUNTER-DISPLAY.                                                  
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.                   
                                                                                
       01 ABEND-AREAS.                                                          
           05 ABEND-CODE               PIC S9(04)  VALUE ZEROS                  
                                                   COMP SYNC.                   
               88  AC-BAD-DATA                     VALUE +4003.                 
               88  AC-DB2-ERROR                    VALUE +4013.                 
           05  AA-ABEND-LIT            PIC X(40)   VALUE                        
                 '*****       ABEND'.                                           
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
                   '*****   PROGRAM: MLKUT448'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER              PIC X(17)   VALUE                        
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER              PIC X(06)   VALUE '*****'.               
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER              PIC X(17)   VALUE                        
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.                
                                                                                
      ******************************************************************        
      * PARTITION NUMBER LOGIC......                                            
      *  M/L DETAIL LEDGER PARTITION BALANCE TBL (MLT_PARTN_BAL)                
      *  M/L DETAIL LEDGER PARTITION CONTROL TBL (MLT_PARTN_CNTL)               
      ******************************************************************        
           COPY MLWS500.                                                        
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  DB2 ERRORS                                                             
      ******************************************************************        
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      *  DB2 COMMUNICATION AREA                                                 
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO *                      
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * MLRD070 - INPUT AND OUTPUT EXTRACT RECORD LAYOUT                        
      ******************************************************************        
           COPY MLRD070.                                                        
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       A000-MAIN-MODULE.                                                        
                                                                                
           PERFORM B000-INITIALIZE.                                             
                                                                                
           PERFORM B100-PROCESS-EXTRACT                                         
               UNTIL PS-NO-MORE-RECORDS                                         
                                                                                
           PERFORM B900-EOJ-ROUTINE.                                            
                                                                                
       Z999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
       B000-INITIALIZE.                                                         
      ******************************************************************        
      *                                                                         
           OPEN INPUT  COMMON-RECALC-IN-FILE                                    
R34517                 CONTROL-TABLE-FILE                                       
                OUTPUT COMMON-RECALC-OUT-FILE.                                  
                                                                                
R34517     PERFORM 4500-READ-CONTROL-TABLE-FILE.                                
R34517     IF PS-EMPTY-CONTROL-FILE                                             
R34517        MOVE 'B000-INITIALIZE'     TO AA-PARAGRAPH-NAME                   
R34517        MOVE 'EMPTY CONTROL FILE'  TO AA-MESSAGE                          
R34517        PERFORM Z997-ERROR                                                
R34517     END-IF.                                                              
R34517                                                                          
           PERFORM 4000-READ-RECALC-INPUT-FILE.                                 
           PERFORM 7100-SELECT-DB2-DATES.                                       
                                                                                
      *** DEFINE THE PARTITIONS FOR THE RECALC SELECTION EXTRACT...             
           DISPLAY '--------------------------------------------'.              
                                                                                
           DISPLAY 'PARTN TBL BGN DATE    ', DTATTR-FSCL-MN-BEG-DTE.            
           DISPLAY 'PARTN TBL END DATE    ', DTATTR-FSCL-MN-END-DTE.            
           MOVE DTATTR-FSCL-MN-BEG-DTE    TO ML500-PV-REQ-FSCL-BEG-DTE.         
           MOVE DTATTR-FSCL-MN-END-DTE    TO ML500-PV-REQ-FSCL-END-DTE.         
                                                                                
           PERFORM ML500-0100-LOAD-PARTN-TABLE                                  
           IF ML500-PV-PARAGRAPH-NAME NOT EQUAL TO SPACES                       
               MOVE ML500-PV-PARAGRAPH-NAME                                     
                                          TO AA-PARAGRAPH-NAME                  
               MOVE 'PARTN_BAL PARTN_CNTL LOAD'                                 
                                          TO AA-MESSAGE                         
               MOVE ML500-PV-OPERATION-ATTEMPTED                                
                                          TO AA-DB2-OPERATION                   
             PERFORM Z998-SQL-ERROR                                             
             MOVE 'Y'                  TO PS-EXTRACT-EOF-SW                     
           END-IF.                                                              
                                                                                
           DISPLAY '--------------------------------------------'.              
                                                                                
      ******************************************************************        
      * PROCESS EXTRACT FILE                                                    
      ******************************************************************        
       B100-PROCESS-EXTRACT.                                                    
                                                                                
           ADD +1 TO PA-INPUT-COUNT-IN.                                         
                                                                                
26006      IF PV-DEPT-NBR NOT = ML070-DEPT-NBR                                  
26006          DISPLAY ' PROCESSING DEPT ' ML070-DEPT-NBR                       
26006                  ' ' FUNCTION CURRENT-DATE                                
26006      END-IF.                                                              
                                                                                
           MOVE ML070-FSCL-END-DTE        TO PV-FSCL-END-DTE.                   
           MOVE ML070-DEPT-NBR            TO PV-DEPT-NBR.                       
           MOVE ML070-PARTN-NBR           TO PV-PARTN-NBR.                      
           MOVE ML070-PARTN-GROUP         TO PV-PARTN-GROUP.                    
                                                                                
           IF PV-FSCL-END-DTE = ML500-PV-FSCL-END-DTE AND                       
              PV-DEPT-NBR     = ML500-PV-DEPT-NBR                               
               ADD +1                      TO PA-PARTN-REUSE                    
               MOVE PV-SAVE-PARTN-NBR      TO ML070-PARTN-NBR                   
               MOVE PV-SAVE-DEPT-GP-CDE-9  TO ML070-PARTN-GROUP                 
           ELSE                                                                 
               ADD +1                      TO PA-PARTN-LOOKUP                   
               MOVE PV-DEPT-NBR            TO ML500-PV-DEPT-NBR                 
               MOVE PV-FSCL-END-DTE        TO ML500-PV-FSCL-END-DTE             
R34517         IF CC-TABLE-WKLY-VND                                             
                  MOVE 'W'                 TO ML500-PV-TM-LVL-CDE               
R34517         ELSE                                                             
R34517            IF CC-TABLE-MNLY-VND                                          
R34517               MOVE 'M'              TO ML500-PV-TM-LVL-CDE               
R34517            ELSE                                                          
R34517               MOVE 'B100-PROCESS-EXTRACT'                                
R34517                                     TO AA-PARAGRAPH-NAME                 
R34517               MOVE 'INVALID TABLE IN CONTROL FILE'                       
R34517                                     TO AA-MESSAGE                        
R34517               PERFORM Z997-ERROR                                         
R34517            END-IF                                                        
R34517         END-IF                                                           
                                                                                
               PERFORM ML500-0500-FIND-PARTITION                                
               IF ML500-PV-PARAGRAPH-NAME NOT EQUAL TO SPACES                   
                   MOVE ML500-PV-PARAGRAPH-NAME                                 
                                          TO AA-PARAGRAPH-NAME                  
                   MOVE 'PARTN_BAL PARTN_CNTL FIND'                             
                                          TO AA-MESSAGE                         
                   MOVE ML500-PV-OPERATION-ATTEMPTED                            
                                          TO AA-DB2-OPERATION                   
                   PERFORM Z998-SQL-ERROR                                       
               END-IF                                                           
                                                                                
               MOVE ML500-PV-PARTN-NBR-OUT    TO ML070-PARTN-NBR                
                                                 PV-SAVE-PARTN-NBR              
               MOVE ML500-PV-DEPT-GP-CDE-OUT  TO PV-SAVE-DEPT-GP-CDE            
               MOVE PV-SAVE-DEPT-GP-CDE-9     TO ML070-PARTN-GROUP              
           END-IF.                                                              
                                                                                
           IF PV-PARTN-NBR NOT EQUAL TO ML070-PARTN-NBR OR                      
              PV-PARTN-GROUP NOT EQUAL TO ML070-PARTN-GROUP                     
               ADD +1 TO PA-PARTN-CHANGE                                        
           END-IF.                                                              
                                                                                
           WRITE RECALC-OUTPUT-REC     FROM ML070-COMMON-RECALC-EXTRACT.        
                                                                                
           ADD +1                       TO PA-OUTPUT-RECS-WRITTEN.              
                                                                                
           PERFORM 4000-READ-RECALC-INPUT-FILE.                                 
                                                                                
      ******************************************************************        
      * READ FILE CONTAINING COMMON RECALC EXTRACT                              
      ******************************************************************        
       4000-READ-RECALC-INPUT-FILE.                                             
                                                                                
            READ COMMON-RECALC-IN-FILE INTO ML070-COMMON-RECALC-EXTRACT         
                AT END SET PS-NO-MORE-RECORDS TO TRUE.                          
                                                                                
R34517******************************************************************        
R34517* READ CONTROL TABLE FILE                                                 
R34517******************************************************************        
R34517 4500-READ-CONTROL-TABLE-FILE.                                            
R34517                                                                          
R34517      READ CONTROL-TABLE-FILE    INTO CC-TABLE-REC                        
R34517          AT END SET PS-EMPTY-CONTROL-FILE TO TRUE.                       
R34517                                                                          
      ******************************************************************        
      * READ THE DB2 CONTROL TABLE CONTAINING OPEN FISCAL DATE INFO             
      ******************************************************************        
       7100-SELECT-DB2-DATES.                                                   
                                                                                
            EXEC SQL                                                            
               SELECT  FSCL_MN_BEG_DTE                                          
                      ,FSCL_MN_END_DTE                                          
                INTO  :DTATTR-FSCL-MN-BEG-DTE                                   
                     ,:DTATTR-FSCL-MN-END-DTE                                   
                FROM TDTATTR                                                    
                WHERE KY_DTE  = :ML070-FSCL-END-DTE                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   DISPLAY 'DTATTR-FSCL-MN-BEG-DTE    : ',                      
                            DTATTR-FSCL-MN-BEG-DTE                              
                   DISPLAY 'DTATTR-FSCL-MN-END-DTE    : ',                      
                            DTATTR-FSCL-MN-END-DTE                              
               WHEN OTHER                                                       
                  MOVE '7100-SELECT-DB2-DATES'                                  
                                       TO AA-PARAGRAPH-NAME                     
                   PERFORM Z998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * PROCESS FINAL COUNTS FOR INPUT AND OUTPUT COUNTS                        
      ******************************************************************        
       B900-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '  PROGRAM MLKUT448 PROCESSING SUMMARY'.                     
           DISPLAY '  -----------------------------------'.                     
           DISPLAY ' '.                                                         
                                                                                
           DISPLAY 'PARTITION CNTL LOOKUP = ' PA-PARTN-LOOKUP.                  
26006      DISPLAY 'PARTITION CNTL INSERT = ' ML500-PV-BAL-ROW-INSERTED.        
           DISPLAY 'PARTITION CNTL REUSE  = ' PA-PARTN-REUSE.                   
           DISPLAY 'PARTITION INFO CHANGE = ' PA-PARTN-CHANGE.                  
           DISPLAY ' '.                                                         
           DISPLAY 'TOTAL INPUT  RECORDS  = ', PA-INPUT-COUNT-IN.               
           DISPLAY 'TOTAL OUTPUT RECORDS  = ', PA-OUTPUT-RECS-WRITTEN.          
           DISPLAY ' '.                                                         
           DISPLAY '***** GOOD EOJ *****'.                                      
                                                                                
           CLOSE COMMON-RECALC-IN-FILE                                          
R34517           CONTROL-TABLE-FILE                                             
                 COMMON-RECALC-OUT-FILE.                                        
                                                                                
R34517******************************************************************        
R34517 Z997-ERROR.                                                              
R34517******************************************************************        
R34517                                                                          
R34517     CLOSE COMMON-RECALC-IN-FILE                                          
R34517           CONTROL-TABLE-FILE                                             
R34517           COMMON-RECALC-OUT-FILE.                                        
R34517                                                                          
R34517     DISPLAY AA-ABEND-LIT.                                                
R34517     DISPLAY AA-PROGRAM-LIT.                                              
R34517     DISPLAY AA-PARAGRAPH-LIT.                                            
R34517     DISPLAY AA-MESSAGE-LINE.                                             
R34517     SET AC-BAD-DATA  TO TRUE.                                            
R34517     MOVE +4003       TO ABEND-CODE.                                      
R34517                                                                          
R34517     CALL 'ILBOABN0' USING ABEND-CODE.                                    
R34517                                                                          
      *                                                                         
      ******************************************************************        
       Z998-SQL-ERROR.                                                          
      ******************************************************************        
                                                                                
           CLOSE COMMON-RECALC-IN-FILE                                          
R34517           CONTROL-TABLE-FILE                                             
                 COMMON-RECALC-OUT-FILE.                                        
                                                                                
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-DB2-ERROR-LIT.                                            
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY AA-DB2-OPERATION-LIT.                                        
           SET AC-DB2-ERROR TO TRUE.                                            
           MOVE +4013                  TO ABEND-CODE.                           
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * MLPD500 - PARTITION                                                     
      ******************************************************************        
           COPY MLPD500.                                                        
                                                                                
